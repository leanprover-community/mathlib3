/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, SÃ©bastien GouÃ«zel, RÃ©my Degenne
-/
import measure_theory.function.simple_func_dense

/-!
# Extension of a linear function from indicators to L1

Let `T : set Î± â†’ E â†’L[â„] F` be additive for measurable sets with finite measure, in the sense that
for `s, t` two such sets, `s âˆ© t = âˆ… â†’ T (s âˆª t) = T s + T t`. `T` is akin to a bilinear map on
`set Î± Ã— E`, or a linear map on indicator functions.

This file constructs an extension of `T` to integrable simple functions, which are finite sums of
indicators of measurable sets with finite measure, then to integrable functions, which are limits of
integrable simple functions.

The main result is a continuous linear map `(Î± â†’â‚[Î¼] E) â†’L[â„] F`. This extension process is used to
define the Bochner integral in the `measure_theory.integral.bochner` file and the conditional
expectation of an integrable function in `measure_theory.function.conditional_expectation`.

## Main Definitions

- `fin_meas_additive Î¼ T`: the property that `T` is additive on measurable sets with finite measure.
  For two such sets, `s âˆ© t = âˆ… â†’ T (s âˆª t) = T s + T t`.
- `dominated_fin_meas_additive Î¼ T C`: `fin_meas_additive Î¼ T âˆ§ âˆ€ s, âˆ¥T sâˆ¥ â‰¤ C * (Î¼ s).to_real`.
  This is the property needed to perform the extension from indicators to L1.
- `set_to_L1 (hT : dominated_fin_meas_additive Î¼ T C) : (Î± â†’â‚[Î¼] E) â†’L[â„] F`: the extension of `T`
  from indicators to L1.
- `set_to_fun Î¼ T (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’ E) : F`: a version of the
  extension which applies to functions (with value 0 if the function is not integrable).

## Implementation notes

The starting object `T : set Î± â†’ E â†’L[â„] F` matters only through its restriction on measurable sets
with finite measure. Its value on other sets is ignored.

The extension step from integrable simple functions to L1 relies on a `second_countable_topology`
assumption. Without it, we could only extend to `ae_fin_strongly_measurable` functions. (TODO: this
might be worth doing?)

-/

noncomputable theory
open_locale classical topological_space big_operators nnreal ennreal measure_theory pointwise
open set filter topological_space ennreal emetric

local attribute [instance] fact_one_le_one_ennreal

namespace measure_theory

variables {Î± E F F' G ğ•œ : Type*} {p : â„â‰¥0âˆ}
  [normed_group E] [measurable_space E] [normed_space â„ E]
  [normed_group F] [normed_space â„ F]
  [normed_group F'] [normed_space â„ F']
  [normed_group G] [measurable_space G]
  {m : measurable_space Î±} {Î¼ : measure Î±}

local infixr ` â†’â‚› `:25 := simple_func

open finset

section fin_meas_additive

/-- A set function is `fin_meas_additive` if its value on the union of two disjoint measurable
sets with finite measure is the sum of its values on each set. -/
def fin_meas_additive {Î²} [add_monoid Î²] {m : measurable_space Î±}
  (Î¼ : measure Î±) (T : set Î± â†’ Î²) : Prop :=
âˆ€ s t, measurable_set s â†’ measurable_set t â†’ Î¼ s â‰  âˆ â†’ Î¼ t â‰  âˆ â†’ s âˆ© t = âˆ… â†’ T (s âˆª t) = T s + T t

namespace fin_meas_additive

variables {Î² : Type*} [add_comm_monoid Î²] {T T' : set Î± â†’ Î²}

lemma zero : fin_meas_additive Î¼ (0 : set Î± â†’ Î²) := Î» s t hs ht hÎ¼s hÎ¼t hst, by simp

lemma add (hT : fin_meas_additive Î¼ T) (hT' : fin_meas_additive Î¼ T') :
  fin_meas_additive Î¼ (T + T') :=
begin
  intros s t hs ht hÎ¼s hÎ¼t hst,
  simp only [hT s t hs ht hÎ¼s hÎ¼t hst, hT' s t hs ht hÎ¼s hÎ¼t hst, pi.add_apply],
  abel,
end

lemma smul [monoid ğ•œ] [distrib_mul_action ğ•œ Î²] (hT : fin_meas_additive Î¼ T) (c : ğ•œ) :
  fin_meas_additive Î¼ (Î» s, c â€¢ (T s)) :=
Î» s t hs ht hÎ¼s hÎ¼t hst, by simp [hT s t hs ht hÎ¼s hÎ¼t hst]

lemma of_eq_top_imp_eq_top {Î¼' : measure Î±}
  (h : âˆ€ s, measurable_set s â†’ Î¼ s = âˆ â†’ Î¼' s = âˆ) (hT : fin_meas_additive Î¼ T) :
  fin_meas_additive Î¼' T :=
Î» s t hs ht hÎ¼'s hÎ¼'t hst, hT s t hs ht (mt (h s hs) hÎ¼'s) (mt (h t ht) hÎ¼'t) hst

lemma of_smul_measure (c : â„â‰¥0âˆ) (hc_ne_top : c â‰  âˆ) (hT : fin_meas_additive (c â€¢ Î¼) T) :
  fin_meas_additive Î¼ T :=
begin
  refine of_eq_top_imp_eq_top (Î» s hs hÎ¼s, _) hT,
  rw [measure.smul_apply, with_top.mul_eq_top_iff] at hÎ¼s,
  simp only [hc_ne_top, or_false, ne.def, false_and] at hÎ¼s,
  exact hÎ¼s.2,
end

lemma smul_measure (c : â„â‰¥0âˆ) (hc_ne_zero : c â‰  0) (hT : fin_meas_additive Î¼ T) :
  fin_meas_additive (c â€¢ Î¼) T :=
begin
  refine of_eq_top_imp_eq_top (Î» s hs hÎ¼s, _) hT,
  rw [measure.smul_apply, with_top.mul_eq_top_iff],
  simp only [hc_ne_zero, true_and, ne.def, not_false_iff],
  exact or.inl hÎ¼s,
end

lemma smul_measure_iff (c : â„â‰¥0âˆ) (hc_ne_zero : c â‰  0) (hc_ne_top : c â‰  âˆ) :
  fin_meas_additive (c â€¢ Î¼) T â†” fin_meas_additive Î¼ T :=
âŸ¨Î» hT, of_smul_measure c hc_ne_top hT, Î» hT, smul_measure c hc_ne_zero hTâŸ©

lemma map_empty_eq_zero {Î²} [add_cancel_monoid Î²] {T : set Î± â†’ Î²} (hT : fin_meas_additive Î¼ T) :
  T âˆ… = 0 :=
begin
  have h_empty : Î¼ âˆ… â‰  âˆ, from (measure_empty.le.trans_lt ennreal.coe_lt_top).ne,
  specialize hT âˆ… âˆ… measurable_set.empty measurable_set.empty h_empty h_empty
    (set.inter_empty âˆ…),
  rw set.union_empty at hT,
  nth_rewrite 0 â† add_zero (T âˆ…) at hT,
  exact (add_left_cancel hT).symm,
end

lemma map_Union_fin_meas_set_eq_sum (T : set Î± â†’ Î²) (T_empty : T âˆ… = 0)
  (h_add : fin_meas_additive Î¼ T)
  {Î¹} (S : Î¹ â†’ set Î±) (sÎ¹ : finset Î¹) (hS_meas : âˆ€ i, measurable_set (S i))
  (hSp : âˆ€ i âˆˆ sÎ¹, Î¼ (S i) â‰  âˆ) (h_disj : âˆ€ i j âˆˆ sÎ¹, i â‰  j â†’ disjoint (S i) (S j)) :
  T (â‹ƒ i âˆˆ sÎ¹, S i) = âˆ‘ i in sÎ¹, T (S i) :=
begin
  revert hSp h_disj,
  refine finset.induction_on sÎ¹ _ _,
  { simp only [finset.not_mem_empty, forall_false_left, Union_false, Union_empty, sum_empty,
    forall_2_true_iff, implies_true_iff, forall_true_left, not_false_iff, T_empty], },
  intros a s has h hps h_disj,
  rw [finset.sum_insert has, â† h],
  swap, { exact Î» i hi, hps i (finset.mem_insert_of_mem hi), },
  swap, { exact Î» i j hi hj hij,
    h_disj i j (finset.mem_insert_of_mem hi) (finset.mem_insert_of_mem hj) hij, },
  rw â† h_add (S a) (â‹ƒ i âˆˆ s, S i) (hS_meas a) (measurable_set_bUnion _ (Î» i _, hS_meas i))
    (hps a (finset.mem_insert_self a s)),
  { congr, convert finset.supr_insert a s S, },
  { exact ((measure_bUnion_finset_le _ _).trans_lt $
      ennreal.sum_lt_top $ Î» i hi, hps i $ finset.mem_insert_of_mem hi).ne, },
  { simp_rw set.inter_Union,
    refine Union_eq_empty.mpr (Î» i, Union_eq_empty.mpr (Î» hi, _)),
    rw â† set.disjoint_iff_inter_eq_empty,
    refine h_disj a i (finset.mem_insert_self a s) (finset.mem_insert_of_mem hi) (Î» hai, _),
    rw â† hai at hi,
    exact has hi, },
end

end fin_meas_additive

/-- A `fin_meas_additive` set function whose norm on every set is less than the measure of the
set (up to a multiplicative constant). -/
def dominated_fin_meas_additive {Î²} [semi_normed_group Î²] {m : measurable_space Î±}
  (Î¼ : measure Î±) (T : set Î± â†’ Î²) (C : â„) : Prop :=
fin_meas_additive Î¼ T âˆ§ âˆ€ s, measurable_set s â†’ Î¼ s < âˆ â†’ âˆ¥T sâˆ¥ â‰¤ C * (Î¼ s).to_real

namespace dominated_fin_meas_additive

variables {Î² : Type*} [semi_normed_group Î²] {T T' : set Î± â†’ Î²} {C C' : â„}

lemma zero {m : measurable_space Î±} (Î¼ : measure Î±) (hC : 0 â‰¤ C) :
  dominated_fin_meas_additive Î¼ (0 : set Î± â†’ Î²) C :=
begin
  refine âŸ¨fin_meas_additive.zero, Î» s hs hÎ¼s, _âŸ©,
  rw [pi.zero_apply, norm_zero],
  exact mul_nonneg hC to_real_nonneg,
end

lemma eq_zero_of_measure_zero {Î² : Type*} [normed_group Î²] {T : set Î± â†’ Î²} {C : â„}
  (hT : dominated_fin_meas_additive Î¼ T C) {s : set Î±}
  (hs : measurable_set s) (hs_zero : Î¼ s = 0) :
  T s = 0 :=
begin
  refine norm_eq_zero.mp _,
  refine ((hT.2 s hs (by simp [hs_zero])).trans (le_of_eq _)).antisymm (norm_nonneg _),
  rw [hs_zero, ennreal.zero_to_real, mul_zero],
end

lemma eq_zero {Î² : Type*} [normed_group Î²] {T : set Î± â†’ Î²} {C : â„}
  {m : measurable_space Î±} (hT : dominated_fin_meas_additive (0 : measure Î±) T C)
  {s : set Î±} (hs : measurable_set s) :
  T s = 0 :=
eq_zero_of_measure_zero hT hs (by simp only [measure.coe_zero, pi.zero_apply])

lemma add (hT : dominated_fin_meas_additive Î¼ T C) (hT' : dominated_fin_meas_additive Î¼ T' C') :
  dominated_fin_meas_additive Î¼ (T + T') (C + C') :=
begin
  refine âŸ¨hT.1.add hT'.1, Î» s hs hÎ¼s, _âŸ©,
  rw [pi.add_apply, add_mul],
  exact (norm_add_le _ _).trans (add_le_add (hT.2 s hs hÎ¼s) (hT'.2 s hs hÎ¼s)),
end

lemma smul [normed_field ğ•œ] [semi_normed_space ğ•œ Î²] (hT : dominated_fin_meas_additive Î¼ T C)
  (c : ğ•œ) :
  dominated_fin_meas_additive Î¼ (Î» s, c â€¢ (T s)) (âˆ¥câˆ¥ * C) :=
begin
  refine âŸ¨hT.1.smul c, Î» s hs hÎ¼s, _âŸ©,
  dsimp only,
  rw [norm_smul, mul_assoc],
  exact mul_le_mul le_rfl (hT.2 s hs hÎ¼s) (norm_nonneg _) (norm_nonneg _),
end

lemma of_measure_le {Î¼' : measure Î±} (h : Î¼ â‰¤ Î¼') (hT : dominated_fin_meas_additive Î¼ T C)
  (hC : 0 â‰¤ C) :
  dominated_fin_meas_additive Î¼' T C :=
begin
  have h' : âˆ€ s, measurable_set s â†’ Î¼ s = âˆ â†’ Î¼' s = âˆ,
  { intros s hs hÎ¼s, rw [eq_top_iff, â† hÎ¼s], exact h s hs, },
  refine âŸ¨hT.1.of_eq_top_imp_eq_top h', Î» s hs hÎ¼'s, _âŸ©,
  have hÎ¼s : Î¼ s < âˆ, from (h s hs).trans_lt hÎ¼'s,
  refine (hT.2 s hs hÎ¼s).trans (mul_le_mul le_rfl _ ennreal.to_real_nonneg hC),
  rw to_real_le_to_real hÎ¼s.ne hÎ¼'s.ne,
  exact h s hs,
end

lemma add_measure_right {m : measurable_space Î±}
  (Î¼ Î½ : measure Î±) (hT : dominated_fin_meas_additive Î¼ T C) (hC : 0 â‰¤ C) :
  dominated_fin_meas_additive (Î¼ + Î½) T C :=
 of_measure_le (measure.le_add_right le_rfl) hT hC

lemma add_measure_left {m : measurable_space Î±}
  (Î¼ Î½ : measure Î±) (hT : dominated_fin_meas_additive Î½ T C) (hC : 0 â‰¤ C) :
  dominated_fin_meas_additive (Î¼ + Î½) T C :=
 of_measure_le (measure.le_add_left le_rfl) hT hC

lemma of_smul_measure (c : â„â‰¥0âˆ) (hc_ne_top : c â‰  âˆ)
  (hT : dominated_fin_meas_additive (c â€¢ Î¼) T C) :
  dominated_fin_meas_additive Î¼ T (c.to_real * C) :=
begin
  have h : âˆ€ s, measurable_set s â†’ c â€¢ Î¼ s = âˆ â†’ Î¼ s = âˆ,
  { intros s hs hcÎ¼s,
    simp only [hc_ne_top, algebra.id.smul_eq_mul, with_top.mul_eq_top_iff, or_false, ne.def,
      false_and] at hcÎ¼s,
    exact hcÎ¼s.2, },
  refine âŸ¨hT.1.of_eq_top_imp_eq_top h, Î» s hs hÎ¼s, _âŸ©,
  have hcÎ¼s : c â€¢ Î¼ s â‰  âˆ, from mt (h s hs) hÎ¼s.ne,
  rw smul_eq_mul at hcÎ¼s,
  simp_rw [dominated_fin_meas_additive, measure.smul_apply, to_real_mul] at hT,
  refine (hT.2 s hs hcÎ¼s.lt_top).trans (le_of_eq _),
  ring,
end

lemma of_measure_le_smul {Î¼' : measure Î±} (c : â„â‰¥0âˆ) (hc : c â‰  âˆ) (h : Î¼ â‰¤ c â€¢ Î¼')
  (hT : dominated_fin_meas_additive Î¼ T C) (hC : 0 â‰¤ C) :
  dominated_fin_meas_additive Î¼' T (c.to_real * C) :=
(hT.of_measure_le h hC).of_smul_measure c hc

end dominated_fin_meas_additive

end fin_meas_additive

namespace simple_func

/-- Extend `set Î± â†’ (F â†’L[â„] F')` to `(Î± â†’â‚› F) â†’ F'`. -/
def set_to_simple_func {m : measurable_space Î±} (T : set Î± â†’ F â†’L[â„] F') (f : Î± â†’â‚› F) : F' :=
âˆ‘ x in f.range, T (f â»Â¹' {x}) x

@[simp] lemma set_to_simple_func_zero {m : measurable_space Î±} (f : Î± â†’â‚› F) :
  set_to_simple_func (0 : set Î± â†’ F â†’L[â„] F') f = 0 :=
by simp [set_to_simple_func]

@[simp] lemma set_to_simple_func_zero_apply {m : measurable_space Î±} (T : set Î± â†’ F â†’L[â„] F') :
  set_to_simple_func T (0 : Î± â†’â‚› F) = 0 :=
by casesI is_empty_or_nonempty Î±; simp [set_to_simple_func]

lemma set_to_simple_func_eq_sum_filter {m : measurable_space Î±}
  (T : set Î± â†’ F â†’L[â„] F') (f : Î± â†’â‚› F) :
  set_to_simple_func T f = âˆ‘ x in f.range.filter (Î» x, x â‰  0), (T (f â»Â¹' {x})) x :=
begin
  symmetry,
  refine sum_filter_of_ne (Î» x hx, mt (Î» hx0, _)),
  rw hx0,
  exact continuous_linear_map.map_zero _,
end

lemma set_to_simple_func_mono {G} [normed_linear_ordered_group G] [normed_space â„ G]
  {m : measurable_space Î±}
  (T : set Î± â†’ F â†’L[â„] G) (T' : set Î± â†’ F â†’L[â„] G) (hTT' : âˆ€ s x, T s x â‰¤ T' s x) (f : Î± â†’â‚› F) :
  set_to_simple_func T f â‰¤ set_to_simple_func T' f :=
by { simp_rw set_to_simple_func, exact sum_le_sum (Î» i hi, hTT' _ i), }

lemma map_set_to_simple_func (T : set Î± â†’ F â†’L[â„] F') (h_add : fin_meas_additive Î¼ T)
  {f : Î± â†’â‚› G} (hf : integrable f Î¼) {g : G â†’ F} (hg : g 0 = 0) :
  (f.map g).set_to_simple_func T = âˆ‘ x in f.range, T (f â»Â¹' {x}) (g x) :=
begin
  have T_empty : T âˆ… = 0, from h_add.map_empty_eq_zero,
  have hfp : âˆ€ x âˆˆ f.range, x â‰  0 â†’ Î¼ (f â»Â¹' {x}) â‰  âˆ,
    from Î» x hx hx0, (measure_preimage_lt_top_of_integrable f hf hx0).ne,
  simp only [set_to_simple_func, range_map],
  refine finset.sum_image' _ (assume b hb, _),
  rcases mem_range.1 hb with âŸ¨a, rflâŸ©,
  by_cases h0 : g (f a) = 0,
  { simp_rw h0,
    rw [continuous_linear_map.map_zero, finset.sum_eq_zero (Î» x hx, _)],
    rw mem_filter at hx,
    rw [hx.2, continuous_linear_map.map_zero], },
  have h_left_eq : T ((map g f) â»Â¹' {g (f a)}) (g (f a))
    = T (f â»Â¹' â†‘(f.range.filter (Î» b, g b = g (f a)))) (g (f a)),
  { congr, rw map_preimage_singleton, },
  rw h_left_eq,
  have h_left_eq' : T (f â»Â¹' â†‘(filter (Î» (b : G), g b = g (f a)) f.range)) (g (f a))
    = T (â‹ƒ y âˆˆ (filter (Î» (b : G), g b = g (f a)) f.range), f â»Â¹' {y}) (g (f a)),
  { congr, rw â† finset.set_bUnion_preimage_singleton, },
  rw h_left_eq',
  rw h_add.map_Union_fin_meas_set_eq_sum T T_empty,
  { simp only [filter_congr_decidable, sum_apply, continuous_linear_map.coe_sum'],
    refine finset.sum_congr rfl (Î» x hx, _),
    rw mem_filter at hx,
    rw hx.2, },
  { exact Î» i, measurable_set_fiber _ _, },
  { intros i hi,
    rw mem_filter at hi,
    refine hfp i hi.1 (Î» hi0, _),
    rw [hi0, hg] at hi,
    exact h0 hi.2.symm, },
  { intros i j hi hj hij,
    rw set.disjoint_iff,
    intros x hx,
    rw [set.mem_inter_iff, set.mem_preimage, set.mem_preimage, set.mem_singleton_iff,
      set.mem_singleton_iff] at hx,
    rw [â† hx.1, â† hx.2] at hij,
    exact absurd rfl hij, },
end

lemma set_to_simple_func_congr' (T : set Î± â†’ E â†’L[â„] F) (h_add : fin_meas_additive Î¼ T)
  {f g : Î± â†’â‚› E} (hf : integrable f Î¼) (hg : integrable g Î¼)
  (h : âˆ€ x y, x â‰  y â†’ T ((f â»Â¹' {x}) âˆ© (g â»Â¹' {y})) = 0) :
  f.set_to_simple_func T = g.set_to_simple_func T :=
show ((pair f g).map prod.fst).set_to_simple_func T
  = ((pair f g).map prod.snd).set_to_simple_func T, from
begin
  have h_pair : integrable (f.pair g) Î¼, from integrable_pair hf hg,
  rw map_set_to_simple_func T h_add h_pair prod.fst_zero,
  rw map_set_to_simple_func T h_add h_pair prod.snd_zero,
  refine finset.sum_congr rfl (Î» p hp, _),
  rcases mem_range.1 hp with âŸ¨a, rflâŸ©,
  by_cases eq : f a = g a,
  { dsimp only [pair_apply], rw eq },
  { have : T ((pair f g) â»Â¹' {(f a, g a)}) = 0,
    { have h_eq : T (â‡‘(f.pair g) â»Â¹' {(f a, g a)}) = T ((f â»Â¹' {f a}) âˆ© (g â»Â¹' {g a})),
      { congr, rw pair_preimage_singleton f g, },
      rw h_eq,
      exact h (f a) (g a) eq, },
    simp only [this, continuous_linear_map.zero_apply, pair_apply], },
end

lemma set_to_simple_func_congr (T : set Î± â†’ (E â†’L[â„] F))
  (h_zero : âˆ€ s, measurable_set s â†’ Î¼ s = 0 â†’ T s = 0) (h_add : fin_meas_additive Î¼ T)
  {f g : Î± â†’â‚› E} (hf : integrable f Î¼) (h : f =áµ[Î¼] g) :
  f.set_to_simple_func T = g.set_to_simple_func T :=
begin
  refine set_to_simple_func_congr' T h_add hf ((integrable_congr h).mp hf) _,
  refine Î» x y hxy, h_zero _ ((measurable_set_fiber f x).inter (measurable_set_fiber g y)) _,
  rw [eventually_eq, ae_iff] at h,
  refine measure_mono_null (Î» z, _) h,
  simp_rw [set.mem_inter_iff, set.mem_set_of_eq, set.mem_preimage, set.mem_singleton_iff],
  intro h,
  rwa [h.1, h.2],
end

lemma set_to_simple_func_add_left {m : measurable_space Î±} (T T' : set Î± â†’ F â†’L[â„] F')
  {f : Î± â†’â‚› F} :
  set_to_simple_func (T + T') f = set_to_simple_func T f + set_to_simple_func T' f :=
begin
  simp_rw [set_to_simple_func, pi.add_apply],
  push_cast,
  simp_rw [pi.add_apply, sum_add_distrib],
end

lemma set_to_simple_func_add_left' (T T' T'' : set Î± â†’ E â†’L[â„] F)
  (h_add : âˆ€ s, measurable_set s â†’ Î¼ s â‰  âˆ â†’ T'' s = T s + T' s) {f : Î± â†’â‚› E}
  (hf : integrable f Î¼) :
  set_to_simple_func (T'') f = set_to_simple_func T f + set_to_simple_func T' f :=
begin
  simp_rw [set_to_simple_func_eq_sum_filter],
  suffices : âˆ€ x âˆˆ filter (Î» (x : E), x â‰  0) f.range,
    T'' (f â»Â¹' {x}) = T (f â»Â¹' {x}) + T' (f â»Â¹' {x}),
  { rw â† sum_add_distrib,
    refine finset.sum_congr rfl (Î» x hx, _),
    rw this x hx,
    push_cast,
    rw pi.add_apply, },
  intros x hx,
  refine h_add (f â»Â¹' {x}) (measurable_set_preimage _ _)
    (measure_preimage_lt_top_of_integrable _ hf _).ne,
  rw mem_filter at hx,
  exact hx.2,
end

lemma set_to_simple_func_add (T : set Î± â†’ E â†’L[â„] F) (h_add : fin_meas_additive Î¼ T)
  {f g : Î± â†’â‚› E} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  set_to_simple_func T (f + g) = set_to_simple_func T f + set_to_simple_func T g :=
have hp_pair : integrable (f.pair g) Î¼, from integrable_pair hf hg,
calc set_to_simple_func T (f + g) = âˆ‘ x in (pair f g).range,
       T ((pair f g) â»Â¹' {x}) (x.fst + x.snd) :
  by { rw [add_eq_mapâ‚‚, map_set_to_simple_func T h_add hp_pair], simp, }
... = âˆ‘ x in (pair f g).range, (T ((pair f g) â»Â¹' {x}) x.fst + T ((pair f g) â»Â¹' {x}) x.snd) :
  finset.sum_congr rfl $ assume a ha, continuous_linear_map.map_add _ _ _
... = âˆ‘ x in (pair f g).range, T ((pair f g) â»Â¹' {x}) x.fst +
      âˆ‘ x in (pair f g).range, T ((pair f g) â»Â¹' {x}) x.snd :
  by rw finset.sum_add_distrib
... = ((pair f g).map prod.fst).set_to_simple_func T
    + ((pair f g).map prod.snd).set_to_simple_func T :
  by rw [map_set_to_simple_func T h_add hp_pair prod.snd_zero,
    map_set_to_simple_func T h_add hp_pair prod.fst_zero]

lemma set_to_simple_func_neg (T : set Î± â†’ E â†’L[â„] F) (h_add : fin_meas_additive Î¼ T)
  {f : Î± â†’â‚› E} (hf : integrable f Î¼) :
  set_to_simple_func T (-f) = - set_to_simple_func T f :=
calc set_to_simple_func T (-f) = set_to_simple_func T (f.map (has_neg.neg)) : rfl
  ... = - set_to_simple_func T f :
  begin
    rw [map_set_to_simple_func T h_add hf neg_zero, set_to_simple_func,
      â† sum_neg_distrib],
    exact finset.sum_congr rfl (Î» x h, continuous_linear_map.map_neg _ _),
  end

lemma set_to_simple_func_sub (T : set Î± â†’ E â†’L[â„] F) (h_add : fin_meas_additive Î¼ T)
  {f g : Î± â†’â‚› E} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  set_to_simple_func T (f - g) = set_to_simple_func T f - set_to_simple_func T g :=
begin
  rw [sub_eq_add_neg, set_to_simple_func_add T h_add hf,
    set_to_simple_func_neg T h_add hg, sub_eq_add_neg],
  rw integrable_iff at hg âŠ¢,
  intros x hx_ne,
  change Î¼ ((has_neg.neg âˆ˜ g) â»Â¹' {x}) < âˆ,
  rw [preimage_comp, neg_preimage, neg_singleton],
  refine hg (-x) _,
  simp [hx_ne],
end

lemma set_to_simple_func_smul_real (T : set Î± â†’ E â†’L[â„] F) (h_add : fin_meas_additive Î¼ T)
  (c : â„) {f : Î± â†’â‚› E} (hf : integrable f Î¼) :
  set_to_simple_func T (c â€¢ f) = c â€¢ set_to_simple_func T f :=
calc set_to_simple_func T (c â€¢ f) = âˆ‘ x in f.range, T (f â»Â¹' {x}) (c â€¢ x) :
  by { rw [smul_eq_map c f, map_set_to_simple_func T h_add hf], rw smul_zero, }
... = âˆ‘ x in f.range, c â€¢ (T (f â»Â¹' {x}) x) :
  finset.sum_congr rfl $ Î» b hb, by { rw continuous_linear_map.map_smul (T (f â»Â¹' {b})) c b, }
... = c â€¢ set_to_simple_func T f :
by simp only [set_to_simple_func, smul_sum, smul_smul, mul_comm]

lemma set_to_simple_func_smul {E} [measurable_space E] [normed_group E] [normed_field ğ•œ]
  [normed_space ğ•œ E] [normed_space â„ E] [normed_space ğ•œ F] (T : set Î± â†’ E â†’L[â„] F)
  (h_add : fin_meas_additive Î¼ T) (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x)
  (c : ğ•œ) {f : Î± â†’â‚› E} (hf : integrable f Î¼) :
  set_to_simple_func T (c â€¢ f) = c â€¢ set_to_simple_func T f :=
calc set_to_simple_func T (c â€¢ f) = âˆ‘ x in f.range, T (f â»Â¹' {x}) (c â€¢ x) :
  by { rw [smul_eq_map c f, map_set_to_simple_func T h_add hf],
    rw smul_zero, }
... = âˆ‘ x in f.range, c â€¢ (T (f â»Â¹' {x}) x) : finset.sum_congr rfl $ Î» b hb, by { rw h_smul, }
... = c â€¢ set_to_simple_func T f : by simp only [set_to_simple_func, smul_sum, smul_smul, mul_comm]

lemma norm_set_to_simple_func_le_sum_op_norm {m : measurable_space Î±}
  (T : set Î± â†’ F' â†’L[â„] F) (f : Î± â†’â‚› F') :
  âˆ¥f.set_to_simple_func Tâˆ¥ â‰¤ âˆ‘ x in f.range, âˆ¥T (f â»Â¹' {x})âˆ¥ * âˆ¥xâˆ¥ :=
calc âˆ¥âˆ‘ x in f.range, T (f â»Â¹' {x}) xâˆ¥
    â‰¤ âˆ‘ x in f.range, âˆ¥T (f â»Â¹' {x}) xâˆ¥ : norm_sum_le _ _
... â‰¤ âˆ‘ x in f.range, âˆ¥T (f â»Â¹' {x})âˆ¥ * âˆ¥xâˆ¥ :
  by { refine finset.sum_le_sum (Î»b hb, _), simp_rw continuous_linear_map.le_op_norm, }

lemma norm_set_to_simple_func_le_sum_mul_norm_of_integrable (T : set Î± â†’ E â†’L[â„] F') {C : â„}
  (hT_norm : âˆ€ s, measurable_set s â†’ Î¼ s < âˆ â†’ âˆ¥T sâˆ¥ â‰¤ C * (Î¼ s).to_real) (f : Î± â†’â‚› E)
  (hf : integrable f Î¼) :
  âˆ¥f.set_to_simple_func Tâˆ¥ â‰¤ C * âˆ‘ x in f.range, (Î¼ (f â»Â¹' {x})).to_real * âˆ¥xâˆ¥ :=
calc âˆ¥f.set_to_simple_func Tâˆ¥
    â‰¤ âˆ‘ x in f.range, âˆ¥T (f â»Â¹' {x})âˆ¥ * âˆ¥xâˆ¥ : norm_set_to_simple_func_le_sum_op_norm T f
... â‰¤ âˆ‘ x in f.range, C * (Î¼ (f â»Â¹' {x})).to_real * âˆ¥xâˆ¥ :
  begin
    refine finset.sum_le_sum (Î» b hb, _),
    by_cases hb : âˆ¥bâˆ¥ = 0,
    { rw hb, simp, },
    rw _root_.mul_le_mul_right _,
    { refine hT_norm _ (simple_func.measurable_set_fiber _ _)
        (simple_func.measure_preimage_lt_top_of_integrable _ hf _),
      rwa norm_eq_zero at hb, },
    { exact lt_of_le_of_ne (norm_nonneg _) (ne.symm hb), },
  end
... â‰¤ C * âˆ‘ x in f.range, (Î¼ (f â»Â¹' {x})).to_real * âˆ¥xâˆ¥ : by simp_rw [mul_sum, â† mul_assoc]

lemma set_to_simple_func_indicator (T : set Î± â†’ F â†’L[â„] F') (hT_empty : T âˆ… = 0)
  {m : measurable_space Î±} {s : set Î±} (hs : measurable_set s) (x : F) :
  simple_func.set_to_simple_func T
    (simple_func.piecewise s hs (simple_func.const Î± x) (simple_func.const Î± 0))
  = T s x :=
begin
  by_cases hs_empty : s = âˆ…,
  { simp only [hs_empty, hT_empty, continuous_linear_map.zero_apply, piecewise_empty, const_zero,
    set_to_simple_func_zero_apply], },
  by_cases hs_univ : s = univ,
  { casesI hÎ± : is_empty_or_nonempty Î±,
    { refine absurd _ hs_empty,
      haveI : subsingleton (set Î±), by { unfold set, apply_instance, },
      exact subsingleton.elim s âˆ…, },
    simp [hs_univ, set_to_simple_func], },
  simp_rw set_to_simple_func,
  rw [â† ne.def, set.ne_empty_iff_nonempty] at hs_empty,
  rw range_indicator hs hs_empty hs_univ,
  by_cases hx0 : x = 0,
  { simp_rw hx0, simp, },
  rw sum_insert,
  swap, { rw finset.mem_singleton, exact hx0, },
  rw [sum_singleton, (T _).map_zero, add_zero],
  congr,
  simp only [coe_piecewise, piecewise_eq_indicator, coe_const, pi.const_zero,
    piecewise_eq_indicator],
  rw [indicator_preimage, preimage_const_of_mem],
  swap, { exact set.mem_singleton x, },
  rw [â† pi.const_zero, preimage_const_of_not_mem],
  swap, { rw set.mem_singleton_iff, exact ne.symm hx0, },
  simp,
end

end simple_func

namespace L1

open ae_eq_fun Lp.simple_func Lp

variables {Î± E Î¼}

namespace simple_func

lemma norm_eq_sum_mul [second_countable_topology G] [borel_space G] (f : Î± â†’â‚â‚›[Î¼] G) :
  âˆ¥fâˆ¥ = âˆ‘ x in (to_simple_func f).range, (Î¼ ((to_simple_func f) â»Â¹' {x})).to_real * âˆ¥xâˆ¥ :=
begin
  rw [norm_to_simple_func, snorm_one_eq_lintegral_nnnorm],
  have h_eq := simple_func.map_apply (Î» x, (nnnorm x : â„â‰¥0âˆ)) (to_simple_func f),
  dsimp only at h_eq,
  simp_rw â† h_eq,
  rw [simple_func.lintegral_eq_lintegral, simple_func.map_lintegral, ennreal.to_real_sum],
  { congr,
    ext1 x,
    rw [ennreal.to_real_mul, mul_comm, â† of_real_norm_eq_coe_nnnorm,
      ennreal.to_real_of_real (norm_nonneg _)], },
  { intros x hx,
    by_cases hx0 : x = 0,
    { rw hx0, simp, },
    { exact ennreal.mul_ne_top ennreal.coe_ne_top
        (simple_func.measure_preimage_lt_top_of_integrable _ (simple_func.integrable f) hx0).ne } }
end

section set_to_L1s

variables [second_countable_topology E] [borel_space E] [normed_field ğ•œ] [normed_space ğ•œ E]

local attribute [instance] Lp.simple_func.module
local attribute [instance] Lp.simple_func.normed_space

/-- Extend `set Î± â†’ (E â†’L[â„] F')` to `(Î± â†’â‚â‚›[Î¼] E) â†’ F'`. -/
def set_to_L1s (T : set Î± â†’ E â†’L[â„] F) (f : Î± â†’â‚â‚›[Î¼] E) : F :=
(to_simple_func f).set_to_simple_func T

lemma set_to_L1s_eq_set_to_simple_func (T : set Î± â†’ E â†’L[â„] F) (f : Î± â†’â‚â‚›[Î¼] E) :
  set_to_L1s T f = (to_simple_func f).set_to_simple_func T :=
rfl

lemma set_to_L1s_congr (T : set Î± â†’ E â†’L[â„] F) (h_zero : âˆ€ s, measurable_set s â†’ Î¼ s = 0 â†’ T s = 0)
  (h_add : fin_meas_additive Î¼ T)
  {f g : Î± â†’â‚â‚›[Î¼] E} (h : to_simple_func f =áµ[Î¼] to_simple_func g) :
  set_to_L1s T f = set_to_L1s T g :=
simple_func.set_to_simple_func_congr T h_zero h_add (simple_func.integrable f) h

lemma set_to_L1s_add (T : set Î± â†’ E â†’L[â„] F) (h_zero : âˆ€ s, measurable_set s â†’ Î¼ s = 0 â†’ T s = 0)
  (h_add : fin_meas_additive Î¼ T) (f g : Î± â†’â‚â‚›[Î¼] E) :
  set_to_L1s T (f + g) = set_to_L1s T f + set_to_L1s T g :=
begin
  simp_rw set_to_L1s,
  rw â† simple_func.set_to_simple_func_add T h_add
    (simple_func.integrable f) (simple_func.integrable g),
  exact simple_func.set_to_simple_func_congr T h_zero h_add (simple_func.integrable _)
    (add_to_simple_func f g),
end

lemma set_to_L1s_smul_real (T : set Î± â†’ E â†’L[â„] F)
  (h_zero : âˆ€ s, measurable_set s â†’ Î¼ s = 0 â†’ T s = 0) (h_add : fin_meas_additive Î¼ T)
  (c : â„) (f : Î± â†’â‚â‚›[Î¼] E) :
  set_to_L1s T (c â€¢ f) = c â€¢ set_to_L1s T f :=
begin
  simp_rw set_to_L1s,
  rw â† simple_func.set_to_simple_func_smul_real T h_add c (simple_func.integrable f),
  refine simple_func.set_to_simple_func_congr T h_zero h_add (simple_func.integrable _) _,
  exact smul_to_simple_func c f,
end

lemma set_to_L1s_smul {E} [normed_group E] [measurable_space E] [normed_space â„ E]
  [normed_space ğ•œ E] [second_countable_topology E] [borel_space E] [normed_space ğ•œ F]
  [measurable_space ğ•œ] [opens_measurable_space ğ•œ]
  (T : set Î± â†’ E â†’L[â„] F) (h_zero : âˆ€ s, measurable_set s â†’ Î¼ s = 0 â†’ T s = 0)
  (h_add : fin_meas_additive Î¼ T)
  (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (c : ğ•œ) (f : Î± â†’â‚â‚›[Î¼] E) :
  set_to_L1s T (c â€¢ f) = c â€¢ set_to_L1s T f :=
begin
  simp_rw set_to_L1s,
  rw â† simple_func.set_to_simple_func_smul T h_add h_smul c (simple_func.integrable f),
  refine simple_func.set_to_simple_func_congr T h_zero h_add (simple_func.integrable _) _,
  exact smul_to_simple_func c f,
end

lemma norm_set_to_L1s_le (T : set Î± â†’ E â†’L[â„] F) {C : â„}
  (hT_norm : âˆ€ s, measurable_set s â†’ Î¼ s < âˆ â†’ âˆ¥T sâˆ¥ â‰¤ C * (Î¼ s).to_real) (f : Î± â†’â‚â‚›[Î¼] E) :
  âˆ¥set_to_L1s T fâˆ¥ â‰¤ C * âˆ¥fâˆ¥ :=
begin
  rw [set_to_L1s, norm_eq_sum_mul f],
  exact simple_func.norm_set_to_simple_func_le_sum_mul_norm_of_integrable T hT_norm _
    (simple_func.integrable f),
end

lemma set_to_L1s_indicator_const {T : set Î± â†’ E â†’L[â„] F} {C : â„} {s : set Î±}
  (hT : dominated_fin_meas_additive Î¼ T C) (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) :
  set_to_L1s T (simple_func.indicator_const 1 hs hÎ¼s x) = T s x :=
begin
  have h_empty : T âˆ… = 0, from hT.eq_zero_of_measure_zero measurable_set.empty measure_empty,
  rw set_to_L1s_eq_set_to_simple_func,
  refine eq.trans _ (simple_func.set_to_simple_func_indicator T h_empty hs x),
  refine simple_func.set_to_simple_func_congr T (Î» _, hT.eq_zero_of_measure_zero) hT.1
    (simple_func.integrable _) _,
  exact Lp.simple_func.to_simple_func_indicator_const hs hÎ¼s x,
end

variables [normed_space ğ•œ F] [measurable_space ğ•œ] [opens_measurable_space ğ•œ]

variables (Î± E Î¼ ğ•œ)
/-- Extend `set Î± â†’ E â†’L[â„] F` to `(Î± â†’â‚â‚›[Î¼] E) â†’L[ğ•œ] F`. -/
def set_to_L1s_clm' {T : set Î± â†’ E â†’L[â„] F} {C : â„} (hT : dominated_fin_meas_additive Î¼ T C)
  (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) :
  (Î± â†’â‚â‚›[Î¼] E) â†’L[ğ•œ] F :=
linear_map.mk_continuous âŸ¨set_to_L1s T, set_to_L1s_add T (Î» _, hT.eq_zero_of_measure_zero) hT.1,
  set_to_L1s_smul T (Î» _, hT.eq_zero_of_measure_zero) hT.1 h_smulâŸ© C
  (Î» f, norm_set_to_L1s_le T hT.2 f)

/-- Extend `set Î± â†’ E â†’L[â„] F` to `(Î± â†’â‚â‚›[Î¼] E) â†’L[â„] F`. -/
def set_to_L1s_clm {T : set Î± â†’ E â†’L[â„] F} {C : â„} (hT : dominated_fin_meas_additive Î¼ T C) :
  (Î± â†’â‚â‚›[Î¼] E) â†’L[â„] F :=
linear_map.mk_continuous âŸ¨set_to_L1s T, set_to_L1s_add T (Î» _, hT.eq_zero_of_measure_zero) hT.1,
  set_to_L1s_smul_real T (Î» _, hT.eq_zero_of_measure_zero) hT.1âŸ© C
  (Î» f, norm_set_to_L1s_le T hT.2 f)

variables {Î± E Î¼ ğ•œ}

lemma norm_set_to_L1s_clm_le {T : set Î± â†’ E â†’L[â„] F} {C : â„}
  (hT : dominated_fin_meas_additive Î¼ T C) (hC : 0 â‰¤ C) :
  âˆ¥set_to_L1s_clm Î± E Î¼ hTâˆ¥ â‰¤ C :=
linear_map.mk_continuous_norm_le _ hC _

lemma norm_set_to_L1s_clm_le' {T : set Î± â†’ E â†’L[â„] F} {C : â„}
  (hT : dominated_fin_meas_additive Î¼ T C) :
  âˆ¥set_to_L1s_clm Î± E Î¼ hTâˆ¥ â‰¤ max C 0 :=
linear_map.mk_continuous_norm_le' _ _

end set_to_L1s

end simple_func

open simple_func

section set_to_L1

local attribute [instance] Lp.simple_func.module
local attribute [instance] Lp.simple_func.normed_space

variables (ğ•œ) [nondiscrete_normed_field ğ•œ] [measurable_space ğ•œ] [opens_measurable_space ğ•œ]
  [second_countable_topology E] [borel_space E] [normed_space ğ•œ E]
  [normed_space ğ•œ F] [complete_space F]
  {T : set Î± â†’ E â†’L[â„] F} {C : â„}

/-- Extend `set Î± â†’ (E â†’L[â„] F)` to `(Î± â†’â‚[Î¼] E) â†’L[ğ•œ] F`. -/
def set_to_L1' (hT : dominated_fin_meas_additive Î¼ T C)
  (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) :
  (Î± â†’â‚[Î¼] E) â†’L[ğ•œ] F :=
(set_to_L1s_clm' Î± E ğ•œ Î¼ hT h_smul).extend
  (coe_to_Lp Î± E ğ•œ) (simple_func.dense_range one_ne_top) simple_func.uniform_inducing

variables {ğ•œ}

/-- Extend `set Î± â†’ E â†’L[â„] F` to `(Î± â†’â‚[Î¼] E) â†’L[â„] F`. -/
def set_to_L1 (hT : dominated_fin_meas_additive Î¼ T C) : (Î± â†’â‚[Î¼] E) â†’L[â„] F :=
(set_to_L1s_clm Î± E Î¼ hT).extend
  (coe_to_Lp Î± E â„) (simple_func.dense_range one_ne_top) simple_func.uniform_inducing

lemma set_to_L1_eq_set_to_L1s_clm (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’â‚â‚›[Î¼] E) :
  set_to_L1 hT f = set_to_L1s_clm Î± E Î¼ hT f :=
uniformly_extend_of_ind simple_func.uniform_inducing (simple_func.dense_range one_ne_top)
  (set_to_L1s_clm Î± E Î¼ hT).uniform_continuous _

lemma set_to_L1_eq_set_to_L1' (hT : dominated_fin_meas_additive Î¼ T C)
  (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (f : Î± â†’â‚[Î¼] E) :
  set_to_L1 hT f = set_to_L1' ğ•œ hT h_smul f :=
rfl

lemma set_to_L1_smul (hT : dominated_fin_meas_additive Î¼ T C)
  (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (c : ğ•œ) (f : Î± â†’â‚[Î¼] E) :
  set_to_L1 hT (c â€¢ f) = c â€¢ set_to_L1 hT f :=
begin
  rw [set_to_L1_eq_set_to_L1' hT h_smul, set_to_L1_eq_set_to_L1' hT h_smul],
  exact continuous_linear_map.map_smul _ _ _,
end

lemma set_to_L1_indicator_const_Lp (hT : dominated_fin_meas_additive Î¼ T C) {s : set Î±}
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) :
  set_to_L1 hT (indicator_const_Lp 1 hs hÎ¼s x) = T s x :=
begin
  rw [â† Lp.simple_func.coe_indicator_const hs hÎ¼s x, set_to_L1_eq_set_to_L1s_clm],
  exact set_to_L1s_indicator_const hT hs hÎ¼s x,
end

lemma norm_set_to_L1_le_norm_set_to_L1s_clm (hT : dominated_fin_meas_additive Î¼ T C) :
  âˆ¥set_to_L1 hTâˆ¥ â‰¤ âˆ¥set_to_L1s_clm Î± E Î¼ hTâˆ¥ :=
calc âˆ¥set_to_L1 hTâˆ¥
    â‰¤ (1 : â„â‰¥0) * âˆ¥set_to_L1s_clm Î± E Î¼ hTâˆ¥ : begin
      refine continuous_linear_map.op_norm_extend_le (set_to_L1s_clm Î± E Î¼ hT) (coe_to_Lp Î± E â„)
        (simple_func.dense_range one_ne_top) (Î» x, le_of_eq _),
      rw [nnreal.coe_one, one_mul],
      refl,
    end
... = âˆ¥set_to_L1s_clm Î± E Î¼ hTâˆ¥ : by rw [nnreal.coe_one, one_mul]

lemma norm_set_to_L1_le_mul_norm (hT : dominated_fin_meas_additive Î¼ T C) (hC : 0 â‰¤ C)
  (f : Î± â†’â‚[Î¼] E) :
  âˆ¥set_to_L1 hT fâˆ¥ â‰¤ C * âˆ¥fâˆ¥ :=
calc âˆ¥set_to_L1 hT fâˆ¥
    â‰¤ âˆ¥set_to_L1s_clm Î± E Î¼ hTâˆ¥ * âˆ¥fâˆ¥ :
  continuous_linear_map.le_of_op_norm_le _ (norm_set_to_L1_le_norm_set_to_L1s_clm hT) _
... â‰¤ C * âˆ¥fâˆ¥ : mul_le_mul (norm_set_to_L1s_clm_le hT hC) le_rfl (norm_nonneg _) hC

lemma norm_set_to_L1_le_mul_norm' (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’â‚[Î¼] E) :
  âˆ¥set_to_L1 hT fâˆ¥ â‰¤ max C 0 * âˆ¥fâˆ¥ :=
calc âˆ¥set_to_L1 hT fâˆ¥
    â‰¤ âˆ¥set_to_L1s_clm Î± E Î¼ hTâˆ¥ * âˆ¥fâˆ¥ :
  continuous_linear_map.le_of_op_norm_le _ (norm_set_to_L1_le_norm_set_to_L1s_clm hT) _
... â‰¤ max C 0 * âˆ¥fâˆ¥ :
  mul_le_mul (norm_set_to_L1s_clm_le' hT) le_rfl (norm_nonneg _) (le_max_right _ _)

lemma norm_set_to_L1_le (hT : dominated_fin_meas_additive Î¼ T C) (hC : 0 â‰¤ C) :
  âˆ¥set_to_L1 hTâˆ¥ â‰¤ C :=
continuous_linear_map.op_norm_le_bound _ hC (norm_set_to_L1_le_mul_norm hT hC)

lemma norm_set_to_L1_le' (hT : dominated_fin_meas_additive Î¼ T C) :
  âˆ¥set_to_L1 hTâˆ¥ â‰¤ max C 0 :=
continuous_linear_map.op_norm_le_bound _ (le_max_right _ _) (norm_set_to_L1_le_mul_norm' hT)

lemma set_to_L1_lipschitz (hT : dominated_fin_meas_additive Î¼ T C) :
  lipschitz_with (real.to_nnreal C) (set_to_L1 hT) :=
(set_to_L1 hT).lipschitz.weaken (norm_set_to_L1_le' hT)

/-- If `fs i â†’ f` in `L1`, then `set_to_L1 hT (fs i) â†’ set_to_L1 hT f`. -/
lemma tendsto_set_to_L1 (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’â‚[Î¼] E)
  {Î¹} (fs : Î¹ â†’ Î± â†’â‚[Î¼] E) {l : filter Î¹} (hfs : tendsto fs l (ğ“ f)) :
  tendsto (Î» i, set_to_L1 hT (fs i)) l (ğ“ $ set_to_L1 hT f) :=
((set_to_L1 hT).continuous.tendsto _).comp hfs

end set_to_L1

end L1

section function

variables [second_countable_topology E] [borel_space E] [complete_space F]
  {T : set Î± â†’ E â†’L[â„] F} {C : â„} {f g : Î± â†’ E}

variables (Î¼ T)
/-- Extend `T : set Î± â†’ E â†’L[â„] F` to `(Î± â†’ E) â†’ F` (for integrable functions `Î± â†’ E`). We set it to
0 if the function is not integrable. -/
def set_to_fun (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’ E) : F :=
if hf : integrable f Î¼ then L1.set_to_L1 hT (hf.to_L1 f) else 0

variables {Î¼ T}

lemma set_to_fun_eq (hT : dominated_fin_meas_additive Î¼ T C) (hf : integrable f Î¼) :
  set_to_fun Î¼ T hT f = L1.set_to_L1 hT (hf.to_L1 f) :=
dif_pos hf

lemma L1.set_to_fun_eq_set_to_L1 (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’â‚[Î¼] E) :
  set_to_fun Î¼ T hT f = L1.set_to_L1 hT f :=
by rw [set_to_fun_eq hT (L1.integrable_coe_fn f), integrable.to_L1_coe_fn]

lemma set_to_fun_undef (hT : dominated_fin_meas_additive Î¼ T C) (hf : Â¬ integrable f Î¼) :
  set_to_fun Î¼ T hT f = 0 :=
dif_neg hf

lemma set_to_fun_non_ae_measurable (hT : dominated_fin_meas_additive Î¼ T C)
  (hf : Â¬ ae_measurable f Î¼) :
  set_to_fun Î¼ T hT f = 0 :=
set_to_fun_undef hT (not_and_of_not_left _ hf)

@[simp] lemma set_to_fun_zero (hT : dominated_fin_meas_additive Î¼ T C) :
  set_to_fun Î¼ T hT (0 : Î± â†’ E) = 0 :=
begin
  rw set_to_fun_eq hT,
  { simp only [integrable.to_L1_zero, continuous_linear_map.map_zero], },
  { exact integrable_zero _ _ _, },
end

lemma set_to_fun_add (hT : dominated_fin_meas_additive Î¼ T C)
  (hf : integrable f Î¼) (hg : integrable g Î¼) :
  set_to_fun Î¼ T hT (f + g) = set_to_fun Î¼ T hT f + set_to_fun Î¼ T hT g :=
by rw [set_to_fun_eq hT (hf.add hg), set_to_fun_eq hT hf, set_to_fun_eq hT hg, integrable.to_L1_add,
  (L1.set_to_L1 hT).map_add]

lemma set_to_fun_neg (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’ E) :
  set_to_fun Î¼ T hT (-f) = - set_to_fun Î¼ T hT f :=
begin
  by_cases hf : integrable f Î¼,
  { rw [set_to_fun_eq hT hf, set_to_fun_eq hT hf.neg,
      integrable.to_L1_neg, (L1.set_to_L1 hT).map_neg], },
  { rw [set_to_fun_undef hT hf, set_to_fun_undef hT, neg_zero],
    rwa [â† integrable_neg_iff] at hf, }
end

lemma set_to_fun_sub (hT : dominated_fin_meas_additive Î¼ T C)
  (hf : integrable f Î¼) (hg : integrable g Î¼) :
  set_to_fun Î¼ T hT (f - g) = set_to_fun Î¼ T hT f - set_to_fun Î¼ T hT g :=
by rw [sub_eq_add_neg, sub_eq_add_neg, set_to_fun_add hT hf hg.neg, set_to_fun_neg hT g]

lemma set_to_fun_smul [nondiscrete_normed_field ğ•œ] [measurable_space ğ•œ] [opens_measurable_space ğ•œ]
  [normed_space ğ•œ E] [normed_space ğ•œ F] (hT : dominated_fin_meas_additive Î¼ T C)
  (h_smul : âˆ€ c : ğ•œ, âˆ€ s x, T s (c â€¢ x) = c â€¢ T s x) (c : ğ•œ) (f : Î± â†’ E) :
  set_to_fun Î¼ T hT (c â€¢ f) = c â€¢ set_to_fun Î¼ T hT f :=
begin
  by_cases hf : integrable f Î¼,
  { rw [set_to_fun_eq hT hf, set_to_fun_eq hT, integrable.to_L1_smul',
      L1.set_to_L1_smul hT h_smul c _], },
  { by_cases hr : c = 0,
    { rw hr, simp, },
    { have hf' : Â¬ integrable (c â€¢ f) Î¼, by rwa [integrable_smul_iff hr f],
      rw [set_to_fun_undef hT hf, set_to_fun_undef hT hf',
        smul_zero], }, },
end

lemma set_to_fun_congr_ae (hT : dominated_fin_meas_additive Î¼ T C) (h : f =áµ[Î¼] g) :
  set_to_fun Î¼ T hT f = set_to_fun Î¼ T hT g :=
begin
  by_cases hfi : integrable f Î¼,
  { have hgi : integrable g Î¼ := hfi.congr h,
    rw [set_to_fun_eq hT hfi, set_to_fun_eq hT hgi,
      (integrable.to_L1_eq_to_L1_iff f g hfi hgi).2 h] },
  { have hgi : Â¬ integrable g Î¼, { rw integrable_congr h at hfi, exact hfi },
    rw [set_to_fun_undef hT hfi, set_to_fun_undef hT hgi] },
end

lemma set_to_fun_to_L1 (hT : dominated_fin_meas_additive Î¼ T C) (hf : integrable f Î¼) :
  set_to_fun Î¼ T hT (hf.to_L1 f) = set_to_fun Î¼ T hT f :=
set_to_fun_congr_ae hT hf.coe_fn_to_L1

lemma set_to_fun_indicator_const (hT : dominated_fin_meas_additive Î¼ T C) {s : set Î±}
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) :
  set_to_fun Î¼ T hT (s.indicator (Î» _, x)) = T s x :=
begin
  rw set_to_fun_congr_ae hT (@indicator_const_Lp_coe_fn _ _ _ 1 _ _ _ _ hs hÎ¼s x _ _).symm,
  rw L1.set_to_fun_eq_set_to_L1 hT,
  exact L1.set_to_L1_indicator_const_Lp hT hs hÎ¼s x,
end

@[continuity]
lemma continuous_set_to_fun (hT : dominated_fin_meas_additive Î¼ T C) :
  continuous (Î» (f : Î± â†’â‚[Î¼] E), set_to_fun Î¼ T hT f) :=
by { simp_rw L1.set_to_fun_eq_set_to_L1 hT, exact continuous_linear_map.continuous _, }

lemma norm_set_to_fun_le_mul_norm (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’â‚[Î¼] E)
  (hC : 0 â‰¤ C) :
  âˆ¥set_to_fun Î¼ T hT fâˆ¥ â‰¤ C * âˆ¥fâˆ¥ :=
by { rw L1.set_to_fun_eq_set_to_L1, exact L1.norm_set_to_L1_le_mul_norm hT hC f, }

lemma norm_set_to_fun_le_mul_norm' (hT : dominated_fin_meas_additive Î¼ T C) (f : Î± â†’â‚[Î¼] E) :
  âˆ¥set_to_fun Î¼ T hT fâˆ¥ â‰¤ max C 0 * âˆ¥fâˆ¥ :=
by { rw L1.set_to_fun_eq_set_to_L1, exact L1.norm_set_to_L1_le_mul_norm' hT f, }

lemma norm_set_to_fun_le (hT : dominated_fin_meas_additive Î¼ T C) (hf : integrable f Î¼)
  (hC : 0 â‰¤ C) :
  âˆ¥set_to_fun Î¼ T hT fâˆ¥ â‰¤ C * âˆ¥hf.to_L1 fâˆ¥ :=
by { rw set_to_fun_eq hT hf, exact L1.norm_set_to_L1_le_mul_norm hT hC _, }

lemma norm_set_to_fun_le' (hT : dominated_fin_meas_additive Î¼ T C) (hf : integrable f Î¼) :
  âˆ¥set_to_fun Î¼ T hT fâˆ¥ â‰¤ max C 0 * âˆ¥hf.to_L1 fâˆ¥ :=
by { rw set_to_fun_eq hT hf, exact L1.norm_set_to_L1_le_mul_norm' hT _, }

/-- Lebesgue dominated convergence theorem provides sufficient conditions under which almost
  everywhere convergence of a sequence of functions implies the convergence of their image by
  `set_to_fun`.
  We could weaken the condition `bound_integrable` to require `has_finite_integral bound Î¼` instead
  (i.e. not requiring that `bound` is measurable), but in all applications proving integrability
  is easier. -/
theorem tendsto_set_to_fun_of_dominated_convergence (hT : dominated_fin_meas_additive Î¼ T C)
  {fs : â„• â†’ Î± â†’ E} {f : Î± â†’ E} (bound : Î± â†’ â„) (fs_measurable : âˆ€ n, ae_measurable (fs n) Î¼)
  (bound_integrable : integrable bound Î¼) (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥fs n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, fs n a) at_top (ğ“ (f a))) :
  tendsto (Î» n, set_to_fun Î¼ T hT (fs n)) at_top (ğ“ $ set_to_fun Î¼ T hT f) :=
begin
  /- `f` is a.e.-measurable, since it is the a.e.-pointwise limit of a.e.-measurable functions. -/
  have f_measurable : ae_measurable f Î¼ := ae_measurable_of_tendsto_metric_ae fs_measurable h_lim,
  /- all functions we consider are integrable -/
  have fs_int : âˆ€ n, integrable (fs n) Î¼ :=
    Î» n, bound_integrable.mono' (fs_measurable n) (h_bound _),
  have f_int : integrable f Î¼ :=
  âŸ¨f_measurable, has_finite_integral_of_dominated_convergence
    bound_integrable.has_finite_integral h_bound h_limâŸ©,
  /- it suffices to prove the result for the corresponding L1 functions -/
  suffices : tendsto (Î» n, L1.set_to_L1 hT ((fs_int n).to_L1 (fs n))) at_top
    (ğ“ (L1.set_to_L1 hT (f_int.to_L1 f))),
  { convert this,
    { ext1 n, exact set_to_fun_eq hT (fs_int n), },
    { exact set_to_fun_eq hT f_int, }, },
  /- the convergence of set_to_L1 follows from the convergence of the L1 functions -/
  refine L1.tendsto_set_to_L1 hT _ _ _,
  /- up to some rewriting, what we need to prove is `h_lim` -/
  rw tendsto_iff_norm_tendsto_zero,
  have lintegral_norm_tendsto_zero :
    tendsto (Î»n, ennreal.to_real $ âˆ«â» a, (ennreal.of_real âˆ¥fs n a - f aâˆ¥) âˆ‚Î¼) at_top (ğ“ 0) :=
  (tendsto_to_real zero_ne_top).comp
    (tendsto_lintegral_norm_of_dominated_convergence
      fs_measurable bound_integrable.has_finite_integral h_bound h_lim),
  convert lintegral_norm_tendsto_zero,
  ext1 n,
  rw L1.norm_def,
  congr' 1,
  refine lintegral_congr_ae _,
  rw â† integrable.to_L1_sub,
  refine ((fs_int n).sub f_int).coe_fn_to_L1.mono (Î» x hx, _),
  dsimp only,
  rw [hx, of_real_norm_eq_coe_nnnorm, pi.sub_apply],
end

/-- Lebesgue dominated convergence theorem for filters with a countable basis -/
lemma tendsto_set_to_fun_filter_of_dominated_convergence (hT : dominated_fin_meas_additive Î¼ T C)
  {Î¹} {l : _root_.filter Î¹} [l.is_countably_generated]
  {fs : Î¹ â†’ Î± â†’ E} {f : Î± â†’ E} (bound : Î± â†’ â„)
  (hfs_meas : âˆ€á¶  n in l, ae_measurable (fs n) Î¼)
  (h_bound : âˆ€á¶  n in l, âˆ€áµ a âˆ‚Î¼, âˆ¥fs n aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, fs n a) l (ğ“ (f a))) :
  tendsto (Î» n, set_to_fun Î¼ T hT (fs n)) l (ğ“ $ set_to_fun Î¼ T hT f) :=
begin
  rw tendsto_iff_seq_tendsto,
  intros x xl,
  have hxl : âˆ€ s âˆˆ l, âˆƒ a, âˆ€ b â‰¥ a, x b âˆˆ s, by { rwa tendsto_at_top' at xl, },
  have h : {x : Î¹ | (Î» n, ae_measurable (fs n) Î¼) x}
      âˆ© {x : Î¹ | (Î» n, âˆ€áµ a âˆ‚Î¼, âˆ¥fs n aâˆ¥ â‰¤ bound a) x} âˆˆ l,
    from inter_mem hfs_meas h_bound,
  obtain âŸ¨k, hâŸ© := hxl _ h,
  rw â† tendsto_add_at_top_iff_nat k,
  refine tendsto_set_to_fun_of_dominated_convergence hT bound _ bound_integrable _ _,
  { exact Î» n, (h _ (self_le_add_left _ _)).1, },
  { exact Î» n, (h _ (self_le_add_left _ _)).2, },
  { filter_upwards [h_lim],
    refine Î» a h_lin, @tendsto.comp _ _ _ (Î» n, x (n + k)) (Î» n, fs n a) _ _ _ h_lin _,
    rw tendsto_add_at_top_iff_nat,
    assumption }
end

variables {X : Type*} [topological_space X] [first_countable_topology X]

lemma continuous_at_set_to_fun_of_dominated (hT : dominated_fin_meas_additive Î¼ T C)
  {fs : X â†’ Î± â†’ E} {xâ‚€ : X} {bound : Î± â†’ â„} (hfs_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (fs x) Î¼)
  (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ a âˆ‚Î¼, âˆ¥fs x aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼) (h_cont : âˆ€áµ a âˆ‚Î¼, continuous_at (Î» x, fs x a) xâ‚€) :
  continuous_at (Î» x, set_to_fun Î¼ T hT (fs x)) xâ‚€ :=
tendsto_set_to_fun_filter_of_dominated_convergence hT bound â€¹_â€º â€¹_â€º â€¹_â€º â€¹_â€º

lemma continuous_set_to_fun_of_dominated (hT : dominated_fin_meas_additive Î¼ T C)
  {fs : X â†’ Î± â†’ E} {bound : Î± â†’ â„}
  (hfs_meas : âˆ€ x, ae_measurable (fs x) Î¼) (h_bound : âˆ€ x, âˆ€áµ a âˆ‚Î¼, âˆ¥fs x aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼) (h_cont : âˆ€áµ a âˆ‚Î¼, continuous (Î» x, fs x a)) :
  continuous (Î» x, set_to_fun Î¼ T hT (fs x)) :=
continuous_iff_continuous_at.mpr (Î» xâ‚€, continuous_at_set_to_fun_of_dominated hT
  (eventually_of_forall hfs_meas) (eventually_of_forall h_bound) â€¹_â€º $ h_cont.mono $
    Î» _, continuous.continuous_at)

end function

end measure_theory
