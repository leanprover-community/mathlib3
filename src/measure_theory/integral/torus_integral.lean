/-
Copyright (c) 2022 Cuma KÃ¶kmen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Cuma KÃ¶kmen, Yury Kudryashov
-/
import measure_theory.integral.circle_integral

/-!
# Integral over a torus in `â„‚â¿`

In this file we define the integral of a function `f : â„‚â¿ â†’ E` over a torus
`{z : â„‚â¿ | âˆ€ i, z i âˆˆ metric.sphere (c i) (R i)}`. In order to do this, we define
`torus_map (c : â„‚â¿) (R Î¸ : â„â¿)` to be the point in `â„‚â¿` given by $z_k=c_k+R_ke^{Î¸_ki}$,
where $i$ is the imaginary unit, then define `torus_integral f c R` as the integral over
the cube $[0, (Î» _, 2Ï€)] = \{Î¸\|âˆ€ k, 0 â‰¤ Î¸_k â‰¤ 2Ï€\}$ of the Jacobian of the
`torus_map` multiplied by `f (torus_map c R Î¸)`.

We also define a predicate saying that `f âˆ˜ torus_map c R` is integrable on the cube
`[0, (Î» _, 2\pi)]`.

## Main definitions

* `torus_map c R`: the generalized multidimensional exponential map from `â„â¿` to `â„‚â¿` that sends
  $Î¸=(Î¸_0,â€¦,Î¸_{n-1})$ to $z=(z_0,â€¦,z_{n-1})$, where $z_k= c_k + R_ke^{Î¸_k i}$;

* `torus_integrable f c R`: a function `f : â„‚â¿ â†’ E` is integrable over the generalized torus
  with center `c : â„‚â¿` and radius `R : â„â¿` if `f âˆ˜ torus_map c R` is integrable on the
  closed cube `Icc (0 : â„â¿) (Î» _, 2 * Ï€)`;

* `torus_integral f c R`: the integral of a function `f : â„‚â¿ â†’ E` over a torus with
  center `c âˆˆ â„‚â¿` and radius `R âˆˆ â„â¿` defined as
  $\iiint_{[0, 2 * Ï€]} (âˆ_{k = 1}^{n} i R_k e^{Î¸_k * i}) â€¢ f (c + Re^{Î¸_k i})\,dÎ¸_0â€¦dÎ¸_{k-1}$.

## Main statements

* `torus_integral_dim0`, `torus_integral_dim1`, `torus_integral_succ`: formulas for `torus_integral`
  in cases of dimension `0`, `1`, and `n + 1`.

## Notations

- `â„â°`, `â„Â¹`, `â„â¿`, `â„â¿âºÂ¹`: local notation for `fin 0 â†’ â„`, `fin 1 â†’ â„`, `fin n â†’ â„`, and
  `fin (n + 1) â†’ â„`, respectively;
- `â„‚â°`, `â„‚Â¹`, `â„‚â¿`, `â„‚â¿âºÂ¹`: local notation for `fin 0 â†’ â„‚`, `fin 1 â†’ â„‚`, `fin n â†’ â„‚`, and
  `fin (n + 1) â†’ â„‚`, respectively;
- `âˆ¯ z in T(c, R), f z`: notation for `torus_integral f c R`;
- `âˆ® z in C(c, R), f z`: notation for `circle_integral f c R`, defined elsewhere;
- `âˆ k, f k`: notation for `finset.prod`, defined elsewhere;
- `Ï€`: notation for `real.pi`, defined elsewhere.

## Tags

integral, torus
-/

variable {n : â„•}
variables {E : Type*} [normed_add_comm_group E]

noncomputable theory

open complex set measure_theory function filter topological_space
open_locale real big_operators

local notation `â„â°` := fin 0 â†’ â„
local notation `â„‚â°` := fin 0 â†’ â„‚
local notation `â„Â¹` := fin 1 â†’ â„
local notation `â„‚Â¹` := fin 1 â†’ â„‚
local notation `â„â¿` := fin n â†’ â„
local notation `â„‚â¿` := fin n â†’ â„‚
local notation `â„â¿âºÂ¹` := fin (n + 1) â†’ â„
local notation `â„‚â¿âºÂ¹` := fin (n + 1) â†’ â„‚

/-!
### `torus_map`, a generalization of a torus
-/

/-- The n dimensional exponential map $Î¸_i â†¦ c + R e^{Î¸_i*I}, Î¸ âˆˆ â„â¿$ representing
a torus in `â„‚â¿` with center `c âˆˆ â„‚â¿` and generalized radius `R âˆˆ â„â¿`, so we can adjust
it to every n axis. -/
def torus_map (c : â„‚â¿) (R : â„â¿) : â„â¿ â†’ â„‚â¿ :=
Î» Î¸ i, c i + R i * exp(Î¸ i * I)

lemma torus_map_sub_center (c : â„‚â¿) (R : â„â¿) (Î¸ : â„â¿) :
  torus_map c R Î¸ - c = torus_map 0 R Î¸ :=
by { ext1 i, simp [torus_map] }

lemma torus_map_eq_center_iff {c : â„‚â¿} {R : â„â¿} {Î¸ : â„â¿} :
  torus_map c R Î¸ = c â†” R = 0 :=
by simp [funext_iff, torus_map, exp_ne_zero]

@[simp] lemma torus_map_zero_radius (c : â„‚â¿) : torus_map c 0 = const â„â¿ c :=
by { ext1, rw torus_map_eq_center_iff.2 rfl }

/-!
### Integrability of a function on a generalized torus
-/

/-- A function `f : â„‚â¿ â†’ E` is integrable on the generalized torus if the function
`f âˆ˜ torus_map c R Î¸` is integrable on `Icc (0 : â„â¿) (Î» _, 2 * Ï€)`-/
def torus_integrable (f : â„‚â¿ â†’ E) (c : â„‚â¿) (R : â„â¿) : Prop :=
  integrable_on (Î» (Î¸ : â„â¿), f (torus_map c R Î¸)) (Icc (0 : â„â¿) (Î» _, 2 * Ï€)) volume

namespace torus_integrable

variables {f g : â„‚â¿ â†’ E} {c : â„‚â¿} {R : â„â¿}

/-- Constant functions are torus integrable -/
lemma torus_integrable_const (a : E) (c : â„‚â¿) (R : â„â¿) :
  torus_integrable (Î» _, a) c R :=
by simp [torus_integrable, measure_Icc_lt_top]

/-- If `f` is torus integrable then `-f` is torus integrable. -/
protected lemma neg (hf : torus_integrable f c R) : torus_integrable (-f) c R := hf.neg

/-- If `f` and `g` are two torus integrable functions, then so is `f + g`. -/
protected lemma add (hf : torus_integrable f c R) (hg : torus_integrable g c R) :
  torus_integrable (f + g) c R :=
hf.add hg

/-- If `f` and `g` are two torus integrable functions, then so is `f - g`. -/
protected lemma sub (hf : torus_integrable f c R) (hg : torus_integrable g c R) :
  torus_integrable (f - g) c R :=
hf.sub hg

lemma torus_integrable_zero_radius {f : â„‚â¿ â†’ E} {c : â„‚â¿} :
  torus_integrable f c 0 :=
begin
  rw [torus_integrable, torus_map_zero_radius],
  apply torus_integrable_const (f c) c 0,
end

/--The function given in the definition of `torus_integral` is integrable. -/
lemma function_integrable [normed_space â„‚ E] (hf : torus_integrable f c R) :
  integrable_on (Î» (Î¸ : â„â¿), (âˆ i, R i * exp(Î¸ i * I) * I : â„‚) â€¢ f (torus_map c R Î¸))
                (Icc (0 : â„â¿) (Î» _, 2 * Ï€)) volume :=
begin
  refine (hf.norm.const_mul (âˆ i, |R i|)).mono' _ _,
  { refine (continuous.ae_strongly_measurable _).smul hf.1,
    exact continuous_finset_prod finset.univ (Î» i hi, (continuous_const.mul
      (((continuous_of_real.comp (continuous_apply i)).mul continuous_const).cexp)).mul
      continuous_const) },
  simp [norm_smul, map_prod],
end

end torus_integrable

variables [normed_space â„‚ E] [complete_space E] {f g : â„‚â¿ â†’ E} {c : â„‚â¿} {R : â„â¿}

/--The definition of the integral over a generalized torus with center `c âˆˆ â„‚â¿` and radius `R âˆˆ â„â¿`
as the `â€¢`-product of the derivative of `torus_map` and `f (torus_map c R Î¸)`-/
def torus_integral (f : â„‚â¿ â†’ E) (c : â„‚â¿) (R : â„â¿) :=
âˆ« (Î¸ : â„â¿) in Icc (0 : â„â¿) (Î» _, 2 * Ï€), (âˆ i, R i * exp(Î¸ i * I) * I : â„‚) â€¢ f (torus_map c R Î¸)

notation `âˆ¯` binders ` in ` `T(` c `, ` R `)` `, ` r:(scoped:60 f, torus_integral f c R) := r

lemma torus_integral_radius_zero (hn : n â‰  0) (f : â„‚â¿ â†’ E) (c : â„‚â¿): âˆ¯ x in T(c, 0), f x = 0 :=
by simp only [torus_integral, pi.zero_apply, of_real_zero, mul_zero, zero_mul, fin.prod_const,
  zero_pow' n hn, zero_smul, integral_zero]

lemma torus_integral_neg (f : â„‚â¿ â†’ E) (c : â„‚â¿) (R : â„â¿) :
  âˆ¯ x in T(c, R), -f x = -âˆ¯ x in T(c, R), f x :=
by simp [torus_integral, integral_neg]

lemma torus_integral_add (hf : torus_integrable f c R) (hg : torus_integrable g c R) :
  âˆ¯ x in T(c, R), f x + g x = (âˆ¯ x in T(c, R), f x) + âˆ¯ x in T(c, R), g x :=
by simpa only [torus_integral, smul_add, pi.add_apply]
  using integral_add hf.function_integrable hg.function_integrable

lemma torus_integral_sub (hf : torus_integrable f c R) (hg : torus_integrable g c R) :
  âˆ¯ x in T(c, R), f x - g x = (âˆ¯ x in T(c, R), f x) - âˆ¯ x in T(c, R), g x :=
by simpa only [sub_eq_add_neg, â† torus_integral_neg] using torus_integral_add hf hg.neg

lemma torus_integral_smul {ğ•œ : Type*} [is_R_or_C ğ•œ] [normed_space ğ•œ E] [smul_comm_class ğ•œ â„‚ E]
  (a : ğ•œ) (f : â„‚â¿ â†’ E) (c : â„‚â¿) (R : â„â¿) :
  âˆ¯ x in T(c, R), a â€¢ f x = a â€¢ âˆ¯ x in T(c, R), f x :=
by simp only [torus_integral, integral_smul, â† smul_comm a]

lemma torus_integral_const_mul (a : â„‚) (f : â„‚â¿ â†’ â„‚) (c : â„‚â¿) (R : â„â¿) :
  âˆ¯ x in T(c, R), a * f x = a * âˆ¯ x in T(c, R), f x :=
torus_integral_smul a f c R

/--If for all `Î¸ : â„â¿`, `â€–f (torus_map c R Î¸)â€–` is less than or equal to a constant `C : â„`, then
`â€–âˆ¯ x in T(c, R), f xâ€–` is less than or equal to `(2 * Ï€)^n * (âˆ i, |R i|) * C`-/
lemma norm_torus_integral_le_of_norm_le_const {C : â„} (hf : âˆ€ Î¸, â€–f (torus_map c R Î¸)â€– â‰¤ C) :
  â€–âˆ¯ x in T(c, R), f xâ€– â‰¤ (2 * Ï€)^(n: â„•) * (âˆ i, |R i|) * C :=
calc â€–âˆ¯ x in T(c, R), f xâ€– â‰¤ (âˆ i, |R i|) * C * (volume (Icc (0 : â„â¿) (Î» _, 2 * Ï€))).to_real :
  norm_set_integral_le_of_norm_le_const' measure_Icc_lt_top measurable_set_Icc $ Î» Î¸ hÎ¸,
    ( calc â€–(âˆ i : fin n, R i * exp (Î¸ i * I) * I : â„‚) â€¢ f (torus_map c R Î¸)â€–
          = (âˆ i : fin n, |R i|) * â€–f (torus_map c R Î¸)â€– : by simp [norm_smul]
      ... â‰¤ (âˆ i : fin n, |R i|) * C :
        mul_le_mul_of_nonneg_left (hf _) (finset.prod_nonneg $ Î» _ _, abs_nonneg _) )
... = (2 * Ï€)^(n: â„•) * (âˆ i, |R i|) * C :
  by simp only [pi.zero_def, real.volume_Icc_pi_to_real (Î» _, real.two_pi_pos.le), sub_zero,
      fin.prod_const, mul_assoc, mul_comm ((2 * Ï€) ^ (n : â„•))]

@[simp] lemma torus_integral_dim0 (f : â„‚â° â†’ E) (c : â„‚â°) (R : â„â°) : âˆ¯ x in T(c, R), f x = f c :=
by simp only [torus_integral, fin.prod_univ_zero, one_smul,
  subsingleton.elim (Î» i : fin 0, 2 * Ï€) 0, Icc_self, measure.restrict_singleton, volume_pi,
  integral_smul_measure, integral_dirac, measure.pi_of_empty _ 0,
  measure.dirac_apply_of_mem (mem_singleton _), subsingleton.elim (torus_map c R 0) c]

/-- In dimension one, `torus_integral` is the same as `circle_integral`
(up to the natural equivalence between `â„‚` and `fin 1 â†’ â„‚`). -/
lemma torus_integral_dim1 (f : â„‚Â¹ â†’ E) (c : â„‚Â¹) (R : â„Â¹) :
  âˆ¯ x in T(c, R), f x = âˆ® z in C(c 0, R 0), f (Î» _, z) :=
begin
  have : (Î» (x : â„) (b : fin 1), x) â»Â¹' Icc 0 (Î» _, 2 * Ï€) = Icc 0 (2 * Ï€),
    from (order_iso.fun_unique (fin 1) â„).symm.preimage_Icc _ _,
  simp only [torus_integral, circle_integral, interval_integral.integral_of_le real.two_pi_pos.le,
    measure.restrict_congr_set Ioc_ae_eq_Icc, deriv_circle_map, fin.prod_univ_one,
    â† ((volume_preserving_fun_unique (fin 1) â„).symm _).set_integral_preimage_emb
      (measurable_equiv.measurable_embedding _), this, measurable_equiv.fun_unique_symm_apply],
  simp only [torus_map, circle_map, zero_add],
  rcongr
end

/-- Recurrent formula for `torus_integral`, see also `torus_integral_succ`. -/
lemma torus_integral_succ_above {f : â„‚â¿âºÂ¹ â†’ E} {c : â„‚â¿âºÂ¹} {R : â„â¿âºÂ¹} (hf : torus_integrable f c R)
  (i : fin (n + 1)) :
  âˆ¯ x in T(c, R), f x =
    âˆ® x in C(c i, R i), âˆ¯ y in T(c âˆ˜ i.succ_above, R âˆ˜ i.succ_above), f (i.insert_nth x y) :=
begin
  set e : â„ Ã— â„â¿ â‰ƒáµ â„â¿âºÂ¹ := (measurable_equiv.pi_fin_succ_above_equiv (Î» _, â„) i).symm,
  have hem : measure_preserving e,
    from (volume_preserving_pi_fin_succ_above_equiv (Î» j : fin (n + 1), â„) i).symm _,
  have heÏ€ : e â»Â¹' (Icc 0 (Î» _, 2 * Ï€)) = Icc 0 (2 * Ï€) Ã—Ë¢ Icc (0 : â„â¿) (Î» _, 2 * Ï€),
    from ((order_iso.pi_fin_succ_above_iso (Î» _, â„) i).symm.preimage_Icc _ _).trans
      (Icc_prod_eq _ _),
  rw [torus_integral, â† hem.map_eq, set_integral_map_equiv, heÏ€, measure.volume_eq_prod,
    set_integral_prod, circle_integral_def_Icc],
  { refine set_integral_congr measurable_set_Icc (Î» Î¸ hÎ¸, _),
    simp only [torus_integral, â† integral_smul, deriv_circle_map, i.prod_univ_succ_above _,
      smul_smul, torus_map, circle_map_zero],
    refine set_integral_congr measurable_set_Icc (Î» Î˜ hÎ˜, _),
    simp only [measurable_equiv.pi_fin_succ_above_equiv_symm_apply, i.insert_nth_apply_same,
      i.insert_nth_apply_succ_above, (âˆ˜)],
    congr' 2,
    simp only [funext_iff, i.forall_iff_succ_above, circle_map, fin.insert_nth_apply_same,
      eq_self_iff_true, fin.insert_nth_apply_succ_above, implies_true_iff, and_self] },
  { have := hf.function_integrable,
    rwa [â† hem.integrable_on_comp_preimage e.measurable_embedding, heÏ€] at this }
end

/-- Recurrent formula for `torus_integral`, see also `torus_integral_succ_above`. -/
lemma torus_integral_succ {f : â„‚â¿âºÂ¹ â†’ E} {c : â„‚â¿âºÂ¹} {R : â„â¿âºÂ¹} (hf : torus_integrable f c R) :
  âˆ¯ x in T(c, R), f x =
    âˆ® x in C(c 0, R 0), âˆ¯ y in T(c âˆ˜ fin.succ, R âˆ˜ fin.succ), f (fin.cons x y) :=
by simpa using torus_integral_succ_above hf 0
