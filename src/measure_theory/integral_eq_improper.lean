/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import measure_theory.interval_integral
import order.filter.at_top_bot

/-!
# Links between an integral and its "improper" version

In its current state, mathlib only knows how to talk about definite/proper integrals,
in the sense that it treats integrals over `[1, +âˆ)` in the same way as integrals over `[0, 1]`
(for example) : an integral over `[1, +âˆ)` is **not** defined to be the limit as `x` goes to `+âˆ` of
the integral over `[1, x]`, which may be named an "improper integral".

Indeed, the "proper" definition is stronger than the "improper" one. The usual counterexample
is `x â†¦ sin(x)/x`, which has an improper integral over `[1, +âˆ)` but no definite integral.

Although definite integrals have better properties, it is hardly usable to actually compute
integrals on unbounded set, which is way easier using limits. Thus, in this file, we prove
various ways of studying the proper integral by studying the improper one.

## Definitions

The main definition of this file is `measure_theory.mono_ae_cover`. It's a rather technical
definition whose sole purpose is generalizing and factoring proofs. For a sequence `Ï†` of subsets
of a measurable space `Î±` equipped with a measure `Î¼`, one should think of an hypothesis
`hÏ† : mono_ae_cover Î¼ Ï†` as a sufficient condition for being able to interpret
`âˆ« x, f x âˆ‚Î¼` (if it exists) as the limit as `n` goes to `âˆ` of `âˆ« x in Ï† n, f x âˆ‚Î¼`.

When using this definition with a measure restricted to a set `s`, which happens fairly often,
one should not try too hard to use a `mono_ae_cover` of subsets of `s`, as it often makes proofs
more complicated than necessary. See for example the proof of
`integrable_on_Iic_of_tendsto_interval_integral_norm` where we use `Ioi`s as a `mono_ae_cover`
w.r.t. `Î¼.restrict (Iic b)`.

## Main statements

- `measure_theory.set_lintegral_tendsto_lintegral` : if `Ï†` is a `mono_ae_cover` and
  `f` a measurable `ennreal`-valued function, then `âˆ«â» x in Ï† n, f x âˆ‚Î¼` tends to `âˆ«â» x, f x âˆ‚Î¼`
  as `n` tends to `+âˆ`
- `measure_theory.integrable_of_tendsto_integral_norm` : if `Ï†` is a `mono_ae_cover`,
  `f` measurable and integrable on each `Ï† n`, and `âˆ« x in Ï† n, âˆ¥f xâˆ¥ âˆ‚Î¼` tends to some
  `I : â„` as n tends to `+âˆ`, then `f` is integrable
- `measure_theory.set_integral_tendsto_integral` : if `Ï†` is a `mono_ae_cover`,
  `f` measurable and integrable (globally), then `âˆ« x in Ï† n, f x âˆ‚Î¼` tends to `âˆ« x, f x âˆ‚Î¼`
  as `n` tends to `+âˆ`

We then specialize these lemmas to various use cases involving intervals, which are frequent
in analysis.

-/
open measure_theory filter set
open_locale ennreal nnreal topological_space

section move_me

lemma supr_eq_supr_coe_nat_of_monotone {Î± Î² : Type*} [complete_lattice Î²] [ordered_semiring Î±]
  [archimedean Î±] {f : Î± â†’ Î²} (hf : monotone f) : (â¨† (n : Î±), f n) = â¨† (n : â„•), f n :=
le_antisymm
  (supr_le_supr2 $ Î» i, exists_imp_exists (Î» a (ha : i â‰¤ a), hf ha) (exists_nat_ge i))
  (supr_le_supr2 $ Î» i, âŸ¨i, le_refl _âŸ©)

end move_me

namespace measure_theory

section mono_ae_cover

variables {Î± Î¹ : Type*} [ordered_add_comm_monoid Î¹]
  [measurable_space Î±] (Î¼ : measure Î±)

structure mono_ae_cover (Ï† : Î¹ â†’ set Î±) : Prop :=
(ae_eventually_mem : âˆ€áµ x âˆ‚Î¼, âˆ€á¶  i in at_top, x âˆˆ Ï† i)
(mono : monotone Ï†)
(measurable : âˆ€ i, measurable_set $ Ï† i)

variables {Î¼}

section preorder

variables [preorder Î±] [topological_space Î±] [order_closed_topology Î±]
  [opens_measurable_space Î±] {a b : Î¹ â†’ Î±} (haâ‚ : âˆ€ â¦ƒx yâ¦„, x â‰¤ y â†’ a y â‰¤ a x)
  (haâ‚‚ : tendsto a at_top at_bot) (hbâ‚ : monotone b) (hbâ‚‚ : tendsto b at_top at_top)

lemma mono_ae_cover_Icc :
  mono_ae_cover Î¼ (Î» i, Icc (a i) (b i)) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (haâ‚‚.eventually $ eventually_le_at_bot x).mp $
    (hbâ‚‚.eventually $ eventually_ge_at_top x).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ© ),
  mono := Î» i j hij, Icc_subset_Icc (haâ‚ hij) (hbâ‚ hij),
  measurable := Î» i, measurable_set_Icc }

lemma mono_ae_cover_Ici :
  mono_ae_cover Î¼ (Î» i, Ici $ a i) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (haâ‚‚.eventually $ eventually_le_at_bot x).mono $
    Î» i hai, hai ),
  mono := Î» i j hij, Ici_subset_Ici.mpr (haâ‚ hij),
  measurable := Î» i, measurable_set_Ici }

lemma mono_ae_cover_Iic :
  mono_ae_cover Î¼ (Î» i, Iic $ b i) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (hbâ‚‚.eventually $ eventually_ge_at_top x).mono $
    Î» i hbi, hbi ),
  mono := Î» i j hij, Iic_subset_Iic.mpr (hbâ‚ hij),
  measurable := Î» i, measurable_set_Iic }

end preorder

section linear_order

variables [linear_order Î±] [topological_space Î±] [order_closed_topology Î±]
  [opens_measurable_space Î±] {a b : Î¹ â†’ Î±} (haâ‚ : âˆ€ â¦ƒx yâ¦„, x â‰¤ y â†’ a y â‰¤ a x)
  (haâ‚‚ : tendsto a at_top at_bot)
  (hbâ‚ : monotone b) (hbâ‚‚ : tendsto b at_top at_top)

lemma mono_ae_cover_Ioo [no_bot_order Î±] [no_top_order Î±] :
  mono_ae_cover Î¼ (Î» i, Ioo (a i) (b i)) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (haâ‚‚.eventually $ eventually_lt_at_bot x).mp $
    (hbâ‚‚.eventually $ eventually_gt_at_top x).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ© ),
  mono := Î» i j hij, Ioo_subset_Ioo (haâ‚ hij) (hbâ‚ hij),
  measurable := Î» i, measurable_set_Ioo }

lemma mono_ae_cover_Ioc [no_bot_order Î±] : mono_ae_cover Î¼ (Î» i, Ioc (a i) (b i)) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (haâ‚‚.eventually $ eventually_lt_at_bot x).mp $
    (hbâ‚‚.eventually $ eventually_ge_at_top x).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ© ),
  mono := Î» i j hij, Ioc_subset_Ioc (haâ‚ hij) (hbâ‚ hij),
  measurable := Î» i, measurable_set_Ioc }

lemma mono_ae_cover_Ico [no_top_order Î±] : mono_ae_cover Î¼ (Î» i, Ico (a i) (b i)) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (haâ‚‚.eventually $ eventually_le_at_bot x).mp $
    (hbâ‚‚.eventually $ eventually_gt_at_top x).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ© ),
  mono := Î» i j hij, Ico_subset_Ico (haâ‚ hij) (hbâ‚ hij),
  measurable := Î» i, measurable_set_Ico }

lemma mono_ae_cover_Ioi [no_bot_order Î±] :
  mono_ae_cover Î¼ (Î» i, Ioi $ a i) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (haâ‚‚.eventually $ eventually_lt_at_bot x).mono $
    Î» i hai, hai ),
  mono := Î» i j hij, Ioi_subset_Ioi (haâ‚ hij),
  measurable := Î» i, measurable_set_Ioi }

lemma mono_ae_cover_Iio [no_top_order Î±] :
  mono_ae_cover Î¼ (Î» i, Iio $ b i) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (hbâ‚‚.eventually $ eventually_gt_at_top x).mono $
    Î» i hbi, hbi ),
  mono := Î» i j hij, Iio_subset_Iio (hbâ‚ hij),
  measurable := Î» i, measurable_set_Iio }

end linear_order

lemma mono_ae_cover.restrict {Ï† : Î¹ â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) {s : set Î±} :
  mono_ae_cover (Î¼.restrict s) Ï† :=
{ ae_eventually_mem := ae_restrict_of_ae hÏ†.ae_eventually_mem,
  mono := hÏ†.mono,
  measurable := hÏ†.measurable }

lemma mono_ae_cover.ae_tendsto_indicator {Î² : Type*} [has_zero Î²] [topological_space Î²]
  {f : Î± â†’ Î²} {Ï† : Î¹ â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) :
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» i, (Ï† i).indicator f x) at_top (ğ“ $ f x) :=
hÏ†.ae_eventually_mem.mono (Î» x hx, tendsto_const_nhds.congr' $
  hx.mono $ Î» n hn, (indicator_of_mem hn _).symm)

lemma mono_ae_cover_restrict_of_ae_imp {s : set Î±} {Ï† : Î¹ â†’ set Î±}
  (hs : measurable_set s) (ae_eventually_mem : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆ€á¶  n in at_top, x âˆˆ Ï† n)
  (mono : monotone Ï†) (measurable : âˆ€ n, measurable_set $ Ï† n) :
  mono_ae_cover (Î¼.restrict s) Ï† :=
{ ae_eventually_mem := by rwa ae_restrict_iff' hs,
  mono := mono,
  measurable := measurable }

lemma mono_ae_cover.inter_restrict {Ï† : Î¹ â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†)
  {s : set Î±} (hs : measurable_set s) :
  mono_ae_cover (Î¼.restrict s) (Î» i, Ï† i âˆ© s) :=
mono_ae_cover_restrict_of_ae_imp hs
  (hÏ†.ae_eventually_mem.mono (Î» x hx hxs, hx.mono $ Î» i hi, âŸ¨hi, hxsâŸ©))
  (Î» i j hij, inter_subset_inter_left s (hÏ†.mono hij))
  (Î» i, (hÏ†.measurable i).inter hs)

end mono_ae_cover

section mono_ae_cover_archimedean

variables {Î± Î¹ : Type*} [ordered_semiring Î¹] [nonempty Î¹] [archimedean Î¹]
  [measurable_space Î±] {Î¼ : measure Î±}

lemma mono_ae_cover.coe_nat {Ï† : Î¹ â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) :
  mono_ae_cover Î¼ (Î» (n : â„•), Ï† n) :=
{ ae_eventually_mem := hÏ†.ae_eventually_mem.mono (Î» x, tendsto_coe_nat_at_top_at_top.eventually),
  mono := Î» i j hij, hÏ†.mono (nat.mono_cast hij),
  measurable := Î» n, hÏ†.measurable n }

end mono_ae_cover_archimedean

section lintegral

variables {Î± Î¹ : Type*} [linear_ordered_semiring Î¹] [nonempty Î¹] [archimedean Î¹]
  [measurable_space Î±] {Î¼ : measure Î±}

lemma lintegral_eq_supr {Ï† : Î¹ â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ â„â‰¥0âˆ}
  (hfm : measurable f) :
  âˆ«â» x, f x âˆ‚Î¼ = â¨† (i : Î¹), âˆ«â» x in Ï† i, f x âˆ‚Î¼ :=
begin
  have hÏ†' := hÏ†.coe_nat,
  let F := Î» (n : â„•), indicator (Ï† n) f,
  have F_tendsto : âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, F n x) at_top (ğ“ $ f x) :=
    hÏ†'.ae_tendsto_indicator,
  have F_mono : âˆ€ x, monotone (Î» n, F n x) :=
    Î» x i j hij, indicator_le_indicator_of_subset (hÏ†'.mono hij) (Î» _, zero_le _) x,
  have f_eq_supr_F : âˆ€áµ x âˆ‚Î¼, f x = â¨† (n : â„•), F n x :=
    F_tendsto.mono (Î» x hx, tendsto_nhds_unique hx
      (tendsto_at_top_csupr (F_mono x) âŸ¨âŠ¤, Î» _ _, le_topâŸ©)),
  have lintegral_F_eq : âˆ€ n, âˆ«â» (x : Î±), F n x âˆ‚Î¼ = âˆ«â» x in Ï† n, f x âˆ‚Î¼ :=
    Î» n, lintegral_indicator _ (hÏ†.measurable n),
  have lintegral_f_mono_iota : monotone (Î» i, âˆ«â» x in Ï† i, f x âˆ‚Î¼) :=
    (Î» i j hij, lintegral_mono' (measure.restrict_mono (hÏ†.mono hij) (le_refl _)) (le_refl _)),
  rw [lintegral_congr_ae f_eq_supr_F, supr_eq_supr_coe_nat_of_monotone lintegral_f_mono_iota],
  dsimp only,
  conv_rhs {congr, funext, rw â† lintegral_F_eq},
  exact lintegral_supr (Î» n, hfm.indicator $ hÏ†.measurable n) (Î» i j hij x, F_mono x hij),
end

lemma tendsto_set_lintegral_of_monotone_set {Ï† : Î¹ â†’ set Î±} (hÏ† : monotone Ï†) {f : Î± â†’ â„â‰¥0âˆ} :
  tendsto (Î» i, âˆ«â» x in Ï† i, f x âˆ‚Î¼) at_top (ğ“ $ â¨† (i : Î¹), âˆ«â» x in Ï† i, f x âˆ‚Î¼) :=
tendsto_at_top_csupr
  (Î» i j hij, lintegral_mono' (measure.restrict_mono (hÏ† hij) (le_refl _)) (le_refl _))
  âŸ¨âŠ¤, Î» _ _, le_topâŸ©

lemma set_lintegral_tendsto_lintegral {Ï† : Î¹ â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ â„â‰¥0âˆ}
  (hfm : measurable f) :
  tendsto (Î» i, âˆ«â» x in Ï† i, f x âˆ‚Î¼) at_top (ğ“ $ âˆ«â» x, f x âˆ‚Î¼) :=
begin
  rw lintegral_eq_supr hÏ† hfm,
  exact tendsto_set_lintegral_of_monotone_set hÏ†.2
end

/-- Slight reformulation of `measure_theory.set_lintegral_tendsto_lintegral`. -/
lemma lintegral_eq_of_tendsto_lintegral {Ï† : Î¹ â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ â„â‰¥0âˆ}
  (I : â„â‰¥0âˆ) (hfm : measurable f) (h : tendsto (Î» i, âˆ«â» x in Ï† i, f x âˆ‚Î¼) at_top (ğ“ I)) :
  âˆ«â» x, f x âˆ‚Î¼ = I :=
tendsto_nhds_unique (set_lintegral_tendsto_lintegral hÏ† hfm) h

end lintegral

section integrable

variables {Î± Î¹ : Type*} [linear_ordered_semiring Î¹] [nonempty Î¹] [archimedean Î¹]
  [measurable_space Î±] {Î¼ : measure Î±} {E : Type*} [normed_group E]
  [measurable_space E] [opens_measurable_space E]

lemma integrable_of_tendsto_lintegral_nnnorm {Ï† : Î¹ â†’ set Î±}
  (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ E} (I : â„) (hfm : measurable f)
  (h : tendsto (Î» i, âˆ«â» x in Ï† i, nnnorm (f x) âˆ‚Î¼) at_top (ğ“ $ ennreal.of_real I)) :
  integrable f Î¼ :=
begin
  refine âŸ¨hfm.ae_measurable, _âŸ©,
  unfold has_finite_integral,
  rw lintegral_eq_of_tendsto_lintegral hÏ† _
    (measurable_ennreal_coe_iff.mpr (measurable_nnnorm.comp hfm)) h,
  exact ennreal.of_real_lt_top
end

lemma integrable_of_tendsto_lintegral_nnnorm' {Ï† : Î¹ â†’ set Î±}
  (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ E} (I : â„â‰¥0) (hfm : measurable f)
  (h : tendsto (Î» i, âˆ«â» x in Ï† i, nnnorm (f x) âˆ‚Î¼) at_top (ğ“ I)) :
  integrable f Î¼ :=
begin
  refine integrable_of_tendsto_lintegral_nnnorm hÏ† (I : â„) hfm _,
  convert h,
  exact ennreal.of_real_coe_nnreal
end

lemma integrable_of_tendsto_integral_norm {Ï† : Î¹ â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ E}
  (I : â„) (hfm : measurable f) (hfi : âˆ€ i, integrable_on f (Ï† i) Î¼)
  (h : tendsto (Î» i, âˆ« x in Ï† i, âˆ¥f xâˆ¥ âˆ‚Î¼) at_top (ğ“ I)) :
  integrable f Î¼ :=
begin
  conv at h in (integral _ _)
  { rw integral_eq_lintegral_of_nonneg_ae (ae_of_all _ (Î» x, @norm_nonneg E _ (f x)))
    hfm.norm.ae_measurable },
  conv at h in (ennreal.of_real _) { dsimp, rw â† coe_nnnorm, rw ennreal.of_real_coe_nnreal },
  have h' : tendsto (Î» (i : Î¹), (âˆ«â» (a : Î±) in Ï† i, nnnorm (f a) âˆ‚Î¼)) at_top
    (ğ“ $ ennreal.of_real I),
  { convert ennreal.tendsto_of_real h,
    ext i : 1,
    rw ennreal.of_real_to_real _,
    exact ne_top_of_lt (hfi i).2 },
  exact integrable_of_tendsto_lintegral_nnnorm hÏ† I hfm h'
end

lemma integrable_of_tendsto_integral_of_nonneg_ae {Ï† : Î¹ â†’ set Î±}
  (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ â„} (I : â„) (hf : 0 â‰¤áµ[Î¼] f) (hfm : measurable f)
  (hfi : âˆ€ n, integrable_on f (Ï† n) Î¼) (h : tendsto (Î» n, âˆ« x in Ï† n, f x âˆ‚Î¼) at_top (ğ“ I)) :
  integrable f Î¼ :=
integrable_of_tendsto_integral_norm hÏ† I hfm hfi
  (h.congr $ Î» i, integral_congr_ae $ ae_restrict_of_ae $ hf.mono $
    Î» x hx, (real.norm_of_nonneg hx).symm)

end integrable

section integral

variables {Î± Î¹ : Type*} [linear_ordered_semiring Î¹] [nonempty Î¹] [archimedean Î¹]
  [measurable_space Î±] {Î¼ : measure Î±} {E : Type*} [normed_group E]
  [measurable_space E] [normed_space â„ E] [complete_space E] [borel_space E]
  [topological_space.second_countable_topology E]

lemma set_integral_tendsto_integral {Ï† : â„• â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ E}
  (hfm : measurable f) (hfi : integrable f Î¼) :
  tendsto (Î» n, âˆ« x in Ï† n, f x âˆ‚Î¼) at_top (ğ“ $ âˆ« x, f x âˆ‚Î¼) :=
begin
  suffices : tendsto (Î» (n : â„•), âˆ« (x : Î±), (Ï† n).indicator f x âˆ‚Î¼) at_top (ğ“ (âˆ« (x : Î±), f x âˆ‚Î¼)),
  { convert this,
    ext n,
    rw integral_indicator (hÏ†.measurable n) },
  exact tendsto_integral_of_dominated_convergence (Î» x, âˆ¥f xâˆ¥)
    (Î» n, (hfm.indicator $ hÏ†.measurable n).ae_measurable) hfm.ae_measurable hfi.norm
    (Î» n, ae_of_all _ $ norm_indicator_le_norm_self f) hÏ†.ae_tendsto_indicator
end

/-- Slight reformulation of `measure_theory.set_integral_tendsto_integral`. -/
lemma integral_eq_of_tendsto_integral {Ï† : â„• â†’ set Î±} (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ E} (I : E)
  (hfm : measurable f) (hfi : integrable f Î¼)
  (h : tendsto (Î» n, âˆ« x in Ï† n, f x âˆ‚Î¼) at_top (ğ“ I)) :
  âˆ« x, f x âˆ‚Î¼ = I :=
tendsto_nhds_unique (set_integral_tendsto_integral hÏ† hfm hfi) h

lemma integral_eq_of_tendsto_integral_of_nonneg_ae {Ï† : â„• â†’ set Î±}
  (hÏ† : mono_ae_cover Î¼ Ï†) {f : Î± â†’ â„} (I : â„) (hf : 0 â‰¤áµ[Î¼] f) (hfm : measurable f)
  (hfi : âˆ€ n, integrable_on f (Ï† n) Î¼) (h : tendsto (Î» n, âˆ« x in Ï† n, f x âˆ‚Î¼) at_top (ğ“ I)) :
  âˆ« x, f x âˆ‚Î¼ = I :=
have hfi' : integrable f Î¼,
  from integrable_of_tendsto_integral_of_nonneg_ae hÏ† I hf hfm hfi h,
integral_eq_of_tendsto_integral hÏ† I hfm hfi' h

end integral

section integrable_of_interval_integral

variables {Î± : Type*} {E : Type*} [topological_space Î±] [linear_order Î±] [order_closed_topology Î±]
  [measurable_space Î±] [opens_measurable_space Î±] [measurable_space E]
  [normed_group E] [borel_space E] {Î¼ : measure Î±} {a b : â„• â†’ Î±}
  (haâ‚ : âˆ€ â¦ƒx yâ¦„, x â‰¤ y â†’ a y â‰¤ a x) (hbâ‚ : monotone b) {f : Î± â†’ E} (hfm : measurable f)

include haâ‚ hbâ‚

include hfm

lemma integrable_of_tendsto_interval_integral_norm [no_bot_order Î±]
  (I : â„) (hfi : âˆ€ n, integrable_on f (Ioc (a n) (b n)) Î¼)
  (haâ‚‚ : tendsto a at_top at_bot) (hbâ‚‚ : tendsto b at_top at_top)
  (h : tendsto (Î» n, âˆ« x in a n .. b n, âˆ¥f xâˆ¥ âˆ‚Î¼) at_top (ğ“ $ I)) :
  integrable f Î¼ :=
begin
  let Ï† := Î» n, Ioc (a n) (b n),
  have hÏ† : mono_ae_cover Î¼ Ï† := mono_ae_cover_Ioc haâ‚ haâ‚‚ hbâ‚ hbâ‚‚,
  refine integrable_of_tendsto_integral_norm hÏ† _ hfm hfi (h.congr' _),
  filter_upwards [haâ‚‚.eventually (eventually_le_at_bot $ b 0)],
  intros n han,
  have : a n â‰¤ b n := han.trans (hbâ‚ $ zero_le n),
  exact interval_integral.integral_of_le this
end

omit hbâ‚

lemma integrable_on_Iic_of_tendsto_interval_integral_norm [no_bot_order Î±] (I : â„) (b : Î±)
  (hfi : âˆ€ n, integrable_on f (Ioc (a n) b) Î¼) (haâ‚‚ : tendsto a at_top at_bot)
  (h : tendsto (Î» n, âˆ« x in a n .. b, âˆ¥f xâˆ¥ âˆ‚Î¼) at_top (ğ“ $ I)) :
  integrable_on f (Iic b) Î¼ :=
begin
  let Ï† := Î» n, Ioi (a n),
  have hÏ† : mono_ae_cover (Î¼.restrict $ Iic b) Ï† := mono_ae_cover_Ioi haâ‚ haâ‚‚,
  have hfi : âˆ€ n, integrable_on f (Ï† n) (Î¼.restrict $ Iic b),
  { intro n,
    rw [integrable_on, measure.restrict_restrict (hÏ†.measurable n)],
    exact hfi n },
  refine integrable_of_tendsto_integral_norm hÏ† _ hfm hfi (h.congr' _),
  filter_upwards [haâ‚‚.eventually (eventually_le_at_bot $ b)],
  intros n han,
  rw [interval_integral.integral_of_le han, measure.restrict_restrict (hÏ†.measurable n)],
  refl
end

omit haâ‚
include hbâ‚

lemma integrable_on_Ioi_of_tendsto_interval_integral_norm (I : â„) (a : Î±)
  (hfi : âˆ€ n, integrable_on f (Ioc a (b n)) Î¼) (hbâ‚‚ : tendsto b at_top at_top)
  (h : tendsto (Î» n, âˆ« x in a .. b n, âˆ¥f xâˆ¥ âˆ‚Î¼) at_top (ğ“ $ I)) :
  integrable_on f (Ioi a) Î¼ :=
begin
  let Ï† := Î» n, Iic (b n),
  have hÏ† : mono_ae_cover (Î¼.restrict $ Ioi a) Ï† := mono_ae_cover_Iic hbâ‚ hbâ‚‚,
  have hfi : âˆ€ n, integrable_on f (Ï† n) (Î¼.restrict $ Ioi a),
  { intro n,
    rw [integrable_on, measure.restrict_restrict (hÏ†.measurable n), inter_comm],
    exact hfi n },
  refine integrable_of_tendsto_integral_norm hÏ† _ hfm hfi (h.congr' _),
  filter_upwards [hbâ‚‚.eventually (eventually_ge_at_top $ a)],
  intros n hbn,
  rw [interval_integral.integral_of_le hbn, measure.restrict_restrict (hÏ†.measurable n),
      inter_comm],
  refl
end

end integrable_of_interval_integral

section integral_of_interval_integral

variables {Î± : Type*} {E : Type*} [topological_space Î±] [linear_order Î±] [order_closed_topology Î±]
  [measurable_space Î±] [opens_measurable_space Î±] [measurable_space E]
  [normed_group E] [topological_space.second_countable_topology E] [complete_space E]
  [normed_space â„ E] [borel_space E] {Î¼ : measure Î±} {a b : â„• â†’ Î±}
  (haâ‚ : âˆ€ â¦ƒx yâ¦„, x â‰¤ y â†’ a y â‰¤ a x) (hbâ‚ : monotone b) {f : Î± â†’ E} (hfm : measurable f)

include hfm haâ‚ hbâ‚

lemma integral_eq_of_tendsto_interval_integral [no_bot_order Î±] (I : E)
  (hfi : integrable f Î¼) (haâ‚‚ : tendsto a at_top at_bot) (hbâ‚‚ : tendsto b at_top at_top)
  (h : tendsto (Î» n, âˆ« x in a n .. b n, f x âˆ‚Î¼) at_top (ğ“ $ I)) :
  âˆ« x, f x âˆ‚Î¼ = I :=
begin
  let Ï† := Î» n, Ioc (a n) (b n),
  have hÏ† : mono_ae_cover Î¼ Ï† := mono_ae_cover_Ioc haâ‚ haâ‚‚ hbâ‚ hbâ‚‚,
  refine integral_eq_of_tendsto_integral hÏ† _ hfm hfi (h.congr' _),
  filter_upwards [haâ‚‚.eventually (eventually_le_at_bot $ b 0)],
  intros n han,
  have : a n â‰¤ b n := han.trans (hbâ‚ $ zero_le n),
  exact interval_integral.integral_of_le this
end

omit hbâ‚

lemma integral_Iic_eq_of_tendsto_interval_integral [no_bot_order Î±] (I : E) (b : Î±)
  (hfi : integrable_on f (Iic b) Î¼) (haâ‚‚ : tendsto a at_top at_bot)
  (h : tendsto (Î» n, âˆ« x in a n .. b, f x âˆ‚Î¼) at_top (ğ“ $ I)) :
  âˆ« x in Iic b, f x âˆ‚Î¼ = I :=
begin
  let Ï† := Î» n, Ioi (a n),
  have hÏ† : mono_ae_cover (Î¼.restrict $ Iic b) Ï† := mono_ae_cover_Ioi haâ‚ haâ‚‚,
  refine integral_eq_of_tendsto_integral hÏ† _ hfm hfi (h.congr' _),
  filter_upwards [haâ‚‚.eventually (eventually_le_at_bot $ b)],
  intros n han,
  rw [interval_integral.integral_of_le han, measure.restrict_restrict (hÏ†.measurable n)],
  refl
end

omit haâ‚
include hbâ‚

lemma integral_Ioi_eq_of_tendsto_interval_integral (I : E) (a : Î±)
  (hfi : integrable_on f (Ioi a) Î¼) (hbâ‚‚ : tendsto b at_top at_top)
  (h : tendsto (Î» n, âˆ« x in a .. b n, f x âˆ‚Î¼) at_top (ğ“ $ I)) :
  âˆ« x in Ioi a, f x âˆ‚Î¼ = I :=
begin
  let Ï† := Î» n, Iic (b n),
  have hÏ† : mono_ae_cover (Î¼.restrict $ Ioi a) Ï† := mono_ae_cover_Iic hbâ‚ hbâ‚‚,
  refine integral_eq_of_tendsto_integral hÏ† _ hfm hfi (h.congr' _),
  filter_upwards [hbâ‚‚.eventually (eventually_ge_at_top $ a)],
  intros n hbn,
  rw [interval_integral.integral_of_le hbn, measure.restrict_restrict (hÏ†.measurable n),
      inter_comm],
  refl
end

end integral_of_interval_integral

end measure_theory
