/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Johannes HÃ¶lzl
-/
import measure_theory.measure_space
import measure_theory.borel_space

/-!
# Lebesgue integral for `ennreal`-valued functions

We define simple functions and show that each Borel measurable function on `ennreal` can be
approximated by a sequence of simple functions.
-/

noncomputable theory
open set (hiding restrict restrict_apply) filter
open_locale classical topological_space big_operators

namespace measure_theory

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

/-- A function `f` from a measurable space to any type is called *simple*,
if every preimage `f â»Â¹' {x}` is measurable, and the range is finite. This structure bundles
a function with these properties. -/
structure {u v} simple_func (Î± : Type u) [measurable_space Î±] (Î² : Type v) :=
(to_fun : Î± â†’ Î²)
(measurable_sn : âˆ€ x, is_measurable (to_fun â»Â¹' {x}))
(finite : (set.range to_fun).finite)

local infixr ` â†’â‚› `:25 := simple_func

namespace simple_func

section measurable
variables [measurable_space Î±]
instance has_coe_to_fun : has_coe_to_fun (Î± â†’â‚› Î²) := âŸ¨_, to_funâŸ©

@[ext] theorem ext {f g : Î± â†’â‚› Î²} (H : âˆ€ a, f a = g a) : f = g :=
by cases f; cases g; congr; exact funext H

/-- Range of a simple function `Î± â†’â‚› Î²` as a `finset Î²`. -/
protected def range (f : Î± â†’â‚› Î²) : finset Î² := f.finite.to_finset

@[simp] theorem mem_range {f : Î± â†’â‚› Î²} {b} : b âˆˆ f.range â†” âˆƒ a, f a = b :=
finite.mem_to_finset

lemma preimage_eq_empty_iff (f : Î± â†’â‚› Î²) (b : Î²) : f â»Â¹' {b} = âˆ… â†” b âˆ‰ f.range :=
preimage_singleton_eq_empty.trans $ not_congr mem_range.symm

/-- Constant function as a `simple_func`. -/
def const (Î±) {Î²} [measurable_space Î±] (b : Î²) : Î± â†’â‚› Î² :=
âŸ¨Î» a, b, Î» x, is_measurable.const _, finite_range_constâŸ©

instance [inhabited Î²] : inhabited (Î± â†’â‚› Î²) := âŸ¨const _ (default _)âŸ©

@[simp] theorem const_apply (a : Î±) (b : Î²) : (const Î± b) a = b := rfl

lemma range_const (Î±) [measurable_space Î±] [nonempty Î±] (b : Î²) :
  (const Î± b).range = {b} :=
begin
  ext b',
  simp [mem_range, eq_comm]
end

lemma is_measurable_cut (p : Î± â†’ Î² â†’ Prop) (f : Î± â†’â‚› Î²)
  (h : âˆ€b, is_measurable {a | p a b}) : is_measurable {a | p a (f a)} :=
begin
  rw (_ : {a | p a (f a)} = â‹ƒ b âˆˆ set.range f, {a | p a b} âˆ© f â»Â¹' {b}),
  { exact is_measurable.bUnion f.finite.countable
      (Î» b _, is_measurable.inter (h b) (f.measurable_sn _)) },
  ext a, simp,
  exact âŸ¨Î» h, âŸ¨a, âŸ¨h, rflâŸ©âŸ©, Î» âŸ¨a', âŸ¨h', eâŸ©âŸ©, e.symm â–¸ h'âŸ©
end

theorem preimage_measurable (f : Î± â†’â‚› Î²) (s) : is_measurable (f â»Â¹' s) :=
is_measurable_cut (Î» _ b, b âˆˆ s) f (Î» b, is_measurable.const (b âˆˆ s))

/-- A simple function is measurable -/
protected theorem measurable [measurable_space Î²] (f : Î± â†’â‚› Î²) : measurable f :=
Î» s _, preimage_measurable f s

/-- If-then-else as a `simple_func`. -/
def ite {s : set Î±} (hs : is_measurable s) (f g : Î± â†’â‚› Î²) : Î± â†’â‚› Î² :=
âŸ¨Î» a, if a âˆˆ s then f a else g a,
 Î» x, by letI : measurable_space Î² := âŠ¤; exact
   measurable.if hs f.measurable g.measurable _ trivial,
 (f.finite.union g.finite).subset begin
   rintro _ âŸ¨a, rflâŸ©,
   by_cases a âˆˆ s; simp [h],
   exacts [or.inl âŸ¨_, rflâŸ©, or.inr âŸ¨_, rflâŸ©]
 endâŸ©

@[simp] theorem ite_apply {s : set Î±} (hs : is_measurable s)
  (f g : Î± â†’â‚› Î²) (a) : ite hs f g a = if a âˆˆ s then f a else g a := rfl

/-- If `f : Î± â†’â‚› Î²` is a simple function and `g : Î² â†’ Î± â†’â‚› Î³` is a family of simple functions,
then `f.bind g` binds the first argument of `g` to `f`. In other words, `f.bind g a = g (f a) a`. -/
def bind (f : Î± â†’â‚› Î²) (g : Î² â†’ Î± â†’â‚› Î³) : Î± â†’â‚› Î³ :=
âŸ¨Î»a, g (f a) a,
 Î» c, is_measurable_cut (Î»a b, g b a âˆˆ ({c} : set Î³)) f (Î» b, (g b).measurable_sn c),
 (f.finite.bUnion (Î» b _, (g b).finite)).subset $
 by rintro _ âŸ¨a, rflâŸ©; simp; exact âŸ¨a, a, rflâŸ©âŸ©

@[simp] theorem bind_apply (f : Î± â†’â‚› Î²) (g : Î² â†’ Î± â†’â‚› Î³) (a) :
  f.bind g a = g (f a) a := rfl

/-- Restrict a simple function `f : Î± â†’â‚› Î²` to a set `s`. If `s` is measurable,
then `f.restrict s a = if a âˆˆ s then f a else 0`, otherwise `f.restrict s = const Î± 0`. -/
def restrict [has_zero Î²] (f : Î± â†’â‚› Î²) (s : set Î±) : Î± â†’â‚› Î² :=
if hs : is_measurable s then ite hs f (const Î± 0) else const Î± 0

@[simp] theorem restrict_apply [has_zero Î²]
  (f : Î± â†’â‚› Î²) {s : set Î±} (hs : is_measurable s) (a) :
  restrict f s a = if a âˆˆ s then f a else 0 :=
by unfold_coes; simp [restrict, hs]; apply ite_apply hs

theorem restrict_preimage [has_zero Î²]
  (f : Î± â†’â‚› Î²) {s : set Î±} (hs : is_measurable s)
  {t : set Î²} (ht : (0:Î²) âˆ‰ t) : restrict f s â»Â¹' t = s âˆ© f â»Â¹' t :=
by ext a; dsimp [preimage]; rw [restrict_apply]; by_cases a âˆˆ s; simp [h, hs, ht]

/-- Given a function `g : Î² â†’ Î³` and a simple function `f : Î± â†’â‚› Î²`, `f.map g` return the simple
    function `g âˆ˜ f : Î± â†’â‚› Î³` -/
def map (g : Î² â†’ Î³) (f : Î± â†’â‚› Î²) : Î± â†’â‚› Î³ := bind f (const Î± âˆ˜ g)

@[simp] theorem map_apply (g : Î² â†’ Î³) (f : Î± â†’â‚› Î²) (a) : f.map g a = g (f a) := rfl

theorem map_map (g : Î² â†’ Î³) (h: Î³ â†’ Î´) (f : Î± â†’â‚› Î²) : (f.map g).map h = f.map (h âˆ˜ g) := rfl

theorem coe_map (g : Î² â†’ Î³) (f : Î± â†’â‚› Î²) : (f.map g : Î± â†’ Î³) = g âˆ˜ f := rfl

@[simp] theorem range_map [decidable_eq Î³] (g : Î² â†’ Î³) (f : Î± â†’â‚› Î²) :
  (f.map g).range = f.range.image g :=
begin
  ext c,
  simp only [mem_range, exists_prop, mem_range, finset.mem_image, map_apply],
  split,
  { rintros âŸ¨a, rflâŸ©, exact âŸ¨f a, âŸ¨_, rflâŸ©, rflâŸ© },
  { rintros âŸ¨_, âŸ¨a, rflâŸ©, rflâŸ©, exact âŸ¨_, rflâŸ© }
end

lemma map_preimage (f : Î± â†’â‚› Î²) (g : Î² â†’ Î³) (s : set Î³) :
  (f.map g) â»Â¹' s = (â‹ƒbâˆˆf.range.filter (Î»b, g b âˆˆ s), f â»Â¹' {b}) :=
begin
  /- True because `f` only takes finitely many values. -/
  ext a',
  simp only [mem_Union, set.mem_preimage, exists_prop, set.mem_preimage, map_apply,
      finset.mem_filter, mem_range, mem_singleton_iff, exists_eq_right'],
  split,
  { assume eq, exact âŸ¨âŸ¨_, rflâŸ©, eqâŸ© },
  { rintros âŸ¨_, eqâŸ©, exact eq }
end

lemma map_preimage_singleton (f : Î± â†’â‚› Î²) (g : Î² â†’ Î³) (c : Î³) :
  (f.map g) â»Â¹' {c} = (â‹ƒbâˆˆf.range.filter (Î»b, g b = c), f â»Â¹' {b}) :=
map_preimage _ _ _

/-- If `f` is a simple function taking values in `Î² â†’ Î³` and `g` is another simple function
with the same domain and codomain `Î²`, then `f.seq g = f a (g a)`. -/
def seq (f : Î± â†’â‚› (Î² â†’ Î³)) (g : Î± â†’â‚› Î²) : Î± â†’â‚› Î³ := f.bind (Î»f, g.map f)

@[simp] lemma seq_apply (f : Î± â†’â‚› (Î² â†’ Î³)) (g : Î± â†’â‚› Î²) (a : Î±) : f.seq g a = f a (g a) := rfl

/-- Combine two simple functions `f : Î± â†’â‚› Î²` and `g : Î± â†’â‚› Î²`
into `Î» a, (f a, g a)`. -/
def pair (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) : Î± â†’â‚› (Î² Ã— Î³) := (f.map prod.mk).seq g

@[simp] lemma pair_apply (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) (a) : pair f g a = (f a, g a) := rfl

lemma pair_preimage (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) (s : set Î²) (t : set Î³) :
  (pair f g) â»Â¹' (set.prod s t) = (f â»Â¹' s) âˆ© (g â»Â¹' t) := rfl

/- A special form of `pair_preimage` -/
lemma pair_preimage_singleton (f : Î± â†’â‚› Î²) (g : Î± â†’â‚› Î³) (b : Î²) (c : Î³) :
  (pair f g) â»Â¹' {(b, c)} = (f â»Â¹' {b}) âˆ© (g â»Â¹' {c}) :=
by { rw â† prod_singleton_singleton, exact pair_preimage _ _ _ _ }

theorem bind_const (f : Î± â†’â‚› Î²) : f.bind (const Î±) = f := by ext; simp

instance [has_zero Î²] : has_zero (Î± â†’â‚› Î²) := âŸ¨const Î± 0âŸ©
instance [has_add Î²] : has_add (Î± â†’â‚› Î²) := âŸ¨Î»f g, (f.map (+)).seq gâŸ©
instance [has_mul Î²] : has_mul (Î± â†’â‚› Î²) := âŸ¨Î»f g, (f.map (*)).seq gâŸ©
instance [has_sup Î²] : has_sup (Î± â†’â‚› Î²) := âŸ¨Î»f g, (f.map (âŠ”)).seq gâŸ©
instance [has_inf Î²] : has_inf (Î± â†’â‚› Î²) := âŸ¨Î»f g, (f.map (âŠ“)).seq gâŸ©
instance [has_le Î²] : has_le (Î± â†’â‚› Î²) := âŸ¨Î»f g, âˆ€a, f a â‰¤ g aâŸ©

@[simp] lemma sup_apply [has_sup Î²] (f g : Î± â†’â‚› Î²) (a : Î±) : (f âŠ” g) a = f a âŠ” g a := rfl
@[simp] lemma mul_apply [has_mul Î²] (f g : Î± â†’â‚› Î²) (a : Î±) : (f * g) a = f a * g a := rfl
lemma add_apply [has_add Î²] (f g : Î± â†’â‚› Î²) (a : Î±) : (f + g) a = f a + g a := rfl

lemma add_eq_mapâ‚‚ [has_add Î²] (f g : Î± â†’â‚› Î²) : f + g = (pair f g).map (Î»p:Î²Ã—Î², p.1 + p.2) :=
rfl

lemma sup_eq_mapâ‚‚ [has_sup Î²] (f g : Î± â†’â‚› Î²) : f âŠ” g = (pair f g).map (Î»p:Î²Ã—Î², p.1 âŠ” p.2) :=
rfl

lemma const_mul_eq_map [has_mul Î²] (f : Î± â†’â‚› Î²) (b : Î²) : const Î± b * f = f.map (Î»a, b * a) := rfl

instance [add_monoid Î²] : add_monoid (Î± â†’â‚› Î²) :=
{ add       := (+), zero := 0,
  add_assoc := assume f g h, ext (assume a, add_assoc _ _ _),
  zero_add  := assume f, ext (assume a, zero_add _),
  add_zero  := assume f, ext (assume a, add_zero _) }

instance add_comm_monoid [add_comm_monoid Î²] : add_comm_monoid (Î± â†’â‚› Î²) :=
{ add_comm := Î» f g, ext (Î»a, add_comm _ _),
  .. simple_func.add_monoid }

instance [has_neg Î²] : has_neg (Î± â†’â‚› Î²) := âŸ¨Î»f, f.map (has_neg.neg)âŸ©

instance [add_group Î²] : add_group (Î± â†’â‚› Î²) :=
{ neg := has_neg.neg,
  add_left_neg := Î»f, ext (Î»a, add_left_neg _),
  .. simple_func.add_monoid }

instance [add_comm_group Î²] : add_comm_group (Î± â†’â‚› Î²) :=
{ add_comm := Î» f g, ext (Î»a, add_comm _ _) ,
  .. simple_func.add_group }

variables {K : Type*}

instance [has_scalar K Î²] : has_scalar K (Î± â†’â‚› Î²) := âŸ¨Î»k f, f.map (Î»b, k â€¢ b)âŸ©

instance [semiring K] [add_comm_monoid Î²] [semimodule K Î²] : semimodule K (Î± â†’â‚› Î²) :=
{ one_smul := Î» f, ext (Î»a, one_smul _ _),
  mul_smul := Î» x y f, ext (Î»a, mul_smul _ _ _),
  smul_add := Î» r f g, ext (Î»a, smul_add _ _ _),
  smul_zero := Î» r, ext (Î»a, smul_zero _),
  add_smul := Î» r s f, ext (Î»a, add_smul _ _ _),
  zero_smul := Î» f, ext (Î»a, zero_smul _ _) }

lemma smul_apply [has_scalar K Î²] (k : K) (f : Î± â†’â‚› Î²) (a : Î±) : (k â€¢ f) a = k â€¢ f a := rfl

lemma smul_eq_map [has_scalar K Î²] (k : K) (f : Î± â†’â‚› Î²) : k â€¢ f = f.map (Î»b, k â€¢ b) := rfl

instance [preorder Î²] : preorder (Î± â†’â‚› Î²) :=
{ le_refl := Î»f a, le_refl _,
  le_trans := Î»f g h hfg hgh a, le_trans (hfg _) (hgh a),
  .. simple_func.has_le }

instance [partial_order Î²] : partial_order (Î± â†’â‚› Î²) :=
{ le_antisymm := assume f g hfg hgf, ext $ assume a, le_antisymm (hfg a) (hgf a),
  .. simple_func.preorder }

instance [order_bot Î²] : order_bot (Î± â†’â‚› Î²) :=
{ bot := const Î± âŠ¥, bot_le := Î»f a, bot_le, .. simple_func.partial_order }

instance [order_top Î²] : order_top (Î± â†’â‚› Î²) :=
{ top := const Î±âŠ¤, le_top := Î»f a, le_top, .. simple_func.partial_order }

instance [semilattice_inf Î²] : semilattice_inf (Î± â†’â‚› Î²) :=
{ inf := (âŠ“),
  inf_le_left := assume f g a, inf_le_left,
  inf_le_right := assume f g a, inf_le_right,
  le_inf := assume f g h hfh hgh a, le_inf (hfh a) (hgh a),
  .. simple_func.partial_order }

instance [semilattice_sup Î²] : semilattice_sup (Î± â†’â‚› Î²) :=
{ sup := (âŠ”),
  le_sup_left := assume f g a, le_sup_left,
  le_sup_right := assume f g a, le_sup_right,
  sup_le := assume f g h hfh hgh a, sup_le (hfh a) (hgh a),
  .. simple_func.partial_order }

instance [semilattice_sup_bot Î²] : semilattice_sup_bot (Î± â†’â‚› Î²) :=
{ .. simple_func.semilattice_sup,.. simple_func.order_bot }

instance [lattice Î²] : lattice (Î± â†’â‚› Î²) :=
{ .. simple_func.semilattice_sup,.. simple_func.semilattice_inf }

instance [bounded_lattice Î²] : bounded_lattice (Î± â†’â‚› Î²) :=
{ .. simple_func.lattice, .. simple_func.order_bot, .. simple_func.order_top }

lemma finset_sup_apply [semilattice_sup_bot Î²] {f : Î³ â†’ Î± â†’â‚› Î²} (s : finset Î³) (a : Î±) :
  s.sup f a = s.sup (Î»c, f c a) :=
begin
  refine finset.induction_on s rfl _,
  assume a s hs ih,
  rw [finset.sup_insert, finset.sup_insert, sup_apply, ih]
end

section approx

section
variables [semilattice_sup_bot Î²] [has_zero Î²]

/-- Fix a sequence `i : â„• â†’ Î²`. Given a function `Î± â†’ Î²`, its `n`-th approximation
by simple functions is defined so that in case `Î² = ennreal` it sends each `a` to the supremum
of the set `{i k | k â‰¤ n âˆ§ i k â‰¤ f a}`, see `approx_apply` and `supr_approx_apply` for details. -/
def approx (i : â„• â†’ Î²) (f : Î± â†’ Î²) (n : â„•) : Î± â†’â‚› Î² :=
(finset.range n).sup (Î»k, restrict (const Î± (i k)) {a:Î± | i k â‰¤ f a})

lemma approx_apply [topological_space Î²] [order_closed_topology Î²] [measurable_space Î²]
  [opens_measurable_space Î²] {i : â„• â†’ Î²} {f : Î± â†’ Î²} {n : â„•} (a : Î±) (hf : _root_.measurable f) :
  (approx i f n : Î± â†’â‚› Î²) a = (finset.range n).sup (Î»k, if i k â‰¤ f a then i k else 0) :=
begin
  dsimp only [approx],
  rw [finset_sup_apply],
  congr,
  funext k,
  rw [restrict_apply],
  refl,
  exact (hf.preimage is_measurable_Ici)
end

lemma monotone_approx (i : â„• â†’ Î²) (f : Î± â†’ Î²) : monotone (approx i f) :=
assume n m h, finset.sup_mono $ finset.range_subset.2 h

lemma approx_comp [topological_space Î²] [order_closed_topology Î²] [measurable_space Î²]
  [opens_measurable_space Î²] [measurable_space Î³]
  {i : â„• â†’ Î²} {f : Î³ â†’ Î²} {g : Î± â†’ Î³} {n : â„•} (a : Î±)
  (hf : _root_.measurable f) (hg : _root_.measurable g) :
  (approx i (f âˆ˜ g) n : Î± â†’â‚› Î²) a = (approx i f n : Î³ â†’â‚› Î²) (g a) :=
by rw [approx_apply _ hf, approx_apply _ (hf.comp hg)]

end

lemma supr_approx_apply [topological_space Î²] [complete_lattice Î²] [order_closed_topology Î²] [has_zero Î²]
  [measurable_space Î²] [opens_measurable_space Î²]
  (i : â„• â†’ Î²) (f : Î± â†’ Î²) (a : Î±) (hf : _root_.measurable f) (h_zero : (0 : Î²) = âŠ¥) :
  (â¨†n, (approx i f n : Î± â†’â‚› Î²) a) = (â¨†k (h : i k â‰¤ f a), i k) :=
begin
  refine le_antisymm (supr_le $ assume n, _) (supr_le $ assume k, supr_le $ assume hk, _),
  { rw [approx_apply a hf, h_zero],
    refine finset.sup_le (assume k hk, _),
    split_ifs,
    exact le_supr_of_le k (le_supr _ h),
    exact bot_le },
  { refine le_supr_of_le (k+1) _,
    rw [approx_apply a hf],
    have : k âˆˆ finset.range (k+1) := finset.mem_range.2 (nat.lt_succ_self _),
    refine le_trans (le_of_eq _) (finset.le_sup this),
    rw [if_pos hk] }
end

end approx

section eapprox

/-- A sequence of `ennreal`s such that its range is the set of non-negative rational numbers. -/
def ennreal_rat_embed (n : â„•) : ennreal :=
nnreal.of_real ((encodable.decode â„š n).get_or_else (0 : â„š))

lemma ennreal_rat_embed_encode (q : â„š) :
  ennreal_rat_embed (encodable.encode q) = nnreal.of_real q :=
by rw [ennreal_rat_embed, encodable.encodek]; refl

/-- Approximate a function `Î± â†’ ennreal` by a sequence of simple functions. -/
def eapprox : (Î± â†’ ennreal) â†’ â„• â†’ Î± â†’â‚› ennreal :=
approx ennreal_rat_embed

lemma monotone_eapprox (f : Î± â†’ ennreal) : monotone (eapprox f) :=
monotone_approx _ f

lemma supr_eapprox_apply (f : Î± â†’ ennreal) (hf : _root_.measurable f) (a : Î±) :
  (â¨†n, (eapprox f n : Î± â†’â‚› ennreal) a) = f a :=
begin
  rw [eapprox, supr_approx_apply ennreal_rat_embed f a hf rfl],
  refine le_antisymm (supr_le $ assume i, supr_le $ assume hi, hi) (le_of_not_gt _),
  assume h,
  rcases ennreal.lt_iff_exists_rat_btwn.1 h with âŸ¨q, hq, lt_q, q_ltâŸ©,
  have : (nnreal.of_real q : ennreal) â‰¤
      (â¨† (k : â„•) (h : ennreal_rat_embed k â‰¤ f a), ennreal_rat_embed k),
  { refine le_supr_of_le (encodable.encode q) _,
    rw [ennreal_rat_embed_encode q],
    refine le_supr_of_le (le_of_lt q_lt) _,
    exact le_refl _ },
  exact lt_irrefl _ (lt_of_le_of_lt this lt_q)
end

lemma eapprox_comp [measurable_space Î³] {f : Î³ â†’ ennreal} {g : Î± â†’ Î³} {n : â„•}
  (hf : _root_.measurable f) (hg : _root_.measurable g) :
  (eapprox (f âˆ˜ g) n : Î± â†’ ennreal) = (eapprox f n : Î³ â†’â‚› ennreal) âˆ˜ g :=
funext $ assume a, approx_comp a hf hg

end eapprox

end measurable

section measure
variables [measure_space Î±]

lemma volume_bUnion_preimage (s : finset Î²) (f : Î± â†’â‚› Î²) :
  volume (â‹ƒb âˆˆ s, f â»Â¹' {b}) = âˆ‘ b in s, volume (f â»Â¹' {b}) :=
begin
  /- Taking advantage of the fact that `f â»Â¹' {b}` are disjoint for `b âˆˆ s`. -/
  rw [measure_bUnion_finset],
  { simp only [pairwise_on, (on), finset.mem_coe, ne.def],
    rintros _ _ _ _ ne _ âŸ¨hâ‚, hâ‚‚âŸ©,
    simp only [mem_singleton_iff, mem_preimage] at hâ‚ hâ‚‚,
    rw [â† hâ‚, hâ‚‚] at ne,
    exact ne rfl },
  exact assume a ha, preimage_measurable _ _
end

/-- Integral of a simple function whose codomain is `ennreal`. -/
def integral (f : Î± â†’â‚› ennreal) : ennreal :=
âˆ‘ x in f.range, x * volume (f â»Â¹' {x})

/-- Calculate the integral of `(g âˆ˜ f)`, where `g : Î² â†’ ennreal` and `f : Î± â†’â‚› Î²`.  -/
lemma map_integral (g : Î² â†’ ennreal) (f : Î± â†’â‚› Î²) :
  (f.map g).integral = âˆ‘ x in f.range, g x * volume (f â»Â¹' {x}) :=
begin
  simp only [integral, range_map],
  refine finset.sum_image' _ (assume b hb, _),
  rcases mem_range.1 hb with âŸ¨a, rflâŸ©,
  rw [map_preimage_singleton, volume_bUnion_preimage, finset.mul_sum],
  refine finset.sum_congr _ _,
  { congr },
  { assume x, simp only [finset.mem_filter], rintro âŸ¨_, hâŸ©, rw h }
end

lemma zero_integral : (0 : Î± â†’â‚› ennreal).integral = 0 :=
begin
  refine (finset.sum_eq_zero_iff_of_nonneg $ assume _ _, zero_le _).2 _,
  assume r hr, rcases mem_range.1 hr with âŸ¨a, rflâŸ©,
  exact zero_mul _
end

lemma add_integral (f g : Î± â†’â‚› ennreal) : (f + g).integral = f.integral + g.integral :=
calc (f + g).integral =
      âˆ‘ x in (pair f g).range, (x.1 * volume (pair f g â»Â¹' {x}) + x.2 * volume (pair f g â»Â¹' {x})) :
    by rw [add_eq_mapâ‚‚, map_integral]; exact finset.sum_congr rfl (assume a ha, add_mul _ _ _)
  ... = âˆ‘ x in (pair f g).range, x.1 * volume (pair f g â»Â¹' {x}) +
      âˆ‘ x in (pair f g).range, x.2 * volume (pair f g â»Â¹' {x}) : by rw [finset.sum_add_distrib]
  ... = ((pair f g).map prod.fst).integral + ((pair f g).map prod.snd).integral :
    by rw [map_integral, map_integral]
  ... = integral f + integral g : rfl

lemma const_mul_integral (f : Î± â†’â‚› ennreal) (x : ennreal) :
  (const Î± x * f).integral = x * f.integral :=
calc (f.map (Î»a, x * a)).integral = âˆ‘ r in f.range, x * r * volume (f â»Â¹' {r}) :
    by rw [map_integral]
  ... = âˆ‘ r in f.range, x * (r * volume (f â»Â¹' {r})) :
    finset.sum_congr rfl (assume a ha, mul_assoc _ _ _)
  ... = x * f.integral :
    finset.mul_sum.symm

lemma mem_restrict_range [has_zero Î²] {r : Î²} {s : set Î±} {f : Î± â†’â‚› Î²} (hs : is_measurable s) :
  r âˆˆ (restrict f s).range â†” (r = 0 âˆ§ s â‰  univ) âˆ¨ (âˆƒaâˆˆs, f a = r) :=
begin
  simp only [mem_range, restrict_apply, hs],
  split,
  { rintros âŸ¨a, haâŸ©,
    split_ifs at ha,
    { exact or.inr âŸ¨a, h, haâŸ© },
    { exact or.inl âŸ¨ha.symm, assume eq, h $ eq.symm â–¸ trivialâŸ© } },
  { rintros (âŸ¨rfl, hâŸ© | âŸ¨a, ha, rflâŸ©),
    { have : Â¬ âˆ€a, a âˆˆ s := assume this, h $ eq_univ_of_forall this,
      rcases not_forall.1 this with âŸ¨a, haâŸ©,
      refine âŸ¨a, _âŸ©,
      rw [if_neg ha] },
    { refine âŸ¨a, _âŸ©,
      rw [if_pos ha] } }
end

lemma restrict_preimage' {r : ennreal} {s : set Î±}
  (f : Î± â†’â‚› ennreal) (hs : is_measurable s) (hr : r â‰  0) :
  (restrict f s) â»Â¹' {r} = (f â»Â¹' {r} âˆ© s) :=
begin
  ext a,
  by_cases a âˆˆ s; simp [hs, h, hr.symm]
end

lemma restrict_integral (f : Î± â†’â‚› ennreal) (s : set Î±) (hs : is_measurable s) :
  (restrict f s).integral = âˆ‘ r in f.range, r * volume (f â»Â¹' {r} âˆ© s) :=
begin
  refine finset.sum_bij_ne_zero (Î»r _ _, r) _ _ _ _,
  { assume r hr,
    rcases (mem_restrict_range hs).1 hr with âŸ¨rfl, hâŸ© | âŸ¨a, ha, rflâŸ©,
    { simp },
    { assume _, exact mem_range.2 âŸ¨a, rflâŸ© } },
  { assume a b _ _ _ _ h, exact h },
  { assume r hr,
    by_cases r0 : r = 0, { simp [r0] },
    assume h0,
    rcases mem_range.1 hr with âŸ¨a, rflâŸ©,
    have : f â»Â¹' {f a} âˆ© s â‰  âˆ…,
    { assume h, simpa [h] using h0 },
    rcases ne_empty_iff_nonempty.1 this with âŸ¨a', eq', ha'âŸ©,
    refine âŸ¨_, (mem_restrict_range hs).2 (or.inr âŸ¨a', ha', _âŸ©), _, rflâŸ©,
    { simpa using eq' },
    { rwa [restrict_preimage' _ hs r0] } },
  { assume r hr ne,
    by_cases r = 0, { simp [h] },
    rw [restrict_preimage' _ hs h] }
end

lemma restrict_const_integral (c : ennreal) (s : set Î±) (hs : is_measurable s) :
  (restrict (const Î± c) s).integral = c * volume s :=
have (@const Î± ennreal _ c) â»Â¹' {c} = univ,
begin
  refine eq_univ_of_forall (assume a, _),
  simp,
end,
calc (restrict (const Î± c) s).integral = c * volume ((const Î± c) â»Â¹' {c} âˆ© s) :
  begin
    rw [restrict_integral (const Î± c) s hs],
    refine finset.sum_eq_single c _ _,
    { assume r hr, rcases mem_range.1 hr with âŸ¨a, rflâŸ©, contradiction },
    { by_cases nonempty Î±,
      { assume ne,
        rcases h with âŸ¨aâŸ©,
        exfalso,
        exact ne (mem_range.2 âŸ¨a, rflâŸ©) },
      { assume empty,
        have : (@const Î± ennreal _ c) â»Â¹' {c} âˆ© s = âˆ…,
        { ext a, exfalso, exact h âŸ¨aâŸ© },
        simp only [this, measure_empty, mul_zero] } }
  end
  ... = c * volume s : by rw [this, univ_inter]

lemma integral_sup_le (f g : Î± â†’â‚› ennreal) : f.integral âŠ” g.integral â‰¤ (f âŠ” g).integral :=
calc f.integral âŠ” g.integral =
      ((pair f g).map prod.fst).integral âŠ” ((pair f g).map prod.snd).integral : rfl
  ... â‰¤ âˆ‘ x in (pair f g).range, (x.1 âŠ” x.2) * volume (pair f g â»Â¹' {x}) :
  begin
    rw [map_integral, map_integral],
    refine sup_le _ _;
      refine finset.sum_le_sum (Î» a _, canonically_ordered_semiring.mul_le_mul _ (le_refl _)),
    exact le_sup_left,
    exact le_sup_right
  end
  ... = (f âŠ” g).integral : by rw [sup_eq_mapâ‚‚, map_integral]

lemma integral_le_integral (f g : Î± â†’â‚› ennreal) (h : f â‰¤ g) : f.integral â‰¤ g.integral :=
calc f.integral â‰¤ f.integral âŠ” g.integral : le_sup_left
  ... â‰¤ (f âŠ” g).integral : integral_sup_le _ _
  ... = g.integral : by rw [sup_of_le_right h]

lemma integral_congr (f g : Î± â†’â‚› ennreal) (h : âˆ€â‚˜ a, f a = g a) :
  f.integral = g.integral :=
show ((pair f g).map prod.fst).integral = ((pair f g).map prod.snd).integral, from
begin
  rw [map_integral, map_integral],
  refine finset.sum_congr rfl (assume p hp, _),
  rcases mem_range.1 hp with âŸ¨a, rflâŸ©,
  by_cases eq : f a = g a,
  { dsimp only [pair_apply], rw eq },
  { have : volume ((pair f g) â»Â¹' {(f a, g a)}) = 0,
    { refine measure_mono_null (assume a' ha', _) h,
      simp at ha',
      show f a' â‰  g a',
      rwa [ha'.1, ha'.2] },
    simp [this] }
end

lemma integral_map {Î²} [measure_space Î²] (f : Î± â†’â‚› ennreal) (g : Î² â†’â‚› ennreal)(m : Î± â†’ Î²)
  (eq : âˆ€a:Î±, f a = g (m a)) (h : âˆ€s:set Î², is_measurable s â†’ volume s = volume (m â»Â¹' s)) :
  f.integral = g.integral :=
have f_eq : (f : Î± â†’ ennreal) = g âˆ˜ m := funext eq,
have vol_f : âˆ€r, volume (f â»Â¹' {r}) = volume (g â»Â¹' {r}),
  by { assume r, rw [h, f_eq, preimage_comp], exact measurable_sn _ _ },
begin
  simp [integral, vol_f],
  refine finset.sum_subset _ _,
  { simp [finset.subset_iff, f_eq],
    rintros r a rfl, exact âŸ¨_, rflâŸ© },
  { assume r hrg hrf,
    rw [simple_func.mem_range, not_exists] at hrf,
    have : f â»Â¹' {r} = âˆ… := set.eq_empty_of_subset_empty (assume a, by simpa using hrf a),
    simp [(vol_f _).symm, this] }
end

end measure

section fin_vol_supp

variables [measure_space Î±] [has_zero Î²] [has_zero Î³]

open finset ennreal

protected def fin_vol_supp (f : Î± â†’â‚› Î²) : Prop := âˆ€b â‰  0, volume (f â»Â¹' {b}) < âŠ¤

lemma fin_vol_supp_map {f : Î± â†’â‚› Î²} {g : Î² â†’ Î³} (hf : f.fin_vol_supp) (hg : g 0 = 0) :
  (f.map g).fin_vol_supp :=
begin
  assume c hc,
  simp only [map_preimage, volume_bUnion_preimage],
  apply sum_lt_top,
  intro b,
  simp only [mem_filter, mem_range, mem_singleton_iff, and_imp, exists_imp_distrib],
  intros a fab gbc,
  apply hf,
  intro b0,
  rw [b0, hg] at gbc, rw gbc at hc,
  contradiction
end

lemma fin_vol_supp_of_fin_vol_supp_map (f : Î± â†’â‚› Î²) {g : Î² â†’ Î³} (h : (f.map g).fin_vol_supp)
  (hg : âˆ€b, g b = 0 â†’ b = 0) : f.fin_vol_supp :=
begin
  assume b hb,
  by_cases b_mem : b âˆˆ f.range,
  { have gb0 : g b â‰  0, { assume h, have := hg b h, contradiction },
    have : f â»Â¹' {b} âŠ† (f.map g) â»Â¹' {g b},
      rw [coe_map, @preimage_comp _ _ _ f g, preimage_subset_preimage_iff],
      { simp only [set.mem_preimage, set.mem_singleton, set.singleton_subset_iff] },
      { rw set.singleton_subset_iff, rw mem_range at b_mem, exact b_mem },
    exact lt_of_le_of_lt (measure_mono this) (h (g b) gb0) },
  { rw â† preimage_eq_empty_iff at b_mem,
    rw [b_mem, measure_empty],
    exact with_top.zero_lt_top }
end

lemma fin_vol_supp_pair {f : Î± â†’â‚› Î²} {g : Î± â†’â‚› Î³} (hf : f.fin_vol_supp) (hg : g.fin_vol_supp) :
  (pair f g).fin_vol_supp :=
begin
  rintros âŸ¨b, câŸ© hbc,
  rw [pair_preimage_singleton],
  rw [ne.def, prod.eq_iff_fst_eq_snd_eq, not_and_distrib] at hbc,
  refine or.elim hbc (Î» h : bâ‰ 0, _) (Î» h : câ‰ 0, _),
  { calc _ â‰¤ volume (f â»Â¹' {b}) : measure_mono (set.inter_subset_left _ _)
      ... < âŠ¤ : hf _ h },
  { calc _ â‰¤ volume (g â»Â¹' {c}) : measure_mono (set.inter_subset_right _ _)
      ... < âŠ¤ : hg _ h },
end

lemma integral_lt_top_of_fin_vol_supp {f : Î± â†’â‚› ennreal} (hâ‚ : âˆ€â‚˜ a, f a < âŠ¤) (hâ‚‚ : f.fin_vol_supp) :
  integral f < âŠ¤ :=
begin
  rw integral, apply sum_lt_top,
  intros a ha,
  have : f â»Â¹' {âŠ¤} = {a : Î± | f a < âŠ¤}á¶œ, { ext, simp },
  have vol_top : volume (f â»Â¹' {âŠ¤}) = 0, { rw [this, â† mem_ae_iff], exact hâ‚ },
  by_cases hat : a = âŠ¤,
  { rw [hat, vol_top, mul_zero], exact with_top.zero_lt_top },
  { by_cases haz : a = 0,
    { rw [haz, zero_mul], exact with_top.zero_lt_top },
    apply mul_lt_top,
    { rw ennreal.lt_top_iff_ne_top, exact hat },
    apply hâ‚‚,
    exact haz }
end

lemma fin_vol_supp_of_integral_lt_top {f : Î± â†’â‚› ennreal} (h : integral f < âŠ¤) : f.fin_vol_supp :=
begin
  assume b hb,
  rw [integral, sum_lt_top_iff] at h,
  by_cases b_mem : b âˆˆ f.range,
  { rw ennreal.lt_top_iff_ne_top,
    have h : Â¬ _ = âŠ¤ := ennreal.lt_top_iff_ne_top.1 (h b b_mem),
    simp only [mul_eq_top, not_or_distrib, not_and_distrib] at h,
    rcases h with âŸ¨h, h'âŸ©,
    refine or.elim h (Î»h, by contradiction) (Î»h, h) },
  { rw â† preimage_eq_empty_iff at b_mem,
    rw [b_mem, measure_empty],
    exact with_top.zero_lt_top }
end

/-- A technical lemma dealing with the definition of `integrable` in `l1_space.lean`. -/
lemma integral_map_coe_lt_top {f : Î± â†’â‚› Î²} {g : Î² â†’ nnreal} (h : f.fin_vol_supp) (hg : g 0 = 0) :
  integral (f.map ((coe : nnreal â†’ ennreal) âˆ˜ g)) < âŠ¤ :=
integral_lt_top_of_fin_vol_supp
  (by { filter_upwards[], assume a, simp only [mem_set_of_eq, map_apply], exact ennreal.coe_lt_top})
  (by { apply fin_vol_supp_map h, simp only [hg, function.comp_app, ennreal.coe_zero] })

end fin_vol_supp

end simple_func

section lintegral
open simple_func
variable [measure_space Î±]

/-- The lower Lebesgue integral -/
def lintegral (f : Î± â†’ ennreal) : ennreal :=
â¨† (s : Î± â†’â‚› ennreal) (hf : â‡‘s â‰¤ f), s.integral

notation `âˆ«â»` binders `, ` r:(scoped f, lintegral f) := r

theorem simple_func.lintegral_eq_integral (f : Î± â†’â‚› ennreal) : (âˆ«â» a, f a) = f.integral :=
le_antisymm
  (supr_le $ assume s, supr_le $ assume hs, integral_le_integral _ _ hs)
  (le_supr_of_le f $ le_supr_of_le (le_refl f) $ le_refl _)

lemma lintegral_mono â¦ƒf g : Î± â†’ ennrealâ¦„ (h : f â‰¤ g) : (âˆ«â» a, f a) â‰¤ (âˆ«â» a, g a) :=
supr_le_supr_of_subset $ assume s hs, le_trans hs h

lemma monotone_lintegral (Î± : Type*) [measure_space Î±] :
  monotone (@lintegral Î± _) :=
Î» f g h, lintegral_mono h

lemma lintegral_eq_nnreal (f : Î± â†’ ennreal) :
  (âˆ«â» a, f a) =
    (â¨† (s : Î± â†’â‚› nnreal) (hf : â‡‘(s.map (coe : nnreal â†’ ennreal)) â‰¤ f),
      (s.map (coe : nnreal â†’ ennreal)).integral) :=
begin
  let c : nnreal â†’ ennreal := coe,
  refine le_antisymm
    (supr_le $ assume s, supr_le $ assume hs, _)
    (supr_le $ assume s, supr_le $ assume hs, le_supr_of_le (s.map c) $ le_supr _ hs),
  by_cases âˆ€â‚˜ a, s a â‰  âŠ¤,
  { have : f â‰¥ (s.map ennreal.to_nnreal).map c :=
      le_trans (assume a, ennreal.coe_to_nnreal_le_self) hs,
    refine le_supr_of_le (s.map ennreal.to_nnreal) (le_supr_of_le this (le_of_eq $ integral_congr _ _ _)),
    exact filter.mem_sets_of_superset h (assume a ha, (ennreal.coe_to_nnreal ha).symm) },
  { have h_vol_s : volume {a : Î± | s a = âŠ¤} â‰  0,
      from mt measure_zero_iff_ae_nmem.1 h,
    let n : â„• â†’ (Î± â†’â‚› nnreal) := Î»n, restrict (const Î± (n : nnreal)) (s â»Â¹' {âŠ¤}),
    have n_le_s : âˆ€i, (n i).map c â‰¤ s,
    { assume i a,
      dsimp [n, c],
      rw [restrict_apply _ (s.preimage_measurable _)],
      split_ifs with ha,
      { simp at ha, exact ha.symm â–¸ le_top },
      { exact zero_le _ } },
    have approx_s : âˆ€ (i : â„•), â†‘i * volume {a : Î± | s a = âŠ¤} â‰¤ integral (map c (n i)),
    { assume i,
      have : {a : Î± | s a = âŠ¤} = s â»Â¹' {âŠ¤}, { ext a, simp },
      rw [this, â† restrict_const_integral _ _ (s.preimage_measurable _)],
      { refine integral_le_integral _ _ (assume a, le_of_eq _),
        simp [n, c, restrict_apply, s.preimage_measurable],
        split_ifs; simp [ennreal.coe_nat] },
     },
    calc s.integral â‰¤ âŠ¤ : le_top
      ... = (â¨†i:â„•, (i : ennreal) * volume {a | s a = âŠ¤}) :
        by rw [â† ennreal.supr_mul, ennreal.supr_coe_nat, ennreal.top_mul, if_neg h_vol_s]
      ... â‰¤ (â¨†i, ((n i).map c).integral) : supr_le_supr approx_s
      ... â‰¤ â¨† (s : Î± â†’â‚› nnreal) (hf : f â‰¥ s.map c), (s.map c).integral :
        have âˆ€i, ((n i).map c : Î± â†’ ennreal) â‰¤ f := assume i, le_trans (n_le_s i) hs,
        (supr_le $ assume i, le_supr_of_le (n i) (le_supr (Î»h, ((n i).map c).integral) (this i))) }
end

theorem supr_lintegral_le {Î¹ : Sort*} (f : Î¹ â†’ Î± â†’ ennreal) :
  (â¨†i, âˆ«â» a, f i a) â‰¤ (âˆ«â» a, â¨†i, f i a) :=
by { simp only [â† supr_apply], exact (monotone_lintegral Î±).le_map_supr }

theorem supr2_lintegral_le {Î¹ : Sort*} {Î¹' : Î¹ â†’ Sort*} (f : Î  i, Î¹' i â†’ Î± â†’ ennreal) :
  (â¨†i (h : Î¹' i), âˆ«â» a, f i h a) â‰¤ (âˆ«â» a, â¨†i (h : Î¹' i), f i h a) :=
by { convert (monotone_lintegral Î±).le_map_supr2 f, ext1 a, simp only [supr_apply] }

theorem le_infi_lintegral {Î¹ : Sort*} (f : Î¹ â†’ Î± â†’ ennreal) :
  (âˆ«â» a, â¨…i, f i a) â‰¤ (â¨…i, âˆ«â» a, f i a) :=
by { simp only [â† infi_apply], exact (monotone_lintegral Î±).map_infi_le }

theorem le_infi2_lintegral {Î¹ : Sort*} {Î¹' : Î¹ â†’ Sort*} (f : Î  i, Î¹' i â†’ Î± â†’ ennreal) :
  (âˆ«â» a, â¨… i (h : Î¹' i), f i h a) â‰¤ (â¨… i (h : Î¹' i), âˆ«â» a, f i h a) :=
by { convert (monotone_lintegral Î±).map_infi2_le f, ext1 a, simp only [infi_apply] }

/-- Monotone convergence theorem -- sometimes called Beppo-Levi convergence.

See `lintegral_supr_directed` for a more general form. -/
theorem lintegral_supr
  {f : â„• â†’ Î± â†’ ennreal} (hf : âˆ€n, measurable (f n)) (h_mono : monotone f) :
  (âˆ«â» a, â¨†n, f n a) = (â¨†n, âˆ«â» a, f n a) :=
begin
  set c : nnreal â†’ ennreal := coe,
  set F := Î» a:Î±, â¨†n, f n a,
  have hF : measurable F := measurable_supr hf,
  refine le_antisymm _ (supr_lintegral_le _),
  rw [lintegral_eq_nnreal],
  refine supr_le (assume s, supr_le (assume hsf, _)),
  refine ennreal.le_of_forall_lt_one_mul_lt (assume a ha, _),
  rcases ennreal.lt_iff_exists_coe.1 ha with âŸ¨r, rfl, haâŸ©,
  have ha : r < 1 := ennreal.coe_lt_coe.1 ha,
  let rs := s.map (Î»a, r * a),
  have eq_rs : (const Î± r : Î± â†’â‚› ennreal) * map c s = rs.map c,
  { ext1 a, exact ennreal.coe_mul.symm },
  have eq : âˆ€p, (rs.map c) â»Â¹' {p} = (â‹ƒn, (rs.map c) â»Â¹' {p} âˆ© {a | p â‰¤ f n a}),
  { assume p,
    rw [â† inter_Union, â† inter_univ ((map c rs) â»Â¹' {p})] {occs := occurrences.pos [1]},
    refine set.ext (assume x, and_congr_right $ assume hx, (true_iff _).2 _),
    by_cases p_eq : p = 0, { simp [p_eq] },
    simp at hx, subst hx,
    have : r * s x â‰  0, { rwa [(â‰ ), â† ennreal.coe_eq_zero] },
    have : s x â‰  0, { refine mt _ this, assume h, rw [h, mul_zero] },
    have : (rs.map c) x < â¨† (n : â„•), f n x,
    { refine lt_of_lt_of_le (ennreal.coe_lt_coe.2 (_)) (hsf x),
      suffices : r * s x < 1 * s x, simpa [rs],
      exact mul_lt_mul_of_pos_right ha (zero_lt_iff_ne_zero.2 this) },
    rcases lt_supr_iff.1 this with âŸ¨i, hiâŸ©,
    exact mem_Union.2 âŸ¨i, le_of_lt hiâŸ© },
  have mono : âˆ€r:ennreal, monotone (Î»n, (rs.map c) â»Â¹' {r} âˆ© {a | r â‰¤ f n a}),
  { assume r i j h,
    refine inter_subset_inter (subset.refl _) _,
    assume x hx, exact le_trans hx (h_mono h x) },
  have h_meas : âˆ€n, is_measurable {a : Î± | â‡‘(map c rs) a â‰¤ f n a} :=
    assume n, is_measurable_le (simple_func.measurable _) (hf n),
  calc (r:ennreal) * integral (s.map c) = âˆ‘ r in (rs.map c).range, r * volume ((rs.map c) â»Â¹' {r}) :
      by rw [â† const_mul_integral, integral, eq_rs]
    ... â‰¤ âˆ‘ r in (rs.map c).range, r * volume (â‹ƒn, (rs.map c) â»Â¹' {r} âˆ© {a | r â‰¤ f n a}) :
      le_of_eq (finset.sum_congr rfl $ assume x hx, by rw â† eq)
    ... â‰¤ âˆ‘ r in (rs.map c).range, (â¨†n, r * volume ((rs.map c) â»Â¹' {r} âˆ© {a | r â‰¤ f n a})) :
      le_of_eq (finset.sum_congr rfl $ assume x hx,
        begin
          rw [measure_Union_eq_supr_nat _ (mono x), ennreal.mul_supr],
          { assume i,
            refine ((rs.map c).preimage_measurable _).inter _,
            exact (hf i).preimage is_measurable_Ici }
        end)
    ... â‰¤ â¨†n, âˆ‘ r in (rs.map c).range, r * volume ((rs.map c) â»Â¹' {r} âˆ© {a | r â‰¤ f n a}) :
      begin
        refine le_of_eq _,
        rw [ennreal.finset_sum_supr_nat],
        assume p i j h,
        exact canonically_ordered_semiring.mul_le_mul (le_refl _) (measure_mono $ mono p h)
      end
    ... â‰¤ (â¨†n:â„•, ((rs.map c).restrict {a | (rs.map c) a â‰¤ f n a}).integral) :
    begin
      refine supr_le_supr (assume n, _),
      rw [restrict_integral _ _ (h_meas n)],
      { refine le_of_eq (finset.sum_congr rfl $ assume r hr, _),
        congr' 2,
        ext a,
        refine and_congr_right _,
        simp {contextual := tt} }
    end
    ... â‰¤ (â¨†n, âˆ«â» a, f n a) :
    begin
      refine supr_le_supr (assume n, _),
      rw [â† simple_func.lintegral_eq_integral],
      refine lintegral_mono (assume a, _),
      dsimp,
      rw [restrict_apply],
      split_ifs; simp, simpa using h,
      exact h_meas n
    end
end

lemma lintegral_eq_supr_eapprox_integral {f : Î± â†’ ennreal} (hf : measurable f) :
  (âˆ«â» a, f a) = (â¨†n, (eapprox f n).integral) :=
calc (âˆ«â» a, f a) = (âˆ«â» a, â¨†n, (eapprox f n : Î± â†’ ennreal) a) :
   by congr; ext a; rw [supr_eapprox_apply f hf]
 ... = (â¨†n, âˆ«â» a, (eapprox f n : Î± â†’ ennreal) a) :
 begin
   rw [lintegral_supr],
   { assume n, exact (eapprox f n).measurable },
   { assume i j h, exact (monotone_eapprox f h) }
 end
 ... = (â¨†n, (eapprox f n).integral) : by congr; ext n; rw [(eapprox f n).lintegral_eq_integral]

lemma lintegral_add {f g : Î± â†’ ennreal} (hf : measurable f) (hg : measurable g) :
  (âˆ«â» a, f a + g a) = (âˆ«â» a, f a) + (âˆ«â» a, g a) :=
calc (âˆ«â» a, f a + g a) =
    (âˆ«â» a, (â¨†n, (eapprox f n : Î± â†’ ennreal) a) + (â¨†n, (eapprox g n : Î± â†’ ennreal) a)) :
    by congr; funext a; rw [supr_eapprox_apply f hf, supr_eapprox_apply g hg]
  ... = (âˆ«â» a, (â¨†n, (eapprox f n + eapprox g n : Î± â†’ ennreal) a)) :
  begin
    congr, funext a,
    rw [ennreal.supr_add_supr_of_monotone], { refl },
    { assume i j h, exact monotone_eapprox _ h a },
    { assume i j h, exact monotone_eapprox _ h a },
  end
  ... = (â¨†n, (eapprox f n).integral + (eapprox g n).integral) :
  begin
    rw [lintegral_supr],
    { congr, funext n, rw [â† simple_func.add_integral, â† simple_func.lintegral_eq_integral], refl },
    { assume n, exact measurable.add (eapprox f n).measurable (eapprox g n).measurable },
    { assume i j h a, exact add_le_add (monotone_eapprox _ h _) (monotone_eapprox _ h _) }
  end
  ... = (â¨†n, (eapprox f n).integral) + (â¨†n, (eapprox g n).integral) :
  by refine (ennreal.supr_add_supr_of_monotone _ _).symm;
     { assume i j h, exact simple_func.integral_le_integral _ _ (monotone_eapprox _ h) }
  ... = (âˆ«â» a, f a) + (âˆ«â» a, g a) :
    by rw [lintegral_eq_supr_eapprox_integral hf, lintegral_eq_supr_eapprox_integral hg]

@[simp] lemma lintegral_zero : (âˆ«â» a:Î±, 0) = 0 :=
show (âˆ«â» a:Î±, (0 : Î± â†’â‚› ennreal) a) = 0, by rw [simple_func.lintegral_eq_integral, zero_integral]

lemma lintegral_finset_sum (s : finset Î²) {f : Î² â†’ Î± â†’ ennreal} (hf : âˆ€b, measurable (f b)) :
  (âˆ«â» a, âˆ‘ b in s, f b a) = âˆ‘ b in s, âˆ«â» a, f b a :=
begin
  refine finset.induction_on s _ _,
  { simp },
  { assume a s has ih,
    simp only [finset.sum_insert has],
    rw [lintegral_add (hf _) (s.measurable_sum hf), ih] }
end

lemma lintegral_const_mul (r : ennreal) {f : Î± â†’ ennreal} (hf : measurable f) :
  (âˆ«â» a, r * f a) = r * (âˆ«â» a, f a) :=
calc (âˆ«â» a, r * f a) = (âˆ«â» a, (â¨†n, (const Î± r * eapprox f n) a)) :
    by { congr, funext a, rw [â† supr_eapprox_apply f hf, ennreal.mul_supr], refl }
  ... = (â¨†n, r * (eapprox f n).integral) :
  begin
    rw [lintegral_supr],
    { congr, funext n, rw [â† simple_func.const_mul_integral, â† simple_func.lintegral_eq_integral] },
    { assume n, dsimp, exact simple_func.measurable _ },
    { assume i j h a, exact canonically_ordered_semiring.mul_le_mul (le_refl _)
        (monotone_eapprox _ h _) }
  end
  ... = r * (âˆ«â» a, f a) : by rw [â† ennreal.mul_supr, lintegral_eq_supr_eapprox_integral hf]

lemma lintegral_const_mul_le (r : ennreal) (f : Î± â†’ ennreal) : r * (âˆ«â» a, f a) â‰¤ (âˆ«â» a, r * f a) :=
begin
  rw [lintegral, ennreal.mul_supr],
  refine supr_le (Î»s, _),
  rw [ennreal.mul_supr],
  simp only [supr_le_iff, ge_iff_le],
  assume hs,
  rw â† simple_func.const_mul_integral,
  refine le_supr_of_le (const Î± r * s) (le_supr_of_le (Î»x, _) (le_refl _)),
  exact canonically_ordered_semiring.mul_le_mul (le_refl _) (hs x)
end

lemma lintegral_const_mul' (r : ennreal) (f : Î± â†’ ennreal) (hr : r â‰  âŠ¤) :
  (âˆ«â» a, r * f a) = r * (âˆ«â» a, f a) :=
begin
  by_cases h : r = 0,
  { simp [h] },
  apply le_antisymm _ (lintegral_const_mul_le r f),
  have rinv : r * râ»Â¹  = 1 := ennreal.mul_inv_cancel h hr,
  have rinv' : r â»Â¹ * r = 1, by { rw mul_comm, exact rinv },
  have := lintegral_const_mul_le (râ»Â¹) (Î»x, r * f x),
  simp [(mul_assoc _ _ _).symm, rinv'] at this,
  simpa [(mul_assoc _ _ _).symm, rinv]
    using canonically_ordered_semiring.mul_le_mul (le_refl r) this
end

lemma lintegral_supr_const (r : ennreal) {s : set Î±} (hs : is_measurable s) :
  (âˆ«â» a, â¨†(h : a âˆˆ s), r) = r * volume s :=
begin
  rw [â† restrict_const_integral r s hs, â† (restrict (const Î± r) s).lintegral_eq_integral],
  congr; ext a; by_cases a âˆˆ s; simp [h, hs]
end

lemma lintegral_le_lintegral_ae {f g : Î± â†’ ennreal} (h : âˆ€â‚˜ a, f a â‰¤ g a) :
  (âˆ«â» a, f a) â‰¤ (âˆ«â» a, g a) :=
begin
  rcases exists_is_measurable_superset_of_measure_eq_zero h with âŸ¨t, hts, ht, ht0âŸ©,
  have : tá¶œ âˆˆ (@volume Î± _).ae,
  { rw [mem_ae_iff, compl_compl, ht0] },
  refine (supr_le $ assume s, supr_le $ assume hfs,
    le_supr_of_le (s.restrict tá¶œ) $ le_supr_of_le _ _),
  { assume a,
    by_cases a âˆˆ t;
      simp [h, restrict_apply, ht.compl],
    exact le_trans (hfs a) (by_contradiction $ assume hnfg, h (hts hnfg)) },
  { refine le_of_eq (s.integral_congr _ _),
    filter_upwards [this],
    refine assume a hnt, _,
    by_cases hat : a âˆˆ t; simp [hat, ht.compl],
    exact (hnt hat).elim }
end

lemma lintegral_congr_ae {f g : Î± â†’ ennreal} (h : âˆ€â‚˜ a, f a = g a) :
  (âˆ«â» a, f a) = (âˆ«â» a, g a) :=
le_antisymm
  (lintegral_le_lintegral_ae $ h.mono $ assume a h, le_of_eq h)
  (lintegral_le_lintegral_ae $ h.mono $ assume a h, le_of_eq h.symm)

lemma lintegral_congr {f g : Î± â†’ ennreal} (h : âˆ€ a, f a = g a) :
  (âˆ«â» a, f a) = (âˆ«â» a, g a) :=
by simp only [h]

-- TODO: Need a better way of rewriting inside of a integral
lemma lintegral_rwâ‚ {f f' : Î± â†’ Î²} (h : âˆ€â‚˜ a, f a = f' a) (g : Î² â†’ ennreal) :
  (âˆ«â» a, g (f a)) = (âˆ«â» a, g (f' a)) :=
lintegral_congr_ae $ h.mono $ Î» a h, by rw h

-- TODO: Need a better way of rewriting inside of a integral
lemma lintegral_rwâ‚‚ {fâ‚ fâ‚' : Î± â†’ Î²} {fâ‚‚ fâ‚‚' : Î± â†’ Î³} (hâ‚ : âˆ€â‚˜ a, fâ‚ a = fâ‚' a)
  (hâ‚‚ : âˆ€â‚˜ a, fâ‚‚ a = fâ‚‚' a) (g : Î² â†’ Î³ â†’ ennreal) :
  (âˆ«â» a, g (fâ‚ a) (fâ‚‚ a)) = (âˆ«â» a, g (fâ‚' a) (fâ‚‚' a)) :=
lintegral_congr_ae $ hâ‚.mp $ hâ‚‚.mono $ Î» _ hâ‚‚ hâ‚, by rw [hâ‚, hâ‚‚]

lemma simple_func.lintegral_map (f : Î± â†’â‚› Î²) (g : Î² â†’ ennreal) :
  (âˆ«â» a, (f.map g) a) = âˆ«â» a, g (f a) :=
by simp only [map_apply]

/-- Chebyshev's inequality -/
lemma mul_volume_ge_le_lintegral {f : Î± â†’ ennreal} (hf : measurable f) (Îµ : ennreal) :
  Îµ * volume {x | Îµ â‰¤ f x} â‰¤ âˆ«â» a, f a :=
begin
  have : is_measurable {a : Î± | Îµ â‰¤ f a }, from hf.preimage is_measurable_Ici,
  rw [â† simple_func.restrict_const_integral _ _ this, â† simple_func.lintegral_eq_integral],
  refine lintegral_mono (Î» a, _),
  simp only [restrict_apply _ this],
  split_ifs; [assumption, exact zero_le _]
end

lemma volume_ge_le_lintegral_div {f : Î± â†’ ennreal} (hf : measurable f) {Îµ : ennreal}
  (hÎµ : Îµ â‰  0) (hÎµ' : Îµ â‰  âŠ¤) :
  volume {x | Îµ â‰¤ f x} â‰¤ (âˆ«â» a, f a) / Îµ :=
(ennreal.le_div_iff_mul_le (or.inl hÎµ) (or.inl hÎµ')).2 $
by { rw [mul_comm], exact mul_volume_ge_le_lintegral hf Îµ }

lemma lintegral_eq_zero_iff {f : Î± â†’ ennreal} (hf : measurable f) :
  lintegral f = 0 â†” (âˆ€â‚˜ a, f a = 0) :=
begin
  refine iff.intro (assume h, _) (assume h, _),
  { have : âˆ€n:â„•, âˆ€â‚˜ a, f a < nâ»Â¹,
    { assume n,
      rw [ae_iff, â† le_zero_iff_eq, â† @ennreal.zero_div nâ»Â¹,
        ennreal.le_div_iff_mul_le, mul_comm],
      simp only [not_lt],
      -- TODO: why `rw â† h` fails with "not an equality or an iff"?
      exacts [h â–¸ mul_volume_ge_le_lintegral hf nâ»Â¹,
        or.inl (ennreal.inv_ne_zero.2 ennreal.coe_nat_ne_top),
        or.inr ennreal.zero_ne_top] },
    refine (ae_all_iff.2 this).mono (Î» a ha, _),
    by_contradiction h,
    rcases ennreal.exists_inv_nat_lt h with âŸ¨n, hnâŸ©,
    exact (lt_irrefl _ $ lt_trans hn $ ha n).elim },
  { calc lintegral f = lintegral (Î»a:Î±, 0) : lintegral_congr_ae h
      ... = 0 : lintegral_zero }
end

/-- Weaker version of the monotone convergence theorem-/
lemma lintegral_supr_ae {f : â„• â†’ Î± â†’ ennreal} (hf : âˆ€n, measurable (f n))
  (h_mono : âˆ€n, âˆ€â‚˜ a, f n a â‰¤ f n.succ a) :
  (âˆ«â» a, â¨†n, f n a) = (â¨†n, âˆ«â» a, f n a) :=
let âŸ¨s, hsâŸ© := exists_is_measurable_superset_of_measure_eq_zero
                       (ae_iff.1 (ae_all_iff.2 h_mono)) in
let g := Î» n a, if a âˆˆ s then 0 else f n a in
have g_eq_f : âˆ€â‚˜ a, âˆ€n, g n a = f n a,
  begin
    have := hs.2.2, rw [â† compl_compl s] at this,
    filter_upwards [(mem_ae_iff sá¶œ).2 this] assume a ha n, if_neg ha
  end,
calc
  (âˆ«â» a, â¨†n, f n a) = (âˆ«â» a, â¨†n, g n a) :
  lintegral_congr_ae
    begin
      filter_upwards [g_eq_f], assume a ha, congr, funext, exact (ha n).symm
    end
  ... = â¨†n, (âˆ«â» a, g n a) :
  lintegral_supr
    (assume n, measurable.if hs.2.1 measurable_const (hf n))
    (monotone_of_monotone_nat $ assume n a,  classical.by_cases
      (assume h : a âˆˆ s, by simp [g, if_pos h])
      (assume h : a âˆ‰ s,
      begin
        simp only [g, if_neg h], have := hs.1, rw subset_def at this, have := mt (this a) h,
        simp only [not_not, mem_set_of_eq] at this, exact this n
      end))
  ... = â¨†n, (âˆ«â» a, f n a) :
  begin
    congr, funext, apply lintegral_congr_ae, filter_upwards [g_eq_f] assume a ha, ha n
  end

lemma lintegral_sub {f g : Î± â†’ ennreal} (hf : measurable f) (hg : measurable g)
  (hg_fin : lintegral g < âŠ¤) (h_le : âˆ€â‚˜ a, g a â‰¤ f a) :
  (âˆ«â» a, f a - g a) = (âˆ«â» a, f a) - (âˆ«â» a, g a) :=
begin
  rw [â† ennreal.add_left_inj hg_fin,
        ennreal.sub_add_cancel_of_le (lintegral_le_lintegral_ae h_le),
      â† lintegral_add (hf.ennreal_sub hg) hg],
  show  (âˆ«â» (a : Î±), f a - g a + g a) = âˆ«â» (a : Î±), f a,
  apply lintegral_congr_ae, filter_upwards [h_le], simp only [add_comm, mem_set_of_eq],
  assume a ha, exact ennreal.add_sub_cancel_of_le ha
end

/-- Monotone convergence theorem for nonincreasing sequences of functions -/
lemma lintegral_infi_ae
  {f : â„• â†’ Î± â†’ ennreal} (h_meas : âˆ€n, measurable (f n))
  (h_mono : âˆ€n:â„•, âˆ€â‚˜ a, f n.succ a â‰¤ f n a) (h_fin : lintegral (f 0) < âŠ¤) :
  (âˆ«â» a, â¨…n, f n a) = (â¨…n, âˆ«â» a, f n a) :=
have fn_le_f0 : (âˆ«â» a, â¨…n, f n a) â‰¤ lintegral (f 0), from
  lintegral_mono (assume a, infi_le_of_le 0 (le_refl _)),
have fn_le_f0' : (â¨…n, âˆ«â» a, f n a) â‰¤ lintegral (f 0), from infi_le_of_le 0 (le_refl _),
(ennreal.sub_right_inj h_fin fn_le_f0 fn_le_f0').1 $
show lintegral (f 0) - (âˆ«â» a, â¨…n, f n a) = lintegral (f 0) - (â¨…n, âˆ«â» a, f n a), from
calc
  lintegral (f 0) - (âˆ«â» a, â¨…n, f n a) = âˆ«â» a, f 0 a - â¨…n, f n a :
    (lintegral_sub (h_meas 0) (measurable_infi h_meas)
    (calc
      (âˆ«â» a, â¨…n, f n a)  â‰¤ lintegral (f 0) : lintegral_mono (assume a, infi_le _ _)
          ... < âŠ¤ : h_fin  )
    (ae_of_all _ $ assume a, infi_le _ _)).symm
  ... = âˆ«â» a, â¨†n, f 0 a - f n a : congr rfl (funext (assume a, ennreal.sub_infi))
  ... = â¨†n, âˆ«â» a, f 0 a - f n a :
    lintegral_supr_ae
      (assume n, (h_meas 0).ennreal_sub (h_meas n))
      (assume n, by
        filter_upwards [h_mono n] assume a ha, ennreal.sub_le_sub (le_refl _) ha)
  ... = â¨†n, lintegral (f 0) - âˆ«â» a, f n a :
    have h_mono : âˆ€â‚˜ a, âˆ€n:â„•, f n.succ a â‰¤ f n a := ae_all_iff.2 h_mono,
    have h_mono : âˆ€n, âˆ€â‚˜a, f n a â‰¤ f 0 a := assume n,
    begin
      filter_upwards [h_mono], simp only [mem_set_of_eq], assume a, assume h, induction n with n ih,
      {exact le_refl _}, {exact le_trans (h n) ih}
    end,
    congr rfl (funext $ assume n, lintegral_sub (h_meas _) (h_meas _)
      (calc
        (âˆ«â» a, f n a) â‰¤ âˆ«â» a, f 0 a : lintegral_le_lintegral_ae $ h_mono n
        ... < âŠ¤ : h_fin)
        (h_mono n))
  ... = lintegral (f 0) - (â¨…n, âˆ«â» a, f n a) : ennreal.sub_infi.symm

/-- Monotone convergence theorem for nonincreasing sequences of functions -/
lemma lintegral_infi
  {f : â„• â†’ Î± â†’ ennreal} (h_meas : âˆ€n, measurable (f n))
  (h_mono : âˆ€ â¦ƒm nâ¦„, m â‰¤ n â†’ f n â‰¤ f m) (h_fin : lintegral (f 0) < âŠ¤) :
  (âˆ«â» a, â¨…n, f n a) = (â¨…n, âˆ«â» a, f n a) :=
lintegral_infi_ae h_meas (Î» n, ae_of_all _ $ h_mono $ le_of_lt n.lt_succ_self) h_fin

section priority
-- for some reason the next proof fails without changing the priority of this instance
local attribute [instance, priority 1000] classical.prop_decidable
/-- Known as Fatou's lemma -/
lemma lintegral_liminf_le {f : â„• â†’ Î± â†’ ennreal} (h_meas : âˆ€n, measurable (f n)) :
  (âˆ«â» a, liminf at_top (Î» n, f n a)) â‰¤ liminf at_top (Î» n, lintegral (f n)) :=
calc
  (âˆ«â» a, liminf at_top (Î» n, f n a)) = âˆ«â» a, â¨†n:â„•, â¨…iâ‰¥n, f i a :
     by simp only [liminf_eq_supr_infi_of_nat]
  ... = â¨†n:â„•, âˆ«â» a, â¨…iâ‰¥n, f i a :
    lintegral_supr
      (assume n, measurable_binfi _ h_meas)
      (assume n m hnm a, infi_le_infi_of_subset $ Î» i hi, le_trans hnm hi)
  ... â‰¤ â¨†n:â„•, â¨…iâ‰¥n, lintegral (f i) :
    supr_le_supr $ Î» n, le_infi2_lintegral _
  ... = liminf at_top (Î» n, lintegral (f n)) : liminf_eq_supr_infi_of_nat.symm
end priority

lemma limsup_lintegral_le {f : â„• â†’ Î± â†’ ennreal} {g : Î± â†’ ennreal}
  (hf_meas : âˆ€ n, measurable (f n)) (h_bound : âˆ€n, âˆ€â‚˜a, f n a â‰¤ g a) (h_fin : lintegral g < âŠ¤) :
  limsup at_top (Î»n, lintegral (f n)) â‰¤ âˆ«â» a, limsup at_top (Î»n, f n a) :=
calc
  limsup at_top (Î»n, lintegral (f n)) = â¨…n:â„•, â¨†iâ‰¥n, lintegral (f i) :
    limsup_eq_infi_supr_of_nat
  ... â‰¤ â¨…n:â„•, âˆ«â» a, â¨†iâ‰¥n, f i a :
    infi_le_infi $ assume n, supr2_lintegral_le _
  ... = âˆ«â» a, â¨…n:â„•, â¨†iâ‰¥n, f i a :
    begin
      refine (lintegral_infi _ _ _).symm,
      { assume n, exact measurable_bsupr _ hf_meas },
      { assume n m hnm a, exact (supr_le_supr_of_subset $ Î» i hi, le_trans hnm hi) },
      { refine lt_of_le_of_lt (lintegral_le_lintegral_ae _) h_fin,
        refine (ae_all_iff.2 h_bound).mono (Î» n hn, _),
        exact supr_le (Î» i, supr_le $ Î» hi, hn i) }
    end
  ... = âˆ«â» a, limsup at_top (Î»n, f n a) :
    by simp only [limsup_eq_infi_supr_of_nat]

/-- Dominated convergence theorem for nonnegative functions -/
lemma tendsto_lintegral_of_dominated_convergence
  {F : â„• â†’ Î± â†’ ennreal} {f : Î± â†’ ennreal} (bound : Î± â†’ ennreal)
  (hF_meas : âˆ€n, measurable (F n)) (h_bound : âˆ€n, âˆ€â‚˜ a, F n a â‰¤ bound a)
  (h_fin : lintegral bound < âŠ¤)
  (h_lim : âˆ€â‚˜ a, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  tendsto (Î»n, lintegral (F n)) at_top (ğ“ (lintegral f)) :=
begin
  have limsup_le_lintegral :=
  calc
    limsup at_top (Î» (n : â„•), lintegral (F n)) â‰¤ âˆ«â» (a : Î±), limsup at_top (Î»n, F n a) :
      limsup_lintegral_le hF_meas h_bound h_fin
    ... = lintegral f :
      lintegral_congr_ae $
          by filter_upwards [h_lim] assume a h, limsup_eq_of_tendsto at_top_ne_bot h,
  have lintegral_le_liminf :=
  calc
    lintegral f = âˆ«â» (a : Î±), liminf at_top (Î» (n : â„•), F n a) :
      lintegral_congr_ae $
      by filter_upwards [h_lim] assume a h, (liminf_eq_of_tendsto at_top_ne_bot h).symm
    ... â‰¤ liminf at_top (Î» n, lintegral (F n)) :
      lintegral_liminf_le hF_meas,
  have liminf_eq_limsup :=
    le_antisymm
      (liminf_le_limsup (map_ne_bot at_top_ne_bot))
      (le_trans limsup_le_lintegral lintegral_le_liminf),
  have liminf_eq_lintegral : liminf at_top (Î» n, lintegral (F n)) = lintegral f :=
    le_antisymm (by convert limsup_le_lintegral) lintegral_le_liminf,
  have limsup_eq_lintegral : limsup at_top (Î» n, lintegral (F n)) = lintegral f :=
    le_antisymm
      limsup_le_lintegral
      begin convert lintegral_le_liminf, exact liminf_eq_limsup.symm end,
  exact tendsto_of_liminf_eq_limsup âŸ¨liminf_eq_lintegral, limsup_eq_lintegralâŸ©
end

/-- Dominated convergence theorem for filters with a countable basis -/
lemma tendsto_lintegral_filter_of_dominated_convergence {Î¹} {l : filter Î¹}
  {F : Î¹ â†’ Î± â†’ ennreal} {f : Î± â†’ ennreal} (bound : Î± â†’ ennreal)
  (hl_cb : l.is_countably_generated)
  (hF_meas : âˆ€á¶  n in l, measurable (F n))
  (h_bound : âˆ€á¶  n in l, âˆ€â‚˜ a, F n a â‰¤ bound a)
  (h_fin : lintegral bound < âŠ¤)
  (h_lim : âˆ€â‚˜ a, tendsto (Î» n, F n a) l (nhds (f a))) :
  tendsto (Î»n, lintegral (F n)) l (nhds (lintegral f)) :=
begin
  rw hl_cb.tendsto_iff_seq_tendsto,
  { intros x xl,
    have hxl, { rw tendsto_at_top' at xl, exact xl },
    have h := inter_mem_sets hF_meas h_bound,
    replace h := hxl _ h,
    rcases h with âŸ¨k, hâŸ©,
    rw â† tendsto_add_at_top_iff_nat k,
    refine tendsto_lintegral_of_dominated_convergence _ _ _ _ _,
    { exact bound },
    { intro, refine (h _ _).1, exact nat.le_add_left _ _ },
    { intro, refine (h _ _).2, exact nat.le_add_left _ _ },
    { assumption },
    { filter_upwards [h_lim],
      simp only [mem_set_of_eq],
      assume a h_lim,
      apply @tendsto.comp _ _ _ (Î»n, x (n + k)) (Î»n, F n a),
      { assumption },
      rw tendsto_add_at_top_iff_nat,
      assumption } },
end

section
open encodable

/-- Monotone convergence for a suprema over a directed family and indexed by an encodable type -/
theorem lintegral_supr_directed [encodable Î²] {f : Î² â†’ Î± â†’ ennreal}
  (hf : âˆ€b, measurable (f b)) (h_directed : directed (â‰¤) f) :
  (âˆ«â» a, â¨†b, f b a) = (â¨†b, âˆ«â» a, f b a) :=
begin
  by_cases hÎ² : Â¬ nonempty Î²,
  { have : âˆ€f : Î² â†’ ennreal, (â¨†(b : Î²), f b) = 0 :=
      assume f, supr_eq_bot.2 (assume b, (hÎ² âŸ¨bâŸ©).elim),
    simp [this] },
  cases of_not_not hÎ² with b,
  haveI iÎ² : inhabited Î² := âŸ¨bâŸ©, clear hÎ² b,
  have : âˆ€a, (â¨† b, f b a) = (â¨† n, f (h_directed.sequence f n) a),
  { assume a,
    refine le_antisymm (supr_le $ assume b, _) (supr_le $ assume n, le_supr (Î»n, f n a) _),
    exact le_supr_of_le (encode b + 1) (h_directed.le_sequence b a) },
  calc (âˆ«â» a, â¨† b, f b a) = (âˆ«â» a, â¨† n, f (h_directed.sequence f n) a) :
      by simp only [this]
    ... = (â¨† n, âˆ«â» a, f (h_directed.sequence f n) a) :
      lintegral_supr (assume n, hf _) h_directed.sequence_mono
    ... = (â¨† b, âˆ«â» a, f b a) :
    begin
      refine le_antisymm (supr_le $ assume n, _) (supr_le $ assume b, _),
      { exact le_supr (Î»b, lintegral (f b)) _ },
      { exact le_supr_of_le (encode b + 1)
          (lintegral_mono $ h_directed.le_sequence b) }
    end
end

end

lemma lintegral_tsum [encodable Î²] {f : Î² â†’ Î± â†’ ennreal} (hf : âˆ€i, measurable (f i)) :
  (âˆ«â» a, âˆ‘' i, f i a) = (âˆ‘' i, âˆ«â» a, f i a) :=
begin
  simp only [ennreal.tsum_eq_supr_sum],
  rw [lintegral_supr_directed],
  { simp [lintegral_finset_sum _ hf] },
  { assume b, exact finset.measurable_sum _ hf },
  { assume s t,
    use [s âˆª t],
    split,
    exact assume a, finset.sum_le_sum_of_subset (finset.subset_union_left _ _),
    exact assume a, finset.sum_le_sum_of_subset (finset.subset_union_right _ _) }
end

end lintegral

namespace measure

def integral [measurable_space Î±] (m : measure Î±) (f : Î± â†’ ennreal) : ennreal :=
@lintegral Î± { volume := m } f

variables [measurable_space Î±] {m : measure Î±}

@[simp] lemma integral_zero : m.integral (Î»a, 0) = 0 := @lintegral_zero Î± { volume := m }

lemma integral_map [measurable_space Î²] {f : Î² â†’ ennreal} {g : Î± â†’ Î²}
  (hf : measurable f) (hg : measurable g) : (map g m).integral f = m.integral (f âˆ˜ g) :=
begin
  rw [integral, integral, lintegral_eq_supr_eapprox_integral, lintegral_eq_supr_eapprox_integral],
  { congr, funext n, symmetry,
    apply simple_func.integral_map,
    { assume a, exact congr_fun (simple_func.eapprox_comp hf hg) a },
    { assume s hs, exact map_apply hg hs } },
  exact hf.comp hg,
  assumption
end

lemma integral_dirac (a : Î±) {f : Î± â†’ ennreal} (hf : measurable f) : (dirac a).integral f = f a :=
have âˆ€f:Î± â†’â‚› ennreal, @simple_func.integral Î± {volume := dirac a} f = f a,
begin
  assume f,
  have : âˆ€r, @volume Î± { volume := dirac a } (â‡‘f â»Â¹' {r}) = â¨† h : f a = r, 1,
  { assume r,
    transitivity,
    apply dirac_apply,
    apply simple_func.measurable_sn,
    refine supr_congr_Prop _ _; simp },
  transitivity,
  apply finset.sum_eq_single (f a),
  { assume b hb h, simp [this, ne.symm h], },
  { assume h, simp at h, exact (h a rfl).elim },
  { rw [this], simp }
end,
begin
  rw [integral, lintegral_eq_supr_eapprox_integral],
  { simp [this, simple_func.supr_eapprox_apply f hf] },
  assumption
end

def with_density (m : measure Î±) (f : Î± â†’ ennreal) : measure Î± :=
if hf : measurable f then
  measure.of_measurable (Î»s hs, m.integral (Î»a, â¨†(h : a âˆˆ s), f a))
    (by simp)
    begin
      assume s hs hd,
      have : âˆ€a, (â¨† (h : a âˆˆ â‹ƒi, s i), f a) = (âˆ‘'i, (â¨† (h : a âˆˆ s i), f a)),
      { assume a,
        by_cases ha : âˆƒj, a âˆˆ s j,
        { rcases ha with âŸ¨j, hajâŸ©,
          have : âˆ€i, a âˆˆ s i â†” j = i := assume i,
            iff.intro
              (assume hai, by_contradiction $ assume hij, hd j i hij âŸ¨haj, haiâŸ©)
              (by rintros rfl; assumption),
          simp [this, ennreal.tsum_supr_eq] },
        { have : âˆ€i, Â¬ a âˆˆ s i, { simpa using ha },
          simp [this] } },
      simp only [this],
      apply lintegral_tsum,
      { assume i,
        simp [supr_eq_if],
        exact measurable.if (hs i) hf measurable_const }
    end
else 0

lemma with_density_apply {m : measure Î±} {f : Î± â†’ ennreal} {s : set Î±}
  (hf : measurable f) (hs : is_measurable s) :
  m.with_density f s = m.integral (Î»a, â¨†(h : a âˆˆ s), f a) :=
by rw [with_density, dif_pos hf]; exact measure.of_measurable_apply s hs

end measure

end measure_theory
