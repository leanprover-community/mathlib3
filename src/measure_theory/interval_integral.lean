import measure_theory.set_integral
import measure_theory.lebesgue_measure
import analysis.calculus.deriv

noncomputable theory
open topological_space (second_countable_topology)
open measure_theory set classical filter

open_locale classical topological_space filter

variables {Î± Î² ğ•œ E F : Type*} [decidable_linear_order Î±] [measurable_space Î±] [normed_group E]

/-- A function `f` is called *interval integrable* with respect to a measure `Î¼` on an unordered
interval `[a..b]` if it is integrable on both intervals `(a, b]` and `(b, a]`. One of these
intervals is always empty, so this property is equivalent to `f` being integrable on
`(min a b, max a b]`. -/
def interval_integrable (f : Î± â†’ E) (Î¼ : measure Î±) (a b : Î±) :=
integrable_on f (Ioc a b) Î¼ âˆ§ integrable_on f (Ioc b a) Î¼

namespace interval_integrable

section

variables {f : Î± â†’ E} {a b c : Î±} {Î¼ : measure Î±}

@[symm] lemma symm (h : interval_integrable f Î¼ a b) : interval_integrable f Î¼ b a :=
h.symm

@[refl] lemma refl : interval_integrable f Î¼ a a :=
by split; simp

@[trans] lemma trans  (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  interval_integrable f Î¼ a c :=
âŸ¨(hab.1.union hbc.1).mono_set Ioc_subset_Ioc_union_Ioc,
  (hbc.2.union hab.2).mono_set Ioc_subset_Ioc_union_IocâŸ©

lemma neg (h : interval_integrable f Î¼ a b) : interval_integrable (-f) Î¼ a b :=
âŸ¨h.1.neg, h.2.negâŸ©

end

lemma smul [normed_field ğ•œ] [normed_space ğ•œ E] {f : Î± â†’ E} {a b : Î±} {Î¼ : measure Î±}
  (h : interval_integrable f Î¼ a b) (r : ğ•œ) :
  interval_integrable (r â€¢ f) Î¼ a b :=
âŸ¨h.1.smul r, h.2.smul râŸ©

variables [measurable_space E] [opens_measurable_space E] {f g : Î± â†’ E} {a b : Î±} {Î¼ : measure Î±}

lemma add (hfm : measurable f) (hfi : interval_integrable f Î¼ a b)
  (hgm : measurable g) (hgi : interval_integrable g Î¼ a b) :
  interval_integrable (f + g) Î¼ a b :=
âŸ¨hfi.1.add hfm hgm hgi.1, hfi.2.add hfm hgm hgi.2âŸ©

lemma sub (hfm : measurable f) (hfi : interval_integrable f Î¼ a b)
  (hgm : measurable g) (hgi : interval_integrable g Î¼ a b) :
  interval_integrable (f - g) Î¼ a b :=
âŸ¨hfi.1.sub hfm hgm hgi.1, hfi.2.sub hfm hgm hgi.2âŸ©

end interval_integrable

variables [second_countable_topology E] [complete_space E] [normed_space â„ E]
  [measurable_space E] [borel_space E]

/-- The interval integral `âˆ« x in a..b, f x âˆ‚Î¼` is defined
as `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`. If `a â‰¤ b`, then it equals
`âˆ« x in Ioc a b, f x âˆ‚Î¼`, otherwise it equals `-âˆ« x in Ioc b a, f x âˆ‚Î¼`. -/
def interval_integral (f : Î± â†’ E) (a b : Î±) (Î¼ : measure Î±) :=
âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼

notation `âˆ«` binders ` in ` a `..` b `, ` r:(scoped:60 f, f) ` âˆ‚` Î¼:70 := interval_integral r a b Î¼
notation `âˆ«` binders ` in ` a `..` b `, ` r:(scoped:60 f, interval_integral f a b volume) := r

namespace interval_integral

variables {a b c : Î±} {f g : Î± â†’ E} {Î¼ : measure Î±}

lemma integral_of_le (h : a â‰¤ b) : âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in Ioc a b, f x âˆ‚Î¼ :=
by simp [interval_integral, h]

@[simp] lemma integral_same : âˆ« x in a..a, f x âˆ‚Î¼ = 0 :=
sub_self _

lemma integral_symm (a b) : âˆ« x in b..a, f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, neg_sub]

lemma integral_of_ge (h : b â‰¤ a) : âˆ« x in a..b, f x âˆ‚Î¼ = -âˆ« x in Ioc b a, f x âˆ‚Î¼ :=
by simp only [integral_symm b, integral_of_le h]

lemma integral_add (hfm : measurable f) (hfi : interval_integrable f Î¼ a b)
  (hgm : measurable g) (hgi : interval_integrable g Î¼ a b) :
  âˆ« x in a..b, f x + g x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in a..b, g x âˆ‚Î¼ :=
begin
  simp only [interval_integral, integral_add hfm hfi.1 hgm hgi.1,
    integral_add hfm hfi.2 hgm hgi.2],
  abel
end

@[simp] lemma integral_neg : âˆ« x in a..b, -f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ :=
begin
  simp only [interval_integral, integral_neg],
  abel
end

variables [topological_space Î±] [opens_measurable_space Î±]

section order_closed_topology

variables [order_closed_topology Î±]

lemma integral_cocycle (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ + âˆ« x in c..a, f x âˆ‚Î¼ = 0 :=
begin
  have hac := hab.trans hbc,
  simp only [interval_integral, â† add_sub_comm, sub_eq_zero],
  iterate 4 { rw â† integral_union },
  { suffices : Ioc a b âˆª Ioc b c âˆª Ioc c a = Ioc b a âˆª Ioc c b âˆª Ioc a c, by rw this,
    rw [Ioc_union_Ioc_union_Ioc_cycle, union_right_comm, Ioc_union_Ioc_union_Ioc_cycle,
      min_left_comm, max_left_comm] },
  all_goals { simp [*, is_measurable.union, is_measurable_Ioc, Ioc_disjoint_Ioc_same,
    Ioc_disjoint_Ioc_same.symm, hab.1, hab.2, hbc.1, hbc.2, hac.1, hac.2] }
end

lemma integral_add_adjacent_intervals (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼ :=
by rw [â† add_neg_eq_zero, â† integral_symm, integral_cocycle hfm hab hbc]

lemma integral_cases (f : Î± â†’ E) (a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ âˆˆ ({âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼,
    -âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼} : set E) :=
(le_total a b).imp (Î» h, by simp [h, integral_of_le]) (Î» h, by simp [h, integral_of_ge])

lemma norm_integral_eq_norm_integral_Ioc :
  âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ = âˆ¥âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼âˆ¥ :=
(integral_cases f a b).elim (congr_arg _) (Î» h, (congr_arg _ h).trans (norm_neg _))

lemma norm_integral_le_integral_norm_Ioc :
  âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ â‰¤ âˆ« x in Ioc (min a b) (max a b), âˆ¥f xâˆ¥ âˆ‚Î¼ :=
calc âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ = âˆ¥âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼âˆ¥ :
  norm_integral_eq_norm_integral_Ioc
... â‰¤ âˆ« x in Ioc (min a b) (max a b), âˆ¥f xâˆ¥ âˆ‚Î¼ :
  norm_integral_le_integral_norm f

lemma norm_integral_le_abs_integral_norm : âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ â‰¤ abs (âˆ« x in a..b, âˆ¥f xâˆ¥ âˆ‚Î¼) :=
begin
  simp only [â† real.norm_eq_abs, norm_integral_eq_norm_integral_Ioc],
  exact le_trans (norm_integral_le_integral_norm _) (le_abs_self _)
end

lemma norm_integral_le_of_norm_le_const_ae {a b C : â„} {f : â„ â†’ E}
  (h : âˆ€áµ x, x âˆˆ Ioc (min a b) (max a b) â†’ âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in a..b, f xâˆ¥ â‰¤ C * abs (b - a) :=
begin
  rw [norm_integral_eq_norm_integral_Ioc],
  have : volume (Ioc (min a b) (max a b)) = ennreal.of_real (abs (b - a)),
  { rw [real.volume_Ioc, max_sub_min_eq_abs, ennreal.of_real] },
  rw [â† ennreal.to_real_of_real (abs_nonneg _), â† this],
  refine norm_set_integral_le_of_norm_le_const_ae'' _ is_measurable_Ioc h,
  simp only [this, ennreal.lt_top_iff_ne_top],
  exact ennreal.of_real_ne_top
end

lemma norm_integral_le_of_norm_le_const {a b C : â„} {f : â„ â†’ E}
  (h : âˆ€ x âˆˆ Ioc (min a b) (max a b), âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in a..b, f xâˆ¥ â‰¤ C * abs (b - a) :=
norm_integral_le_of_norm_le_const_ae $ eventually_of_forall h

end order_closed_topology

variables [order_topology Î±]

open asymptotics

lemma integral_sub_linear_is_o_of_tendsto_ae [locally_finite_measure Î¼] {f : Î± â†’ E} {a : Î±} {c : E}
  (hfm : measurable f) (ha : tendsto f (ğ“ a âŠ“ Î¼.ae) (ğ“ c)) :
  is_o (Î» b, âˆ« x in a..b, f x âˆ‚Î¼ - ((Î¼ (Ioc a b)).to_real - (Î¼ (Ioc b a)).to_real) â€¢ c)
    (Î» b, (Î¼ (Ioc (min a b) (max a b))).to_real) (ğ“ a) :=
begin
  have A : is_o (Î» b, âˆ« x in Ioc a b, f x âˆ‚Î¼ - (Î¼ $ Ioc a b).to_real â€¢ c)
    (Î» b, (Î¼ $ Ioc a b).to_real) (ğ“ a),
  { refine (ha.integral_sub_linear_is_o_ae hfm (Î¼.finite_at_nhds _)).comp_tendsto _,
    exact tendsto_const_nhds.Ioc tendsto_id },
  have B : is_o (Î» b, âˆ« x in Ioc b a, f x âˆ‚Î¼ - (Î¼ $ Ioc b a).to_real â€¢ c)
    (Î» b, (Î¼ $ Ioc b a).to_real) (ğ“ a),
  { refine (ha.integral_sub_linear_is_o_ae hfm (Î¼.finite_at_nhds _)).comp_tendsto _,
    exact tendsto_id.Ioc tendsto_const_nhds },
  change is_o _ _ _,
  convert (A.trans_le _).sub (B.trans_le _),
  { ext b,
    simp only [interval_integral, sub_smul],
    abel },
  { intro b,
    cases le_total a b with hab hab; simp [hab] },
  { intro b,
    cases le_total a b with hab hab; simp [hab] }
end

lemma integral_same_has_deriv_at_of_tendsto_ae {f : â„ â†’ E} {a : â„} {c : E} (hfm : measurable f)
  (ha : tendsto f (ğ“ a âŠ“ volume.ae) (ğ“ c)) :
  has_deriv_at (Î» b, âˆ« x in a..b, f x) c a :=
begin
  change is_o _ _ _,
  rw [â† is_o_norm_right],
  convert integral_sub_linear_is_o_of_tendsto_ae hfm ha,
  { ext b,
    dsimp,
    simp only [integral_same, sub_zero, real.volume_Ioc, ennreal.to_real_of_real'],
    congr' 2,
    rw [â† neg_sub b, max_zero_sub_eq_self] },
  { ext b,
    rw [real.volume_Ioc, ennreal.to_real_of_real, max_sub_min_eq_abs, real.norm_eq_abs],
    exact sub_nonneg.2 min_le_max }
end

lemma integral_has_deriv_at_of_tendsto_ae {f : â„ â†’ E} {a b : â„} {c : E} (hfm : measurable f)
  (hfi : interval_integrable f volume a b) (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ c)) :
  has_deriv_at (Î» u, âˆ« x in a..u, f x) c b :=
begin
  refine ((integral_same_has_deriv_at_of_tendsto_ae hfm hb).const_add
    (âˆ« x in a..b, f x)).congr_of_eventually_eq _,
  suffices : âˆ€á¶  u in ğ“ b, interval_integrable f volume b u,
  { refine this.mono (Î» u hu, (integral_add_adjacent_intervals hfm hfi hu).symm) },
  simp only [interval_integrable, eventually_and],
  exact âŸ¨(tendsto_const_nhds.Ioc tendsto_id).eventually
    (hb.integrable_at_filter_ae (volume.finite_at_nhds _).inf_of_left).eventually,
    (tendsto_id.Ioc tendsto_const_nhds).eventually
      (hb.integrable_at_filter_ae (volume.finite_at_nhds _).inf_of_left).eventuallyâŸ©,
end

end interval_integral
