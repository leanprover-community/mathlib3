/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury G. Kudryashov
-/
import measure_theory.set_integral
import measure_theory.lebesgue_measure
import analysis.calculus.deriv

/-!
# Integral over an interval

In this file we define `âˆ« x in a..b, f x âˆ‚Î¼` to be `âˆ« x in Ioc a b, f x âˆ‚Î¼` if `a â‰¤ b`
and `-âˆ« x in Ioc b a, f x âˆ‚Î¼` if `b â‰¤ a`. We prove a few simple properties and the first part of the
[fundamental theorem of calculus](https://en.wikipedia.org/wiki/Fundamental_theorem_of_calculus),
see `integral_has_strict_deriv_at_of_tendsto_ae`.

## Implementation notes

### Avoiding `if`, `min`, and `max`

In order to avoid `if`s in the definition, we define `interval_integrable f Î¼ a b` as
`integrable_on f (Ioc a b) Î¼ âˆ§ integrable_on f (Ioc b a) Î¼`. For any `a`, `b` one of these
intervals is empty and the other coincides with `Ioc (min a b) (max a b)`.

Similarly, we define `âˆ« x in a..b, f x âˆ‚Î¼` to be `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`.
Again, for any `a`, `b` one of these integrals is zero, and the other gives the expected result.

This way some properties can be translated from integrals over sets without dealing with
the cases `a â‰¤ b` and `b â‰¤ a` separately.

### Choice of the interval

We use integral over `Ioc (min a b) (max a b)` instead of one of the other three possible
intervals with the same endpoints for two reasons:

* this way `âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼` holds whenever
  `f` is integrable on each interval; in particular, it works even if the measure `Î¼` has an atom
  at `b`; this rules out `Ioo` and `Icc` intervals;
* with this definition for a probability measure `Î¼`, the integral `âˆ« x in a..b, 1 âˆ‚Î¼` equals
  the difference $F_Î¼(b)-F_Î¼(a)$, where $F_Î¼(a)=Î¼(-âˆ, a]$ is the
  [cumulative distribution function](https://en.wikipedia.org/wiki/Cumulative_distribution_function)
  of `Î¼`.
-/

noncomputable theory
open topological_space (second_countable_topology)
open measure_theory set classical filter

open_locale classical topological_space filter

variables {Î± Î² ğ•œ E F : Type*} [decidable_linear_order Î±] [measurable_space Î±] [normed_group E]

/-!
### Integrability at an interval
-/

/-- A function `f` is called *interval integrable* with respect to a measure `Î¼` on an unordered
interval `a..b` if it is integrable on both intervals `(a, b]` and `(b, a]`. One of these
intervals is always empty, so this property is equivalent to `f` being integrable on
`(min a b, max a b]`. -/
def interval_integrable (f : Î± â†’ E) (Î¼ : measure Î±) (a b : Î±) :=
integrable_on f (Ioc a b) Î¼ âˆ§ integrable_on f (Ioc b a) Î¼

namespace interval_integrable

section

variables {f : Î± â†’ E} {a b c : Î±} {Î¼ : measure Î±}

@[symm] lemma symm (h : interval_integrable f Î¼ a b) : interval_integrable f Î¼ b a :=
h.symm

@[refl] lemma refl : interval_integrable f Î¼ a a :=
by split; simp

@[trans] lemma trans  (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  interval_integrable f Î¼ a c :=
âŸ¨(hab.1.union hbc.1).mono_set Ioc_subset_Ioc_union_Ioc,
  (hbc.2.union hab.2).mono_set Ioc_subset_Ioc_union_IocâŸ©

lemma neg (h : interval_integrable f Î¼ a b) : interval_integrable (-f) Î¼ a b :=
âŸ¨h.1.neg, h.2.negâŸ©

end

lemma smul [normed_field ğ•œ] [normed_space ğ•œ E] {f : Î± â†’ E} {a b : Î±} {Î¼ : measure Î±}
  (h : interval_integrable f Î¼ a b) (r : ğ•œ) :
  interval_integrable (r â€¢ f) Î¼ a b :=
âŸ¨h.1.smul r, h.2.smul râŸ©

variables [measurable_space E] [opens_measurable_space E] {f g : Î± â†’ E} {a b : Î±} {Î¼ : measure Î±}

lemma add (hfm : measurable f) (hfi : interval_integrable f Î¼ a b)
  (hgm : measurable g) (hgi : interval_integrable g Î¼ a b) :
  interval_integrable (f + g) Î¼ a b :=
âŸ¨hfi.1.add hfm hgm hgi.1, hfi.2.add hfm hgm hgi.2âŸ©

lemma sub (hfm : measurable f) (hfi : interval_integrable f Î¼ a b)
  (hgm : measurable g) (hgi : interval_integrable g Î¼ a b) :
  interval_integrable (f - g) Î¼ a b :=
âŸ¨hfi.1.sub hfm hgm hgi.1, hfi.2.sub hfm hgm hgi.2âŸ©

end interval_integrable

-- TODO: rewrite docstring
/-- If `f : Î± â†’ E` has a finite limit at `l âŠ“ Î¼.ae`, where `l` is a measurably generated interval
generated filter and `Î¼` is a measure finite at this filter, then `f` is interval integrable
with respect to `Î¼` on `u..v` as both `u` and `v` tend to `l`. -/
lemma filter.tendsto.eventually_interval_integrable_ae {f : Î± â†’ E} {Î¼ : measure Î±}
  {l l' : filter Î±} [tendsto_Ixx_class Ioc l l'] [is_measurably_generated l']
  (hÎ¼ : Î¼.finite_at_filter l') {c : E} (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c))
  {u v : Î² â†’ Î±} {lb : filter Î²} (hu : tendsto u lb l) (hv : tendsto v lb l) :
  âˆ€á¶  t in lb, interval_integrable f Î¼ (u t) (v t) :=
have _ := (hf.integrable_at_filter_ae hÎ¼).eventually,
((hu.Ioc hv).eventually this).and $ (hv.Ioc hu).eventually this

-- TODO: rewrite docstring
/-- If `f : Î± â†’ E` has a finite limit at a measurably generated interval generated filter `l`
and `Î¼` is a measure finite at this filter, then `f` is interval integrable with respect
to `Î¼` on `u..v` as both `u` and `v` tend to `l`. -/
lemma filter.tendsto.eventually_interval_integrable {f : Î± â†’ E} {Î¼ : measure Î±}
  {l l' : filter Î±} [tendsto_Ixx_class Ioc l l'] [is_measurably_generated l']
  (hÎ¼ : Î¼.finite_at_filter l') {c : E} (hf : tendsto f l' (ğ“ c))
  {u v : Î² â†’ Î±} {lb : filter Î²} (hu : tendsto u lb l) (hv : tendsto v lb l) :
  âˆ€á¶  t in lb, interval_integrable f Î¼ (u t) (v t) :=
(tendsto_le_left (inf_le_left : l' âŠ“ Î¼.ae â‰¤ l') hf).eventually_interval_integrable_ae hÎ¼ hu hv

/-!
### Interval integral: definition and basic properties
-/

variables [second_countable_topology E] [complete_space E] [normed_space â„ E]
  [measurable_space E] [borel_space E]

/-- The interval integral `âˆ« x in a..b, f x âˆ‚Î¼` is defined
as `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`. If `a â‰¤ b`, then it equals
`âˆ« x in Ioc a b, f x âˆ‚Î¼`, otherwise it equals `-âˆ« x in Ioc b a, f x âˆ‚Î¼`. -/
def interval_integral (f : Î± â†’ E) (a b : Î±) (Î¼ : measure Î±) :=
âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼

notation `âˆ«` binders ` in ` a `..` b `, ` r:(scoped:60 f, f) ` âˆ‚` Î¼:70 := interval_integral r a b Î¼
notation `âˆ«` binders ` in ` a `..` b `, ` r:(scoped:60 f, interval_integral f a b volume) := r

namespace interval_integral

section

variables {a b c d : Î±} {f g : Î± â†’ E} {Î¼ : measure Î±}

lemma integral_of_le (h : a â‰¤ b) : âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in Ioc a b, f x âˆ‚Î¼ :=
by simp [interval_integral, h]

@[simp] lemma integral_same : âˆ« x in a..a, f x âˆ‚Î¼ = 0 :=
sub_self _

lemma integral_symm (a b) : âˆ« x in b..a, f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, neg_sub]

lemma integral_of_ge (h : b â‰¤ a) : âˆ« x in a..b, f x âˆ‚Î¼ = -âˆ« x in Ioc b a, f x âˆ‚Î¼ :=
by simp only [integral_symm b, integral_of_le h]

lemma integral_cases (f : Î± â†’ E) (a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ âˆˆ ({âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼,
    -âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼} : set E) :=
(le_total a b).imp (Î» h, by simp [h, integral_of_le]) (Î» h, by simp [h, integral_of_ge])

lemma norm_integral_eq_norm_integral_Ioc :
  âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ = âˆ¥âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼âˆ¥ :=
(integral_cases f a b).elim (congr_arg _) (Î» h, (congr_arg _ h).trans (norm_neg _))

lemma norm_integral_le_integral_norm_Ioc :
  âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ â‰¤ âˆ« x in Ioc (min a b) (max a b), âˆ¥f xâˆ¥ âˆ‚Î¼ :=
calc âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ = âˆ¥âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼âˆ¥ :
  norm_integral_eq_norm_integral_Ioc
... â‰¤ âˆ« x in Ioc (min a b) (max a b), âˆ¥f xâˆ¥ âˆ‚Î¼ :
  norm_integral_le_integral_norm f

lemma norm_integral_le_abs_integral_norm : âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ â‰¤ abs (âˆ« x in a..b, âˆ¥f xâˆ¥ âˆ‚Î¼) :=
begin
  simp only [â† real.norm_eq_abs, norm_integral_eq_norm_integral_Ioc],
  exact le_trans (norm_integral_le_integral_norm _) (le_abs_self _)
end

lemma norm_integral_le_of_norm_le_const_ae {a b C : â„} {f : â„ â†’ E}
  (h : âˆ€áµ x, x âˆˆ Ioc (min a b) (max a b) â†’ âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in a..b, f xâˆ¥ â‰¤ C * abs (b - a) :=
begin
  rw [norm_integral_eq_norm_integral_Ioc],
  convert norm_set_integral_le_of_norm_le_const_ae'' _ is_measurable_Ioc h,
  { rw [real.volume_Ioc, max_sub_min_eq_abs, ennreal.to_real_of_real (abs_nonneg _)] },
  { simp only [real.volume_Ioc, ennreal.of_real_lt_top] },
end

lemma norm_integral_le_of_norm_le_const {a b C : â„} {f : â„ â†’ E}
  (h : âˆ€ x âˆˆ Ioc (min a b) (max a b), âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in a..b, f xâˆ¥ â‰¤ C * abs (b - a) :=
norm_integral_le_of_norm_le_const_ae $ eventually_of_forall h

lemma integral_add (hfm : measurable f) (hfi : interval_integrable f Î¼ a b)
  (hgm : measurable g) (hgi : interval_integrable g Î¼ a b) :
  âˆ« x in a..b, f x + g x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in a..b, g x âˆ‚Î¼ :=
begin
  simp only [interval_integral, integral_add hfm hfi.1 hgm hgi.1,
    integral_add hfm hfi.2 hgm hgi.2],
  abel
end

@[simp] lemma integral_neg : âˆ« x in a..b, -f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ :=
begin
  simp only [interval_integral, integral_neg],
  abel
end

lemma integral_sub (hfm : measurable f) (hfi : interval_integrable f Î¼ a b)
  (hgm : measurable g) (hgi : interval_integrable g Î¼ a b) :
  âˆ« x in a..b, f x - g x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in a..b, g x âˆ‚Î¼ :=
(integral_add hfm hfi hgm.neg hgi.neg).trans $ congr_arg _ integral_neg

lemma integral_smul (r : â„) : âˆ« x in a..b, r â€¢ f x âˆ‚Î¼ = r â€¢ âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, integral_smul, smul_sub]

lemma integral_const' (c : E) :
  âˆ« x in a..b, c âˆ‚Î¼ = ((Î¼ $ Ioc a b).to_real - (Î¼ $ Ioc b a).to_real) â€¢ c :=
by simp only [interval_integral, set_integral_const, sub_smul]

lemma integral_const {a b : â„} (c : E) : (âˆ« (x : â„) in a..b, c) = (b - a) â€¢ c :=
by simp only [integral_const', real.volume_Ioc, ennreal.to_real_of_real', â† neg_sub b,
  max_zero_sub_eq_self]

/-!
### Additivity in intervals

In this section we prove that `âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼`
as well as a few other identities trivially equivalent to this one.
-/

variables [topological_space Î±] [opens_measurable_space Î±]

section order_closed_topology

variables [order_closed_topology Î±]

lemma integral_add_adjacent_intervals_cancel (hfm : measurable f)
  (hab : interval_integrable f Î¼ a b) (hbc : interval_integrable f Î¼ b c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ + âˆ« x in c..a, f x âˆ‚Î¼ = 0 :=
begin
  have hac := hab.trans hbc,
  simp only [interval_integral, â† add_sub_comm, sub_eq_zero],
  iterate 4 { rw â† integral_union },
  { suffices : Ioc a b âˆª Ioc b c âˆª Ioc c a = Ioc b a âˆª Ioc c b âˆª Ioc a c, by rw this,
    rw [Ioc_union_Ioc_union_Ioc_cycle, union_right_comm, Ioc_union_Ioc_union_Ioc_cycle,
      min_left_comm, max_left_comm] },
  all_goals { simp [*, is_measurable.union, is_measurable_Ioc, Ioc_disjoint_Ioc_same,
    Ioc_disjoint_Ioc_same.symm, hab.1, hab.2, hbc.1, hbc.2, hac.1, hac.2] }
end

lemma integral_add_adjacent_intervals (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼ :=
by rw [â† add_neg_eq_zero, â† integral_symm, integral_add_adjacent_intervals_cancel hfm hab hbc]

lemma integral_interval_sub_left (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in a..c, f x âˆ‚Î¼ = âˆ« x in c..b, f x âˆ‚Î¼ :=
sub_eq_of_eq_add' $ eq.symm $ integral_add_adjacent_intervals hfm hac (hac.symm.trans hab)

lemma integral_interval_add_interval_comm (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in a..d, f x âˆ‚Î¼ + âˆ« x in c..b, f x âˆ‚Î¼ :=
by rw [â† integral_add_adjacent_intervals hfm hac hcd, add_assoc, add_left_comm,
  integral_add_adjacent_intervals hfm hac (hac.symm.trans hab), add_comm]

lemma integral_interval_sub_interval_comm (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼ - âˆ« x in b..d, f x âˆ‚Î¼ :=
by simp only [sub_eq_add_neg, â† integral_symm,
  integral_interval_add_interval_comm hfm hab hcd.symm (hac.trans hcd)]

lemma integral_interval_sub_interval_comm' (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in d..b, f x âˆ‚Î¼ - âˆ« x in c..a, f x âˆ‚Î¼ :=
by rw [integral_interval_sub_interval_comm hfm hab hcd hac, integral_symm b d, integral_symm a c,
  sub_neg_eq_add, sub_eq_neg_add]

lemma integral_Iic_sub_Iic (hfm : measurable f) (ha : integrable_on f (Iic a) Î¼)
  (hb : integrable_on f (Iic b) Î¼) :
  âˆ« x in Iic b, f x âˆ‚Î¼ - âˆ« x in Iic a, f x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ :=
begin
  wlog hab : a â‰¤ b using [a b] tactic.skip,
  { rw [sub_eq_iff_eq_add', integral_of_le hab, â† integral_union (Iic_disjoint_Ioc (le_refl _)),
      Iic_union_Ioc_eq_Iic hab],
    exacts [is_measurable_Iic, is_measurable_Ioc, hfm, ha, hb.mono_set (Î» _, and.right)] },
  { intros ha hb,
    rw [integral_symm, â† this hb ha, neg_sub] }
end

/-- If `Î¼` is a finite measure then `âˆ« x in a..b, c âˆ‚Î¼ = (Î¼ (Iic b) - Î¼ (Iic a)) â€¢ c`. -/
lemma integral_const_of_cdf [finite_measure Î¼] (c : E) :
  âˆ« x in a..b, c âˆ‚Î¼ = ((Î¼ (Iic b)).to_real - (Î¼ (Iic a)).to_real) â€¢ c :=
begin
  simp only [sub_smul, â† set_integral_const],
  refine (integral_Iic_sub_Iic measurable_const _ _).symm;
    simp only [integrable_on_const, measure_lt_top, or_true]
end

end order_closed_topology

end

/-!
### Fundamental theorem of calculus, part 1, for any measure

In this section we prove a few lemmas that can be seen as versions of FTC-1 for interval integral
w.r.t. any measure. Many theorems are formulated for any measurably generated interval generated
filter `l` such that `pure b â‰¤ l â‰¤ ğ“ b`. In the most interesting case `Î± = â„`, there are only four
filters with these properties: `pure b`, `ğ“[Ici b] b`, `ğ“[Iic b] b`, and `ğ“ b`. For `pure b` most
of these theorems are trivial; we use `filter` versions to avoid repeating the same arguments for
the other three filters.

The most general theorem `measure_integral_sub_linear_is_o_of_tendsto_ae` says
that `âˆ« x in u t..v t, f x âˆ‚Î¼ = âˆ« x in u t..v t, c âˆ‚Î¼ + o(âˆ« x in u t..v t, 1 âˆ‚Î¼)` provided that both
`u` and `v` tend to a measurably generated interval generated filter `l` (e.g., `ğ“ a`, `ğ“[Ici a] a`,
`ğ“[Iic a] a`, or `at_top`) such that `Î¼` is finite at this filter, and `f x` tends to `c` as `x`
tends to `l âŠ“ Î¼.ae`.

This theorem is formulated with integral of constants instead of measures in the right hand sides
for two reasons: first, this way we avoid `min`/`max` in the statements; second, often it is
possible to write better `simp` lemmas for these integrals, see `integral_const` and
`integral_const_of_cdf`.

We apply this theorem to prove lemma `measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae`
which corresponds to the `has_strict_deriv_at` version of FTC-1. If `f` is a measurable function
integrable on `a..b` and `l`, `pure b â‰¤ l â‰¤ ğ“ b`, is a measurably generated interval generated
filter (e.g., `ğ“ b`, `ğ“[Ici b] b`, or `ğ“[Iic b] b`) such that `Î¼` is finite at `l` and `f x` tends
to `c` as `x` tends to `l âŠ“ Î¼.ae` then
`âˆ« x in a..v, f x âˆ‚Î¼ - âˆ« x in a..u, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, 1 âˆ‚Î¼)`
as `u` and `v` tend to `l`.

-/

namespace FTC

class FTC_filter {Î² : Type*} [linear_order Î²] [measurable_space Î²] [topological_space Î²]
  (a : out_param Î²) (outer : filter Î²) (inner : out_param $ filter Î²)
  extends tendsto_Ixx_class Ioc outer inner : Prop :=
(pure_le : pure a â‰¤ outer)
(le_nhds : inner â‰¤ ğ“ a)
[meas_gen : is_measurably_generated inner]

namespace FTC_filter

variables [linear_order Î²] [measurable_space Î²] [topological_space Î²]

instance pure (a : Î²) : FTC_filter a (pure a) âŠ¥ :=
{ pure_le := le_refl _,
  le_nhds := bot_le }

variables [opens_measurable_space Î²] [order_topology Î²]

instance nhds (a : Î²) : FTC_filter a (ğ“ a) (ğ“ a) :=
{ pure_le := pure_le_nhds a,
  le_nhds := le_refl _ }

instance nhds_left (a : Î²) : FTC_filter a (ğ“[Iic a] a) (ğ“[Iic a] a) :=
{ pure_le := pure_le_nhds_within right_mem_Iic,
  le_nhds := inf_le_left }

instance nhds_right (a : Î²) : FTC_filter a (ğ“[Ici a] a) (ğ“[Ioi a] a) :=
{ pure_le := pure_le_nhds_within left_mem_Ici,
  le_nhds := inf_le_left }

end FTC_filter

open asymptotics

section

variables {f : Î± â†’ E} {c ca cb : E} {l l' la la' lb lb' : filter Î±} {lt : filter Î²}
  {Î¼ : measure Î±} {u v ua va ub vb : Î² â†’ Î±}

/-- Fundamental theorem of calculus-1, local version for any measure.
If `f` has a finite limit `c` at `l âŠ“ Î¼.ae`, where `l` is a measurably generated interval
generated filter (e.g., `ğ“ a`, `ğ“[Ici a] a`, `ğ“[Iic a] a`, or `at_top`) and `Î¼` is a measure
finite at `l`, then `âˆ« x in u..v, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, 1 âˆ‚Î¼)` as both
`u` and `v` tend to `l`.

We use integrals of constants instead of measures because this way it is easier to formulate
a statement that works in both cases `u â‰¤ v` and `v â‰¤ u`. -/
lemma measure_integral_sub_linear_is_o_of_tendsto_ae
  [is_measurably_generated l'] [tendsto_Ixx_class Ioc l l']
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c)) (hl : Î¼.finite_at_filter l')
  (hu : tendsto u lt l) (hv : tendsto v lt l) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ - âˆ« x in u t..v t, c âˆ‚Î¼)
    (Î» t, âˆ« x in u t..v t, (1:â„) âˆ‚Î¼) lt :=
begin
  have A := (hf.integral_sub_linear_is_o_ae hfm hl).comp_tendsto (hu.Ioc hv),
  have B := (hf.integral_sub_linear_is_o_ae hfm hl).comp_tendsto (hv.Ioc hu),
  simp only [integral_const'],
  convert (A.trans_le _).sub (B.trans_le _),
  { ext t,
    simp_rw [(âˆ˜), interval_integral, sub_smul],
    abel },
  all_goals { intro t, cases le_total (u t) (v t) with huv huv; simp [huv] }
end

lemma measure_integral_sub_linear_is_o_of_tendsto_ae_of_le
  [is_measurably_generated l'] [tendsto_Ixx_class Ioc l l']
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c)) (hl : Î¼.finite_at_filter l')
  (hu : tendsto u lt l) (hv : tendsto v lt l) (huv : u â‰¤á¶ [lt] v) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ - (Î¼ (Ioc (u t) (v t))).to_real â€¢ c)
    (Î» t, (Î¼ $ Ioc (u t) (v t)).to_real) lt :=
(measure_integral_sub_linear_is_o_of_tendsto_ae hfm hf hl hu hv).congr'
  (huv.mono $ Î» x hx, by simp [integral_const', hx])
  (huv.mono $ Î» x hx, by simp [integral_const', hx])

lemma measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge
  [is_measurably_generated l'] [tendsto_Ixx_class Ioc l l']
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c)) (hl : Î¼.finite_at_filter l')
  (hu : tendsto u lt l) (hv : tendsto v lt l) (huv : v â‰¤á¶ [lt] u) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ + (Î¼ (Ioc (v t) (u t))).to_real â€¢ c)
    (Î» t, (Î¼ $ Ioc (v t) (u t)).to_real) lt :=
(measure_integral_sub_linear_is_o_of_tendsto_ae_of_le hfm hf hl hv hu huv).neg_left.congr_left $
  Î» t, by simp [integral_symm (u t), add_comm]

variables [topological_space Î±] [order_topology Î±] [borel_space Î±]

/-- Fundamental theorem of calculus-1, strict derivative in both limits for any measure.
Let `f` be a measurable function integrable on `a..b`.
Let `la`, `pure a â‰¤ la â‰¤ ğ“ a`, be a measurably generated interval generated filter such that
`Î¼` is finite at `la` and `f x` has a finite limit `ca` almost surely at `la`.
Let `lb`, `pure b â‰¤ lb â‰¤ ğ“ b`, be a measurably generated interval generated filter such that
`Î¼` is finite at `lb` and `f x` has a finite limit `cb` almost surely at `lb`.
Then
`âˆ« x in va t..vb t, f x âˆ‚Î¼ - âˆ« x in ua t..ub t, f x âˆ‚Î¼ =
  âˆ« x in ub t..vb t, cb âˆ‚Î¼ - âˆ« x in ua t..va t, ca âˆ‚Î¼ +
    o(âˆ¥âˆ« x in ua t..va t, (1:â„) âˆ‚Î¼âˆ¥ + âˆ¥âˆ« x in ub t..vb t, (1:â„) âˆ‚Î¼âˆ¥)`
as `ua` and `va` tend to `la` while `ub` and `vb` tend to `lb`.
 -/
lemma measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_of_tendsto_ae
  [tendsto_Ixx_class Ioc la la'] [is_measurably_generated la']
  [tendsto_Ixx_class Ioc lb lb'] [is_measurably_generated lb']
  {a b} (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (ha_lim : tendsto f (la' âŠ“ Î¼.ae) (ğ“ ca)) (ha_fin : Î¼.finite_at_filter la') (ha_le : pure a â‰¤ la)
  (hb_lim : tendsto f (lb' âŠ“ Î¼.ae) (ğ“ cb)) (hb_fin : Î¼.finite_at_filter lb') (hb_le : pure b â‰¤ lb)
  (hua : tendsto ua lt la) (hva : tendsto va lt la)
  (hub : tendsto ub lt lb) (hvb : tendsto vb lt lb) :
  is_o (Î» t, (âˆ« x in va t..vb t, f x âˆ‚Î¼) - (âˆ« x in ua t..ub t, f x âˆ‚Î¼) -
    (âˆ« x in ub t..vb t, cb âˆ‚Î¼ - âˆ« x in ua t..va t, ca âˆ‚Î¼))
    (Î» t, âˆ¥âˆ« x in ua t..va t, (1:â„) âˆ‚Î¼âˆ¥ + âˆ¥âˆ« x in ub t..vb t, (1:â„) âˆ‚Î¼âˆ¥) lt :=
begin
  refine
    ((measure_integral_sub_linear_is_o_of_tendsto_ae hfm ha_lim ha_fin hua hva).neg_left.add_add
    (measure_integral_sub_linear_is_o_of_tendsto_ae hfm hb_lim hb_fin hub hvb)).congr'
      _ (eventually_eq.refl _ _),
  have A : âˆ€á¶  t in lt, interval_integrable f Î¼ (ua t) (va t) :=
    ha_lim.eventually_interval_integrable_ae ha_fin hua hva,
  have A' : âˆ€á¶  t in lt, interval_integrable f Î¼ a (ua t) :=
    ha_lim.eventually_interval_integrable_ae ha_fin (tendsto_le_right ha_le tendsto_const_pure) hua,
  have B : âˆ€á¶  t in lt, interval_integrable f Î¼ (ub t) (vb t) :=
    hb_lim.eventually_interval_integrable_ae hb_fin hub hvb,
  have B' : âˆ€á¶  t in lt, interval_integrable f Î¼ b (ub t) :=
    hb_lim.eventually_interval_integrable_ae hb_fin (tendsto_le_right hb_le tendsto_const_pure) hub,
  filter_upwards [A, A', B, B'], simp only [mem_set_of_eq],
  intros t ua_va a_ua ub_vb b_ub,
  rw [â† integral_interval_sub_interval_comm' hfm],
  { dsimp only [], abel },
  exacts [ub_vb, ua_va, b_ub.symm.trans $ hab.symm.trans a_ua]
end

/-- Fundamental theorem of calculus-1 for any measure.
Let f` be a measurable function integrable on `a..b`. Let `l` be one of `pure b`, `ğ“[Iic b] b`,
`ğ“[Ici b] b`, or `ğ“ b`. Suppose that `f x` has a finite limit `c` as `x` tends to `l âŠ“ Î¼.ae`.
Then `âˆ« x in a..v, f x âˆ‚Î¼ - âˆ« x in a..u, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, 1 âˆ‚Î¼)`
as `u` and `v` tend to `l`.

We use `pure b â‰¤ l â‰¤ ğ“ b` together with two typeclasses as a fancy way to say
"let `l` be one of `pure b`, `ğ“[Iic b] b`, `ğ“[Ici b] b`, or `ğ“ b`". -/
lemma measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_right
  [tendsto_Ixx_class Ioc lb lb'] [is_measurably_generated lb']
  {a b} (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hf : tendsto f (lb' âŠ“ Î¼.ae) (ğ“ c)) (hl : Î¼.finite_at_filter lb') (h_le : pure b â‰¤ lb)
  (hu : tendsto u lt lb) (hv : tendsto v lt lb) :
  is_o (Î» t, âˆ« x in a..v t, f x âˆ‚Î¼ - âˆ« x in a..u t, f x âˆ‚Î¼ - âˆ« x in u t..v t, c âˆ‚Î¼)
    (Î» t, âˆ« x in u t..v t, (1:â„) âˆ‚Î¼) lt :=
by simpa using measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_of_tendsto_ae
  hfm hab (flip tendsto_le_left (tendsto_bot : tendsto _ âŠ¥ (ğ“ 0)) inf_le_left)
  Î¼.finite_at_bot (le_refl _) hf hl h_le tendsto_const_pure tendsto_const_pure hu hv

lemma measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_left
  [tendsto_Ixx_class Ioc la la'] [is_measurably_generated la']
  {a b} (hfm : measurable f) (hab : interval_integrable f Î¼ a b)
  (hf : tendsto f (la' âŠ“ Î¼.ae) (ğ“ c)) (hl : Î¼.finite_at_filter la') (h_le : pure a â‰¤ la)
  (hu : tendsto u lt la) (hv : tendsto v lt la) :
  is_o (Î» t, âˆ« x in v t..b, f x âˆ‚Î¼ - âˆ« x in u t..b, f x âˆ‚Î¼ + âˆ« x in u t..v t, c âˆ‚Î¼)
    (Î» t, âˆ« x in u t..v t, (1:â„) âˆ‚Î¼) lt :=
by simpa using measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_of_tendsto_ae
  hfm hab hf hl h_le (flip tendsto_le_left (tendsto_bot : tendsto _ âŠ¥ (ğ“ 0)) inf_le_left)
  Î¼.finite_at_bot (le_refl _) hu hv tendsto_const_pure tendsto_const_pure

#exit
end

/-!
### Fundamental theorem of calculus-1 for Lebesgue measure

In this section we restate theorems from the previous section for Lebesgue measure.
In particular, we prove that `âˆ« x in a..u, f x` is strictly differentiable in `u`
at `b` provided that `f` is integrable on `a..b` and is continuous at `b`.
-/

variables {f : â„ â†’ E} {c : E} {l : filter â„} {lb : filter Î²} [is_measurably_generated l]
  [is_interval_generated l] {a b z : â„}

/-- Fundamental theorem of calculus-1, local version. If `f` has a finite limit `c` at
`l âŠ“ volume.ae`, where `l â‰¤ ğ“ a` is a measurably generated interval generated filter (e.g., `ğ“ a`,
`ğ“[Ici a] a`, `ğ“[Iic a] a`), then `âˆ« x in u..v, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, 1 âˆ‚Î¼)`
as both `u` and `v` tend to `l`. -/
lemma integral_sub_linear_is_o_of_tendsto_ae (hfm : measurable f)
  (hf : tendsto f (l âŠ“ volume.ae) (ğ“ c)) (ha : l â‰¤ ğ“ a)
  {u v : Î² â†’ â„} (hu : tendsto u lb l) (hv : tendsto v lb l) :
  is_o (Î» t, (âˆ« x in u t..v t, f x) - (v t - u t) â€¢ c) (v - u) lb :=
by simpa [integral_const] using measure_integral_sub_linear_is_o_of_tendsto_ae hfm hf
  ((volume.finite_at_nhds _).filter_mono ha) hu hv

/-- Fundamental theorem of calculus-1. If `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely at `l`, where `l â‰¤ ğ“ a` is a measurably generated interval generated filter
(e.g., `ğ“ a`, `ğ“[Ici a] a`, `ğ“[Iic a] a`), then
`âˆ« x in a..v, f x - âˆ« x in a..u, f x = (v - u) â€¢ c + o(v - u)` as both `u` and `v` tend to `l`.

This is a generalization of the `has_strict_deriv_at` version of FTC-1 below. -/
lemma integral_sub_integral_sub_linear_is_o_of_tendsto_ae
  (hfm : measurable f) (hf : tendsto f (l âŠ“ volume.ae) (ğ“ c))
  (hb : l â‰¤ ğ“ b) (hb' : pure b â‰¤ l) (hab : interval_integrable f volume a b)
  {u v : Î² â†’ â„} (hu : tendsto u lb l) (hv : tendsto v lb l) :
  is_o (Î» t, (âˆ« x in a..v t, f x) - (âˆ« x in a..u t, f x) - (v t - u t) â€¢ c) (v - u) lb :=
by simpa only [integral_const, smul_eq_mul, mul_one] using
  measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae hfm hf
    ((volume.finite_at_nhds _).filter_mono hb) hb hb' hab hu hv

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `c` at `b`
in the sense of strict differentiability. -/
lemma integral_has_strict_deriv_at_of_tendsto_ae (hfm : measurable f)
  (hfi : interval_integrable f volume a b) (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ c)) :
  has_strict_deriv_at (Î» u, âˆ« x in a..u, f x) c b :=
integral_sub_integral_sub_linear_is_o_of_tendsto_ae hfm hb (le_refl _) (pure_le_nhds _)
  hfi continuous_at_snd continuous_at_fst

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `c` at
`b`. -/
lemma integral_has_deriv_at_of_tendsto_ae (hfm : measurable f)
  (hfi : interval_integrable f volume a b) (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ c)) :
  has_deriv_at (Î» u, âˆ« x in a..u, f x) c b :=
(integral_has_strict_deriv_at_of_tendsto_ae hfm hfi hb).has_deriv_at

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
at `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `f b` at `b`. -/
lemma integral_has_deriv_at (hfm : measurable f) (hfi : interval_integrable f volume a b)
  (hb : continuous_at f b) :
  has_deriv_at (Î» u, âˆ« x in a..u, f x) (f b) b :=
integral_has_deriv_at_of_tendsto_ae hfm hfi (flip tendsto_le_left hb inf_le_left)

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b` from the right, then `u â†¦ âˆ« x in a..u, f x` has
right derivative `c` at `b`. -/
lemma integral_has_deriv_within_at_Ici_of_tendsto_ae (hfm : measurable f)
  (hfi : interval_integrable f volume a b)
  (hb : tendsto f (ğ“[Ici b] b âŠ“ volume.ae) (ğ“ c)) :
  has_deriv_within_at (Î» u, âˆ« x in a..u, f x) c (Ici b) b :=
have pure b â‰¤ ğ“[Ici b] b := pure_le_nhds_within left_mem_Ici,
integral_sub_integral_sub_linear_is_o_of_tendsto_ae hfm hb inf_le_left this hfi
  (flip tendsto_le_right tendsto_const_pure this) tendsto_id

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
from the right at `b`, then `u â†¦ âˆ« x in a..u, f x` has right derivative `f b` at `b`. -/
lemma integral_has_deriv_within_at_Ici (hfm : measurable f) (hfi : interval_integrable f volume a b)
  (hb : continuous_within_at f (Ici b) b) :
  has_deriv_within_at (Î» u, âˆ« x in a..u, f x) (f b) (Ici b) b :=
integral_has_deriv_within_at_Ici_of_tendsto_ae hfm hfi (flip tendsto_le_left hb inf_le_left)

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b` from the left, then `u â†¦ âˆ« x in a..u, f x` has left
derivative `c` at `b`. -/
lemma integral_has_deriv_within_at_Iic_of_tendsto_ae (hfm : measurable f)
  (hfi : interval_integrable f volume a b)
  (hb : tendsto f (ğ“[Iic b] b âŠ“ volume.ae) (ğ“ c)) :
  has_deriv_within_at (Î» u, âˆ« x in a..u, f x) c (Iic b) b :=
have pure b â‰¤ ğ“[Iic b] b := pure_le_nhds_within right_mem_Iic,
integral_sub_integral_sub_linear_is_o_of_tendsto_ae hfm hb inf_le_left this hfi
  (flip tendsto_le_right tendsto_const_pure this) tendsto_id

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
from the left at `b`, then `u â†¦ âˆ« x in a..u, f x` has left derivative `f b` at `b`. -/
lemma integral_has_deriv_within_at_Iic (hfm : measurable f) (hfi : interval_integrable f volume a b)
  (hb : continuous_within_at f (Iic b) b) :
  has_deriv_within_at (Î» u, âˆ« x in a..u, f x) (f b) (Iic b) b :=
integral_has_deriv_within_at_Iic_of_tendsto_ae hfm hfi (flip tendsto_le_left hb inf_le_left)

/-!
### Fundamental theorem of calculus-1: formulas for `(d/du) âˆ« x in a..u, f x`

In this section we reformulate FTC-1 in terms of `deriv ... = ...` or `deriv_within ... = ...`.
-/

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b`, then the derivative of `u â†¦ âˆ« x in a..u, f x` at `b`
equals `c`. -/
lemma deriv_integral_eq_of_tendsto_ae (hfm : measurable f) (hfi : interval_integrable f volume a b)
  (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ c)) :
  deriv (Î» u, âˆ« x in a..u, f x) b = c :=
(integral_has_deriv_at_of_tendsto_ae hfm hfi hb).deriv

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
at `b`, then the derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `f b`. -/
lemma deriv_integral_eq (hfm : measurable f) (hfi : interval_integrable f volume a b)
  (hb : continuous_at f b) :
  deriv (Î» u, âˆ« x in a..u, f x) b = f b :=
(integral_has_deriv_at hfm hfi hb).deriv

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b` from the right, then the right derivative
of `u â†¦ âˆ« x in a..u, f x` at `b` equals `c`. -/
lemma deriv_within_Ici_integral_of_tendsto_ae (hfm : measurable f)
  (hfi : interval_integrable f volume a b)
  (hb : tendsto f (ğ“[Ici b] b âŠ“ volume.ae) (ğ“ c)) :
  deriv_within (Î» u, âˆ« x in a..u, f x) (Ici b) b = c :=
(integral_has_deriv_within_at_Ici_of_tendsto_ae hfm hfi hb).deriv_within $
  unique_diff_on_Ici _ _ left_mem_Ici

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
from the right at `b`, then the right derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `c`. -/
lemma deriv_within_Ici_integral (hfm : measurable f) (hfi : interval_integrable f volume a b)
  (hb : continuous_within_at f (Ici b) b) :
  deriv_within (Î» u, âˆ« x in a..u, f x) (Ici b) b = f b :=
(integral_has_deriv_within_at_Ici hfm hfi hb).deriv_within $
  unique_diff_on_Ici _ _ left_mem_Ici

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b` from the left, then the left derivative
of `u â†¦ âˆ« x in a..u, f x` at `b` equals `c`. -/
lemma deriv_within_Iic_integral_of_tendsto_ae (hfm : measurable f)
  (hfi : interval_integrable f volume a b)
  (hb : tendsto f (ğ“[Iic b] b âŠ“ volume.ae) (ğ“ c)) :
  deriv_within (Î» u, âˆ« x in a..u, f x) (Iic b) b = c :=
(integral_has_deriv_within_at_Iic_of_tendsto_ae hfm hfi hb).deriv_within $
  unique_diff_on_Iic _ _ right_mem_Iic

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
from the left at `b`, then the left derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `c`. -/
lemma deriv_within_Iic_integral (hfm : measurable f) (hfi : interval_integrable f volume a b)
  (hb : continuous_within_at f (Iic b) b) :
  deriv_within (Î» u, âˆ« x in a..u, f x) (Iic b) b = f b :=
(integral_has_deriv_within_at_Iic hfm hfi hb).deriv_within $
  unique_diff_on_Iic _ _ right_mem_Iic

end interval_integral
