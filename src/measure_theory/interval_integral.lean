/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury G. Kudryashov
-/
import measure_theory.set_integral
import measure_theory.lebesgue_measure
import analysis.calculus.deriv

/-!
# Integral over an interval

In this file we define `âˆ« x in a..b, f x âˆ‚Î¼` to be `âˆ« x in Ioc a b, f x âˆ‚Î¼` if `a â‰¤ b`
and `-âˆ« x in Ioc b a, f x âˆ‚Î¼` if `b â‰¤ a`. We prove a few simple properties and many versions
of the first part of the
[fundamental theorem of calculus](https://en.wikipedia.org/wiki/Fundamental_theorem_of_calculus).
Recall that it states that the function `(u, v) â†¦ âˆ« x in u..v, f x` has derivative
`(Î´u, Î´v) â†¦ Î´v â€¢ f b - Î´u â€¢ f a` at `(a, b)` provided that `f` is continuous at `a` and `b`.

## Main statements

### FTC-1 for Lebesgue measure

We prove several versions of FTC-1, all in the `interval_integral` namespace. Many of them follow
the naming scheme `integral_has(_strict?)_(f?)deriv(_within?)_at(_of_tendsto_ae?)(_right|_left?)`.
They formulate FTC in terms of `has(_strict?)_(f?)deriv(_within?)_at`.
Let us explain the meaning of each part of the name:

* `_strict` means that the theorem is about strict differentiability;
* `f` means that the theorem is about differentiability in both endpoints; incompatible with
  `_right|_left`;
* `_within` means that the theorem is about one-sided derivatives, see below for details;
* `_of_tendsto_ae` means that instead of continuity the theorem assumes that `f` has a finite limit
  almost surely as `x` tends to `a` and/or `b`;
* `_right` or `_left` mean that the theorem is about differentiability in the right (resp., left)
  endpoint.

We also reformulate these theorems in terms of `(f?)deriv(_within?)`. These theorems are named
`(f?)deriv(_within?)_integral(_of_tendsto_ae?)(_right|_left?)` with the same meaning of parts of the
name.

### One-sided derivatives

Theorem `integral_has_fderiv_within_at_of_tendsto_ae` states that `(u, v) â†¦ âˆ« x in u..v, f x` has a
derivative `(Î´u, Î´v) â†¦ Î´v â€¢ cb - Î´u â€¢ ca` within the set `s Ã— t` at `(a, b)` provided that `f` tends
to `ca` (resp., `cb`) almost surely at `la` (resp., `lb`), where possible values of `s`, `t`, and
corresponding filters `la`, `lb` are given in the following table.

| `s`     | `la`         | `t`     | `lb`         |
| ------- | ----         | ---     | ----         |
| `Iic a` | `ğ“[Iic a] a` | `Iic b` | `ğ“[Iic b] b` |
| `Ici a` | `ğ“[Ioi a] a` | `Ici b` | `ğ“[Ioi b] b` |
| `{a}`   | `âŠ¥`          | `{b}`   | `âŠ¥`          |
| `univ`  | `ğ“ a`        | `univ`  | `ğ“ b`        |

We use a typeclass `FTC_filter` to make Lean automatically find `la`/`lb` based on `s`/`t`. This way
we can formulate one theorem instead of `16` (or `8` if we leave only non-trivial ones not covered
by `integral_has_deriv_within_at_of_tendsto_ae_(left|right)` and
`integral_has_fderiv_at_of_tendsto_ae`). Similarly,
`integral_has_deriv_within_at_of_tendsto_ae_right` works for both one-sided derivatives using the
same typeclass to find an appropriate filter.

### FTC for a locally finite measure

Before proving FTC for the Lebesgue measure, we prove a few statements that can be seen as FTC for
any measure. The most general of them,
`measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae`, states the following. Let `(la, la')`
be an `FTC_filter` pair of filters around `a` (i.e., `FTC_filter a la la'`) and let `(lb, lb')` be
an `FTC_filter` pair of filters around `b`. If `f` has finite limits `ca` and `cb` almost surely at
`la'` and `lb'`, respectively, then
`âˆ« x in va..vb, f x âˆ‚Î¼ - âˆ« x in ua..ub, f x âˆ‚Î¼ = âˆ« x in ub..vb, cb âˆ‚Î¼ - âˆ« x in ua..va, ca âˆ‚Î¼ +
  o(âˆ¥âˆ« x in ua..va, (1:â„) âˆ‚Î¼âˆ¥ + âˆ¥âˆ« x in ub..vb, (1:â„) âˆ‚Î¼âˆ¥)` as `ua` and `va` tend to `la` while
`ub` and `vb` tend to `lb`.

## Implementation notes

### Avoiding `if`, `min`, and `max`

In order to avoid `if`s in the definition, we define `interval_integrable f Î¼ a b` as
`integrable_on f (Ioc a b) Î¼ âˆ§ integrable_on f (Ioc b a) Î¼`. For any `a`, `b` one of these
intervals is empty and the other coincides with `Ioc (min a b) (max a b)`.

Similarly, we define `âˆ« x in a..b, f x âˆ‚Î¼` to be `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`.
Again, for any `a`, `b` one of these integrals is zero, and the other gives the expected result.

This way some properties can be translated from integrals over sets without dealing with
the cases `a â‰¤ b` and `b â‰¤ a` separately.

### Choice of the interval

We use integral over `Ioc (min a b) (max a b)` instead of one of the other three possible
intervals with the same endpoints for two reasons:

* this way `âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼` holds whenever
  `f` is integrable on each interval; in particular, it works even if the measure `Î¼` has an atom
  at `b`; this rules out `Ioo` and `Icc` intervals;
* with this definition for a probability measure `Î¼`, the integral `âˆ« x in a..b, 1 âˆ‚Î¼` equals
  the difference $F_Î¼(b)-F_Î¼(a)$, where $F_Î¼(a)=Î¼(-âˆ, a]$ is the
  [cumulative distribution function](https://en.wikipedia.org/wiki/Cumulative_distribution_function)
  of `Î¼`.

### `FTC_filter` class

As explained above, many theorems in this file rely on the typeclass
`FTC_filter (a : Î±) (l l' : filter Î±)` to avoid code duplication. This typeclass combines four
assumptions:

- `pure a â‰¤ l`;
- `l' â‰¤ ğ“ a`;
- `l'` has a basis of measurable sets;
- if `u n` and `v n` tend to `l`, then for any `s âˆˆ l'`, `Ioc (u n) (v n)` is eventually included
  in `s`.

This typeclass has exactly four â€œrealâ€ instances: `(a, pure a, âŠ¥)`, `(a, ğ“[Ici a] a, ğ“[Ioi a] a)`,
`(a, ğ“[Iic a] a, ğ“[Iic a] a)`, `(a, ğ“ a, ğ“ a)`, and two instances that are equal to the first and
last â€œrealâ€ instances: `(a, ğ“[{a}] a, âŠ¥)` and `(a, ğ“[univ] a, ğ“[univ] a)`. While the difference
between `Ici a` and `Ioi a` doesn't matter for theorems about Lebesgue measure, it becomes important
in the versions of FTC about any locally finite measure if this measure has an atom at one of the
endpoints.

## Tags

integral, fundamental theorem of calculus
 -/

noncomputable theory
open topological_space (second_countable_topology)
open measure_theory set classical filter

open_locale classical topological_space filter

variables {Î± Î² ğ•œ E F : Type*} [decidable_linear_order Î±] [measurable_space Î±]
  [measurable_space E] [normed_group E]

/-!
### Integrability at an interval
-/

/-- A function `f` is called *interval integrable* with respect to a measure `Î¼` on an unordered
interval `a..b` if it is integrable on both intervals `(a, b]` and `(b, a]`. One of these
intervals is always empty, so this property is equivalent to `f` being integrable on
`(min a b, max a b]`. -/
def interval_integrable (f : Î± â†’ E) (Î¼ : measure Î±) (a b : Î±) :=
integrable_on f (Ioc a b) Î¼ âˆ§ integrable_on f (Ioc b a) Î¼

lemma measure_theory.integrable.interval_integrable {f : Î± â†’ E} {Î¼ : measure Î±}
  (hf : integrable f Î¼) {a b : Î±} :
  interval_integrable f Î¼ a b :=
âŸ¨hf.integrable_on, hf.integrable_onâŸ©

namespace interval_integrable

section

variables {f : Î± â†’ E} {a b c : Î±} {Î¼ : measure Î±}

@[symm] lemma symm (h : interval_integrable f Î¼ a b) : interval_integrable f Î¼ b a :=
h.symm

@[refl] lemma refl (hf : measurable f) : interval_integrable f Î¼ a a :=
by split; simp [hf]

@[trans] lemma trans  (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  interval_integrable f Î¼ a c :=
âŸ¨(hab.1.union hbc.1).mono_set Ioc_subset_Ioc_union_Ioc,
  (hbc.2.union hab.2).mono_set Ioc_subset_Ioc_union_IocâŸ©

lemma neg [borel_space E] (h : interval_integrable f Î¼ a b) : interval_integrable (-f) Î¼ a b :=
âŸ¨h.1.neg, h.2.negâŸ©

protected lemma measurable (h : interval_integrable f Î¼ a b) : measurable f :=
h.1.measurable

end

variables [borel_space E] {f g : Î± â†’ E} {a b : Î±} {Î¼ : measure Î±}

lemma smul [normed_field ğ•œ] [normed_space ğ•œ E] {f : Î± â†’ E} {a b : Î±} {Î¼ : measure Î±}
  (h : interval_integrable f Î¼ a b) (r : ğ•œ) :
  interval_integrable (r â€¢ f) Î¼ a b :=
âŸ¨h.1.smul r, h.2.smul râŸ©

lemma add [second_countable_topology E] (hf : interval_integrable f Î¼ a b)
  (hg : interval_integrable g Î¼ a b) : interval_integrable (f + g) Î¼ a b :=
âŸ¨hf.1.add hg.1, hf.2.add hg.2âŸ©

lemma sub [second_countable_topology E] (hf : interval_integrable f Î¼ a b)
  (hg : interval_integrable g Î¼ a b) : interval_integrable (f - g) Î¼ a b :=
âŸ¨hf.1.sub hg.1, hf.2.sub hg.2âŸ©

end interval_integrable

/-- Let `l'` be a measurably generated filter; let `l` be a of filter such that each `s âˆˆ l'`
eventually includes `Ioc u v` as both `u` and `v` tend to `l`. Let `Î¼` be a measure finite at `l'`.

Suppose that `f : Î± â†’ E` has a finite limit at `l' âŠ“ Î¼.ae`. Then `f` is interval integrable on
`u..v` provided that both `u` and `v` tend to `l`.

Typeclass instances allow Lean to find `l'` based on `l` but not vice versa, so
`apply tendsto.eventually_interval_integrable_ae` will generate goals `filter Î±` and
`tendsto_Ixx_class Ioc ?m_1 l'`. -/
lemma filter.tendsto.eventually_interval_integrable_ae {f : Î± â†’ E} (hfm : measurable f)
  {Î¼ : measure Î±} {l l' : filter Î±} [tendsto_Ixx_class Ioc l l'] [is_measurably_generated l']
  (hÎ¼ : Î¼.finite_at_filter l') {c : E} (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c))
  {u v : Î² â†’ Î±} {lt : filter Î²} (hu : tendsto u lt l) (hv : tendsto v lt l) :
  âˆ€á¶  t in lt, interval_integrable f Î¼ (u t) (v t) :=
have _ := (hf.integrable_at_filter_ae hfm hÎ¼).eventually,
((hu.Ioc hv).eventually this).and $ (hv.Ioc hu).eventually this

/-- Let `l'` be a measurably generated filter; let `l` be a of filter such that each `s âˆˆ l'`
eventually includes `Ioc u v` as both `u` and `v` tend to `l`. Let `Î¼` be a measure finite at `l'`.

Suppose that `f : Î± â†’ E` has a finite limit at `l`. Then `f` is interval integrable on `u..v`
provided that both `u` and `v` tend to `l`.

Typeclass instances allow Lean to find `l'` based on `l` but not vice versa, so
`apply tendsto.eventually_interval_integrable_ae` will generate goals `filter Î±` and
`tendsto_Ixx_class Ioc ?m_1 l'`. -/
lemma filter.tendsto.eventually_interval_integrable {f : Î± â†’ E} (hfm : measurable f)
  {Î¼ : measure Î±} {l l' : filter Î±} [tendsto_Ixx_class Ioc l l'] [is_measurably_generated l']
  (hÎ¼ : Î¼.finite_at_filter l') {c : E} (hf : tendsto f l' (ğ“ c))
  {u v : Î² â†’ Î±} {lt : filter Î²} (hu : tendsto u lt l) (hv : tendsto v lt l) :
  âˆ€á¶  t in lt, interval_integrable f Î¼ (u t) (v t) :=
(hf.mono_left inf_le_left).eventually_interval_integrable_ae hfm hÎ¼ hu hv

/-!
### Interval integral: definition and basic properties

In this section we define `âˆ« x in a..b, f x âˆ‚Î¼` as `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`
and prove some basic properties.
-/

variables [second_countable_topology E] [complete_space E] [normed_space â„ E]
  [borel_space E]

/-- The interval integral `âˆ« x in a..b, f x âˆ‚Î¼` is defined
as `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`. If `a â‰¤ b`, then it equals
`âˆ« x in Ioc a b, f x âˆ‚Î¼`, otherwise it equals `-âˆ« x in Ioc b a, f x âˆ‚Î¼`. -/
def interval_integral (f : Î± â†’ E) (a b : Î±) (Î¼ : measure Î±) :=
âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼

notation `âˆ«` binders ` in ` a `..` b `, ` r:(scoped:60 f, f) ` âˆ‚` Î¼:70 := interval_integral r a b Î¼
notation `âˆ«` binders ` in ` a `..` b `, ` r:(scoped:60 f, interval_integral f a b volume) := r

namespace interval_integral

section

variables {a b c d : Î±} {f g : Î± â†’ E} {Î¼ : measure Î±}

@[simp] lemma integral_zero : âˆ« x in a..b, (0 : E) âˆ‚Î¼ = 0 :=
by simp [interval_integral]

lemma integral_of_le (h : a â‰¤ b) : âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in Ioc a b, f x âˆ‚Î¼ :=
by simp [interval_integral, h]

@[simp] lemma integral_same : âˆ« x in a..a, f x âˆ‚Î¼ = 0 :=
sub_self _

lemma integral_symm (a b) : âˆ« x in b..a, f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, neg_sub]

lemma integral_of_ge (h : b â‰¤ a) : âˆ« x in a..b, f x âˆ‚Î¼ = -âˆ« x in Ioc b a, f x âˆ‚Î¼ :=
by simp only [integral_symm b, integral_of_le h]

lemma integral_cases (f : Î± â†’ E) (a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ âˆˆ ({âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼,
    -âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼} : set E) :=
(le_total a b).imp (Î» h, by simp [h, integral_of_le]) (Î» h, by simp [h, integral_of_ge])

lemma integral_non_measurable {f : Î± â†’ E} {a b} (hf : Â¬measurable f) :
  âˆ« x in a..b, f x âˆ‚Î¼ = 0 :=
by rw [interval_integral, integral_non_measurable hf, integral_non_measurable hf, sub_zero]

lemma norm_integral_eq_norm_integral_Ioc :
  âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ = âˆ¥âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼âˆ¥ :=
(integral_cases f a b).elim (congr_arg _) (Î» h, (congr_arg _ h).trans (norm_neg _))

lemma norm_integral_le_integral_norm_Ioc :
  âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ â‰¤ âˆ« x in Ioc (min a b) (max a b), âˆ¥f xâˆ¥ âˆ‚Î¼ :=
calc âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ = âˆ¥âˆ« x in Ioc (min a b) (max a b), f x âˆ‚Î¼âˆ¥ :
  norm_integral_eq_norm_integral_Ioc
... â‰¤ âˆ« x in Ioc (min a b) (max a b), âˆ¥f xâˆ¥ âˆ‚Î¼ :
  norm_integral_le_integral_norm f

lemma norm_integral_le_abs_integral_norm : âˆ¥âˆ« x in a..b, f x âˆ‚Î¼âˆ¥ â‰¤ abs (âˆ« x in a..b, âˆ¥f xâˆ¥ âˆ‚Î¼) :=
begin
  simp only [â† real.norm_eq_abs, norm_integral_eq_norm_integral_Ioc],
  exact le_trans (norm_integral_le_integral_norm _) (le_abs_self _)
end

lemma norm_integral_le_of_norm_le_const_ae {a b C : â„} {f : â„ â†’ E}
  (h : âˆ€áµ x, x âˆˆ Ioc (min a b) (max a b) â†’ âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in a..b, f xâˆ¥ â‰¤ C * abs (b - a) :=
begin
  rw [norm_integral_eq_norm_integral_Ioc],
  convert norm_set_integral_le_of_norm_le_const_ae'' _ is_measurable_Ioc h,
  { rw [real.volume_Ioc, max_sub_min_eq_abs, ennreal.to_real_of_real (abs_nonneg _)] },
  { simp only [real.volume_Ioc, ennreal.of_real_lt_top] },
end

lemma norm_integral_le_of_norm_le_const {a b C : â„} {f : â„ â†’ E}
  (h : âˆ€ x âˆˆ Ioc (min a b) (max a b), âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in a..b, f xâˆ¥ â‰¤ C * abs (b - a) :=
norm_integral_le_of_norm_le_const_ae $ eventually_of_forall h

lemma integral_add (hf : interval_integrable f Î¼ a b) (hg : interval_integrable g Î¼ a b) :
  âˆ« x in a..b, f x + g x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in a..b, g x âˆ‚Î¼ :=
by { simp only [interval_integral, integral_add hf.1 hg.1, integral_add hf.2 hg.2], abel }

@[simp] lemma integral_neg : âˆ« x in a..b, -f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ :=
by { simp only [interval_integral, integral_neg], abel }

lemma integral_sub (hf : interval_integrable f Î¼ a b) (hg : interval_integrable g Î¼ a b) :
  âˆ« x in a..b, f x - g x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in a..b, g x âˆ‚Î¼ :=
(integral_add hf hg.neg).trans $ congr_arg _ integral_neg

lemma integral_smul (r : â„) : âˆ« x in a..b, r â€¢ f x âˆ‚Î¼ = r â€¢ âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, integral_smul, smul_sub]

lemma integral_const' (c : E) :
  âˆ« x in a..b, c âˆ‚Î¼ = ((Î¼ $ Ioc a b).to_real - (Î¼ $ Ioc b a).to_real) â€¢ c :=
by simp only [interval_integral, set_integral_const, sub_smul]

lemma integral_const {a b : â„} (c : E) : (âˆ« (x : â„) in a..b, c) = (b - a) â€¢ c :=
by simp only [integral_const', real.volume_Ioc, ennreal.to_real_of_real', â† neg_sub b,
  max_zero_sub_eq_self]

lemma integral_smul_measure (c : ennreal) :
  âˆ« x in a..b, f x âˆ‚(c â€¢ Î¼) = c.to_real â€¢ âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, measure.restrict_smul, integral_smul_measure, smul_sub]

lemma integral_comp_add_right (a b c : â„) (f : â„ â†’ E) (hfm : measurable f) :
  âˆ« x in a..b, f (x + c) = âˆ« x in a+c..b+c, f x :=
calc âˆ« x in a..b, f (x + c) = âˆ« x in a+c..b+c, f x âˆ‚(measure.map (Î» x, x + c) volume) :
  by simp only [interval_integral, set_integral_map is_measurable_Ioc hfm (measurable_add_right _),
    preimage_add_const_Ioc, add_sub_cancel]
... = âˆ« x in a+c..b+c, f x : by rw [real.map_volume_add_right]

lemma integral_comp_mul_right {c : â„} (hc : 0 < c) (a b : â„) (f : â„ â†’ E) (hfm : measurable f) :
  âˆ« x in a..b, f (x * c) = câ»Â¹ â€¢ âˆ« x in a*c..b*c, f x :=
begin
  conv_rhs { rw [â† real.smul_map_volume_mul_right (ne_of_gt hc)] },
  rw [integral_smul_measure],
  simp only [interval_integral, set_integral_map is_measurable_Ioc hfm (measurable_mul_right _),
    hc, preimage_mul_const_Ioc, mul_div_cancel _ (ne_of_gt hc), abs_of_pos,
    ennreal.to_real_of_real (le_of_lt hc), inv_smul_smul' (ne_of_gt hc)]
end

lemma integral_comp_neg (a b : â„) (f : â„ â†’ E) (hfm : measurable f) :
  âˆ« x in a..b, f (-x) = âˆ« x in -b..-a, f x :=
begin
  conv_rhs { rw â† real.map_volume_neg },
  simp only [interval_integral, set_integral_map is_measurable_Ioc hfm measurable_neg, neg_preimage,
    preimage_neg_Ioc, neg_neg, restrict_congr_set Ico_ae_eq_Ioc]
end

/-!
### Integral is an additive function of the interval

In this section we prove that `âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼`
as well as a few other identities trivially equivalent to this one. We also prove that
`âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼` provided that `support f âŠ† Ioc a b`.
-/

variables [topological_space Î±] [opens_measurable_space Î±]

section order_closed_topology

variables [order_closed_topology Î±]

lemma integral_add_adjacent_intervals_cancel (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ + âˆ« x in c..a, f x âˆ‚Î¼ = 0 :=
begin
  have hac := hab.trans hbc,
  simp only [interval_integral, â† add_sub_comm, sub_eq_zero],
  iterate 4 { rw â† integral_union },
  { suffices : Ioc a b âˆª Ioc b c âˆª Ioc c a = Ioc b a âˆª Ioc c b âˆª Ioc a c, by rw this,
    rw [Ioc_union_Ioc_union_Ioc_cycle, union_right_comm, Ioc_union_Ioc_union_Ioc_cycle,
      min_left_comm, max_left_comm] },
  all_goals { simp [*, is_measurable.union, is_measurable_Ioc, Ioc_disjoint_Ioc_same,
    Ioc_disjoint_Ioc_same.symm, hab.1, hab.2, hbc.1, hbc.2, hac.1, hac.2] }
end

lemma integral_add_adjacent_intervals (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼ :=
by rw [â† add_neg_eq_zero, â† integral_symm, integral_add_adjacent_intervals_cancel hab hbc]

lemma integral_interval_sub_left (hab : interval_integrable f Î¼ a b)
  (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in a..c, f x âˆ‚Î¼ = âˆ« x in c..b, f x âˆ‚Î¼ :=
sub_eq_of_eq_add' $ eq.symm $ integral_add_adjacent_intervals hac (hac.symm.trans hab)

lemma integral_interval_add_interval_comm (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in a..d, f x âˆ‚Î¼ + âˆ« x in c..b, f x âˆ‚Î¼ :=
by rw [â† integral_add_adjacent_intervals hac hcd, add_assoc, add_left_comm,
  integral_add_adjacent_intervals hac (hac.symm.trans hab), add_comm]

lemma integral_interval_sub_interval_comm (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼ - âˆ« x in b..d, f x âˆ‚Î¼ :=
by simp only [sub_eq_add_neg, â† integral_symm,
  integral_interval_add_interval_comm hab hcd.symm (hac.trans hcd)]

lemma integral_interval_sub_interval_comm' (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in d..b, f x âˆ‚Î¼ - âˆ« x in c..a, f x âˆ‚Î¼ :=
by { rw [integral_interval_sub_interval_comm hab hcd hac, integral_symm b d, integral_symm a c,
  sub_neg_eq_add, sub_eq_neg_add],  }

lemma integral_Iic_sub_Iic (ha : integrable_on f (Iic a) Î¼) (hb : integrable_on f (Iic b) Î¼) :
  âˆ« x in Iic b, f x âˆ‚Î¼ - âˆ« x in Iic a, f x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ :=
begin
  wlog hab : a â‰¤ b using [a b] tactic.skip,
  { rw [sub_eq_iff_eq_add', integral_of_le hab, â† integral_union (Iic_disjoint_Ioc (le_refl _)),
      Iic_union_Ioc_eq_Iic hab],
    exacts [is_measurable_Iic, is_measurable_Ioc, ha, hb.mono_set (Î» _, and.right)] },
  { intros ha hb,
    rw [integral_symm, â† this hb ha, neg_sub] }
end

/-- If `Î¼` is a finite measure then `âˆ« x in a..b, c âˆ‚Î¼ = (Î¼ (Iic b) - Î¼ (Iic a)) â€¢ c`. -/
lemma integral_const_of_cdf [finite_measure Î¼] (c : E) :
  âˆ« x in a..b, c âˆ‚Î¼ = ((Î¼ (Iic b)).to_real - (Î¼ (Iic a)).to_real) â€¢ c :=
begin
  simp only [sub_smul, â† set_integral_const],
  refine (integral_Iic_sub_Iic _ _).symm;
    simp only [integrable_on_const, measure_lt_top, or_true]
end

lemma integral_eq_integral_of_support_subset {f : Î± â†’ E} {a b} (h : function.support f âŠ† Ioc a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
begin
  by_cases hfm : measurable f,
  { cases le_total a b with hab hab,
    { rw [integral_of_le hab, â† integral_indicator hfm is_measurable_Ioc,
        indicator_of_support_subset h] },
    { rw [Ioc_eq_empty hab, subset_empty_iff, function.support_eq_empty_iff] at h,
      simp [h] } },
  { rw [integral_non_measurable hfm, measure_theory.integral_non_measurable hfm] },
end

end order_closed_topology

end

lemma integral_eq_zero_iff_of_le_of_nonneg_ae {f : â„ â†’ â„} {a b : â„} (hab : a â‰¤ b)
  (hf : 0 â‰¤áµ[volume.restrict (Ioc a b)] f) (hfi : interval_integrable f volume a b) :
  âˆ« x in a..b, f x = 0 â†” f =áµ[volume.restrict (Ioc a b)] 0 :=
by rw [integral_of_le hab, integral_eq_zero_iff_of_nonneg_ae hf hfi.1]

lemma integral_eq_zero_iff_of_nonneg_ae {f : â„ â†’ â„} {a b : â„}
  (hf : 0 â‰¤áµ[volume.restrict (Ioc a b âˆª Ioc b a)] f) (hfi : interval_integrable f volume a b) :
  âˆ« x in a..b, f x = 0 â†” f =áµ[volume.restrict (Ioc a b âˆª Ioc b a)] 0 :=
begin
  cases le_total a b with hab hab;
    simp only [Ioc_eq_empty hab, empty_union, union_empty] at *,
  { exact integral_eq_zero_iff_of_le_of_nonneg_ae hab hf hfi },
  { rw [integral_symm, neg_eq_zero],
    exact integral_eq_zero_iff_of_le_of_nonneg_ae hab hf hfi.symm }
end

lemma integral_pos_iff_support_of_nonneg_ae' {f : â„ â†’ â„} {a b : â„}
  (hf : 0 â‰¤áµ[volume.restrict (Ioc a b âˆª Ioc b a)] f) (hfi : interval_integrable f volume a b) :
  0 < âˆ« x in a..b, f x â†” a < b âˆ§ 0 < volume (function.support f âˆ© Ioc a b) :=
begin
  cases le_total a b with hab hab,
  { simp only [integral_of_le hab, Ioc_eq_empty hab, union_empty] at hf âŠ¢,
    symmetry,
    rw [set_integral_pos_iff_support_of_nonneg_ae hf hfi.1, and_iff_right_iff_imp],
    contrapose!,
    intro h,
    simp [Ioc_eq_empty h] },
  { rw [Ioc_eq_empty hab, empty_union] at hf,
    simp [integral_of_ge hab, Ioc_eq_empty hab, integral_nonneg_of_ae hf] }
end
  
lemma integral_pos_iff_support_of_nonneg_ae {f : â„ â†’ â„} {a b : â„}
  (hf : 0 â‰¤áµ[volume] f) (hfi : interval_integrable f volume a b) :
  0 < âˆ« x in a..b, f x â†” a < b âˆ§ 0 < volume (function.support f âˆ© Ioc a b) :=
integral_pos_iff_support_of_nonneg_ae' (ae_mono measure.restrict_le_self hf) hfi

/-!
### Fundamental theorem of calculus, part 1, for any measure

In this section we prove a few lemmas that can be seen as versions of FTC-1 for interval integral
w.r.t. any measure. Many theorems are formulated for one or two pairs of filters related by
`FTC_filter a l l'`. This typeclass has exactly four â€œrealâ€ instances: `(a, pure a, âŠ¥)`,
`(a, ğ“[Ici a] a, ğ“[Ioi a] a)`, `(a, ğ“[Iic a] a, ğ“[Iic a] a)`, `(a, ğ“ a, ğ“ a)`, and two instances
that are equal to the first and last â€œrealâ€ instances: `(a, ğ“[{a}] a, âŠ¥)` and
`(a, ğ“[univ] a, ğ“[univ] a)`.  We use this approach to avoid repeating arguments in many very similar
cases.  Lean can automatically find both `a` and `l'` based on `l`.

The most general theorem `measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae` can be seen
as a generalization of lemma `integral_has_strict_fderiv_at` below which states strict
differentiability of `âˆ« x in u..v, f x` in `(u, v)` at `(a, b)` for a measurable function `f` that
is integrable on `a..b` and is continuous at `a` and `b`. The lemma is generalized in three
directions: first, `measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae` deals with any
locally finite measure `Î¼`; second, it works for one-sided limits/derivatives; third, it assumes
only that `f` has finite limits almost surely at `a` and `b`.

Namely, let `f` be a measurable function integrable on `a..b`. Let `(la, la')` be a pair of
`FTC_filter`s around `a`; let `(lb, lb')` be a pair of `FTC_filter`s around `b`. Suppose that `f`
has finite limits `ca` and `cb` at `la' âŠ“ Î¼.ae` and `lb' âŠ“ Î¼.ae`, respectively.  Then
`âˆ« x in va..vb, f x âˆ‚Î¼ - âˆ« x in ua..ub, f x âˆ‚Î¼ = âˆ« x in ub..vb, cb âˆ‚Î¼ - âˆ« x in ua..va, ca âˆ‚Î¼ +
  o(âˆ¥âˆ« x in ua..va, (1:â„) âˆ‚Î¼âˆ¥ + âˆ¥âˆ« x in ub..vb, (1:â„) âˆ‚Î¼âˆ¥)`
as `ua` and `va` tend to `la` while `ub` and `vb` tend to `lb`.

This theorem is formulated with integral of constants instead of measures in the right hand sides
for two reasons: first, this way we avoid `min`/`max` in the statements; second, often it is
possible to write better `simp` lemmas for these integrals, see `integral_const` and
`integral_const_of_cdf`.

In the next subsection we apply this theorem to prove various theorems about differentiability
of the integral w.r.t. Lebesgue measure. -/

/-- An auxiliary typeclass for the Fundamental theorem of calculus, part 1. It is used to formulate
theorems that work simultaneously for left and right one-sided derivatives of `âˆ« x in u..v, f x`.
There are four instances: `(a, pure a, âŠ¥)`, `(a, ğ“[Ici a], ğ“[Ioi a])`,
`(a, ğ“[Iic a], ğ“[Iic a])`, and `(a, ğ“ a, ğ“ a)`. -/
class FTC_filter {Î² : Type*} [linear_order Î²] [measurable_space Î²] [topological_space Î²]
  (a : out_param Î²) (outer : filter Î²) (inner : out_param $ filter Î²)
  extends tendsto_Ixx_class Ioc outer inner : Prop :=
(pure_le : pure a â‰¤ outer)
(le_nhds : inner â‰¤ ğ“ a)
[meas_gen : is_measurably_generated inner]

/- The `dangerous_instance` linter doesn't take `out_param`s into account, so it thinks that
`FTC_filter.to_tendsto_Ixx_class` is dangerous. Disable this linter using `nolint`.
-/
attribute [nolint dangerous_instance] FTC_filter.to_tendsto_Ixx_class

namespace FTC_filter

variables [linear_order Î²] [measurable_space Î²] [topological_space Î²]

instance pure (a : Î²) : FTC_filter a (pure a) âŠ¥ :=
{ pure_le := le_refl _,
  le_nhds := bot_le }

instance nhds_within_singleton (a : Î²) : FTC_filter a (ğ“[{a}] a) âŠ¥ :=
by { rw [nhds_within, principal_singleton, inf_eq_right.2 (pure_le_nhds a)], apply_instance }

lemma finite_at_inner {a : Î²} (l : filter Î²) {l'} [h : FTC_filter a l l']
  {Î¼ : measure Î²} [locally_finite_measure Î¼] :
  Î¼.finite_at_filter l' :=
(Î¼.finite_at_nhds a).filter_mono h.le_nhds

variables [opens_measurable_space Î²] [order_topology Î²]

instance nhds (a : Î²) : FTC_filter a (ğ“ a) (ğ“ a) :=
{ pure_le := pure_le_nhds a,
  le_nhds := le_refl _ }

instance nhds_univ (a : Î²) : FTC_filter a (ğ“[univ] a) (ğ“ a) :=
by { rw nhds_within_univ, apply_instance }

instance nhds_left (a : Î²) : FTC_filter a (ğ“[Iic a] a) (ğ“[Iic a] a) :=
{ pure_le := pure_le_nhds_within right_mem_Iic,
  le_nhds := inf_le_left }

instance nhds_right (a : Î²) : FTC_filter a (ğ“[Ici a] a) (ğ“[Ioi a] a) :=
{ pure_le := pure_le_nhds_within left_mem_Ici,
  le_nhds := inf_le_left }

end FTC_filter

open asymptotics

section

variables {f : Î± â†’ E} {a b : Î±} {c ca cb : E} {l l' la la' lb lb' : filter Î±} {lt : filter Î²}
  {Î¼ : measure Î±} {u v ua va ub vb : Î² â†’ Î±}

/-- Fundamental theorem of calculus-1, local version for any measure.
Let filters `l` and `l'` be related by `tendsto_Ixx_class Ioc`.
If `f` has a finite limit `c` at `l' âŠ“ Î¼.ae`, where `Î¼` is a measure
finite at `l'`, then `âˆ« x in u..v, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, 1 âˆ‚Î¼)` as both
`u` and `v` tend to `l`.

See also `measure_integral_sub_linear_is_o_of_tendsto_ae` for a version assuming
`[FTC_filter a l l']` and `[locally_finite_measure Î¼]`. If `l` is one of `ğ“[Ici a] a`,
`ğ“[Iic a] a`, `ğ“ a`, then it's easier to apply the non-primed version.
The primed version also works, e.g., for `l = l' = at_top`.

We use integrals of constants instead of measures because this way it is easier to formulate
a statement that works in both cases `u â‰¤ v` and `v â‰¤ u`. -/
lemma measure_integral_sub_linear_is_o_of_tendsto_ae'
  [is_measurably_generated l'] [tendsto_Ixx_class Ioc l l']
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c)) (hl : Î¼.finite_at_filter l')
  (hu : tendsto u lt l) (hv : tendsto v lt l) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ - âˆ« x in u t..v t, c âˆ‚Î¼)
    (Î» t, âˆ« x in u t..v t, (1:â„) âˆ‚Î¼) lt :=
begin
  have A := (hf.integral_sub_linear_is_o_ae hfm hl).comp_tendsto (hu.Ioc hv),
  have B := (hf.integral_sub_linear_is_o_ae hfm hl).comp_tendsto (hv.Ioc hu),
  simp only [integral_const'],
  convert (A.trans_le _).sub (B.trans_le _),
  { ext t,
    simp_rw [(âˆ˜), interval_integral, sub_smul],
    abel },
  all_goals { intro t, cases le_total (u t) (v t) with huv huv; simp [huv] }
end

/-- Fundamental theorem of calculus-1, local version for any measure.
Let filters `l` and `l'` be related by `tendsto_Ixx_class Ioc`.
If `f` has a finite limit `c` at `l âŠ“ Î¼.ae`, where `Î¼` is a measure
finite at `l`, then `âˆ« x in u..v, f x âˆ‚Î¼ = Î¼ (Ioc u v) â€¢ c + o(Î¼(Ioc u v))` as both
`u` and `v` tend to `l` so that `u â‰¤ v`.

See also `measure_integral_sub_linear_is_o_of_tendsto_ae_of_le` for a version assuming
`[FTC_filter a l l']` and `[locally_finite_measure Î¼]`. If `l` is one of `ğ“[Ici a] a`,
`ğ“[Iic a] a`, `ğ“ a`, then it's easier to apply the non-primed version.
The primed version also works, e.g., for `l = l' = at_top`. -/
lemma measure_integral_sub_linear_is_o_of_tendsto_ae_of_le'
  [is_measurably_generated l'] [tendsto_Ixx_class Ioc l l']
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c)) (hl : Î¼.finite_at_filter l')
  (hu : tendsto u lt l) (hv : tendsto v lt l) (huv : u â‰¤á¶ [lt] v) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ - (Î¼ (Ioc (u t) (v t))).to_real â€¢ c)
    (Î» t, (Î¼ $ Ioc (u t) (v t)).to_real) lt :=
(measure_integral_sub_linear_is_o_of_tendsto_ae' hfm hf hl hu hv).congr'
  (huv.mono $ Î» x hx, by simp [integral_const', hx])
  (huv.mono $ Î» x hx, by simp [integral_const', hx])

/-- Fundamental theorem of calculus-1, local version for any measure.
Let filters `l` and `l'` be related by `tendsto_Ixx_class Ioc`.
If `f` has a finite limit `c` at `l âŠ“ Î¼.ae`, where `Î¼` is a measure
finite at `l`, then `âˆ« x in u..v, f x âˆ‚Î¼ = -Î¼ (Ioc v u) â€¢ c + o(Î¼(Ioc v u))` as both
`u` and `v` tend to `l` so that `v â‰¤ u`.

See also `measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge` for a version assuming
`[FTC_filter a l l']` and `[locally_finite_measure Î¼]`. If `l` is one of `ğ“[Ici a] a`,
`ğ“[Iic a] a`, `ğ“ a`, then it's easier to apply the non-primed version.
The primed version also works, e.g., for `l = l' = at_top`. -/
lemma measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge'
  [is_measurably_generated l'] [tendsto_Ixx_class Ioc l l']
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c)) (hl : Î¼.finite_at_filter l')
  (hu : tendsto u lt l) (hv : tendsto v lt l) (huv : v â‰¤á¶ [lt] u) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ + (Î¼ (Ioc (v t) (u t))).to_real â€¢ c)
    (Î» t, (Î¼ $ Ioc (v t) (u t)).to_real) lt :=
(measure_integral_sub_linear_is_o_of_tendsto_ae_of_le' hfm hf hl hv hu huv).neg_left.congr_left $
  Î» t, by simp [integral_symm (u t), add_comm]

variables [topological_space Î±]

section

variables [locally_finite_measure Î¼] [FTC_filter a l l']

include a

local attribute [instance] FTC_filter.meas_gen

/-- Fundamental theorem of calculus-1, local version for any measure.
Let filters `l` and `l'` be related by `[FTC_filter a l l']`; let `Î¼` be a locally finite measure.
If `f` has a finite limit `c` at `l' âŠ“ Î¼.ae`, then
`âˆ« x in u..v, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, 1 âˆ‚Î¼)` as both `u` and `v` tend to `l`.

See also `measure_integral_sub_linear_is_o_of_tendsto_ae'` for a version that also works, e.g., for
`l = l' = at_top`.

We use integrals of constants instead of measures because this way it is easier to formulate
a statement that works in both cases `u â‰¤ v` and `v â‰¤ u`. -/
lemma measure_integral_sub_linear_is_o_of_tendsto_ae (hfm : measurable f)
  (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c)) (hu : tendsto u lt l) (hv : tendsto v lt l) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ - âˆ« x in u t..v t, c âˆ‚Î¼)
    (Î» t, âˆ« x in u t..v t, (1:â„) âˆ‚Î¼) lt :=
measure_integral_sub_linear_is_o_of_tendsto_ae' hfm hf (FTC_filter.finite_at_inner l) hu hv

/-- Fundamental theorem of calculus-1, local version for any measure.
Let filters `l` and `l'` be related by `[FTC_filter a l l']`; let `Î¼` be a locally finite measure.
If `f` has a finite limit `c` at `l' âŠ“ Î¼.ae`, then
`âˆ« x in u..v, f x âˆ‚Î¼ = Î¼ (Ioc u v) â€¢ c + o(Î¼(Ioc u v))` as both `u` and `v` tend to `l`.

See also `measure_integral_sub_linear_is_o_of_tendsto_ae_of_le'` for a version that also works,
e.g., for `l = l' = at_top`. -/
lemma measure_integral_sub_linear_is_o_of_tendsto_ae_of_le
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c))
  (hu : tendsto u lt l) (hv : tendsto v lt l) (huv : u â‰¤á¶ [lt] v) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ - (Î¼ (Ioc (u t) (v t))).to_real â€¢ c)
    (Î» t, (Î¼ $ Ioc (u t) (v t)).to_real) lt :=
measure_integral_sub_linear_is_o_of_tendsto_ae_of_le' hfm hf (FTC_filter.finite_at_inner l)
  hu hv huv

/-- Fundamental theorem of calculus-1, local version for any measure.
Let filters `l` and `l'` be related by `[FTC_filter a l l']`; let `Î¼` be a locally finite measure.
If `f` has a finite limit `c` at `l' âŠ“ Î¼.ae`, then
`âˆ« x in u..v, f x âˆ‚Î¼ = -Î¼ (Ioc v u) â€¢ c + o(Î¼(Ioc v u))` as both `u` and `v` tend to `l`.

See also `measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge'` for a version that also works,
e.g., for `l = l' = at_top`. -/
lemma measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c))
  (hu : tendsto u lt l) (hv : tendsto v lt l) (huv : v â‰¤á¶ [lt] u) :
  is_o (Î» t, âˆ« x in u t..v t, f x âˆ‚Î¼ + (Î¼ (Ioc (v t) (u t))).to_real â€¢ c)
    (Î» t, (Î¼ $ Ioc (v t) (u t)).to_real) lt :=
measure_integral_sub_linear_is_o_of_tendsto_ae_of_ge' hfm hf (FTC_filter.finite_at_inner l)
  hu hv huv

end

variables [order_topology Î±] [borel_space Î±]

local attribute [instance] FTC_filter.meas_gen

variables [FTC_filter a la la'] [FTC_filter b lb lb'] [locally_finite_measure Î¼]

/-- Fundamental theorem of calculus-1, strict derivative in both limits for a locally finite
measure.

Let `f` be a measurable function integrable on `a..b`. Let `(la, la')` be a pair of `FTC_filter`s
around `a`; let `(lb, lb')` be a pair of `FTC_filter`s around `b`. Suppose that `f` has finite
limits `ca` and `cb` at `la' âŠ“ Î¼.ae` and `lb' âŠ“ Î¼.ae`, respectively.
Then `âˆ« x in va..vb, f x âˆ‚Î¼ - âˆ« x in ua..ub, f x âˆ‚Î¼ =
  âˆ« x in ub..vb, cb âˆ‚Î¼ - âˆ« x in ua..va, ca âˆ‚Î¼ +
    o(âˆ¥âˆ« x in ua..va, (1:â„) âˆ‚Î¼âˆ¥ + âˆ¥âˆ« x in ub..vb, (1:â„) âˆ‚Î¼âˆ¥)`
as `ua` and `va` tend to `la` while `ub` and `vb` tend to `lb`.
-/
lemma measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae
  (hab : interval_integrable f Î¼ a b)
  (ha_lim : tendsto f (la' âŠ“ Î¼.ae) (ğ“ ca)) (hb_lim : tendsto f (lb' âŠ“ Î¼.ae) (ğ“ cb))
  (hua : tendsto ua lt la) (hva : tendsto va lt la)
  (hub : tendsto ub lt lb) (hvb : tendsto vb lt lb) :
  is_o (Î» t, (âˆ« x in va t..vb t, f x âˆ‚Î¼) - (âˆ« x in ua t..ub t, f x âˆ‚Î¼) -
    (âˆ« x in ub t..vb t, cb âˆ‚Î¼ - âˆ« x in ua t..va t, ca âˆ‚Î¼))
    (Î» t, âˆ¥âˆ« x in ua t..va t, (1:â„) âˆ‚Î¼âˆ¥ + âˆ¥âˆ« x in ub t..vb t, (1:â„) âˆ‚Î¼âˆ¥) lt :=
begin
  refine
    ((measure_integral_sub_linear_is_o_of_tendsto_ae hab.measurable ha_lim hua hva).neg_left.add_add
    (measure_integral_sub_linear_is_o_of_tendsto_ae hab.measurable hb_lim hub hvb)).congr'
      _ (eventually_eq.refl _ _),
  have A : âˆ€á¶  t in lt, interval_integrable f Î¼ (ua t) (va t) :=
    ha_lim.eventually_interval_integrable_ae hab.measurable (FTC_filter.finite_at_inner la) hua hva,
  have A' : âˆ€á¶  t in lt, interval_integrable f Î¼ a (ua t) :=
    ha_lim.eventually_interval_integrable_ae hab.measurable (FTC_filter.finite_at_inner la)
      (tendsto_const_pure.mono_right FTC_filter.pure_le) hua,
  have B : âˆ€á¶  t in lt, interval_integrable f Î¼ (ub t) (vb t) :=
    hb_lim.eventually_interval_integrable_ae hab.measurable (FTC_filter.finite_at_inner lb) hub hvb,
  have B' : âˆ€á¶  t in lt, interval_integrable f Î¼ b (ub t) :=
    hb_lim.eventually_interval_integrable_ae hab.measurable (FTC_filter.finite_at_inner lb)
      (tendsto_const_pure.mono_right FTC_filter.pure_le) hub,
  filter_upwards [A, A', B, B'], simp only [mem_set_of_eq],
  intros t ua_va a_ua ub_vb b_ub,
  rw [â† integral_interval_sub_interval_comm'],
  { dsimp only [], abel },
  exacts [ub_vb, ua_va, b_ub.symm.trans $ hab.symm.trans a_ua]
end

/-- Fundamental theorem of calculus-1, strict derivative in right endpoint for a locally finite
measure.

Let `f` be a measurable function integrable on `a..b`. Let `(lb, lb')` be a pair of `FTC_filter`s
around `b`. Suppose that `f` has a finite limit `c` at `lb' âŠ“ Î¼.ae`.

Then `âˆ« x in a..v, f x âˆ‚Î¼ - âˆ« x in a..u, f x âˆ‚Î¼ = âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, (1:â„) âˆ‚Î¼)`
as `u` and `v` tend to `lb`.
-/
lemma measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_right
  (hab : interval_integrable f Î¼ a b) (hf : tendsto f (lb' âŠ“ Î¼.ae) (ğ“ c))
  (hu : tendsto u lt lb) (hv : tendsto v lt lb) :
  is_o (Î» t, âˆ« x in a..v t, f x âˆ‚Î¼ - âˆ« x in a..u t, f x âˆ‚Î¼ - âˆ« x in u t..v t, c âˆ‚Î¼)
    (Î» t, âˆ« x in u t..v t, (1:â„) âˆ‚Î¼) lt :=
by simpa using measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae
  hab ((tendsto_bot : tendsto _ âŠ¥ (ğ“ 0)).mono_left inf_le_left)
  hf (tendsto_const_pure : tendsto _ _ (pure a)) tendsto_const_pure hu hv

/-- Fundamental theorem of calculus-1, strict derivative in left endpoint for a locally finite
measure.

Let `f` be a measurable function integrable on `a..b`. Let `(la, la')` be a pair of `FTC_filter`s
around `a`. Suppose that `f` has a finite limit `c` at `la' âŠ“ Î¼.ae`.

Then `âˆ« x in v..b, f x âˆ‚Î¼ - âˆ« x in u..b, f x âˆ‚Î¼ = -âˆ« x in u..v, c âˆ‚Î¼ + o(âˆ« x in u..v, (1:â„) âˆ‚Î¼)`
as `u` and `v` tend to `la`.
-/
lemma measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_left
  (hab : interval_integrable f Î¼ a b)
  (hf : tendsto f (la' âŠ“ Î¼.ae) (ğ“ c)) (hu : tendsto u lt la) (hv : tendsto v lt la) :
  is_o (Î» t, âˆ« x in v t..b, f x âˆ‚Î¼ - âˆ« x in u t..b, f x âˆ‚Î¼ + âˆ« x in u t..v t, c âˆ‚Î¼)
    (Î» t, âˆ« x in u t..v t, (1:â„) âˆ‚Î¼) lt :=
by simpa using measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae
  hab hf ((tendsto_bot : tendsto _ âŠ¥ (ğ“ 0)).mono_left inf_le_left)
  hu hv (tendsto_const_pure : tendsto _ _ (pure b)) tendsto_const_pure

end

/-!
### Fundamental theorem of calculus-1 for Lebesgue measure

In this section we restate theorems from the previous section for Lebesgue measure.
In particular, we prove that `âˆ« x in u..v, f x` is strictly differentiable in `(u, v)`
at `(a, b)` provided that `f` is integrable on `a..b` and is continuous at `a` and `b`.
-/

variables {f : â„ â†’ E} {c ca cb : E} {l l' la la' lb lb' : filter â„} {lt : filter Î²}
  {a b z : â„} {u v ua ub va vb : Î² â†’ â„} [FTC_filter a la la'] [FTC_filter b lb lb']

/-!
#### Auxiliary `is_o` statements

In this section we prove several lemmas that can be interpreted as strict differentiability of
`(u, v) â†¦ âˆ« x in u..v, f x âˆ‚Î¼` in `u` and/or `v` at a filter. The statements use `is_o` because
we have no definition of `has_strict_(f)deriv_at_filter` in the library.
-/

/-- Fundamental theorem of calculus-1, local version. If `f` has a finite limit `c` almost surely at
`l'`, where `(l, l')` is an `FTC_filter` pair around `a`, then
`âˆ« x in u..v, f x âˆ‚Î¼ = (v - u) â€¢ c + o (v - u)` as both `u` and `v` tend to `l`. -/
lemma integral_sub_linear_is_o_of_tendsto_ae [FTC_filter a l l']
  (hfm : measurable f) (hf : tendsto f (l' âŠ“ volume.ae) (ğ“ c))
  {u v : Î² â†’ â„} (hu : tendsto u lt l) (hv : tendsto v lt l) :
  is_o (Î» t, (âˆ« x in u t..v t, f x) - (v t - u t) â€¢ c) (v - u) lt :=
by simpa [integral_const] using measure_integral_sub_linear_is_o_of_tendsto_ae hfm hf hu hv

/-- Fundamental theorem of calculus-1, strict differentiability at filter in both endpoints.
If `f` is a measurable function integrable on `a..b`, `(la, la')` is an `FTC_filter` pair around
`a`, and `(lb, lb')` is an `FTC_filter` pair around `b`, and `f` has finite limits `ca` and `cb`
almost surely at `la'` and `lb'`, respectively, then
`(âˆ« x in va..vb, f x) - âˆ« x in ua..ub, f x = (vb - ub) â€¢ cb - (va - ua) â€¢ ca +
  o(âˆ¥va - uaâˆ¥ + âˆ¥vb - ubâˆ¥)` as `ua` and `va` tend to `la` while `ub` and `vb` tend to `lb`.

This lemma could've been formulated using `has_strict_fderiv_at_filter` if we had this
definition. -/
lemma integral_sub_integral_sub_linear_is_o_of_tendsto_ae
  (hab : interval_integrable f volume a b)
  (ha_lim : tendsto f (la' âŠ“ volume.ae) (ğ“ ca)) (hb_lim : tendsto f (lb' âŠ“ volume.ae) (ğ“ cb))
  (hua : tendsto ua lt la) (hva : tendsto va lt la)
  (hub : tendsto ub lt lb) (hvb : tendsto vb lt lb) :
  is_o (Î» t, (âˆ« x in va t..vb t, f x) - (âˆ« x in ua t..ub t, f x) -
    ((vb t - ub t) â€¢ cb - (va t - ua t) â€¢ ca)) (Î» t, âˆ¥va t - ua tâˆ¥ + âˆ¥vb t - ub tâˆ¥) lt :=
by simpa [integral_const] using
measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae hab ha_lim hb_lim hua hva hub hvb

/-- Fundamental theorem of calculus-1, strict differentiability at filter in both endpoints.
If `f` is a measurable function integrable on `a..b`, `(lb, lb')` is an `FTC_filter` pair
around `b`, and `f` has a finite limit `c` almost surely at `lb'`, then
`(âˆ« x in a..v, f x) - âˆ« x in a..u, f x = (v - u) â€¢ c + o(âˆ¥v - uâˆ¥)` as `u` and `v` tend to `lb`.

This lemma could've been formulated using `has_strict_deriv_at_filter` if we had this definition. -/
lemma integral_sub_integral_sub_linear_is_o_of_tendsto_ae_right
  (hab : interval_integrable f volume a b) (hf : tendsto f (lb' âŠ“ volume.ae) (ğ“ c))
  (hu : tendsto u lt lb) (hv : tendsto v lt lb) :
  is_o (Î» t, (âˆ« x in a..v t, f x) - (âˆ« x in a..u t, f x) - (v t - u t) â€¢ c) (v - u) lt :=
by simpa only [integral_const, smul_eq_mul, mul_one] using
  measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_right hab hf hu hv

/-- Fundamental theorem of calculus-1, strict differentiability at filter in both endpoints.
If `f` is a measurable function integrable on `a..b`, `(la, la')` is an `FTC_filter` pair
around `a`, and `f` has a finite limit `c` almost surely at `la'`, then
`(âˆ« x in v..b, f x) - âˆ« x in u..b, f x = -(v - u) â€¢ c + o(âˆ¥v - uâˆ¥)` as `u` and `v` tend to `la`.

This lemma could've been formulated using `has_strict_deriv_at_filter` if we had this definition. -/
lemma integral_sub_integral_sub_linear_is_o_of_tendsto_ae_left
  (hab : interval_integrable f volume a b) (hf : tendsto f (la' âŠ“ volume.ae) (ğ“ c))
  (hu : tendsto u lt la) (hv : tendsto v lt la) :
  is_o (Î» t, (âˆ« x in v t..b, f x) - (âˆ« x in u t..b, f x) + (v t - u t) â€¢ c) (v - u) lt :=
by simpa only [integral_const, smul_eq_mul, mul_one] using
  measure_integral_sub_integral_sub_linear_is_o_of_tendsto_ae_left hab hf hu hv

open continuous_linear_map (fst snd smul_right sub_apply smul_right_apply coe_fst' coe_snd' map_sub)

/-!
#### Strict differentiability

In this section we prove that for a measurable function `f` integrable on `a..b`,

* `integral_has_strict_fderiv_at_of_tendsto_ae`: the function `(u, v) â†¦ âˆ« x in u..v, f x` has
  derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` at `(a, b)` in the sense of strict differentiability
  provided that `f` tends to `ca` and `cb` almost surely as `x` tendsto to `a` and `b`,
  respectively;

* `integral_has_strict_fderiv_at`: the function `(u, v) â†¦ âˆ« x in u..v, f x` has
  derivative `(u, v) â†¦ v â€¢ f b - u â€¢ f a` at `(a, b)` in the sense of strict differentiability
  provided that `f` is continuous at `a` and `b`;

* `integral_has_strict_deriv_at_of_tendsto_ae_right`: the function `u â†¦ âˆ« x in a..u, f x` has
  derivative `c` at `b` in the sense of strict differentiability provided that `f` tends to `c`
  almost surely as `x` tends to `b`;

* `integral_has_strict_deriv_at_right`: the function `u â†¦ âˆ« x in a..u, f x` has derivative `f b` at
  `b` in the sense of strict differentiability provided that `f` is continuous at `b`;

* `integral_has_strict_deriv_at_of_tendsto_ae_left`: the function `u â†¦ âˆ« x in u..b, f x` has
  derivative `-c` at `a` in the sense of strict differentiability provided that `f` tends to `c`
  almost surely as `x` tends to `a`;

* `integral_has_strict_deriv_at_left`: the function `u â†¦ âˆ« x in u..b, f x` has derivative `-f a` at
  `a` in the sense of strict differentiability provided that `f` is continuous at `a`.
-/

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has finite
limits `ca` and `cb` almost surely as `x` tends to `a` and `b`, respectively, then
`(u, v) â†¦ âˆ« x in u..v, f x` has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` at `(a, b)`
in the sense of strict differentiability. -/
lemma integral_has_strict_fderiv_at_of_tendsto_ae (hf : interval_integrable f volume a b)
  (ha : tendsto f (ğ“ a âŠ“ volume.ae) (ğ“ ca)) (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ cb)) :
  has_strict_fderiv_at (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x)
    ((snd â„ â„ â„).smul_right cb - (fst â„ â„ â„).smul_right ca) (a, b) :=
begin
  have := integral_sub_integral_sub_linear_is_o_of_tendsto_ae hf ha hb
    ((continuous_fst.comp continuous_snd).tendsto ((a, b), (a, b)))
    ((continuous_fst.comp continuous_fst).tendsto ((a, b), (a, b)))
    ((continuous_snd.comp continuous_snd).tendsto ((a, b), (a, b)))
    ((continuous_snd.comp continuous_fst).tendsto ((a, b), (a, b))),
  refine (this.congr_left _).trans_is_O _,
  { intro x, simp [sub_smul] },
  { exact is_O_fst_prod.norm_left.add is_O_snd_prod.norm_left }
end

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `a` and `b`, then `(u, v) â†¦ âˆ« x in u..v, f x` has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca`
at `(a, b)` in the sense of strict differentiability. -/
lemma integral_has_strict_fderiv_at (hf : interval_integrable f volume a b)
  (ha : continuous_at f a) (hb : continuous_at f b) :
  has_strict_fderiv_at (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x)
    ((snd â„ â„ â„).smul_right (f b) - (fst â„ â„ â„).smul_right (f a)) (a, b) :=
integral_has_strict_fderiv_at_of_tendsto_ae hf
  (ha.mono_left inf_le_left) (hb.mono_left inf_le_left)

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely at `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `c` at `b` in the sense
of strict differentiability. -/
lemma integral_has_strict_deriv_at_of_tendsto_ae_right (hf : interval_integrable f volume a b)
  (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ c)) : has_strict_deriv_at (Î» u, âˆ« x in a..u, f x) c b :=
integral_sub_integral_sub_linear_is_o_of_tendsto_ae_right hf hb continuous_at_snd
  continuous_at_fst

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `f b` at `b` in the sense of strict
differentiability. -/
lemma integral_has_strict_deriv_at_right (hf : interval_integrable f volume a b)
  (hb : continuous_at f b) : has_strict_deriv_at (Î» u, âˆ« x in a..u, f x) (f b) b :=
integral_has_strict_deriv_at_of_tendsto_ae_right hf (hb.mono_left inf_le_left)

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely at `a`, then `u â†¦ âˆ« x in u..b, f x` has derivative `-c` at `a` in the sense
of strict differentiability. -/
lemma integral_has_strict_deriv_at_of_tendsto_ae_left (hf : interval_integrable f volume a b)
  (ha : tendsto f (ğ“ a âŠ“ volume.ae) (ğ“ c)) : has_strict_deriv_at (Î» u, âˆ« x in u..b, f x) (-c) a :=
by simpa only [â† integral_symm]
  using (integral_has_strict_deriv_at_of_tendsto_ae_right hf.symm ha).neg

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `a`, then `u â†¦ âˆ« x in u..b, f x` has derivative `-f a` at `a` in the sense of strict
differentiability. -/
lemma integral_has_strict_deriv_at_left (hf : interval_integrable f volume a b)
  (ha : continuous_at f a) : has_strict_deriv_at (Î» u, âˆ« x in u..b, f x) (-f a) a :=
by simpa only [â† integral_symm] using (integral_has_strict_deriv_at_right hf.symm ha).neg

/-!
#### FrÃ©chet differentiability

In this subsection we restate results from the previous subsection in terms of `has_fderiv_at`,
`has_deriv_at`, `fderiv`, and `deriv`.
-/

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has finite
limits `ca` and `cb` almost surely as `x` tends to `a` and `b`, respectively, then
`(u, v) â†¦ âˆ« x in u..v, f x` has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` at `(a, b)`. -/
lemma integral_has_fderiv_at_of_tendsto_ae (hf : interval_integrable f volume a b)
  (ha : tendsto f (ğ“ a âŠ“ volume.ae) (ğ“ ca)) (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ cb)) :
  has_fderiv_at (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x)
    ((snd â„ â„ â„).smul_right cb - (fst â„ â„ â„).smul_right ca) (a, b) :=
(integral_has_strict_fderiv_at_of_tendsto_ae hf ha hb).has_fderiv_at

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `a` and `b`, then `(u, v) â†¦ âˆ« x in u..v, f x` has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca`
at `(a, b)`. -/
lemma integral_has_fderiv_at (hf : interval_integrable f volume a b)
  (ha : continuous_at f a) (hb : continuous_at f b) :
  has_fderiv_at (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x)
    ((snd â„ â„ â„).smul_right (f b) - (fst â„ â„ â„).smul_right (f a)) (a, b) :=
(integral_has_strict_fderiv_at hf ha hb).has_fderiv_at

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has finite
limits `ca` and `cb` almost surely as `x` tends to `a` and `b`, respectively, then `fderiv`
derivative of `(u, v) â†¦ âˆ« x in u..v, f x` at `(a, b)` equals `(u, v) â†¦ v â€¢ cb - u â€¢ ca`. -/
lemma fderiv_integral_of_tendsto_ae (hf : interval_integrable f volume a b)
  (ha : tendsto f (ğ“ a âŠ“ volume.ae) (ğ“ ca)) (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ cb)) :
  fderiv â„ (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x) (a, b) =
    (snd â„ â„ â„).smul_right cb - (fst â„ â„ â„).smul_right ca :=
(integral_has_fderiv_at_of_tendsto_ae hf ha hb).fderiv

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `a` and `b`, then `fderiv` derivative of `(u, v) â†¦ âˆ« x in u..v, f x` at `(a, b)` equals `(u, v) â†¦
v â€¢ cb - u â€¢ ca`. -/
lemma fderiv_integral (hf : interval_integrable f volume a b)
  (ha : continuous_at f a) (hb : continuous_at f b) :
  fderiv â„ (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x) (a, b) =
    (snd â„ â„ â„).smul_right (f b) - (fst â„ â„ â„).smul_right (f a) :=
(integral_has_fderiv_at hf ha hb).fderiv

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely at `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `c` at `b`. -/
lemma integral_has_deriv_at_of_tendsto_ae_right (hf : interval_integrable f volume a b)
  (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ c)) : has_deriv_at (Î» u, âˆ« x in a..u, f x) c b :=
(integral_has_strict_deriv_at_of_tendsto_ae_right hf hb).has_deriv_at

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `b`, then `u â†¦ âˆ« x in a..u, f x` has derivative `f b` at `b`. -/
lemma integral_has_deriv_at_right (hf : interval_integrable f volume a b)
  (hb : continuous_at f b) : has_deriv_at (Î» u, âˆ« x in a..u, f x) (f b) b :=
(integral_has_strict_deriv_at_right hf hb).has_deriv_at

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f` has a finite
limit `c` almost surely at `b`, then the derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `c`. -/
lemma deriv_integral_of_tendsto_ae_right (hf : interval_integrable f volume a b)
  (hb : tendsto f (ğ“ b âŠ“ volume.ae) (ğ“ c)) : deriv (Î» u, âˆ« x in a..u, f x) b = c :=
(integral_has_deriv_at_of_tendsto_ae_right hf hb).deriv

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `b`, then the derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `f b`. -/
lemma deriv_integral_right (hf : interval_integrable f volume a b) (hb : continuous_at f b) :
  deriv (Î» u, âˆ« x in a..u, f x) b = f b :=
(integral_has_deriv_at_right hf hb).deriv

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely at `a`, then `u â†¦ âˆ« x in u..b, f x` has derivative `-c` at `a`. -/
lemma integral_has_deriv_at_of_tendsto_ae_left (hf : interval_integrable f volume a b)
  (ha : tendsto f (ğ“ a âŠ“ volume.ae) (ğ“ c)) : has_deriv_at (Î» u, âˆ« x in u..b, f x) (-c) a :=
(integral_has_strict_deriv_at_of_tendsto_ae_left hf ha).has_deriv_at

/-- Fundamental theorem of calculus-1: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `a`, then `u â†¦ âˆ« x in u..b, f x` has derivative `-f a` at `a`. -/
lemma integral_has_deriv_at_left (hf : interval_integrable f volume a b) (ha : continuous_at f a) :
  has_deriv_at (Î» u, âˆ« x in u..b, f x) (-f a) a :=
(integral_has_strict_deriv_at_left hf ha).has_deriv_at

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f` has a finite
limit `c` almost surely at `a`, then the derivative of `u â†¦ âˆ« x in u..b, f x` at `a` equals `-c`. -/
lemma deriv_integral_of_tendsto_ae_left (hf : interval_integrable f volume a b)
  (hb : tendsto f (ğ“ a âŠ“ volume.ae) (ğ“ c)) : deriv (Î» u, âˆ« x in u..b, f x) a = -c :=
(integral_has_deriv_at_of_tendsto_ae_left hf hb).deriv

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f` is continuous
at `a`, then the derivative of `u â†¦ âˆ« x in u..b, f x` at `a` equals `-f a`. -/
lemma deriv_integral_left (hf : interval_integrable f volume a b) (hb : continuous_at f a) :
  deriv (Î» u, âˆ« x in u..b, f x) a = -f a :=
(integral_has_deriv_at_left hf hb).deriv

/-!
#### One-sided derivatives
-/

/-- Let `f` be a measurable function integrable on `a..b`. The function `(u, v) â†¦ âˆ« x in u..v, f x`
has derivative `(u, v) â†¦ v â€¢ cb - u â€¢ ca` within `s Ã— t` at `(a, b)`, where
`s âˆˆ {Iic a, {a}, Ici a, univ}` and `t âˆˆ {Iic b, {b}, Ici b, univ}` provided that `f` tends to `ca`
and `cb` almost surely at the filters `la` and `lb` from the following table.

| `s`     | `la`         | `t`     | `lb`         |
| ------- | ----         | ---     | ----         |
| `Iic a` | `ğ“[Iic a] a` | `Iic b` | `ğ“[Iic b] b` |
| `Ici a` | `ğ“[Ioi a] a` | `Ici b` | `ğ“[Ioi b] b` |
| `{a}`   | `âŠ¥`          | `{b}`   | `âŠ¥`          |
| `univ`  | `ğ“ a`        | `univ`  | `ğ“ b`        |
-/
lemma integral_has_fderiv_within_at_of_tendsto_ae (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter a (ğ“[s] a) la] [FTC_filter b (ğ“[t] b) lb]
  (ha : tendsto f (la âŠ“ volume.ae) (ğ“ ca)) (hb : tendsto f (lb âŠ“ volume.ae) (ğ“ cb)) :
  has_fderiv_within_at (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x)
    ((snd â„ â„ â„).smul_right cb - (fst â„ â„ â„).smul_right ca) (s.prod t) (a, b) :=
begin
  rw [has_fderiv_within_at, nhds_within_prod_eq],
  have := integral_sub_integral_sub_linear_is_o_of_tendsto_ae hf ha hb
    (tendsto_const_pure.mono_right FTC_filter.pure_le : tendsto _ _ (ğ“[s] a)) tendsto_fst
    (tendsto_const_pure.mono_right FTC_filter.pure_le : tendsto _ _ (ğ“[t] b)) tendsto_snd,
  refine (this.congr_left _).trans_is_O _,
  { intro x, simp [sub_smul] },
  { exact is_O_fst_prod.norm_left.add is_O_snd_prod.norm_left }
end

/-- Let `f` be a measurable function integrable on `a..b`. The function `(u, v) â†¦ âˆ« x in u..v, f x`
has derivative `(u, v) â†¦ v â€¢ f b - u â€¢ f a` within `s Ã— t` at `(a, b)`, where
`s âˆˆ {Iic a, {a}, Ici a, univ}` and `t âˆˆ {Iic b, {b}, Ici b, univ}` provided that `f` tends to
`f a` and `f b` at the filters `la` and `lb` from the following table. In most cases this assumption
is definitionally equal `continuous_at f _` or `continuous_within_at f _ _`.

| `s`     | `la`         | `t`     | `lb`         |
| ------- | ----         | ---     | ----         |
| `Iic a` | `ğ“[Iic a] a` | `Iic b` | `ğ“[Iic b] b` |
| `Ici a` | `ğ“[Ioi a] a` | `Ici b` | `ğ“[Ioi b] b` |
| `{a}`   | `âŠ¥`          | `{b}`   | `âŠ¥`          |
| `univ`  | `ğ“ a`        | `univ`  | `ğ“ b`        |
-/
lemma integral_has_fderiv_within_at (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter a (ğ“[s] a) la] [FTC_filter b (ğ“[t] b) lb]
  (ha : tendsto f la (ğ“ $ f a)) (hb : tendsto f lb (ğ“ $ f b)) :
  has_fderiv_within_at (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x)
    ((snd â„ â„ â„).smul_right (f b) - (fst â„ â„ â„).smul_right (f a)) (s.prod t) (a, b) :=
integral_has_fderiv_within_at_of_tendsto_ae hf (ha.mono_left inf_le_left)
  (hb.mono_left inf_le_left)

/-- An auxiliary tactic closing goals `unique_diff_within_at â„ s a` where
`s âˆˆ {Iic a, Ici a, univ}`. -/
meta def unique_diff_within_at_Ici_Iic_univ : tactic unit :=
`[apply_rules [unique_diff_on.unique_diff_within_at, unique_diff_on_Ici, unique_diff_on_Iic,
  left_mem_Ici, right_mem_Iic, unique_diff_within_at_univ]]

/-- Let `f` be a measurable function integrable on `a..b`. Choose `s âˆˆ {Iic a, Ici a, univ}`
and `t âˆˆ {Iic b, Ici b, univ}`. Suppose that `f` tends to `ca` and `cb` almost surely at the filters
`la` and `lb` from the table below. Then `fderiv_within â„ (Î» p, âˆ« x in p.1..p.2, f x) (s.prod t)`
is equal to `(u, v) â†¦ u â€¢ cb - v â€¢ ca`.

| `s`     | `la`         | `t`     | `lb`         |
| ------- | ----         | ---     | ----         |
| `Iic a` | `ğ“[Iic a] a` | `Iic b` | `ğ“[Iic b] b` |
| `Ici a` | `ğ“[Ioi a] a` | `Ici b` | `ğ“[Ioi b] b` |
| `univ`  | `ğ“ a`        | `univ`  | `ğ“ b`        |

-/
lemma fderiv_within_integral_of_tendsto_ae (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter a (ğ“[s] a) la] [FTC_filter b (ğ“[t] b) lb]
  (ha : tendsto f (la âŠ“ volume.ae) (ğ“ ca)) (hb : tendsto f (lb âŠ“ volume.ae) (ğ“ cb))
  (hs : unique_diff_within_at â„ s a . unique_diff_within_at_Ici_Iic_univ)
  (ht : unique_diff_within_at â„ t b . unique_diff_within_at_Ici_Iic_univ) :
  fderiv_within â„ (Î» p : â„ Ã— â„, âˆ« x in p.1..p.2, f x) (s.prod t) (a, b) =
    ((snd â„ â„ â„).smul_right cb - (fst â„ â„ â„).smul_right ca) :=
(integral_has_fderiv_within_at_of_tendsto_ae hf ha hb).fderiv_within $ hs.prod ht

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b` from the right or from the left,
then `u â†¦ âˆ« x in a..u, f x` has right (resp., left) derivative `c` at `b`. -/
lemma integral_has_deriv_within_at_of_tendsto_ae_right (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter b (ğ“[s] b) (ğ“[t] b)] (hb : tendsto f (ğ“[t] b âŠ“ volume.ae) (ğ“ c)) :
  has_deriv_within_at (Î» u, âˆ« x in a..u, f x) c s b :=
integral_sub_integral_sub_linear_is_o_of_tendsto_ae_right hf hb
  (tendsto_const_pure.mono_right FTC_filter.pure_le) tendsto_id

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
from the left or from the right at `b`, then `u â†¦ âˆ« x in a..u, f x` has left (resp., right)
derivative `f b` at `b`. -/
lemma integral_has_deriv_within_at_right (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter b (ğ“[s] b) (ğ“[t] b)] (hb : continuous_within_at f t b) :
  has_deriv_within_at (Î» u, âˆ« x in a..u, f x) (f b) s b :=
integral_has_deriv_within_at_of_tendsto_ae_right hf (hb.mono_left inf_le_left)

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `b` from the right or from the left, then the right
(resp., left) derivative of `u â†¦ âˆ« x in a..u, f x` at `b` equals `c`. -/
lemma deriv_within_integral_of_tendsto_ae_right (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter b (ğ“[s] b) (ğ“[t] b)] (hb : tendsto f (ğ“[t] b âŠ“ volume.ae) (ğ“ c))
  (hs : unique_diff_within_at â„ s b . unique_diff_within_at_Ici_Iic_univ) :
  deriv_within (Î» u, âˆ« x in a..u, f x) s b = c :=
(integral_has_deriv_within_at_of_tendsto_ae_right hf hb).deriv_within hs

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
on the right or on the left at `b`, then the right (resp., left) derivative of
`u â†¦ âˆ« x in a..u, f x` at `b` equals `f b`. -/
lemma deriv_within_integral_right (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter b (ğ“[s] b) (ğ“[t] b)] (hb : continuous_within_at f t b)
  (hs : unique_diff_within_at â„ s b . unique_diff_within_at_Ici_Iic_univ) :
  deriv_within (Î» u, âˆ« x in a..u, f x) s b = f b :=
(integral_has_deriv_within_at_right hf hb).deriv_within hs

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `a` from the right or from the left,
then `u â†¦ âˆ« x in u..b, f x` has right (resp., left) derivative `-c` at `a`. -/
lemma integral_has_deriv_within_at_of_tendsto_ae_left (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter a (ğ“[s] a) (ğ“[t] a)] (ha : tendsto f (ğ“[t] a âŠ“ volume.ae) (ğ“ c)) :
  has_deriv_within_at (Î» u, âˆ« x in u..b, f x) (-c) s a :=
by { simp only [integral_symm b],
  exact (integral_has_deriv_within_at_of_tendsto_ae_right hf.symm ha).neg }

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
from the left or from the right at `a`, then `u â†¦ âˆ« x in u..b, f x` has left (resp., right)
derivative `-f a` at `a`. -/
lemma integral_has_deriv_within_at_left (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter a (ğ“[s] a) (ğ“[t] a)] (ha : continuous_within_at f t a) :
  has_deriv_within_at (Î» u, âˆ« x in u..b, f x) (-f a) s a :=
integral_has_deriv_within_at_of_tendsto_ae_left hf (ha.mono_left inf_le_left)

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` has a finite
limit `c` almost surely as `x` tends to `a` from the right or from the left, then the right
(resp., left) derivative of `u â†¦ âˆ« x in u..b, f x` at `a` equals `-c`. -/
lemma deriv_within_integral_of_tendsto_ae_left (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter a (ğ“[s] a) (ğ“[t] a)] (ha : tendsto f (ğ“[t] a âŠ“ volume.ae) (ğ“ c))
  (hs : unique_diff_within_at â„ s a . unique_diff_within_at_Ici_Iic_univ) :
  deriv_within (Î» u, âˆ« x in u..b, f x) s a = -c :=
(integral_has_deriv_within_at_of_tendsto_ae_left hf ha).deriv_within hs

/-- Fundamental theorem of calculus: if `f : â„ â†’ E` is integrable on `a..b` and `f x` is continuous
on the right or on the left at `a`, then the right (resp., left) derivative of
`u â†¦ âˆ« x in u..b, f x` at `a` equals `-f a`. -/
lemma deriv_within_integral_left (hf : interval_integrable f volume a b)
  {s t : set â„} [FTC_filter a (ğ“[s] a) (ğ“[t] a)] (ha : continuous_within_at f t a)
  (hs : unique_diff_within_at â„ s a . unique_diff_within_at_Ici_Iic_univ) :
  deriv_within (Î» u, âˆ« x in u..b, f x) s a = -f a :=
(integral_has_deriv_within_at_left hf ha).deriv_within hs

end interval_integral
