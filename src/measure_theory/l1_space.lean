/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/
import measure_theory.ae_eq_fun

/-!
# Integrable functions and `LÂ¹` space

In the first part of this file, the predicate `integrable` is defined and basic properties of
integrable functions are proved.

In the second part, the space `LÂ¹` of equivalence classes of integrable functions under the relation
of being almost everywhere equal is defined as a subspace of the space `Lâ°`. See the file
`src/measure_theory/ae_eq_fun.lean` for information on `Lâ°` space.

## Notation

* `Î± â†’â‚ Î²` is the type of `LÂ¹` space, where `Î±` is a `measure_space` and `Î²` is a `normed_group`
  with a `second_countable_topology`. `f : Î± â†’â‚˜ Î²` is a "function" in `LÂ¹`. In comments, `[f]` is
  also used to denote an `LÂ¹` function.

  `â‚` can be typed as `\1`.

## Main definitions

* Let `f : Î± â†’ Î²` be a function, where `Î±` is a `measure_space` and `Î²` a `normed_group`.
  Then `has_finite_integral f` means `(âˆ«â» a, nnnorm (f a)) < âŠ¤`.

* If `Î²` is moreover a `measurable_space` then `f` is called `integrable` if
  `f` is `measurable` and `has_finite_integral f` holds.

* The space `LÂ¹` is defined as a subspace of `Lâ°` :
  An `ae_eq_fun` `[f] : Î± â†’â‚˜ Î²` is in the space `LÂ¹` if `edist [f] 0 < âŠ¤`, which means
  `(âˆ«â» a, edist (f a) 0) < âŠ¤` if we expand the definition of `edist` in `Lâ°`.

## Main statements

`LÂ¹`, as a subspace, inherits most of the structures of `Lâ°`.

## Implementation notes

Maybe `integrable f` should be mean `(âˆ«â» a, edist (f a) 0) < âŠ¤`, so that `integrable` and
`ae_eq_fun.integrable` are more aligned. But in the end one can use the lemma
`lintegral_nnnorm_eq_lintegral_edist : (âˆ«â» a, nnnorm (f a)) = (âˆ«â» a, edist (f a) 0)` to switch the
two forms.

To prove something for an arbitrary integrable + measurable function, a useful theorem is
`integrable.induction` in the file `set_integral`.

## Tags

integrable, function space, l1

-/

noncomputable theory
open_locale classical topological_space big_operators

open set filter topological_space ennreal emetric measure_theory

variables {Î± Î² Î³ Î´ : Type*} [measurable_space Î±] {Î¼ Î½ : measure Î±}
variables [normed_group Î²]
variables [normed_group Î³]

namespace measure_theory

/-! ### Some results about the Lebesgue integral involving a normed group -/

lemma lintegral_nnnorm_eq_lintegral_edist (f : Î± â†’ Î²) :
  âˆ«â» a, nnnorm (f a) âˆ‚Î¼ = âˆ«â» a, edist (f a) 0 âˆ‚Î¼ :=
by simp only [edist_eq_coe_nnnorm]

lemma lintegral_norm_eq_lintegral_edist (f : Î± â†’ Î²) :
  âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ = âˆ«â» a, edist (f a) 0 âˆ‚Î¼ :=
by simp only [of_real_norm_eq_coe_nnnorm, edist_eq_coe_nnnorm]

lemma lintegral_edist_triangle [second_countable_topology Î²] [measurable_space Î²]
  [opens_measurable_space Î²] {f g h : Î± â†’ Î²}
  (hf : measurable f) (hg : measurable g) (hh : measurable h) :
  âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ â‰¤ âˆ«â» a, edist (f a) (h a) âˆ‚Î¼ + âˆ«â» a, edist (g a) (h a) âˆ‚Î¼ :=
begin
  rw â† lintegral_add (hf.edist hh) (hg.edist hh),
  refine lintegral_mono (Î» a, _),
  apply edist_triangle_right
end

lemma lintegral_nnnorm_zero : âˆ«â» a : Î±, nnnorm (0 : Î²) âˆ‚Î¼ = 0 := by simp

lemma lintegral_nnnorm_add [measurable_space Î²] [opens_measurable_space Î²]
  [measurable_space Î³] [opens_measurable_space Î³]
  {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : measurable f) (hg : measurable g) :
  âˆ«â» a, nnnorm (f a) + nnnorm (g a) âˆ‚Î¼ = âˆ«â» a, nnnorm (f a) âˆ‚Î¼ + âˆ«â» a, nnnorm (g a) âˆ‚Î¼ :=
lintegral_add hf.ennnorm hg.ennnorm

lemma lintegral_nnnorm_neg {f : Î± â†’ Î²} :
  âˆ«â» a, nnnorm ((-f) a) âˆ‚Î¼ = âˆ«â» a, nnnorm (f a) âˆ‚Î¼ :=
by simp only [pi.neg_apply, nnnorm_neg]

/-! ### The predicate `has_finite_integral` -/

/-- `has_finite_integral f Î¼` means that the integral `âˆ«â» a, âˆ¥f aâˆ¥ âˆ‚Î¼` is finite.
  `has_finite_integral f` means `has_finite_integral f volume`. -/
def has_finite_integral (f : Î± â†’ Î²) (Î¼ : measure Î± . volume_tac) : Prop :=
âˆ«â» a, nnnorm (f a) âˆ‚Î¼ < âŠ¤

lemma has_finite_integral_iff_norm (f : Î± â†’ Î²) :
  has_finite_integral f Î¼ â†” âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ < âŠ¤ :=
by simp only [has_finite_integral, of_real_norm_eq_coe_nnnorm]

lemma has_finite_integral_iff_edist (f : Î± â†’ Î²) :
  has_finite_integral f Î¼ â†” âˆ«â» a, edist (f a) 0 âˆ‚Î¼ < âŠ¤ :=
by simp only [has_finite_integral_iff_norm, edist_dist, dist_zero_right]

lemma has_finite_integral_iff_of_real {f : Î± â†’ â„} (h : 0 â‰¤áµ[Î¼] f) :
  has_finite_integral f Î¼ â†” âˆ«â» a, ennreal.of_real (f a) âˆ‚Î¼ < âŠ¤ :=
have lintegral_eq : âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ = âˆ«â» a, ennreal.of_real (f a) âˆ‚Î¼ :=
begin
  refine lintegral_congr_ae (h.mono $ Î» a h, _),
  rwa [real.norm_eq_abs, abs_of_nonneg]
end,
by rw [has_finite_integral_iff_norm, lintegral_eq]

lemma has_finite_integral.mono {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hg : has_finite_integral g Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ âˆ¥g aâˆ¥) : has_finite_integral f Î¼ :=
begin
  simp only [has_finite_integral_iff_norm] at *,
  calc âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ â‰¤ âˆ«â» (a : Î±), (ennreal.of_real âˆ¥g aâˆ¥) âˆ‚Î¼ :
    lintegral_mono_ae (h.mono $ assume a h, of_real_le_of_real h)
    ... < âŠ¤ : hg
end

lemma has_finite_integral.mono' {f : Î± â†’ Î²} {g : Î± â†’ â„} (hg : has_finite_integral g Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ g a) : has_finite_integral f Î¼ :=
hg.mono $ h.mono $ Î» x hx, le_trans hx (le_abs_self _)

lemma has_finite_integral.congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : has_finite_integral f Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ = âˆ¥g aâˆ¥) :
  has_finite_integral g Î¼ :=
hf.mono $ eventually_eq.le $ eventually_eq.symm h

lemma has_finite_integral_congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ = âˆ¥g aâˆ¥) :
  has_finite_integral f Î¼ â†” has_finite_integral g Î¼ :=
âŸ¨Î» hf, hf.congr' h, Î» hg, hg.congr' $ eventually_eq.symm hâŸ©

lemma has_finite_integral.congr {f g : Î± â†’ Î²} (hf : has_finite_integral f Î¼) (h : f =áµ[Î¼] g) :
  has_finite_integral g Î¼ :=
hf.congr' $ h.fun_comp norm

lemma has_finite_integral_congr {f g : Î± â†’ Î²} (h : f =áµ[Î¼] g) :
  has_finite_integral f Î¼ â†” has_finite_integral g Î¼ :=
has_finite_integral_congr' $ h.fun_comp norm

lemma has_finite_integral_const_iff {c : Î²} :
  has_finite_integral (Î» x : Î±, c) Î¼ â†” c = 0 âˆ¨ Î¼ univ < âŠ¤ :=
begin
  simp only [has_finite_integral, lintegral_const],
  by_cases hc : c = 0,
  { simp [hc] },
  { simp only [hc, false_or],
    refine âŸ¨Î» h, _, Î» h, mul_lt_top coe_lt_top hâŸ©,
    replace h := mul_lt_top (@coe_lt_top $ (nnnorm c)â»Â¹) h,
    rwa [â† mul_assoc, â† coe_mul, _root_.inv_mul_cancel, coe_one, one_mul] at h,
    rwa [ne.def, nnnorm_eq_zero] }
end

lemma has_finite_integral_const [finite_measure Î¼] (c : Î²) : has_finite_integral (Î» x : Î±, c) Î¼ :=
has_finite_integral_const_iff.2 (or.inr $ measure_lt_top _ _)

lemma has_finite_integral_of_bounded [finite_measure Î¼] {f : Î± â†’ Î²} {C : â„}
  (hC : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ C) : has_finite_integral f Î¼ :=
(has_finite_integral_const C).mono' hC

lemma has_finite_integral.mono_measure {f : Î± â†’ Î²} (h : has_finite_integral f Î½) (hÎ¼ : Î¼ â‰¤ Î½) :
  has_finite_integral f Î¼ :=
lt_of_le_of_lt (lintegral_mono' hÎ¼ (le_refl _)) h

lemma has_finite_integral.add_measure {f : Î± â†’ Î²} (hÎ¼ : has_finite_integral f Î¼)
  (hÎ½ : has_finite_integral f Î½) : has_finite_integral f (Î¼ + Î½) :=
begin
  simp only [has_finite_integral, lintegral_add_measure] at *,
  exact add_lt_top.2 âŸ¨hÎ¼, hÎ½âŸ©
end

lemma has_finite_integral.left_of_add_measure {f : Î± â†’ Î²} (h : has_finite_integral f (Î¼ + Î½)) :
  has_finite_integral f Î¼ :=
h.mono_measure $ measure.le_add_right $ le_refl _

lemma has_finite_integral.right_of_add_measure {f : Î± â†’ Î²} (h : has_finite_integral f (Î¼ + Î½)) :
  has_finite_integral f Î½ :=
h.mono_measure $ measure.le_add_left $ le_refl _

@[simp] lemma has_finite_integral_add_measure {f : Î± â†’ Î²} :
  has_finite_integral f (Î¼ + Î½) â†” has_finite_integral f Î¼ âˆ§ has_finite_integral f Î½ :=
âŸ¨Î» h, âŸ¨h.left_of_add_measure, h.right_of_add_measureâŸ©, Î» h, h.1.add_measure h.2âŸ©

lemma has_finite_integral.smul_measure {f : Î± â†’ Î²} (h : has_finite_integral f Î¼) {c : ennreal}
  (hc : c < âŠ¤) : has_finite_integral f (c â€¢ Î¼) :=
begin
  simp only [has_finite_integral, lintegral_smul_measure] at *,
  exact mul_lt_top hc h
end

@[simp] lemma has_finite_integral_zero_measure (f : Î± â†’ Î²) : has_finite_integral f 0 :=
by simp only [has_finite_integral, lintegral_zero_measure, with_top.zero_lt_top]

variables (Î± Î² Î¼)
@[simp] lemma has_finite_integral_zero : has_finite_integral (Î»a:Î±, (0:Î²)) Î¼ :=
by simp [has_finite_integral]
variables {Î± Î² Î¼}

lemma has_finite_integral.neg {f : Î± â†’ Î²} (hfi : has_finite_integral f Î¼) :
  has_finite_integral (-f) Î¼ :=
by simpa [has_finite_integral] using hfi

@[simp] lemma has_finite_integral_neg_iff {f : Î± â†’ Î²} :
  has_finite_integral (-f) Î¼ â†” has_finite_integral f Î¼ :=
âŸ¨Î» h, neg_neg f â–¸ h.neg, has_finite_integral.negâŸ©

lemma has_finite_integral.norm {f : Î± â†’ Î²} (hfi : has_finite_integral f Î¼) :
  has_finite_integral (Î»a, âˆ¥f aâˆ¥) Î¼ :=
have eq : (Î»a, (nnnorm âˆ¥f aâˆ¥ : ennreal)) = Î»a, (nnnorm (f a) : ennreal),
  by { funext, rw nnnorm_norm },
by { rwa [has_finite_integral, eq] }

lemma has_finite_integral_norm_iff (f : Î± â†’ Î²) :
  has_finite_integral (Î»a, âˆ¥f aâˆ¥) Î¼ â†” has_finite_integral f Î¼ :=
has_finite_integral_congr' $ eventually_of_forall $ Î» x, norm_norm (f x)

section dominated_convergence

variables {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}

lemma all_ae_of_real_F_le_bound (h : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a) :
  âˆ€ n, âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥F n aâˆ¥ â‰¤ ennreal.of_real (bound a) :=
Î»n, (h n).mono $ Î» a h, ennreal.of_real_le_of_real h

lemma all_ae_tendsto_of_real_norm (h : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top $ ğ“ $ f a) :
  âˆ€áµ a âˆ‚Î¼, tendsto (Î»n, ennreal.of_real âˆ¥F n aâˆ¥) at_top $ ğ“ $ ennreal.of_real âˆ¥f aâˆ¥ :=
h.mono $
  Î» a h, tendsto_of_real $ tendsto.comp (continuous.tendsto continuous_norm _) h

lemma all_ae_of_real_f_le_bound (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥f aâˆ¥ â‰¤ ennreal.of_real (bound a) :=
begin
  have F_le_bound := all_ae_of_real_F_le_bound h_bound,
  rw â† ae_all_iff at F_le_bound,
  apply F_le_bound.mp ((all_ae_tendsto_of_real_norm h_lim).mono _),
  assume a tendsto_norm F_le_bound,
  exact le_of_tendsto' tendsto_norm (F_le_bound)
end

lemma has_finite_integral_of_dominated_convergence {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}
  (bound_has_finite_integral : has_finite_integral bound Î¼)
  (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  has_finite_integral f Î¼ :=
/- `âˆ¥F n aâˆ¥ â‰¤ bound a` and `âˆ¥F n aâˆ¥ --> âˆ¥f aâˆ¥` implies `âˆ¥f aâˆ¥ â‰¤ bound a`,
  and so `âˆ« âˆ¥fâˆ¥ â‰¤ âˆ« bound < âŠ¤` since `bound` is has_finite_integral -/
begin
  rw has_finite_integral_iff_norm,
  calc âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ â‰¤ âˆ«â» a, ennreal.of_real (bound a) âˆ‚Î¼ :
    lintegral_mono_ae $ all_ae_of_real_f_le_bound h_bound h_lim
    ... < âŠ¤ :
    begin
      rw â† has_finite_integral_iff_of_real,
      { exact bound_has_finite_integral },
      exact (h_bound 0).mono (Î» a h, le_trans (norm_nonneg _) h)
    end
end

lemma tendsto_lintegral_norm_of_dominated_convergence [measurable_space Î²]
  [borel_space Î²] [second_countable_topology Î²]
  {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}
  (F_measurable : âˆ€ n, measurable (F n))
  (f_measurable : measurable f)
  (bound_has_finite_integral : has_finite_integral bound Î¼)
  (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  tendsto (Î»n, âˆ«â» a, (ennreal.of_real âˆ¥F n a - f aâˆ¥) âˆ‚Î¼) at_top (ğ“ 0) :=
let b := Î»a, 2 * ennreal.of_real (bound a) in
/- `âˆ¥F n aâˆ¥ â‰¤ bound a` and `F n a --> f a` implies `âˆ¥f aâˆ¥ â‰¤ bound a`, and thus by the
  triangle inequality, have `âˆ¥F n a - f aâˆ¥ â‰¤ 2 * (bound a). -/
have hb : âˆ€ n, âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥F n a - f aâˆ¥ â‰¤ b a,
begin
  assume n,
  filter_upwards [all_ae_of_real_F_le_bound h_bound n, all_ae_of_real_f_le_bound h_bound h_lim],
  assume a hâ‚ hâ‚‚,
  calc ennreal.of_real âˆ¥F n a - f aâˆ¥ â‰¤ (ennreal.of_real âˆ¥F n aâˆ¥) + (ennreal.of_real âˆ¥f aâˆ¥) :
  begin
    rw [â† ennreal.of_real_add],
    apply of_real_le_of_real,
    { apply norm_sub_le }, { exact norm_nonneg _ }, { exact norm_nonneg _ }
  end
    ... â‰¤ (ennreal.of_real (bound a)) + (ennreal.of_real (bound a)) : add_le_add hâ‚ hâ‚‚
    ... = b a : by rw â† two_mul
end,
/- On the other hand, `F n a --> f a` implies that `âˆ¥F n a - f aâˆ¥ --> 0`  -/
have h : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, ennreal.of_real âˆ¥F n a - f aâˆ¥) at_top (ğ“ 0),
begin
  rw â† ennreal.of_real_zero,
  refine h_lim.mono (Î» a h, (continuous_of_real.tendsto _).comp _),
  rwa â† tendsto_iff_norm_tendsto_zero
end,
/- Therefore, by the dominated convergence theorem for nonnegative integration, have
  ` âˆ« âˆ¥f a - F n aâˆ¥ --> 0 ` -/
begin
  suffices h : tendsto (Î»n, âˆ«â» a, (ennreal.of_real âˆ¥F n a - f aâˆ¥) âˆ‚Î¼) at_top (ğ“ (âˆ«â» (a:Î±), 0 âˆ‚Î¼)),
  { rwa lintegral_zero at h },
  -- Using the dominated convergence theorem.
  refine tendsto_lintegral_of_dominated_convergence _ _ hb _ _,
  -- Show `Î»a, âˆ¥f a - F n aâˆ¥` is measurable for all `n`
  { exact Î»n, measurable_of_real.comp ((F_measurable n).sub f_measurable).norm },
  -- Show `2 * bound` is has_finite_integral
  { rw has_finite_integral_iff_of_real at bound_has_finite_integral,
    { calc âˆ«â» a, b a âˆ‚Î¼ = 2 * âˆ«â» a, ennreal.of_real (bound a) âˆ‚Î¼ :
        by { rw lintegral_const_mul', exact coe_ne_top }
        ... < âŠ¤ : mul_lt_top (coe_lt_top) bound_has_finite_integral },
    filter_upwards [h_bound 0] Î» a h, le_trans (norm_nonneg _) h },
  -- Show `âˆ¥f a - F n aâˆ¥ --> 0`
  { exact h }
end

end dominated_convergence

section pos_part
/-! Lemmas used for defining the positive part of a `LÂ¹` function -/

lemma has_finite_integral.max_zero {f : Î± â†’ â„} (hf : has_finite_integral f Î¼) :
  has_finite_integral (Î»a, max (f a) 0) Î¼ :=
hf.mono $ eventually_of_forall $ Î» x, by simp [real.norm_eq_abs, abs_le, abs_nonneg, le_abs_self]

lemma has_finite_integral.min_zero {f : Î± â†’ â„} (hf : has_finite_integral f Î¼) :
  has_finite_integral (Î»a, min (f a) 0) Î¼ :=
hf.mono $ eventually_of_forall $ Î» x,
  by simp [real.norm_eq_abs, abs_le, abs_nonneg, neg_le, neg_le_abs_self]

end pos_part

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma has_finite_integral.smul (c : ğ•œ) {f : Î± â†’ Î²} : has_finite_integral f Î¼ â†’
  has_finite_integral (c â€¢ f) Î¼ :=
begin
  simp only [has_finite_integral], assume hfi,
  calc
    âˆ«â» (a : Î±), nnnorm (c â€¢ f a) âˆ‚Î¼ = âˆ«â» (a : Î±), (nnnorm c) * nnnorm (f a) âˆ‚Î¼ :
      by simp only [nnnorm_smul, ennreal.coe_mul]
    ... < âŠ¤ :
    begin
      rw lintegral_const_mul',
      exacts [mul_lt_top coe_lt_top hfi, coe_ne_top]
    end
end

lemma has_finite_integral_smul_iff {c : ğ•œ} (hc : c â‰  0) (f : Î± â†’ Î²) :
  has_finite_integral (c â€¢ f) Î¼ â†” has_finite_integral f Î¼ :=
begin
  split,
  { assume h,
    simpa only [smul_smul, inv_mul_cancel hc, one_smul] using h.smul câ»Â¹ },
  exact has_finite_integral.smul _
end

lemma has_finite_integral.const_mul {f : Î± â†’ â„} (h : has_finite_integral f Î¼) (c : â„) :
  has_finite_integral (Î» x, c * f x) Î¼ :=
(has_finite_integral.smul c h : _)

lemma has_finite_integral.mul_const {f : Î± â†’ â„} (h : has_finite_integral f Î¼) (c : â„) :
  has_finite_integral (Î» x, f x * c) Î¼ :=
by simp_rw [mul_comm, h.const_mul _]
end normed_space

/-! ### The predicate `integrable` -/

variables [measurable_space Î²] [measurable_space Î³] [measurable_space Î´]

/-- `integrable f Î¼` means that `f` is measurable and that the integral `âˆ«â» a, âˆ¥f aâˆ¥ âˆ‚Î¼` is finite.
  `integrable f` means `integrable f volume`. -/
def integrable (f : Î± â†’ Î²) (Î¼ : measure Î± . volume_tac) : Prop :=
measurable f âˆ§ has_finite_integral f Î¼

lemma integrable.measurable {f : Î± â†’ Î²} (hf : integrable f Î¼) : measurable f := hf.1
lemma integrable.has_finite_integral {f : Î± â†’ Î²} (hf : integrable f Î¼) : has_finite_integral f Î¼ :=
hf.2

lemma integrable.mono {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hg : integrable g Î¼) (hf : measurable f)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ âˆ¥g aâˆ¥) : integrable f Î¼ :=
âŸ¨hf, hg.has_finite_integral.mono hâŸ©

lemma integrable.mono' {f : Î± â†’ Î²} {g : Î± â†’ â„} (hg : integrable g Î¼) (hf : measurable f)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ g a) : integrable f Î¼ :=
âŸ¨hf, hg.has_finite_integral.mono' hâŸ©

lemma integrable.congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : integrable f Î¼) (hg : measurable g)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ = âˆ¥g aâˆ¥) : integrable g Î¼ :=
âŸ¨hg, hf.has_finite_integral.congr' hâŸ©

lemma integrable_congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : measurable f) (hg : measurable g)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ = âˆ¥g aâˆ¥) : integrable f Î¼ â†” integrable g Î¼ :=
âŸ¨Î» h2f, h2f.congr' hg h, Î» h2g, h2g.congr' hf $ eventually_eq.symm hâŸ©

lemma integrable.congr {f g : Î± â†’ Î²} (hf : integrable f Î¼) (hg : measurable g) (h : f =áµ[Î¼] g) :
  integrable g Î¼ :=
hf.congr' hg $ h.fun_comp norm

lemma integrable_congr {f g : Î± â†’ Î²} (hf : measurable f) (hg : measurable g) (h : f =áµ[Î¼] g) :
  integrable f Î¼ â†” integrable g Î¼ :=
integrable_congr' hf hg $ h.fun_comp norm

lemma integrable_const_iff {c : Î²} : integrable (Î» x : Î±, c) Î¼ â†” c = 0 âˆ¨ Î¼ univ < âŠ¤ :=
by rw [integrable, and_iff_right measurable_const, has_finite_integral_const_iff]

lemma integrable_const [finite_measure Î¼] (c : Î²) : integrable (Î» x : Î±, c) Î¼ :=
integrable_const_iff.2 $ or.inr $ measure_lt_top _ _

lemma integrable.mono_measure {f : Î± â†’ Î²} (h : integrable f Î½) (hÎ¼ : Î¼ â‰¤ Î½) : integrable f Î¼ :=
âŸ¨h.measurable, h.has_finite_integral.mono_measure hÎ¼âŸ©

lemma integrable.add_measure {f : Î± â†’ Î²} (hÎ¼ : integrable f Î¼) (hÎ½ : integrable f Î½) :
  integrable f (Î¼ + Î½) :=
âŸ¨hÎ¼.measurable, hÎ¼.has_finite_integral.add_measure hÎ½.has_finite_integralâŸ©

lemma integrable.left_of_add_measure {f : Î± â†’ Î²} (h : integrable f (Î¼ + Î½)) : integrable f Î¼ :=
h.mono_measure $ measure.le_add_right $ le_refl _

lemma integrable.right_of_add_measure {f : Î± â†’ Î²} (h : integrable f (Î¼ + Î½)) : integrable f Î½ :=
h.mono_measure $ measure.le_add_left $ le_refl _

@[simp] lemma integrable_add_measure {f : Î± â†’ Î²} :
  integrable f (Î¼ + Î½) â†” integrable f Î¼ âˆ§ integrable f Î½ :=
âŸ¨Î» h, âŸ¨h.left_of_add_measure, h.right_of_add_measureâŸ©, Î» h, h.1.add_measure h.2âŸ©

lemma integrable.smul_measure {f : Î± â†’ Î²} (h : integrable f Î¼) {c : ennreal} (hc : c < âŠ¤) :
  integrable f (c â€¢ Î¼) :=
âŸ¨h.measurable, h.has_finite_integral.smul_measure hcâŸ©

lemma integrable_map_measure [opens_measurable_space Î²] {f : Î± â†’ Î´} {g : Î´ â†’ Î²}
  (hf : measurable f) (hg : measurable g) :
  integrable g (measure.map f Î¼) â†” integrable (g âˆ˜ f) Î¼ :=
by { simp only [integrable, has_finite_integral, lintegral_map hg.ennnorm hf, hf, hg, hg.comp hf] }

lemma lintegral_edist_lt_top [second_countable_topology Î²] [opens_measurable_space Î²] {f g : Î± â†’ Î²}
  (hf : integrable f Î¼) (hg : integrable g Î¼) :
  âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ < âŠ¤ :=
lt_of_le_of_lt
  (lintegral_edist_triangle hf.measurable hg.measurable
    (measurable_const : measurable (Î»a, (0 : Î²))))
  (ennreal.add_lt_top.2 $ by { simp_rw â† has_finite_integral_iff_edist,
                               exact âŸ¨hf.has_finite_integral, hg.has_finite_integralâŸ© })


variables (Î± Î² Î¼)
@[simp] lemma integrable_zero : integrable (Î» _, (0 : Î²)) Î¼ :=
by simp [integrable, measurable_const]
variables {Î± Î² Î¼}

lemma integrable.add' [opens_measurable_space Î²] {f g : Î± â†’ Î²} (hf : integrable f Î¼)
  (hg : integrable g Î¼) :
  has_finite_integral (f + g) Î¼ :=
calc âˆ«â» a, nnnorm (f a + g a) âˆ‚Î¼ â‰¤ âˆ«â» a, nnnorm (f a) + nnnorm (g a) âˆ‚Î¼ :
  lintegral_mono (Î» a, by exact_mod_cast nnnorm_add_le _ _)
... = _ : lintegral_nnnorm_add hf.measurable hg.measurable
... < âŠ¤ : add_lt_top.2 âŸ¨hf.has_finite_integral, hg.has_finite_integralâŸ©

lemma integrable.add [borel_space Î²] [second_countable_topology Î²]
  {f g : Î± â†’ Î²} (hf : integrable f Î¼) (hg : integrable g Î¼) : integrable (f + g) Î¼ :=
âŸ¨hf.measurable.add hg.measurable, hf.add' hgâŸ©

lemma integrable_finset_sum {Î¹} [borel_space Î²] [second_countable_topology Î²] (s : finset Î¹)
  {f : Î¹ â†’ Î± â†’ Î²} (hf : âˆ€ i, integrable (f i) Î¼) : integrable (Î» a, âˆ‘ i in s, f i a) Î¼ :=
begin
  refine finset.induction_on s _ _,
  { simp only [finset.sum_empty, integrable_zero] },
  { assume i s his ih, simp only [his, finset.sum_insert, not_false_iff],
    exact (hf _).add ih }
end

lemma integrable.neg [borel_space Î²] {f : Î± â†’ Î²} (hf : integrable f Î¼) : integrable (-f) Î¼ :=
âŸ¨hf.measurable.neg, hf.has_finite_integral.negâŸ©

@[simp] lemma integrable_neg_iff [borel_space Î²] {f : Î± â†’ Î²} : integrable (-f) Î¼ â†” integrable f Î¼ :=
âŸ¨Î» h, neg_neg f â–¸ h.neg, integrable.negâŸ©

lemma integrable.sub' [opens_measurable_space Î²] {f g : Î± â†’ Î²}
  (hf : integrable f Î¼) (hg : integrable g Î¼) : has_finite_integral (f - g) Î¼ :=
calc âˆ«â» a, nnnorm (f a - g a) âˆ‚Î¼ â‰¤ âˆ«â» a, nnnorm (f a) + nnnorm (-g a) âˆ‚Î¼ :
  lintegral_mono (assume a, by exact_mod_cast nnnorm_add_le _ _ )
... = _ :
  by { simp only [nnnorm_neg], exact lintegral_nnnorm_add hf.measurable hg.measurable }
... < âŠ¤ : add_lt_top.2 âŸ¨hf.has_finite_integral, hg.has_finite_integralâŸ©

lemma integrable.sub [borel_space Î²] [second_countable_topology Î²] {f g : Î± â†’ Î²}
  (hf : integrable f Î¼) (hg : integrable g Î¼) : integrable (f - g) Î¼ :=
hf.add hg.neg

lemma integrable.norm [opens_measurable_space Î²] {f : Î± â†’ Î²} (hf : integrable f Î¼) :
  integrable (Î»a, âˆ¥f aâˆ¥) Î¼ :=
âŸ¨hf.measurable.norm, hf.has_finite_integral.normâŸ©

lemma integrable_norm_iff [opens_measurable_space Î²] {f : Î± â†’ Î²} (hf : measurable f) :
  integrable (Î»a, âˆ¥f aâˆ¥) Î¼ â†” integrable f Î¼ :=
by simp_rw [integrable, and_iff_right hf, and_iff_right hf.norm, has_finite_integral_norm_iff]

lemma integrable.prod_mk [opens_measurable_space Î²] [opens_measurable_space Î³] {f : Î± â†’ Î²}
  {g : Î± â†’ Î³} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  integrable (Î» x, (f x, g x)) Î¼ :=
âŸ¨hf.measurable.prod_mk hg.measurable,
  (hf.norm.add' hg.norm).mono $ eventually_of_forall $ Î» x,
  calc max âˆ¥f xâˆ¥ âˆ¥g xâˆ¥ â‰¤ âˆ¥f xâˆ¥ + âˆ¥g xâˆ¥   : max_le_add_of_nonneg (norm_nonneg _) (norm_nonneg _)
                 ... â‰¤ âˆ¥(âˆ¥f xâˆ¥ + âˆ¥g xâˆ¥)âˆ¥ : le_abs_self _âŸ©

section pos_part
/-! ### Lemmas used for defining the positive part of a `LÂ¹` function -/

lemma integrable.max_zero {f : Î± â†’ â„} (hf : integrable f Î¼) : integrable (Î»a, max (f a) 0) Î¼ :=
âŸ¨hf.measurable.max measurable_const, hf.has_finite_integral.max_zeroâŸ©

lemma integrable.min_zero {f : Î± â†’ â„} (hf : integrable f Î¼) : integrable (Î»a, min (f a) 0) Î¼ :=
âŸ¨hf.measurable.min measurable_const, hf.has_finite_integral.min_zeroâŸ©

end pos_part

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma integrable.smul [borel_space Î²] (c : ğ•œ) {f : Î± â†’ Î²}
  (hf : integrable f Î¼) : integrable (c â€¢ f) Î¼ :=
âŸ¨hf.measurable.const_smul c, hf.has_finite_integral.smul câŸ©

lemma integrable_smul_iff [borel_space Î²] {c : ğ•œ} (hc : c â‰  0) (f : Î± â†’ Î²) :
  integrable (c â€¢ f) Î¼ â†” integrable f Î¼ :=
and_congr (measurable_const_smul_iff hc) (has_finite_integral_smul_iff hc f)

lemma integrable.const_mul {f : Î± â†’ â„} (h : integrable f Î¼) (c : â„) : integrable (Î» x, c * f x) Î¼ :=
integrable.smul c h

lemma integrable.mul_const {f : Î± â†’ â„} (h : integrable f Î¼) (c : â„) : integrable (Î» x, f x * c) Î¼ :=
by simp_rw [mul_comm, h.const_mul _]

end normed_space

section normed_space_over_complete_field
variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] [complete_space ğ•œ] [measurable_space ğ•œ]
variables [borel_space ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E] [measurable_space E] [borel_space E]

lemma integrable_smul_const {f : Î± â†’ ğ•œ} {c : E} (hc : c â‰  0) :
  integrable (Î» x, f x â€¢ c) Î¼ â†” integrable f Î¼ :=
begin
  simp_rw [integrable, measurable_smul_const hc, and.congr_right_iff, has_finite_integral,
    nnnorm_smul, ennreal.coe_mul],
  intro hf, rw [lintegral_mul_const' _ _ ennreal.coe_ne_top, ennreal.mul_lt_top_iff],
  have : âˆ€ x : ennreal, x = 0 â†’ x < âŠ¤ := by simp,
  simp [hc, or_iff_left_of_imp (this _)]
end
end normed_space_over_complete_field


variables [second_countable_topology Î²]

/-! ### The predicate `integrable` on measurable functions modulo a.e.-equality -/

namespace ae_eq_fun

section

variable [opens_measurable_space Î²]

/-- A class of almost everywhere equal functions is `integrable` if it has a finite distance to
  the origin. It means the same thing as the predicate `integrable` over functions. -/
def integrable (f : Î± â†’â‚˜[Î¼] Î²) : Prop := f âˆˆ ball (0 : Î± â†’â‚˜[Î¼] Î²) âŠ¤

lemma integrable_mk {f : Î± â†’ Î²} (hf : measurable f) :
  (integrable (mk f hf : Î± â†’â‚˜[Î¼] Î²)) â†” measure_theory.integrable f Î¼ :=
by simp [integrable, zero_def, edist_mk_mk', measure_theory.integrable, nndist_eq_nnnorm,
         has_finite_integral, hf]

lemma integrable_coe_fn {f : Î± â†’â‚˜[Î¼] Î²} : (measure_theory.integrable f Î¼) â†” integrable f :=
by rw [â† integrable_mk, mk_coe_fn]

lemma integrable_zero : integrable (0 : Î± â†’â‚˜[Î¼] Î²) := mem_ball_self coe_lt_top

end

section

variable [borel_space Î²]

lemma integrable.add {f g : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable g â†’ integrable (f + g) :=
begin
  refine induction_onâ‚‚ f g (Î» f hf g hg hfi hgi, _),
  simp only [integrable_mk, mk_add_mk] at hfi hgi âŠ¢,
  exact hfi.add hgi
end

lemma integrable.neg {f : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable (-f) :=
induction_on f $ Î» f hfm hfi, (integrable_mk _).2 ((integrable_mk hfm).1 hfi).neg

lemma integrable.sub {f g : Î± â†’â‚˜[Î¼] Î²} (hf : integrable f) (hg : integrable g) :
  integrable (f - g) :=
hf.add hg.neg

protected lemma is_add_subgroup : is_add_subgroup (ball (0 : Î± â†’â‚˜[Î¼] Î²) âŠ¤) :=
{ zero_mem := integrable_zero,
  add_mem := Î» _ _, integrable.add,
  neg_mem := Î» _, integrable.neg }

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma integrable.smul {c : ğ•œ} {f : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable (c â€¢ f) :=
induction_on f $ Î» f hfm hfi, (integrable_mk _).2 $ ((integrable_mk hfm).1 hfi).smul _

end normed_space

end

end ae_eq_fun

/-! ### The `LÂ¹` space of functions -/

variables (Î± Î²)
/-- The space of equivalence classes of integrable (and measurable) functions, where two integrable
    functions are equivalent if they agree almost everywhere, i.e., they differ on a set of measure
    `0`. -/
def l1 [opens_measurable_space Î²] (Î¼ : measure Î±) : Type* :=
{f : Î± â†’â‚˜[Î¼] Î² // f.integrable}

notation Î± ` â†’â‚[`:25 Î¼ `] ` Î² := l1 Î± Î² Î¼

variables {Î± Î²}

namespace l1
open ae_eq_fun
local attribute [instance] ae_eq_fun.is_add_subgroup

section

variable [opens_measurable_space Î²]

instance : has_coe (Î± â†’â‚[Î¼] Î²) (Î± â†’â‚˜[Î¼] Î²) := coe_subtype
instance : has_coe_to_fun (Î± â†’â‚[Î¼] Î²) := âŸ¨Î» f, Î± â†’ Î², Î» f, â‡‘(f : Î± â†’â‚˜[Î¼] Î²)âŸ©

@[simp, norm_cast] lemma coe_coe (f : Î± â†’â‚[Î¼] Î²) : â‡‘(f : Î± â†’â‚˜[Î¼] Î²) = f := rfl

protected lemma eq {f g : Î± â†’â‚[Î¼] Î²} : (f : Î± â†’â‚˜[Î¼] Î²) = (g : Î± â†’â‚˜[Î¼] Î²) â†’ f = g := subtype.eq
@[norm_cast] protected lemma eq_iff {f g : Î± â†’â‚[Î¼] Î²} : (f : Î± â†’â‚˜[Î¼] Î²) = (g : Î± â†’â‚˜[Î¼] Î²) â†” f = g :=
iff.intro (l1.eq) (congr_arg coe)

/- TODO : order structure of l1-/

/-- `LÂ¹` space forms a `emetric_space`, with the emetric being inherited from almost everywhere
  functions, i.e., `edist f g = âˆ«â» a, edist (f a) (g a)`. -/
instance : emetric_space (Î± â†’â‚[Î¼] Î²) := subtype.emetric_space

/-- `LÂ¹` space forms a `metric_space`, with the metric being inherited from almost everywhere
  functions, i.e., `edist f g = ennreal.to_real (âˆ«â» a, edist (f a) (g a))`. -/
instance : metric_space (Î± â†’â‚[Î¼] Î²) := metric_space_emetric_ball 0 âŠ¤

end

variable [borel_space Î²]

instance : add_comm_group (Î± â†’â‚[Î¼] Î²) := subtype.add_comm_group

instance : inhabited (Î± â†’â‚[Î¼] Î²) := âŸ¨0âŸ©

@[simp, norm_cast] lemma coe_zero : ((0 : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = 0 := rfl
@[simp, norm_cast]
lemma coe_add (f g : Î± â†’â‚[Î¼] Î²) : ((f + g : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = f + g := rfl
@[simp, norm_cast] lemma coe_neg (f : Î± â†’â‚[Î¼] Î²) : ((-f : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = -f := rfl
@[simp, norm_cast]
lemma coe_sub (f g : Î± â†’â‚[Î¼] Î²) : ((f - g : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = f - g := rfl

@[simp] lemma edist_eq (f g : Î± â†’â‚[Î¼] Î²) : edist f g = edist (f : Î± â†’â‚˜[Î¼] Î²) (g : Î± â†’â‚˜[Î¼] Î²) := rfl

lemma dist_eq (f g : Î± â†’â‚[Î¼] Î²) :
  dist f g = ennreal.to_real (edist (f : Î± â†’â‚˜[Î¼] Î²) (g : Î± â†’â‚˜[Î¼] Î²)) :=
rfl

/-- The norm on `LÂ¹` space is defined to be `âˆ¥fâˆ¥ = âˆ«â» a, edist (f a) 0`. -/
instance : has_norm (Î± â†’â‚[Î¼] Î²) := âŸ¨Î» f, dist f 0âŸ©

lemma norm_eq (f : Î± â†’â‚[Î¼] Î²) : âˆ¥fâˆ¥ = ennreal.to_real (edist (f : Î± â†’â‚˜[Î¼] Î²) 0) := rfl

instance : normed_group (Î± â†’â‚[Î¼] Î²) := normed_group.of_add_dist (Î» x, rfl) $ by
{ intros, simp only [dist_eq, coe_add], rw edist_add_right }

section normed_space

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

instance : has_scalar ğ•œ (Î± â†’â‚[Î¼] Î²) := âŸ¨Î» x f, âŸ¨x â€¢ (f : Î± â†’â‚˜[Î¼] Î²), ae_eq_fun.integrable.smul f.2âŸ©âŸ©

@[simp, norm_cast] lemma coe_smul (c : ğ•œ) (f : Î± â†’â‚[Î¼] Î²) :
  ((c â€¢ f : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = c â€¢ (f : Î± â†’â‚˜[Î¼] Î²) := rfl

instance : semimodule ğ•œ (Î± â†’â‚[Î¼] Î²) :=
{ one_smul  := Î»f, l1.eq (by { simp only [coe_smul], exact one_smul _ _ }),
  mul_smul  := Î»x y f, l1.eq (by { simp only [coe_smul], exact mul_smul _ _ _ }),
  smul_add  := Î»x f g, l1.eq (by { simp only [coe_smul, coe_add], exact smul_add _ _ _ }),
  smul_zero := Î»x, l1.eq (by { simp only [coe_zero, coe_smul], exact smul_zero _ }),
  add_smul  := Î»x y f, l1.eq (by { simp only [coe_smul], exact add_smul _ _ _ }),
  zero_smul := Î»f, l1.eq (by { simp only [coe_smul], exact zero_smul _ _ }) }

instance : normed_space ğ•œ (Î± â†’â‚[Î¼] Î²) :=
âŸ¨ begin
    rintros x âŸ¨f, hfâŸ©,
    show ennreal.to_real (edist (x â€¢ f) 0) â‰¤ âˆ¥xâˆ¥ * ennreal.to_real (edist f 0),
    rw [edist_smul, to_real_of_real_mul],
    exact norm_nonneg _
  end âŸ©

end normed_space

section of_fun

/-- Construct the equivalence class `[f]` of a measurable and integrable function `f`. -/
def of_fun (f : Î± â†’ Î²) (hf : integrable f Î¼) : (Î± â†’â‚[Î¼] Î²) :=
âŸ¨mk f hf.measurable, by { rw integrable_mk, exact hf }âŸ©

@[simp] lemma of_fun_eq_mk (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  (of_fun f hf : Î± â†’â‚˜[Î¼] Î²) = mk f hf.measurable :=
rfl

lemma of_fun_eq_of_fun (f g : Î± â†’ Î²) (hf : integrable f Î¼) (hg : integrable g Î¼) :
  of_fun f hf = of_fun g hg â†” f =áµ[Î¼] g :=
by { rw â† l1.eq_iff, simp only [of_fun_eq_mk, mk_eq_mk] }

lemma of_fun_zero : of_fun (Î» _, (0 : Î²)) (integrable_zero Î± Î² Î¼) = 0 := rfl

lemma of_fun_add (f g : Î± â†’ Î²) (hf : integrable f Î¼) (hg : integrable g Î¼) :
  of_fun (f + g) (hf.add hg) = of_fun f hf + of_fun g hg :=
rfl

lemma of_fun_neg (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  of_fun (- f) (integrable.neg hf) = - of_fun f hf := rfl

lemma of_fun_sub (f g : Î± â†’ Î²) (hf : integrable f Î¼) (hg : integrable g Î¼) :
  of_fun (f - g) (hf.sub hg) = of_fun f hf - of_fun g hg :=
rfl

lemma norm_of_fun (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  âˆ¥ of_fun f hf âˆ¥ = ennreal.to_real (âˆ«â» a, edist (f a) 0 âˆ‚Î¼) :=
rfl

lemma norm_of_fun_eq_lintegral_norm (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  âˆ¥ of_fun f hf âˆ¥ = ennreal.to_real (âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼) :=
by { rw [norm_of_fun, lintegral_norm_eq_lintegral_edist] }

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma of_fun_smul (f : Î± â†’ Î²) (hf : integrable f Î¼) (k : ğ•œ) :
  of_fun (Î»a, k â€¢ f a) (hf.smul k) = k â€¢ of_fun f hf := rfl

end of_fun

section to_fun

protected lemma measurable (f : Î± â†’â‚[Î¼] Î²) : measurable f := f.1.measurable

lemma measurable_norm (f : Î± â†’â‚[Î¼] Î²) : measurable (Î» a, âˆ¥f aâˆ¥) :=
f.measurable.norm

protected lemma integrable (f : Î± â†’â‚[Î¼] Î²) : integrable â‡‘f Î¼ :=
integrable_coe_fn.2 f.2

protected lemma has_finite_integral (f : Î± â†’â‚[Î¼] Î²) : has_finite_integral â‡‘f Î¼ :=
f.integrable.has_finite_integral

lemma integrable_norm (f : Î± â†’â‚[Î¼] Î²) : integrable (Î» a, âˆ¥f aâˆ¥) Î¼ :=
(integrable_norm_iff f.measurable).mpr f.integrable

lemma of_fun_to_fun (f : Î± â†’â‚[Î¼] Î²) : of_fun f f.integrable = f :=
subtype.ext (f : Î± â†’â‚˜[Î¼] Î²).mk_coe_fn

lemma mk_to_fun (f : Î± â†’â‚[Î¼] Î²) : (mk f f.measurable : Î± â†’â‚˜[Î¼] Î²) = f :=
by { rw â† of_fun_eq_mk, rw l1.eq_iff, exact of_fun_to_fun f }

lemma to_fun_of_fun (f : Î± â†’ Î²) (hf : integrable f Î¼) : â‡‘(of_fun f hf : Î± â†’â‚[Î¼] Î²) =áµ[Î¼] f :=
coe_fn_mk f hf.measurable

variables (Î± Î²)
lemma zero_to_fun : â‡‘(0 : Î± â†’â‚[Î¼] Î²) =áµ[Î¼] 0 := ae_eq_fun.coe_fn_zero
variables {Î± Î²}

lemma add_to_fun (f g : Î± â†’â‚[Î¼] Î²) : â‡‘(f + g) =áµ[Î¼] f + g :=
ae_eq_fun.coe_fn_add _ _

lemma neg_to_fun (f : Î± â†’â‚[Î¼] Î²) : â‡‘(-f) =áµ[Î¼] -â‡‘f := ae_eq_fun.coe_fn_neg _

lemma sub_to_fun (f g : Î± â†’â‚[Î¼] Î²) : â‡‘(f - g) =áµ[Î¼] â‡‘f - â‡‘g :=
ae_eq_fun.coe_fn_sub _ _

lemma dist_to_fun (f g : Î± â†’â‚[Î¼] Î²) : dist f g = ennreal.to_real (âˆ«â» x, edist (f x) (g x) âˆ‚Î¼) :=
by { simp only [â† coe_coe, dist_eq, edist_eq_coe] }

lemma norm_eq_nnnorm_to_fun (f : Î± â†’â‚[Î¼] Î²) : âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â» a, nnnorm (f a) âˆ‚Î¼) :=
by { rw [â† coe_coe, lintegral_nnnorm_eq_lintegral_edist, â† edist_zero_eq_coe], refl }

lemma norm_eq_norm_to_fun (f : Î± â†’â‚[Î¼] Î²) :
  âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼) :=
by { rw norm_eq_nnnorm_to_fun, congr, funext, rw of_real_norm_eq_coe_nnnorm }

lemma lintegral_edist_to_fun_lt_top (f g : Î± â†’â‚[Î¼] Î²) : (âˆ«â» a, edist (f a) (g a) âˆ‚Î¼) < âŠ¤ :=
lintegral_edist_lt_top f.integrable g.integrable

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma smul_to_fun (c : ğ•œ) (f : Î± â†’â‚[Î¼] Î²) : â‡‘(c â€¢ f) =áµ[Î¼] c â€¢ f :=
ae_eq_fun.coe_fn_smul _ _

lemma norm_eq_lintegral (f : Î± â†’â‚[Î¼] Î²) : âˆ¥fâˆ¥ = (âˆ«â» x, (nnnorm (f x) : ennreal) âˆ‚Î¼).to_real :=
by simp [l1.norm_eq, ae_eq_fun.edist_zero_eq_coe, â† edist_eq_coe_nnnorm]

/-- Computing the norm of a difference between two LÂ¹-functions. Note that this is not a
  special case of `norm_eq_lintegral` since `(f - g) x` and `f x - g x` are not equal
  (but only a.e.-equal). -/
lemma norm_sub_eq_lintegral (f g : Î± â†’â‚[Î¼] Î²) :
  âˆ¥f - gâˆ¥ = (âˆ«â» x, (nnnorm (f x - g x) : ennreal) âˆ‚Î¼).to_real :=
begin
  simp_rw [l1.norm_eq, ae_eq_fun.edist_zero_eq_coe, â† edist_eq_coe_nnnorm],
  rw lintegral_congr_ae,
  refine (ae_eq_fun.coe_fn_sub (f : Î± â†’â‚˜[Î¼] Î²) g).mp _,
  apply eventually_of_forall, intros x hx, simp [hx]
end

lemma of_real_norm_eq_lintegral (f : Î± â†’â‚[Î¼] Î²) :
  ennreal.of_real âˆ¥fâˆ¥ = âˆ«â» x, (nnnorm (f x) : ennreal) âˆ‚Î¼ :=
by { rw [norm_eq_lintegral, ennreal.of_real_to_real], rw [â† ennreal.lt_top_iff_ne_top],
  exact f.has_finite_integral }

/-- Computing the norm of a difference between two LÂ¹-functions. Note that this is not a
  special case of `of_real_norm_eq_lintegral` since `(f - g) x` and `f x - g x` are not equal
  (but only a.e.-equal). -/
lemma of_real_norm_sub_eq_lintegral (f g : Î± â†’â‚[Î¼] Î²) :
  ennreal.of_real âˆ¥f - gâˆ¥ = âˆ«â» x, (nnnorm (f x - g x) : ennreal) âˆ‚Î¼ :=
begin
  simp_rw [of_real_norm_eq_lintegral, â† edist_eq_coe_nnnorm],
  apply lintegral_congr_ae,
  refine (ae_eq_fun.coe_fn_sub (f : Î± â†’â‚˜[Î¼] Î²) g).mp _,
  apply eventually_of_forall, intros x hx, simp only [l1.coe_coe, pi.sub_apply] at hx,
  simp_rw [â† hx, â† l1.coe_sub, l1.coe_coe]
end

end to_fun

section pos_part

/-- Positive part of a function in `LÂ¹` space. -/
def pos_part (f : Î± â†’â‚[Î¼] â„) : Î± â†’â‚[Î¼] â„ :=
âŸ¨ae_eq_fun.pos_part f,
  begin
    rw [â† ae_eq_fun.integrable_coe_fn, integrable_congr (ae_eq_fun.measurable _)
      (f.measurable.max measurable_const) (coe_fn_pos_part _)],
    exact f.integrable.max_zero
  end âŸ©

/-- Negative part of a function in `LÂ¹` space. -/
def neg_part (f : Î± â†’â‚[Î¼] â„) : Î± â†’â‚[Î¼] â„ := pos_part (-f)

@[norm_cast]
lemma coe_pos_part (f : Î± â†’â‚[Î¼] â„) : (f.pos_part : Î± â†’â‚˜[Î¼] â„) = (f : Î± â†’â‚˜[Î¼] â„).pos_part := rfl

lemma pos_part_to_fun (f : Î± â†’â‚[Î¼] â„) : â‡‘(pos_part f) =áµ[Î¼] Î» a, max (f a) 0 :=
ae_eq_fun.coe_fn_pos_part _

lemma neg_part_to_fun_eq_max (f : Î± â†’â‚[Î¼] â„) : âˆ€áµ a âˆ‚Î¼, neg_part f a = max (- f a) 0 :=
begin
  rw neg_part,
  filter_upwards [pos_part_to_fun (-f), neg_to_fun f],
  simp only [mem_set_of_eq],
  assume a hâ‚ hâ‚‚,
  rw [hâ‚, hâ‚‚, pi.neg_apply]
end

lemma neg_part_to_fun_eq_min (f : Î± â†’â‚[Î¼] â„) : âˆ€áµ a âˆ‚Î¼, neg_part f a = - min (f a) 0 :=
(neg_part_to_fun_eq_max f).mono $ assume a h,
by rw [h, â† max_neg_neg, neg_zero]

lemma norm_le_norm_of_ae_le {f g : Î± â†’â‚[Î¼] Î²} (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ âˆ¥g aâˆ¥) : âˆ¥fâˆ¥ â‰¤ âˆ¥gâˆ¥ :=
begin
  simp only [l1.norm_eq_norm_to_fun],
  rw to_real_le_to_real,
  { apply lintegral_mono_ae,
    exact h.mono (Î» a h, of_real_le_of_real h) },
  { rw [â† lt_top_iff_ne_top, â† has_finite_integral_iff_norm], exact f.has_finite_integral },
  { rw [â† lt_top_iff_ne_top, â† has_finite_integral_iff_norm], exact g.has_finite_integral }
end

lemma continuous_pos_part : continuous $ Î»f : Î± â†’â‚[Î¼] â„, pos_part f :=
begin
  simp only [metric.continuous_iff],
  assume g Îµ hÎµ,
  use Îµ, use hÎµ,
  simp only [dist_eq_norm],
  assume f hfg,
  refine lt_of_le_of_lt (norm_le_norm_of_ae_le _) hfg,
  filter_upwards [l1.sub_to_fun f g, l1.sub_to_fun (pos_part f) (pos_part g),
    pos_part_to_fun f, pos_part_to_fun g],
  simp only [mem_set_of_eq],
  assume a hâ‚ hâ‚‚ hâ‚ƒ hâ‚„,
  simp only [real.norm_eq_abs, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, pi.sub_apply],
  exact abs_max_sub_max_le_abs _ _ _
end

lemma continuous_neg_part : continuous $ Î»f : Î± â†’â‚[Î¼] â„, neg_part f :=
have eq : (Î»f : Î± â†’â‚[Î¼] â„, neg_part f) = (Î»f : Î± â†’â‚[Î¼] â„, pos_part (-f)) := rfl,
by { rw eq, exact continuous_pos_part.comp continuous_neg }

end pos_part

/- TODO: l1 is a complete space -/

end l1

end measure_theory
open measure_theory

lemma measurable.integrable_zero [measurable_space Î²] {f : Î± â†’ Î²} (hf : measurable f) :
  integrable f 0 :=
âŸ¨hf, has_finite_integral_zero_measure fâŸ©
