/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/

import measure_theory.ae_eq_fun

/-!
# Integrable functions and `LÂ¹` space

In the first part of this file, the predicate `integrable` is defined and basic properties of
integrable functions are proved.

In the second part, the space `LÂ¹` of equivalence classes of integrable functions under the relation
of being almost everywhere equal is defined as a subspace of the space `Lâ°`. See the file
`src/measure_theory/ae_eq_fun.lean` for information on `Lâ°` space.

## Notation

* `Î± â†’â‚ Î²` is the type of `LÂ¹` space, where `Î±` is a `measure_space` and `Î²` is a `normed_group` with
  a `second_countable_topology`. `f : Î± â†’â‚˜ Î²` is a "function" in `LÂ¹`. In comments, `[f]` is also used
  to denote an `LÂ¹` function.

  `â‚` can be typed as `\1`.

## Main definitions

* Let `f : Î± â†’ Î²` be a function, where `Î±` is a `measure_space` and `Î²` a `normed_group`.
  Then `f` is called `integrable` if `(âˆ«â» a, nnnorm (f a)) < âŠ¤` holds.

* The space `LÂ¹` is defined as a subspace of `Lâ°` :
  An `ae_eq_fun` `[f] : Î± â†’â‚˜ Î²` is in the space `LÂ¹` if `edist [f] 0 < âŠ¤`, which means
  `(âˆ«â» a, edist (f a) 0) < âŠ¤` if we expand the definition of `edist` in `Lâ°`.

## Main statements

`LÂ¹`, as a subspace, inherits most of the structures of `Lâ°`.

## Implementation notes

Maybe `integrable f` should be mean `(âˆ«â» a, edist (f a) 0) < âŠ¤`, so that `integrable` and
`ae_eq_fun.integrable` are more aligned. But in the end one can use the lemma
`lintegral_nnnorm_eq_lintegral_edist : (âˆ«â» a, nnnorm (f a)) = (âˆ«â» a, edist (f a) 0)` to switch the
two forms.

## Tags

integrable, function space, l1

-/

noncomputable theory
open_locale classical topological_space

set_option class.instance_max_depth 100

namespace measure_theory
open set lattice filter topological_space ennreal emetric

universes u v w
variables {Î± : Type u} [measure_space Î±]
variables {Î² : Type v} [normed_group Î²] {Î³ : Type w} [normed_group Î³]

/-- A function is `integrable` if the integral of its pointwise norm is less than infinity. -/
def integrable (f : Î± â†’ Î²) : Prop := (âˆ«â» a, nnnorm (f a)) < âŠ¤

lemma integrable_iff_norm (f : Î± â†’ Î²) : integrable f â†” (âˆ«â» a, ennreal.of_real âˆ¥f aâˆ¥) < âŠ¤ :=
have eq : (Î»a, ennreal.of_real âˆ¥f aâˆ¥) = (Î»a, (nnnorm(f a) : ennreal)),
  by { funext, rw of_real_norm_eq_coe_nnnorm },
iff.intro (by { rw eq, exact Î»h, h }) $ by { rw eq, exact Î»h, h }

lemma integrable_iff_edist (f : Î± â†’ Î²) : integrable f â†” (âˆ«â» a, edist (f a) 0) < âŠ¤ :=
have eq : (Î»a, edist (f a) 0) = (Î»a, (nnnorm(f a) : ennreal)),
  by { funext, rw edist_eq_coe_nnnorm },
iff.intro (by { rw eq, exact Î»h, h }) $ by { rw eq, exact Î»h, h }

lemma integrable_iff_of_real {f : Î± â†’ â„} (h : âˆ€â‚˜ a, 0 â‰¤ f a) :
  integrable f â†” (âˆ«â» a, ennreal.of_real (f a)) < âŠ¤ :=
have lintegral_eq :  (âˆ«â» a, ennreal.of_real âˆ¥f aâˆ¥) = (âˆ«â» a, ennreal.of_real (f a)) :=
begin
  apply lintegral_congr_ae,
  filter_upwards [h],
  simp only [mem_set_of_eq],
  assume a h,
  rw [real.norm_eq_abs, abs_of_nonneg],
  exact h
end,
by rw [integrable_iff_norm, lintegral_eq]

lemma integrable_of_ae_eq {f g : Î± â†’ Î²} (hf : integrable f) (h : âˆ€â‚˜ a, f a = g a) : integrable g :=
begin
  simp only [integrable] at *,
  have : (âˆ«â» (a : Î±), â†‘(nnnorm (f a))) = (âˆ«â» (a : Î±), â†‘(nnnorm (g a))),
  { apply lintegral_congr_ae,
    filter_upwards [h],
    assume a,
    simp only [mem_set_of_eq],
    assume h,
    rw h },
  rwa â† this
end

lemma integrable_congr_ae {f g : Î± â†’ Î²} (h : âˆ€â‚˜ a, f a = g a) : integrable f â†” integrable g :=
iff.intro (Î»hf, integrable_of_ae_eq hf h) (Î»hg, integrable_of_ae_eq hg (all_ae_eq_symm h))

lemma integrable_of_le_ae {f : Î± â†’ Î²} {g : Î± â†’ Î³} (h : âˆ€â‚˜ a, âˆ¥f aâˆ¥ â‰¤ âˆ¥g aâˆ¥) (hg : integrable g) :
  integrable f :=
begin
  simp only [integrable_iff_norm] at *,
  calc (âˆ«â» a, ennreal.of_real âˆ¥f aâˆ¥) â‰¤ (âˆ«â» (a : Î±), ennreal.of_real âˆ¥g aâˆ¥) :
    lintegral_le_lintegral_ae (by { filter_upwards [h], assume a h, exact of_real_le_of_real h })
    ... < âŠ¤ : hg
end

lemma integrable_of_le {f : Î± â†’ Î²} {g : Î± â†’ Î³} (h : âˆ€a, âˆ¥f aâˆ¥ â‰¤ âˆ¥g aâˆ¥) (hg : integrable g) :
  integrable f :=
integrable_of_le_ae (univ_mem_sets' h) hg

lemma lintegral_nnnorm_eq_lintegral_edist (f : Î± â†’ Î²) :
  (âˆ«â» a, nnnorm (f a)) = âˆ«â» a, edist (f a) 0 :=
by { congr, funext, rw edist_eq_coe_nnnorm }

lemma lintegral_norm_eq_lintegral_edist (f : Î± â†’ Î²) :
  (âˆ«â» a, ennreal.of_real âˆ¥f aâˆ¥) = âˆ«â» a, edist (f a) 0 :=
by { congr, funext, rw [of_real_norm_eq_coe_nnnorm, edist_eq_coe_nnnorm] }

lemma lintegral_edist_triangle [second_countable_topology Î²] {f g h : Î± â†’ Î²}
  (hf : measurable f) (hg : measurable g) (hh : measurable h) :
  (âˆ«â» a, edist (f a) (g a)) â‰¤ (âˆ«â» a, edist (f a) (h a)) + âˆ«â» a, edist (g a) (h a) :=
begin
  rw â† lintegral_add (measurable.edist hf hh) (measurable.edist hg hh),
  apply lintegral_le_lintegral,
  assume a,
  have := edist_triangle (f a) (h a) (g a),
  convert this,
  rw edist_comm (h a) (g a),
end

lemma lintegral_edist_lt_top [second_countable_topology Î²] {f g : Î± â†’ Î²}
  (hfm : measurable f) (hfi : integrable f) (hgm : measurable g) (hgi : integrable g) :
  (âˆ«â» a, edist (f a) (g a)) < âŠ¤ :=
lt_of_le_of_lt
  (lintegral_edist_triangle hfm hgm (measurable_const : measurable (Î»a, (0 : Î²))))
  (ennreal.add_lt_top.2 $ by { split; rw â† integrable_iff_edist; assumption })

@[simp] lemma lintegral_nnnorm_zero : (âˆ«â» a : Î±, nnnorm (0 : Î²)) = 0 := by simp

variables (Î± Î²)
@[simp] lemma integrable_zero : integrable (Î»a:Î±, (0:Î²)) :=
by { have := coe_lt_top, simpa [integrable] }
variables {Î± Î²}

lemma lintegral_nnnorm_add {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : measurable f) (hg : measurable g) :
  (âˆ«â» a, nnnorm (f a) + nnnorm (g a)) = (âˆ«â» a, nnnorm (f a)) + âˆ«â» a, nnnorm (g a) :=
lintegral_add (measurable.coe_nnnorm hf) (measurable.coe_nnnorm hg)

lemma integrable.add {f g : Î± â†’ Î²} (hfm : measurable f) (hfi : integrable f) (hgm : measurable g)
  (hgi : integrable g): integrable (Î»a, f a + g a) :=
calc
  (âˆ«â» (a : Î±), â†‘(nnnorm ((f + g) a))) â‰¤ âˆ«â» (a : Î±), â†‘(nnnorm (f a)) + â†‘(nnnorm (g a)) :
    lintegral_le_lintegral _ _
      (assume a, by { simp only [coe_add.symm, coe_le_coe], exact nnnorm_add_le _ _ })
  ... = _ :
    lintegral_nnnorm_add hfm hgm
  ... < âŠ¤ : add_lt_top.2 âŸ¨hfi, hgiâŸ©

lemma integrable_finset_sum {Î¹} [second_countable_topology Î²] (s : finset Î¹) {f : Î¹ â†’ Î± â†’ Î²}
  (hfm : âˆ€ i, measurable (f i)) (hfi : âˆ€ i, integrable (f i)) :
  integrable (Î» a, s.sum (Î» i, f i a)) :=
begin
  refine finset.induction_on s _ _,
  { simp only [finset.sum_empty, integrable_zero] },
  { assume i s his ih,
    simp only [his, finset.sum_insert, not_false_iff],
    refine (hfi _).add (hfm _) (measurable_finset_sum s hfm) ih }
end

lemma lintegral_nnnorm_neg {f : Î± â†’ Î²} :
  (âˆ«â» (a : Î±), â†‘(nnnorm ((-f) a))) = âˆ«â» (a : Î±), â†‘(nnnorm ((f) a)) :=
lintegral_congr_ae $ by { filter_upwards [], simp }

lemma integrable.neg {f : Î± â†’ Î²} : integrable f â†’ integrable (Î»a, -f a) :=
assume hfi, calc _ = _ : lintegral_nnnorm_neg
                 ... < âŠ¤ : hfi

@[simp] lemma integrable_neg_iff (f : Î± â†’ Î²) : integrable (Î»a, -f a) â†” integrable f :=
begin
  split,
  { assume h,
    simpa only [_root_.neg_neg] using h.neg },
  exact integrable.neg
end

lemma integrable.sub {f g : Î± â†’ Î²} (hfm : measurable f) (hfi : integrable f) (hgm : measurable g)
  (hgi : integrable g) : integrable (Î»a, f a - g a) :=
by { simp only [sub_eq_add_neg], exact hfi.add hfm (measurable.neg hgm) (integrable.neg hgi) }

lemma integrable.norm {f : Î± â†’ Î²} (hfi : integrable f) : integrable (Î»a, âˆ¥f aâˆ¥) :=
have eq : (Î»a, (nnnorm âˆ¥f aâˆ¥ : ennreal)) = Î»a, (nnnorm (f a) : ennreal),
  by { funext, rw nnnorm_norm },
by { rwa [integrable, eq] }

lemma integrable_norm_iff (f : Î± â†’ Î²) : integrable (Î»a, âˆ¥f aâˆ¥) â†” integrable f :=
have eq : (Î»a, (nnnorm âˆ¥f aâˆ¥ : ennreal)) = Î»a, (nnnorm (f a) : ennreal),
  by { funext, rw nnnorm_norm },
by { rw [integrable, integrable, eq] }

lemma integrable_of_integrable_bound {f : Î± â†’ Î²} {bound : Î± â†’ â„} (h : integrable bound)
  (h_bound : âˆ€â‚˜ a, âˆ¥f aâˆ¥ â‰¤ bound a) : integrable f :=
have hâ‚ : âˆ€â‚˜ a, (nnnorm (f a) : ennreal) â‰¤ ennreal.of_real (bound a),
begin
  filter_upwards [h_bound],
  simp only [mem_set_of_eq],
  assume a h,
  calc (nnnorm (f a) : ennreal) = ennreal.of_real (âˆ¥f aâˆ¥) : by rw of_real_norm_eq_coe_nnnorm
    ... â‰¤ ennreal.of_real (bound a) : ennreal.of_real_le_of_real h
end,
calc (âˆ«â» a, nnnorm (f a)) â‰¤ (âˆ«â» a, ennreal.of_real (bound a)) :
    by { apply lintegral_le_lintegral_ae, exact hâ‚ }
  ... â‰¤ (âˆ«â» a, ennreal.of_real âˆ¥bound aâˆ¥) : lintegral_le_lintegral _ _ $
    by { assume a, apply ennreal.of_real_le_of_real, exact le_max_left (bound a) (-bound a) }
  ... < âŠ¤ : by { rwa [integrable_iff_norm] at h }

section dominated_convergence

variables {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}

lemma all_ae_of_real_F_le_bound (h : âˆ€ n, âˆ€â‚˜ a, âˆ¥F n aâˆ¥ â‰¤ bound a) :
  âˆ€ n, âˆ€â‚˜ a, ennreal.of_real âˆ¥F n aâˆ¥ â‰¤ ennreal.of_real (bound a) :=
Î»n, by filter_upwards [h n] Î» a h, ennreal.of_real_le_of_real h

lemma all_ae_tendsto_of_real_norm (h : âˆ€â‚˜ a, tendsto (Î» n, F n a) at_top $ ğ“ $ f a) :
  âˆ€â‚˜ a, tendsto (Î»n, ennreal.of_real âˆ¥F n aâˆ¥) at_top $ ğ“ $ ennreal.of_real âˆ¥f aâˆ¥ :=
by filter_upwards [h]
  Î» a h, tendsto_of_real $ tendsto.comp (continuous.tendsto continuous_norm _) h

lemma all_ae_of_real_f_le_bound (h_bound : âˆ€ n, âˆ€â‚˜ a, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€â‚˜ a, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  âˆ€â‚˜ a, ennreal.of_real âˆ¥f aâˆ¥ â‰¤ ennreal.of_real (bound a) :=
begin
  have F_le_bound := all_ae_of_real_F_le_bound h_bound,
  rw â† all_ae_all_iff at F_le_bound,
  filter_upwards [all_ae_tendsto_of_real_norm h_lim, F_le_bound],
  assume a tendsto_norm F_le_bound,
  refine le_of_tendsto at_top_ne_bot tendsto_norm _,
  simp only [mem_at_top_sets, ge_iff_le, mem_set_of_eq, preimage_set_of_eq, nonempty_of_inhabited],
  use 0,
  assume n hn,
  exact F_le_bound n
end

lemma integrable_of_dominated_convergence {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}
  (bound_integrable : integrable bound)
  (h_bound : âˆ€ n, âˆ€â‚˜ a, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€â‚˜ a, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  integrable f :=
/- `âˆ¥F n aâˆ¥ â‰¤ bound a` and `âˆ¥F n aâˆ¥ --> âˆ¥f aâˆ¥` implies `âˆ¥f aâˆ¥ â‰¤ bound a`,
  and so `âˆ« âˆ¥fâˆ¥ â‰¤ âˆ« bound < âŠ¤` since `bound` is integrable -/
begin
  rw integrable_iff_norm,
  calc (âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥)) â‰¤ âˆ«â» a, ennreal.of_real (bound a) :
    lintegral_le_lintegral_ae $ all_ae_of_real_f_le_bound h_bound h_lim
    ... < âŠ¤ :
    begin
      rw â† integrable_iff_of_real,
      { exact bound_integrable },
      filter_upwards [h_bound 0] Î» a h, le_trans (norm_nonneg _) h,
    end
end

lemma tendsto_lintegral_norm_of_dominated_convergence [second_countable_topology Î²]
  {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}
  (F_measurable : âˆ€ n, measurable (F n))
  (f_measurable : measurable f)
  (bound_integrable : integrable bound)
  (h_bound : âˆ€ n, âˆ€â‚˜ a, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€â‚˜ a, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  tendsto (Î»n, âˆ«â» a, ennreal.of_real âˆ¥F n a - f aâˆ¥) at_top (ğ“ 0) :=
let b := Î»a, 2 * ennreal.of_real (bound a) in
/- `âˆ¥F n aâˆ¥ â‰¤ bound a` and `F n a --> f a` implies `âˆ¥f aâˆ¥ â‰¤ bound a`, and thus by the
  triangle inequality, have `âˆ¥F n a - f aâˆ¥ â‰¤ 2 * (bound a). -/
have hb : âˆ€ n, âˆ€â‚˜ a, ennreal.of_real âˆ¥F n a - f aâˆ¥ â‰¤ b a,
begin
  assume n,
  filter_upwards [all_ae_of_real_F_le_bound h_bound n, all_ae_of_real_f_le_bound h_bound h_lim],
  assume a hâ‚ hâ‚‚,
  calc ennreal.of_real âˆ¥F n a - f aâˆ¥ â‰¤ (ennreal.of_real âˆ¥F n aâˆ¥) + (ennreal.of_real âˆ¥f aâˆ¥) :
  begin
    rw [â† ennreal.of_real_add],
    apply of_real_le_of_real,
    { apply norm_sub_le }, { exact norm_nonneg _ }, { exact norm_nonneg _ }
  end
    ... â‰¤ (ennreal.of_real (bound a)) + (ennreal.of_real (bound a)) : add_le_add' hâ‚ hâ‚‚
    ... = b a : by rw â† two_mul
end,
/- On the other hand, `F n a --> f a` implies that `âˆ¥F n a - f aâˆ¥ --> 0`  -/
have h : âˆ€â‚˜ a, tendsto (Î» n, ennreal.of_real âˆ¥F n a - f aâˆ¥) at_top (ğ“ 0),
begin
  suffices h : âˆ€â‚˜ a, tendsto (Î» n, ennreal.of_real âˆ¥F n a - f aâˆ¥) at_top (ğ“ $ ennreal.of_real 0),
  { rwa ennreal.of_real_zero at h },
  filter_upwards [h_lim],
  assume a h,
  refine tendsto.comp (continuous.tendsto continuous_of_real _) _,
  rw â† tendsto_iff_norm_tendsto_zero,
  exact h
end,
/- Therefore, by the dominated convergence theorem for nonnegative integration, have
  ` âˆ« âˆ¥f a - F n aâˆ¥ --> 0 ` -/
begin
  suffices h : tendsto (Î»n, âˆ«â» a, ennreal.of_real âˆ¥F n a - f aâˆ¥) at_top (ğ“ (âˆ«â» (a:Î±), 0)),
  { rwa lintegral_zero at h },
  -- Using the dominated convergence theorem.
  refine tendsto_lintegral_of_dominated_convergence _ _ hb _ _,
  -- Show `Î»a, âˆ¥f a - F n aâˆ¥` is measurable for all `n`
  { exact Î»n, measurable.comp measurable_of_real (measurable.norm (measurable.sub (F_measurable n)
      f_measurable)) },
  -- Show `2 * bound` is integrable
  { rw integrable_iff_of_real at bound_integrable,
    { calc (âˆ«â» a, b a) = 2 * (âˆ«â» a, ennreal.of_real (bound a)) :
        by { rw lintegral_const_mul', exact coe_ne_top }
        ... < âŠ¤ : mul_lt_top (coe_lt_top) bound_integrable },
    filter_upwards [h_bound 0] Î» a h, le_trans (norm_nonneg _) h },
  -- Show `âˆ¥f a - F n aâˆ¥ --> 0`
  { exact h }
end

end dominated_convergence

section pos_part
/-! Lemmas used for defining the positive part of a `LÂ¹` function -/

lemma integrable.max_zero {f : Î± â†’ â„} (hf : integrable f) : integrable (Î»a, max (f a) 0) :=
begin
  simp only [integrable_iff_norm] at *,
  calc (âˆ«â» a, ennreal.of_real âˆ¥max (f a) 0âˆ¥) â‰¤ (âˆ«â» (a : Î±), ennreal.of_real âˆ¥f aâˆ¥) :
    lintegral_le_lintegral _ _
    begin
      assume a,
      apply of_real_le_of_real,
      simp only [real.norm_eq_abs],
      calc abs (max (f a) 0) = max (f a) 0 : by { rw abs_of_nonneg, apply le_max_right }
        ... â‰¤ abs (f a) : max_le (le_abs_self _) (abs_nonneg _)
    end
    ... < âŠ¤ : hf
end

lemma integrable.min_zero {f : Î± â†’ â„} (hf : integrable f) : integrable (Î»a, min (f a) 0) :=
begin
  have : (Î»a, min (f a) 0) = (Î»a, - max (-f a) 0),
  { funext, rw [min_eq_neg_max_neg_neg, neg_zero] },
  rw this,
  exact (integrable.max_zero hf.neg).neg,
end

end pos_part

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma integrable.smul (c : ğ•œ) {f : Î± â†’ Î²} : integrable f â†’ integrable (Î»a, c â€¢ f a) :=
begin
  simp only [integrable], assume hfi,
  calc
    (âˆ«â» (a : Î±), nnnorm ((c â€¢ f) a)) = (âˆ«â» (a : Î±), (nnnorm c) * nnnorm (f a)) :
    begin
      apply lintegral_congr_ae,
      filter_upwards [],
      assume a,
      simp only [nnnorm_smul, set.mem_set_of_eq, pi.smul_apply, ennreal.coe_mul]
    end
    ... < âŠ¤ :
    begin
      rw lintegral_const_mul',
      apply mul_lt_top,
      { exact coe_lt_top },
      { exact hfi },
      { simp only [ennreal.coe_ne_top, ne.def, not_false_iff] }
    end
end

lemma integrable_smul_iff {c : ğ•œ} (hc : c â‰  0) (f : Î± â†’ Î²) : integrable (Î»a, c â€¢ f a) â†” integrable f :=
begin
  split,
  { assume h,
    simpa only [smul_smul, inv_mul_cancel hc, one_smul] using h.smul câ»Â¹ },
  exact integrable.smul _
end

end normed_space

variables [second_countable_topology Î²]

namespace ae_eq_fun

/-- An almost everywhere equal function is `integrable` if it has a finite distance to the origin.
  Should mean the same thing as the predicate `integrable` over functions. -/
def integrable (f : Î± â†’â‚˜ Î²) : Prop := f âˆˆ ball (0 : Î± â†’â‚˜ Î²) âŠ¤

lemma integrable_mk (f : Î± â†’ Î²) (hf : measurable f) :
  (integrable (mk f hf)) â†” measure_theory.integrable f :=
by simp [integrable, zero_def, edist_mk_mk', measure_theory.integrable, nndist_eq_nnnorm]

lemma integrable_to_fun (f : Î± â†’â‚˜ Î²) : integrable f â†” (measure_theory.integrable f.to_fun) :=
by conv_lhs { rw [self_eq_mk f, integrable_mk] }

local attribute [simp] integrable_mk

lemma integrable_zero : integrable (0 : Î± â†’â‚˜ Î²) := mem_ball_self coe_lt_top

lemma integrable.add : âˆ€ {f g : Î± â†’â‚˜ Î²}, integrable f â†’ integrable g â†’ integrable (f + g) :=
begin
  rintros âŸ¨f, hfâŸ© âŸ¨g, hgâŸ©,
  simp only [mem_ball, zero_def, mk_add_mk, integrable_mk, quot_mk_eq_mk],
  assume hfi hgi,
  exact hfi.add hf hg hgi
end

lemma integrable.neg : âˆ€ {f : Î± â†’â‚˜ Î²}, integrable f â†’ integrable (-f) :=
by { rintros âŸ¨f, hfâŸ©, have := measure_theory.integrable.neg, simpa }

lemma integrable.sub : âˆ€ {f g : Î± â†’â‚˜ Î²}, integrable f â†’ integrable g â†’ integrable (f - g) :=
begin
  rintros âŸ¨f, hfmâŸ© âŸ¨g, hgmâŸ©,
  simp only [mem_ball, zero_def, mk_sub_mk, integrable_mk, quot_mk_eq_mk],
  assume hfi hgi,
  exact hfi.sub hfm hgm hgi
end

protected lemma is_add_subgroup : is_add_subgroup (ball (0 : Î± â†’â‚˜ Î²) âŠ¤) :=
{ zero_mem := integrable_zero,
  add_mem := Î» _ _, integrable.add,
  neg_mem := Î» _, integrable.neg }

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma integrable.smul : âˆ€ {c : ğ•œ} {f : Î± â†’â‚˜ Î²}, integrable f â†’ integrable (c â€¢ f) :=
by { assume c, rintros âŸ¨f, hfâŸ©, simpa using integrable.smul _ }

end normed_space

end ae_eq_fun

section
variables (Î± Î²)

/-- The space of equivalence classes of integrable (and measurable) functions, where two integrable
    functions are equivalent if they agree almost everywhere, i.e., they differ on a set of measure
    `0`. -/
def l1 : Type (max u v) := subtype (@ae_eq_fun.integrable Î± _ Î² _ _)

infixr ` â†’â‚ `:25 := l1

end

namespace l1
open ae_eq_fun
local attribute [instance] ae_eq_fun.is_add_subgroup

instance : has_coe (Î± â†’â‚ Î²) (Î± â†’â‚˜ Î²) := âŸ¨subtype.valâŸ©

protected lemma eq {f g : Î± â†’â‚ Î²} : (f : Î± â†’â‚˜ Î²) = (g : Î± â†’â‚˜ Î²) â†’ f = g := subtype.eq
@[elim_cast] protected lemma eq_iff {f g : Î± â†’â‚ Î²} : (f : Î± â†’â‚˜ Î²) = (g : Î± â†’â‚˜ Î²) â†” f = g :=
iff.intro (l1.eq) (congr_arg coe)

/- TODO : order structure of l1-/

/-- `LÂ¹` space forms a `emetric_space`, with the emetric being inherited from almost everywhere
  functions, i.e., `edist f g = âˆ«â» a, edist (f a) (g a)`. -/
instance : emetric_space (Î± â†’â‚ Î²) := subtype.emetric_space

/-- `LÂ¹` space forms a `metric_space`, with the metric being inherited from almost everywhere
  functions, i.e., `edist f g = ennreal.to_real (âˆ«â» a, edist (f a) (g a))`. -/
instance : metric_space (Î± â†’â‚ Î²) := metric_space_emetric_ball 0 âŠ¤
instance : add_comm_group (Î± â†’â‚ Î²) := subtype.add_comm_group

instance : inhabited (Î± â†’â‚ Î²) := âŸ¨0âŸ©

@[simp, elim_cast] lemma coe_zero : ((0 : Î± â†’â‚ Î²) : Î± â†’â‚˜ Î²) = 0 := rfl
@[simp, move_cast] lemma coe_add (f g : Î± â†’â‚ Î²) : ((f + g : Î± â†’â‚ Î²) : Î± â†’â‚˜ Î²) = f + g := rfl
@[simp, move_cast] lemma coe_neg (f : Î± â†’â‚ Î²) : ((-f : Î± â†’â‚ Î²) : Î± â†’â‚˜ Î²) = -f := rfl
@[simp, move_cast] lemma coe_sub (f g : Î± â†’â‚ Î²) : ((f - g : Î± â†’â‚ Î²) : Î± â†’â‚˜ Î²) = f - g := rfl
@[simp] lemma edist_eq (f g : Î± â†’â‚ Î²) : edist f g = edist (f : Î± â†’â‚˜ Î²) (g : Î± â†’â‚˜ Î²) := rfl

lemma dist_eq (f g : Î± â†’â‚ Î²) : dist f g = ennreal.to_real (edist (f : Î± â†’â‚˜ Î²) (g : Î± â†’â‚˜ Î²)) := rfl

/-- The norm on `LÂ¹` space is defined to be `âˆ¥fâˆ¥ = âˆ«â» a, edist (f a) 0`. -/
instance : has_norm (Î± â†’â‚ Î²) := âŸ¨Î» f, dist f 0âŸ©

lemma norm_eq (f : Î± â†’â‚ Î²) : âˆ¥fâˆ¥ = ennreal.to_real (edist (f : Î± â†’â‚˜ Î²) 0) := rfl

instance : normed_group (Î± â†’â‚ Î²) := normed_group.of_add_dist (Î» x, rfl) $ by
{ intros, simp only [dist_eq, coe_add], rw edist_eq_add_add }

section normed_space

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

instance : has_scalar ğ•œ (Î± â†’â‚ Î²) := âŸ¨Î» x f, âŸ¨x â€¢ (f : Î± â†’â‚˜ Î²), ae_eq_fun.integrable.smul f.2âŸ©âŸ©

@[simp, move_cast] lemma coe_smul (c : ğ•œ) (f : Î± â†’â‚ Î²) :
  ((c â€¢ f : Î± â†’â‚ Î²) : Î± â†’â‚˜ Î²) = c â€¢ (f : Î± â†’â‚˜ Î²) := rfl

instance : semimodule ğ•œ (Î± â†’â‚ Î²) :=
{ one_smul  := Î»f, l1.eq (by { simp only [coe_smul], exact one_smul _ _ }),
  mul_smul  := Î»x y f, l1.eq (by { simp only [coe_smul], exact mul_smul _ _ _ }),
  smul_add  := Î»x f g, l1.eq (by { simp only [coe_smul, coe_add], exact smul_add _ _ _ }),
  smul_zero := Î»x, l1.eq (by { simp only [coe_zero, coe_smul], exact smul_zero _ }),
  add_smul  := Î»x y f, l1.eq (by { simp only [coe_smul], exact add_smul _ _ _ }),
  zero_smul := Î»f, l1.eq (by { simp only [coe_smul], exact zero_smul _ _ }) }

instance : module ğ•œ (Î± â†’â‚ Î²) := { .. l1.semimodule }

instance : vector_space ğ•œ (Î± â†’â‚ Î²) := { .. l1.semimodule }

instance : normed_space ğ•œ (Î± â†’â‚ Î²) :=
âŸ¨ begin
    rintros x âŸ¨f, hfâŸ©,
    show ennreal.to_real (edist (x â€¢ f) 0) = âˆ¥xâˆ¥ * ennreal.to_real (edist f 0),
    rw [edist_smul, to_real_of_real_mul],
    exact norm_nonneg _
  end âŸ©

end normed_space

section of_fun

/-- Construct the equivalence class `[f]` of a measurable and integrable function `f`. -/
def of_fun (f : Î± â†’ Î²) (hfm : measurable f) (hfi : integrable f) : (Î± â†’â‚ Î²) :=
âŸ¨mk f hfm, by { rw integrable_mk, exact hfi }âŸ©

lemma of_fun_eq_mk (f : Î± â†’ Î²) (hfm hfi) : (of_fun f hfm hfi : Î± â†’â‚˜ Î²) = mk f hfm := rfl

lemma of_fun_eq_of_fun (f g : Î± â†’ Î²) (hfm hfi hgm hgi) :
  of_fun f hfm hfi = of_fun g hgm hgi â†” âˆ€â‚˜ a, f a = g a :=
by { rw â† l1.eq_iff, simp only [of_fun_eq_mk, mk_eq_mk] }

lemma of_fun_zero :
  of_fun (Î»a:Î±, (0:Î²)) (@measurable_const _ _ _ _ (0:Î²)) (integrable_zero Î± Î²) = 0 := rfl

lemma of_fun_add (f g : Î± â†’ Î²) (hfm hfi hgm hgi) :
  of_fun (Î»a, f a + g a) (measurable.add hfm hgm) (integrable.add hfm hfi hgm hgi)
    = of_fun f hfm hfi + of_fun g hgm hgi :=
rfl

lemma of_fun_neg (f : Î± â†’ Î²) (hfm hfi) :
  of_fun (Î»a, - f a) (measurable.neg hfm) (integrable.neg hfi) = - of_fun f hfm hfi := rfl

lemma of_fun_sub (f g : Î± â†’ Î²) (hfm hfi hgm hgi) :
  of_fun (Î»a, f a - g a) (measurable.sub hfm hgm) (integrable.sub hfm hfi hgm hgi)
    = of_fun f hfm hfi - of_fun g hgm hgi :=
rfl

lemma norm_of_fun (f : Î± â†’ Î²) (hfm hfi) : âˆ¥of_fun f hfm hfiâˆ¥ = ennreal.to_real (âˆ«â» a, edist (f a) 0) :=
rfl

lemma norm_of_fun_eq_lintegral_norm (f : Î± â†’ Î²) (hfm hfi) :
  âˆ¥of_fun f hfm hfiâˆ¥ = ennreal.to_real (âˆ«â» a, ennreal.of_real âˆ¥f aâˆ¥) :=
by { rw [norm_of_fun, lintegral_norm_eq_lintegral_edist] }

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma of_fun_smul (f : Î± â†’ Î²) (hfm hfi) (k : ğ•œ) :
  of_fun (Î»a, k â€¢ f a) (measurable.smul _ hfm) (integrable.smul _ hfi) = k â€¢ of_fun f hfm hfi := rfl

end of_fun

section to_fun

/-- Find a representative of a `LÂ¹` function [f] -/
@[reducible]
protected def to_fun (f : Î± â†’â‚ Î²) : Î± â†’ Î² := (f : Î± â†’â‚˜ Î²).to_fun

protected lemma measurable (f : Î± â†’â‚ Î²) : measurable f.to_fun := f.1.measurable

protected lemma integrable (f : Î± â†’â‚ Î²) : integrable f.to_fun :=
by { rw [l1.to_fun, â† integrable_to_fun], exact f.2 }

lemma of_fun_to_fun (f : Î± â†’â‚ Î²) : of_fun (f.to_fun) f.measurable f.integrable = f :=
begin
  rcases f with âŸ¨f, hfiâŸ©,
  rw [of_fun, subtype.mk_eq_mk],
  exact (self_eq_mk f).symm
end

lemma mk_to_fun (f : Î± â†’â‚ Î²) : mk (f.to_fun) f.measurable = f :=
by { rw â† of_fun_eq_mk, rw l1.eq_iff, exact of_fun_to_fun f }

lemma to_fun_of_fun (f : Î± â†’ Î²) (hfm hfi) : âˆ€â‚˜ a, (of_fun f hfm hfi).to_fun a = f a :=
begin
  filter_upwards [all_ae_mk_to_fun f hfm],
  assume a,
  simp only [mem_set_of_eq],
  assume h,
  rw â† h,
  refl
end

variables (Î± Î²)
lemma zero_to_fun : âˆ€â‚˜ a, (0 : Î± â†’â‚ Î²).to_fun a = 0 := ae_eq_fun.zero_to_fun
variables {Î± Î²}

lemma add_to_fun (f g : Î± â†’â‚ Î²) : âˆ€â‚˜ a, (f + g).to_fun a = f.to_fun a + g.to_fun a :=
ae_eq_fun.add_to_fun _ _

lemma neg_to_fun (f : Î± â†’â‚ Î²) : âˆ€â‚˜ a, (-f).to_fun a = -f.to_fun a := ae_eq_fun.neg_to_fun _

lemma sub_to_fun (f g : Î± â†’â‚ Î²) : âˆ€â‚˜ a, (f - g).to_fun a = f.to_fun a - g.to_fun a :=
ae_eq_fun.sub_to_fun _ _

lemma dist_to_fun (f g : Î± â†’â‚ Î²) : dist f g = ennreal.to_real (âˆ«â» x, edist (f.to_fun x) (g.to_fun x)) :=
by { simp only [dist_eq, edist_to_fun] }

lemma norm_eq_nnnorm_to_fun (f : Î± â†’â‚ Î²) : âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â» a, nnnorm (f.to_fun a)) :=
by { rw [lintegral_nnnorm_eq_lintegral_edist, â† edist_zero_to_fun], refl }

lemma norm_eq_norm_to_fun (f : Î± â†’â‚ Î²) : âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â» a, ennreal.of_real âˆ¥f.to_fun aâˆ¥) :=
by { rw norm_eq_nnnorm_to_fun, congr, funext, rw of_real_norm_eq_coe_nnnorm }

lemma lintegral_edist_to_fun_lt_top (f g : Î± â†’â‚ Î²) : (âˆ«â» a, edist (f.to_fun a) (g.to_fun a)) < âŠ¤ :=
begin
  apply lintegral_edist_lt_top,
  exact f.measurable, exact f.integrable, exact g.measurable, exact g.integrable
end

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma smul_to_fun (c : ğ•œ) (f : Î± â†’â‚ Î²) : âˆ€â‚˜ a, (c â€¢ f).to_fun a = c â€¢ f.to_fun a :=
ae_eq_fun.smul_to_fun _ _

end to_fun

section pos_part

/-- Positive part of a function in `LÂ¹` space. -/
def pos_part (f : Î± â†’â‚ â„) : Î± â†’â‚ â„ :=
âŸ¨ ae_eq_fun.pos_part f,
  begin
    rw [ae_eq_fun.integrable_to_fun, integrable_congr_ae (pos_part_to_fun _)],
    exact integrable.max_zero f.integrable
  end âŸ©

/-- Negative part of a function in `LÂ¹` space. -/
def neg_part (f : Î± â†’â‚ â„) : Î± â†’â‚ â„ := pos_part (-f)

@[move_cast] lemma coe_pos_part (f : Î± â†’â‚ â„) : (f.pos_part : Î± â†’â‚˜ â„) = (f : Î± â†’â‚˜ â„).pos_part := rfl

lemma pos_part_to_fun (f : Î± â†’â‚ â„) : âˆ€â‚˜ a, (pos_part f).to_fun a = max (f.to_fun a) 0 :=
ae_eq_fun.pos_part_to_fun _

lemma neg_part_to_fun_eq_max (f : Î± â†’â‚ â„) : âˆ€â‚˜ a, (neg_part f).to_fun a = max (- f.to_fun a) 0 :=
begin
  rw neg_part,
  filter_upwards [pos_part_to_fun (-f), neg_to_fun f],
  simp only [mem_set_of_eq],
  assume a hâ‚ hâ‚‚,
  rw [hâ‚, hâ‚‚]
end

lemma neg_part_to_fun_eq_min (f : Î± â†’â‚ â„) : âˆ€â‚˜ a, (neg_part f).to_fun a = - min (f.to_fun a) 0 :=
begin
  filter_upwards [neg_part_to_fun_eq_max f],
  simp only [mem_set_of_eq],
  assume a h,
  rw [h, min_eq_neg_max_neg_neg, _root_.neg_neg, neg_zero],
end

lemma norm_le_norm_of_ae_le {f g : Î± â†’â‚ Î²} (h : âˆ€â‚˜ a, âˆ¥f.to_fun aâˆ¥ â‰¤ âˆ¥g.to_fun aâˆ¥) : âˆ¥fâˆ¥ â‰¤ âˆ¥gâˆ¥ :=
begin
  simp only [l1.norm_eq_norm_to_fun],
  rw to_real_le_to_real,
  { apply lintegral_le_lintegral_ae,
    filter_upwards [h],
    simp only [mem_set_of_eq],
    assume a h,
    exact of_real_le_of_real h },
  { rw [â† lt_top_iff_ne_top, â† integrable_iff_norm], exact f.integrable },
  { rw [â† lt_top_iff_ne_top, â† integrable_iff_norm], exact g.integrable }
end

lemma continuous_pos_part : continuous $ Î»f : Î± â†’â‚ â„, pos_part f :=
begin
  simp only [metric.continuous_iff],
  assume g Îµ hÎµ,
  use Îµ, use hÎµ,
  simp only [dist_eq_norm],
  assume f hfg,
  refine lt_of_le_of_lt (norm_le_norm_of_ae_le _) hfg,
  filter_upwards [l1.sub_to_fun f g, l1.sub_to_fun (pos_part f) (pos_part g),
    pos_part_to_fun f, pos_part_to_fun g],
  simp only [mem_set_of_eq],
  assume a hâ‚ hâ‚‚ hâ‚ƒ hâ‚„,
  simp only [real.norm_eq_abs, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„],
  exact abs_max_sub_max_le_abs _ _ _
end

lemma continuous_neg_part : continuous $ Î»f : Î± â†’â‚ â„, neg_part f :=
have eq : (Î»f : Î± â†’â‚ â„, neg_part f) = (Î»f : Î± â†’â‚ â„, pos_part (-f)) := rfl,
by { rw eq, exact continuous_pos_part.comp continuous_neg }

end pos_part

/- TODO: l1 is a complete space -/

end l1

end measure_theory
