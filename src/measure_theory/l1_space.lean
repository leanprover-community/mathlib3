/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/
import measure_theory.ae_eq_fun

/-!
# Integrable functions and `LÂ¹` space

In the first part of this file, the predicate `integrable` is defined and basic properties of
integrable functions are proved.

In the second part, the space `LÂ¹` of equivalence classes of integrable functions under the relation
of being almost everywhere equal is defined as a subspace of the space `Lâ°`. See the file
`src/measure_theory/ae_eq_fun.lean` for information on `Lâ°` space.

## Notation

* `Î± â†’â‚ Î²` is the type of `LÂ¹` space, where `Î±` is a `measure_space` and `Î²` is a `normed_group`
  with a `second_countable_topology`. `f : Î± â†’â‚˜ Î²` is a "function" in `LÂ¹`. In comments, `[f]` is
  also used to denote an `LÂ¹` function.

  `â‚` can be typed as `\1`.

## Main definitions

* Let `f : Î± â†’ Î²` be a function, where `Î±` is a `measure_space` and `Î²` a `normed_group`.
  Then `f` is called `integrable` if `(âˆ«â» a, nnnorm (f a)) < âŠ¤` holds.

* The space `LÂ¹` is defined as a subspace of `Lâ°` :
  An `ae_eq_fun` `[f] : Î± â†’â‚˜ Î²` is in the space `LÂ¹` if `edist [f] 0 < âŠ¤`, which means
  `(âˆ«â» a, edist (f a) 0) < âŠ¤` if we expand the definition of `edist` in `Lâ°`.

## Main statements

`LÂ¹`, as a subspace, inherits most of the structures of `Lâ°`.

## Implementation notes

Maybe `integrable f` should be mean `(âˆ«â» a, edist (f a) 0) < âŠ¤`, so that `integrable` and
`ae_eq_fun.integrable` are more aligned. But in the end one can use the lemma
`lintegral_nnnorm_eq_lintegral_edist : (âˆ«â» a, nnnorm (f a)) = (âˆ«â» a, edist (f a) 0)` to switch the
two forms.

## Tags

integrable, function space, l1

-/

noncomputable theory
open_locale classical topological_space


namespace measure_theory
open set filter topological_space ennreal emetric
open_locale big_operators

universes u v w
variables {Î± : Type u} [measurable_space Î±] {Î¼ : measure Î±}
variables {Î² : Type v} [normed_group Î²] {Î³ : Type w} [normed_group Î³]

/-- A function is `integrable` if the integral of its pointwise norm is less than infinity. -/
def integrable (Î¼ : measure Î±) (f : Î± â†’ Î²) : Prop := âˆ«â» a, nnnorm (f a) âˆ‚Î¼ < âŠ¤

lemma integrable_iff_norm (f : Î± â†’ Î²) : integrable Î¼ f â†” âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ < âŠ¤ :=
by simp only [integrable, of_real_norm_eq_coe_nnnorm]

lemma integrable_iff_edist (f : Î± â†’ Î²) : integrable Î¼  f â†” âˆ«â» a, edist (f a) 0 âˆ‚Î¼ < âŠ¤ :=
by simp only [integrable_iff_norm, edist_dist, dist_zero_right]

lemma integrable_iff_of_real {f : Î± â†’ â„} (h : 0 â‰¤áµ[Î¼] f) :
  integrable Î¼ f â†” âˆ«â» a, ennreal.of_real (f a) âˆ‚Î¼ < âŠ¤ :=
have lintegral_eq : âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ = âˆ«â» a, ennreal.of_real (f a) âˆ‚Î¼ :=
begin
  refine lintegral_congr_ae (h.mono $ Î» a h, _),
  rwa [real.norm_eq_abs, abs_of_nonneg]
end,
by rw [integrable_iff_norm, lintegral_eq]

lemma integrable.congr {f g : Î± â†’ Î²} (hf : integrable Î¼ f) (h : f =áµ[Î¼] g) : integrable Î¼ g :=
begin
  simp only [integrable],
  convert hf using 1,
  exact lintegral_rwâ‚ (h.symm.fun_comp _) _
end

lemma integrable_congr {f g : Î± â†’ Î²} (h : f =áµ[Î¼] g) : integrable Î¼ f â†” integrable Î¼ g :=
âŸ¨Î» hf, hf.congr h, Î» hg, hg.congr h.symmâŸ©

lemma integrable.mono {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hg : integrable Î¼ g) (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ âˆ¥g aâˆ¥) :
  integrable Î¼ f :=
begin
  simp only [integrable_iff_norm] at *,
  calc âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ â‰¤ âˆ«â» (a : Î±), (ennreal.of_real âˆ¥g aâˆ¥) âˆ‚Î¼ :
    lintegral_mono_ae (h.mono $ assume a h, of_real_le_of_real h)
    ... < âŠ¤ : hg
end

lemma lintegral_nnnorm_eq_lintegral_edist (f : Î± â†’ Î²) :
  âˆ«â» a, nnnorm (f a) âˆ‚Î¼ = âˆ«â» a, edist (f a) 0 âˆ‚Î¼ :=
by simp only [edist_eq_coe_nnnorm]

lemma lintegral_norm_eq_lintegral_edist (f : Î± â†’ Î²) :
  âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ = âˆ«â» a, edist (f a) 0 âˆ‚Î¼ :=
by simp only [of_real_norm_eq_coe_nnnorm, edist_eq_coe_nnnorm]

lemma lintegral_edist_triangle [second_countable_topology Î²] [measurable_space Î²]
  [opens_measurable_space Î²] {f g h : Î± â†’ Î²}
  (hf : measurable f) (hg : measurable g) (hh : measurable h) :
  âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ â‰¤ âˆ«â» a, edist (f a) (h a) âˆ‚Î¼ + âˆ«â» a, edist (g a) (h a) âˆ‚Î¼ :=
begin
  rw â† lintegral_add (hf.edist hh) (hg.edist hh),
  refine lintegral_mono (Î» a, _),
  apply edist_triangle_right
end

lemma lintegral_edist_lt_top [second_countable_topology Î²] [measurable_space Î²]
  [opens_measurable_space Î²] {f g : Î± â†’ Î²}
  (hfm : measurable f) (hfi : integrable Î¼ f) (hgm : measurable g) (hgi : integrable Î¼ g) :
  âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ < âŠ¤ :=
lt_of_le_of_lt
  (lintegral_edist_triangle hfm hgm (measurable_const : measurable (Î»a, (0 : Î²))))
  (ennreal.add_lt_top.2 $ by { split; rw â† integrable_iff_edist; assumption })

lemma lintegral_nnnorm_zero : âˆ«â» a : Î±, nnnorm (0 : Î²) âˆ‚Î¼ = 0 := by simp

variables (Î± Î² Î¼)
@[simp] lemma integrable_zero : integrable Î¼ (Î»a:Î±, (0:Î²)) :=
by simp [integrable]
variables {Î± Î² Î¼}

lemma lintegral_nnnorm_add [measurable_space Î²] [opens_measurable_space Î²]
  [measurable_space Î³] [opens_measurable_space Î³]
  {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : measurable f) (hg : measurable g) :
  âˆ«â» a, nnnorm (f a) + nnnorm (g a) âˆ‚Î¼ = âˆ«â» a, nnnorm (f a) âˆ‚Î¼ + âˆ«â» a, nnnorm (g a) âˆ‚Î¼ :=
lintegral_add hf.ennnorm hg.ennnorm

lemma integrable.add [measurable_space Î²] [opens_measurable_space Î²]
  {f g : Î± â†’ Î²} (hfm : measurable f) (hfi : integrable Î¼ f)
  (hgm : measurable g) (hgi : integrable Î¼ g) :
  integrable Î¼ (f + g) :=
calc
  âˆ«â» a, nnnorm (f a + g a) âˆ‚Î¼ â‰¤ âˆ«â» a, nnnorm (f a) + nnnorm (g a) âˆ‚Î¼ :
    lintegral_mono
      (assume a, by { simp only [â† coe_add, coe_le_coe], exact nnnorm_add_le _ _ })
  ... = _ :
    lintegral_nnnorm_add hfm hgm
  ... < âŠ¤ : add_lt_top.2 âŸ¨hfi, hgiâŸ©

lemma integrable_finset_sum {Î¹} [measurable_space Î²] [borel_space Î²]
  [second_countable_topology Î²] (s : finset Î¹) {f : Î¹ â†’ Î± â†’ Î²}
  (hfm : âˆ€ i, measurable (f i)) (hfi : âˆ€ i, integrable Î¼ (f i)) :
  integrable Î¼ (Î» a, âˆ‘ i in s, f i a) :=
begin
  refine finset.induction_on s _ _,
  { simp only [finset.sum_empty, integrable_zero] },
  { assume i s his ih,
    simp only [his, finset.sum_insert, not_false_iff],
    refine (hfi _).add (hfm _) (s.measurable_sum hfm) ih }
end

lemma lintegral_nnnorm_neg {f : Î± â†’ Î²} :
  âˆ«â» a, nnnorm ((-f) a) âˆ‚Î¼ = âˆ«â» a, nnnorm (f a) âˆ‚Î¼ :=
by simp only [pi.neg_apply, nnnorm_neg]

lemma integrable.neg {f : Î± â†’ Î²} (hfi : integrable Î¼ f) : integrable Î¼ (-f) :=
calc _ = _ : lintegral_nnnorm_neg
   ... < âŠ¤ : hfi

@[simp] lemma integrable_neg_iff {f : Î± â†’ Î²} : integrable Î¼ (-f) â†” integrable Î¼ f :=
âŸ¨Î» h, neg_neg f â–¸ h.neg, integrable.negâŸ©

lemma integrable.sub [measurable_space Î²] [opens_measurable_space Î²]
  {f g : Î± â†’ Î²} (hfm : measurable f) (hfi : integrable Î¼ f) (hgm : measurable g)
  (hgi : integrable Î¼ g) : integrable Î¼ (f - g) :=
calc
  âˆ«â» a, nnnorm (f a - g a) âˆ‚Î¼ â‰¤ âˆ«â» a, nnnorm (f a) + nnnorm (-g a) âˆ‚Î¼ :
    lintegral_mono (assume a, by exact_mod_cast nnnorm_add_le _ _ )
  ... = _ :
    by { simp only [nnnorm_neg], exact lintegral_nnnorm_add hfm hgm }
  ... < âŠ¤ : add_lt_top.2 âŸ¨hfi, hgiâŸ©

lemma integrable.norm {f : Î± â†’ Î²} (hfi : integrable Î¼ f) : integrable Î¼ (Î»a, âˆ¥f aâˆ¥) :=
have eq : (Î»a, (nnnorm âˆ¥f aâˆ¥ : ennreal)) = Î»a, (nnnorm (f a) : ennreal),
  by { funext, rw nnnorm_norm },
by { rwa [integrable, eq] }

lemma integrable_norm_iff (f : Î± â†’ Î²) : integrable Î¼ (Î»a, âˆ¥f aâˆ¥) â†” integrable Î¼ f :=
have eq : (Î»a, (nnnorm âˆ¥f aâˆ¥ : ennreal)) = Î»a, (nnnorm (f a) : ennreal),
  by { funext, rw nnnorm_norm },
by { rw [integrable, integrable, eq] }

lemma integrable_of_integrable_bound {f : Î± â†’ Î²} {bound : Î± â†’ â„} (h : integrable Î¼ bound)
  (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ bound a) : integrable Î¼ f :=
have hâ‚ : âˆ€áµ a âˆ‚Î¼, (nnnorm (f a) : ennreal) â‰¤ ennreal.of_real (bound a),
begin
  filter_upwards [h_bound],
  simp only [mem_set_of_eq],
  assume a h,
  calc (nnnorm (f a) : ennreal) = ennreal.of_real (âˆ¥f aâˆ¥) : by rw of_real_norm_eq_coe_nnnorm
    ... â‰¤ ennreal.of_real (bound a) : ennreal.of_real_le_of_real h
end,
calc âˆ«â» a, nnnorm (f a) âˆ‚Î¼ â‰¤ âˆ«â» a, ennreal.of_real (bound a) âˆ‚Î¼ :
    lintegral_mono_ae hâ‚
  ... â‰¤ âˆ«â» a, (ennreal.of_real âˆ¥bound aâˆ¥) âˆ‚Î¼ : lintegral_mono $
    assume a, ennreal.of_real_le_of_real $ le_max_left (bound a) (-bound a)
  ... < âŠ¤ : by { rwa [integrable_iff_norm] at h }

section dominated_convergence

variables {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}

lemma all_ae_of_real_F_le_bound (h : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a) :
  âˆ€ n, âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥F n aâˆ¥ â‰¤ ennreal.of_real (bound a) :=
Î»n, by filter_upwards [h n] Î» a h, ennreal.of_real_le_of_real h

lemma all_ae_tendsto_of_real_norm (h : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top $ ğ“ $ f a) :
  âˆ€áµ a âˆ‚Î¼, tendsto (Î»n, ennreal.of_real âˆ¥F n aâˆ¥) at_top $ ğ“ $ ennreal.of_real âˆ¥f aâˆ¥ :=
by filter_upwards [h]
  Î» a h, tendsto_of_real $ tendsto.comp (continuous.tendsto continuous_norm _) h

lemma all_ae_of_real_f_le_bound (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥f aâˆ¥ â‰¤ ennreal.of_real (bound a) :=
begin
  have F_le_bound := all_ae_of_real_F_le_bound h_bound,
  rw â† ae_all_iff at F_le_bound,
  apply F_le_bound.mp ((all_ae_tendsto_of_real_norm h_lim).mono _),
  assume a tendsto_norm F_le_bound,
  exact le_of_tendsto' at_top_ne_bot tendsto_norm (F_le_bound)
end

lemma integrable_of_dominated_convergence {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}
  (bound_integrable : integrable Î¼ bound)
  (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  integrable Î¼ f :=
/- `âˆ¥F n aâˆ¥ â‰¤ bound a` and `âˆ¥F n aâˆ¥ --> âˆ¥f aâˆ¥` implies `âˆ¥f aâˆ¥ â‰¤ bound a`,
  and so `âˆ« âˆ¥fâˆ¥ â‰¤ âˆ« bound < âŠ¤` since `bound` is integrable -/
begin
  rw integrable_iff_norm,
  calc âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ â‰¤ âˆ«â» a, ennreal.of_real (bound a) âˆ‚Î¼ :
    lintegral_mono_ae $ all_ae_of_real_f_le_bound h_bound h_lim
    ... < âŠ¤ :
    begin
      rw â† integrable_iff_of_real,
      { exact bound_integrable },
      filter_upwards [h_bound 0] Î» a h, le_trans (norm_nonneg _) h,
    end
end

lemma tendsto_lintegral_norm_of_dominated_convergence [measurable_space Î²]
  [borel_space Î²] [second_countable_topology Î²]
  {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}
  (F_measurable : âˆ€ n, measurable (F n))
  (f_measurable : measurable f)
  (bound_integrable : integrable Î¼ bound)
  (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  tendsto (Î»n, âˆ«â» a, (ennreal.of_real âˆ¥F n a - f aâˆ¥) âˆ‚Î¼) at_top (ğ“ 0) :=
let b := Î»a, 2 * ennreal.of_real (bound a) in
/- `âˆ¥F n aâˆ¥ â‰¤ bound a` and `F n a --> f a` implies `âˆ¥f aâˆ¥ â‰¤ bound a`, and thus by the
  triangle inequality, have `âˆ¥F n a - f aâˆ¥ â‰¤ 2 * (bound a). -/
have hb : âˆ€ n, âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥F n a - f aâˆ¥ â‰¤ b a,
begin
  assume n,
  filter_upwards [all_ae_of_real_F_le_bound h_bound n, all_ae_of_real_f_le_bound h_bound h_lim],
  assume a hâ‚ hâ‚‚,
  calc ennreal.of_real âˆ¥F n a - f aâˆ¥ â‰¤ (ennreal.of_real âˆ¥F n aâˆ¥) + (ennreal.of_real âˆ¥f aâˆ¥) :
  begin
    rw [â† ennreal.of_real_add],
    apply of_real_le_of_real,
    { apply norm_sub_le }, { exact norm_nonneg _ }, { exact norm_nonneg _ }
  end
    ... â‰¤ (ennreal.of_real (bound a)) + (ennreal.of_real (bound a)) : add_le_add hâ‚ hâ‚‚
    ... = b a : by rw â† two_mul
end,
/- On the other hand, `F n a --> f a` implies that `âˆ¥F n a - f aâˆ¥ --> 0`  -/
have h : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, ennreal.of_real âˆ¥F n a - f aâˆ¥) at_top (ğ“ 0),
begin
  suffices h : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, ennreal.of_real âˆ¥F n a - f aâˆ¥) at_top (ğ“ $ ennreal.of_real 0),
  { rwa ennreal.of_real_zero at h },
  filter_upwards [h_lim],
  assume a h,
  refine tendsto.comp (continuous.tendsto continuous_of_real _) _,
  rw â† tendsto_iff_norm_tendsto_zero,
  exact h
end,
/- Therefore, by the dominated convergence theorem for nonnegative integration, have
  ` âˆ« âˆ¥f a - F n aâˆ¥ --> 0 ` -/
begin
  suffices h : tendsto (Î»n, âˆ«â» a, (ennreal.of_real âˆ¥F n a - f aâˆ¥) âˆ‚Î¼) at_top (ğ“ (âˆ«â» (a:Î±), 0 âˆ‚Î¼)),
  { rwa lintegral_zero at h },
  -- Using the dominated convergence theorem.
  refine tendsto_lintegral_of_dominated_convergence _ _ hb _ _,
  -- Show `Î»a, âˆ¥f a - F n aâˆ¥` is measurable for all `n`
  { exact Î»n, measurable_of_real.comp ((F_measurable n).sub f_measurable).norm },
  -- Show `2 * bound` is integrable
  { rw integrable_iff_of_real at bound_integrable,
    { calc âˆ«â» a, b a âˆ‚Î¼ = 2 * âˆ«â» a, ennreal.of_real (bound a) âˆ‚Î¼ :
        by { rw lintegral_const_mul', exact coe_ne_top }
        ... < âŠ¤ : mul_lt_top (coe_lt_top) bound_integrable },
    filter_upwards [h_bound 0] Î» a h, le_trans (norm_nonneg _) h },
  -- Show `âˆ¥f a - F n aâˆ¥ --> 0`
  { exact h }
end

end dominated_convergence

section pos_part
/-! Lemmas used for defining the positive part of a `LÂ¹` function -/

lemma integrable.max_zero {f : Î± â†’ â„} (hf : integrable Î¼ f) : integrable Î¼ (Î»a, max (f a) 0) :=
begin
  simp only [integrable_iff_norm] at *,
  calc âˆ«â» a, (ennreal.of_real âˆ¥max (f a) 0âˆ¥) âˆ‚Î¼ â‰¤ âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ :
    lintegral_mono
    begin
      assume a,
      apply of_real_le_of_real,
      simp only [real.norm_eq_abs],
      calc abs (max (f a) 0) = max (f a) 0 : by { rw abs_of_nonneg, apply le_max_right }
        ... â‰¤ abs (f a) : max_le (le_abs_self _) (abs_nonneg _)
    end
    ... < âŠ¤ : hf
end

lemma integrable.min_zero {f : Î± â†’ â„} (hf : integrable Î¼ f) : integrable Î¼ (Î»a, min (f a) 0) :=
begin
  have : (Î»a, min (f a) 0) = (Î»a, - max (-f a) 0),
  { funext, rw [min_eq_neg_max_neg_neg, neg_zero] },
  rw this,
  exact (integrable.max_zero hf.neg).neg,
end

end pos_part

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma integrable.smul (c : ğ•œ) {f : Î± â†’ Î²} : integrable Î¼ f â†’ integrable Î¼ (c â€¢ f) :=
begin
  simp only [integrable], assume hfi,
  calc
    âˆ«â» (a : Î±), nnnorm (c â€¢ f a) âˆ‚Î¼ = âˆ«â» (a : Î±), (nnnorm c) * nnnorm (f a) âˆ‚Î¼ :
      by simp only [nnnorm_smul, ennreal.coe_mul]
    ... < âŠ¤ :
    begin
      rw lintegral_const_mul',
      exacts [mul_lt_top coe_lt_top hfi, coe_ne_top]
    end
end

lemma integrable_smul_iff {c : ğ•œ} (hc : c â‰  0) (f : Î± â†’ Î²) :
  integrable Î¼ (c â€¢ f) â†” integrable Î¼ f :=
begin
  split,
  { assume h,
    simpa only [smul_smul, inv_mul_cancel hc, one_smul] using h.smul câ»Â¹ },
  exact integrable.smul _
end

end normed_space

variables [second_countable_topology Î²]

namespace ae_eq_fun

variable [measurable_space Î²]

section

variable [opens_measurable_space Î²]

/-- An almost everywhere equal function is `integrable` if it has a finite distance to the origin.
  Should mean the same thing as the predicate `integrable` over functions. -/
def integrable (f : Î± â†’â‚˜[Î¼] Î²) : Prop := f âˆˆ ball (0 : Î± â†’â‚˜[Î¼] Î²) âŠ¤

lemma integrable_mk {f : Î± â†’ Î²} (hf : measurable f) :
  (integrable (mk f hf : Î± â†’â‚˜[Î¼] Î²)) â†” measure_theory.integrable Î¼ f :=
by simp [integrable, zero_def, edist_mk_mk', measure_theory.integrable, nndist_eq_nnnorm]

lemma integrable_coe_fn (f : Î± â†’â‚˜[Î¼] Î²) : (measure_theory.integrable Î¼ f) â†” integrable f :=
by rw [â† integrable_mk, mk_coe_fn]

local attribute [simp] integrable_mk

lemma integrable_zero : integrable (0 : Î± â†’â‚˜[Î¼] Î²) := mem_ball_self coe_lt_top

end

section

variable [borel_space Î²]

lemma integrable.add {f g : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable g â†’ integrable (f + g) :=
begin
  refine induction_onâ‚‚ f g (Î» f hf g hg, _),
  simp only [integrable_mk, mk_add_mk],
  exact Î» hfi hgi, hfi.add hf hg hgi
end

lemma integrable.neg {f : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable (-f) :=
induction_on f $ Î» f hfm hfi, (integrable_mk _).2 ((integrable_mk hfm).1 hfi).neg

lemma integrable.sub {f g : Î± â†’â‚˜[Î¼] Î²} (hf : integrable f) (hg : integrable g) :
  integrable (f - g) :=
hf.add hg.neg

protected lemma is_add_subgroup : is_add_subgroup (ball (0 : Î± â†’â‚˜[Î¼] Î²) âŠ¤) :=
{ zero_mem := integrable_zero,
  add_mem := Î» _ _, integrable.add,
  neg_mem := Î» _, integrable.neg }

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma integrable.smul {c : ğ•œ} {f : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable (c â€¢ f) :=
induction_on f $ Î» f hfm hfi, (integrable_mk _).2 $ ((integrable_mk hfm).1 hfi).smul _

end normed_space

end

end ae_eq_fun

variables (Î± Î²)
/-- The space of equivalence classes of integrable (and measurable) functions, where two integrable
    functions are equivalent if they agree almost everywhere, i.e., they differ on a set of measure
    `0`. -/
def l1 [measurable_space Î²] [opens_measurable_space Î²] (Î¼ : measure Î±) : Type (max u v) := {f : Î± â†’â‚˜[Î¼] Î² // f.integrable}

notation Î± ` â†’â‚[`:25 Î¼ `] ` Î² := l1 Î± Î² Î¼

variables {Î± Î²}

namespace l1
open ae_eq_fun
local attribute [instance] ae_eq_fun.is_add_subgroup

variables [measurable_space Î²]

section

variable [opens_measurable_space Î²]

instance : has_coe (Î± â†’â‚[Î¼] Î²) (Î± â†’â‚˜[Î¼] Î²) := coe_subtype

protected lemma eq {f g : Î± â†’â‚[Î¼] Î²} : (f : Î± â†’â‚˜[Î¼] Î²) = (g : Î± â†’â‚˜[Î¼] Î²) â†’ f = g := subtype.eq
@[norm_cast] protected lemma eq_iff {f g : Î± â†’â‚[Î¼] Î²} : (f : Î± â†’â‚˜[Î¼] Î²) = (g : Î± â†’â‚˜[Î¼] Î²) â†” f = g :=
iff.intro (l1.eq) (congr_arg coe)

/- TODO : order structure of l1-/

/-- `LÂ¹` space forms a `emetric_space`, with the emetric being inherited from almost everywhere
  functions, i.e., `edist f g = âˆ«â» a, edist (f a) (g a)`. -/
instance : emetric_space (Î± â†’â‚[Î¼] Î²) := subtype.emetric_space

/-- `LÂ¹` space forms a `metric_space`, with the metric being inherited from almost everywhere
  functions, i.e., `edist f g = ennreal.to_real (âˆ«â» a, edist (f a) (g a))`. -/
instance : metric_space (Î± â†’â‚[Î¼] Î²) := metric_space_emetric_ball 0 âŠ¤

end

variable [borel_space Î²]

instance : add_comm_group (Î± â†’â‚[Î¼] Î²) := subtype.add_comm_group

instance : inhabited (Î± â†’â‚[Î¼] Î²) := âŸ¨0âŸ©

@[simp, norm_cast] lemma coe_zero : ((0 : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = 0 := rfl
@[simp, norm_cast] lemma coe_add (f g : Î± â†’â‚[Î¼] Î²) : ((f + g : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = f + g := rfl
@[simp, norm_cast] lemma coe_neg (f : Î± â†’â‚[Î¼] Î²) : ((-f : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = -f := rfl
@[simp, norm_cast] lemma coe_sub (f g : Î± â†’â‚[Î¼] Î²) : ((f - g : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = f - g := rfl

@[simp] lemma edist_eq (f g : Î± â†’â‚[Î¼] Î²) : edist f g = edist (f : Î± â†’â‚˜[Î¼] Î²) (g : Î± â†’â‚˜[Î¼] Î²) := rfl

lemma dist_eq (f g : Î± â†’â‚[Î¼] Î²) : dist f g = ennreal.to_real (edist (f : Î± â†’â‚˜[Î¼] Î²) (g : Î± â†’â‚˜[Î¼] Î²)) := rfl

/-- The norm on `LÂ¹` space is defined to be `âˆ¥fâˆ¥ = âˆ«â» a, edist (f a) 0`. -/
instance : has_norm (Î± â†’â‚[Î¼] Î²) := âŸ¨Î» f, dist f 0âŸ©

lemma norm_eq (f : Î± â†’â‚[Î¼] Î²) : âˆ¥fâˆ¥ = ennreal.to_real (edist (f : Î± â†’â‚˜[Î¼] Î²) 0) := rfl

instance : normed_group (Î± â†’â‚[Î¼] Î²) := normed_group.of_add_dist (Î» x, rfl) $ by
{ intros, simp only [dist_eq, coe_add], rw edist_add_right }

section normed_space

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

instance : has_scalar ğ•œ (Î± â†’â‚[Î¼] Î²) := âŸ¨Î» x f, âŸ¨x â€¢ (f : Î± â†’â‚˜[Î¼] Î²), ae_eq_fun.integrable.smul f.2âŸ©âŸ©

@[simp, norm_cast] lemma coe_smul (c : ğ•œ) (f : Î± â†’â‚[Î¼] Î²) :
  ((c â€¢ f : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = c â€¢ (f : Î± â†’â‚˜[Î¼] Î²) := rfl

instance : semimodule ğ•œ (Î± â†’â‚[Î¼] Î²) :=
{ one_smul  := Î»f, l1.eq (by { simp only [coe_smul], exact one_smul _ _ }),
  mul_smul  := Î»x y f, l1.eq (by { simp only [coe_smul], exact mul_smul _ _ _ }),
  smul_add  := Î»x f g, l1.eq (by { simp only [coe_smul, coe_add], exact smul_add _ _ _ }),
  smul_zero := Î»x, l1.eq (by { simp only [coe_zero, coe_smul], exact smul_zero _ }),
  add_smul  := Î»x y f, l1.eq (by { simp only [coe_smul], exact add_smul _ _ _ }),
  zero_smul := Î»f, l1.eq (by { simp only [coe_smul], exact zero_smul _ _ }) }

instance : normed_space ğ•œ (Î± â†’â‚[Î¼] Î²) :=
âŸ¨ begin
    rintros x âŸ¨f, hfâŸ©,
    show ennreal.to_real (edist (x â€¢ f) 0) â‰¤ âˆ¥xâˆ¥ * ennreal.to_real (edist f 0),
    rw [edist_smul, to_real_of_real_mul],
    exact norm_nonneg _
  end âŸ©

end normed_space

section of_fun

/-- Construct the equivalence class `[f]` of a measurable and integrable function `f`. -/
def of_fun (f : Î± â†’ Î²) (hfm : measurable f) (hfi : integrable Î¼ f) : (Î± â†’â‚[Î¼] Î²) :=
âŸ¨mk f hfm, by { rw integrable_mk, exact hfi }âŸ©

@[simp] lemma of_fun_eq_mk (f : Î± â†’ Î²) (hfm hfi) :
  ((of_fun f hfm hfi : Î± â†’â‚[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î²) = mk f hfm :=
rfl

lemma of_fun_eq_of_fun (f g : Î± â†’ Î²) (hfm hfi hgm hgi) :
  (of_fun f hfm hfi : Î± â†’â‚[Î¼] Î²) = of_fun g hgm hgi â†” f =áµ[Î¼] g :=
by { rw â† l1.eq_iff, simp only [of_fun_eq_mk, mk_eq_mk] }

lemma of_fun_zero :
  (of_fun (Î» _, 0) measurable_zero (integrable_zero Î± Î¼ Î²) : Î± â†’â‚[Î¼] Î²) = 0 := rfl

lemma of_fun_add (f g : Î± â†’ Î²) (hfm hfi hgm hgi) :
  (of_fun (f + g) (measurable.add hfm hgm) (integrable.add hfm hfi hgm hgi) : Î± â†’â‚[Î¼] Î²)
    = of_fun f hfm hfi + of_fun g hgm hgi :=
rfl

lemma of_fun_neg (f : Î± â†’ Î²) (hfm hfi) :
  (of_fun (- f) (measurable.neg hfm) (integrable.neg hfi) : Î± â†’â‚[Î¼] Î²) = - of_fun f hfm hfi := rfl

lemma of_fun_sub (f g : Î± â†’ Î²) (hfm hfi hgm hgi) :
  (of_fun (f - g) (measurable.sub hfm hgm) (integrable.sub hfm hfi hgm hgi) : Î± â†’â‚[Î¼] Î²)
    = of_fun f hfm hfi - of_fun g hgm hgi :=
rfl

lemma norm_of_fun (f : Î± â†’ Î²) (hfm hfi) :
  âˆ¥(of_fun f hfm hfi : Î± â†’â‚[Î¼] Î²)âˆ¥ = ennreal.to_real (âˆ«â» a, edist (f a) 0 âˆ‚Î¼) :=
rfl

lemma norm_of_fun_eq_lintegral_norm (f : Î± â†’ Î²) (hfm hfi) :
  âˆ¥(of_fun f hfm hfi : Î± â†’â‚[Î¼] Î²)âˆ¥ = ennreal.to_real (âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼) :=
by { rw [norm_of_fun, lintegral_norm_eq_lintegral_edist] }

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma of_fun_smul (f : Î± â†’ Î²) (hfm : measurable f) (hfi : integrable Î¼ f) (k : ğ•œ) :
  of_fun (Î»a, k â€¢ f a) (hfm.const_smul _) (hfi.smul _) = k â€¢ of_fun f hfm hfi := rfl

end of_fun

section to_fun

/-- Find a representative of an `LÂ¹` function [f] -/
@[reducible]
protected def to_fun (f : Î± â†’â‚[Î¼] Î²) : Î± â†’ Î² := (f : Î± â†’â‚˜[Î¼] Î²)

protected lemma measurable (f : Î± â†’â‚[Î¼] Î²) : measurable f.to_fun := f.1.measurable

protected lemma integrable (f : Î± â†’â‚[Î¼] Î²) : integrable Î¼ f.to_fun :=
by { rw [l1.to_fun, integrable_coe_fn], exact f.2 }

lemma of_fun_to_fun (f : Î± â†’â‚[Î¼] Î²) : of_fun (f.to_fun) f.measurable f.integrable = f :=
begin
  rcases f with âŸ¨f, hfiâŸ©,
  rw [of_fun, subtype.mk_eq_mk],
  exact f.mk_coe_fn
end

lemma mk_to_fun (f : Î± â†’â‚[Î¼] Î²) : (mk (f.to_fun) f.measurable : Î± â†’â‚˜[Î¼] Î²)= f :=
by { rw â† of_fun_eq_mk, rw l1.eq_iff, exact of_fun_to_fun f }

lemma to_fun_of_fun (f : Î± â†’ Î²) (hfm hfi) :
  (of_fun f hfm hfi : Î± â†’â‚[Î¼] Î²).to_fun =áµ[Î¼] f :=
coe_fn_mk f hfm

variables (Î± Î²)
lemma zero_to_fun : (0 : Î± â†’â‚[Î¼] Î²).to_fun =áµ[Î¼] 0 := ae_eq_fun.coe_fn_zero
variables {Î± Î²}

lemma add_to_fun (f g : Î± â†’â‚[Î¼] Î²) : (f + g).to_fun =áµ[Î¼] f.to_fun + g.to_fun :=
ae_eq_fun.coe_fn_add _ _

lemma neg_to_fun (f : Î± â†’â‚[Î¼] Î²) : (-f).to_fun =áµ[Î¼] -f.to_fun := ae_eq_fun.coe_fn_neg _

lemma sub_to_fun (f g : Î± â†’â‚[Î¼] Î²) : (f - g).to_fun =áµ[Î¼] f.to_fun - g.to_fun :=
ae_eq_fun.coe_fn_sub _ _

lemma dist_to_fun (f g : Î± â†’â‚[Î¼] Î²) :
  dist f g = ennreal.to_real (âˆ«â» x, edist (f.to_fun x) (g.to_fun x) âˆ‚Î¼) :=
by { simp only [dist_eq, edist_eq_coe] }

lemma norm_eq_nnnorm_to_fun (f : Î± â†’â‚[Î¼] Î²) : âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â» a, nnnorm (f.to_fun a) âˆ‚Î¼) :=
by { rw [lintegral_nnnorm_eq_lintegral_edist, â† edist_zero_eq_coe], refl }

lemma norm_eq_norm_to_fun (f : Î± â†’â‚[Î¼] Î²) :
  âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â» a, (ennreal.of_real âˆ¥f.to_fun aâˆ¥) âˆ‚Î¼) :=
by { rw norm_eq_nnnorm_to_fun, congr, funext, rw of_real_norm_eq_coe_nnnorm }

lemma lintegral_edist_to_fun_lt_top (f g : Î± â†’â‚[Î¼] Î²) : (âˆ«â» a, edist (f.to_fun a) (g.to_fun a) âˆ‚Î¼) < âŠ¤ :=
begin
  apply lintegral_edist_lt_top,
  exact f.measurable, exact f.integrable, exact g.measurable, exact g.integrable
end

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma smul_to_fun (c : ğ•œ) (f : Î± â†’â‚[Î¼] Î²) : (c â€¢ f).to_fun =áµ[Î¼] c â€¢ f.to_fun :=
ae_eq_fun.coe_fn_smul _ _

end to_fun

section pos_part

/-- Positive part of a function in `LÂ¹` space. -/
def pos_part (f : Î± â†’â‚[Î¼] â„) : Î± â†’â‚[Î¼] â„ :=
âŸ¨ae_eq_fun.pos_part f,
  begin
    rw [â† ae_eq_fun.integrable_coe_fn, integrable_congr (coe_fn_pos_part _)],
    exact integrable.max_zero f.integrable
  end âŸ©

/-- Negative part of a function in `LÂ¹` space. -/
def neg_part (f : Î± â†’â‚[Î¼] â„) : Î± â†’â‚[Î¼] â„ := pos_part (-f)

@[norm_cast] lemma coe_pos_part (f : Î± â†’â‚[Î¼] â„) : (f.pos_part : Î± â†’â‚˜[Î¼] â„) = (f : Î± â†’â‚˜[Î¼] â„).pos_part := rfl

lemma pos_part_to_fun (f : Î± â†’â‚[Î¼] â„) : (pos_part f).to_fun =áµ[Î¼] Î» a, max (f.to_fun a) 0 :=
ae_eq_fun.coe_fn_pos_part _

lemma neg_part_to_fun_eq_max (f : Î± â†’â‚[Î¼] â„) : âˆ€áµ a âˆ‚Î¼, (neg_part f).to_fun a = max (- f.to_fun a) 0 :=
begin
  rw neg_part,
  filter_upwards [pos_part_to_fun (-f), neg_to_fun f],
  simp only [mem_set_of_eq],
  assume a hâ‚ hâ‚‚,
  rw [hâ‚, hâ‚‚, pi.neg_apply]
end

lemma neg_part_to_fun_eq_min (f : Î± â†’â‚[Î¼] â„) : âˆ€áµ a âˆ‚Î¼, (neg_part f).to_fun a = - min (f.to_fun a) 0 :=
begin
  filter_upwards [neg_part_to_fun_eq_max f],
  simp only [mem_set_of_eq],
  assume a h,
  rw [h, min_eq_neg_max_neg_neg, _root_.neg_neg, neg_zero],
end

lemma norm_le_norm_of_ae_le {f g : Î± â†’â‚[Î¼] Î²} (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f.to_fun aâˆ¥ â‰¤ âˆ¥g.to_fun aâˆ¥) : âˆ¥fâˆ¥ â‰¤ âˆ¥gâˆ¥ :=
begin
  simp only [l1.norm_eq_norm_to_fun],
  rw to_real_le_to_real,
  { apply lintegral_mono_ae,
    exact h.mono (Î» a h, of_real_le_of_real h) },
  { rw [â† lt_top_iff_ne_top, â† integrable_iff_norm], exact f.integrable },
  { rw [â† lt_top_iff_ne_top, â† integrable_iff_norm], exact g.integrable }
end

lemma continuous_pos_part : continuous $ Î»f : Î± â†’â‚[Î¼] â„, pos_part f :=
begin
  simp only [metric.continuous_iff],
  assume g Îµ hÎµ,
  use Îµ, use hÎµ,
  simp only [dist_eq_norm],
  assume f hfg,
  refine lt_of_le_of_lt (norm_le_norm_of_ae_le _) hfg,
  filter_upwards [l1.sub_to_fun f g, l1.sub_to_fun (pos_part f) (pos_part g),
    pos_part_to_fun f, pos_part_to_fun g],
  simp only [mem_set_of_eq],
  assume a hâ‚ hâ‚‚ hâ‚ƒ hâ‚„,
  simp only [real.norm_eq_abs, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, pi.sub_apply],
  exact abs_max_sub_max_le_abs _ _ _
end

lemma continuous_neg_part : continuous $ Î»f : Î± â†’â‚[Î¼] â„, neg_part f :=
have eq : (Î»f : Î± â†’â‚[Î¼] â„, neg_part f) = (Î»f : Î± â†’â‚[Î¼] â„, pos_part (-f)) := rfl,
by { rw eq, exact continuous_pos_part.comp continuous_neg }

end pos_part

/- TODO: l1 is a complete space -/

end l1

end measure_theory
