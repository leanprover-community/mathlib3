/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import analysis.normed_space.inner_product
import measure_theory.set_integral

/-! # `L^2` space

-/

noncomputable theory
open topological_space measure_theory measure_theory.Lp
open_locale nnreal ennreal

namespace measure_theory

variables {Î± E F ğ•œ : Type*} [is_R_or_C ğ•œ] [measurable_space Î±] {Î¼ : measure Î±}
  [measurable_space E] [inner_product_space ğ•œ E] [borel_space E] [second_countable_topology E]
  [normed_group F] [measurable_space F] [borel_space F] [second_countable_topology F]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y

lemma two_mul_le_add_sq (a b : â„) : 2 * a * b â‰¤ a ^ 2 + b ^ 2 :=
begin
  suffices h_nonneg : 0 â‰¤ a ^ 2 + b ^ 2 - 2 * a * b, by rwa sub_nonneg at h_nonneg,
  calc 0 â‰¤ (a - b) ^ 2               : pow_two_nonneg _
     ... = a ^ 2 + b ^ 2 - 2 * a * b : by ring,
end

lemma snorm_rpow_two_norm_lt_top (f : Lp F 2 Î¼) : snorm (Î» x, âˆ¥f xâˆ¥ ^ (2 : â„)) 1 Î¼ < âˆ :=
begin
  have h_two : ennreal.of_real (2 : â„) = 2, by simp [zero_le_one],
  rw [snorm_norm_rpow f zero_lt_two, one_mul, h_two],
  exact ennreal.rpow_lt_top_of_nonneg zero_le_two (Lp.snorm_ne_top f),
end

lemma snorm_inner_lt_top (f g : Lp E 2 Î¼) : snorm (Î» (x : Î±), âŸªf x, g xâŸ«) 1 Î¼ < âˆ :=
begin
  have h : âˆ€ x, is_R_or_C.abs âŸªf x, g xâŸ« â‰¤ âˆ¥f xâˆ¥ * âˆ¥g xâˆ¥, from Î» x, abs_inner_le_norm _ _,
  have h' : âˆ€ x, is_R_or_C.abs âŸªf x, g xâŸ« â‰¤ is_R_or_C.abs (âˆ¥f xâˆ¥^2 + âˆ¥g xâˆ¥^2),
  { refine Î» x, le_trans (h x) _,
    rw [is_R_or_C.abs_to_real, abs_eq_self.mpr],
    swap, { exact add_nonneg (by simp) (by simp), },
    refine le_trans _ (half_le_self (add_nonneg (pow_two_nonneg _) (pow_two_nonneg _))),
    refine (le_div_iff (@zero_lt_two â„ _ _)).mpr (le_trans (le_of_eq _) (two_mul_le_add_sq _ _)),
    ring, },
  simp_rw [â† is_R_or_C.norm_eq_abs, â† real.rpow_nat_cast] at h',
  refine (snorm_mono_ae (ae_of_all _ h')).trans_lt ((snorm_add_le _ _ le_rfl).trans_lt _),
  { exact (Lp.ae_measurable f).norm.rpow_const, },
  { exact (Lp.ae_measurable g).norm.rpow_const, },
  simp only [nat.cast_bit0, ennreal.add_lt_top, nat.cast_one],
  exact âŸ¨snorm_rpow_two_norm_lt_top f, snorm_rpow_two_norm_lt_top gâŸ©,
end

section inner_product_space

variables [measurable_space ğ•œ] [borel_space ğ•œ]

include ğ•œ

instance : has_inner ğ•œ (Lp E 2 Î¼) := âŸ¨Î» f g, âˆ« a, âŸªf a, g aâŸ« âˆ‚Î¼âŸ©

lemma inner_def (f g : Lp E 2 Î¼) : inner f g = âˆ« a : Î±, âŸªf a, g aâŸ« âˆ‚Î¼ := rfl

lemma integral_inner_eq_sq_snorm (f : Lp E 2 Î¼) :
  âˆ« a, (inner (f a) (f a) : ğ•œ) âˆ‚Î¼ = ennreal.to_real âˆ«â» a, (nnnorm (f a) : â„â‰¥0âˆ) ^ (2:â„) âˆ‚Î¼ :=
begin
  simp_rw inner_self_eq_norm_sq_to_K,
  norm_cast,
  rw integral_eq_lintegral_of_nonneg_ae,
  swap, { exact filter.eventually_of_forall (Î» x, pow_two_nonneg _), },
  swap, { exact (Lp.ae_measurable f).norm.pow, },
  congr,
  ext1 x,
  have h_two : (2 : â„) = ((2 : â„•) : â„), by simp,
  rw [â† real.rpow_nat_cast _ 2, â† h_two,
    â† ennreal.of_real_rpow_of_nonneg (norm_nonneg _) zero_le_two, of_real_norm_eq_coe_nnnorm],
  norm_cast,
end

private lemma norm_sq_eq_inner' (f : Lp E 2 Î¼) : âˆ¥fâˆ¥ ^ 2 = is_R_or_C.re (inner f f : ğ•œ) :=
begin
  have h_two : (2 : â„â‰¥0âˆ).to_real = 2 := by simp,
  rw [inner_def, integral_inner_eq_sq_snorm, norm_def, â† ennreal.to_real_pow, is_R_or_C.of_real_re,
    ennreal.to_real_eq_to_real (ennreal.pow_lt_top (Lp.snorm_lt_top f) 2) _],
  { rw [â†ennreal.rpow_nat_cast, snorm_eq_snorm' ennreal.two_ne_zero ennreal.two_ne_top, snorm',
      â† ennreal.rpow_mul, one_div, h_two],
    simp, },
  { refine lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top zero_lt_two _,
    rw [â† h_two, â† snorm_eq_snorm' ennreal.two_ne_zero ennreal.two_ne_top],
    exact Lp.snorm_lt_top f, },
end

lemma mem_L1_inner (f g : Lp E 2 Î¼) :
  ae_eq_fun.mk (Î» x, âŸªf x, g xâŸ«) ((Lp.ae_measurable f).inner (Lp.ae_measurable g)) âˆˆ Lp ğ•œ 1 Î¼ :=
by { simp_rw [mem_Lp_iff_snorm_lt_top, snorm_ae_eq_fun], exact snorm_inner_lt_top f g, }

lemma integrable_inner (f g : Lp E 2 Î¼) : integrable (Î» x : Î±, âŸªf x, g xâŸ«) Î¼ :=
(integrable_congr (ae_eq_fun.coe_fn_mk (Î» x, âŸªf x, g xâŸ«)
    ((Lp.ae_measurable f).inner (Lp.ae_measurable g)))).mp
  (ae_eq_fun.integrable_iff_mem_L1.mpr (mem_L1_inner f g))

private lemma add_left' (f f' g : Lp E 2 Î¼) : (inner (f + f') g : ğ•œ) = inner f g + inner f' g :=
begin
  simp_rw [inner_def, â† integral_add (integrable_inner f g) (integrable_inner f' g),
    â†inner_add_left],
  refine integral_congr_ae ((coe_fn_add f f').mono (Î» x hx, _)),
  congr,
  rwa pi.add_apply at hx,
end

private lemma smul_left' (f g : Lp E 2 Î¼) (r : ğ•œ) :
  inner (r â€¢ f) g = is_R_or_C.conj r * inner f g :=
begin
  rw [inner_def, inner_def, â† smul_eq_mul, â† integral_smul],
  refine integral_congr_ae ((coe_fn_smul r f).mono (Î» x hx, _)),
  rw [smul_eq_mul, â† inner_smul_left],
  congr,
  rwa pi.smul_apply at hx,
end

instance : inner_product_space ğ•œ (Lp E 2 Î¼) :=
{ norm_sq_eq_inner := norm_sq_eq_inner',
  conj_sym := Î» _ _, by simp_rw [inner_def, â† integral_conj, inner_conj_sym],
  add_left := add_left',
  smul_left := smul_left', }

end inner_product_space

end measure_theory
