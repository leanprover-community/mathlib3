/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import analysis.normed_space.inner_product
import measure_theory.set_integral

/-! # `L^2` space

-/

noncomputable theory
open topological_space measure_theory measure_theory.Lp
open_locale nnreal ennreal

namespace measure_theory

variables {Œ± E F G ùïú : Type*} [is_R_or_C ùïú] [measurable_space Œ±] {Œº : measure Œ±} {p : ‚Ñù‚â•0‚àû}
  [measurable_space E] [inner_product_space ùïú E] [borel_space E] [second_countable_topology E]
  [normed_group F] [measurable_space F] [borel_space F] [second_countable_topology F]
  [normed_group G]

lemma two_mul_le_add_sq (a b : ‚Ñù) : 2 * a * b ‚â§ a ^ 2 + b ^ 2 :=
begin
  suffices h_nonneg : 0 ‚â§ a ^ 2 + b ^ 2 - 2 * a * b, by rwa sub_nonneg at h_nonneg,
  calc 0 ‚â§ (a - b) ^ 2               : pow_two_nonneg _
     ... = a ^ 2 + b ^ 2 - 2 * a * b : by ring,
end

lemma snorm_rpow_two_norm_lt_top (f : Lp F 2 Œº) :
  snorm (Œª (x : Œ±), ‚à•f x‚à• ^ (2 : ‚Ñù)) 1 Œº < ‚àû :=
begin
  have h_two : ennreal.of_real (2 : ‚Ñù) = 2, by simp [zero_le_one],
  rw [snorm_norm_rpow f zero_lt_two, one_mul, h_two],
  exact ennreal.rpow_lt_top_of_nonneg zero_le_two (Lp.snorm_ne_top f),
end

lemma snorm_inner_lt_top (f g : Lp E 2 Œº) : snorm (Œª (x : Œ±), (inner (f x) (g x) : ùïú)) 1 Œº < ‚àû :=
begin
  have h : ‚àÄ x, is_R_or_C.abs (inner (f x) (g x) : ùïú) ‚â§ ‚à•f x‚à• * ‚à•g x‚à•,
    from Œª x, abs_inner_le_norm _ _,
  have h' : ‚àÄ x, is_R_or_C.abs (inner (f x) (g x) : ùïú) ‚â§ ‚à• ‚à•f x‚à•^2 + ‚à•g x‚à•^2 ‚à•,
  { simp_rw real.norm_eq_abs,
    refine Œª x, le_trans (h x) _,
    rw abs_eq_self.mpr,
    swap, { exact add_nonneg (by simp) (by simp), },
    refine le_trans _ (half_le_self (add_nonneg (pow_two_nonneg _) (pow_two_nonneg _))),
    refine (le_div_iff (@zero_lt_two ‚Ñù _ _)).mpr (le_trans (le_of_eq _) (two_mul_le_add_sq _ _)),
    ring, },
  simp_rw [‚Üê is_R_or_C.norm_eq_abs, ‚Üê real.rpow_nat_cast] at h',
  refine (snorm_mono_ae (ae_of_all _ h')).trans_lt ((snorm_add_le _ _ le_rfl).trans_lt _),
  { exact (Lp.ae_measurable f).norm.rpow_const, },
  { exact (Lp.ae_measurable g).norm.rpow_const, },
  simp only [nat.cast_bit0, ennreal.add_lt_top, nat.cast_one],
  exact ‚ü®snorm_rpow_two_norm_lt_top f, snorm_rpow_two_norm_lt_top g‚ü©,
end

section inner_product_space

variables [measurable_space ùïú] [borel_space ùïú]

include ùïú

instance : has_inner ùïú (Lp E 2 Œº) :=
{inner := Œª (f g : Lp E 2 Œº), ‚à´ a : Œ±, (inner (f a) (g a)) ‚àÇŒº }

lemma inner_def (f g : Lp E 2 Œº) : inner f g = ‚à´ a : Œ±, (inner (f a) (g a) : ùïú) ‚àÇŒº := rfl

lemma integral_inner_eq_sq_snorm (f : Lp E 2 Œº) :
  ‚à´ a, (inner (f a) (f a) : ùïú) ‚àÇŒº = ennreal.to_real ‚à´‚Åª a, (nnnorm (f a) : ‚Ñù‚â•0‚àû) ^ (2:‚Ñù) ‚àÇŒº :=
begin
  simp_rw inner_self_eq_norm_sq_to_K,
  norm_cast,
  rw integral_eq_lintegral_of_nonneg_ae,
  swap, { exact filter.eventually_of_forall (Œª x, pow_two_nonneg _), },
  swap, { exact (Lp.ae_measurable f).norm.pow, },
  congr,
  ext1 x,
  have h_two : (2 : ‚Ñù) = ((2 : ‚Ñï) : ‚Ñù), by simp,
  rw [‚Üê real.rpow_nat_cast _ 2, ‚Üê h_two,
    ‚Üêennreal.of_real_rpow_of_nonneg (norm_nonneg _) zero_le_two, of_real_norm_eq_coe_nnnorm],
  norm_cast,
end

private lemma norm_sq_eq_inner' (f : Lp E 2 Œº) : ‚à•f‚à• ^ 2 = is_R_or_C.re (inner f f : ùïú) :=
begin
  have h_two : (2 : ‚Ñù‚â•0‚àû).to_real = 2 := by simp,
  rw [inner_def, integral_inner_eq_sq_snorm, norm_def, ‚Üê ennreal.to_real_pow, is_R_or_C.of_real_re,
    ennreal.to_real_eq_to_real (ennreal.pow_lt_top (Lp.snorm_lt_top f) 2) _],
  { rw [‚Üêennreal.rpow_nat_cast, snorm_eq_snorm' ennreal.two_ne_zero ennreal.two_ne_top, snorm',
      ‚Üê ennreal.rpow_mul, one_div, h_two],
    simp, },
  { refine lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top zero_lt_two _,
    rw [‚Üê h_two, ‚Üê snorm_eq_snorm' ennreal.two_ne_zero ennreal.two_ne_top],
    exact Lp.snorm_lt_top f, },
end

lemma mem_L1_inner (f g : Lp E 2 Œº) :
  ae_eq_fun.mk (Œª x, inner (f x) (g x))
    (ae_measurable.inner (Lp.ae_measurable f) (Lp.ae_measurable g)) ‚àà Lp ùïú 1 Œº :=
by { simp_rw [mem_Lp_iff_snorm_lt_top, snorm_ae_eq_fun], exact snorm_inner_lt_top f g, }

lemma integrable_inner (f g : Lp E 2 Œº) : integrable (Œª x : Œ±, (inner (f x) (g x) : ùïú)) Œº :=
begin
  refine (integrable_congr (ae_eq_fun.coe_fn_mk (Œª x, inner (f x) (g x))
    (ae_measurable.inner (Lp.ae_measurable f) (Lp.ae_measurable g)))).mp _,
  exact ae_eq_fun.integrable_iff_mem_L1.mpr (mem_L1_inner f g),
end

private lemma add_left' (f f' g : Lp E 2 Œº) : (inner (f + f') g : ùïú) = inner f g + inner f' g :=
begin
  simp_rw [inner_def, ‚Üê integral_add (integrable_inner f g) (integrable_inner f' g),
    ‚Üêinner_add_left],
  refine integral_congr_ae ((coe_fn_add f f').mono (Œª x hx, _)),
  congr,
  rwa pi.add_apply at hx,
end

private lemma smul_left' (f g : Lp E 2 Œº) (r : ùïú) :
  inner (r ‚Ä¢ f) g = is_R_or_C.conj r * inner f g :=
begin
  rw [inner_def, inner_def, ‚Üê smul_eq_mul, ‚Üê integral_smul],
  refine integral_congr_ae ((coe_fn_smul r f).mono (Œª x hx, _)),
  rw [smul_eq_mul, ‚Üê inner_smul_left],
  congr,
  rwa pi.smul_apply at hx,
end

instance : inner_product_space ùïú (Lp E 2 Œº) :=
{ norm_sq_eq_inner := norm_sq_eq_inner',
  conj_sym := Œª _ _, by simp_rw [inner_def, ‚Üê integral_conj, inner_conj_sym],
  add_left := add_left',
  smul_left := smul_left', }

end inner_product_space

end measure_theory
