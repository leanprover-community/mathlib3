/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import measure_theory.measure_space
import measure_theory.borel_space

/-!
# Lebesgue measure on the real line
-/

noncomputable theory
open classical set filter
open ennreal (of_real)
open_locale big_operators

namespace measure_theory

/-- Length of an interval. This is the largest monotonic function which correctly
  measures all intervals. -/
def lebesgue_length (s : set ‚Ñù) : ennreal := ‚®Öa b (h : s ‚äÜ Ico a b), of_real (b - a)

@[simp] lemma lebesgue_length_empty : lebesgue_length ‚àÖ = 0 :=
le_zero_iff_eq.1 $ infi_le_of_le 0 $ infi_le_of_le 0 $ by simp

@[simp] lemma lebesgue_length_Ico (a b : ‚Ñù) :
  lebesgue_length (Ico a b) = of_real (b - a) :=
begin
  refine le_antisymm (infi_le_of_le a $ binfi_le b (subset.refl _))
    (le_infi $ Œª a', le_infi $ Œª b', le_infi $ Œª h, ennreal.coe_le_coe.2 _),
  cases le_or_lt b a with ab ab,
  { rw nnreal.of_real_of_nonpos (sub_nonpos.2 ab), apply zero_le },
  cases (Ico_subset_Ico_iff ab).1 h with h‚ÇÅ h‚ÇÇ,
  exact nnreal.of_real_le_of_real (sub_le_sub h‚ÇÇ h‚ÇÅ)
end

lemma lebesgue_length_mono {s‚ÇÅ s‚ÇÇ : set ‚Ñù} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) :
  lebesgue_length s‚ÇÅ ‚â§ lebesgue_length s‚ÇÇ :=
infi_le_infi $ Œª a, infi_le_infi $ Œª b, infi_le_infi2 $ Œª h', ‚ü®subset.trans h h', le_refl _‚ü©

lemma lebesgue_length_eq_infi_Ioo (s) :
  lebesgue_length s = ‚®Öa b (h : s ‚äÜ Ioo a b), of_real (b - a) :=
begin
  refine le_antisymm
    (infi_le_infi $ Œª a, infi_le_infi $ Œª b, infi_le_infi2 $ Œª h,
      ‚ü®subset.trans h Ioo_subset_Ico_self, le_refl _‚ü©) _,
  refine le_infi (Œª a, le_infi $ Œª b, le_infi $ Œª h, _),
  refine ennreal.le_of_forall_epsilon_le (Œª Œµ Œµ0 _, _),
  refine infi_le_of_le (a - Œµ) (infi_le_of_le b $ infi_le_of_le
    (subset.trans h $ Ico_subset_Ioo_left $ (sub_lt_self_iff _).2 Œµ0) _),
  rw ‚Üê sub_add,
  refine le_trans ennreal.of_real_add_le (add_le_add_left _ _),
  simp only [ennreal.of_real_coe_nnreal, le_refl]
end

@[simp] lemma lebesgue_length_Ioo (a b : ‚Ñù) :
  lebesgue_length (Ioo a b) = of_real (b - a) :=
begin
  rw ‚Üê lebesgue_length_Ico,
  refine le_antisymm (lebesgue_length_mono Ioo_subset_Ico_self) _,
  rw lebesgue_length_eq_infi_Ioo (Ioo a b),
  refine (le_infi $ Œª a', le_infi $ Œª b', le_infi $ Œª h, _),
  cases le_or_lt b a with ab ab, {simp [ab]},
  cases (Ioo_subset_Ioo_iff ab).1 h with h‚ÇÅ h‚ÇÇ,
  rw [lebesgue_length_Ico],
  exact ennreal.of_real_le_of_real (sub_le_sub h‚ÇÇ h‚ÇÅ)
end

lemma lebesgue_length_eq_infi_Icc (s) :
  lebesgue_length s = ‚®Öa b (h : s ‚äÜ Icc a b), of_real (b - a) :=
begin
  refine le_antisymm _
    (infi_le_infi $ Œª a, infi_le_infi $ Œª b, infi_le_infi2 $ Œª h,
      ‚ü®subset.trans h Ico_subset_Icc_self, le_refl _‚ü©),
  refine le_infi (Œª a, le_infi $ Œª b, le_infi $ Œª h, _),
  refine ennreal.le_of_forall_epsilon_le (Œª Œµ Œµ0 _, _),
  refine infi_le_of_le a (infi_le_of_le (b + Œµ) $ infi_le_of_le
    (subset.trans h $ Icc_subset_Ico_right $ (lt_add_iff_pos_right _).2 Œµ0) _),
  rw [‚Üê sub_add_eq_add_sub],
  refine le_trans ennreal.of_real_add_le (add_le_add_left _ _),
  simp only [ennreal.of_real_coe_nnreal, le_refl]
end

@[simp] lemma lebesgue_length_Icc (a b : ‚Ñù) :
  lebesgue_length (Icc a b) = of_real (b - a) :=
begin
  rw ‚Üê lebesgue_length_Ico,
  refine le_antisymm _ (lebesgue_length_mono Ico_subset_Icc_self),
  rw lebesgue_length_eq_infi_Icc (Icc a b),
  exact infi_le_of_le a (infi_le_of_le b $ infi_le_of_le (by refl) (by simp [le_refl]))
end

/-- The Lebesgue outer measure, as an outer measure of ‚Ñù. -/
def lebesgue_outer : outer_measure ‚Ñù :=
outer_measure.of_function lebesgue_length lebesgue_length_empty

lemma lebesgue_outer_le_length (s : set ‚Ñù) : lebesgue_outer s ‚â§ lebesgue_length s :=
outer_measure.of_function_le _

lemma lebesgue_length_subadditive {a b : ‚Ñù} {c d : ‚Ñï ‚Üí ‚Ñù}
  (ss : Icc a b ‚äÜ ‚ãÉi, Ioo (c i) (d i)) :
  (of_real (b - a) : ennreal) ‚â§ ‚àë' i, of_real (d i - c i) :=
begin
  suffices : ‚àÄ (s:finset ‚Ñï) b
    (cv : Icc a b ‚äÜ ‚ãÉ i ‚àà (‚Üës:set ‚Ñï), Ioo (c i) (d i)),
    (of_real (b - a) : ennreal) ‚â§ ‚àë i in s, of_real (d i - c i),
  { rcases compact_Icc.elim_finite_subcover_image (Œª (i : ‚Ñï) (_ : i ‚àà univ),
      @is_open_Ioo _ _ _ _ (c i) (d i)) (by simpa using ss) with ‚ü®s, su, hf, hs‚ü©,
    have e : (‚ãÉ i ‚àà (‚Üëhf.to_finset:set ‚Ñï),
      Ioo (c i) (d i)) = (‚ãÉ i ‚àà s, Ioo (c i) (d i)), {simp [set.ext_iff]},
    rw ennreal.tsum_eq_supr_sum,
    refine le_trans _ (le_supr _ hf.to_finset),
    exact this hf.to_finset _ (by simpa [e]) },
  clear ss b,
  refine Œª s, finset.strong_induction_on s (Œª s IH b cv, _),
  cases le_total b a with ab ab,
  { rw ennreal.of_real_eq_zero.2 (sub_nonpos.2 ab), exact zero_le _ },
  have := cv ‚ü®ab, le_refl _‚ü©, simp at this,
  rcases this with ‚ü®i, is, cb, bd‚ü©,
  rw [‚Üê finset.insert_erase is] at cv ‚ä¢,
  rw [finset.coe_insert, bUnion_insert] at cv,
  rw [finset.sum_insert (finset.not_mem_erase _ _)],
  refine le_trans _ (add_le_add_left (IH _ (finset.erase_ssubset is) (c i) _) _),
  { refine le_trans (ennreal.of_real_le_of_real _) ennreal.of_real_add_le,
    rw sub_add_sub_cancel,
    exact sub_le_sub_right (le_of_lt bd) _ },
  { rintro x ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
    refine (cv ‚ü®h‚ÇÅ, le_trans h‚ÇÇ (le_of_lt cb)‚ü©).resolve_left
      (mt and.left (not_lt_of_le h‚ÇÇ)) }
end

@[simp] lemma lebesgue_outer_Icc (a b : ‚Ñù) :
  lebesgue_outer (Icc a b) = of_real (b - a) :=
begin
  refine le_antisymm (by rw ‚Üê lebesgue_length_Icc; apply lebesgue_outer_le_length)
    (le_binfi $ Œª f hf, ennreal.le_of_forall_epsilon_le $ Œª Œµ Œµ0 h, _),
  rcases ennreal.exists_pos_sum_of_encodable
    (ennreal.zero_lt_coe_iff.2 Œµ0) ‚Ñï with ‚ü®Œµ', Œµ'0, hŒµ‚ü©,
  refine le_trans _ (add_le_add_left (le_of_lt hŒµ) _),
  rw ‚Üê ennreal.tsum_add,
  choose g hg using show
    ‚àÄ i, ‚àÉ p:‚Ñù√ó‚Ñù, f i ‚äÜ Ioo p.1 p.2 ‚àß (of_real (p.2 - p.1) : ennreal) <
      lebesgue_length (f i) + Œµ' i,
  { intro i,
    have := (ennreal.lt_add_right (lt_of_le_of_lt (ennreal.le_tsum i) h)
        (ennreal.zero_lt_coe_iff.2 (Œµ'0 i))),
    conv at this {to_lhs, rw lebesgue_length_eq_infi_Ioo},
    simpa [infi_lt_iff] },
  refine le_trans _ (ennreal.tsum_le_tsum $ Œª i, le_of_lt (hg i).2),
  exact lebesgue_length_subadditive (subset.trans hf $
    Union_subset_Union $ Œª i, (hg i).1)
end

@[simp] lemma lebesgue_outer_singleton (a : ‚Ñù) : lebesgue_outer {a} = 0 :=
by simpa using lebesgue_outer_Icc a a

@[simp] lemma lebesgue_outer_Ico (a b : ‚Ñù) :
  lebesgue_outer (Ico a b) = of_real (b - a) :=
by rw [‚Üê Icc_diff_right, lebesgue_outer.diff_null _ (lebesgue_outer_singleton _),
  lebesgue_outer_Icc]

@[simp] lemma lebesgue_outer_Ioo (a b : ‚Ñù) :
  lebesgue_outer (Ioo a b) = of_real (b - a) :=
by rw [‚Üê Ico_diff_left, lebesgue_outer.diff_null _ (lebesgue_outer_singleton _), lebesgue_outer_Ico]

@[simp] lemma lebesgue_outer_Ioc (a b : ‚Ñù) :
  lebesgue_outer (Ioc a b) = of_real (b - a) :=
by rw [‚Üê Icc_diff_left, lebesgue_outer.diff_null _ (lebesgue_outer_singleton _), lebesgue_outer_Icc]

lemma is_lebesgue_measurable_Iio {c : ‚Ñù} :
  lebesgue_outer.caratheodory.is_measurable' (Iio c) :=
outer_measure.of_function_caratheodory $ Œª t,
le_infi $ Œª a, le_infi $ Œª b, le_infi $ Œª h, begin
  refine le_trans (add_le_add
    (lebesgue_length_mono $ inter_subset_inter_left _ h)
    (lebesgue_length_mono $ diff_subset_diff_left h)) _,
  cases le_total a c with hac hca; cases le_total b c with hbc hcb;
    simp [*, -sub_eq_add_neg, sub_add_sub_cancel', le_refl],
  { simp [*, ‚Üê ennreal.of_real_add, -sub_eq_add_neg, sub_add_sub_cancel', le_refl] },
  { simp only [ennreal.of_real_eq_zero.2 (sub_nonpos.2 (le_trans hbc hca)), zero_add, le_refl] }
end

theorem lebesgue_outer_trim : lebesgue_outer.trim = lebesgue_outer :=
begin
  refine le_antisymm (Œª s, _) (outer_measure.le_trim _),
  rw outer_measure.trim_eq_infi,
  refine le_infi (Œª f, le_infi $ Œª hf,
    ennreal.le_of_forall_epsilon_le $ Œª Œµ Œµ0 h, _),
  rcases ennreal.exists_pos_sum_of_encodable
    (ennreal.zero_lt_coe_iff.2 Œµ0) ‚Ñï with ‚ü®Œµ', Œµ'0, hŒµ‚ü©,
  refine le_trans _ (add_le_add_left (le_of_lt hŒµ) _),
  rw ‚Üê ennreal.tsum_add,
  choose g hg using show
    ‚àÄ i, ‚àÉ s, f i ‚äÜ s ‚àß is_measurable s ‚àß lebesgue_outer s ‚â§ lebesgue_length (f i) + of_real (Œµ' i),
  { intro i,
    have := (ennreal.lt_add_right (lt_of_le_of_lt (ennreal.le_tsum i) h)
        (ennreal.zero_lt_coe_iff.2 (Œµ'0 i))),
    conv at this {to_lhs, rw lebesgue_length},
    simp only [infi_lt_iff] at this,
    rcases this with ‚ü®a, b, h‚ÇÅ, h‚ÇÇ‚ü©,
    rw ‚Üê lebesgue_outer_Ico at h‚ÇÇ,
    exact ‚ü®_, h‚ÇÅ, is_measurable_Ico, le_of_lt $ by simpa using h‚ÇÇ‚ü© },
  simp at hg,
  apply infi_le_of_le (Union g) _,
  apply infi_le_of_le (subset.trans hf $ Union_subset_Union (Œª i, (hg i).1)) _,
  apply infi_le_of_le (is_measurable.Union (Œª i, (hg i).2.1)) _,
  exact le_trans (lebesgue_outer.Union _) (ennreal.tsum_le_tsum $ Œª i, (hg i).2.2)
end

lemma borel_le_lebesgue_measurable : borel ‚Ñù ‚â§ lebesgue_outer.caratheodory :=
begin
  rw real.borel_eq_generate_from_Iio_rat,
  refine measurable_space.generate_from_le _,
  simp [is_lebesgue_measurable_Iio] { contextual := tt }
end

/-- Lebesgue measure on the Borel sets

The outer Lebesgue measure is the completion of this measure. (TODO: proof this)
-/
instance real.measure_space : measure_space ‚Ñù :=
‚ü®{to_outer_measure := lebesgue_outer,
  m_Union := Œª f hf, lebesgue_outer.Union_eq_of_caratheodory $
    Œª i, borel_le_lebesgue_measurable _ (hf i),
  trimmed := lebesgue_outer_trim }‚ü©

@[simp] theorem lebesgue_to_outer_measure :
  (volume : measure ‚Ñù).to_outer_measure = lebesgue_outer := rfl

end measure_theory

open measure_theory

namespace real

open_locale topological_space

theorem volume_val (s) : volume s = lebesgue_outer s := rfl

instance has_no_atoms_volume : has_no_atoms (volume : measure ‚Ñù) :=
‚ü®lebesgue_outer_singleton‚ü©

@[simp] lemma volume_Ico {a b : ‚Ñù} : volume (Ico a b) = of_real (b - a) := lebesgue_outer_Ico a b

@[simp] lemma volume_Icc {a b : ‚Ñù} : volume (Icc a b) = of_real (b - a) := lebesgue_outer_Icc a b

@[simp] lemma volume_Ioo {a b : ‚Ñù} : volume (Ioo a b) = of_real (b - a) := lebesgue_outer_Ioo a b

@[simp] lemma volume_Ioc {a b : ‚Ñù} : volume (Ioc a b) = of_real (b - a) := lebesgue_outer_Ioc a b

@[simp] lemma volume_singleton {a : ‚Ñù} : volume ({a} : set ‚Ñù) = 0 := lebesgue_outer_singleton a

@[simp] lemma volume_interval {a b : ‚Ñù} : volume (interval a b) = of_real (abs (b - a)) :=
by rw [interval, volume_Icc, max_sub_min_eq_abs]

instance locally_finite_volume : locally_finite_measure (volume : measure ‚Ñù) :=
‚ü®Œª x, ‚ü®Ioo (x - 1) (x + 1),
  mem_nhds_sets is_open_Ioo ‚ü®sub_lt_self _ zero_lt_one, lt_add_of_pos_right _ zero_lt_one‚ü©,
  by simp only [real.volume_Ioo, ennreal.of_real_lt_top]‚ü©‚ü©

lemma map_volume_add_left (a : ‚Ñù) : measure.map ((+) a) volume = volume :=
eq.symm $ real.measure_ext_Ioo_rat $ Œª p q,
  by simp [measure.map_apply (measurable_add_left a) is_measurable_Ioo, sub_sub_sub_cancel_right]

lemma map_volume_add_right (a : ‚Ñù) : measure.map (+ a) volume = volume :=
by simpa only [add_comm] using real.map_volume_add_left a

lemma smul_map_volume_mul_left {a : ‚Ñù} (h : a ‚â† 0) :
  ennreal.of_real (abs a) ‚Ä¢ measure.map ((*) a) volume = volume :=
begin
  refine (real.measure_ext_Ioo_rat $ Œª p q, _).symm,
  cases lt_or_gt_of_ne h with h h,
  { simp only [real.volume_Ioo, measure.smul_apply, ‚Üê ennreal.of_real_mul (le_of_lt $ neg_pos.2 h),
      measure.map_apply (measurable_mul_left a) is_measurable_Ioo, neg_sub_neg,
      ‚Üê neg_mul_eq_neg_mul, preimage_const_mul_Ioo_of_neg _ _ h, abs_of_neg h, mul_sub,
      mul_div_cancel' _ (ne_of_lt h)] },
  { simp only [real.volume_Ioo, measure.smul_apply, ‚Üê ennreal.of_real_mul (le_of_lt h),
      measure.map_apply (measurable_mul_left a) is_measurable_Ioo, preimage_const_mul_Ioo _ _ h,
      abs_of_pos h, mul_sub, mul_div_cancel' _ (ne_of_gt h)] }
end

lemma map_volume_mul_left {a : ‚Ñù} (h : a ‚â† 0) :
  measure.map ((*) a) volume = ennreal.of_real (abs a‚Åª¬π) ‚Ä¢ volume :=
by conv_rhs { rw [‚Üê real.smul_map_volume_mul_left h, smul_smul,
  ‚Üê ennreal.of_real_mul (abs_nonneg _), ‚Üê abs_mul, inv_mul_cancel h, abs_one, ennreal.of_real_one,
  one_smul] }

lemma smul_map_volume_mul_right {a : ‚Ñù} (h : a ‚â† 0) :
  ennreal.of_real (abs a) ‚Ä¢ measure.map (* a) volume = volume :=
by simpa only [mul_comm] using real.smul_map_volume_mul_left h

lemma map_volume_mul_right {a : ‚Ñù} (h : a ‚â† 0) :
  measure.map (* a) volume = ennreal.of_real (abs a‚Åª¬π) ‚Ä¢ volume :=
by simpa only [mul_comm] using real.map_volume_mul_left h

@[simp] lemma map_volume_neg : measure.map has_neg.neg (volume : measure ‚Ñù) = volume :=
eq.symm $ real.measure_ext_Ioo_rat $ Œª p q,
  by simp [measure.map_apply measurable_neg is_measurable_Ioo]

end real

open_locale topological_space

lemma filter.eventually.volume_pos_of_nhds_real {p : ‚Ñù ‚Üí Prop} {a : ‚Ñù} (h : ‚àÄ·∂† x in ùìù a, p x) :
  (0 : ennreal) < volume {x | p x} :=
begin
  rcases h.exists_Ioo_subset with ‚ü®l, u, hx, hs‚ü©,
  refine lt_of_lt_of_le _ (measure_mono hs),
  simpa [-mem_Ioo] using hx.1.trans hx.2
end

/-
section vitali

def vitali_aux_h (x : ‚Ñù) (h : x ‚àà Icc (0:‚Ñù) 1) :
  ‚àÉ y ‚àà Icc (0:‚Ñù) 1, ‚àÉ q:‚Ñö, ‚Üëq = x - y :=
‚ü®x, h, 0, by simp‚ü©

def vitali_aux (x : ‚Ñù) (h : x ‚àà Icc (0:‚Ñù) 1) : ‚Ñù :=
classical.some (vitali_aux_h x h)

theorem vitali_aux_mem (x : ‚Ñù) (h : x ‚àà Icc (0:‚Ñù) 1) : vitali_aux x h ‚àà Icc (0:‚Ñù) 1 :=
Exists.fst (classical.some_spec (vitali_aux_h x h):_)

theorem vitali_aux_rel (x : ‚Ñù) (h : x ‚àà Icc (0:‚Ñù) 1) :
 ‚àÉ q:‚Ñö, ‚Üëq = x - vitali_aux x h :=
Exists.snd (classical.some_spec (vitali_aux_h x h):_)

def vitali : set ‚Ñù := {x | ‚àÉ h, x = vitali_aux x h}

theorem vitali_nonmeasurable : ¬¨ is_null_measurable measure_space.Œº vitali :=
sorry

end vitali
-/
