/-
Copyright (c) 2020 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: R√©my Degenne.
-/
import measure_theory.ess_sup
import measure_theory.l1_space
import analysis.mean_inequalities

/-!
# ‚Ñíp space

This file describes properties of almost everywhere measurable functions with finite seminorm
`(‚à´ ‚à•f a‚à•^p ‚àÇŒº) ^ (1/p)` for `p:ennreal`.

## Main definitions

* `snorm' f p Œº` : `(‚à´ ‚à•f a‚à•^p ‚àÇŒº) ^ (1/p)` for `f : Œ± ‚Üí F` and `p : ‚Ñù`, where `Œ±` is a  measurable
  space and `F` is a normed group.
* `snorm_inf f Œº` : seminorm in `‚Ñí‚àû`, equal to the essential supremum `ess_sup ‚à•f‚à• Œº`.
* `snorm f p Œº` : for `p : ennreal`, seminorm in `‚Ñíp`, equal to `0` for `p=0`, to `snorm' f p Œº`
  for `0 < p < ‚àû` and to `snorm_inf f Œº` for `p = ‚àû`.

* `mem_‚Ñíp f p Œº` : property that the function `f` is almost everywhere measurable and has finite
  p-seminorm for measure `Œº` (`snorm f p Œº < ‚àû`)

-/

open measure_theory

noncomputable theory

namespace ‚Ñíp_space

variables {Œ± E F : Type*} [measurable_space Œ±] {Œº : measure Œ±}
  [measurable_space E] [normed_group E]
  [normed_group F]
  {p : ‚Ñù} {q : ennreal}

section ‚Ñíp_space_definition

/-- `(‚à´ ‚à•f a‚à•^p ‚àÇŒº) ^ (1/p)`, which is a seminorm on the space of measurable functions for which
this quantity is finite -/
def snorm' (f : Œ± ‚Üí F) (p : ‚Ñù) (Œº : measure Œ±) : ennreal := (‚à´‚Åª a, (nnnorm (f a))^p ‚àÇŒº) ^ (1/p)

/-- seminorm for `‚Ñí‚àû`, equal to the essential supremum of `‚à•f‚à•`. -/
def snorm_inf (f : Œ± ‚Üí F) (Œº : measure Œ±) := ess_sup (Œª x, (nnnorm (f x) : ennreal)) Œº

/-- `‚Ñíp` seminorm, equal to `0` for `p=0`, to `(‚à´ ‚à•f a‚à•^p ‚àÇŒº) ^ (1/p)` for `0 < p < ‚àû` and to
`ess_sup ‚à•f‚à• Œº` for `p = ‚àû`. -/
def snorm (f : Œ± ‚Üí F) (q : ennreal) (Œº : measure Œ±) : ennreal :=
if q = 0 then 0 else (if q = ‚ä§ then snorm_inf f Œº else snorm' f (ennreal.to_real q) Œº)

lemma snorm_eq_snorm' (hq_ne_zero : q ‚â† 0) (hq_ne_top : q ‚â† ‚ä§) {f : Œ± ‚Üí F} :
  snorm f q Œº = snorm' f (ennreal.to_real q) Œº :=
by simp [snorm, hq_ne_zero, hq_ne_top]

@[simp] lemma snorm_exponent_top {f : Œ± ‚Üí F} : snorm f ‚ä§ Œº = snorm_inf f Œº :=
by simp [snorm]

/-- The property that `f:Œ±‚ÜíE` is ae_measurable and `(‚à´ ‚à•f a‚à•^p ‚àÇŒº)^(1/p)` is finite -/
def mem_‚Ñíp (f : Œ± ‚Üí E) (p : ennreal) (Œº : measure Œ±) : Prop :=
ae_measurable f Œº ‚àß snorm f p Œº < ‚ä§

lemma lintegral_rpow_nnnorm_eq_rpow_snorm' {f : Œ± ‚Üí F} (hp0_lt : 0 < p) :
  ‚à´‚Åª a, (nnnorm (f a)) ^ p ‚àÇŒº = (snorm' f p Œº) ^ p :=
begin
  rw [snorm', ‚Üêennreal.rpow_mul, one_div, inv_mul_cancel, ennreal.rpow_one],
  exact (ne_of_lt hp0_lt).symm,
end

end ‚Ñíp_space_definition

lemma mem_‚Ñíp_one_iff_integrable {f : Œ± ‚Üí E} : mem_‚Ñíp f 1 Œº ‚Üî integrable f Œº :=
by simp_rw [integrable, has_finite_integral, mem_‚Ñíp,
    snorm_eq_snorm' one_ne_zero ennreal.one_ne_top, ennreal.one_to_real, snorm', one_div_one,
    ennreal.rpow_one]

section top

lemma mem_‚Ñíp.snorm_lt_top {f : Œ± ‚Üí E} (hfp : mem_‚Ñíp f q Œº) : snorm f q Œº < ‚ä§ := hfp.2

lemma mem_‚Ñíp.snorm_ne_top {f : Œ± ‚Üí E} (hfp : mem_‚Ñíp f q Œº) : snorm f q Œº ‚â† ‚ä§ := ne_of_lt (hfp.2)

lemma lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top {f : Œ± ‚Üí F} (hp0_lt : 0 < p)
  (hfp : snorm' f p Œº < ‚ä§) :
  ‚à´‚Åª a, (nnnorm (f a)) ^ p ‚àÇŒº < ‚ä§ :=
begin
  rw lintegral_rpow_nnnorm_eq_rpow_snorm' hp0_lt,
  exact ennreal.rpow_lt_top_of_nonneg (le_of_lt hp0_lt) (ne_of_lt hfp),
end

end top

section zero

@[simp] lemma snorm'_exponent_zero {f : Œ± ‚Üí F} : snorm' f 0 Œº = 1 :=
by rw [snorm', div_zero, ennreal.rpow_zero]

@[simp] lemma snorm_exponent_zero {f : Œ± ‚Üí F} : snorm f 0 Œº = 0 :=
by simp [snorm]

lemma mem_‚Ñíp_zero_iff_ae_measurable {f : Œ± ‚Üí E} : mem_‚Ñíp f 0 Œº ‚Üî ae_measurable f Œº :=
by simp [mem_‚Ñíp, snorm_exponent_zero]

@[simp] lemma snorm'_zero (hp0_lt : 0 < p) : snorm' (0 : Œ± ‚Üí F) p Œº = 0 :=
by simp [snorm', hp0_lt]

@[simp] lemma snorm'_zero' (hp0_ne : p ‚â† 0) (hŒº : Œº ‚â† 0) : snorm' (0 : Œ± ‚Üí F) p Œº = 0 :=
begin
  cases le_or_lt 0 p with hp0 hp_neg,
  { exact snorm'_zero (lt_of_le_of_ne hp0 hp0_ne.symm), },
  { simp [snorm', ennreal.rpow_eq_zero_iff, hŒº, hp_neg], },
end

@[simp] lemma snorm_inf_zero : snorm_inf (0 : Œ± ‚Üí F) Œº = 0 :=
begin
  simp_rw [snorm_inf, pi.zero_apply, nnnorm_zero, ennreal.coe_zero, ‚Üêennreal.bot_eq_zero],
  exact ess_sup_const_bot,
end

@[simp] lemma snorm_zero : snorm (0 : Œ± ‚Üí F) q Œº = 0 :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = ‚ä§,
  { simp only [h_top, snorm_exponent_top, snorm_inf_zero], },
  rw ‚Üêne.def at h0,
  simp [snorm_eq_snorm' h0 h_top,
    ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) h0.symm, h_top‚ü©],
end

lemma zero_mem_‚Ñíp : mem_‚Ñíp (0 : Œ± ‚Üí E) q Œº :=
begin
  refine ‚ü®measurable_zero.ae_measurable, _‚ü©,
  rw snorm_zero,
  exact ennreal.coe_lt_top,
end

lemma snorm'_measure_zero_of_pos {f : Œ± ‚Üí F} (hp_pos : 0 < p) : snorm' f p 0 = 0 :=
by simp [snorm', hp_pos]

lemma snorm'_measure_zero_of_exponent_zero {f : Œ± ‚Üí F} : snorm' f 0 0 = 1 := by simp [snorm']

lemma snorm'_measure_zero_of_neg {f : Œ± ‚Üí F} (hp_neg : p < 0) : snorm' f p 0 = ‚ä§ :=
by simp [snorm', hp_neg]

@[simp] lemma snorm_inf_measure_zero {f : Œ± ‚Üí F} : snorm_inf f 0 = 0 :=
by simp [snorm_inf]

@[simp] lemma snorm_measure_zero {f : Œ± ‚Üí F} : snorm f q 0 = 0 :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = ‚ä§,
  { simp [h_top], },
  rw ‚Üêne.def at h0,
  simp [snorm_eq_snorm' h0 h_top, snorm',
    ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) h0.symm, h_top‚ü©],
end

end zero

section const

lemma snorm'_const (c : F) (hp_pos : 0 < p) :
  snorm' (Œª x : Œ± , c) p Œº = (nnnorm c : ennreal) * (Œº set.univ) ^ (1/p) :=
begin
  rw [snorm', lintegral_const, @ennreal.mul_rpow_of_nonneg _ _ (1/p) (by simp [le_of_lt hp_pos])],
  congr,
  rw ‚Üêennreal.rpow_mul,
  suffices hp_cancel : p * (1/p) = 1, by rw [hp_cancel, ennreal.rpow_one],
  rw [one_div, mul_inv_cancel (ne_of_lt hp_pos).symm],
end

lemma snorm'_const' [finite_measure Œº] (c : F) (hc_ne_zero : c ‚â† 0) (hp_ne_zero : p ‚â† 0) :
  snorm' (Œª x : Œ± , c) p Œº = (nnnorm c : ennreal) * (Œº set.univ) ^ (1/p) :=
begin
  rw [snorm', lintegral_const, ennreal.mul_rpow_of_ne_top _ (measure_ne_top Œº set.univ)],
  { congr,
    rw ‚Üêennreal.rpow_mul,
    suffices hp_cancel : p * (1/p) = 1, by rw [hp_cancel, ennreal.rpow_one],
    rw [one_div, mul_inv_cancel hp_ne_zero], },
  { rw [ne.def, ennreal.rpow_eq_top_iff, auto.not_or_eq, auto.not_and_eq, auto.not_and_eq],
    split,
    { left,
      rwa [ennreal.coe_eq_zero, nnnorm_eq_zero], },
    { exact or.inl ennreal.coe_ne_top, }, },
end

lemma snorm_inf_const (c : F) (hŒº : Œº ‚â† 0) : snorm_inf (Œª x : Œ±, c) Œº = (nnnorm c : ennreal) :=
by rw [snorm_inf, ess_sup_const _ hŒº]

lemma snorm'_const_of_probability_measure (c : F) (hp_pos : 0 < p) [probability_measure Œº] :
  snorm' (Œª x : Œ± , c) p Œº = (nnnorm c : ennreal) :=
by simp [snorm'_const c hp_pos, measure_univ]

lemma snorm_const (c : F) (h0 : q ‚â† 0) (hŒº : Œº ‚â† 0) :
  snorm (Œª x : Œ± , c) q Œº = (nnnorm c : ennreal) * (Œº set.univ) ^ (1/(ennreal.to_real q)) :=
begin
  by_cases h_top : q = ‚ä§,
  { simp [h_top, snorm_inf_const c hŒº], },
  simp [snorm_eq_snorm' h0 h_top, snorm'_const,
    ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) h0.symm, h_top‚ü©],
end

lemma snorm_const' (c : F) (h0 : q ‚â† 0) (h_top: q ‚â† ‚ä§) :
  snorm (Œª x : Œ± , c) q Œº = (nnnorm c : ennreal) * (Œº set.univ) ^ (1/(ennreal.to_real q)) :=
begin
  simp [snorm_eq_snorm' h0 h_top, snorm'_const,
    ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) h0.symm, h_top‚ü©],
end

lemma mem_‚Ñíp_const (c : E) [finite_measure Œº] : mem_‚Ñíp (Œª a:Œ±, c) q Œº :=
begin
  refine ‚ü®measurable_const.ae_measurable, _‚ü©,
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases hŒº : Œº = 0,
  { simp [hŒº], },
  rw snorm_const c h0 hŒº,
  refine ennreal.mul_lt_top ennreal.coe_lt_top _,
  refine ennreal.rpow_lt_top_of_nonneg _ (measure_ne_top Œº set.univ),
  simp,
end

end const

lemma snorm'_congr_ae {f g : Œ± ‚Üí F} (hfg : f =·µê[Œº] g) :
  snorm' f p Œº = snorm' g p Œº :=
begin
  suffices h_no_pow : ‚à´‚Åª a, (nnnorm (f a)) ^ p ‚àÇŒº = ‚à´‚Åª a, (nnnorm (g a)) ^ p ‚àÇŒº,
  { simp_rw [snorm', h_no_pow], },
  exact lintegral_congr_ae (hfg.mono (Œª x hx, by simp [*])),
end

lemma snorm_inf_congr_ae {f g : Œ± ‚Üí F} (hfg : f =·µê[Œº] g) :
  snorm_inf f Œº = snorm_inf g Œº :=
ess_sup_congr_ae (hfg.mono (Œª x hx, by rw hx))

lemma snorm_congr_ae {f g : Œ± ‚Üí F} (hfg : f =·µê[Œº] g) :
  snorm f q Œº = snorm g q Œº :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = ‚ä§,
  { rw [h_top, snorm_exponent_top],
    exact snorm_inf_congr_ae hfg, },
  repeat { rw snorm_eq_snorm' h0 h_top, },
  exact snorm'_congr_ae hfg,
end

lemma mem_‚Ñíp.ae_eq {f g : Œ± ‚Üí E} (hfg : f =·µê[Œº] g) (hf_Lp : mem_‚Ñíp f q Œº) :
  mem_‚Ñíp g q Œº :=
begin
  split,
  { cases hf_Lp.1 with f' hf',
    exact ‚ü®f', ‚ü®hf'.1, ae_eq_trans hfg.symm hf'.2‚ü©‚ü©, },
  { rw snorm_congr_ae hfg.symm,
    exact hf_Lp.2, },
end

lemma mem_‚Ñíp_congr_ae {f g : Œ± ‚Üí E} (hfg : f =·µê[Œº] g) : mem_‚Ñíp f q Œº ‚Üî mem_‚Ñíp g q Œº :=
‚ü®Œª h, h.ae_eq hfg, Œª h, h.ae_eq hfg.symm‚ü©

section opens_measurable_space
variable [opens_measurable_space E]

lemma snorm'_eq_zero_of_ae_zero {f : Œ± ‚Üí F} (hp0_lt : 0 < p) (hf_zero : f =·µê[Œº] 0) :
  snorm' f p Œº = 0 :=
by rw [snorm'_congr_ae hf_zero, snorm'_zero hp0_lt]

lemma snorm'_eq_zero_of_ae_zero' (hp0_ne : p ‚â† 0) (hŒº : Œº ‚â† 0) {f : Œ± ‚Üí F} (hf_zero : f =·µê[Œº] 0) :
  snorm' f p Œº = 0 :=
by rw [snorm'_congr_ae hf_zero, snorm'_zero' hp0_ne hŒº]

lemma ae_eq_zero_of_snorm'_eq_zero {f : Œ± ‚Üí E} (hp0 : 0 ‚â§ p) (hf : ae_measurable f Œº)
  (h : snorm' f p Œº = 0) :
  f =·µê[Œº] 0 :=
begin
  rw [snorm', ennreal.rpow_eq_zero_iff] at h,
  cases h,
  { rw lintegral_eq_zero_iff' hf.nnnorm.ennreal_coe.ennreal_rpow_const at h,
    refine h.left.mono (Œª x hx, _),
    rw [pi.zero_apply, ennreal.rpow_eq_zero_iff] at hx,
    cases hx,
    { cases hx with hx _,
      rwa [‚Üêennreal.coe_zero, ennreal.coe_eq_coe, nnnorm_eq_zero] at hx, },
    { exact absurd hx.left ennreal.coe_ne_top, }, },
  { exfalso,
    rw [one_div, inv_lt_zero] at h,
    linarith, },
end

lemma snorm'_eq_zero_iff (hp0_lt : 0 < p) {f : Œ± ‚Üí E} (hf : ae_measurable f Œº) :
  snorm' f p Œº = 0 ‚Üî f =·µê[Œº] 0 :=
‚ü®ae_eq_zero_of_snorm'_eq_zero (le_of_lt hp0_lt) hf, snorm'_eq_zero_of_ae_zero hp0_lt‚ü©

lemma coe_nnnorm_ae_le_snorm_inf (f : Œ± ‚Üí F) (Œº : measure Œ±) :
  ‚àÄ·µê x ‚àÇŒº, (nnnorm (f x) : ennreal) ‚â§ snorm_inf f Œº :=
ennreal.ae_le_ess_sup (Œª x, (nnnorm (f x) : ennreal))

lemma snorm_inf_eq_zero_iff {f : Œ± ‚Üí F} : snorm_inf f Œº = 0 ‚Üî f =·µê[Œº] 0 :=
begin
  rw [snorm_inf, ennreal.ess_sup_eq_zero_iff],
  split; intro h;
    { refine h.mono (Œª x hx, _),
      simp_rw pi.zero_apply at hx ‚ä¢,
      simpa using hx, },
end

lemma snorm_eq_zero_iff {f : Œ± ‚Üí E} (hf : ae_measurable f Œº) (h0 : q ‚â† 0) :
  snorm f q Œº = 0 ‚Üî f =·µê[Œº] 0 :=
begin
  by_cases h_top : q = ‚ä§,
  { rw [h_top, snorm_exponent_top, snorm_inf_eq_zero_iff], },
  rw snorm_eq_snorm' h0 h_top,
  exact snorm'_eq_zero_iff
    (ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) h0.symm, h_top‚ü©) hf,
end

end opens_measurable_space

@[simp] lemma snorm'_neg {f : Œ± ‚Üí F} : snorm' (-f) p Œº = snorm' f p Œº := by simp [snorm']

@[simp] lemma snorm_neg {f : Œ± ‚Üí F} : snorm (-f) q Œº = snorm f q Œº :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = ‚ä§,
  { simp [h_top, snorm_inf], },
  simp [snorm_eq_snorm' h0 h_top],
end

section borel_space
variable [borel_space E]

lemma mem_‚Ñíp.neg {f : Œ± ‚Üí E} (hf : mem_‚Ñíp f q Œº) : mem_‚Ñíp (-f) q Œº :=
‚ü®ae_measurable.neg hf.1, by simp [hf.right]‚ü©

lemma snorm'_le_snorm'_mul_rpow_measure_univ {p q : ‚Ñù} (hp0_lt : 0 < p) (hpq : p ‚â§ q)
  {f : Œ± ‚Üí E} (hf : ae_measurable f Œº) :
  snorm' f p Œº ‚â§ snorm' f q Œº * (Œº set.univ) ^ (1/p - 1/q) :=
begin
  have hq0_lt : 0 < q, from lt_of_lt_of_le hp0_lt hpq,
  by_cases hpq_eq : p = q,
  { rw [hpq_eq, sub_self, ennreal.rpow_zero, mul_one],
    exact le_refl _, },
  have hpq : p < q, from lt_of_le_of_ne hpq hpq_eq,
  let g := Œª a : Œ±, (1 : ennreal),
  have h_rw : ‚à´‚Åª a, ‚Üë(nnnorm (f a))^p ‚àÇ Œº = ‚à´‚Åª a, (nnnorm (f a) * (g a))^p ‚àÇ Œº,
  from lintegral_congr (Œª a, by simp),
  repeat {rw snorm'},
  rw h_rw,
  let r := p * q / (q - p),
  have hpqr : 1/p = 1/q + 1/r,
  { field_simp [(ne_of_lt hp0_lt).symm,
      (ne_of_lt hq0_lt).symm],
    ring, },
  calc (‚à´‚Åª (a : Œ±), (‚Üë(nnnorm (f a)) * g a) ^ p ‚àÇŒº) ^ (1/p)
      ‚â§ (‚à´‚Åª (a : Œ±), ‚Üë(nnnorm (f a)) ^ q ‚àÇŒº) ^ (1/q) * (‚à´‚Åª (a : Œ±), (g a) ^ r ‚àÇŒº) ^ (1/r) :
    ennreal.lintegral_Lp_mul_le_Lq_mul_Lr hp0_lt hpq hpqr Œº hf.nnnorm.ennreal_coe
      ae_measurable_const
  ... = (‚à´‚Åª (a : Œ±), ‚Üë(nnnorm (f a)) ^ q ‚àÇŒº) ^ (1/q) * Œº set.univ ^ (1/p - 1/q) :
    by simp [hpqr],
end

lemma snorm'_le_snorm_inf_mul_rpow_measure_univ (hp_pos : 0 < p) {f : Œ± ‚Üí F} :
  snorm' f p Œº ‚â§ snorm_inf f Œº * (Œº set.univ) ^ (1/p) :=
begin
  have h_le : ‚à´‚Åª (a : Œ±), ‚Üë(nnnorm (f a)) ^ p ‚àÇŒº ‚â§ ‚à´‚Åª (a : Œ±), (snorm_inf f Œº) ^ p ‚àÇŒº,
  { refine lintegral_mono_ae _,
    have h_nnnorm_le_snorm_inf := coe_nnnorm_ae_le_snorm_inf f Œº,
    refine h_nnnorm_le_snorm_inf.mono (Œª x hx, ennreal.rpow_le_rpow hx (le_of_lt hp_pos)), },
  rw [snorm', ‚Üêennreal.rpow_one (snorm_inf f Œº)],
  nth_rewrite 1 ‚Üêmul_inv_cancel (ne_of_lt hp_pos).symm,
  rw [ennreal.rpow_mul, one_div,
    ‚Üê@ennreal.mul_rpow_of_nonneg _ _ p‚Åª¬π (by simp [le_of_lt hp_pos])],
  refine ennreal.rpow_le_rpow _ (by simp [le_of_lt hp_pos]),
  rwa lintegral_const at h_le,
end

lemma snorm'_le_snorm'_of_exponent_le {p q : ‚Ñù} (hp0_lt : 0 < p) (hpq : p ‚â§ q) (Œº : measure Œ±)
  [probability_measure Œº] {f : Œ± ‚Üí E} (hf : ae_measurable f Œº) :
  snorm' f p Œº ‚â§ snorm' f q Œº :=
begin
  have h_le_Œº := snorm'_le_snorm'_mul_rpow_measure_univ hp0_lt hpq hf,
  rwa [measure_univ, ennreal.one_rpow, mul_one] at h_le_Œº,
end

lemma snorm'_le_snorm_inf (hp_pos : 0 < p) {f : Œ± ‚Üí F} [probability_measure Œº] :
  snorm' f p Œº ‚â§ snorm_inf f Œº :=
begin
  refine le_trans (snorm'_le_snorm_inf_mul_rpow_measure_univ hp_pos) (le_of_eq _),
  simp [measure_univ],
end

lemma snorm_le_snorm_of_exponent_le {p q : ennreal} (hpq : p ‚â§ q) [probability_measure Œº]
  {f : Œ± ‚Üí E} (hf : ae_measurable f Œº) :
  snorm f p Œº ‚â§ snorm f q Œº :=
begin
  by_cases hp0 : p = 0,
  { simp [hp0], },
  rw ‚Üêne.def at hp0,
  by_cases hq_top : q = ‚ä§,
  { by_cases hp_top : p = ‚ä§,
    { rw [hq_top, hp_top],
      exact le_refl _, },
    { have hp_pos : 0 < p.to_real,
      from ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) hp0.symm, hp_top‚ü©,
      rw [snorm_eq_snorm' hp0 hp_top, hq_top, snorm_exponent_top],
      refine le_trans (snorm'_le_snorm_inf_mul_rpow_measure_univ hp_pos) (le_of_eq _),
      simp [measure_univ], }, },
  { have hp_top : p ‚â† ‚ä§,
    { by_contra hp_eq_top,
      push_neg at hp_eq_top,
      refine hq_top _,
      rwa [hp_eq_top, top_le_iff] at hpq, },
    have hp_pos : 0 < p.to_real,
    from ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) hp0.symm, hp_top‚ü©,
    have hq0 : q ‚â† 0,
    { by_contra hq_eq_zero,
      push_neg at hq_eq_zero,
      have hp_eq_zero : p = 0, from le_antisymm (by rwa hq_eq_zero at hpq) (zero_le _),
      rw [hp_eq_zero, ennreal.zero_to_real] at hp_pos,
      exact (lt_irrefl _) hp_pos, },
    have hpq_real : p.to_real ‚â§ q.to_real, by rwa ennreal.to_real_le_to_real hp_top hq_top,
    rw [snorm_eq_snorm' hp0 hp_top, snorm_eq_snorm' hq0 hq_top],
    exact snorm'_le_snorm'_of_exponent_le hp_pos hpq_real _ hf, },
end

lemma snorm'_lt_top_of_snorm'_lt_top_of_exponent_le {p q : ‚Ñù} [finite_measure Œº] {f : Œ± ‚Üí E}
  (hf : ae_measurable f Œº) (hfq_lt_top : snorm' f q Œº < ‚ä§) (hp_nonneg : 0 ‚â§ p) (hpq : p ‚â§ q) :
  snorm' f p Œº < ‚ä§ :=
begin
  cases le_or_lt p 0 with hp_nonpos hp_pos,
  { rw le_antisymm hp_nonpos hp_nonneg,
    simp, },
  have hq_pos : 0 < q, from lt_of_lt_of_le hp_pos hpq,
  calc snorm' f p Œº
      ‚â§ snorm' f q Œº * (Œº set.univ) ^ (1/p - 1/q) :
    snorm'_le_snorm'_mul_rpow_measure_univ hp_pos hpq hf
  ... < ‚ä§ :
  begin
    rw ennreal.mul_lt_top_iff,
    refine or.inl ‚ü®hfq_lt_top, ennreal.rpow_lt_top_of_nonneg _ (measure_ne_top Œº set.univ)‚ü©,
    rwa [le_sub, sub_zero, one_div, one_div, inv_le_inv hq_pos hp_pos],
  end
end

lemma mem_‚Ñíp.mem_‚Ñíp_of_exponent_le {p q : ennreal} [finite_measure Œº] {f : Œ± ‚Üí E}
  (hfq : mem_‚Ñíp f q Œº) (hpq : p ‚â§ q) :
  mem_‚Ñíp f p Œº :=
begin
  cases hfq with hfq_m hfq_lt_top,
  by_cases hp0 : p = 0,
  { rwa [hp0, mem_‚Ñíp_zero_iff_ae_measurable], },
  rw ‚Üêne.def at hp0,
  refine ‚ü®hfq_m, _‚ü©,
  by_cases hp_top : p = ‚ä§,
  { have hq_top : q = ‚ä§,
      by rwa [hp_top, top_le_iff] at hpq,
    rw [hp_top],
    rwa hq_top at hfq_lt_top, },
  have hp_pos : 0 < p.to_real,
    from ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) hp0.symm, hp_top‚ü©,
  by_cases hq_top : q = ‚ä§,
  { rw snorm_eq_snorm' hp0 hp_top,
    rw [hq_top, snorm_exponent_top] at hfq_lt_top,
    refine lt_of_le_of_lt (snorm'_le_snorm_inf_mul_rpow_measure_univ hp_pos) _,
    refine ennreal.mul_lt_top hfq_lt_top _,
    exact ennreal.rpow_lt_top_of_nonneg (by simp [le_of_lt hp_pos]) (measure_ne_top Œº set.univ), },
  have hq0 : q ‚â† 0,
  { by_contra hq_eq_zero,
    push_neg at hq_eq_zero,
    have hp_eq_zero : p = 0, from le_antisymm (by rwa hq_eq_zero at hpq) (zero_le _),
    rw [hp_eq_zero, ennreal.zero_to_real] at hp_pos,
    exact (lt_irrefl _) hp_pos, },
  have hpq_real : p.to_real ‚â§ q.to_real, by rwa ennreal.to_real_le_to_real hp_top hq_top,
  rw snorm_eq_snorm' hp0 hp_top,
  rw snorm_eq_snorm' hq0 hq_top at hfq_lt_top,
  exact snorm'_lt_top_of_snorm'_lt_top_of_exponent_le hfq_m hfq_lt_top (le_of_lt hp_pos) hpq_real,
end

lemma mem_‚Ñíp.integrable (hq1 : 1 ‚â§ q) {f : Œ± ‚Üí E} [finite_measure Œº] (hfq : mem_‚Ñíp f q Œº) :
  integrable f Œº :=
mem_‚Ñíp_one_iff_integrable.mp (hfq.mem_‚Ñíp_of_exponent_le hq1)

lemma snorm'_add_le {f g : Œ± ‚Üí E} (hf : ae_measurable f Œº) (hg : ae_measurable g Œº) (hp1 : 1 ‚â§ p) :
  snorm' (f + g) p Œº ‚â§ snorm' f p Œº + snorm' g p Œº :=
calc (‚à´‚Åª a, ‚Üë(nnnorm ((f + g) a)) ^ p ‚àÇŒº) ^ (1 / p)
    ‚â§ (‚à´‚Åª a, (((Œª a, (nnnorm (f a) : ennreal))
        + (Œª a, (nnnorm (g a) : ennreal))) a) ^ p ‚àÇŒº) ^ (1 / p) :
begin
  refine @ennreal.rpow_le_rpow _ _ (1/p) _ (by simp [le_trans zero_le_one hp1]),
  refine lintegral_mono (Œª a, ennreal.rpow_le_rpow _ (le_trans zero_le_one hp1)),
  simp [‚Üêennreal.coe_add, nnnorm_add_le],
end
... ‚â§ snorm' f p Œº + snorm' g p Œº :
  ennreal.lintegral_Lp_add_le hf.nnnorm.ennreal_coe hg.nnnorm.ennreal_coe hp1

lemma snorm_inf_add_le {f g : Œ± ‚Üí F} :
  snorm_inf (f + g) Œº ‚â§ snorm_inf f Œº + snorm_inf g Œº :=
begin
  refine le_trans (ess_sup_mono_ae (filter.eventually_of_forall (Œª x, _)))
    (ennreal.ess_sup_add_le _ _),
  simp_rw [pi.add_apply, ‚Üêennreal.coe_add, ennreal.coe_le_coe],
  exact nnnorm_add_le _ _,
end

lemma snorm_add_le {f g : Œ± ‚Üí E} (hf : ae_measurable f Œº) (hg : ae_measurable g Œº) (hq1 : 1 ‚â§ q) :
  snorm (f + g) q Œº ‚â§ snorm f q Œº + snorm g q Œº :=
begin
  by_cases hq0 : q = 0,
  { simp [hq0], },
  by_cases hq_top : q = ‚ä§,
  { simp [hq_top, snorm_inf_add_le], },
  have hq1_real : 1 ‚â§ q.to_real,
  by rwa [‚Üêennreal.one_to_real, ennreal.to_real_le_to_real ennreal.one_ne_top hq_top],
  repeat { rw snorm_eq_snorm' hq0 hq_top, },
  exact snorm'_add_le hf hg hq1_real,
end

section second_countable_topology
variable [topological_space.second_countable_topology E]

lemma mem_‚Ñíp.add {f g : Œ± ‚Üí E} (hf : mem_‚Ñíp f q Œº) (hg : mem_‚Ñíp g q Œº) (hq1 : 1 ‚â§ q) :
  mem_‚Ñíp (f + g) q Œº :=
begin
  refine ‚ü®ae_measurable.add hf.1 hg.1, lt_of_le_of_lt (snorm_add_le hf.1 hg.1 hq1) _‚ü©,
  rw ennreal.add_lt_top,
  exact ‚ü®hf.2, hg.2‚ü©,
end

lemma mem_‚Ñíp.sub {f g : Œ± ‚Üí E} (hf : mem_‚Ñíp f q Œº) (hg : mem_‚Ñíp g q Œº) (hq1 : 1 ‚â§ q) :
  mem_‚Ñíp (f-g) q Œº :=
by { rw sub_eq_add_neg, exact hf.add hg.neg hq1 }

end second_countable_topology

end borel_space

section normed_space

variables {ùïú : Type*} [normed_field ùïú] [normed_space ùïú E] [normed_space ùïú F]

lemma snorm'_const_smul {f : Œ± ‚Üí F} (c : ùïú) (hp0_lt : 0 < p) :
  snorm' (c ‚Ä¢ f) p Œº = (nnnorm c : ennreal) * snorm' f p Œº :=
begin
  rw snorm',
  simp_rw [pi.smul_apply, nnnorm_smul, ennreal.coe_mul,
    ennreal.mul_rpow_of_nonneg _ _ (le_of_lt hp0_lt)],
  suffices h_integral : ‚à´‚Åª a, ‚Üë(nnnorm c) ^ p * ‚Üë(nnnorm (f a)) ^ p ‚àÇŒº
    = (nnnorm c : ennreal)^p * ‚à´‚Åª a, (nnnorm (f a)) ^ p ‚àÇŒº,
  { apply_fun (Œª x, x ^ (1/p)) at h_integral,
    rw [h_integral, @ennreal.mul_rpow_of_nonneg _ _ (1/p) (by simp [le_of_lt hp0_lt])],
    congr,
    simp_rw [‚Üêennreal.rpow_mul, one_div, mul_inv_cancel (ne_of_lt hp0_lt).symm,
      ennreal.rpow_one], },
  rw lintegral_const_mul',
  rw ennreal.coe_rpow_of_nonneg _ (le_of_lt hp0_lt),
  exact ennreal.coe_ne_top,
end

lemma snorm_inf_const_smul {f : Œ± ‚Üí F} (c : ùïú) :
  snorm_inf (c ‚Ä¢ f) Œº = (nnnorm c : ennreal) * snorm_inf f Œº :=
by simp_rw [snorm_inf,  pi.smul_apply, nnnorm_smul, ennreal.coe_mul, ennreal.ess_sup_const_mul]

lemma snorm_const_smul {f : Œ± ‚Üí F} (c : ùïú) :
  snorm (c ‚Ä¢ f) q Œº = (nnnorm c : ennreal) * snorm f q Œº :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = ‚ä§,
  { simp [h_top, snorm_inf_const_smul], },
  repeat { rw snorm_eq_snorm' h0 h_top, },
  rw ‚Üêne.def at h0,
  exact snorm'_const_smul c
    (ennreal.to_real_pos_iff.mpr ‚ü®lt_of_le_of_ne (zero_le _) h0.symm, h_top‚ü©),
end

lemma mem_‚Ñíp.const_smul [borel_space E] {f : Œ± ‚Üí E} (hf : mem_‚Ñíp f q Œº) (c : ùïú) :
  mem_‚Ñíp (c ‚Ä¢ f) q Œº :=
‚ü®ae_measurable.const_smul hf.1 c,
  lt_of_le_of_lt (le_of_eq (snorm_const_smul c)) (ennreal.mul_lt_top ennreal.coe_lt_top hf.2)‚ü©

lemma snorm'_smul_le_mul_snorm' [opens_measurable_space E] [measurable_space ùïú]
  [opens_measurable_space ùïú] {q r : ‚Ñù}
  {f : Œ± ‚Üí E} (hf : ae_measurable f Œº) {œÜ : Œ± ‚Üí ùïú} (hœÜ : ae_measurable œÜ Œº)
  (hp0_lt : 0 < p) (hpq : p < q) (hpqr : 1/p = 1/q + 1/r) :
  snorm' (œÜ ‚Ä¢ f) p Œº ‚â§ snorm' œÜ q Œº * snorm' f r Œº :=
begin
  simp_rw [snorm', pi.smul_apply', nnnorm_smul, ennreal.coe_mul],
  exact ennreal.lintegral_Lp_mul_le_Lq_mul_Lr hp0_lt hpq hpqr Œº hœÜ.nnnorm.ennreal_coe
    hf.nnnorm.ennreal_coe,
end

end normed_space

end ‚Ñíp_space
