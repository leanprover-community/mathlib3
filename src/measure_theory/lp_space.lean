/-
Copyright (c) 2020 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: RÃ©my Degenne.
-/
import measure_theory.ess_sup
import measure_theory.l1_space
import analysis.mean_inequalities

/-!
# â„’p space and Lp space

This file describes properties of almost everywhere measurable functions with finite seminorm,
denoted by `snorm f p Î¼` and defined for `p:ennreal` as `0` if `p=0`, `(âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼) ^ (1/p)` for
`0 < p < âˆž` and `ess_sup âˆ¥fâˆ¥ Î¼` for `p=âˆž`.

The Prop-valued `mem_â„’p f p Î¼` states that a function `f : Î± â†’ E` has finite seminorm.
The space `Lp Î± E p Î¼` is the subtype of elements of `Î± â†’â‚˜[Î¼] E` (see ae_eq_fun) such that
`snorm f p Î¼` is finite. For `1 â‰¤ p`, `snorm` defines a norm and Lp is a metric space.

TODO: prove that Lp is complete.

## Main definitions

* `snorm' f p Î¼` : `(âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼) ^ (1/p)` for `f : Î± â†’ F` and `p : â„`, where `Î±` is a  measurable
  space and `F` is a normed group.
* `snorm_ess_sup f Î¼` : seminorm in `â„’âˆž`, equal to the essential supremum `ess_sup âˆ¥fâˆ¥ Î¼`.
* `snorm f p Î¼` : for `p : ennreal`, seminorm in `â„’p`, equal to `0` for `p=0`, to `snorm' f p Î¼`
  for `0 < p < âˆž` and to `snorm_ess_sup f Î¼` for `p = âˆž`.

* `mem_â„’p f p Î¼` : property that the function `f` is almost everywhere measurable and has finite
  p-seminorm for measure `Î¼` (`snorm f p Î¼ < âˆž`)
* `Lp Î± E p Î¼ := {f : Î± â†’â‚˜[Î¼} E // snorm f p Î¼ < âˆž}` : elements of `Î± â†’â‚˜[Î¼] E` (see ae_eq_fun) such
  that `snorm f p Î¼` is finite.

-/

open measure_theory

noncomputable theory

namespace Lp_space

section â„’p

variables {Î± E F : Type*} [measurable_space Î±] {Î¼ : measure Î±}
  [measurable_space E] [normed_group E]
  [normed_group F]
  {p : â„} {q : ennreal}

section â„’p_space_definition

/-- `(âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼) ^ (1/p)`, which is a seminorm on the space of measurable functions for which
this quantity is finite -/
def snorm' (f : Î± â†’ F) (p : â„) (Î¼ : measure Î±) : ennreal := (âˆ«â» a, (nnnorm (f a))^p âˆ‚Î¼) ^ (1/p)

/-- seminorm for `â„’âˆž`, equal to the essential supremum of `âˆ¥fâˆ¥`. -/
def snorm_ess_sup (f : Î± â†’ F) (Î¼ : measure Î±) := ess_sup (Î» x, (nnnorm (f x) : ennreal)) Î¼

/-- `â„’p` seminorm, equal to `0` for `p=0`, to `(âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼) ^ (1/p)` for `0 < p < âˆž` and to
`ess_sup âˆ¥fâˆ¥ Î¼` for `p = âˆž`. -/
def snorm (f : Î± â†’ F) (q : ennreal) (Î¼ : measure Î±) : ennreal :=
if q = 0 then 0 else (if q = âŠ¤ then snorm_ess_sup f Î¼ else snorm' f (ennreal.to_real q) Î¼)

lemma snorm_eq_snorm' (hq_ne_zero : q â‰  0) (hq_ne_top : q â‰  âŠ¤) {f : Î± â†’ F} :
  snorm f q Î¼ = snorm' f (ennreal.to_real q) Î¼ :=
by simp [snorm, hq_ne_zero, hq_ne_top]

@[simp] lemma snorm_exponent_top {f : Î± â†’ F} : snorm f âŠ¤ Î¼ = snorm_ess_sup f Î¼ := by simp [snorm]

/-- The property that `f:Î±â†’E` is ae_measurable and `(âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼)^(1/p)` is finite -/
def mem_â„’p (f : Î± â†’ E) (p : ennreal) (Î¼ : measure Î±) : Prop :=
ae_measurable f Î¼ âˆ§ snorm f p Î¼ < âŠ¤

lemma lintegral_rpow_nnnorm_eq_rpow_snorm' {f : Î± â†’ F} (hp0_lt : 0 < p) :
  âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼ = (snorm' f p Î¼) ^ p :=
begin
  rw [snorm', â†ennreal.rpow_mul, one_div, inv_mul_cancel, ennreal.rpow_one],
  exact (ne_of_lt hp0_lt).symm,
end

end â„’p_space_definition

lemma mem_â„’p_one_iff_integrable {f : Î± â†’ E} : mem_â„’p f 1 Î¼ â†” integrable f Î¼ :=
by simp_rw [integrable, has_finite_integral, mem_â„’p,
    snorm_eq_snorm' one_ne_zero ennreal.one_ne_top, ennreal.one_to_real, snorm', one_div_one,
    ennreal.rpow_one]

section top

lemma mem_â„’p.snorm_lt_top {f : Î± â†’ E} (hfp : mem_â„’p f q Î¼) : snorm f q Î¼ < âŠ¤ := hfp.2

lemma mem_â„’p.snorm_ne_top {f : Î± â†’ E} (hfp : mem_â„’p f q Î¼) : snorm f q Î¼ â‰  âŠ¤ := ne_of_lt (hfp.2)

lemma lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top {f : Î± â†’ F} (hp0_lt : 0 < p)
  (hfp : snorm' f p Î¼ < âŠ¤) :
  âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼ < âŠ¤ :=
begin
  rw lintegral_rpow_nnnorm_eq_rpow_snorm' hp0_lt,
  exact ennreal.rpow_lt_top_of_nonneg (le_of_lt hp0_lt) (ne_of_lt hfp),
end

end top

section zero

@[simp] lemma snorm'_exponent_zero {f : Î± â†’ F} : snorm' f 0 Î¼ = 1 :=
by rw [snorm', div_zero, ennreal.rpow_zero]

@[simp] lemma snorm_exponent_zero {f : Î± â†’ F} : snorm f 0 Î¼ = 0 :=
by simp [snorm]

lemma mem_â„’p_zero_iff_ae_measurable {f : Î± â†’ E} : mem_â„’p f 0 Î¼ â†” ae_measurable f Î¼ :=
by simp [mem_â„’p, snorm_exponent_zero]

@[simp] lemma snorm'_zero (hp0_lt : 0 < p) : snorm' (0 : Î± â†’ F) p Î¼ = 0 :=
by simp [snorm', hp0_lt]

@[simp] lemma snorm'_zero' (hp0_ne : p â‰  0) (hÎ¼ : Î¼ â‰  0) : snorm' (0 : Î± â†’ F) p Î¼ = 0 :=
begin
  cases le_or_lt 0 p with hp0 hp_neg,
  { exact snorm'_zero (lt_of_le_of_ne hp0 hp0_ne.symm), },
  { simp [snorm', ennreal.rpow_eq_zero_iff, hÎ¼, hp_neg], },
end

@[simp] lemma snorm_ess_sup_zero : snorm_ess_sup (0 : Î± â†’ F) Î¼ = 0 :=
begin
  simp_rw [snorm_ess_sup, pi.zero_apply, nnnorm_zero, ennreal.coe_zero, â†ennreal.bot_eq_zero],
  exact ess_sup_const_bot,
end

@[simp] lemma snorm_zero : snorm (0 : Î± â†’ F) q Î¼ = 0 :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = âŠ¤,
  { simp only [h_top, snorm_exponent_top, snorm_ess_sup_zero], },
  rw â†ne.def at h0,
  simp [snorm_eq_snorm' h0 h_top,
    ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) h0.symm, h_topâŸ©],
end

lemma zero_mem_â„’p : mem_â„’p (0 : Î± â†’ E) q Î¼ :=
âŸ¨measurable_zero.ae_measurable, by { rw snorm_zero, exact ennreal.coe_lt_top, } âŸ©

lemma snorm'_measure_zero_of_pos {f : Î± â†’ F} (hp_pos : 0 < p) : snorm' f p 0 = 0 :=
by simp [snorm', hp_pos]

lemma snorm'_measure_zero_of_exponent_zero {f : Î± â†’ F} : snorm' f 0 0 = 1 := by simp [snorm']

lemma snorm'_measure_zero_of_neg {f : Î± â†’ F} (hp_neg : p < 0) : snorm' f p 0 = âŠ¤ :=
by simp [snorm', hp_neg]

@[simp] lemma snorm_ess_sup_measure_zero {f : Î± â†’ F} : snorm_ess_sup f 0 = 0 :=
by simp [snorm_ess_sup]

@[simp] lemma snorm_measure_zero {f : Î± â†’ F} : snorm f q 0 = 0 :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = âŠ¤,
  { simp [h_top], },
  rw â†ne.def at h0,
  simp [snorm_eq_snorm' h0 h_top, snorm',
    ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) h0.symm, h_topâŸ©],
end

end zero

section const

lemma snorm'_const (c : F) (hp_pos : 0 < p) :
  snorm' (Î» x : Î± , c) p Î¼ = (nnnorm c : ennreal) * (Î¼ set.univ) ^ (1/p) :=
begin
  rw [snorm', lintegral_const, @ennreal.mul_rpow_of_nonneg _ _ (1/p) (by simp [le_of_lt hp_pos])],
  congr,
  rw â†ennreal.rpow_mul,
  suffices hp_cancel : p * (1/p) = 1, by rw [hp_cancel, ennreal.rpow_one],
  rw [one_div, mul_inv_cancel (ne_of_lt hp_pos).symm],
end

lemma snorm'_const' [finite_measure Î¼] (c : F) (hc_ne_zero : c â‰  0) (hp_ne_zero : p â‰  0) :
  snorm' (Î» x : Î± , c) p Î¼ = (nnnorm c : ennreal) * (Î¼ set.univ) ^ (1/p) :=
begin
  rw [snorm', lintegral_const, ennreal.mul_rpow_of_ne_top _ (measure_ne_top Î¼ set.univ)],
  { congr,
    rw â†ennreal.rpow_mul,
    suffices hp_cancel : p * (1/p) = 1, by rw [hp_cancel, ennreal.rpow_one],
    rw [one_div, mul_inv_cancel hp_ne_zero], },
  { rw [ne.def, ennreal.rpow_eq_top_iff, auto.not_or_eq, auto.not_and_eq, auto.not_and_eq],
    split,
    { left,
      rwa [ennreal.coe_eq_zero, nnnorm_eq_zero], },
    { exact or.inl ennreal.coe_ne_top, }, },
end

lemma snorm_ess_sup_const (c : F) (hÎ¼ : Î¼ â‰  0) :
  snorm_ess_sup (Î» x : Î±, c) Î¼ = (nnnorm c : ennreal) :=
by rw [snorm_ess_sup, ess_sup_const _ hÎ¼]

lemma snorm'_const_of_probability_measure (c : F) (hp_pos : 0 < p) [probability_measure Î¼] :
  snorm' (Î» x : Î± , c) p Î¼ = (nnnorm c : ennreal) :=
by simp [snorm'_const c hp_pos, measure_univ]

lemma snorm_const (c : F) (h0 : q â‰  0) (hÎ¼ : Î¼ â‰  0) :
  snorm (Î» x : Î± , c) q Î¼ = (nnnorm c : ennreal) * (Î¼ set.univ) ^ (1/(ennreal.to_real q)) :=
begin
  by_cases h_top : q = âŠ¤,
  { simp [h_top, snorm_ess_sup_const c hÎ¼], },
  simp [snorm_eq_snorm' h0 h_top, snorm'_const,
    ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) h0.symm, h_topâŸ©],
end

lemma snorm_const' (c : F) (h0 : q â‰  0) (h_top: q â‰  âŠ¤) :
  snorm (Î» x : Î± , c) q Î¼ = (nnnorm c : ennreal) * (Î¼ set.univ) ^ (1/(ennreal.to_real q)) :=
begin
  simp [snorm_eq_snorm' h0 h_top, snorm'_const,
    ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) h0.symm, h_topâŸ©],
end

lemma mem_â„’p_const (c : E) [finite_measure Î¼] : mem_â„’p (Î» a:Î±, c) q Î¼ :=
begin
  refine âŸ¨measurable_const.ae_measurable, _âŸ©,
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases hÎ¼ : Î¼ = 0,
  { simp [hÎ¼], },
  rw snorm_const c h0 hÎ¼,
  refine ennreal.mul_lt_top ennreal.coe_lt_top _,
  refine ennreal.rpow_lt_top_of_nonneg _ (measure_ne_top Î¼ set.univ),
  simp,
end

end const

lemma snorm'_congr_ae {f g : Î± â†’ F} (hfg : f =áµ[Î¼] g) : snorm' f p Î¼ = snorm' g p Î¼ :=
begin
  suffices h_no_pow : âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼ = âˆ«â» a, (nnnorm (g a)) ^ p âˆ‚Î¼,
  { simp_rw [snorm', h_no_pow], },
  exact lintegral_congr_ae (hfg.mono (Î» x hx, by simp [*])),
end

lemma snorm_ess_sup_congr_ae {f g : Î± â†’ F} (hfg : f =áµ[Î¼] g) :
  snorm_ess_sup f Î¼ = snorm_ess_sup g Î¼ :=
ess_sup_congr_ae (hfg.mono (Î» x hx, by rw hx))

lemma snorm_congr_ae {f g : Î± â†’ F} (hfg : f =áµ[Î¼] g) : snorm f q Î¼ = snorm g q Î¼ :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = âŠ¤,
  { rw [h_top, snorm_exponent_top],
    exact snorm_ess_sup_congr_ae hfg, },
  repeat { rw snorm_eq_snorm' h0 h_top, },
  exact snorm'_congr_ae hfg,
end

lemma mem_â„’p.ae_eq {f g : Î± â†’ E} (hfg : f =áµ[Î¼] g) (hf_Lp : mem_â„’p f q Î¼) : mem_â„’p g q Î¼ :=
begin
  split,
  { cases hf_Lp.1 with f' hf',
    exact âŸ¨f', âŸ¨hf'.1, ae_eq_trans hfg.symm hf'.2âŸ©âŸ©, },
  { rw snorm_congr_ae hfg.symm,
    exact hf_Lp.2, },
end

lemma mem_â„’p_congr_ae {f g : Î± â†’ E} (hfg : f =áµ[Î¼] g) : mem_â„’p f q Î¼ â†” mem_â„’p g q Î¼ :=
âŸ¨Î» h, h.ae_eq hfg, Î» h, h.ae_eq hfg.symmâŸ©

section opens_measurable_space
variable [opens_measurable_space E]

lemma snorm'_eq_zero_of_ae_zero {f : Î± â†’ F} (hp0_lt : 0 < p) (hf_zero : f =áµ[Î¼] 0) :
  snorm' f p Î¼ = 0 :=
by rw [snorm'_congr_ae hf_zero, snorm'_zero hp0_lt]

lemma snorm'_eq_zero_of_ae_zero' (hp0_ne : p â‰  0) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ F} (hf_zero : f =áµ[Î¼] 0) :
  snorm' f p Î¼ = 0 :=
by rw [snorm'_congr_ae hf_zero, snorm'_zero' hp0_ne hÎ¼]

lemma ae_eq_zero_of_snorm'_eq_zero {f : Î± â†’ E} (hp0 : 0 â‰¤ p) (hf : ae_measurable f Î¼)
  (h : snorm' f p Î¼ = 0) :
  f =áµ[Î¼] 0 :=
begin
  rw [snorm', ennreal.rpow_eq_zero_iff] at h,
  cases h,
  { rw lintegral_eq_zero_iff' hf.nnnorm.ennreal_coe.ennreal_rpow_const at h,
    refine h.left.mono (Î» x hx, _),
    rw [pi.zero_apply, ennreal.rpow_eq_zero_iff] at hx,
    cases hx,
    { cases hx with hx _,
      rwa [â†ennreal.coe_zero, ennreal.coe_eq_coe, nnnorm_eq_zero] at hx, },
    { exact absurd hx.left ennreal.coe_ne_top, }, },
  { exfalso,
    rw [one_div, inv_lt_zero] at h,
    linarith, },
end

lemma snorm'_eq_zero_iff (hp0_lt : 0 < p) {f : Î± â†’ E} (hf : ae_measurable f Î¼) :
  snorm' f p Î¼ = 0 â†” f =áµ[Î¼] 0 :=
âŸ¨ae_eq_zero_of_snorm'_eq_zero (le_of_lt hp0_lt) hf, snorm'_eq_zero_of_ae_zero hp0_ltâŸ©

lemma coe_nnnorm_ae_le_snorm_ess_sup (f : Î± â†’ F) (Î¼ : measure Î±) :
  âˆ€áµ x âˆ‚Î¼, (nnnorm (f x) : ennreal) â‰¤ snorm_ess_sup f Î¼ :=
ennreal.ae_le_ess_sup (Î» x, (nnnorm (f x) : ennreal))

lemma snorm_ess_sup_eq_zero_iff {f : Î± â†’ F} : snorm_ess_sup f Î¼ = 0 â†” f =áµ[Î¼] 0 :=
begin
  rw [snorm_ess_sup, ennreal.ess_sup_eq_zero_iff],
  split; intro h;
    { refine h.mono (Î» x hx, _),
      simp_rw pi.zero_apply at hx âŠ¢,
      simpa using hx, },
end

lemma snorm_eq_zero_iff {f : Î± â†’ E} (hf : ae_measurable f Î¼) (h0 : q â‰  0) :
  snorm f q Î¼ = 0 â†” f =áµ[Î¼] 0 :=
begin
  by_cases h_top : q = âŠ¤,
  { rw [h_top, snorm_exponent_top, snorm_ess_sup_eq_zero_iff], },
  rw snorm_eq_snorm' h0 h_top,
  exact snorm'_eq_zero_iff
    (ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) h0.symm, h_topâŸ©) hf,
end

end opens_measurable_space

@[simp] lemma snorm'_neg {f : Î± â†’ F} : snorm' (-f) p Î¼ = snorm' f p Î¼ := by simp [snorm']

@[simp] lemma snorm_neg {f : Î± â†’ F} : snorm (-f) q Î¼ = snorm f q Î¼ :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = âŠ¤,
  { simp [h_top, snorm_ess_sup], },
  simp [snorm_eq_snorm' h0 h_top],
end

section borel_space
variable [borel_space E]

lemma mem_â„’p.neg {f : Î± â†’ E} (hf : mem_â„’p f q Î¼) : mem_â„’p (-f) q Î¼ :=
âŸ¨ae_measurable.neg hf.1, by simp [hf.right]âŸ©

lemma snorm'_le_snorm'_mul_rpow_measure_univ {p q : â„} (hp0_lt : 0 < p) (hpq : p â‰¤ q)
  {f : Î± â†’ E} (hf : ae_measurable f Î¼) :
  snorm' f p Î¼ â‰¤ snorm' f q Î¼ * (Î¼ set.univ) ^ (1/p - 1/q) :=
begin
  have hq0_lt : 0 < q, from lt_of_lt_of_le hp0_lt hpq,
  by_cases hpq_eq : p = q,
  { rw [hpq_eq, sub_self, ennreal.rpow_zero, mul_one],
    exact le_refl _, },
  have hpq : p < q, from lt_of_le_of_ne hpq hpq_eq,
  let g := Î» a : Î±, (1 : ennreal),
  have h_rw : âˆ«â» a, â†‘(nnnorm (f a))^p âˆ‚ Î¼ = âˆ«â» a, (nnnorm (f a) * (g a))^p âˆ‚ Î¼,
  from lintegral_congr (Î» a, by simp),
  repeat {rw snorm'},
  rw h_rw,
  let r := p * q / (q - p),
  have hpqr : 1/p = 1/q + 1/r,
  { field_simp [(ne_of_lt hp0_lt).symm,
      (ne_of_lt hq0_lt).symm],
    ring, },
  calc (âˆ«â» (a : Î±), (â†‘(nnnorm (f a)) * g a) ^ p âˆ‚Î¼) ^ (1/p)
      â‰¤ (âˆ«â» (a : Î±), â†‘(nnnorm (f a)) ^ q âˆ‚Î¼) ^ (1/q) * (âˆ«â» (a : Î±), (g a) ^ r âˆ‚Î¼) ^ (1/r) :
    ennreal.lintegral_Lp_mul_le_Lq_mul_Lr hp0_lt hpq hpqr Î¼ hf.nnnorm.ennreal_coe
      ae_measurable_const
  ... = (âˆ«â» (a : Î±), â†‘(nnnorm (f a)) ^ q âˆ‚Î¼) ^ (1/q) * Î¼ set.univ ^ (1/p - 1/q) :
    by simp [hpqr],
end

lemma snorm'_le_snorm_ess_sup_mul_rpow_measure_univ (hp_pos : 0 < p) {f : Î± â†’ F} :
  snorm' f p Î¼ â‰¤ snorm_ess_sup f Î¼ * (Î¼ set.univ) ^ (1/p) :=
begin
  have h_le : âˆ«â» (a : Î±), â†‘(nnnorm (f a)) ^ p âˆ‚Î¼ â‰¤ âˆ«â» (a : Î±), (snorm_ess_sup f Î¼) ^ p âˆ‚Î¼,
  { refine lintegral_mono_ae _,
    have h_nnnorm_le_snorm_ess_sup := coe_nnnorm_ae_le_snorm_ess_sup f Î¼,
    refine h_nnnorm_le_snorm_ess_sup.mono (Î» x hx, ennreal.rpow_le_rpow hx (le_of_lt hp_pos)), },
  rw [snorm', â†ennreal.rpow_one (snorm_ess_sup f Î¼)],
  nth_rewrite 1 â†mul_inv_cancel (ne_of_lt hp_pos).symm,
  rw [ennreal.rpow_mul, one_div,
    â†@ennreal.mul_rpow_of_nonneg _ _ pâ»Â¹ (by simp [le_of_lt hp_pos])],
  refine ennreal.rpow_le_rpow _ (by simp [le_of_lt hp_pos]),
  rwa lintegral_const at h_le,
end

lemma snorm'_le_snorm'_of_exponent_le {p q : â„} (hp0_lt : 0 < p) (hpq : p â‰¤ q) (Î¼ : measure Î±)
  [probability_measure Î¼] {f : Î± â†’ E} (hf : ae_measurable f Î¼) :
  snorm' f p Î¼ â‰¤ snorm' f q Î¼ :=
begin
  have h_le_Î¼ := snorm'_le_snorm'_mul_rpow_measure_univ hp0_lt hpq hf,
  rwa [measure_univ, ennreal.one_rpow, mul_one] at h_le_Î¼,
end

lemma snorm'_le_snorm_ess_sup (hp_pos : 0 < p) {f : Î± â†’ F} [probability_measure Î¼] :
  snorm' f p Î¼ â‰¤ snorm_ess_sup f Î¼ :=
le_trans (snorm'_le_snorm_ess_sup_mul_rpow_measure_univ hp_pos) (le_of_eq (by simp [measure_univ]))

lemma snorm_le_snorm_of_exponent_le {p q : ennreal} (hpq : p â‰¤ q) [probability_measure Î¼]
  {f : Î± â†’ E} (hf : ae_measurable f Î¼) :
  snorm f p Î¼ â‰¤ snorm f q Î¼ :=
begin
  by_cases hp0 : p = 0,
  { simp [hp0], },
  rw â†ne.def at hp0,
  by_cases hq_top : q = âŠ¤,
  { by_cases hp_top : p = âŠ¤,
    { rw [hq_top, hp_top],
      exact le_refl _, },
    { have hp_pos : 0 < p.to_real,
      from ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) hp0.symm, hp_topâŸ©,
      rw [snorm_eq_snorm' hp0 hp_top, hq_top, snorm_exponent_top],
      refine le_trans (snorm'_le_snorm_ess_sup_mul_rpow_measure_univ hp_pos) (le_of_eq _),
      simp [measure_univ], }, },
  { have hp_top : p â‰  âŠ¤,
    { by_contra hp_eq_top,
      push_neg at hp_eq_top,
      refine hq_top _,
      rwa [hp_eq_top, top_le_iff] at hpq, },
    have hp_pos : 0 < p.to_real,
    from ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) hp0.symm, hp_topâŸ©,
    have hq0 : q â‰  0,
    { by_contra hq_eq_zero,
      push_neg at hq_eq_zero,
      have hp_eq_zero : p = 0, from le_antisymm (by rwa hq_eq_zero at hpq) (zero_le _),
      rw [hp_eq_zero, ennreal.zero_to_real] at hp_pos,
      exact (lt_irrefl _) hp_pos, },
    have hpq_real : p.to_real â‰¤ q.to_real, by rwa ennreal.to_real_le_to_real hp_top hq_top,
    rw [snorm_eq_snorm' hp0 hp_top, snorm_eq_snorm' hq0 hq_top],
    exact snorm'_le_snorm'_of_exponent_le hp_pos hpq_real _ hf, },
end

lemma snorm'_lt_top_of_snorm'_lt_top_of_exponent_le {p q : â„} [finite_measure Î¼] {f : Î± â†’ E}
  (hf : ae_measurable f Î¼) (hfq_lt_top : snorm' f q Î¼ < âŠ¤) (hp_nonneg : 0 â‰¤ p) (hpq : p â‰¤ q) :
  snorm' f p Î¼ < âŠ¤ :=
begin
  cases le_or_lt p 0 with hp_nonpos hp_pos,
  { rw le_antisymm hp_nonpos hp_nonneg,
    simp, },
  have hq_pos : 0 < q, from lt_of_lt_of_le hp_pos hpq,
  calc snorm' f p Î¼
      â‰¤ snorm' f q Î¼ * (Î¼ set.univ) ^ (1/p - 1/q) :
    snorm'_le_snorm'_mul_rpow_measure_univ hp_pos hpq hf
  ... < âŠ¤ :
  begin
    rw ennreal.mul_lt_top_iff,
    refine or.inl âŸ¨hfq_lt_top, ennreal.rpow_lt_top_of_nonneg _ (measure_ne_top Î¼ set.univ)âŸ©,
    rwa [le_sub, sub_zero, one_div, one_div, inv_le_inv hq_pos hp_pos],
  end
end

lemma mem_â„’p.mem_â„’p_of_exponent_le {p q : ennreal} [finite_measure Î¼] {f : Î± â†’ E}
  (hfq : mem_â„’p f q Î¼) (hpq : p â‰¤ q) :
  mem_â„’p f p Î¼ :=
begin
  cases hfq with hfq_m hfq_lt_top,
  by_cases hp0 : p = 0,
  { rwa [hp0, mem_â„’p_zero_iff_ae_measurable], },
  rw â†ne.def at hp0,
  refine âŸ¨hfq_m, _âŸ©,
  by_cases hp_top : p = âŠ¤,
  { have hq_top : q = âŠ¤,
      by rwa [hp_top, top_le_iff] at hpq,
    rw [hp_top],
    rwa hq_top at hfq_lt_top, },
  have hp_pos : 0 < p.to_real,
    from ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) hp0.symm, hp_topâŸ©,
  by_cases hq_top : q = âŠ¤,
  { rw snorm_eq_snorm' hp0 hp_top,
    rw [hq_top, snorm_exponent_top] at hfq_lt_top,
    refine lt_of_le_of_lt (snorm'_le_snorm_ess_sup_mul_rpow_measure_univ hp_pos) _,
    refine ennreal.mul_lt_top hfq_lt_top _,
    exact ennreal.rpow_lt_top_of_nonneg (by simp [le_of_lt hp_pos]) (measure_ne_top Î¼ set.univ), },
  have hq0 : q â‰  0,
  { by_contra hq_eq_zero,
    push_neg at hq_eq_zero,
    have hp_eq_zero : p = 0, from le_antisymm (by rwa hq_eq_zero at hpq) (zero_le _),
    rw [hp_eq_zero, ennreal.zero_to_real] at hp_pos,
    exact (lt_irrefl _) hp_pos, },
  have hpq_real : p.to_real â‰¤ q.to_real, by rwa ennreal.to_real_le_to_real hp_top hq_top,
  rw snorm_eq_snorm' hp0 hp_top,
  rw snorm_eq_snorm' hq0 hq_top at hfq_lt_top,
  exact snorm'_lt_top_of_snorm'_lt_top_of_exponent_le hfq_m hfq_lt_top (le_of_lt hp_pos) hpq_real,
end

lemma mem_â„’p.integrable (hq1 : 1 â‰¤ q) {f : Î± â†’ E} [finite_measure Î¼] (hfq : mem_â„’p f q Î¼) :
  integrable f Î¼ :=
mem_â„’p_one_iff_integrable.mp (hfq.mem_â„’p_of_exponent_le hq1)

lemma snorm'_add_le {f g : Î± â†’ E} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) (hp1 : 1 â‰¤ p) :
  snorm' (f + g) p Î¼ â‰¤ snorm' f p Î¼ + snorm' g p Î¼ :=
calc (âˆ«â» a, â†‘(nnnorm ((f + g) a)) ^ p âˆ‚Î¼) ^ (1 / p)
    â‰¤ (âˆ«â» a, (((Î» a, (nnnorm (f a) : ennreal))
        + (Î» a, (nnnorm (g a) : ennreal))) a) ^ p âˆ‚Î¼) ^ (1 / p) :
begin
  refine @ennreal.rpow_le_rpow _ _ (1/p) _ (by simp [le_trans zero_le_one hp1]),
  refine lintegral_mono (Î» a, ennreal.rpow_le_rpow _ (le_trans zero_le_one hp1)),
  simp [â†ennreal.coe_add, nnnorm_add_le],
end
... â‰¤ snorm' f p Î¼ + snorm' g p Î¼ :
  ennreal.lintegral_Lp_add_le hf.nnnorm.ennreal_coe hg.nnnorm.ennreal_coe hp1

lemma snorm_ess_sup_add_le {f g : Î± â†’ F} :
  snorm_ess_sup (f + g) Î¼ â‰¤ snorm_ess_sup f Î¼ + snorm_ess_sup g Î¼ :=
begin
  refine le_trans (ess_sup_mono_ae (filter.eventually_of_forall (Î» x, _)))
    (ennreal.ess_sup_add_le _ _),
  simp_rw [pi.add_apply, â†ennreal.coe_add, ennreal.coe_le_coe],
  exact nnnorm_add_le _ _,
end

lemma snorm_add_le {f g : Î± â†’ E} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) (hq1 : 1 â‰¤ q) :
  snorm (f + g) q Î¼ â‰¤ snorm f q Î¼ + snorm g q Î¼ :=
begin
  by_cases hq0 : q = 0,
  { simp [hq0], },
  by_cases hq_top : q = âŠ¤,
  { simp [hq_top, snorm_ess_sup_add_le], },
  have hq1_real : 1 â‰¤ q.to_real,
  by rwa [â†ennreal.one_to_real, ennreal.to_real_le_to_real ennreal.one_ne_top hq_top],
  repeat { rw snorm_eq_snorm' hq0 hq_top, },
  exact snorm'_add_le hf hg hq1_real,
end

lemma snorm_add_lt_top_of_one_le {f g : Î± â†’ E} (hf : mem_â„’p f q Î¼) (hg : mem_â„’p g q Î¼)
  (hq1 : 1 â‰¤ q) :
  snorm (f + g) q Î¼ < âŠ¤ :=
lt_of_le_of_lt (snorm_add_le hf.1 hg.1 hq1) (ennreal.add_lt_top.mpr âŸ¨hf.2, hg.2âŸ©)

lemma snorm'_add_lt_top_of_le_one {f g : Î± â†’ E} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼)
  (hf_snorm : snorm' f p Î¼ < âŠ¤) (hg_snorm : snorm' g p Î¼ < âŠ¤) (hp_pos : 0 < p) (hp1 : p â‰¤ 1) :
  snorm' (f + g) p Î¼ < âŠ¤ :=
calc (âˆ«â» a, â†‘(nnnorm ((f + g) a)) ^ p âˆ‚Î¼) ^ (1 / p)
    â‰¤ (âˆ«â» a, (((Î» a, (nnnorm (f a) : ennreal))
        + (Î» a, (nnnorm (g a) : ennreal))) a) ^ p âˆ‚Î¼) ^ (1 / p) :
begin
  refine @ennreal.rpow_le_rpow _ _ (1/p) _ (by simp [hp_pos.le]),
  refine lintegral_mono (Î» a, ennreal.rpow_le_rpow _ hp_pos.le),
  simp [â†ennreal.coe_add, nnnorm_add_le],
end
... â‰¤ (âˆ«â» a, (nnnorm (f a) : ennreal) ^ p + (nnnorm (g a) : ennreal) ^ p âˆ‚Î¼) ^ (1 / p) :
begin
  refine @ennreal.rpow_le_rpow _ _ (1/p) (lintegral_mono (Î» a, _)) (by simp [hp_pos.le]),
  exact ennreal.rpow_add_le_add_rpow _ _ hp_pos hp1,
end
... < âŠ¤ :
begin
  refine @ennreal.rpow_lt_top_of_nonneg _ (1/p) (by simp [hp_pos.le]) _,
  rw [lintegral_add' hf.nnnorm.ennreal_coe.ennreal_rpow_const
    hg.nnnorm.ennreal_coe.ennreal_rpow_const, ennreal.add_ne_top, â†lt_top_iff_ne_top,
    â†lt_top_iff_ne_top],
  exact âŸ¨lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top hp_pos hf_snorm,
    lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top hp_pos hg_snormâŸ©,
end

lemma snorm_add_lt_top {f g : Î± â†’ E} (hf : mem_â„’p f q Î¼) (hg : mem_â„’p g q Î¼) :
  snorm (f + g) q Î¼ < âŠ¤ :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  rw â†ne.def at h0,
  cases le_total 1 q with hq1 hq1,
  { exact snorm_add_lt_top_of_one_le hf hg hq1, },
  have hq_top : q â‰  âŠ¤, from (lt_of_le_of_lt hq1 ennreal.coe_lt_top).ne,
  have hq_pos : 0 < q.to_real,
  { rw [â†ennreal.zero_to_real, @ennreal.to_real_lt_to_real 0 q ennreal.coe_ne_top hq_top],
    exact ((zero_le q).lt_of_ne h0.symm), },
  have hq1_real : q.to_real â‰¤ 1,
  { rwa [â†ennreal.one_to_real, @ennreal.to_real_le_to_real q 1 hq_top ennreal.coe_ne_top], },
  rw snorm_eq_snorm' h0 hq_top,
  rw [mem_â„’p, snorm_eq_snorm' h0 hq_top] at hf hg,
  exact snorm'_add_lt_top_of_le_one hf.1 hg.1 hf.2 hg.2 hq_pos hq1_real,
end

section second_countable_topology
variable [topological_space.second_countable_topology E]

lemma mem_â„’p.add {f g : Î± â†’ E} (hf : mem_â„’p f q Î¼) (hg : mem_â„’p g q Î¼) : mem_â„’p (f + g) q Î¼ :=
âŸ¨ae_measurable.add hf.1 hg.1, snorm_add_lt_top hf hgâŸ©

lemma mem_â„’p.sub {f g : Î± â†’ E} (hf : mem_â„’p f q Î¼) (hg : mem_â„’p g q Î¼) : mem_â„’p (f - g) q Î¼ :=
by { rw sub_eq_add_neg, exact hf.add hg.neg }

end second_countable_topology

end borel_space

section normed_space

variables {ð•œ : Type*} [normed_field ð•œ] [normed_space ð•œ E] [normed_space ð•œ F]

lemma snorm'_const_smul {f : Î± â†’ F} (c : ð•œ) (hp0_lt : 0 < p) :
  snorm' (c â€¢ f) p Î¼ = (nnnorm c : ennreal) * snorm' f p Î¼ :=
begin
  rw snorm',
  simp_rw [pi.smul_apply, nnnorm_smul, ennreal.coe_mul,
    ennreal.mul_rpow_of_nonneg _ _ (le_of_lt hp0_lt)],
  suffices h_integral : âˆ«â» a, â†‘(nnnorm c) ^ p * â†‘(nnnorm (f a)) ^ p âˆ‚Î¼
    = (nnnorm c : ennreal)^p * âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼,
  { apply_fun (Î» x, x ^ (1/p)) at h_integral,
    rw [h_integral, @ennreal.mul_rpow_of_nonneg _ _ (1/p) (by simp [le_of_lt hp0_lt])],
    congr,
    simp_rw [â†ennreal.rpow_mul, one_div, mul_inv_cancel (ne_of_lt hp0_lt).symm,
      ennreal.rpow_one], },
  rw lintegral_const_mul',
  rw ennreal.coe_rpow_of_nonneg _ (le_of_lt hp0_lt),
  exact ennreal.coe_ne_top,
end

lemma snorm_ess_sup_const_smul {f : Î± â†’ F} (c : ð•œ) :
  snorm_ess_sup (c â€¢ f) Î¼ = (nnnorm c : ennreal) * snorm_ess_sup f Î¼ :=
by simp_rw [snorm_ess_sup,  pi.smul_apply, nnnorm_smul, ennreal.coe_mul, ennreal.ess_sup_const_mul]

lemma snorm_const_smul {f : Î± â†’ F} (c : ð•œ) :
  snorm (c â€¢ f) q Î¼ = (nnnorm c : ennreal) * snorm f q Î¼ :=
begin
  by_cases h0 : q = 0,
  { simp [h0], },
  by_cases h_top : q = âŠ¤,
  { simp [h_top, snorm_ess_sup_const_smul], },
  repeat { rw snorm_eq_snorm' h0 h_top, },
  rw â†ne.def at h0,
  exact snorm'_const_smul c
    (ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) h0.symm, h_topâŸ©),
end

lemma mem_â„’p.const_smul [borel_space E] {f : Î± â†’ E} (hf : mem_â„’p f q Î¼) (c : ð•œ) :
  mem_â„’p (c â€¢ f) q Î¼ :=
âŸ¨ae_measurable.const_smul hf.1 c,
  lt_of_le_of_lt (le_of_eq (snorm_const_smul c)) (ennreal.mul_lt_top ennreal.coe_lt_top hf.2)âŸ©

lemma snorm'_smul_le_mul_snorm' [opens_measurable_space E] [measurable_space ð•œ]
  [opens_measurable_space ð•œ] {q r : â„}
  {f : Î± â†’ E} (hf : ae_measurable f Î¼) {Ï† : Î± â†’ ð•œ} (hÏ† : ae_measurable Ï† Î¼)
  (hp0_lt : 0 < p) (hpq : p < q) (hpqr : 1/p = 1/q + 1/r) :
  snorm' (Ï† â€¢ f) p Î¼ â‰¤ snorm' Ï† q Î¼ * snorm' f r Î¼ :=
begin
  simp_rw [snorm', pi.smul_apply', nnnorm_smul, ennreal.coe_mul],
  exact ennreal.lintegral_Lp_mul_le_Lq_mul_Lr hp0_lt hpq hpqr Î¼ hÏ†.nnnorm.ennreal_coe
    hf.nnnorm.ennreal_coe,
end

end normed_space

end â„’p

/-! ### Lp space

The space of equivalence classes of measurable functions for which `snorm f p Î¼ < âŠ¤`.
-/

variables {Î± E F : Type*} [measurable_space Î±] {Î¼ : measure Î±} [measurable_space E] [normed_group E]
  [normed_group F] {p : ennreal}

lemma mem_â„’p.snorm_mk_lt_top {f : Î± â†’ E} (hfp : mem_â„’p f p Î¼) :
  snorm (@ae_eq_fun.mk Î± E _ Î¼ _ f hfp.1) p Î¼ < âŠ¤ :=
by { rw snorm_congr_ae (ae_eq_fun.coe_fn_mk _ _), exact hfp.2 }

/-- Lp space -/
def Lp (Î± E : Type*) [measurable_space Î±] [measurable_space E] [normed_group E]
  (p : ennreal) (Î¼ : measure Î±) :=
{f : Î± â†’â‚˜[Î¼] E // snorm f p Î¼ < âŠ¤}

namespace Lp

lemma mk_coe_fn {f : Lp Î± E p Î¼} : (âŸ¨f.val, f.propâŸ© : Lp Î± E p Î¼) = f := subtype.eq rfl

/-- make an element of Lp from a function verifying `mem_â„’p` -/
def mk_of_fun {f : Î± â†’ E} (h_mem_â„’p : mem_â„’p f p Î¼) : Lp Î± E p Î¼ :=
âŸ¨@ae_eq_fun.mk Î± E _ Î¼ _ f h_mem_â„’p.1, h_mem_â„’p.snorm_mk_lt_topâŸ©

instance : has_coe_to_fun (Lp Î± E p Î¼) := âŸ¨_, Î» f, (f.val : Î± â†’ E)âŸ©

lemma coe_fn_mk {f : Î± â†’â‚˜[Î¼] E} (hf : snorm f p Î¼ < âŠ¤) : (âŸ¨f, hfâŸ© : Lp Î± E p Î¼) =áµ[Î¼] f :=
by refl

lemma coe_fn_mk_of_fun {f : Î± â†’ E} (hf : mem_â„’p f p Î¼) : mk_of_fun hf =áµ[Î¼] f :=
ae_eq_fun.coe_fn_mk _ _

lemma snorm_lt_top (f : Lp Î± E p Î¼) : snorm f p Î¼ < âŠ¤ := f.prop

lemma snorm_ne_top (f : Lp Î± E p Î¼) : snorm f p Î¼ â‰  âŠ¤ := f.prop.ne

lemma measurable (f : Lp Î± E p Î¼) : measurable f := f.val.measurable

lemma ae_measurable (f : Lp Î± E p Î¼) : ae_measurable f Î¼ := f.val.ae_measurable

lemma mem_â„’p (f : Lp Î± E p Î¼) : mem_â„’p f p Î¼ := âŸ¨f.ae_measurable, f.propâŸ©

/-- The norm in Lp space (which verifies the triangle inequality only for `1 â‰¤ p`) -/
def Lp_norm (f : Lp Î± E p Î¼) : â„ := ennreal.to_real (snorm f p Î¼)

instance : has_norm (Lp Î± E p Î¼) := { norm := Î» f, Lp_norm f }

@[simp] lemma norm_eq_Lp_norm {f : Lp Î± E p Î¼} : âˆ¥fâˆ¥ = Lp_norm f := rfl

lemma zero_mem_Lp : snorm (0 : Î± â†’â‚˜[Î¼] E) p Î¼ < âŠ¤ :=
by { rw snorm_congr_ae ae_eq_fun.coe_fn_zero, simp, }

instance : has_zero (Lp Î± E p Î¼) := { zero := âŸ¨0, zero_mem_LpâŸ© }

@[simp] lemma coe_zero : (0 : Lp Î± E p Î¼).val = (0 : Î± â†’â‚˜[Î¼] E) := rfl

lemma coe_fn_zero : â‡‘(0 : Lp Î± E p Î¼) =áµ[Î¼] 0 :=
ae_eq_fun.coe_fn_zero

instance : inhabited (Lp Î± E p Î¼) := âŸ¨0âŸ©

@[simp] lemma Lp_norm_zero : Lp_norm (0 : Lp Î± E p Î¼) = 0 :=
by simp [Lp_norm, snorm_congr_ae coe_fn_zero, snorm_zero]

lemma mem_Lp_const (Î±) [measurable_space Î±] (Î¼ : measure Î±) (c : E) [finite_measure Î¼] :
  snorm (@ae_eq_fun.const Î± _ _ Î¼ _ c) p Î¼ < âŠ¤ :=
(mem_â„’p_const c).snorm_mk_lt_top

/-- Constant function, as an element of Lp -/
def const (c : E) [finite_measure Î¼] : Lp Î± E p Î¼ := âŸ¨ae_eq_fun.const Î± c, mem_Lp_const Î± Î¼ câŸ©

section opens_measurable_space
variables [opens_measurable_space E]

lemma Lp_norm_eq_zero_iff {f : Lp Î± E p Î¼} (hp : 0 < p) : Lp_norm f = 0 â†” f = 0 :=
begin
  refine âŸ¨Î» hf, _, Î» hf, by simp [hf]âŸ©,
  rw [Lp_norm, ennreal.to_real_eq_zero_iff] at hf,
  cases hf,
  { rw snorm_eq_zero_iff f.ae_measurable hp.ne.symm at hf,
    exact subtype.eq (ae_eq_fun.ext (hf.trans ae_eq_fun.coe_fn_zero.symm)), },
  { exact absurd hf f.prop.ne, },
end

end opens_measurable_space

section borel_space
variables [borel_space E]

instance : has_neg (Lp Î± E p Î¼) :=
{ neg := Î» f,
    âŸ¨-f.val, by { rw [snorm_congr_ae (ae_eq_fun.coe_fn_neg _), snorm_neg], exact f.prop }âŸ© }

@[simp] lemma coe_neg {f : Lp Î± E p Î¼} : (-f).val = -f.val := rfl

lemma coe_fn_neg {f : Lp Î± E p Î¼} : â‡‘(-f) =áµ[Î¼] -f := ae_eq_fun.coe_fn_neg _

@[simp] lemma Lp_norm_neg {f : Lp Î± E p Î¼} : Lp_norm (-f) = Lp_norm f :=
by rw [Lp_norm, Lp_norm, snorm_congr_ae coe_fn_neg, snorm_neg]

section second_countable_topology
variable [topological_space.second_countable_topology E]

instance : has_add (Lp Î± E p Î¼) :=
{ add := Î» f g, âŸ¨f.val + g.val,
  by { rw snorm_congr_ae (ae_eq_fun.coe_fn_add _ _), exact snorm_add_lt_top f.mem_â„’p g.mem_â„’p }âŸ© }

@[simp] lemma coe_add {f g : Lp Î± E p Î¼} : (f + g).val = f.val + g.val := rfl

lemma coe_fn_add {f g : Lp Î± E p Î¼} : â‡‘(f + g) =áµ[Î¼] f + g := ae_eq_fun.coe_fn_add _ _

instance : add_comm_group (Lp Î± E p Î¼) :=
{ add := (+),
  neg := has_neg.neg,
  zero := 0,
  add_assoc := Î» _ _ _ , subtype.eq (add_assoc _ _ _),
  zero_add := Î» _, subtype.eq (zero_add _),
  add_zero := Î» _, subtype.eq (add_zero _),
  add_comm := Î» _ _, subtype.eq (add_comm _ _),
  add_left_neg := Î» _, subtype.eq (add_left_neg _), }

@[simp] lemma coe_sub {f g : Lp Î± E p Î¼} : (f - g).val = f.val - g.val := rfl

lemma coe_fn_sub {f g : Lp Î± E p Î¼} : â‡‘(f - g) =áµ[Î¼] f - g := ae_eq_fun.coe_fn_sub _ _

instance : has_dist (Lp Î± E p Î¼) := { dist := Î» f g, Lp_norm (f - g) }

private lemma dist_triangle' [hp : fact (1 â‰¤ p)] (f g h : Lp Î± E p Î¼) :
  dist f h â‰¤ dist f g + dist g h :=
begin
  simp only [dist, Lp_norm],
  rw â†ennreal.to_real_add (f - g).snorm_ne_top (g - h).snorm_ne_top,
  suffices h_snorm : snorm â‡‘(f - h) p Î¼ â‰¤ snorm â‡‘(f - g) p Î¼ + snorm â‡‘(g - h) p Î¼,
  { rwa ennreal.to_real_le_to_real (f - h).snorm_ne_top,
    exact ennreal.add_ne_top.mpr âŸ¨(f - g).snorm_ne_top, (g - h).snorm_ne_topâŸ©, },
  have h_add : (f - h) = f - g + (g - h), by abel,
  rw [h_add, snorm_congr_ae coe_fn_add],
  exact snorm_add_le (f - g).ae_measurable (g - h).ae_measurable hp,
end

instance [hp : fact (1 â‰¤ p)] : metric_space (Lp Î± E p Î¼) :=
{ dist_self := Î» _, by simp [dist, Lp_norm_zero],
  dist_comm := Î» _ _, by { simp only [dist], rw [â†Lp_norm_neg, neg_sub] },
  dist_triangle := dist_triangle',
  eq_of_dist_eq_zero := Î» _ _ h, by simpa [dist,
    Lp_norm_eq_zero_iff (ennreal.zero_lt_one.trans_le hp), sub_eq_zero] using h, }

instance [fact (1 â‰¤ p)] : normed_group (Lp Î± E p Î¼) := { dist_eq := Î» _ _, by simp [dist, norm] }

end second_countable_topology

section normed_space

variables {ð•œ : Type*} [normed_field ð•œ] [normed_space ð•œ E]

lemma mem_Lp_const_smul (c : ð•œ) (f : Lp Î± E p Î¼) : snorm (â‡‘(c â€¢ f.val)) p Î¼ < âŠ¤ :=
begin
  rw [snorm_congr_ae (ae_eq_fun.coe_fn_smul _ _), snorm_const_smul, ennreal.mul_lt_top_iff],
  exact or.inl âŸ¨ennreal.coe_lt_top, f.propâŸ©,
end

instance : has_scalar ð•œ (Lp Î± E p Î¼) := { smul := Î» c f, âŸ¨c â€¢ f.val, mem_Lp_const_smul c fâŸ© }

@[simp] lemma coe_smul {f : Lp Î± E p Î¼} {c : ð•œ} : (c â€¢ f).val = c â€¢ f.val := rfl

lemma coe_fn_smul {f : Lp Î± E p Î¼} {c : ð•œ} : â‡‘(c â€¢ f) =áµ[Î¼] c â€¢ f := ae_eq_fun.coe_fn_smul _ _

lemma Lp_norm_const_smul (c : ð•œ) (f : Lp Î± E p Î¼) : Lp_norm (c â€¢ f) = âˆ¥câˆ¥ * Lp_norm f :=
by simp [Lp_norm, snorm_congr_ae coe_fn_smul, snorm_const_smul c, ennreal.to_real_mul]

section normed_space_second_countable_topology
variable [topological_space.second_countable_topology E]

instance : mul_action ð•œ (Lp Î± E p Î¼) :=
{ one_smul := Î» _, subtype.eq (one_smul ð•œ _),
  mul_smul := Î» _ _ _, subtype.eq (mul_smul _ _ _) }

instance : distrib_mul_action ð•œ (Lp Î± E p Î¼) :=
{ smul_add := Î» _ _ _, subtype.eq (smul_add _ _ _),
  smul_zero := Î» _, subtype.eq (smul_zero _) }

instance : semimodule ð•œ (Lp Î± E p Î¼) :=
{ add_smul := Î» _ _ _, subtype.eq (add_smul _ _ _),
  zero_smul := Î» _, subtype.eq (zero_smul _ _) }

instance : vector_space ð•œ (Lp Î± E p Î¼) := infer_instance

instance [fact (1 â‰¤ p)] : normed_space ð•œ (Lp Î± E p Î¼) :=
{ norm_smul_le := Î» _ _, by simp only [norm, Lp_norm_const_smul] }

end normed_space_second_countable_topology

end normed_space

end borel_space

end Lp

end Lp_space
