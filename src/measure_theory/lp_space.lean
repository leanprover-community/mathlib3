/-
Copyright (c) 2020 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: RÃ©my Degenne.
-/
import measure_theory.l1_space
import analysis.mean_inequalities

/-!
# â„’p space

This file describes properties of measurable functions with finite seminorm `(âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼) ^ (1/p)`
for `p:â„` with `1 â‰¤ p`.

## Main definitions

* `mem_â„’p f p Î¼` : the function `f` has finite p-seminorm for measure `Î¼`, for `p:â„` such that
                  `hp1 : 1 â‰¤ p`,

## Notation

* `snorm f p Î¼` : `(âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼) ^ (1/p)` for `f : Î± â†’ F`, where `Î±` is a  measurable space and
                  `F` is a normed group.

-/

open measure_theory

noncomputable theory

namespace â„’p_space

variables {Î± E F : Type*} [measurable_space Î±] {Î¼ : measure Î±}
  [measurable_space E] [normed_group E]
  [normed_group F]
  {p : â„}

section â„’p_space_definition

/-- The property that `f:Î±â†’E` is ae_measurable and `âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼` is finite -/
def mem_â„’p (f : Î± â†’ E) (p : â„) (Î¼ : measure Î±) : Prop :=
ae_measurable f Î¼ âˆ§ âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼ < âŠ¤

/-- `(âˆ« âˆ¥f aâˆ¥^p âˆ‚Î¼) ^ (1/p)`, which is a seminorm on the space of measurable functions for which
this quantity is finite -/
def snorm (f : Î± â†’ F) (p : â„) (Î¼ : measure Î±) : ennreal := (âˆ«â» a, (nnnorm (f a))^p âˆ‚Î¼) ^ (1/p)

lemma lintegral_rpow_nnnorm_eq_rpow_snorm {f : Î± â†’ F} (hp0_lt : 0 < p) :
  âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼ = (snorm f p Î¼) ^ p :=
begin
  rw [snorm, â†ennreal.rpow_mul, one_div, inv_mul_cancel, ennreal.rpow_one],
  exact (ne_of_lt hp0_lt).symm,
end

end â„’p_space_definition

lemma mem_â„’p_one_iff_integrable {f : Î± â†’ E} : mem_â„’p f 1 Î¼ â†” integrable f Î¼ :=
by simp only [integrable, has_finite_integral, mem_â„’p, ennreal.rpow_one, nnreal.coe_one]

section top

lemma mem_â„’p.snorm_lt_top {f : Î± â†’ E} (hp0 : 0 â‰¤ p) (hfp : mem_â„’p f p Î¼) : snorm f p Î¼ < âŠ¤ :=
begin
  refine ennreal.rpow_lt_top_of_nonneg _ (ne_of_lt hfp.right),
  rw [one_div, inv_nonneg],
  exact hp0,
end

lemma mem_â„’p.snorm_ne_top {f : Î± â†’ E} (hp0 : 0 â‰¤ p) (hfp : mem_â„’p f p Î¼) : snorm f p Î¼ â‰  âŠ¤ :=
ne_of_lt (hfp.snorm_lt_top hp0)

lemma lintegral_rpow_nnnorm_lt_top_of_snorm_lt_top {f : Î± â†’ F} (hp0_lt : 0 < p)
  (hfp : snorm f p Î¼ < âŠ¤) :
  âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼ < âŠ¤ :=
begin
  rw lintegral_rpow_nnnorm_eq_rpow_snorm hp0_lt,
  exact ennreal.rpow_lt_top_of_nonneg (le_of_lt hp0_lt) (ne_of_lt hfp),
end

lemma mem_â„’p_of_snorm_lt_top {f : Î± â†’ E} (hp0_lt : 0 < p) (hfm : ae_measurable f Î¼)
  (hfp : snorm f p Î¼ < âŠ¤) : mem_â„’p f p Î¼ :=
âŸ¨hfm, lintegral_rpow_nnnorm_lt_top_of_snorm_lt_top hp0_lt hfpâŸ©

end top

section zero

@[simp] lemma snorm_exponent_zero {f : Î± â†’ F} : snorm f 0 Î¼ = 1 :=
by rw [snorm, div_zero, ennreal.rpow_zero]

lemma zero_mem_â„’p_of_pos (hp_pos : 0 < p) : mem_â„’p (0 : Î± â†’ E) p Î¼ :=
âŸ¨measurable_zero.ae_measurable, by simp [hp_pos]âŸ©

lemma zero_mem_â„’p_of_nonneg [finite_measure Î¼] (hp0 : 0 â‰¤ p) : mem_â„’p (0 : Î± â†’ E) p Î¼ :=
begin
  by_cases h0 : p = 0,
  { rw h0,
    split,
    { exact measurable_zero.ae_measurable, },
    { simp [measure_lt_top Î¼ set.univ], }, },
  { rw â†ne.def at h0,
    exact zero_mem_â„’p_of_pos (lt_of_le_of_ne hp0 h0.symm), },
end

@[simp] lemma snorm_zero (hp0_lt : 0 < p) : snorm (0 : Î± â†’ F) p Î¼ = 0 :=
by simp [snorm, hp0_lt]

@[simp] lemma snorm_zero' (hp0_ne : p â‰  0) (hÎ¼ : Î¼ â‰  0) : snorm (0 : Î± â†’ F) p Î¼ = 0 :=
begin
  cases le_or_lt 0 p with hp0 hp_neg,
  { exact snorm_zero (lt_of_le_of_ne hp0 hp0_ne.symm), },
  { rw [snorm, ennreal.rpow_eq_zero_iff],
    simp [hÎ¼, hp_neg], },
end

/-- When `Î¼ = 0`, we have `âˆ« f^p âˆ‚Î¼ = 0`. `snorm f p Î¼` is then `0`, `1` or `âŠ¤` depending on `p`. -/
lemma snorm_measure_zero_of_pos {f : Î± â†’ F} (hp_pos : 0 < p) : snorm f p 0 = 0 :=
by simp [snorm, hp_pos]

/-- When `Î¼ = 0`, we have `âˆ« f^p âˆ‚Î¼ = 0`. `snorm f p Î¼` is then `0`, `1` or `âŠ¤` depending on `p`. -/
lemma snorm_measure_zero_of_exponent_zero {f : Î± â†’ F} : snorm f 0 0 = 1 := by simp [snorm]

/-- When `Î¼ = 0`, we have `âˆ« f^p âˆ‚Î¼ = 0`. `snorm f p Î¼` is then `0`, `1` or `âŠ¤` depending on `p`. -/
lemma snorm_measure_zero_of_neg {f : Î± â†’ F} (hp_neg : p < 0) : snorm f p 0 = âŠ¤ :=
by simp [snorm, hp_neg]

end zero

lemma snorm_const (c : F) (hp_pos : 0 < p) :
  snorm (Î» x : Î± , c) p Î¼ = (nnnorm c : ennreal) * (Î¼ set.univ) ^ (1/p) :=
begin
  rw [snorm, lintegral_const, @ennreal.mul_rpow_of_nonneg _ _ (1/p) (by simp [le_of_lt hp_pos])],
  congr,
  rw â†ennreal.rpow_mul,
  suffices hp_cancel : p * (1/p) = 1, by rw [hp_cancel, ennreal.rpow_one],
  rw [one_div, mul_inv_cancel (ne_of_lt hp_pos).symm],
end

lemma snorm_const' [finite_measure Î¼] (c : F) (hc_ne_zero : c â‰  0) (hp_ne_zero : p â‰  0) :
  snorm (Î» x : Î± , c) p Î¼ = (nnnorm c : ennreal) * (Î¼ set.univ) ^ (1/p) :=
begin
  rw [snorm, lintegral_const, ennreal.mul_rpow_of_ne_top _ (measure_ne_top Î¼ set.univ)],
  { congr,
    rw â†ennreal.rpow_mul,
    suffices hp_cancel : p * (1/p) = 1, by rw [hp_cancel, ennreal.rpow_one],
    rw [one_div, mul_inv_cancel hp_ne_zero], },
  { rw [ne.def, ennreal.rpow_eq_top_iff, auto.not_or_eq, auto.not_and_eq, auto.not_and_eq],
    split,
    { left,
      rwa [ennreal.coe_eq_zero, nnnorm_eq_zero], },
    { exact or.inl ennreal.coe_ne_top, }, },
end

lemma snorm_const_of_probability_measure (c : F) (hp_pos : 0 < p) [probability_measure Î¼] :
  snorm (Î» x : Î± , c) p Î¼ = (nnnorm c : ennreal) :=
by simp [snorm_const c hp_pos, measure_univ]

lemma mem_â„’p_const (c : E) (h : c â‰  0 âˆ¨ 0 â‰¤ p) [finite_measure Î¼] : mem_â„’p (Î» a:Î±, c) p Î¼ :=
begin
  split,
  { exact measurable_const.ae_measurable, },
  dsimp only,
  rw lintegral_const,
  refine ennreal.mul_lt_top _ (measure_lt_top Î¼ set.univ),
  rw [lt_top_iff_ne_top, ne.def, ennreal.rpow_eq_top_iff, auto.not_or_eq, auto.not_and_eq,
    auto.not_and_eq],
  split,
  { rw [ennreal.coe_eq_zero, nnnorm_eq_zero],
    push_neg,
    exact h, },
  { exact or.inl ennreal.coe_ne_top, },
end

lemma mem_â„’p_const_of_nonneg (c : E) (hp0 : 0 â‰¤ p) [finite_measure Î¼] : mem_â„’p (Î» a:Î±, c) p Î¼ :=
mem_â„’p_const c (or.inr hp0)

lemma mem_â„’p_const_of_ne_zero (c : E) (hc : c â‰  0) [finite_measure Î¼] : mem_â„’p (Î» a:Î±, c) p Î¼ :=
mem_â„’p_const c (or.inl hc)

lemma snorm_congr_ae {f g : Î± â†’ F} (hfg : f =áµ[Î¼] g) :
  snorm f p Î¼ = snorm g p Î¼ :=
begin
  suffices h_no_pow : âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼ = âˆ«â» a, (nnnorm (g a)) ^ p âˆ‚Î¼,
  { simp_rw [snorm, h_no_pow], },
  exact lintegral_congr_ae
    (filter.eventually.mp hfg (filter.eventually_of_forall (Î» x hx, by simp [*]))),
end

lemma mem_â„’p.ae_eq {f g : Î± â†’ E} (hfg : f =áµ[Î¼] g) (hf_Lp : mem_â„’p f p Î¼) :
  mem_â„’p g p Î¼ :=
begin
  split,
  { cases hf_Lp.1 with f' hf',
    use f',
    exact âŸ¨hf'.1, ae_eq_trans hfg.symm hf'.2âŸ©, },
  have h_eq : âˆ«â» (a : Î±), (nnnorm (g a)) ^ p âˆ‚Î¼ = âˆ«â» (a : Î±), (nnnorm (f a)) ^ p âˆ‚Î¼,
  from lintegral_congr_ae
    (filter.eventually.mp hfg (filter.eventually_of_forall (Î» x hx, by simp [hx]))),
  rw h_eq,
  exact hf_Lp.2,
end

lemma mem_â„’p_congr_ae {f g : Î± â†’ E} (hfg : f =áµ[Î¼] g) :
  mem_â„’p f p Î¼ â†” mem_â„’p g p Î¼ :=
âŸ¨Î» h, h.ae_eq hfg, Î» h, h.ae_eq hfg.symmâŸ©

section opens_measurable_space
variable [opens_measurable_space E]

lemma snorm_eq_zero_of_ae_zero {f : Î± â†’ F} (hp0_lt : 0 < p) (hf_zero : f =áµ[Î¼] 0) :
  snorm f p Î¼ = 0 :=
by rw [snorm_congr_ae hf_zero, snorm_zero hp0_lt]

lemma snorm_eq_zero_of_ae_zero' (hp0_ne : p â‰  0) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ F} (hf_zero : f =áµ[Î¼] 0) :
  snorm f p Î¼ = 0 :=
by rw [snorm_congr_ae hf_zero, snorm_zero' hp0_ne hÎ¼]

lemma ae_eq_zero_of_snorm_eq_zero {f : Î± â†’ E} (hp0 : 0 â‰¤ p) (hf : ae_measurable f Î¼)
  (h : snorm f p Î¼ = 0) :
  f =áµ[Î¼] 0 :=
begin
  rw [snorm, ennreal.rpow_eq_zero_iff] at h,
  cases h,
  { rw lintegral_eq_zero_iff' hf.nnnorm.ennreal_coe.ennreal_rpow_const at h,
    refine filter.eventually.mp h.left (filter.eventually_of_forall (Î» x hx, _)),
    rw [pi.zero_apply, ennreal.rpow_eq_zero_iff] at hx,
    cases hx,
    { cases hx with hx _,
      rwa [â†ennreal.coe_zero, ennreal.coe_eq_coe, nnnorm_eq_zero] at hx, },
    { exfalso,
      exact ennreal.coe_ne_top hx.left, }, },
  { exfalso,
    rw [one_div, inv_lt_zero] at h,
    linarith, },
end

lemma snorm_eq_zero_iff (hp0_lt : 0 < p) {f : Î± â†’ E} (hf : ae_measurable f Î¼) :
  snorm f p Î¼ = 0 â†” f =áµ[Î¼] 0 :=
âŸ¨ae_eq_zero_of_snorm_eq_zero (le_of_lt hp0_lt) hf, snorm_eq_zero_of_ae_zero hp0_ltâŸ©

end opens_measurable_space

@[simp] lemma snorm_neg {f : Î± â†’ F} : snorm (-f) p Î¼ = snorm f p Î¼ :=
by simp [snorm]

section borel_space
variable [borel_space E]

lemma mem_â„’p.neg {f : Î± â†’ E} (hf : mem_â„’p f p Î¼) : mem_â„’p (-f) p Î¼ :=
âŸ¨ae_measurable.neg hf.1, by simp [hf.right]âŸ©

lemma snorm_le_snorm_mul_rpow_measure_univ {p q : â„} (hp0_lt : 0 < p) (hpq : p â‰¤ q) (Î¼ : measure Î±)
  {f : Î± â†’ E} (hf : ae_measurable f Î¼) :
  snorm f p Î¼ â‰¤ snorm f q Î¼ * (Î¼ set.univ) ^ (1/p - 1/q) :=
begin
  have hq0_lt : 0 < q, from lt_of_lt_of_le hp0_lt hpq,
  by_cases hpq_eq : p = q,
  { rw [hpq_eq, sub_self, ennreal.rpow_zero, mul_one],
    exact le_refl _, },
  have hpq : p < q, from lt_of_le_of_ne hpq hpq_eq,
  let g := Î» a : Î±, (1 : ennreal),
  have h_rw : âˆ«â» a, â†‘(nnnorm (f a))^p âˆ‚ Î¼ = âˆ«â» a, (nnnorm (f a) * (g a))^p âˆ‚ Î¼,
  from lintegral_congr (Î» a, by simp),
  repeat {rw snorm},
  rw h_rw,
  let r := p * q / (q - p),
  have hpqr : 1/p = 1/q + 1/r,
  { field_simp [(ne_of_lt hp0_lt).symm,
      (ne_of_lt hq0_lt).symm],
    ring, },
  calc (âˆ«â» (a : Î±), (â†‘(nnnorm (f a)) * g a) ^ p âˆ‚Î¼) ^ (1/p)
      â‰¤ (âˆ«â» (a : Î±), â†‘(nnnorm (f a)) ^ q âˆ‚Î¼) ^ (1/q) * (âˆ«â» (a : Î±), (g a) ^ r âˆ‚Î¼) ^ (1/r) :
    ennreal.lintegral_Lp_mul_le_Lq_mul_Lr hp0_lt hpq hpqr Î¼ hf.nnnorm.ennreal_coe
      ae_measurable_const
  ... = (âˆ«â» (a : Î±), â†‘(nnnorm (f a)) ^ q âˆ‚Î¼) ^ (1/q) * Î¼ set.univ ^ (1/p - 1/q) :
    by simp [hpqr],
end

lemma snorm_le_snorm_of_exponent_le {p q : â„} (hp0_lt : 0 < p) (hpq : p â‰¤ q) (Î¼ : measure Î±)
  [probability_measure Î¼] {f : Î± â†’ E} (hf : ae_measurable f Î¼) :
  snorm f p Î¼ â‰¤ snorm f q Î¼ :=
begin
  have h_le_Î¼ := snorm_le_snorm_mul_rpow_measure_univ hp0_lt hpq Î¼ hf,
  rwa [measure_univ, ennreal.one_rpow, mul_one] at h_le_Î¼,
end

lemma mem_â„’p.mem_â„’p_of_exponent_le {p q : â„} {Î¼ : measure Î±} [finite_measure Î¼] {f : Î± â†’ E}
  (hfq : mem_â„’p f q Î¼) (hp_pos : 0 < p) (hpq : p â‰¤ q) :
  mem_â„’p f p Î¼ :=
begin
  cases hfq with hfq_m hfq_lt_top,
  split,
  { exact hfq_m, },
  have hq_pos : 0 < q, from lt_of_lt_of_le  hp_pos hpq,
  suffices h_snorm : snorm f p Î¼ < âŠ¤,
  { have h_top_eq : (âŠ¤ : ennreal) = âŠ¤ ^ (1/p), by simp [hp_pos],
    rw [snorm, h_top_eq] at h_snorm,
    have h_snorm_pow : ((âˆ«â» (a : Î±), â†‘(nnnorm (f a)) ^ p âˆ‚Î¼) ^ (1/p)) ^ p < (âŠ¤ ^ (1/p)) ^ p,
    from ennreal.rpow_lt_rpow h_snorm hp_pos,
    rw [â†ennreal.rpow_mul, â†ennreal.rpow_mul] at h_snorm_pow,
    simpa [(ne_of_lt hp_pos).symm] using h_snorm_pow, },
  calc snorm f p Î¼
      â‰¤ snorm f q Î¼ * (Î¼ set.univ) ^ (1/p - 1/q) :
    snorm_le_snorm_mul_rpow_measure_univ hp_pos hpq Î¼ hfq_m
  ... < âŠ¤ :
  begin
    rw ennreal.mul_lt_top_iff,
    left,
    split,
    { exact mem_â„’p.snorm_lt_top (le_of_lt hq_pos) âŸ¨hfq_m, hfq_lt_topâŸ©, },
    { refine ennreal.rpow_lt_top_of_nonneg _ (measure_ne_top Î¼ set.univ),
      rwa [le_sub, sub_zero, one_div, one_div, inv_le_inv hq_pos hp_pos], },
  end
end

lemma mem_â„’p.integrable (hp1 : 1 â‰¤ p) {f : Î± â†’ E} [finite_measure Î¼] (hfp : mem_â„’p f p Î¼) :
  integrable f Î¼ :=
begin
  rw â†mem_â„’p_one_iff_integrable,
  exact hfp.mem_â„’p_of_exponent_le zero_lt_one hp1,
end

lemma snorm_add_le {f g : Î± â†’ E} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) (hp1 : 1 â‰¤ p) :
  snorm (f + g) p Î¼ â‰¤ snorm f p Î¼ + snorm g p Î¼ :=
calc (âˆ«â» a, â†‘(nnnorm ((f + g) a)) ^ p âˆ‚Î¼) ^ (1 / p)
    â‰¤ (âˆ«â» a, (((Î» a, (nnnorm (f a) : ennreal))
        + (Î» a, (nnnorm (g a) : ennreal))) a) ^ p âˆ‚Î¼) ^ (1 / p) :
begin
  refine @ennreal.rpow_le_rpow _ _ (1/p) _ (by simp [le_trans zero_le_one hp1]),
  refine lintegral_mono (Î» a, ennreal.rpow_le_rpow _ (le_trans zero_le_one hp1)),
  simp [â†ennreal.coe_add, nnnorm_add_le],
end
... â‰¤ snorm f p Î¼ + snorm g p Î¼ :
  ennreal.lintegral_Lp_add_le hf.nnnorm.ennreal_coe hg.nnnorm.ennreal_coe hp1

section second_countable_topology
variable [topological_space.second_countable_topology E]

lemma mem_â„’p.add {f g : Î± â†’ E} (hf : mem_â„’p f p Î¼) (hg : mem_â„’p g p Î¼) (hp1 : 1 â‰¤ p) :
  mem_â„’p (f+g) p Î¼ :=
begin
  have hp0_lt : 0 < p, from lt_of_lt_of_le zero_lt_one hp1,
  have hp0 : 0 â‰¤ p, from le_of_lt hp0_lt,
  split,
  { exact ae_measurable.add hf.1 hg.1, },
  simp_rw [pi.add_apply, ennreal.coe_rpow_of_nonneg _ hp0],
  have h_nnnorm_add_le : âˆ«â» (a : Î±), â†‘(nnnorm (f a + g a) ^ p) âˆ‚Î¼
    â‰¤ âˆ«â» a, â†‘((nnnorm (f a) + nnnorm (g a)) ^ p) âˆ‚Î¼,
  { refine lintegral_mono_nnreal (Î» a, _),
    exact nnreal.rpow_le_rpow (nnnorm_add_le (f a) (g a)) (le_of_lt hp0_lt), },
  refine lt_of_le_of_lt h_nnnorm_add_le _,
  simp_rw [â†ennreal.coe_rpow_of_nonneg _ hp0, ennreal.coe_add],
  let f_nnnorm := (Î» a : Î±, (nnnorm (f a) : ennreal)),
  let g_nnnorm := (Î» a : Î±, (nnnorm (g a) : ennreal)),
  change âˆ«â» (a : Î±), ((f_nnnorm + g_nnnorm) a) ^ p âˆ‚Î¼ < âŠ¤,
  exact ennreal.lintegral_rpow_add_lt_top_of_lintegral_rpow_lt_top hf.1.nnnorm.ennreal_coe hf.2
    hg.1.nnnorm.ennreal_coe hg.2 hp1,
end

lemma mem_â„’p.sub {f g : Î± â†’ E} (hf : mem_â„’p f p Î¼) (hg : mem_â„’p g p Î¼) (hp1 : 1 â‰¤ p) :
  mem_â„’p (f-g) p Î¼ :=
by { rw sub_eq_add_neg, exact hf.add hg.neg hp1 }

end second_countable_topology

section normed_space

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ E]

lemma mem_â„’p.const_smul {f : Î± â†’ E} (hfp : mem_â„’p f p Î¼) (c : ğ•œ) (hp0 : 0 â‰¤ p) :
  mem_â„’p (c â€¢ f) p Î¼ :=
begin
  split,
  { exact ae_measurable.const_smul hfp.1 c, },
  simp_rw [pi.smul_apply, nnnorm_smul, ennreal.coe_mul, ennreal.mul_rpow_of_nonneg _ _ hp0],
  rw lintegral_const_mul'' _ hfp.1.nnnorm.ennreal_coe.ennreal_rpow_const,
  exact ennreal.mul_lt_top (ennreal.rpow_lt_top_of_nonneg hp0 ennreal.coe_ne_top) hfp.2,
end

lemma snorm_const_smul {f : Î± â†’ E} (hf : ae_measurable f Î¼) (c : ğ•œ) (hp0_lt : 0 < p) :
  snorm (c â€¢ f) p Î¼ = (nnnorm c : ennreal) * snorm f p Î¼ :=
begin
  rw snorm,
  simp_rw [pi.smul_apply, nnnorm_smul, ennreal.coe_mul],
  simp_rw ennreal.mul_rpow_of_nonneg _ _ (le_of_lt hp0_lt),
  suffices h_integral : âˆ«â» a, â†‘(nnnorm c) ^ p * â†‘(nnnorm (f a)) ^ p âˆ‚Î¼
    = (nnnorm c : ennreal)^p * âˆ«â» a, (nnnorm (f a)) ^ p âˆ‚Î¼,
  { apply_fun (Î» x, x ^ (1/p)) at h_integral,
    rw [h_integral, @ennreal.mul_rpow_of_nonneg _ _ (1/p) (by simp [le_of_lt hp0_lt])],
    congr,
    simp_rw [â†ennreal.rpow_mul, one_div, mul_inv_cancel (ne_of_lt hp0_lt).symm,
      ennreal.rpow_one], },
  rw lintegral_const_mul'' _ hf.nnnorm.ennreal_coe.ennreal_rpow_const,
end

lemma snorm_smul_le_mul_snorm [measurable_space ğ•œ] [opens_measurable_space ğ•œ] {q r : â„}
  {f : Î± â†’ E} (hf : ae_measurable f Î¼) {Ï† : Î± â†’ ğ•œ} (hÏ† : ae_measurable Ï† Î¼)
  (hp0_lt : 0 < p) (hpq : p < q) (hpqr : 1/p = 1/q + 1/r) :
  snorm (Ï† â€¢ f) p Î¼ â‰¤ snorm Ï† q Î¼ * snorm f r Î¼ :=
begin
  rw snorm,
  simp_rw [pi.smul_apply', nnnorm_smul, ennreal.coe_mul],
  exact ennreal.lintegral_Lp_mul_le_Lq_mul_Lr hp0_lt hpq hpqr Î¼ hÏ†.nnnorm.ennreal_coe
    hf.nnnorm.ennreal_coe,
end

end normed_space

end borel_space

end â„’p_space
