/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/

import measure_theory.measurable_space_def
import measure_theory.tactic
import data.tprod
import data.equiv.fin

/-!
# Measurable spaces and measurable functions

This file provides properties of measurable spaces and the functions and isomorphisms
between them. The definition of a measurable space is in `measure_theory.measurable_space_def`.

A measurable space is a set equipped with a Ïƒ-algebra, a collection of
subsets closed under complementation and countable union. A function
between measurable spaces is measurable if the preimage of each
measurable subset is measurable.

Ïƒ-algebras on a fixed set `Î±` form a complete lattice. Here we order
Ïƒ-algebras by writing `mâ‚ â‰¤ mâ‚‚` if every set which is `mâ‚`-measurable is
also `mâ‚‚`-measurable (that is, `mâ‚` is a subset of `mâ‚‚`). In particular, any
collection of subsets of `Î±` generates a smallest Ïƒ-algebra which
contains all of them. A function `f : Î± â†’ Î²` induces a Galois connection
between the lattices of Ïƒ-algebras on `Î±` and `Î²`.

A measurable equivalence between measurable spaces is an equivalence
which respects the Ïƒ-algebras, that is, for which both directions of
the equivalence are measurable functions.

We say that a filter `f` is measurably generated if every set `s âˆˆ f` includes a measurable
set `t âˆˆ f`. This property is useful, e.g., to extract a measurable witness of `filter.eventually`.

## Notation

* We write `Î± â‰ƒáµ Î²` for measurable equivalences between the measurable spaces `Î±` and `Î²`.
  This should not be confused with `â‰ƒâ‚˜` which is used for diffeomorphisms between manifolds.

## Implementation notes

Measurability of a function `f : Î± â†’ Î²` between measurable spaces is
defined in terms of the Galois connection induced by f.

## References

* <https://en.wikipedia.org/wiki/Measurable_space>
* <https://en.wikipedia.org/wiki/Sigma-algebra>
* <https://en.wikipedia.org/wiki/Dynkin_system>

## Tags

measurable space, Ïƒ-algebra, measurable function, measurable equivalence, dynkin system,
Ï€-Î» theorem, Ï€-system
-/

open set encodable function equiv
open_locale classical filter


variables {Î± Î² Î³ Î´ Î´' : Type*} {Î¹ : Sort*} {s t u : set Î±}


namespace measurable_space

section functors
variables {m mâ‚ mâ‚‚ : measurable_space Î±} {m' : measurable_space Î²} {f : Î± â†’ Î²} {g : Î² â†’ Î±}

/-- The forward image of a measurable space under a function. `map f m` contains the sets
  `s : set Î²` whose preimage under `f` is measurable. -/
protected def map (f : Î± â†’ Î²) (m : measurable_space Î±) : measurable_space Î² :=
{ measurable_set'      := Î» s, m.measurable_set' $ f â»Â¹' s,
  measurable_set_empty := m.measurable_set_empty,
  measurable_set_compl := assume s hs, m.measurable_set_compl _ hs,
  measurable_set_Union := assume f hf, by { rw preimage_Union, exact m.measurable_set_Union _ hf }}

@[simp] lemma map_id : m.map id = m :=
measurable_space.ext $ assume s, iff.rfl

@[simp] lemma map_comp {f : Î± â†’ Î²} {g : Î² â†’ Î³} : (m.map f).map g = m.map (g âˆ˜ f) :=
measurable_space.ext $ assume s, iff.rfl

/-- The reverse image of a measurable space under a function. `comap f m` contains the sets
  `s : set Î±` such that `s` is the `f`-preimage of a measurable set in `Î²`. -/
protected def comap (f : Î± â†’ Î²) (m : measurable_space Î²) : measurable_space Î± :=
{ measurable_set'      := Î» s, âˆƒs', m.measurable_set' s' âˆ§ f â»Â¹' s' = s,
  measurable_set_empty := âŸ¨âˆ…, m.measurable_set_empty, rflâŸ©,
  measurable_set_compl := assume s âŸ¨s', hâ‚, hâ‚‚âŸ©, âŸ¨s'á¶œ, m.measurable_set_compl _ hâ‚, hâ‚‚ â–¸ rflâŸ©,
  measurable_set_Union := assume s hs,
    let âŸ¨s', hs'âŸ© := classical.axiom_of_choice hs in
    âŸ¨â‹ƒ i, s' i, m.measurable_set_Union _ (Î» i, (hs' i).left), by simp [hs'] âŸ© }

@[simp] lemma comap_id : m.comap id = m :=
measurable_space.ext $ assume s, âŸ¨assume âŸ¨s', hs', hâŸ©, h â–¸ hs', assume h, âŸ¨s, h, rflâŸ©âŸ©

@[simp] lemma comap_comp {f : Î² â†’ Î±} {g : Î³ â†’ Î²} : (m.comap f).comap g = m.comap (f âˆ˜ g) :=
measurable_space.ext $ assume s,
  âŸ¨assume âŸ¨t, âŸ¨u, h, huâŸ©, htâŸ©, âŸ¨u, h, ht â–¸ hu â–¸ rflâŸ©, assume âŸ¨t, h, htâŸ©, âŸ¨f â»Â¹' t, âŸ¨_, h, rflâŸ©, htâŸ©âŸ©

lemma comap_le_iff_le_map {f : Î± â†’ Î²} : m'.comap f â‰¤ m â†” m' â‰¤ m.map f :=
âŸ¨assume h s hs, h _ âŸ¨_, hs, rflâŸ©, assume h s âŸ¨t, ht, heqâŸ©, heq â–¸ h _ htâŸ©

lemma gc_comap_map (f : Î± â†’ Î²) :
  galois_connection (measurable_space.comap f) (measurable_space.map f) :=
assume f g, comap_le_iff_le_map

lemma map_mono (h : mâ‚ â‰¤ mâ‚‚) : mâ‚.map f â‰¤ mâ‚‚.map f := (gc_comap_map f).monotone_u h
lemma monotone_map : monotone (measurable_space.map f) := assume a b h, map_mono h
lemma comap_mono (h : mâ‚ â‰¤ mâ‚‚) : mâ‚.comap g â‰¤ mâ‚‚.comap g := (gc_comap_map g).monotone_l h
lemma monotone_comap : monotone (measurable_space.comap g) := assume a b h, comap_mono h

@[simp] lemma comap_bot : (âŠ¥ : measurable_space Î±).comap g = âŠ¥ := (gc_comap_map g).l_bot
@[simp] lemma comap_sup : (mâ‚ âŠ” mâ‚‚).comap g = mâ‚.comap g âŠ” mâ‚‚.comap g := (gc_comap_map g).l_sup
@[simp] lemma comap_supr {m : Î¹ â†’ measurable_space Î±} : (â¨†i, m i).comap g = (â¨†i, (m i).comap g) :=
(gc_comap_map g).l_supr

@[simp] lemma map_top : (âŠ¤ : measurable_space Î±).map f = âŠ¤ := (gc_comap_map f).u_top
@[simp] lemma map_inf : (mâ‚ âŠ“ mâ‚‚).map f = mâ‚.map f âŠ“ mâ‚‚.map f := (gc_comap_map f).u_inf
@[simp] lemma map_infi {m : Î¹ â†’ measurable_space Î±} : (â¨…i, m i).map f = (â¨…i, (m i).map f) :=
(gc_comap_map f).u_infi

lemma comap_map_le : (m.map f).comap f â‰¤ m := (gc_comap_map f).l_u_le _
lemma le_map_comap : m â‰¤ (m.comap g).map g := (gc_comap_map g).le_u_l _

end functors

lemma generate_from_le_generate_from {s t : set (set Î±)} (h : s âŠ† t) :
  generate_from s â‰¤ generate_from t :=
gi_generate_from.gc.monotone_l h

lemma generate_from_sup_generate_from {s t : set (set Î±)} :
  generate_from s âŠ” generate_from t = generate_from (s âˆª t) :=
(@gi_generate_from Î±).gc.l_sup.symm

lemma comap_generate_from {f : Î± â†’ Î²} {s : set (set Î²)} :
  (generate_from s).comap f = generate_from (preimage f '' s) :=
le_antisymm
  (comap_le_iff_le_map.2 $ generate_from_le $ assume t hts,
    generate_measurable.basic _ $ mem_image_of_mem _ $ hts)
  (generate_from_le $ assume t âŸ¨u, hu, eqâŸ©, eq â–¸ âŸ¨u, generate_measurable.basic _ hu, rflâŸ©)

end measurable_space

section measurable_functions
open measurable_space

lemma measurable_iff_le_map {mâ‚ : measurable_space Î±} {mâ‚‚ : measurable_space Î²} {f : Î± â†’ Î²} :
  measurable f â†” mâ‚‚ â‰¤ mâ‚.map f :=
iff.rfl

alias measurable_iff_le_map â†” measurable.le_map measurable.of_le_map

lemma measurable_iff_comap_le {mâ‚ : measurable_space Î±} {mâ‚‚ : measurable_space Î²} {f : Î± â†’ Î²} :
  measurable f â†” mâ‚‚.comap f â‰¤ mâ‚ :=
comap_le_iff_le_map.symm

alias measurable_iff_comap_le â†” measurable.comap_le measurable.of_comap_le

lemma measurable.mono {ma ma' : measurable_space Î±} {mb mb' : measurable_space Î²} {f : Î± â†’ Î²}
  (hf : @measurable Î± Î² ma mb f) (ha : ma â‰¤ ma') (hb : mb' â‰¤ mb) :
  @measurable Î± Î² ma' mb' f :=
Î» t ht, ha _ $ hf $ hb _ ht

@[measurability]
lemma measurable_from_top [measurable_space Î²] {f : Î± â†’ Î²} : @measurable _ _ âŠ¤ _ f :=
Î» s hs, trivial

lemma measurable_generate_from [measurable_space Î±] {s : set (set Î²)} {f : Î± â†’ Î²}
  (h : âˆ€ t âˆˆ s, measurable_set (f â»Â¹' t)) : @measurable _ _ _ (generate_from s) f :=
measurable.of_le_map $ generate_from_le h

variables [measurable_space Î±] [measurable_space Î²] [measurable_space Î³]

@[measurability]
lemma measurable_set_preimage {f : Î± â†’ Î²} {t : set Î²} (hf : measurable f) (ht : measurable_set t) :
  measurable_set (f â»Â¹' t) :=
hf ht

@[measurability] lemma measurable.iterate {f : Î± â†’ Î±} (hf : measurable f) : âˆ€ n, measurable (f^[n])
| 0 := measurable_id
| (n+1) := (measurable.iterate n).comp hf

@[nontriviality, measurability]
lemma subsingleton.measurable [subsingleton Î±] {f : Î± â†’ Î²} : measurable f :=
Î» s hs, @subsingleton.measurable_set Î± _ _ _

@[measurability]
lemma measurable.piecewise {s : set Î±} {_ : decidable_pred (âˆˆ s)} {f g : Î± â†’ Î²}
  (hs : measurable_set s) (hf : measurable f) (hg : measurable g) :
  measurable (piecewise s f g) :=
begin
  intros t ht,
  rw piecewise_preimage,
  exact hs.ite (hf ht) (hg ht)
end

/-- this is slightly different from `measurable.piecewise`. It can be used to show
`measurable (ite (x=0) 0 1)` by
`exact measurable.ite (measurable_set_singleton 0) measurable_const measurable_const`,
but replacing `measurable.ite` by `measurable.piecewise` in that example proof does not work. -/
lemma measurable.ite {p : Î± â†’ Prop} {_ : decidable_pred p} {f g : Î± â†’ Î²}
  (hp : measurable_set {a : Î± | p a}) (hf : measurable f) (hg : measurable g) :
  measurable (Î» x, ite (p x) (f x) (g x)) :=
measurable.piecewise hp hf hg

@[measurability]
lemma measurable.indicator [has_zero Î²] {s : set Î±} {f : Î± â†’ Î²}
  (hf : measurable f) (hs : measurable_set s) : measurable (s.indicator f) :=
hf.piecewise hs measurable_const

@[to_additive]
lemma measurable_one [has_one Î±] : measurable (1 : Î² â†’ Î±) := @measurable_const _ _ _ _ 1

lemma measurable_of_empty [is_empty Î±] (f : Î± â†’ Î²) : measurable f :=
begin
  assume s hs,
  convert measurable_set.empty,
  exact eq_empty_of_is_empty _,
end

lemma measurable_of_empty_codomain [is_empty Î²] (f : Î± â†’ Î²) : measurable f :=
by { haveI := function.is_empty f, exact measurable_of_empty f }

/-- A version of `measurable_const` that assumes `f x = f y` for all `x, y`. This version works
for functions between empty types. -/
lemma measurable_const' {f : Î² â†’ Î±} (hf : âˆ€ x y, f x = f y) : measurable f :=
begin
  casesI is_empty_or_nonempty Î²,
  { exact measurable_of_empty f },
  { convert measurable_const, exact funext (Î» x, hf x h.some) }
end

@[to_additive, measurability] lemma measurable_set_mul_support [has_one Î²]
  [measurable_singleton_class Î²] {f : Î± â†’ Î²} (hf : measurable f) :
  measurable_set (mul_support f) :=
hf (measurable_set_singleton 1).compl

attribute [measurability] measurable_set_support

/-- If a function coincides with a measurable function outside of a countable set, it is
measurable. -/
lemma measurable.measurable_of_countable_ne [measurable_singleton_class Î±]
  {f g : Î± â†’ Î²} (hf : measurable f) (h : countable {x | f x â‰  g x}) : measurable g :=
begin
  assume t ht,
  have : g â»Â¹' t = (g â»Â¹' t âˆ© {x | f x = g x}á¶œ) âˆª (g â»Â¹' t âˆ© {x | f x = g x}),
    by simp [â† inter_union_distrib_left],
  rw this,
  apply measurable_set.union (h.mono (inter_subset_right _ _)).measurable_set,
  have : g â»Â¹' t âˆ© {x : Î± | f x = g x} = f â»Â¹' t âˆ© {x : Î± | f x = g x},
    by { ext x, simp {contextual := tt} },
  rw this,
  exact (hf ht).inter h.measurable_set.of_compl,
end

lemma measurable_of_fintype [fintype Î±] [measurable_singleton_class Î±] (f : Î± â†’ Î²) :
  measurable f :=
Î» s hs, (finite.of_fintype (f â»Â¹' s)).measurable_set

end measurable_functions

section constructions

variables [measurable_space Î±] [measurable_space Î²] [measurable_space Î³]

instance : measurable_space empty := âŠ¤
instance : measurable_space punit := âŠ¤ -- this also works for `unit`
instance : measurable_space bool := âŠ¤
instance : measurable_space â„• := âŠ¤
instance : measurable_space â„¤ := âŠ¤
instance : measurable_space â„š := âŠ¤

lemma measurable_to_encodable [encodable Î±] {f : Î² â†’ Î±} (h : âˆ€ y, measurable_set (f â»Â¹' {f y})) :
  measurable f :=
begin
  assume s hs,
  rw [â† bUnion_preimage_singleton],
  refine measurable_set.Union (Î» y, measurable_set.Union_Prop $ Î» hy, _),
  by_cases hyf : y âˆˆ range f,
  { rcases hyf with âŸ¨y, rflâŸ©,
    apply h },
  { simp only [preimage_singleton_eq_empty.2 hyf, measurable_set.empty] }
end

@[measurability] lemma measurable_unit (f : unit â†’ Î±) : measurable f :=
measurable_from_top

section nat

@[measurability] lemma measurable_from_nat {f : â„• â†’ Î±} : measurable f :=
measurable_from_top

lemma measurable_to_nat {f : Î± â†’ â„•} : (âˆ€ y, measurable_set (f â»Â¹' {f y})) â†’ measurable f :=
measurable_to_encodable

lemma measurable_find_greatest' {p : Î± â†’ â„• â†’ Prop}
  {N} (hN : âˆ€ k â‰¤ N, measurable_set {x | nat.find_greatest (p x) N = k}) :
  measurable (Î» x, nat.find_greatest (p x) N) :=
measurable_to_nat $ Î» x, hN _ nat.find_greatest_le

lemma measurable_find_greatest {p : Î± â†’ â„• â†’ Prop} {N} (hN : âˆ€ k â‰¤ N, measurable_set {x | p x k}) :
  measurable (Î» x, nat.find_greatest (p x) N) :=
begin
  refine measurable_find_greatest' (Î» k hk, _),
  simp only [nat.find_greatest_eq_iff, set_of_and, set_of_forall, â† compl_set_of],
  repeat { apply_rules [measurable_set.inter, measurable_set.const, measurable_set.Inter,
    measurable_set.Inter_Prop, measurable_set.compl, hN]; try { intros } }
end

lemma measurable_find {p : Î± â†’ â„• â†’ Prop} (hp : âˆ€ x, âˆƒ N, p x N)
  (hm : âˆ€ k, measurable_set {x | p x k}) :
  measurable (Î» x, nat.find (hp x)) :=
begin
  refine measurable_to_nat (Î» x, _),
  rw [preimage_find_eq_disjointed],
  exact measurable_set.disjointed hm _
end

end nat

section subtype

instance {Î±} {p : Î± â†’ Prop} [m : measurable_space Î±] : measurable_space (subtype p) :=
m.comap (coe : _ â†’ Î±)

@[measurability] lemma measurable_subtype_coe {p : Î± â†’ Prop} : measurable (coe : subtype p â†’ Î±) :=
measurable_space.le_map_comap

@[measurability] lemma measurable.subtype_coe {p : Î² â†’ Prop} {f : Î± â†’ subtype p}
  (hf : measurable f) :
  measurable (Î» a : Î±, (f a : Î²)) :=
measurable_subtype_coe.comp hf

@[measurability]
lemma measurable.subtype_mk {p : Î² â†’ Prop} {f : Î± â†’ Î²} (hf : measurable f) {h : âˆ€ x, p (f x)} :
  measurable (Î» x, (âŸ¨f x, h xâŸ© : subtype p)) :=
Î» t âŸ¨s, hsâŸ©, hs.2 â–¸ by simp only [â† preimage_comp, (âˆ˜), subtype.coe_mk, hf hs.1]

lemma measurable_set.subtype_image {s : set Î±} {t : set s}
  (hs : measurable_set s) : measurable_set t â†’ measurable_set ((coe : s â†’ Î±) '' t)
| âŸ¨u, (hu : measurable_set u), (eq : coe â»Â¹' u = t)âŸ© :=
  begin
    rw [â† eq, subtype.image_preimage_coe],
    exact hu.inter hs
  end

lemma measurable_of_measurable_union_cover
  {f : Î± â†’ Î²} (s t : set Î±) (hs : measurable_set s) (ht : measurable_set t) (h : univ âŠ† s âˆª t)
  (hc : measurable (Î» a : s, f a)) (hd : measurable (Î» a : t, f a)) :
  measurable f :=
begin
  intros u hu,
  convert (hs.subtype_image (hc hu)).union (ht.subtype_image (hd hu)),
  change f â»Â¹' u = coe '' (coe â»Â¹' (f â»Â¹' u) : set s) âˆª coe '' (coe â»Â¹' (f â»Â¹' u) : set t),
  rw [image_preimage_eq_inter_range, image_preimage_eq_inter_range, subtype.range_coe,
      subtype.range_coe, â† inter_distrib_left, univ_subset_iff.1 h, inter_univ],
end

instance {Î±} {p : Î± â†’ Prop} [measurable_space Î±] [measurable_singleton_class Î±] :
  measurable_singleton_class (subtype p) :=
{ measurable_set_singleton := Î» x,
  begin
    have : measurable_set {(x : Î±)} := measurable_set_singleton _,
    convert @measurable_subtype_coe Î± _ p _ this,
    ext y,
    simp [subtype.ext_iff],
  end }

lemma measurable_of_measurable_on_compl_finite [measurable_singleton_class Î±]
  {f : Î± â†’ Î²} (s : set Î±) (hs : finite s) (hf : measurable (set.restrict f sá¶œ)) :
  measurable f :=
begin
  letI : fintype s := finite.fintype hs,
  exact measurable_of_measurable_union_cover s sá¶œ hs.measurable_set hs.measurable_set.compl
    (by simp only [union_compl_self]) (measurable_of_fintype _) hf
end

lemma measurable_of_measurable_on_compl_singleton [measurable_singleton_class Î±]
  {f : Î± â†’ Î²} (a : Î±) (hf : measurable (set.restrict f {x | x â‰  a})) :
  measurable f :=
measurable_of_measurable_on_compl_finite {a} (finite_singleton a) hf

end subtype

section prod

instance {Î± Î²} [mâ‚ : measurable_space Î±] [mâ‚‚ : measurable_space Î²] : measurable_space (Î± Ã— Î²) :=
mâ‚.comap prod.fst âŠ” mâ‚‚.comap prod.snd

@[measurability] lemma measurable_fst : measurable (prod.fst : Î± Ã— Î² â†’ Î±) :=
measurable.of_comap_le le_sup_left

lemma measurable.fst {f : Î± â†’ Î² Ã— Î³} (hf : measurable f) :
  measurable (Î» a : Î±, (f a).1) :=
measurable_fst.comp hf

@[measurability] lemma measurable_snd : measurable (prod.snd : Î± Ã— Î² â†’ Î²) :=
measurable.of_comap_le le_sup_right

lemma measurable.snd {f : Î± â†’ Î² Ã— Î³} (hf : measurable f) :
  measurable (Î» a : Î±, (f a).2) :=
measurable_snd.comp hf

@[measurability] lemma measurable.prod {f : Î± â†’ Î² Ã— Î³}
  (hfâ‚ : measurable (Î» a, (f a).1)) (hfâ‚‚ : measurable (Î» a, (f a).2)) : measurable f :=
measurable.of_le_map $ sup_le
  (by { rw [measurable_space.comap_le_iff_le_map, measurable_space.map_comp], exact hfâ‚ })
  (by { rw [measurable_space.comap_le_iff_le_map, measurable_space.map_comp], exact hfâ‚‚ })

lemma measurable_prod {f : Î± â†’ Î² Ã— Î³} : measurable f â†”
  measurable (Î» a, (f a).1) âˆ§ measurable (Î» a, (f a).2) :=
âŸ¨Î» hf, âŸ¨measurable_fst.comp hf, measurable_snd.comp hfâŸ©, Î» h, measurable.prod h.1 h.2âŸ©

lemma measurable.prod_mk {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : measurable f) (hg : measurable g) :
  measurable (Î» a : Î±, (f a, g a)) :=
measurable.prod hf hg

lemma measurable_prod_mk_left {x : Î±} : measurable (@prod.mk _ Î² x) :=
measurable_const.prod_mk measurable_id

lemma measurable_prod_mk_right {y : Î²} : measurable (Î» x : Î±, (x, y)) :=
measurable_id.prod_mk measurable_const

lemma measurable.prod_map [measurable_space Î´] {f : Î± â†’ Î²} {g : Î³ â†’ Î´} (hf : measurable f)
  (hg : measurable g) : measurable (prod.map f g) :=
(hf.comp measurable_fst).prod_mk (hg.comp measurable_snd)

lemma measurable.of_uncurry_left {f : Î± â†’ Î² â†’ Î³} (hf : measurable (uncurry f)) {x : Î±} :
  measurable (f x) :=
hf.comp measurable_prod_mk_left

lemma measurable.of_uncurry_right {f : Î± â†’ Î² â†’ Î³} (hf : measurable (uncurry f)) {y : Î²} :
  measurable (Î» x, f x y) :=
hf.comp measurable_prod_mk_right

@[measurability] lemma measurable_swap : measurable (prod.swap : Î± Ã— Î² â†’ Î² Ã— Î±) :=
measurable.prod measurable_snd measurable_fst

lemma measurable_swap_iff {f : Î± Ã— Î² â†’ Î³} : measurable (f âˆ˜ prod.swap) â†” measurable f :=
âŸ¨Î» hf, by { convert hf.comp measurable_swap, ext âŸ¨x, yâŸ©, refl }, Î» hf, hf.comp measurable_swapâŸ©

@[measurability]
lemma measurable_set.prod {s : set Î±} {t : set Î²} (hs : measurable_set s) (ht : measurable_set t) :
  measurable_set (s.prod t) :=
measurable_set.inter (measurable_fst hs) (measurable_snd ht)

lemma measurable_set_prod_of_nonempty {s : set Î±} {t : set Î²} (h : (s.prod t).nonempty) :
  measurable_set (s.prod t) â†” measurable_set s âˆ§ measurable_set t :=
begin
  rcases h with âŸ¨âŸ¨x, yâŸ©, hx, hyâŸ©,
  refine âŸ¨Î» hst, _, Î» h, h.1.prod h.2âŸ©,
  have : measurable_set ((Î» x, (x, y)) â»Â¹' s.prod t) := measurable_id.prod_mk measurable_const hst,
  have : measurable_set (prod.mk x â»Â¹' s.prod t) := measurable_const.prod_mk measurable_id hst,
  simp * at *
end

lemma measurable_set_prod {s : set Î±} {t : set Î²} :
  measurable_set (s.prod t) â†” (measurable_set s âˆ§ measurable_set t) âˆ¨ s = âˆ… âˆ¨ t = âˆ… :=
begin
  cases (s.prod t).eq_empty_or_nonempty with h h,
  { simp [h, prod_eq_empty_iff.mp h] },
  { simp [â†not_nonempty_iff_eq_empty, prod_nonempty_iff.mp h, measurable_set_prod_of_nonempty h] }
end

lemma measurable_set_swap_iff {s : set (Î± Ã— Î²)} :
  measurable_set (prod.swap â»Â¹' s) â†” measurable_set s :=
âŸ¨Î» hs, by { convert measurable_swap hs, ext âŸ¨x, yâŸ©, refl }, Î» hs, measurable_swap hsâŸ©

lemma measurable_from_prod_encodable [encodable Î²] [measurable_singleton_class Î²]
  {f : Î± Ã— Î² â†’ Î³} (hf : âˆ€ y, measurable (Î» x, f (x, y))) :
  measurable f :=
begin
  intros s hs,
  have : f â»Â¹' s = â‹ƒ y, ((Î» x, f (x, y)) â»Â¹' s).prod {y},
  { ext1 âŸ¨x, yâŸ©,
    simp [and_assoc, and.left_comm] },
  rw this,
  exact measurable_set.Union (Î» y, (hf y hs).prod (measurable_set_singleton y))
end

end prod

section pi

variables {Ï€ : Î´ â†’ Type*}

instance measurable_space.pi [m : Î  a, measurable_space (Ï€ a)] : measurable_space (Î  a, Ï€ a) :=
â¨† a, (m a).comap (Î» b, b a)

variables [Î  a, measurable_space (Ï€ a)] [measurable_space Î³]

lemma measurable_pi_iff {g : Î± â†’ Î  a, Ï€ a} :
  measurable g â†” âˆ€ a, measurable (Î» x, g x a) :=
by simp_rw [measurable_iff_comap_le, measurable_space.pi, measurable_space.comap_supr,
    measurable_space.comap_comp, function.comp, supr_le_iff]

@[measurability]
lemma measurable_pi_apply (a : Î´) : measurable (Î» f : Î  a, Ï€ a, f a) :=
measurable.of_comap_le $ le_supr _ a

@[measurability]
lemma measurable.eval {a : Î´} {g : Î± â†’ Î  a, Ï€ a}
  (hg : measurable g) : measurable (Î» x, g x a) :=
(measurable_pi_apply a).comp hg

@[measurability]
lemma measurable_pi_lambda (f : Î± â†’ Î  a, Ï€ a) (hf : âˆ€ a, measurable (Î» c, f c a)) :
  measurable f :=
measurable_pi_iff.mpr hf

/-- The function `update f a : Ï€ a â†’ Î  a, Ï€ a` is always measurable.
  This doesn't require `f` to be measurable.
  This should not be confused with the statement that `update f a x` is measurable. -/
@[measurability]
lemma measurable_update (f : Î  (a : Î´), Ï€ a) {a : Î´} : measurable (update f a) :=
begin
  apply measurable_pi_lambda,
  intro x, by_cases hx : x = a,
  { cases hx, convert measurable_id, ext, simp },
  simp_rw [update_noteq hx], apply measurable_const,
end

/- Even though we cannot use projection notation, we still keep a dot to be consistent with similar
  lemmas, like `measurable_set.prod`. -/
@[measurability]
lemma measurable_set.pi {s : set Î´} {t : Î  i : Î´, set (Ï€ i)} (hs : countable s)
  (ht : âˆ€ i âˆˆ s, measurable_set (t i)) :
  measurable_set (s.pi t) :=
by { rw [pi_def], exact measurable_set.bInter hs (Î» i hi, measurable_pi_apply _ (ht i hi)) }

lemma measurable_set.univ_pi [encodable Î´] {t : Î  i : Î´, set (Ï€ i)}
  (ht : âˆ€ i, measurable_set (t i)) : measurable_set (pi univ t) :=
measurable_set.pi (countable_encodable _) (Î» i _, ht i)

lemma measurable_set_pi_of_nonempty {s : set Î´} {t : Î  i, set (Ï€ i)} (hs : countable s)
  (h : (pi s t).nonempty) : measurable_set (pi s t) â†” âˆ€ i âˆˆ s, measurable_set (t i) :=
begin
  rcases h with âŸ¨f, hfâŸ©, refine âŸ¨Î» hst i hi, _, measurable_set.pi hsâŸ©,
  convert measurable_update f hst, rw [update_preimage_pi hi], exact Î» j hj _, hf j hj
end

lemma measurable_set_pi {s : set Î´} {t : Î  i, set (Ï€ i)} (hs : countable s) :
  measurable_set (pi s t) â†” (âˆ€ i âˆˆ s, measurable_set (t i)) âˆ¨ pi s t = âˆ… :=
begin
  cases (pi s t).eq_empty_or_nonempty with h h,
  { simp [h] },
  { simp [measurable_set_pi_of_nonempty hs, h, â† not_nonempty_iff_eq_empty] }
end

section
variable (Ï€)

@[measurability]
lemma measurable_pi_equiv_pi_subtype_prod_symm (p : Î´ â†’ Prop) [decidable_pred p] :
  measurable (equiv.pi_equiv_pi_subtype_prod p Ï€).symm :=
begin
  apply measurable_pi_iff.2 (Î» j, _),
  by_cases hj : p j,
  { simp only [hj, dif_pos, equiv.pi_equiv_pi_subtype_prod_symm_apply],
    have : measurable (Î» (f : (Î  (i : {x // p x}), Ï€ â†‘i)), f âŸ¨j, hjâŸ©) :=
      measurable_pi_apply âŸ¨j, hjâŸ©,
    exact measurable.comp this measurable_fst },
  { simp only [hj, equiv.pi_equiv_pi_subtype_prod_symm_apply, dif_neg, not_false_iff],
    have : measurable (Î» (f : (Î  (i : {x // Â¬ p x}), Ï€ â†‘i)), f âŸ¨j, hjâŸ©) :=
      measurable_pi_apply âŸ¨j, hjâŸ©,
    exact measurable.comp this measurable_snd }
end

@[measurability]
lemma measurable_pi_equiv_pi_subtype_prod (p : Î´ â†’ Prop) [decidable_pred p] :
  measurable (equiv.pi_equiv_pi_subtype_prod p Ï€) :=
begin
  refine measurable_prod.2 _,
  split;
  { apply measurable_pi_iff.2 (Î» j, _),
    simp only [pi_equiv_pi_subtype_prod_apply, measurable_pi_apply] }
end

end

section fintype

local attribute [instance] fintype.encodable

lemma measurable_set.pi_fintype [fintype Î´] {s : set Î´} {t : Î  i, set (Ï€ i)}
  (ht : âˆ€ i âˆˆ s, measurable_set (t i)) : measurable_set (pi s t) :=
measurable_set.pi (countable_encodable _) ht

lemma measurable_set.univ_pi_fintype [fintype Î´] {t : Î  i, set (Ï€ i)}
  (ht : âˆ€ i, measurable_set (t i)) : measurable_set (pi univ t) :=
measurable_set.pi_fintype (Î» i _, ht i)

end fintype
end pi

instance tprod.measurable_space (Ï€ : Î´ â†’ Type*) [âˆ€ x, measurable_space (Ï€ x)] :
  âˆ€ (l : list Î´), measurable_space (list.tprod Ï€ l)
| []        := punit.measurable_space
| (i :: is) := @prod.measurable_space _ _ _ (tprod.measurable_space is)

section tprod

open list

variables {Ï€ : Î´ â†’ Type*} [âˆ€ x, measurable_space (Ï€ x)]

lemma measurable_tprod_mk (l : list Î´) : measurable (@tprod.mk Î´ Ï€ l) :=
begin
  induction l with i l ih,
  { exact measurable_const },
  { exact (measurable_pi_apply i).prod_mk ih }
end

lemma measurable_tprod_elim : âˆ€ {l : list Î´} {i : Î´} (hi : i âˆˆ l),
  measurable (Î» (v : tprod Ï€ l), v.elim hi)
| (i :: is) j hj := begin
  by_cases hji : j = i,
  { subst hji, simp [measurable_fst] },
  { rw [funext $ tprod.elim_of_ne _ hji],
    exact (measurable_tprod_elim (hj.resolve_left hji)).comp measurable_snd }
end

lemma measurable_tprod_elim' {l : list Î´} (h : âˆ€ i, i âˆˆ l) :
  measurable (tprod.elim' h : tprod Ï€ l â†’ Î  i, Ï€ i) :=
measurable_pi_lambda _ (Î» i, measurable_tprod_elim (h i))

lemma measurable_set.tprod (l : list Î´) {s : âˆ€ i, set (Ï€ i)} (hs : âˆ€ i, measurable_set (s i)) :
  measurable_set (set.tprod l s) :=
by { induction l with i l ih, exact measurable_set.univ, exact (hs i).prod ih }

end tprod

instance {Î± Î²} [mâ‚ : measurable_space Î±] [mâ‚‚ : measurable_space Î²] : measurable_space (Î± âŠ• Î²) :=
mâ‚.map sum.inl âŠ“ mâ‚‚.map sum.inr

section sum

@[measurability] lemma measurable_inl : measurable (@sum.inl Î± Î²) :=
measurable.of_le_map inf_le_left

@[measurability] lemma measurable_inr : measurable (@sum.inr Î± Î²) :=
measurable.of_le_map inf_le_right

lemma measurable_sum {f : Î± âŠ• Î² â†’ Î³}
  (hl : measurable (f âˆ˜ sum.inl)) (hr : measurable (f âˆ˜ sum.inr)) : measurable f :=
measurable.of_comap_le $ le_inf
  (measurable_space.comap_le_iff_le_map.2 $ hl)
  (measurable_space.comap_le_iff_le_map.2 $ hr)

@[measurability]
lemma measurable.sum_elim {f : Î± â†’ Î³} {g : Î² â†’ Î³} (hf : measurable f) (hg : measurable g) :
  measurable (sum.elim f g) :=
measurable_sum hf hg

lemma measurable_set.inl_image {s : set Î±} (hs : measurable_set s) :
  measurable_set (sum.inl '' s : set (Î± âŠ• Î²)) :=
âŸ¨show measurable_set (sum.inl â»Â¹' _), by { rwa [preimage_image_eq], exact (Î» a b, sum.inl.inj) },
  have sum.inr â»Â¹' (sum.inl '' s : set (Î± âŠ• Î²)) = âˆ… :=
    eq_empty_of_subset_empty $ assume x âŸ¨y, hy, eqâŸ©, by contradiction,
  show measurable_set (sum.inr â»Â¹' _), by { rw [this], exact measurable_set.empty }âŸ©

lemma measurable_set_range_inl : measurable_set (range sum.inl : set (Î± âŠ• Î²)) :=
by { rw [â† image_univ], exact measurable_set.univ.inl_image }

lemma measurable_set_inr_image {s : set Î²} (hs : measurable_set s) :
  measurable_set (sum.inr '' s : set (Î± âŠ• Î²)) :=
âŸ¨ have sum.inl â»Â¹' (sum.inr '' s : set (Î± âŠ• Î²)) = âˆ… :=
    eq_empty_of_subset_empty $ assume x âŸ¨y, hy, eqâŸ©, by contradiction,
  show measurable_set (sum.inl â»Â¹' _), by { rw [this], exact measurable_set.empty },
  show measurable_set (sum.inr â»Â¹' _), by { rwa [preimage_image_eq], exact Î» a b, sum.inr.inj }âŸ©

lemma measurable_set_range_inr : measurable_set (range sum.inr : set (Î± âŠ• Î²)) :=
by { rw [â† image_univ], exact measurable_set_inr_image measurable_set.univ }

end sum

instance {Î±} {Î² : Î± â†’ Type*} [m : Î a, measurable_space (Î² a)] : measurable_space (sigma Î²) :=
â¨…a, (m a).map (sigma.mk a)

end constructions

/-- Equivalences between measurable spaces. Main application is the simplification of measurability
statements along measurable equivalences. -/
structure measurable_equiv (Î± Î² : Type*) [measurable_space Î±] [measurable_space Î²] extends Î± â‰ƒ Î² :=
(measurable_to_fun : measurable to_equiv)
(measurable_inv_fun : measurable to_equiv.symm)

infix ` â‰ƒáµ `:25 := measurable_equiv

namespace measurable_equiv

variables (Î± Î²) [measurable_space Î±] [measurable_space Î²] [measurable_space Î³] [measurable_space Î´]

instance : has_coe_to_fun (Î± â‰ƒáµ Î²) :=
âŸ¨Î» _, Î± â†’ Î², Î» e, e.to_equivâŸ©

variables {Î± Î²}

@[simp] lemma coe_to_equiv (e : Î± â‰ƒáµ Î²) : (e.to_equiv : Î± â†’ Î²) = e := rfl

@[measurability]
protected lemma measurable (e : Î± â‰ƒáµ Î²) : measurable (e : Î± â†’ Î²) :=
e.measurable_to_fun

@[simp] lemma coe_mk (e : Î± â‰ƒ Î²) (h1 : measurable e) (h2 : measurable e.symm) :
  ((âŸ¨e, h1, h2âŸ© : Î± â‰ƒáµ Î²) : Î± â†’ Î²) = e := rfl

/-- Any measurable space is equivalent to itself. -/
def refl (Î± : Type*) [measurable_space Î±] : Î± â‰ƒáµ Î± :=
{ to_equiv := equiv.refl Î±,
  measurable_to_fun := measurable_id, measurable_inv_fun := measurable_id }

instance : inhabited (Î± â‰ƒáµ Î±) := âŸ¨refl Î±âŸ©

/-- The composition of equivalences between measurable spaces. -/
def trans (ab : Î± â‰ƒáµ Î²) (bc : Î² â‰ƒáµ Î³) :
  Î± â‰ƒáµ Î³ :=
{ to_equiv := ab.to_equiv.trans bc.to_equiv,
  measurable_to_fun := bc.measurable_to_fun.comp ab.measurable_to_fun,
  measurable_inv_fun := ab.measurable_inv_fun.comp bc.measurable_inv_fun }

/-- The inverse of an equivalence between measurable spaces. -/
def symm (ab : Î± â‰ƒáµ Î²) : Î² â‰ƒáµ Î± :=
{ to_equiv := ab.to_equiv.symm,
  measurable_to_fun := ab.measurable_inv_fun,
  measurable_inv_fun := ab.measurable_to_fun }

@[simp] lemma coe_to_equiv_symm (e : Î± â‰ƒáµ Î²) : (e.to_equiv.symm : Î² â†’ Î±) = e.symm := rfl

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def simps.apply (h : Î± â‰ƒáµ Î²) : Î± â†’ Î² := h
/-- See Note [custom simps projection] -/
def simps.symm_apply (h : Î± â‰ƒáµ Î²) : Î² â†’ Î± := h.symm

initialize_simps_projections measurable_equiv
  (to_equiv_to_fun â†’ apply, to_equiv_inv_fun â†’ symm_apply)

lemma to_equiv_injective : injective (to_equiv : (Î± â‰ƒáµ Î²) â†’ (Î± â‰ƒ Î²)) :=
by { rintro âŸ¨eâ‚, _, _âŸ© âŸ¨eâ‚‚, _, _âŸ© (rfl : eâ‚ = eâ‚‚), refl }

@[ext] lemma ext {eâ‚ eâ‚‚ : Î± â‰ƒáµ Î²} (h : (eâ‚ : Î± â†’ Î²) = eâ‚‚) : eâ‚ = eâ‚‚ :=
to_equiv_injective $ equiv.coe_fn_injective h

@[simp] lemma symm_mk (e : Î± â‰ƒ Î²) (h1 : measurable e) (h2 : measurable e.symm) :
  (âŸ¨e, h1, h2âŸ© : Î± â‰ƒáµ Î²).symm = âŸ¨e.symm, h2, h1âŸ© := rfl

attribute [simps apply to_equiv] trans refl

@[simp] lemma symm_refl (Î± : Type*) [measurable_space Î±] : (refl Î±).symm = refl Î± := rfl

@[simp] theorem symm_comp_self (e : Î± â‰ƒáµ Î²) : e.symm âˆ˜ e = id := funext e.left_inv

@[simp] theorem self_comp_symm (e : Î± â‰ƒáµ Î²) : e âˆ˜ e.symm = id := funext e.right_inv

@[simp] theorem apply_symm_apply (e : Î± â‰ƒáµ Î²) (y : Î²) : e (e.symm y) = y := e.right_inv y

@[simp] theorem symm_apply_apply (e : Î± â‰ƒáµ Î²) (x : Î±) : e.symm (e x) = x := e.left_inv x

@[simp] theorem symm_trans_self (e : Î± â‰ƒáµ Î²) : e.symm.trans e = refl Î² :=
ext e.self_comp_symm

@[simp] theorem self_trans_symm (e : Î± â‰ƒáµ Î²) : e.trans e.symm = refl Î± :=
ext e.symm_comp_self

protected theorem surjective (e : Î± â‰ƒáµ Î²) : surjective e := e.to_equiv.surjective
protected theorem bijective (e : Î± â‰ƒáµ Î²) : bijective e := e.to_equiv.bijective
protected theorem injective (e : Î± â‰ƒáµ Î²) : injective e := e.to_equiv.injective

/-- Equal measurable spaces are equivalent. -/
protected def cast {Î± Î²} [iâ‚ : measurable_space Î±] [iâ‚‚ : measurable_space Î²]
  (h : Î± = Î²) (hi : iâ‚ == iâ‚‚) : Î± â‰ƒáµ Î² :=
{ to_equiv := equiv.cast h,
  measurable_to_fun  := by { substI h, substI hi, exact measurable_id },
  measurable_inv_fun := by { substI h, substI hi, exact measurable_id }}

protected lemma measurable_coe_iff {f : Î² â†’ Î³} (e : Î± â‰ƒáµ Î²) :
  measurable (f âˆ˜ e) â†” measurable f :=
iff.intro
  (assume hfe,
    have measurable (f âˆ˜ (e.symm.trans e).to_equiv) := hfe.comp e.symm.measurable,
    by rwa [coe_to_equiv, symm_trans_self] at this)
  (Î» h, h.comp e.measurable)

/-- Products of equivalent measurable spaces are equivalent. -/
def prod_congr (ab : Î± â‰ƒáµ Î²) (cd : Î³ â‰ƒáµ Î´) : Î± Ã— Î³ â‰ƒáµ Î² Ã— Î´ :=
{ to_equiv := prod_congr ab.to_equiv cd.to_equiv,
  measurable_to_fun := (ab.measurable_to_fun.comp measurable_id.fst).prod_mk
    (cd.measurable_to_fun.comp measurable_id.snd),
  measurable_inv_fun := (ab.measurable_inv_fun.comp measurable_id.fst).prod_mk
    (cd.measurable_inv_fun.comp measurable_id.snd) }

/-- Products of measurable spaces are symmetric. -/
def prod_comm : Î± Ã— Î² â‰ƒáµ Î² Ã— Î± :=
{ to_equiv := prod_comm Î± Î²,
  measurable_to_fun  := measurable_id.snd.prod_mk measurable_id.fst,
  measurable_inv_fun := measurable_id.snd.prod_mk measurable_id.fst }

/-- Products of measurable spaces are associative. -/
def prod_assoc : (Î± Ã— Î²) Ã— Î³ â‰ƒáµ Î± Ã— (Î² Ã— Î³) :=
{ to_equiv := prod_assoc Î± Î² Î³,
  measurable_to_fun  := measurable_fst.fst.prod_mk $ measurable_fst.snd.prod_mk measurable_snd,
  measurable_inv_fun := (measurable_fst.prod_mk measurable_snd.fst).prod_mk measurable_snd.snd }

/-- Sums of measurable spaces are symmetric. -/
def sum_congr (ab : Î± â‰ƒáµ Î²) (cd : Î³ â‰ƒáµ Î´) : Î± âŠ• Î³ â‰ƒáµ Î² âŠ• Î´ :=
{ to_equiv := sum_congr ab.to_equiv cd.to_equiv,
  measurable_to_fun :=
    begin
      cases ab with ab' abm, cases ab', cases cd with cd' cdm, cases cd',
      refine measurable_sum (measurable_inl.comp abm) (measurable_inr.comp cdm)
    end,
  measurable_inv_fun :=
    begin
      cases ab with ab' _ abm, cases ab', cases cd with cd' _ cdm, cases cd',
      refine measurable_sum (measurable_inl.comp abm) (measurable_inr.comp cdm)
    end }

/-- `set.prod s t â‰ƒ (s Ã— t)` as measurable spaces. -/
def set.prod (s : set Î±) (t : set Î²) : s.prod t â‰ƒáµ s Ã— t :=
{ to_equiv := equiv.set.prod s t,
  measurable_to_fun := measurable_id.subtype_coe.fst.subtype_mk.prod_mk
    measurable_id.subtype_coe.snd.subtype_mk,
  measurable_inv_fun := measurable.subtype_mk $ measurable_id.fst.subtype_coe.prod_mk
    measurable_id.snd.subtype_coe }

/-- `univ Î± â‰ƒ Î±` as measurable spaces. -/
def set.univ (Î± : Type*) [measurable_space Î±] : (univ : set Î±) â‰ƒáµ Î± :=
{ to_equiv := equiv.set.univ Î±,
  measurable_to_fun := measurable_id.subtype_coe,
  measurable_inv_fun := measurable_id.subtype_mk }

/-- `{a} â‰ƒ unit` as measurable spaces. -/
def set.singleton (a : Î±) : ({a} : set Î±) â‰ƒáµ unit :=
{ to_equiv := equiv.set.singleton a,
  measurable_to_fun := measurable_const,
  measurable_inv_fun := measurable_const }

/-- A set is equivalent to its image under a function `f` as measurable spaces,
  if `f` is an injective measurable function that sends measurable sets to measurable sets. -/
noncomputable def set.image (f : Î± â†’ Î²) (s : set Î±) (hf : injective f)
  (hfm : measurable f) (hfi : âˆ€ s, measurable_set s â†’ measurable_set (f '' s)) : s â‰ƒáµ (f '' s) :=
{ to_equiv := equiv.set.image f s hf,
  measurable_to_fun  := (hfm.comp measurable_id.subtype_coe).subtype_mk,
  measurable_inv_fun :=
    begin
      rintro t âŸ¨u, hu, rflâŸ©, simp [preimage_preimage, set.image_symm_preimage hf],
      exact measurable_subtype_coe (hfi u hu)
    end }

/-- The domain of `f` is equivalent to its range as measurable spaces,
  if `f` is an injective measurable function that sends measurable sets to measurable sets. -/
noncomputable def set.range (f : Î± â†’ Î²) (hf : injective f) (hfm : measurable f)
  (hfi : âˆ€ s, measurable_set s â†’ measurable_set (f '' s)) :
  Î± â‰ƒáµ (range f) :=
(measurable_equiv.set.univ _).symm.trans $
  (measurable_equiv.set.image f univ hf hfm hfi).trans $
  measurable_equiv.cast (by rw image_univ) (by rw image_univ)

/-- `Î±` is equivalent to its image in `Î± âŠ• Î²` as measurable spaces. -/
def set.range_inl : (range sum.inl : set (Î± âŠ• Î²)) â‰ƒáµ Î± :=
{ to_fun    := Î» ab, match ab with
    | âŸ¨sum.inl a, _âŸ© := a
    | âŸ¨sum.inr b, pâŸ© := have false, by { cases p, contradiction }, this.elim
    end,
  inv_fun   := Î» a, âŸ¨sum.inl a, a, rflâŸ©,
  left_inv  := by { rintro âŸ¨ab, a, rflâŸ©, refl },
  right_inv := assume a, rfl,
  measurable_to_fun  := assume s (hs : measurable_set s),
    begin
      refine âŸ¨_, hs.inl_image, set.ext _âŸ©,
      rintros âŸ¨ab, a, rflâŸ©,
      simp [set.range_inl._match_1]
    end,
  measurable_inv_fun := measurable.subtype_mk measurable_inl }

/-- `Î²` is equivalent to its image in `Î± âŠ• Î²` as measurable spaces. -/
def set.range_inr : (range sum.inr : set (Î± âŠ• Î²)) â‰ƒáµ Î² :=
{ to_fun    := Î» ab, match ab with
    | âŸ¨sum.inr b, _âŸ© := b
    | âŸ¨sum.inl a, pâŸ© := have false, by { cases p, contradiction }, this.elim
    end,
  inv_fun   := Î» b, âŸ¨sum.inr b, b, rflâŸ©,
  left_inv  := by { rintro âŸ¨ab, b, rflâŸ©, refl },
  right_inv := assume b, rfl,
  measurable_to_fun  := assume s (hs : measurable_set s),
    begin
      refine âŸ¨_, measurable_set_inr_image hs, set.ext _âŸ©,
      rintros âŸ¨ab, b, rflâŸ©,
      simp [set.range_inr._match_1]
    end,
  measurable_inv_fun := measurable.subtype_mk measurable_inr }

/-- Products distribute over sums (on the right) as measurable spaces. -/
def sum_prod_distrib (Î± Î² Î³) [measurable_space Î±] [measurable_space Î²] [measurable_space Î³] :
  (Î± âŠ• Î²) Ã— Î³ â‰ƒáµ (Î± Ã— Î³) âŠ• (Î² Ã— Î³) :=
{ to_equiv := sum_prod_distrib Î± Î² Î³,
  measurable_to_fun  :=
  begin
    refine measurable_of_measurable_union_cover
      ((range sum.inl).prod univ)
      ((range sum.inr).prod univ)
      (measurable_set_range_inl.prod measurable_set.univ)
      (measurable_set_range_inr.prod measurable_set.univ)
      (by { rintro âŸ¨a|b, câŸ©; simp [set.prod_eq] })
      _
      _,
    { refine (set.prod (range sum.inl) univ).symm.measurable_coe_iff.1 _,
      refine (prod_congr set.range_inl (set.univ _)).symm.measurable_coe_iff.1 _,
      dsimp [(âˆ˜)],
      convert measurable_inl,
      ext âŸ¨a, câŸ©, refl },
    { refine (set.prod (range sum.inr) univ).symm.measurable_coe_iff.1 _,
      refine (prod_congr set.range_inr (set.univ _)).symm.measurable_coe_iff.1 _,
      dsimp [(âˆ˜)],
      convert measurable_inr,
      ext âŸ¨b, câŸ©, refl }
  end,
  measurable_inv_fun :=
    measurable_sum
      ((measurable_inl.comp measurable_fst).prod_mk measurable_snd)
      ((measurable_inr.comp measurable_fst).prod_mk measurable_snd) }

/-- Products distribute over sums (on the left) as measurable spaces. -/
def prod_sum_distrib (Î± Î² Î³) [measurable_space Î±] [measurable_space Î²] [measurable_space Î³] :
  Î± Ã— (Î² âŠ• Î³) â‰ƒáµ (Î± Ã— Î²) âŠ• (Î± Ã— Î³) :=
prod_comm.trans $ (sum_prod_distrib _ _ _).trans $ sum_congr prod_comm prod_comm

/-- Products distribute over sums as measurable spaces. -/
def sum_prod_sum (Î± Î² Î³ Î´)
  [measurable_space Î±] [measurable_space Î²] [measurable_space Î³] [measurable_space Î´] :
  (Î± âŠ• Î²) Ã— (Î³ âŠ• Î´) â‰ƒáµ ((Î± Ã— Î³) âŠ• (Î± Ã— Î´)) âŠ• ((Î² Ã— Î³) âŠ• (Î² Ã— Î´)) :=
(sum_prod_distrib _ _ _).trans $ sum_congr (prod_sum_distrib _ _ _) (prod_sum_distrib _ _ _)

variables {Ï€ Ï€' : Î´' â†’ Type*} [âˆ€ x, measurable_space (Ï€ x)] [âˆ€ x, measurable_space (Ï€' x)]

/-- A family of measurable equivalences `Î  a, Î²â‚ a â‰ƒáµ Î²â‚‚ a` generates a measurable equivalence
  between  `Î  a, Î²â‚ a` and `Î  a, Î²â‚‚ a`. -/
def Pi_congr_right (e : Î  a, Ï€ a â‰ƒáµ Ï€' a) : (Î  a, Ï€ a) â‰ƒáµ (Î  a, Ï€' a) :=
{ to_equiv := Pi_congr_right (Î» a, (e a).to_equiv),
  measurable_to_fun :=
    measurable_pi_lambda _ (Î» i, (e i).measurable_to_fun.comp (measurable_pi_apply i)),
  measurable_inv_fun :=
    measurable_pi_lambda _ (Î» i, (e i).measurable_inv_fun.comp (measurable_pi_apply i)) }

/-- Pi-types are measurably equivalent to iterated products. -/
@[simps {fully_applied := ff}]
noncomputable def pi_measurable_equiv_tprod {l : list Î´'} (hnd : l.nodup) (h : âˆ€ i, i âˆˆ l) :
  (Î  i, Ï€ i) â‰ƒáµ list.tprod Ï€ l :=
{ to_equiv := list.tprod.pi_equiv_tprod hnd h,
  measurable_to_fun := measurable_tprod_mk l,
  measurable_inv_fun := measurable_tprod_elim' h }

/-- If `Î±` has a unique term, then the type of function `Î± â†’ Î²` is measurably equivalent to `Î²`. -/
@[simps {fully_applied := ff}] def fun_unique (Î± Î² : Type*) [unique Î±] [measurable_space Î²] :
  (Î± â†’ Î²) â‰ƒáµ Î² :=
{ to_equiv := equiv.fun_unique Î± Î²,
  measurable_to_fun := measurable_pi_apply _,
  measurable_inv_fun := measurable_pi_iff.2 $ Î» b, measurable_id }

/-- The space `Î  i : fin 2, Î± i` is measurably equivalent to `Î± 0 Ã— Î± 1`. -/
@[simps {fully_applied := ff}] def pi_fin_two (Î± : fin 2 â†’ Type*) [âˆ€ i, measurable_space (Î± i)] :
  (Î  i, Î± i) â‰ƒáµ Î± 0 Ã— Î± 1 :=
{ to_equiv := pi_fin_two_equiv Î±,
  measurable_to_fun := measurable.prod (measurable_pi_apply _) (measurable_pi_apply _),
  measurable_inv_fun := measurable_pi_iff.2 $
    fin.forall_fin_two.2 âŸ¨measurable_fst, measurable_sndâŸ© }

/-- The space `fin 2 â†’ Î±` is measurably equivalent to `Î± Ã— Î±`. -/
@[simps {fully_applied := ff}] def fin_two_arrow : (fin 2 â†’ Î±) â‰ƒáµ Î± Ã— Î± := pi_fin_two (Î» _, Î±)

end measurable_equiv

namespace filter

variables [measurable_space Î±]

/-- A filter `f` is measurably generates if each `s âˆˆ f` includes a measurable `t âˆˆ f`. -/
class is_measurably_generated (f : filter Î±) : Prop :=
(exists_measurable_subset : âˆ€ â¦ƒsâ¦„, s âˆˆ f â†’ âˆƒ t âˆˆ f, measurable_set t âˆ§ t âŠ† s)

instance is_measurably_generated_bot : is_measurably_generated (âŠ¥ : filter Î±) :=
âŸ¨Î» _ _, âŸ¨âˆ…, mem_bot, measurable_set.empty, empty_subset _âŸ©âŸ©

instance is_measurably_generated_top : is_measurably_generated (âŠ¤ : filter Î±) :=
âŸ¨Î» s hs, âŸ¨univ, univ_mem, measurable_set.univ, Î» x _, hs xâŸ©âŸ©

lemma eventually.exists_measurable_mem {f : filter Î±} [is_measurably_generated f]
  {p : Î± â†’ Prop} (h : âˆ€á¶  x in f, p x) :
  âˆƒ s âˆˆ f, measurable_set s âˆ§ âˆ€ x âˆˆ s, p x :=
is_measurably_generated.exists_measurable_subset h

lemma eventually.exists_measurable_mem_of_lift' {f : filter Î±} [is_measurably_generated f]
  {p : set Î± â†’ Prop} (h : âˆ€á¶  s in f.lift' powerset, p s) :
  âˆƒ s âˆˆ f, measurable_set s âˆ§ p s :=
let âŸ¨s, hsf, hsâŸ© := eventually_lift'_powerset.1 h,
  âŸ¨t, htf, htm, htsâŸ© := is_measurably_generated.exists_measurable_subset hsf
in âŸ¨t, htf, htm, hs t htsâŸ©

instance inf_is_measurably_generated (f g : filter Î±) [is_measurably_generated f]
  [is_measurably_generated g] :
  is_measurably_generated (f âŠ“ g) :=
begin
  refine âŸ¨_âŸ©,
  rintros t âŸ¨sf, hsf, sg, hsg, rflâŸ©,
  rcases is_measurably_generated.exists_measurable_subset hsf with âŸ¨s'f, hs'f, hmf, hs'sfâŸ©,
  rcases is_measurably_generated.exists_measurable_subset hsg with âŸ¨s'g, hs'g, hmg, hs'sgâŸ©,
  refine âŸ¨s'f âˆ© s'g, inter_mem_inf hs'f hs'g, hmf.inter hmg, _âŸ©,
  exact inter_subset_inter hs'sf hs'sg
end

lemma principal_is_measurably_generated_iff {s : set Î±} :
  is_measurably_generated (ğ“Ÿ s) â†” measurable_set s :=
begin
  refine âŸ¨_, Î» hs, âŸ¨Î» t ht, âŸ¨s, mem_principal_self s, hs, htâŸ©âŸ©âŸ©,
  rintros âŸ¨hsâŸ©,
  rcases hs (mem_principal_self s) with âŸ¨t, ht, htm, htsâŸ©,
  have : t = s := subset.antisymm hts ht,
  rwa â† this
end

alias principal_is_measurably_generated_iff â†”
  _ measurable_set.principal_is_measurably_generated

instance infi_is_measurably_generated {f : Î¹ â†’ filter Î±} [âˆ€ i, is_measurably_generated (f i)] :
  is_measurably_generated (â¨… i, f i) :=
begin
  refine âŸ¨Î» s hs, _âŸ©,
  rw [â† equiv.plift.surjective.infi_comp, mem_infi] at hs,
  rcases hs with âŸ¨t, ht, âŸ¨V, hVf, rflâŸ©âŸ©,
  choose U hUf hU using Î» i, is_measurably_generated.exists_measurable_subset (hVf i),
  refine âŸ¨â‹‚ i : t, U i, _, _, _âŸ©,
  { rw [â† equiv.plift.surjective.infi_comp, mem_infi],
    refine âŸ¨t, ht, U, hUf, rflâŸ© },
  { haveI := ht.countable.to_encodable,
    refine measurable_set.Inter (Î» i, (hU i).1) },
  { exact Inter_subset_Inter (Î» i, (hU i).2) }
end

end filter

/-- We say that a collection of sets is countably spanning if a countable subset spans the
  whole type. This is a useful condition in various parts of measure theory. For example, it is
  a needed condition to show that the product of two collections generate the product sigma algebra,
  see `generate_from_prod_eq`. -/
def is_countably_spanning (C : set (set Î±)) : Prop :=
âˆƒ (s : â„• â†’ set Î±), (âˆ€ n, s n âˆˆ C) âˆ§ (â‹ƒ n, s n) = univ

lemma is_countably_spanning_measurable_set [measurable_space Î±] :
  is_countably_spanning {s : set Î± | measurable_set s} :=
âŸ¨Î» _, univ, Î» _, measurable_set.univ, Union_const _âŸ©

namespace measurable_set

/-!
### Typeclasses on `subtype measurable_set`
-/

variables [measurable_space Î±]

instance : has_mem Î± (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨Î» a s, a âˆˆ (s : set Î±)âŸ©

@[simp] lemma mem_coe (a : Î±) (s : subtype (measurable_set : set Î± â†’ Prop)) :
  a âˆˆ (s : set Î±) â†” a âˆˆ s := iff.rfl

instance : has_emptyc (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨âŸ¨âˆ…, measurable_set.emptyâŸ©âŸ©

@[simp] lemma coe_empty : â†‘(âˆ… : subtype (measurable_set : set Î± â†’ Prop)) = (âˆ… : set Î±) := rfl

instance [measurable_singleton_class Î±] : has_insert Î± (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨Î» a s, âŸ¨has_insert.insert a s, s.prop.insert aâŸ©âŸ©

@[simp] lemma coe_insert [measurable_singleton_class Î±] (a : Î±)
  (s : subtype (measurable_set : set Î± â†’ Prop)) :
  â†‘(has_insert.insert a s) = (has_insert.insert a s : set Î±) := rfl

instance : has_compl (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨Î» x, âŸ¨xá¶œ, x.prop.complâŸ©âŸ©

@[simp] lemma coe_compl (s : subtype (measurable_set : set Î± â†’ Prop)) : â†‘(sá¶œ) = (sá¶œ : set Î±) := rfl

instance : has_union (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨Î» x y, âŸ¨x âˆª y, x.prop.union y.propâŸ©âŸ©

@[simp] lemma coe_union (s t : subtype (measurable_set : set Î± â†’ Prop)) :
  â†‘(s âˆª t) = (s âˆª t : set Î±) := rfl

instance : has_inter (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨Î» x y, âŸ¨x âˆ© y, x.prop.inter y.propâŸ©âŸ©

@[simp] lemma coe_inter (s t : subtype (measurable_set : set Î± â†’ Prop)) :
  â†‘(s âˆ© t) = (s âˆ© t : set Î±) := rfl

instance : has_sdiff (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨Î» x y, âŸ¨x \ y, x.prop.diff y.propâŸ©âŸ©

@[simp] lemma coe_sdiff (s t : subtype (measurable_set : set Î± â†’ Prop)) :
  â†‘(s \ t) = (s \ t : set Î±) := rfl

instance : has_bot (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨âŸ¨âŠ¥, measurable_set.emptyâŸ©âŸ©

@[simp] lemma coe_bot : â†‘(âŠ¥ : subtype (measurable_set : set Î± â†’ Prop)) = (âŠ¥ : set Î±) := rfl

instance : has_top (subtype (measurable_set : set Î± â†’ Prop)) :=
âŸ¨âŸ¨âŠ¤, measurable_set.univâŸ©âŸ©

@[simp] lemma coe_top : â†‘(âŠ¤ : subtype (measurable_set : set Î± â†’ Prop)) = (âŠ¤ : set Î±) := rfl

instance : partial_order (subtype (measurable_set : set Î± â†’ Prop)) :=
partial_order.lift _ subtype.coe_injective

instance : bounded_distrib_lattice (subtype (measurable_set : set Î± â†’ Prop)) :=
{ sup := (âˆª),
  le_sup_left := Î» a b, show (a : set Î±) â‰¤ a âŠ” b, from le_sup_left,
  le_sup_right := Î» a b, show (b : set Î±) â‰¤ a âŠ” b, from le_sup_right,
  sup_le := Î» a b c ha hb, show (a âŠ” b : set Î±) â‰¤ c, from sup_le ha hb,
  inf := (âˆ©),
  inf_le_left := Î» a b, show (a âŠ“ b : set Î±) â‰¤ a, from inf_le_left,
  inf_le_right := Î» a b, show (a âŠ“ b : set Î±) â‰¤ b, from inf_le_right,
  le_inf := Î» a b c ha hb, show (a : set Î±) â‰¤ b âŠ“ c, from le_inf ha hb,
  top := âŠ¤,
  le_top := Î» a, show (a : set Î±) â‰¤ âŠ¤, from le_top,
  bot := âŠ¥,
  bot_le := Î» a, show (âŠ¥ : set Î±) â‰¤ a, from bot_le,
  le_sup_inf := Î» x y z, show ((x âŠ” y) âŠ“ (x âŠ” z) : set Î±) â‰¤ x âŠ” y âŠ“ z, from le_sup_inf,
  .. measurable_set.subtype.partial_order }

instance : boolean_algebra (subtype (measurable_set : set Î± â†’ Prop)) :=
{ sdiff := (\),
  sup_inf_sdiff := Î» a b, subtype.eq $ sup_inf_sdiff a b,
  inf_inf_sdiff := Î» a b, subtype.eq $ inf_inf_sdiff a b,
  compl := has_compl.compl,
  inf_compl_le_bot := Î» a, boolean_algebra.inf_compl_le_bot (a : set Î±),
  top_le_sup_compl := Î» a, boolean_algebra.top_le_sup_compl (a : set Î±),
  sdiff_eq := Î» a b, subtype.eq $ sdiff_eq,
  .. measurable_set.subtype.bounded_distrib_lattice }

end measurable_set
