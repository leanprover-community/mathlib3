/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import measure_theory.measure.measure_space

/-!
# Almost everywhere measurable functions

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A function is almost everywhere measurable if it coincides almost everywhere with a measurable
function. This property, called `ae_measurable f Œº`, is defined in the file `measure_space_def`.
We discuss several of its properties that are analogous to properties of measurable functions.
-/

open measure_theory measure_theory.measure filter set function
open_locale measure_theory filter classical ennreal interval

variables {Œπ Œ± Œ≤ Œ≥ Œ¥ R : Type*} {m0 : measurable_space Œ±} [measurable_space Œ≤]
   [measurable_space Œ≥] [measurable_space Œ¥] {f g : Œ± ‚Üí Œ≤} {Œº ŒΩ : measure Œ±}

include m0

section

@[nontriviality, measurability]
lemma subsingleton.ae_measurable [subsingleton Œ±] : ae_measurable f Œº :=
subsingleton.measurable.ae_measurable

@[nontriviality, measurability]
lemma ae_measurable_of_subsingleton_codomain [subsingleton Œ≤] : ae_measurable f Œº :=
(measurable_of_subsingleton_codomain f).ae_measurable

@[simp, measurability] lemma ae_measurable_zero_measure : ae_measurable f (0 : measure Œ±) :=
begin
  nontriviality Œ±, inhabit Œ±,
  exact ‚ü®Œª x, f default, measurable_const, rfl‚ü©
end

namespace ae_measurable

lemma mono_measure (h : ae_measurable f Œº) (h' : ŒΩ ‚â§ Œº) : ae_measurable f ŒΩ :=
‚ü®h.mk f, h.measurable_mk, eventually.filter_mono (ae_mono h') h.ae_eq_mk‚ü©

lemma mono_set {s t} (h : s ‚äÜ t) (ht : ae_measurable f (Œº.restrict t)) :
  ae_measurable f (Œº.restrict s) :=
ht.mono_measure (restrict_mono h le_rfl)

protected lemma mono' (h : ae_measurable f Œº) (h' : ŒΩ ‚â™ Œº) : ae_measurable f ŒΩ :=
‚ü®h.mk f, h.measurable_mk, h' h.ae_eq_mk‚ü©

lemma ae_mem_imp_eq_mk {s} (h : ae_measurable f (Œº.restrict s)) :
  ‚àÄ·µê x ‚àÇŒº, x ‚àà s ‚Üí f x = h.mk f x :=
ae_imp_of_ae_restrict h.ae_eq_mk

lemma ae_inf_principal_eq_mk {s} (h : ae_measurable f (Œº.restrict s)) :
  f =·∂†[Œº.ae ‚äì ùìü s] h.mk f :=
le_ae_restrict h.ae_eq_mk

@[measurability]
lemma sum_measure [countable Œπ] {Œº : Œπ ‚Üí measure Œ±} (h : ‚àÄ i, ae_measurable f (Œº i)) :
  ae_measurable f (sum Œº) :=
begin
  nontriviality Œ≤, inhabit Œ≤,
  set s : Œπ ‚Üí set Œ± := Œª i, to_measurable (Œº i) {x | f x ‚â† (h i).mk f x},
  have hsŒº : ‚àÄ i, Œº i (s i) = 0,
  { intro i, rw measure_to_measurable, exact (h i).ae_eq_mk },
  have hsm : measurable_set (‚ãÇ i, s i),
    from measurable_set.Inter (Œª i, measurable_set_to_measurable _ _),
  have hs : ‚àÄ i x, x ‚àâ s i ‚Üí f x = (h i).mk f x,
  { intros i x hx, contrapose! hx, exact subset_to_measurable _ _ hx },
  set g : Œ± ‚Üí Œ≤ := (‚ãÇ i, s i).piecewise (const Œ± default) f,
  refine ‚ü®g, measurable_of_restrict_of_restrict_compl hsm _ _, ae_sum_iff.mpr $ Œª i, _‚ü©,
  { rw [restrict_piecewise], simp only [set.restrict, const], exact measurable_const },
  { rw [restrict_piecewise_compl, compl_Inter],
    intros t ht,
    refine ‚ü®‚ãÉ i, ((h i).mk f ‚Åª¬π' t) ‚à© (s i)·∂ú, measurable_set.Union $
      Œª i, (measurable_mk _ ht).inter (measurable_set_to_measurable _ _).compl, _‚ü©,
    ext ‚ü®x, hx‚ü©,
    simp only [mem_preimage, mem_Union, subtype.coe_mk, set.restrict, mem_inter_iff,
      mem_compl_iff] at hx ‚ä¢,
    split,
    { rintro ‚ü®i, hxt, hxs‚ü©, rwa hs _ _ hxs },
    { rcases hx with ‚ü®i, hi‚ü©, rw hs _ _ hi, exact Œª h, ‚ü®i, h, hi‚ü© } },
  { refine measure_mono_null (Œª x (hx : f x ‚â† g x), _) (hsŒº i),
    contrapose! hx, refine (piecewise_eq_of_not_mem _ _ _ _).symm,
    exact Œª h, hx (mem_Inter.1 h i) }
end

@[simp] lemma _root_.ae_measurable_sum_measure_iff [countable Œπ] {Œº : Œπ ‚Üí measure Œ±} :
  ae_measurable f (sum Œº) ‚Üî ‚àÄ i, ae_measurable f (Œº i) :=
‚ü®Œª h i, h.mono_measure (le_sum _ _), sum_measure‚ü©

@[simp] lemma _root_.ae_measurable_add_measure_iff :
  ae_measurable f (Œº + ŒΩ) ‚Üî ae_measurable f Œº ‚àß ae_measurable f ŒΩ :=
by { rw [‚Üê sum_cond, ae_measurable_sum_measure_iff, bool.forall_bool, and.comm], refl }

@[measurability]
lemma add_measure {f : Œ± ‚Üí Œ≤} (hŒº : ae_measurable f Œº) (hŒΩ : ae_measurable f ŒΩ) :
  ae_measurable f (Œº + ŒΩ) :=
ae_measurable_add_measure_iff.2 ‚ü®hŒº, hŒΩ‚ü©

@[measurability]
protected lemma Union [countable Œπ] {s : Œπ ‚Üí set Œ±} (h : ‚àÄ i, ae_measurable f (Œº.restrict (s i))) :
  ae_measurable f (Œº.restrict (‚ãÉ i, s i)) :=
(sum_measure h).mono_measure $ restrict_Union_le

@[simp] lemma _root_.ae_measurable_Union_iff [countable Œπ] {s : Œπ ‚Üí set Œ±} :
  ae_measurable f (Œº.restrict (‚ãÉ i, s i)) ‚Üî ‚àÄ i, ae_measurable f (Œº.restrict (s i)) :=
‚ü®Œª h i, h.mono_measure $ restrict_mono (subset_Union _ _) le_rfl, ae_measurable.Union‚ü©

@[simp] lemma _root_.ae_measurable_union_iff {s t : set Œ±} :
  ae_measurable f (Œº.restrict (s ‚à™ t)) ‚Üî
    ae_measurable f (Œº.restrict s) ‚àß ae_measurable f (Œº.restrict t) :=
by simp only [union_eq_Union, ae_measurable_Union_iff, bool.forall_bool, cond, and.comm]

@[measurability]
lemma smul_measure [monoid R] [distrib_mul_action R ‚Ñù‚â•0‚àû] [is_scalar_tower R ‚Ñù‚â•0‚àû ‚Ñù‚â•0‚àû]
  (h : ae_measurable f Œº) (c : R) :
  ae_measurable f (c ‚Ä¢ Œº) :=
‚ü®h.mk f, h.measurable_mk, ae_smul_measure h.ae_eq_mk c‚ü©

lemma comp_ae_measurable {f : Œ± ‚Üí Œ¥} {g : Œ¥ ‚Üí Œ≤}
  (hg : ae_measurable g (Œº.map f)) (hf : ae_measurable f Œº) : ae_measurable (g ‚àò f) Œº :=
‚ü®hg.mk g ‚àò hf.mk f, hg.measurable_mk.comp hf.measurable_mk,
  (ae_eq_comp hf hg.ae_eq_mk).trans ((hf.ae_eq_mk).fun_comp (mk g hg))‚ü©

lemma comp_measurable {f : Œ± ‚Üí Œ¥} {g : Œ¥ ‚Üí Œ≤}
  (hg : ae_measurable g (Œº.map f)) (hf : measurable f) : ae_measurable (g ‚àò f) Œº :=
hg.comp_ae_measurable hf.ae_measurable

lemma comp_quasi_measure_preserving {ŒΩ : measure Œ¥} {f : Œ± ‚Üí Œ¥} {g : Œ¥ ‚Üí Œ≤} (hg : ae_measurable g ŒΩ)
  (hf : quasi_measure_preserving f Œº ŒΩ) : ae_measurable (g ‚àò f) Œº :=
(hg.mono' hf.absolutely_continuous).comp_measurable hf.measurable

lemma map_map_of_ae_measurable {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤}
  (hg : ae_measurable g (measure.map f Œº)) (hf : ae_measurable f Œº) :
  (Œº.map f).map g = Œº.map (g ‚àò f) :=
begin
  ext1 s hs,
  let g' := hg.mk g,
  have A : map g (map f Œº) = map g' (map f Œº),
  { apply measure_theory.measure.map_congr,
    exact hg.ae_eq_mk },
  have B : map (g ‚àò f) Œº = map (g' ‚àò f) Œº,
  { apply measure_theory.measure.map_congr,
    exact ae_of_ae_map hf hg.ae_eq_mk },
  simp only [A, B, hs, hg.measurable_mk.ae_measurable.comp_ae_measurable hf, hg.measurable_mk,
    hg.measurable_mk hs, hf, map_apply, map_apply_of_ae_measurable],
  refl,
end

@[measurability]
lemma prod_mk {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} (hf : ae_measurable f Œº) (hg : ae_measurable g Œº) :
  ae_measurable (Œª x, (f x, g x)) Œº :=
‚ü®Œª a, (hf.mk f a, hg.mk g a), hf.measurable_mk.prod_mk hg.measurable_mk,
  eventually_eq.prod_mk hf.ae_eq_mk hg.ae_eq_mk‚ü©

lemma exists_ae_eq_range_subset (H : ae_measurable f Œº) {t : set Œ≤} (ht : ‚àÄ·µê x ‚àÇŒº, f x ‚àà t)
  (h‚ÇÄ : t.nonempty) :
  ‚àÉ g, measurable g ‚àß range g ‚äÜ t ‚àß f =·µê[Œº] g :=
begin
  let s : set Œ± := to_measurable Œº {x | f x = H.mk f x ‚àß f x ‚àà t}·∂ú,
  let g : Œ± ‚Üí Œ≤ := piecewise s (Œª x, h‚ÇÄ.some) (H.mk f),
  refine ‚ü®g, _, _, _‚ü©,
  { exact measurable.piecewise (measurable_set_to_measurable _ _)
      measurable_const H.measurable_mk },
  { rintros _ ‚ü®x, rfl‚ü©,
    by_cases hx : x ‚àà s,
    { simpa [g, hx] using h‚ÇÄ.some_mem },
    { simp only [g, hx, piecewise_eq_of_not_mem, not_false_iff],
      contrapose! hx,
      apply subset_to_measurable,
      simp only [hx, mem_compl_iff, mem_set_of_eq, not_and, not_false_iff, implies_true_iff]
        {contextual := tt} } },
  { have A : Œº (to_measurable Œº {x | f x = H.mk f x ‚àß f x ‚àà t}·∂ú) = 0,
    { rw [measure_to_measurable, ‚Üê compl_mem_ae_iff, compl_compl],
      exact H.ae_eq_mk.and ht },
    filter_upwards [compl_mem_ae_iff.2 A] with x hx,
    rw mem_compl_iff at hx,
    simp only [g, hx, piecewise_eq_of_not_mem, not_false_iff],
    contrapose! hx,
    apply subset_to_measurable,
    simp only [hx, mem_compl_iff, mem_set_of_eq, false_and, not_false_iff] }
end

lemma exists_measurable_nonneg {Œ≤} [preorder Œ≤] [has_zero Œ≤] {mŒ≤ : measurable_space Œ≤} {f : Œ± ‚Üí Œ≤}
  (hf : ae_measurable f Œº) (f_nn : ‚àÄ·µê t ‚àÇŒº, 0 ‚â§ f t) :
  ‚àÉ g, measurable g ‚àß 0 ‚â§ g ‚àß f =·µê[Œº] g :=
begin
  obtain ‚ü®G, hG_meas, hG_mem, hG_ae_eq‚ü© := hf.exists_ae_eq_range_subset f_nn ‚ü®0, le_rfl‚ü©,
  exact ‚ü®G, hG_meas, Œª x, hG_mem (mem_range_self x), hG_ae_eq‚ü©,
end

lemma subtype_mk (h : ae_measurable f Œº) {s : set Œ≤} {hfs : ‚àÄ x, f x ‚àà s} :
  ae_measurable (cod_restrict f s hfs) Œº :=
begin
  nontriviality Œ±, inhabit Œ±,
  obtain ‚ü®g, g_meas, hg, fg‚ü© : ‚àÉ (g : Œ± ‚Üí Œ≤), measurable g ‚àß range g ‚äÜ s ‚àß f =·µê[Œº] g :=
    h.exists_ae_eq_range_subset (eventually_of_forall hfs) ‚ü®_, hfs default‚ü©,
  refine ‚ü®cod_restrict g s (Œª x, hg (mem_range_self _)), measurable.subtype_mk g_meas, _‚ü©,
  filter_upwards [fg] with x hx,
  simpa [subtype.ext_iff],
end

protected lemma null_measurable (h : ae_measurable f Œº) : null_measurable f Œº :=
let ‚ü®g, hgm, hg‚ü© := h in hgm.null_measurable.congr hg.symm

end ae_measurable

lemma ae_measurable_const' (h : ‚àÄ·µê x y ‚àÇŒº, f x = f y) : ae_measurable f Œº :=
begin
  rcases eq_or_ne Œº 0 with rfl | hŒº,
  { exact ae_measurable_zero_measure },
  { haveI := ae_ne_bot.2 hŒº,
    rcases h.exists with ‚ü®x, hx‚ü©,
    exact ‚ü®const Œ± (f x), measurable_const, eventually_eq.symm hx‚ü© }
end

lemma ae_measurable_uIoc_iff [linear_order Œ±] {f : Œ± ‚Üí Œ≤} {a b : Œ±} :
  (ae_measurable f $ Œº.restrict $ Œô a b) ‚Üî
    (ae_measurable f $ Œº.restrict $ Ioc a b) ‚àß (ae_measurable f $ Œº.restrict $ Ioc b a) :=
by rw [uIoc_eq_union, ae_measurable_union_iff]

lemma ae_measurable_iff_measurable [Œº.is_complete] :
  ae_measurable f Œº ‚Üî measurable f :=
‚ü®Œª h, h.null_measurable.measurable_of_complete, Œª h, h.ae_measurable‚ü©

lemma measurable_embedding.ae_measurable_map_iff {g : Œ≤ ‚Üí Œ≥} (hf : measurable_embedding f) :
  ae_measurable g (Œº.map f) ‚Üî ae_measurable (g ‚àò f) Œº :=
begin
  refine ‚ü®Œª H, H.comp_measurable hf.measurable, _‚ü©,
  rintro ‚ü®g‚ÇÅ, hgm‚ÇÅ, heq‚ü©,
  rcases hf.exists_measurable_extend hgm‚ÇÅ (Œª x, ‚ü®g x‚ü©) with ‚ü®g‚ÇÇ, hgm‚ÇÇ, rfl‚ü©,
  exact ‚ü®g‚ÇÇ, hgm‚ÇÇ, hf.ae_map_iff.2 heq‚ü©
end

lemma measurable_embedding.ae_measurable_comp_iff {g : Œ≤ ‚Üí Œ≥}
  (hg : measurable_embedding g) {Œº : measure Œ±} :
  ae_measurable (g ‚àò f) Œº ‚Üî ae_measurable f Œº :=
begin
  refine ‚ü®Œª H, _, hg.measurable.comp_ae_measurable‚ü©,
  suffices : ae_measurable ((range_splitting g ‚àò range_factorization g) ‚àò f) Œº,
    by rwa [(right_inverse_range_splitting hg.injective).comp_eq_id] at this,
  exact hg.measurable_range_splitting.comp_ae_measurable H.subtype_mk
end

lemma ae_measurable_restrict_iff_comap_subtype {s : set Œ±} (hs : measurable_set s)
  {Œº : measure Œ±} {f : Œ± ‚Üí Œ≤} :
  ae_measurable f (Œº.restrict s) ‚Üî ae_measurable (f ‚àò coe : s ‚Üí Œ≤) (comap coe Œº) :=
by rw [‚Üê map_comap_subtype_coe hs, (measurable_embedding.subtype_coe hs).ae_measurable_map_iff]

@[simp, to_additive] lemma ae_measurable_one [has_one Œ≤] : ae_measurable (Œª a : Œ±, (1 : Œ≤)) Œº :=
measurable_one.ae_measurable

@[simp] lemma ae_measurable_smul_measure_iff {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† 0) :
  ae_measurable f (c ‚Ä¢ Œº) ‚Üî ae_measurable f Œº :=
‚ü®Œª h, ‚ü®h.mk f, h.measurable_mk, (ae_smul_measure_iff hc).1 h.ae_eq_mk‚ü©,
  Œª h, ‚ü®h.mk f, h.measurable_mk, (ae_smul_measure_iff hc).2 h.ae_eq_mk‚ü©‚ü©

lemma ae_measurable_of_ae_measurable_trim {Œ±} {m m0 : measurable_space Œ±}
  {Œº : measure Œ±} (hm : m ‚â§ m0) {f : Œ± ‚Üí Œ≤} (hf : ae_measurable f (Œº.trim hm)) :
  ae_measurable f Œº :=
‚ü®hf.mk f, measurable.mono hf.measurable_mk hm le_rfl, ae_eq_of_ae_eq_trim hf.ae_eq_mk‚ü©

lemma ae_measurable_restrict_of_measurable_subtype {s : set Œ±}
  (hs : measurable_set s) (hf : measurable (Œª x : s, f x)) : ae_measurable f (Œº.restrict s) :=
(ae_measurable_restrict_iff_comap_subtype hs).2 hf.ae_measurable

lemma ae_measurable_map_equiv_iff (e : Œ± ‚âÉ·µê Œ≤) {f : Œ≤ ‚Üí Œ≥} :
  ae_measurable f (Œº.map e) ‚Üî ae_measurable (f ‚àò e) Œº :=
e.measurable_embedding.ae_measurable_map_iff

end

lemma ae_measurable.restrict (hfm : ae_measurable f Œº) {s} :
  ae_measurable f (Œº.restrict s) :=
‚ü®ae_measurable.mk f hfm, hfm.measurable_mk, ae_restrict_of_ae hfm.ae_eq_mk‚ü©

lemma ae_measurable_Ioi_of_forall_Ioc {Œ≤} {mŒ≤ : measurable_space Œ≤}
  [linear_order Œ±] [(at_top : filter Œ±).is_countably_generated] {x : Œ±} {g : Œ± ‚Üí Œ≤}
  (g_meas : ‚àÄ t > x, ae_measurable g (Œº.restrict (Ioc x t))) :
  ae_measurable g (Œº.restrict (Ioi x)) :=
begin
  haveI : nonempty Œ± := ‚ü®x‚ü©,
  obtain ‚ü®u, hu_tendsto‚ü© := exists_seq_tendsto (at_top : filter Œ±),
  have Ioi_eq_Union : Ioi x = ‚ãÉ n : ‚Ñï, Ioc x (u n),
  { rw Union_Ioc_eq_Ioi_self_iff.mpr _,
    exact Œª y _, (hu_tendsto.eventually (eventually_ge_at_top y)).exists },
  rw [Ioi_eq_Union, ae_measurable_Union_iff],
  intros n,
  cases lt_or_le x (u n),
  { exact g_meas (u n) h, },
  { rw [Ioc_eq_empty (not_lt.mpr h), measure.restrict_empty],
    exact ae_measurable_zero_measure, },
end

variables [has_zero Œ≤]

lemma ae_measurable_indicator_iff {s} (hs : measurable_set s) :
  ae_measurable (indicator s f) Œº ‚Üî ae_measurable f (Œº.restrict s) :=
begin
  split,
  { intro h,
    exact (h.mono_measure measure.restrict_le_self).congr (indicator_ae_eq_restrict hs) },
  { intro h,
    refine ‚ü®indicator s (h.mk f), h.measurable_mk.indicator hs, _‚ü©,
    have A : s.indicator f =·µê[Œº.restrict s] s.indicator (ae_measurable.mk f h) :=
      (indicator_ae_eq_restrict hs).trans (h.ae_eq_mk.trans $ (indicator_ae_eq_restrict hs).symm),
    have B : s.indicator f =·µê[Œº.restrict s·∂ú] s.indicator (ae_measurable.mk f h) :=
      (indicator_ae_eq_restrict_compl hs).trans (indicator_ae_eq_restrict_compl hs).symm,
    exact ae_of_ae_restrict_of_ae_restrict_compl _ A B },
end

@[measurability]
lemma ae_measurable.indicator (hfm : ae_measurable f Œº) {s} (hs : measurable_set s) :
  ae_measurable (s.indicator f) Œº :=
(ae_measurable_indicator_iff hs).mpr hfm.restrict

lemma measure_theory.measure.restrict_map_of_ae_measurable
  {f : Œ± ‚Üí Œ¥} (hf : ae_measurable f Œº) {s : set Œ¥} (hs : measurable_set s) :
  (Œº.map f).restrict s = (Œº.restrict $ f ‚Åª¬π' s).map f :=
calc
(Œº.map f).restrict s = (Œº.map (hf.mk f)).restrict s :
  by { congr' 1, apply measure.map_congr hf.ae_eq_mk }
... = (Œº.restrict $ (hf.mk f) ‚Åª¬π' s).map (hf.mk f) :
  measure.restrict_map hf.measurable_mk hs
... = (Œº.restrict $ (hf.mk f) ‚Åª¬π' s).map f :
  measure.map_congr (ae_restrict_of_ae (hf.ae_eq_mk.symm))
... = (Œº.restrict $ f ‚Åª¬π' s).map f :
begin
  apply congr_arg,
  ext1 t ht,
  simp only [ht, measure.restrict_apply],
  apply measure_congr,
  apply (eventually_eq.refl _ _).inter (hf.ae_eq_mk.symm.preimage s)
end

lemma measure_theory.measure.map_mono_of_ae_measurable
  {f : Œ± ‚Üí Œ¥} (h : Œº ‚â§ ŒΩ) (hf : ae_measurable f ŒΩ) :
  Œº.map f ‚â§ ŒΩ.map f :=
Œª s hs, by simpa [hf, hs, hf.mono_measure h] using measure.le_iff'.1 h (f ‚Åª¬π' s)
