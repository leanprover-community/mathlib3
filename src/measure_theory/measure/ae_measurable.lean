/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import measure_theory.measure.measure_space

/-!
# Almost everywhere measurable functions

A function is almost everywhere measurable if it coincides almost everywhere with a measurable
function. This property, called `ae_measurable f Î¼`, is defined in the file `measure_space_def`.
We discuss several of its properties that are analogous to properties of measurable functions.
-/

open measure_theory measure_theory.measure filter set function
open_locale measure_theory filter classical ennreal interval

variables {Î¹ Î± Î² Î³ Î´ R : Type*} {m0 : measurable_space Î±} [measurable_space Î²]
   [measurable_space Î³] [measurable_space Î´] {f g : Î± â†’ Î²} {Î¼ Î½ : measure Î±}

include m0

section

@[nontriviality, measurability]
lemma subsingleton.ae_measurable [subsingleton Î±] : ae_measurable f Î¼ :=
subsingleton.measurable.ae_measurable

@[nontriviality, measurability]
lemma ae_measurable_of_subsingleton_codomain [subsingleton Î²] : ae_measurable f Î¼ :=
(measurable_of_subsingleton_codomain f).ae_measurable

@[simp, measurability] lemma ae_measurable_zero_measure : ae_measurable f (0 : measure Î±) :=
begin
  nontriviality Î±, inhabit Î±,
  exact âŸ¨Î» x, f default, measurable_const, rflâŸ©
end

namespace ae_measurable

lemma mono_measure (h : ae_measurable f Î¼) (h' : Î½ â‰¤ Î¼) : ae_measurable f Î½ :=
âŸ¨h.mk f, h.measurable_mk, eventually.filter_mono (ae_mono h') h.ae_eq_mkâŸ©

lemma mono_set {s t} (h : s âŠ† t) (ht : ae_measurable f (Î¼.restrict t)) :
  ae_measurable f (Î¼.restrict s) :=
ht.mono_measure (restrict_mono h le_rfl)

protected lemma mono' (h : ae_measurable f Î¼) (h' : Î½ â‰ª Î¼) : ae_measurable f Î½ :=
âŸ¨h.mk f, h.measurable_mk, h' h.ae_eq_mkâŸ©

lemma ae_mem_imp_eq_mk {s} (h : ae_measurable f (Î¼.restrict s)) :
  âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = h.mk f x :=
ae_imp_of_ae_restrict h.ae_eq_mk

lemma ae_inf_principal_eq_mk {s} (h : ae_measurable f (Î¼.restrict s)) :
  f =á¶ [Î¼.ae âŠ“ ğ“Ÿ s] h.mk f :=
le_ae_restrict h.ae_eq_mk

@[measurability]
lemma sum_measure [encodable Î¹] {Î¼ : Î¹ â†’ measure Î±} (h : âˆ€ i, ae_measurable f (Î¼ i)) :
  ae_measurable f (sum Î¼) :=
begin
  nontriviality Î², inhabit Î²,
  set s : Î¹ â†’ set Î± := Î» i, to_measurable (Î¼ i) {x | f x â‰  (h i).mk f x},
  have hsÎ¼ : âˆ€ i, Î¼ i (s i) = 0,
  { intro i, rw measure_to_measurable, exact (h i).ae_eq_mk },
  have hsm : measurable_set (â‹‚ i, s i),
    from measurable_set.Inter (Î» i, measurable_set_to_measurable _ _),
  have hs : âˆ€ i x, x âˆ‰ s i â†’ f x = (h i).mk f x,
  { intros i x hx, contrapose! hx, exact subset_to_measurable _ _ hx },
  set g : Î± â†’ Î² := (â‹‚ i, s i).piecewise (const Î± default) f,
  refine âŸ¨g, measurable_of_restrict_of_restrict_compl hsm _ _, ae_sum_iff.mpr $ Î» i, _âŸ©,
  { rw [restrict_piecewise], simp only [set.restrict, const], exact measurable_const },
  { rw [restrict_piecewise_compl, compl_Inter],
    intros t ht,
    refine âŸ¨â‹ƒ i, ((h i).mk f â»Â¹' t) âˆ© (s i)á¶œ, measurable_set.Union $
      Î» i, (measurable_mk _ ht).inter (measurable_set_to_measurable _ _).compl, _âŸ©,
    ext âŸ¨x, hxâŸ©,
    simp only [mem_preimage, mem_Union, subtype.coe_mk, set.restrict, mem_inter_eq,
      mem_compl_iff] at hx âŠ¢,
    split,
    { rintro âŸ¨i, hxt, hxsâŸ©, rwa hs _ _ hxs },
    { rcases hx with âŸ¨i, hiâŸ©, rw hs _ _ hi, exact Î» h, âŸ¨i, h, hiâŸ© } },
  { refine measure_mono_null (Î» x (hx : f x â‰  g x), _) (hsÎ¼ i),
    contrapose! hx, refine (piecewise_eq_of_not_mem _ _ _ _).symm,
    exact Î» h, hx (mem_Inter.1 h i) }
end

@[simp] lemma _root_.ae_measurable_sum_measure_iff [encodable Î¹] {Î¼ : Î¹ â†’ measure Î±} :
  ae_measurable f (sum Î¼) â†” âˆ€ i, ae_measurable f (Î¼ i) :=
âŸ¨Î» h i, h.mono_measure (le_sum _ _), sum_measureâŸ©

@[simp] lemma _root_.ae_measurable_add_measure_iff :
  ae_measurable f (Î¼ + Î½) â†” ae_measurable f Î¼ âˆ§ ae_measurable f Î½ :=
by { rw [â† sum_cond, ae_measurable_sum_measure_iff, bool.forall_bool, and.comm], refl }

@[measurability]
lemma add_measure {f : Î± â†’ Î²} (hÎ¼ : ae_measurable f Î¼) (hÎ½ : ae_measurable f Î½) :
  ae_measurable f (Î¼ + Î½) :=
ae_measurable_add_measure_iff.2 âŸ¨hÎ¼, hÎ½âŸ©

@[measurability]
protected lemma Union [encodable Î¹] {s : Î¹ â†’ set Î±} (h : âˆ€ i, ae_measurable f (Î¼.restrict (s i))) :
  ae_measurable f (Î¼.restrict (â‹ƒ i, s i)) :=
(sum_measure h).mono_measure $ restrict_Union_le

@[simp] lemma _root_.ae_measurable_Union_iff [encodable Î¹] {s : Î¹ â†’ set Î±} :
  ae_measurable f (Î¼.restrict (â‹ƒ i, s i)) â†” âˆ€ i, ae_measurable f (Î¼.restrict (s i)) :=
âŸ¨Î» h i, h.mono_measure $ restrict_mono (subset_Union _ _) le_rfl, ae_measurable.UnionâŸ©

@[simp] lemma _root_.ae_measurable_union_iff {s t : set Î±} :
  ae_measurable f (Î¼.restrict (s âˆª t)) â†”
    ae_measurable f (Î¼.restrict s) âˆ§ ae_measurable f (Î¼.restrict t) :=
by simp only [union_eq_Union, ae_measurable_Union_iff, bool.forall_bool, cond, and.comm]

@[measurability]
lemma smul_measure [monoid R] [distrib_mul_action R â„â‰¥0âˆ] [is_scalar_tower R â„â‰¥0âˆ â„â‰¥0âˆ]
  (h : ae_measurable f Î¼) (c : R) :
  ae_measurable f (c â€¢ Î¼) :=
âŸ¨h.mk f, h.measurable_mk, ae_smul_measure h.ae_eq_mk câŸ©

lemma comp_ae_measurable {f : Î± â†’ Î´} {g : Î´ â†’ Î²}
  (hg : ae_measurable g (Î¼.map f)) (hf : ae_measurable f Î¼) : ae_measurable (g âˆ˜ f) Î¼ :=
âŸ¨hg.mk g âˆ˜ hf.mk f, hg.measurable_mk.comp hf.measurable_mk,
  (ae_eq_comp hf hg.ae_eq_mk).trans ((hf.ae_eq_mk).fun_comp (mk g hg))âŸ©

lemma comp_measurable {f : Î± â†’ Î´} {g : Î´ â†’ Î²}
  (hg : ae_measurable g (Î¼.map f)) (hf : measurable f) : ae_measurable (g âˆ˜ f) Î¼ :=
hg.comp_ae_measurable hf.ae_measurable

lemma comp_measurable' {Î½ : measure Î´} {f : Î± â†’ Î´} {g : Î´ â†’ Î²} (hg : ae_measurable g Î½)
  (hf : measurable f) (h : Î¼.map f â‰ª Î½) : ae_measurable (g âˆ˜ f) Î¼ :=
(hg.mono' h).comp_measurable hf

@[measurability]
lemma prod_mk {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) :
  ae_measurable (Î» x, (f x, g x)) Î¼ :=
âŸ¨Î» a, (hf.mk f a, hg.mk g a), hf.measurable_mk.prod_mk hg.measurable_mk,
  eventually_eq.prod_mk hf.ae_eq_mk hg.ae_eq_mkâŸ©

lemma exists_ae_eq_range_subset (H : ae_measurable f Î¼) {t : set Î²} (ht : âˆ€áµ x âˆ‚Î¼, f x âˆˆ t)
  (hâ‚€ : t.nonempty) :
  âˆƒ g, measurable g âˆ§ range g âŠ† t âˆ§ f =áµ[Î¼] g :=
begin
  let s : set Î± := to_measurable Î¼ {x | f x = H.mk f x âˆ§ f x âˆˆ t}á¶œ,
  let g : Î± â†’ Î² := piecewise s (Î» x, hâ‚€.some) (H.mk f),
  refine âŸ¨g, _, _, _âŸ©,
  { exact measurable.piecewise (measurable_set_to_measurable _ _)
      measurable_const H.measurable_mk },
  { rintros - âŸ¨x, rflâŸ©,
    by_cases hx : x âˆˆ s,
    { simpa [g, hx] using hâ‚€.some_mem },
    { simp only [g, hx, piecewise_eq_of_not_mem, not_false_iff],
      contrapose! hx,
      apply subset_to_measurable,
      simp only [hx, mem_compl_eq, mem_set_of_eq, not_and, not_false_iff, implies_true_iff]
        {contextual := tt} } },
  { have A : Î¼ (to_measurable Î¼ {x | f x = H.mk f x âˆ§ f x âˆˆ t}á¶œ) = 0,
    { rw [measure_to_measurable, â† compl_mem_ae_iff, compl_compl],
      exact H.ae_eq_mk.and ht },
    filter_upwards [compl_mem_ae_iff.2 A] with x hx,
    rw mem_compl_iff at hx,
    simp only [g, hx, piecewise_eq_of_not_mem, not_false_iff],
    contrapose! hx,
    apply subset_to_measurable,
    simp only [hx, mem_compl_eq, mem_set_of_eq, false_and, not_false_iff] }
end

lemma subtype_mk (h : ae_measurable f Î¼) {s : set Î²} {hfs : âˆ€ x, f x âˆˆ s} :
  ae_measurable (cod_restrict f s hfs) Î¼ :=
begin
  nontriviality Î±, inhabit Î±,
  obtain âŸ¨g, g_meas, hg, fgâŸ© : âˆƒ (g : Î± â†’ Î²), measurable g âˆ§ range g âŠ† s âˆ§ f =áµ[Î¼] g :=
    h.exists_ae_eq_range_subset (eventually_of_forall hfs) âŸ¨_, hfs defaultâŸ©,
  refine âŸ¨cod_restrict g s (Î» x, hg (mem_range_self _)), measurable.subtype_mk g_meas, _âŸ©,
  filter_upwards [fg] with x hx,
  simpa [subtype.ext_iff],
end

protected lemma null_measurable (h : ae_measurable f Î¼) : null_measurable f Î¼ :=
let âŸ¨g, hgm, hgâŸ© := h in hgm.null_measurable.congr hg.symm

end ae_measurable

lemma ae_measurable_interval_oc_iff [linear_order Î±] {f : Î± â†’ Î²} {a b : Î±} :
  (ae_measurable f $ Î¼.restrict $ Î™ a b) â†”
    (ae_measurable f $ Î¼.restrict $ Ioc a b) âˆ§ (ae_measurable f $ Î¼.restrict $ Ioc b a) :=
by rw [interval_oc_eq_union, ae_measurable_union_iff]

lemma ae_measurable_iff_measurable [Î¼.is_complete] :
  ae_measurable f Î¼ â†” measurable f :=
âŸ¨Î» h, h.null_measurable.measurable_of_complete, Î» h, h.ae_measurableâŸ©

lemma measurable_embedding.ae_measurable_map_iff {g : Î² â†’ Î³} (hf : measurable_embedding f) :
  ae_measurable g (Î¼.map f) â†” ae_measurable (g âˆ˜ f) Î¼ :=
begin
  refine âŸ¨Î» H, H.comp_measurable hf.measurable, _âŸ©,
  rintro âŸ¨gâ‚, hgmâ‚, heqâŸ©,
  rcases hf.exists_measurable_extend hgmâ‚ (Î» x, âŸ¨g xâŸ©) with âŸ¨gâ‚‚, hgmâ‚‚, rflâŸ©,
  exact âŸ¨gâ‚‚, hgmâ‚‚, hf.ae_map_iff.2 heqâŸ©
end

lemma measurable_embedding.ae_measurable_comp_iff {g : Î² â†’ Î³}
  (hg : measurable_embedding g) {Î¼ : measure Î±} :
  ae_measurable (g âˆ˜ f) Î¼ â†” ae_measurable f Î¼ :=
begin
  refine âŸ¨Î» H, _, hg.measurable.comp_ae_measurableâŸ©,
  suffices : ae_measurable ((range_splitting g âˆ˜ range_factorization g) âˆ˜ f) Î¼,
    by rwa [(right_inverse_range_splitting hg.injective).comp_eq_id] at this,
  exact hg.measurable_range_splitting.comp_ae_measurable H.subtype_mk
end

lemma ae_measurable_restrict_iff_comap_subtype {s : set Î±} (hs : measurable_set s)
  {Î¼ : measure Î±} {f : Î± â†’ Î²} :
  ae_measurable f (Î¼.restrict s) â†” ae_measurable (f âˆ˜ coe : s â†’ Î²) (comap coe Î¼) :=
by rw [â† map_comap_subtype_coe hs, (measurable_embedding.subtype_coe hs).ae_measurable_map_iff]

@[simp, to_additive] lemma ae_measurable_one [has_one Î²] : ae_measurable (Î» a : Î±, (1 : Î²)) Î¼ :=
measurable_one.ae_measurable

@[simp] lemma ae_measurable_smul_measure_iff {c : â„â‰¥0âˆ} (hc : c â‰  0) :
  ae_measurable f (c â€¢ Î¼) â†” ae_measurable f Î¼ :=
âŸ¨Î» h, âŸ¨h.mk f, h.measurable_mk, (ae_smul_measure_iff hc).1 h.ae_eq_mkâŸ©,
  Î» h, âŸ¨h.mk f, h.measurable_mk, (ae_smul_measure_iff hc).2 h.ae_eq_mkâŸ©âŸ©

lemma ae_measurable_of_ae_measurable_trim {Î±} {m m0 : measurable_space Î±}
  {Î¼ : measure Î±} (hm : m â‰¤ m0) {f : Î± â†’ Î²} (hf : ae_measurable f (Î¼.trim hm)) :
  ae_measurable f Î¼ :=
âŸ¨hf.mk f, measurable.mono hf.measurable_mk hm le_rfl, ae_eq_of_ae_eq_trim hf.ae_eq_mkâŸ©

lemma ae_measurable_restrict_of_measurable_subtype {s : set Î±}
  (hs : measurable_set s) (hf : measurable (Î» x : s, f x)) : ae_measurable f (Î¼.restrict s) :=
(ae_measurable_restrict_iff_comap_subtype hs).2 hf.ae_measurable

lemma ae_measurable_map_equiv_iff (e : Î± â‰ƒáµ Î²) {f : Î² â†’ Î³} :
  ae_measurable f (Î¼.map e) â†” ae_measurable (f âˆ˜ e) Î¼ :=
e.measurable_embedding.ae_measurable_map_iff

end

lemma ae_measurable.restrict (hfm : ae_measurable f Î¼) {s} :
  ae_measurable f (Î¼.restrict s) :=
âŸ¨ae_measurable.mk f hfm, hfm.measurable_mk, ae_restrict_of_ae hfm.ae_eq_mkâŸ©

variables [has_zero Î²]

lemma ae_measurable_indicator_iff {s} (hs : measurable_set s) :
  ae_measurable (indicator s f) Î¼ â†” ae_measurable f (Î¼.restrict s) :=
begin
  split,
  { intro h,
    exact (h.mono_measure measure.restrict_le_self).congr (indicator_ae_eq_restrict hs) },
  { intro h,
    refine âŸ¨indicator s (h.mk f), h.measurable_mk.indicator hs, _âŸ©,
    have A : s.indicator f =áµ[Î¼.restrict s] s.indicator (ae_measurable.mk f h) :=
      (indicator_ae_eq_restrict hs).trans (h.ae_eq_mk.trans $ (indicator_ae_eq_restrict hs).symm),
    have B : s.indicator f =áµ[Î¼.restrict sá¶œ] s.indicator (ae_measurable.mk f h) :=
      (indicator_ae_eq_restrict_compl hs).trans (indicator_ae_eq_restrict_compl hs).symm,
    exact ae_of_ae_restrict_of_ae_restrict_compl _ A B },
end

@[measurability]
lemma ae_measurable.indicator (hfm : ae_measurable f Î¼) {s} (hs : measurable_set s) :
  ae_measurable (s.indicator f) Î¼ :=
(ae_measurable_indicator_iff hs).mpr hfm.restrict

lemma measure_theory.measure.restrict_map_of_ae_measurable
  {f : Î± â†’ Î´} (hf : ae_measurable f Î¼) {s : set Î´} (hs : measurable_set s) :
  (Î¼.map f).restrict s = (Î¼.restrict $ f â»Â¹' s).map f :=
calc
(Î¼.map f).restrict s = (Î¼.map (hf.mk f)).restrict s :
  by { congr' 1, apply measure.map_congr hf.ae_eq_mk }
... = (Î¼.restrict $ (hf.mk f) â»Â¹' s).map (hf.mk f) :
  measure.restrict_map hf.measurable_mk hs
... = (Î¼.restrict $ (hf.mk f) â»Â¹' s).map f :
  measure.map_congr (ae_restrict_of_ae (hf.ae_eq_mk.symm))
... = (Î¼.restrict $ f â»Â¹' s).map f :
begin
  apply congr_arg,
  ext1 t ht,
  simp only [ht, measure.restrict_apply],
  apply measure_congr,
  apply (eventually_eq.refl _ _).inter (hf.ae_eq_mk.symm.preimage s)
end

lemma measure_theory.measure.map_mono_of_ae_measurable
  {f : Î± â†’ Î´} (h : Î¼ â‰¤ Î½) (hf : ae_measurable f Î½) :
  Î¼.map f â‰¤ Î½.map f :=
Î» s hs, by simpa [hf, hs, hf.mono_measure h] using measure.le_iff'.1 h (f â»Â¹' s)
