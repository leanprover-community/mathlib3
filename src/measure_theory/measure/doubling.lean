/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import analysis.special_functions.log.base
import measure_theory.measure.measure_space_def

/-!
# Doubling measures

A doubling measure `Î¼` on a metric space is a measure for which there exists a constant `C` such
that for all sufficiently small radii `Îµ`, and for any centre, the measure of a ball of radius
`2 * Îµ` is bounded by `C` times the measure of the concentric ball of radius `Îµ`.

This file records basic files on doubling measures.

## Main definitions

  * `is_doubling_measure`: the definition of a doubling measure (as a typeclass).
  * `is_doubling_measure.doubling_constant`: a function yielding the doubling constant `C` appearing
  in the definition of a doubling measure.
-/

noncomputable theory

open set filter metric measure_theory topological_space
open_locale nnreal topological_space

/-- A measure `Î¼` is said to be a doubling measure if there exists a constant `C` such that for
all sufficiently small radii `Îµ`, and for any centre, the measure of a ball of radius `2 * Îµ` is
bounded by `C` times the measure of the concentric ball of radius `Îµ`.

Note: it is important that this definition makes a demand only for sufficiently small `Îµ`. For
example we want hyperbolic space to carry the instance `is_doubling_measure volume` but volumes grow
exponentially in hyperbolic space. To be really explicit, consider the hyperbolic plane of
curvature -1, the area of a disc of radius `Îµ` is `A(Îµ) = 2Ï€(cosh(Îµ) - 1)` so `A(2Îµ)/A(Îµ) ~ exp(Îµ)`.
-/
class is_doubling_measure {Î± : Type*} [metric_space Î±] [measurable_space Î±] (Î¼ : measure Î±) :=
(exists_measure_closed_ball_le_mul [] :
  âˆƒ (C : â„â‰¥0), âˆ€á¶  Îµ in ð“[>] 0, âˆ€ x, Î¼ (closed_ball x (2 * Îµ)) â‰¤ C * Î¼ (closed_ball x Îµ))

namespace is_doubling_measure

variables {Î± : Type*} [metric_space Î±] [measurable_space Î±] (Î¼ : measure Î±) [is_doubling_measure Î¼]

/-- A doubling constant for a doubling measure.

See also `is_doubling_measure.scaling_constant_of`. -/
def doubling_constant : â„â‰¥0 := classical.some $ exists_measure_closed_ball_le_mul Î¼

lemma exists_measure_closed_ball_le_mul' :
  âˆ€á¶  Îµ in ð“[>] 0, âˆ€ x, Î¼ (closed_ball x (2 * Îµ)) â‰¤ doubling_constant Î¼ * Î¼ (closed_ball x Îµ) :=
classical.some_spec $ exists_measure_closed_ball_le_mul Î¼

lemma exists_eventually_forall_measure_closed_ball_le_mul (K : â„) :
  âˆƒ (C : â„â‰¥0), âˆ€á¶  Îµ in ð“[>] 0, âˆ€ x t (ht : t â‰¤ K),
    Î¼ (closed_ball x (t * Îµ)) â‰¤ C * Î¼ (closed_ball x Îµ) :=
begin
  let C := doubling_constant Î¼,
  have hÎ¼ : âˆ€ (n : â„•), âˆ€á¶  Îµ in ð“[>] 0, âˆ€ x,
    Î¼ (closed_ball x (2^n * Îµ)) â‰¤ â†‘(C^n) * Î¼ (closed_ball x Îµ),
  { intros n,
    induction n with n ih, { simp, },
    replace ih := eventually_nhds_within_pos_mul_left (two_pos : 0 < (2 : â„)) ih,
    refine (ih.and (exists_measure_closed_ball_le_mul' Î¼)).mono (Î» Îµ hÎµ x, _),
    calc Î¼ (closed_ball x (2^(n + 1) * Îµ))
          = Î¼ (closed_ball x (2^n * (2 * Îµ))) : by rw [pow_succ', mul_assoc]
      ... â‰¤ â†‘(C^n) * Î¼ (closed_ball x (2 * Îµ)) : hÎµ.1 x
      ... â‰¤ â†‘(C^n) * (C * Î¼ (closed_ball x Îµ)) : ennreal.mul_left_mono (hÎµ.2 x)
      ... = â†‘(C^(n + 1)) * Î¼ (closed_ball x Îµ) : by rw [â† mul_assoc, pow_succ', ennreal.coe_mul], },
  rcases lt_or_le K 1 with hK | hK,
  { refine âŸ¨1, _âŸ©,
    simp only [ennreal.coe_one, one_mul],
    exact eventually_mem_nhds_within.mono (Î» Îµ hÎµ x t ht,
      measure_mono $ closed_ball_subset_closed_ball (by nlinarith [mem_Ioi.mp hÎµ])), },
  { refine âŸ¨C^âŒˆreal.logb 2 KâŒ‰â‚Š, ((hÎ¼ âŒˆreal.logb 2 KâŒ‰â‚Š).and eventually_mem_nhds_within).mono
      (Î» Îµ hÎµ x t ht, le_trans (measure_mono $ closed_ball_subset_closed_ball _) (hÎµ.1 x))âŸ©,
    refine mul_le_mul_of_nonneg_right (ht.trans _) (mem_Ioi.mp hÎµ.2).le,
    conv_lhs { rw â† real.rpow_logb two_pos (by norm_num) (by linarith : 0 < K), },
    rw â† real.rpow_nat_cast,
    exact real.rpow_le_rpow_of_exponent_le one_le_two (nat.le_ceil (real.logb 2 K)), },
end

/-- A variant of `is_doubling_measure.doubling_constant` which allows for scaling the radius by
values other than `2`. -/
def scaling_constant_of (K : â„) : â„â‰¥0 :=
max (classical.some $ exists_eventually_forall_measure_closed_ball_le_mul Î¼ K) 1

lemma eventually_measure_mul_le_scaling_constant_of_mul (K : â„) :
  âˆƒ (R : â„), 0 < R âˆ§ âˆ€ x t r (ht : t âˆˆ Ioc 0 K) (hr : r â‰¤ R),
    Î¼ (closed_ball x (t * r)) â‰¤ scaling_constant_of Î¼ K * Î¼ (closed_ball x r) :=
begin
  have h := classical.some_spec (exists_eventually_forall_measure_closed_ball_le_mul Î¼ K),
  rcases mem_nhds_within_Ioi_iff_exists_Ioc_subset.1 h with âŸ¨R, Rpos, hRâŸ©,
  refine âŸ¨R, Rpos, Î» x t r ht hr, _âŸ©,
  rcases lt_trichotomy r 0 with rneg|rfl|rpos,
  { have : t * r < 0, from mul_neg_of_pos_of_neg ht.1 rneg,
    simp only [closed_ball_eq_empty.2 this, measure_empty, zero_le'] },
  { simp only [mul_zero, closed_ball_zero],
    refine le_mul_of_one_le_of_le _ le_rfl,
    apply ennreal.one_le_coe_iff.2 (le_max_right _ _) },
  { apply (hR âŸ¨rpos, hrâŸ© x t ht.2).trans _,
    exact ennreal.mul_le_mul (ennreal.coe_le_coe.2 (le_max_left _ _)) le_rfl }
end

/-- A scale below which the doubling measure `Î¼` satisfies good rescaling properties when one
multiplies the radius of balls by at most `K`, as stated
in `measure_mul_le_scaling_constant_of_mul`. -/
def scaling_scale_of (K : â„) : â„ :=
(eventually_measure_mul_le_scaling_constant_of_mul Î¼ K).some

lemma scaling_scale_of_pos (K : â„) : 0 < scaling_scale_of Î¼ K :=
(eventually_measure_mul_le_scaling_constant_of_mul Î¼ K).some_spec.1

lemma measure_mul_le_scaling_constant_of_mul {K : â„} {x : Î±} {t r : â„}
  (ht : t âˆˆ Ioc 0 K) (hr : r â‰¤ scaling_scale_of Î¼ K) :
  Î¼ (closed_ball x (t * r)) â‰¤ scaling_constant_of Î¼ K * Î¼ (closed_ball x r) :=
(eventually_measure_mul_le_scaling_constant_of_mul Î¼ K).some_spec.2 x t r ht hr

end is_doubling_measure
