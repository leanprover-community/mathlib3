/-
Copyright (c) 2021 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import topology.continuous_function.bounded
import topology.algebra.module.weak_dual
import measure_theory.integral.bochner

/-!
# Finite measures

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the type of finite measures on a given measurable space. When the underlying
space has a topology and the measurable space structure (sigma algebra) is finer than the Borel
sigma algebra, then the type of finite measures is equipped with the topology of weak convergence
of measures. The topology of weak convergence is the coarsest topology w.r.t. which
for every bounded continuous `‚Ñù‚â•0`-valued function `f`, the integration of `f` against the
measure is continuous.

## Main definitions

The main definitions are
 * the type `measure_theory.finite_measure Œ©` with the topology of weak convergence;
 * `measure_theory.finite_measure.to_weak_dual_bcnn : finite_measure Œ© ‚Üí (weak_dual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0))`
   allowing to interpret a finite measure as a continuous linear functional on the space of
   bounded continuous nonnegative functions on `Œ©`. This is used for the definition of the
   topology of weak convergence.

## Main results

 * Finite measures `Œº` on `Œ©` give rise to continuous linear functionals on the space of
   bounded continuous nonnegative functions on `Œ©` via integration:
   `measure_theory.finite_measure.to_weak_dual_bcnn : finite_measure Œ© ‚Üí (weak_dual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0))`
 * `measure_theory.finite_measure.tendsto_iff_forall_integral_tendsto`: Convergence of finite
   measures is characterized by the convergence of integrals of all bounded continuous functions.
   This shows that the chosen definition of topology coincides with the common textbook definition
   of weak convergence of measures. A similar characterization by the convergence of integrals (in
   the `measure_theory.lintegral` sense) of all bounded continuous nonnegative functions is
   `measure_theory.finite_measure.tendsto_iff_forall_lintegral_tendsto`.

## Implementation notes

The topology of weak convergence of finite Borel measures is defined using a mapping from
`measure_theory.finite_measure Œ©` to `weak_dual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0)`, inheriting the topology from the
latter.

The implementation of `measure_theory.finite_measure Œ©` and is directly as a subtype of
`measure_theory.measure Œ©`, and the coercion to a function is the composition `ennreal.to_nnreal`
and the coercion to function of `measure_theory.measure Œ©`. Another alternative would have been to
use a bijection with `measure_theory.vector_measure Œ© ‚Ñù‚â•0` as an intermediate step. Some
considerations:
 * Potential advantages of using the `nnreal`-valued vector measure alternative:
   * The coercion to function would avoid need to compose with `ennreal.to_nnreal`, the
     `nnreal`-valued API could be more directly available.
 * Potential drawbacks of the vector measure alternative:
   * The coercion to function would lose monotonicity, as non-measurable sets would be defined to
     have measure 0.
   * No integration theory directly. E.g., the topology definition requires
     `measure_theory.lintegral` w.r.t. a coercion to `measure_theory.measure Œ©` in any case.

## References

* [Billingsley, *Convergence of probability measures*][billingsley1999]

## Tags

weak convergence of measures, finite measure

-/

noncomputable theory
open measure_theory
open set
open filter
open bounded_continuous_function
open_locale topology ennreal nnreal bounded_continuous_function

namespace measure_theory

namespace finite_measure

section finite_measure
/-! ### Finite measures

In this section we define the `Type` of `finite_measure Œ©`, when `Œ©` is a measurable space. Finite
measures on `Œ©` are a module over `‚Ñù‚â•0`.

If `Œ©` is moreover a topological space and the sigma algebra on `Œ©` is finer than the Borel sigma
algebra (i.e. `[opens_measurable_space Œ©]`), then `finite_measure Œ©` is equipped with the topology
of weak convergence of measures. This is implemented by defining a pairing of finite measures `Œº`
on `Œ©` with continuous bounded nonnegative functions `f : Œ© ‚Üí·µá ‚Ñù‚â•0` via integration, and using the
associated weak topology (essentially the weak-star topology on the dual of `Œ© ‚Üí·µá ‚Ñù‚â•0`).
-/

variables {Œ© : Type*} [measurable_space Œ©]

/-- Finite measures are defined as the subtype of measures that have the property of being finite
measures (i.e., their total mass is finite). -/
def _root_.measure_theory.finite_measure (Œ© : Type*) [measurable_space Œ©] : Type* :=
{Œº : measure Œ© // is_finite_measure Œº}

/-- A finite measure can be interpreted as a measure. -/
instance : has_coe (finite_measure Œ©) (measure_theory.measure Œ©) := coe_subtype

instance is_finite_measure (Œº : finite_measure Œ©) :
  is_finite_measure (Œº : measure Œ©) := Œº.prop

instance : has_coe_to_fun (finite_measure Œ©) (Œª _, set Œ© ‚Üí ‚Ñù‚â•0) :=
‚ü®Œª Œº s, (Œº s).to_nnreal‚ü©

lemma coe_fn_eq_to_nnreal_coe_fn_to_measure (ŒΩ : finite_measure Œ©) :
  (ŒΩ : set Œ© ‚Üí ‚Ñù‚â•0) = Œª s, ((ŒΩ : measure Œ©) s).to_nnreal := rfl

@[simp] lemma ennreal_coe_fn_eq_coe_fn_to_measure (ŒΩ : finite_measure Œ©) (s : set Œ©) :
  (ŒΩ s : ‚Ñù‚â•0‚àû) = (ŒΩ : measure Œ©) s := ennreal.coe_to_nnreal (measure_lt_top ‚ÜëŒΩ s).ne

@[simp] lemma val_eq_to_measure (ŒΩ : finite_measure Œ©) : ŒΩ.val = (ŒΩ : measure Œ©) := rfl

lemma coe_injective : function.injective (coe : finite_measure Œ© ‚Üí measure Œ©) :=
subtype.coe_injective

lemma apply_mono (Œº : finite_measure Œ©) {s‚ÇÅ s‚ÇÇ : set Œ©} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) :
  Œº s‚ÇÅ ‚â§ Œº s‚ÇÇ :=
begin
  change ((Œº : measure Œ©) s‚ÇÅ).to_nnreal ‚â§ ((Œº : measure Œ©) s‚ÇÇ).to_nnreal,
  have key : (Œº : measure Œ©) s‚ÇÅ ‚â§ (Œº : measure Œ©) s‚ÇÇ := (Œº : measure Œ©).mono h,
  apply (ennreal.to_nnreal_le_to_nnreal (measure_ne_top _ s‚ÇÅ) (measure_ne_top _ s‚ÇÇ)).mpr key,
end

/-- The (total) mass of a finite measure `Œº` is `Œº univ`, i.e., the cast to `nnreal` of
`(Œº : measure Œ©) univ`. -/
def mass (Œº : finite_measure Œ©) : ‚Ñù‚â•0 := Œº univ

@[simp] lemma ennreal_mass {Œº : finite_measure Œ©} :
  (Œº.mass : ‚Ñù‚â•0‚àû) = (Œº : measure Œ©) univ := ennreal_coe_fn_eq_coe_fn_to_measure Œº set.univ

instance has_zero : has_zero (finite_measure Œ©) :=
{ zero := ‚ü®0, measure_theory.is_finite_measure_zero‚ü© }

@[simp] lemma zero.mass : (0 : finite_measure Œ©).mass = 0 := rfl

@[simp] lemma mass_zero_iff (Œº : finite_measure Œ©) : Œº.mass = 0 ‚Üî Œº = 0 :=
begin
  refine ‚ü®Œª Œº_mass, _, (Œª hŒº, by simp only [hŒº, zero.mass])‚ü©,
  ext1,
  apply measure.measure_univ_eq_zero.mp,
  rwa [‚Üê ennreal_mass, ennreal.coe_eq_zero],
end

lemma mass_nonzero_iff (Œº : finite_measure Œ©) : Œº.mass ‚â† 0 ‚Üî Œº ‚â† 0 :=
begin
  rw not_iff_not,
  exact finite_measure.mass_zero_iff Œº,
end

@[ext] lemma eq_of_forall_measure_apply_eq (Œº ŒΩ : finite_measure Œ©)
  (h : ‚àÄ (s : set Œ©), measurable_set s ‚Üí (Œº : measure Œ©) s = (ŒΩ : measure Œ©) s) :
  Œº = ŒΩ :=
by { ext1, ext1 s s_mble, exact h s s_mble, }

lemma eq_of_forall_apply_eq (Œº ŒΩ : finite_measure Œ©)
  (h : ‚àÄ (s : set Œ©), measurable_set s ‚Üí Œº s = ŒΩ s) :
  Œº = ŒΩ :=
begin
  ext1 s s_mble,
  simpa [ennreal_coe_fn_eq_coe_fn_to_measure] using congr_arg (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) (h s s_mble),
end

instance : inhabited (finite_measure Œ©) := ‚ü®0‚ü©

instance : has_add (finite_measure Œ©) :=
{ add := Œª Œº ŒΩ, ‚ü®Œº + ŒΩ, measure_theory.is_finite_measure_add‚ü© }

variables {R : Type*} [has_smul R ‚Ñù‚â•0] [has_smul R ‚Ñù‚â•0‚àû] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù‚â•0‚àû]
  [is_scalar_tower R ‚Ñù‚â•0‚àû ‚Ñù‚â•0‚àû]

instance : has_smul R (finite_measure Œ©) :=
{ smul := Œª (c : R) Œº, ‚ü®c ‚Ä¢ Œº, measure_theory.is_finite_measure_smul_of_nnreal_tower‚ü©, }

@[simp, norm_cast] lemma coe_zero : (coe : finite_measure Œ© ‚Üí measure Œ©) 0 = 0 := rfl

@[simp, norm_cast] lemma coe_add (Œº ŒΩ : finite_measure Œ©) : ‚Üë(Œº + ŒΩ) = (‚ÜëŒº + ‚ÜëŒΩ : measure Œ©) := rfl

@[simp, norm_cast] lemma coe_smul (c : R) (Œº : finite_measure Œ©) :
  ‚Üë(c ‚Ä¢ Œº) = (c ‚Ä¢ ‚ÜëŒº : measure Œ©) := rfl

@[simp, norm_cast] lemma coe_fn_zero :
  (‚áë(0 : finite_measure Œ©) : set Œ© ‚Üí ‚Ñù‚â•0) = (0 : set Œ© ‚Üí ‚Ñù‚â•0) := by { funext, refl, }

@[simp, norm_cast] lemma coe_fn_add (Œº ŒΩ : finite_measure Œ©) :
  (‚áë(Œº + ŒΩ) : set Œ© ‚Üí ‚Ñù‚â•0) = (‚áëŒº + ‚áëŒΩ : set Œ© ‚Üí ‚Ñù‚â•0) :=
by { funext, simp [‚Üê ennreal.coe_eq_coe], }

@[simp, norm_cast] lemma coe_fn_smul [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù‚â•0] (c : R) (Œº : finite_measure Œ©) :
  (‚áë(c ‚Ä¢ Œº) : set Œ© ‚Üí ‚Ñù‚â•0) = c ‚Ä¢ (‚áëŒº : set Œ© ‚Üí ‚Ñù‚â•0) :=
by { funext, simp [‚Üê ennreal.coe_eq_coe, ennreal.coe_smul], }

instance : add_comm_monoid (finite_measure Œ©) :=
coe_injective.add_comm_monoid coe coe_zero coe_add (Œª _ _, coe_smul _ _)

/-- Coercion is an `add_monoid_hom`. -/
@[simps]
def coe_add_monoid_hom : finite_measure Œ© ‚Üí+ measure Œ© :=
{ to_fun := coe, map_zero' := coe_zero, map_add' := coe_add }

instance {Œ© : Type*} [measurable_space Œ©] : module ‚Ñù‚â•0 (finite_measure Œ©) :=
function.injective.module _ coe_add_monoid_hom coe_injective coe_smul

@[simp] lemma coe_fn_smul_apply [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù‚â•0]
  (c : R) (Œº : finite_measure Œ©) (s : set Œ©) :
  (c ‚Ä¢ Œº) s  = c ‚Ä¢ (Œº s) :=
by { simp only [coe_fn_smul, pi.smul_apply], }

/-- Restrict a finite measure Œº to a set A. -/
def restrict (Œº : finite_measure Œ©) (A : set Œ©) : finite_measure Œ© :=
{ val := (Œº : measure Œ©).restrict A,
  property := measure_theory.is_finite_measure_restrict Œº A, }

lemma restrict_measure_eq (Œº : finite_measure Œ©) (A : set Œ©) :
  (Œº.restrict A : measure Œ©) = (Œº : measure Œ©).restrict A := rfl

lemma restrict_apply_measure (Œº : finite_measure Œ©) (A : set Œ©)
  {s : set Œ©} (s_mble : measurable_set s) :
  (Œº.restrict A : measure Œ©) s = (Œº : measure Œ©) (s ‚à© A) :=
measure.restrict_apply s_mble

lemma restrict_apply (Œº : finite_measure Œ©) (A : set Œ©)
  {s : set Œ©} (s_mble : measurable_set s) :
  (Œº.restrict A) s = Œº (s ‚à© A) :=
begin
  apply congr_arg ennreal.to_nnreal,
  exact measure.restrict_apply s_mble,
end

lemma restrict_mass (Œº : finite_measure Œ©) (A : set Œ©) :
  (Œº.restrict A).mass = Œº A :=
by simp only [mass, restrict_apply Œº A measurable_set.univ, univ_inter]

lemma restrict_eq_zero_iff (Œº : finite_measure Œ©) (A : set Œ©) :
  Œº.restrict A = 0 ‚Üî Œº A = 0 :=
by rw [‚Üê mass_zero_iff, restrict_mass]

lemma restrict_nonzero_iff (Œº : finite_measure Œ©) (A : set Œ©) :
  Œº.restrict A ‚â† 0 ‚Üî Œº A ‚â† 0 :=
by rw [‚Üê mass_nonzero_iff, restrict_mass]

variables [topological_space Œ©]

/-- The pairing of a finite (Borel) measure `Œº` with a nonnegative bounded continuous
function is obtained by (Lebesgue) integrating the (test) function against the measure.
This is `finite_measure.test_against_nn`. -/
def test_against_nn (Œº : finite_measure Œ©) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) : ‚Ñù‚â•0 :=
(‚à´‚Åª œâ, f œâ ‚àÇ(Œº : measure Œ©)).to_nnreal

lemma _root_.bounded_continuous_function.nnreal.to_ennreal_comp_measurable {Œ© : Type*}
  [topological_space Œ©] [measurable_space Œ©] [opens_measurable_space Œ©] (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  measurable (Œª œâ, (f œâ : ‚Ñù‚â•0‚àû)) :=
measurable_coe_nnreal_ennreal.comp f.continuous.measurable

lemma _root_.measure_theory.lintegral_lt_top_of_bounded_continuous_to_nnreal
  (Œº : measure Œ©) [is_finite_measure Œº] (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  ‚à´‚Åª œâ, f œâ ‚àÇŒº < ‚àû :=
begin
  apply is_finite_measure.lintegral_lt_top_of_bounded_to_ennreal,
  use nndist f 0,
  intros x,
  have key := bounded_continuous_function.nnreal.upper_bound f x,
  rw ennreal.coe_le_coe,
  have eq : nndist f 0 = ‚ü®dist f 0, dist_nonneg‚ü©,
  { ext,
    simp only [real.coe_to_nnreal', max_eq_left_iff, subtype.coe_mk, coe_nndist], },
  rwa eq at key,
end

@[simp] lemma test_against_nn_coe_eq {Œº : finite_measure Œ©} {f : Œ© ‚Üí·µá ‚Ñù‚â•0} :
  (Œº.test_against_nn f : ‚Ñù‚â•0‚àû) = ‚à´‚Åª œâ, f œâ ‚àÇ(Œº : measure Œ©) :=
ennreal.coe_to_nnreal (lintegral_lt_top_of_bounded_continuous_to_nnreal _ f).ne

lemma test_against_nn_const (Œº : finite_measure Œ©) (c : ‚Ñù‚â•0) :
  Œº.test_against_nn (bounded_continuous_function.const Œ© c) = c * Œº.mass :=
by simp [‚Üê ennreal.coe_eq_coe]

lemma test_against_nn_mono (Œº : finite_measure Œ©)
  {f g : Œ© ‚Üí·µá ‚Ñù‚â•0} (f_le_g : (f : Œ© ‚Üí ‚Ñù‚â•0) ‚â§ g) :
  Œº.test_against_nn f ‚â§ Œº.test_against_nn g :=
begin
  simp only [‚Üêennreal.coe_le_coe, test_against_nn_coe_eq],
  exact lintegral_mono (Œª œâ, ennreal.coe_mono (f_le_g œâ)),
end

@[simp] lemma test_against_nn_zero (Œº : finite_measure Œ©) : Œº.test_against_nn 0 = 0 :=
by simpa only [zero_mul] using Œº.test_against_nn_const 0

@[simp] lemma test_against_nn_one (Œº : finite_measure Œ©) : Œº.test_against_nn 1 = Œº.mass :=
begin
  simp only [test_against_nn, coe_one, pi.one_apply, ennreal.coe_one, lintegral_one],
  refl,
end

@[simp] lemma zero.test_against_nn_apply (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  (0 : finite_measure Œ©).test_against_nn f = 0 :=
by simp only [test_against_nn, coe_zero, lintegral_zero_measure, ennreal.zero_to_nnreal]

lemma zero.test_against_nn : (0 : finite_measure Œ©).test_against_nn = 0 :=
by { funext, simp only [zero.test_against_nn_apply, pi.zero_apply], }

@[simp] lemma smul_test_against_nn_apply (c : ‚Ñù‚â•0) (Œº : finite_measure Œ©) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  (c ‚Ä¢ Œº).test_against_nn f  = c ‚Ä¢ (Œº.test_against_nn f) :=
by simp only [test_against_nn, coe_smul, smul_eq_mul, ‚Üê ennreal.smul_to_nnreal,
  ennreal.smul_def, lintegral_smul_measure]

variables [opens_measurable_space Œ©]

lemma test_against_nn_add (Œº : finite_measure Œ©) (f‚ÇÅ f‚ÇÇ : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn (f‚ÇÅ + f‚ÇÇ) = Œº.test_against_nn f‚ÇÅ + Œº.test_against_nn f‚ÇÇ :=
begin
  simp only [‚Üêennreal.coe_eq_coe, bounded_continuous_function.coe_add, ennreal.coe_add,
             pi.add_apply, test_against_nn_coe_eq],
  exact lintegral_add_left (bounded_continuous_function.nnreal.to_ennreal_comp_measurable _) _
end

lemma test_against_nn_smul [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù‚â•0] [pseudo_metric_space R] [has_zero R]
  [has_bounded_smul R ‚Ñù‚â•0]
  (Œº : finite_measure Œ©) (c : R) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn (c ‚Ä¢ f) = c ‚Ä¢ Œº.test_against_nn f :=
begin
  simp only [‚Üêennreal.coe_eq_coe, bounded_continuous_function.coe_smul,
             test_against_nn_coe_eq, ennreal.coe_smul],
  simp_rw [‚Üêsmul_one_smul ‚Ñù‚â•0‚àû c (f _ : ‚Ñù‚â•0‚àû), ‚Üêsmul_one_smul ‚Ñù‚â•0‚àû c (lintegral _ _ : ‚Ñù‚â•0‚àû),
           smul_eq_mul],
  exact @lintegral_const_mul _ _ (Œº : measure Œ©) (c ‚Ä¢ 1)  _
                   (bounded_continuous_function.nnreal.to_ennreal_comp_measurable f),
end

lemma test_against_nn_lipschitz_estimate (Œº : finite_measure Œ©) (f g : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn f ‚â§ Œº.test_against_nn g + (nndist f g) * Œº.mass :=
begin
  simp only [‚ÜêŒº.test_against_nn_const (nndist f g), ‚Üêtest_against_nn_add, ‚Üêennreal.coe_le_coe,
             bounded_continuous_function.coe_add, const_apply, ennreal.coe_add, pi.add_apply,
             coe_nnreal_ennreal_nndist, test_against_nn_coe_eq],
  apply lintegral_mono,
  have le_dist : ‚àÄ œâ, dist (f œâ) (g œâ) ‚â§ nndist f g :=
  bounded_continuous_function.dist_coe_le_dist,
  intros œâ,
  have le' : f œâ ‚â§ g œâ + nndist f g,
  { apply (nnreal.le_add_nndist (f œâ) (g œâ)).trans,
    rw add_le_add_iff_left,
    exact dist_le_coe.mp (le_dist œâ), },
  have le : (f œâ : ‚Ñù‚â•0‚àû) ‚â§ (g œâ : ‚Ñù‚â•0‚àû) + (nndist f g),
  by { rw ‚Üêennreal.coe_add, exact ennreal.coe_mono le', },
  rwa [coe_nnreal_ennreal_nndist] at le,
end

lemma test_against_nn_lipschitz (Œº : finite_measure Œ©) :
  lipschitz_with Œº.mass (Œª (f : Œ© ‚Üí·µá ‚Ñù‚â•0), Œº.test_against_nn f) :=
begin
  rw lipschitz_with_iff_dist_le_mul,
  intros f‚ÇÅ f‚ÇÇ,
  suffices : abs (Œº.test_against_nn f‚ÇÅ - Œº.test_against_nn f‚ÇÇ : ‚Ñù) ‚â§ Œº.mass * (dist f‚ÇÅ f‚ÇÇ),
  { rwa nnreal.dist_eq, },
  apply abs_le.mpr,
  split,
  { have key' := Œº.test_against_nn_lipschitz_estimate f‚ÇÇ f‚ÇÅ,
    rw mul_comm at key',
    suffices : ‚Üë(Œº.test_against_nn f‚ÇÇ) ‚â§ ‚Üë(Œº.test_against_nn f‚ÇÅ) + ‚Üë(Œº.mass) * dist f‚ÇÅ f‚ÇÇ,
    { linarith, },
    have key := nnreal.coe_mono key',
    rwa [nnreal.coe_add, nnreal.coe_mul, nndist_comm] at key, },
  { have key' := Œº.test_against_nn_lipschitz_estimate f‚ÇÅ f‚ÇÇ,
    rw mul_comm at key',
    suffices : ‚Üë(Œº.test_against_nn f‚ÇÅ) ‚â§ ‚Üë(Œº.test_against_nn f‚ÇÇ) + ‚Üë(Œº.mass) * dist f‚ÇÅ f‚ÇÇ,
    { linarith, },
    have key := nnreal.coe_mono key',
    rwa [nnreal.coe_add, nnreal.coe_mul] at key, },
end

/-- Finite measures yield elements of the `weak_dual` of bounded continuous nonnegative
functions via `measure_theory.finite_measure.test_against_nn`, i.e., integration. -/
def to_weak_dual_bcnn (Œº : finite_measure Œ©) :
  weak_dual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0) :=
{ to_fun := Œª f, Œº.test_against_nn f,
  map_add' := test_against_nn_add Œº,
  map_smul' := test_against_nn_smul Œº,
  cont := Œº.test_against_nn_lipschitz.continuous, }

@[simp] lemma coe_to_weak_dual_bcnn (Œº : finite_measure Œ©) :
  ‚áëŒº.to_weak_dual_bcnn = Œº.test_against_nn := rfl

@[simp] lemma to_weak_dual_bcnn_apply (Œº : finite_measure Œ©) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  Œº.to_weak_dual_bcnn f = (‚à´‚Åª x, f x ‚àÇ(Œº : measure Œ©)).to_nnreal := rfl

/-- The topology of weak convergence on `measure_theory.finite_measure Œ©` is inherited (induced)
from the weak-* topology on `weak_dual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0)` via the function
`measure_theory.finite_measure.to_weak_dual_bcnn`. -/
instance : topological_space (finite_measure Œ©) :=
topological_space.induced to_weak_dual_bcnn infer_instance

lemma to_weak_dual_bcnn_continuous :
  continuous (@to_weak_dual_bcnn Œ© _ _ _) :=
continuous_induced_dom

/- Integration of (nonnegative bounded continuous) test functions against finite Borel measures
depends continuously on the measure. -/
lemma continuous_test_against_nn_eval (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  continuous (Œª (Œº : finite_measure Œ©), Œº.test_against_nn f) :=
(by apply (weak_bilin.eval_continuous _ _).comp to_weak_dual_bcnn_continuous :
  continuous ((Œª œÜ : weak_dual ‚Ñù‚â•0 (Œ© ‚Üí·µá ‚Ñù‚â•0), œÜ f) ‚àò to_weak_dual_bcnn))

/-- The total mass of a finite measure depends continuously on the measure. -/
lemma continuous_mass : continuous (Œª (Œº : finite_measure Œ©), Œº.mass) :=
by { simp_rw ‚Üêtest_against_nn_one, exact continuous_test_against_nn_eval 1, }

/-- Convergence of finite measures implies the convergence of their total masses. -/
lemma _root_.filter.tendsto.mass {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measure Œ©} {Œº : finite_measure Œ©} (h : tendsto Œºs F (ùìù Œº)) :
  tendsto (Œª i, (Œºs i).mass) F (ùìù Œº.mass) :=
(continuous_mass.tendsto Œº).comp h

lemma tendsto_iff_weak_star_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measure Œ©} {Œº : finite_measure Œ©} :
  tendsto Œºs F (ùìù Œº) ‚Üî tendsto (Œª i, (Œºs(i)).to_weak_dual_bcnn) F (ùìù Œº.to_weak_dual_bcnn) :=
inducing.tendsto_nhds_iff ‚ü®rfl‚ü©

theorem tendsto_iff_forall_to_weak_dual_bcnn_tendsto
  {Œ≥ : Type*} {F : filter Œ≥} {Œºs : Œ≥ ‚Üí finite_measure Œ©} {Œº : finite_measure Œ©} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ© ‚Üí·µá ‚Ñù‚â•0), tendsto (Œª i, (Œºs i).to_weak_dual_bcnn f) F (ùìù (Œº.to_weak_dual_bcnn f)) :=
by { rw [tendsto_iff_weak_star_tendsto, tendsto_iff_forall_eval_tendsto_top_dual_pairing], refl, }

theorem tendsto_iff_forall_test_against_nn_tendsto
  {Œ≥ : Type*} {F : filter Œ≥} {Œºs : Œ≥ ‚Üí finite_measure Œ©} {Œº : finite_measure Œ©} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ© ‚Üí·µá ‚Ñù‚â•0), tendsto (Œª i, (Œºs i).test_against_nn f) F (ùìù (Œº.test_against_nn f)) :=
by { rw finite_measure.tendsto_iff_forall_to_weak_dual_bcnn_tendsto, refl, }

/-- If the total masses of finite measures tend to zero, then the measures tend to
zero. This formulation concerns the associated functionals on bounded continuous
nonnegative test functions. See `finite_measure.tendsto_zero_of_tendsto_zero_mass` for
a formulation stating the weak convergence of measures. -/
lemma tendsto_zero_test_against_nn_of_tendsto_zero_mass
  {Œ≥ : Type*} {F : filter Œ≥} {Œºs : Œ≥ ‚Üí finite_measure Œ©}
  (mass_lim : tendsto (Œª i, (Œºs i).mass) F (ùìù 0)) (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  tendsto (Œª i, (Œºs i).test_against_nn f) F (ùìù 0) :=
begin
  apply tendsto_iff_dist_tendsto_zero.mpr,
  have obs := Œª i, (Œºs i).test_against_nn_lipschitz_estimate f 0,
  simp_rw [test_against_nn_zero, zero_add] at obs,
  simp_rw (show ‚àÄ i, dist ((Œºs i).test_against_nn f) 0 = (Œºs i).test_against_nn f,
    by simp only [dist_nndist, nnreal.nndist_zero_eq_val', eq_self_iff_true,
                  implies_true_iff]),
  refine squeeze_zero (Œª i, nnreal.coe_nonneg _) obs _,
  simp_rw nnreal.coe_mul,
  have lim_pair : tendsto (Œª i, (‚ü®nndist f 0, (Œºs i).mass‚ü© : ‚Ñù √ó ‚Ñù)) F (ùìù (‚ü®nndist f 0, 0‚ü©)),
  { refine (prod.tendsto_iff _ _).mpr ‚ü®tendsto_const_nhds, _‚ü©,
    exact (nnreal.continuous_coe.tendsto 0).comp mass_lim, },
  have key := tendsto_mul.comp lim_pair,
  rwa mul_zero at key,
end

/-- If the total masses of finite measures tend to zero, then the measures tend to zero. -/
lemma tendsto_zero_of_tendsto_zero_mass {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measure Œ©} (mass_lim : tendsto (Œª i, (Œºs i).mass) F (ùìù 0)) :
  tendsto Œºs F (ùìù 0) :=
begin
  rw tendsto_iff_forall_test_against_nn_tendsto,
  intro f,
  convert tendsto_zero_test_against_nn_of_tendsto_zero_mass mass_lim f,
  rw [zero.test_against_nn_apply],
end

/-- A characterization of weak convergence in terms of integrals of bounded continuous
nonnegative functions. -/
theorem tendsto_iff_forall_lintegral_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measure Œ©} {Œº : finite_measure Œ©} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ© ‚Üí·µá ‚Ñù‚â•0),
    tendsto (Œª i, (‚à´‚Åª x, (f x) ‚àÇ(Œºs(i) : measure Œ©))) F (ùìù ((‚à´‚Åª x, (f x) ‚àÇ(Œº : measure Œ©)))) :=
begin
  rw tendsto_iff_forall_to_weak_dual_bcnn_tendsto,
  simp_rw [to_weak_dual_bcnn_apply _ _, ‚Üêtest_against_nn_coe_eq,
           ennreal.tendsto_coe, ennreal.to_nnreal_coe],
end

end finite_measure -- section

section finite_measure_bounded_convergence
/-! ### Bounded convergence results for finite measures

This section is about bounded convergence theorems for finite measures.
-/

variables {Œ© : Type*} [measurable_space Œ©] [topological_space Œ©] [opens_measurable_space Œ©]

/-- A bounded convergence theorem for a finite measure:
If bounded continuous non-negative functions are uniformly bounded by a constant and tend to a
limit, then their integrals against the finite measure tend to the integral of the limit.
This formulation assumes:
 * the functions tend to a limit along a countably generated filter;
 * the limit is in the almost everywhere sense;
 * boundedness holds almost everywhere;
 * integration is `measure_theory.lintegral`, i.e., the functions and their integrals are
   `‚Ñù‚â•0‚àû`-valued.
-/
lemma tendsto_lintegral_nn_filter_of_le_const {Œπ : Type*} {L : filter Œπ} [L.is_countably_generated]
  (Œº : measure Œ©) [is_finite_measure Œº] {fs : Œπ ‚Üí (Œ© ‚Üí·µá ‚Ñù‚â•0)} {c : ‚Ñù‚â•0}
  (fs_le_const : ‚àÄ·∂† i in L, ‚àÄ·µê (œâ : Œ©) ‚àÇŒº, fs i œâ ‚â§ c) {f : Œ© ‚Üí ‚Ñù‚â•0}
  (fs_lim : ‚àÄ·µê (œâ : Œ©) ‚àÇŒº, tendsto (Œª i, fs i œâ) L (ùìù (f œâ))) :
  tendsto (Œª i, (‚à´‚Åª œâ, fs i œâ ‚àÇŒº)) L (ùìù (‚à´‚Åª œâ, (f œâ) ‚àÇŒº)) :=
begin
  simpa only using tendsto_lintegral_filter_of_dominated_convergence (Œª _, c)
    (eventually_of_forall ((Œª i, (ennreal.continuous_coe.comp (fs i).continuous).measurable)))
    _ ((@lintegral_const_lt_top _ _ Œº _ _ (@ennreal.coe_ne_top c)).ne) _,
  { simpa only [ennreal.coe_le_coe] using fs_le_const, },
  { simpa only [ennreal.tendsto_coe] using fs_lim, },
end

/-- A bounded convergence theorem for a finite measure:
If a sequence of bounded continuous non-negative functions are uniformly bounded by a constant
and tend pointwise to a limit, then their integrals (`measure_theory.lintegral`) against the finite
measure tend to the integral of the limit.

A related result with more general assumptions is
`measure_theory.finite_measure.tendsto_lintegral_nn_filter_of_le_const`.
-/
lemma tendsto_lintegral_nn_of_le_const (Œº : finite_measure Œ©) {fs : ‚Ñï ‚Üí (Œ© ‚Üí·µá ‚Ñù‚â•0)} {c : ‚Ñù‚â•0}
  (fs_le_const : ‚àÄ n œâ, fs n œâ ‚â§ c) {f : Œ© ‚Üí ‚Ñù‚â•0}
  (fs_lim : ‚àÄ œâ, tendsto (Œª n, fs n œâ) at_top (ùìù (f œâ))) :
  tendsto (Œª n, (‚à´‚Åª œâ, fs n œâ ‚àÇ(Œº : measure Œ©))) at_top (ùìù (‚à´‚Åª œâ, (f œâ) ‚àÇ(Œº : measure Œ©))) :=
tendsto_lintegral_nn_filter_of_le_const Œº
  (eventually_of_forall (Œª n, eventually_of_forall (fs_le_const n))) (eventually_of_forall fs_lim)

/-- A bounded convergence theorem for a finite measure:
If bounded continuous non-negative functions are uniformly bounded by a constant and tend to a
limit, then their integrals against the finite measure tend to the integral of the limit.
This formulation assumes:
 * the functions tend to a limit along a countably generated filter;
 * the limit is in the almost everywhere sense;
 * boundedness holds almost everywhere;
 * integration is the pairing against non-negative continuous test functions
   (`measure_theory.finite_measure.test_against_nn`).

A related result using `measure_theory.lintegral` for integration is
`measure_theory.finite_measure.tendsto_lintegral_nn_filter_of_le_const`.
-/
lemma tendsto_test_against_nn_filter_of_le_const {Œπ : Type*} {L : filter Œπ}
  [L.is_countably_generated] {Œº : finite_measure Œ©} {fs : Œπ ‚Üí (Œ© ‚Üí·µá ‚Ñù‚â•0)} {c : ‚Ñù‚â•0}
  (fs_le_const : ‚àÄ·∂† i in L, ‚àÄ·µê (œâ : Œ©) ‚àÇ(Œº : measure Œ©), fs i œâ ‚â§ c) {f : Œ© ‚Üí·µá ‚Ñù‚â•0}
  (fs_lim : ‚àÄ·µê (œâ : Œ©) ‚àÇ(Œº : measure Œ©), tendsto (Œª i, fs i œâ) L (ùìù (f œâ))) :
  tendsto (Œª i, Œº.test_against_nn (fs i)) L (ùìù (Œº.test_against_nn f)) :=
begin
  apply (ennreal.tendsto_to_nnreal
         (lintegral_lt_top_of_bounded_continuous_to_nnreal (Œº : measure Œ©) f).ne).comp,
  exact tendsto_lintegral_nn_filter_of_le_const Œº fs_le_const fs_lim,
end

/-- A bounded convergence theorem for a finite measure:
If a sequence of bounded continuous non-negative functions are uniformly bounded by a constant and
tend pointwise to a limit, then their integrals (`measure_theory.finite_measure.test_against_nn`)
against the finite measure tend to the integral of the limit.

Related results:
 * `measure_theory.finite_measure.tendsto_test_against_nn_filter_of_le_const`:
   more general assumptions
 * `measure_theory.finite_measure.tendsto_lintegral_nn_of_le_const`:
   using `measure_theory.lintegral` for integration.
-/
lemma tendsto_test_against_nn_of_le_const {Œº : finite_measure Œ©}
  {fs : ‚Ñï ‚Üí (Œ© ‚Üí·µá ‚Ñù‚â•0)} {c : ‚Ñù‚â•0} (fs_le_const : ‚àÄ n œâ, fs n œâ ‚â§ c) {f : Œ© ‚Üí·µá ‚Ñù‚â•0}
  (fs_lim : ‚àÄ œâ, tendsto (Œª n, fs n œâ) at_top (ùìù (f œâ))) :
  tendsto (Œª n, Œº.test_against_nn (fs n)) at_top (ùìù (Œº.test_against_nn f)) :=
tendsto_test_against_nn_filter_of_le_const
  (eventually_of_forall (Œª n, eventually_of_forall (fs_le_const n))) (eventually_of_forall fs_lim)

end finite_measure_bounded_convergence -- section

section finite_measure_convergence_by_bounded_continuous_functions
/-! ### Weak convergence of finite measures with bounded continuous real-valued functions

In this section we characterize the weak convergence of finite measures by the usual (defining)
condition that the integrals of all bounded continuous real-valued functions converge.
-/

variables {Œ© : Type*} [measurable_space Œ©] [topological_space Œ©] [opens_measurable_space Œ©]

lemma integrable_of_bounded_continuous_to_nnreal
  (Œº : measure Œ©) [is_finite_measure Œº] (f : Œ© ‚Üí·µá ‚Ñù‚â•0) :
  integrable ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù) ‚àò ‚áëf) Œº :=
begin
  refine ‚ü®(nnreal.continuous_coe.comp f.continuous).measurable.ae_strongly_measurable, _‚ü©,
  simp only [has_finite_integral, nnreal.nnnorm_eq],
  exact lintegral_lt_top_of_bounded_continuous_to_nnreal _ f,
end

lemma integrable_of_bounded_continuous_to_real
  (Œº : measure Œ©) [is_finite_measure Œº] (f : Œ© ‚Üí·µá ‚Ñù) :
  integrable ‚áëf Œº :=
begin
  refine ‚ü®f.continuous.measurable.ae_strongly_measurable, _‚ü©,
  have aux : (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù) ‚àò ‚áëf.nnnorm = (Œª x, ‚Äñf x‚Äñ),
  { ext œâ,
    simp only [function.comp_app, bounded_continuous_function.nnnorm_coe_fun_eq, coe_nnnorm], },
  apply (has_finite_integral_iff_norm ‚áëf).mpr,
  rw ‚Üê of_real_integral_eq_lintegral_of_real,
  { exact ennreal.of_real_lt_top, },
  { exact aux ‚ñ∏ integrable_of_bounded_continuous_to_nnreal Œº f.nnnorm, },
  { exact eventually_of_forall (Œª œâ, norm_nonneg (f œâ)), },
end

lemma _root_.bounded_continuous_function.integral_eq_integral_nnreal_part_sub
  (Œº : measure Œ©) [is_finite_measure Œº] (f : Œ© ‚Üí·µá ‚Ñù) :
  ‚à´ œâ, f œâ ‚àÇŒº = ‚à´ œâ, f.nnreal_part œâ ‚àÇŒº - ‚à´ œâ, (-f).nnreal_part œâ ‚àÇŒº :=
by simp only [f.self_eq_nnreal_part_sub_nnreal_part_neg,
              pi.sub_apply, integral_sub, integrable_of_bounded_continuous_to_nnreal]

lemma lintegral_lt_top_of_bounded_continuous_to_real
  {Œ© : Type*} [measurable_space Œ©] [topological_space Œ©] (Œº : measure Œ©) [is_finite_measure Œº]
  (f : Œ© ‚Üí·µá ‚Ñù) :
  ‚à´‚Åª œâ, ennreal.of_real (f œâ) ‚àÇŒº < ‚àû :=
lintegral_lt_top_of_bounded_continuous_to_nnreal _ f.nnreal_part

theorem tendsto_of_forall_integral_tendsto
  {Œ≥ : Type*} {F : filter Œ≥} {Œºs : Œ≥ ‚Üí finite_measure Œ©} {Œº : finite_measure Œ©}
  (h : (‚àÄ (f : Œ© ‚Üí·µá ‚Ñù),
       tendsto (Œª i, (‚à´ x, (f x) ‚àÇ(Œºs i : measure Œ©))) F (ùìù ((‚à´ x, (f x) ‚àÇ(Œº : measure Œ©)))))) :
  tendsto Œºs F (ùìù Œº) :=
begin
  apply (@tendsto_iff_forall_lintegral_tendsto Œ© _ _ _ Œ≥ F Œºs Œº).mpr,
  intro f,
  have key := @ennreal.tendsto_to_real_iff _ F
              _ (Œª i, (lintegral_lt_top_of_bounded_continuous_to_nnreal (Œºs i : measure Œ©) f).ne)
              _ (lintegral_lt_top_of_bounded_continuous_to_nnreal (Œº : measure Œ©) f).ne,
  simp only [ennreal.of_real_coe_nnreal] at key,
  apply key.mp,
  have lip : lipschitz_with 1 (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù), from isometry_subtype_coe.lipschitz,
  set f‚ÇÄ := bounded_continuous_function.comp _ lip f with def_f‚ÇÄ,
  have f‚ÇÄ_eq : ‚áëf‚ÇÄ = (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù) ‚àò ‚áëf, by refl,
  have f‚ÇÄ_nn : 0 ‚â§ ‚áëf‚ÇÄ, from Œª _, by simp only [f‚ÇÄ_eq, pi.zero_apply, nnreal.zero_le_coe],
  have f‚ÇÄ_ae_nn : 0 ‚â§·µê[(Œº : measure Œ©)] ‚áëf‚ÇÄ, from eventually_of_forall f‚ÇÄ_nn,
  have f‚ÇÄ_ae_nns : ‚àÄ i, 0 ‚â§·µê[(Œºs i : measure Œ©)] ‚áëf‚ÇÄ, from Œª i, eventually_of_forall f‚ÇÄ_nn,
  have aux := integral_eq_lintegral_of_nonneg_ae f‚ÇÄ_ae_nn
              f‚ÇÄ.continuous.measurable.ae_strongly_measurable,
  have auxs := Œª i, integral_eq_lintegral_of_nonneg_ae (f‚ÇÄ_ae_nns i)
              f‚ÇÄ.continuous.measurable.ae_strongly_measurable,
  simp only [f‚ÇÄ_eq, ennreal.of_real_coe_nnreal] at aux auxs,
  simpa only [‚Üêaux, ‚Üêauxs] using h f‚ÇÄ,
end

lemma _root_.bounded_continuous_function.nnreal.to_real_lintegral_eq_integral
  (f : Œ© ‚Üí·µá ‚Ñù‚â•0) (Œº : measure Œ©) :
  (‚à´‚Åª x, (f x : ‚Ñù‚â•0‚àû) ‚àÇŒº).to_real = (‚à´ x, f x ‚àÇŒº) :=
begin
  rw integral_eq_lintegral_of_nonneg_ae _
     (nnreal.continuous_coe.comp f.continuous).measurable.ae_strongly_measurable,
  { simp only [ennreal.of_real_coe_nnreal], },
  { apply eventually_of_forall,
    simp only [pi.zero_apply, nnreal.zero_le_coe, implies_true_iff], },
end

/-- A characterization of weak convergence in terms of integrals of bounded continuous
real-valued functions. -/
theorem tendsto_iff_forall_integral_tendsto
  {Œ≥ : Type*} {F : filter Œ≥} {Œºs : Œ≥ ‚Üí finite_measure Œ©} {Œº : finite_measure Œ©} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ© ‚Üí·µá ‚Ñù),
    tendsto (Œª i, (‚à´ x, (f x) ‚àÇ(Œºs i : measure Œ©))) F (ùìù ((‚à´ x, (f x) ‚àÇ(Œº : measure Œ©)))) :=
begin
  refine ‚ü®_, tendsto_of_forall_integral_tendsto‚ü©,
  rw tendsto_iff_forall_lintegral_tendsto,
  intros h f,
  simp_rw bounded_continuous_function.integral_eq_integral_nnreal_part_sub,
  set f_pos := f.nnreal_part with def_f_pos,
  set f_neg := (-f).nnreal_part with def_f_neg,
  have tends_pos := (ennreal.tendsto_to_real
    ((lintegral_lt_top_of_bounded_continuous_to_nnreal (Œº : measure Œ©) f_pos).ne)).comp (h f_pos),
  have tends_neg := (ennreal.tendsto_to_real
    ((lintegral_lt_top_of_bounded_continuous_to_nnreal (Œº : measure Œ©) f_neg).ne)).comp (h f_neg),
  have aux : ‚àÄ (g : Œ© ‚Üí·µá ‚Ñù‚â•0), ennreal.to_real ‚àò (Œª (i : Œ≥), ‚à´‚Åª (x : Œ©), ‚Üë(g x) ‚àÇ(Œºs i : measure Œ©))
         = Œª (i : Œ≥), (‚à´‚Åª (x : Œ©), ‚Üë(g x) ‚àÇ(Œºs i : measure Œ©)).to_real, from Œª _, rfl,
  simp_rw [aux, bounded_continuous_function.nnreal.to_real_lintegral_eq_integral]
          at tends_pos tends_neg,
  exact tendsto.sub tends_pos tends_neg,
end

end finite_measure_convergence_by_bounded_continuous_functions -- section

end finite_measure -- namespace

end measure_theory -- namespace
