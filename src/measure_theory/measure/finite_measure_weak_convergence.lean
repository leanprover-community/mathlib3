/-
Copyright (c) 2021 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import measure_theory.measure.measure_space
import measure_theory.integral.bochner
import topology.continuous_function.bounded
import topology.algebra.module.weak_dual

/-!
# Weak convergence of (finite) measures

This file defines the topology of weak convergence of finite measures and probability measures
on topological spaces. The topology of weak convergence is the coarsest topology w.r.t. which
for every bounded continuous `‚Ñù‚â•0`-valued function `f`, the integration of `f` against the
measure is continuous.

TODOs:
* Prove that an equivalent definition of the topologies is obtained requiring continuity of
  integration of bounded continuous `‚Ñù`-valued functions instead.
* Include the portmanteau theorem on characterizations of weak convergence of (Borel) probability
  measures.

## Main definitions

The main definitions are the
 * types `finite_measure Œ±` and `probability_measure Œ±` with topologies of weak convergence;
 * `to_weak_dual_bcnn : finite_measure Œ± ‚Üí (weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0))`
   allowing to interpret a finite measure as a continuous linear functional on the space of
   bounded continuous nonnegative functions on `Œ±`. This is used for the definition of the
   topology of weak convergence.

## Main results

 * Finite measures `Œº` on `Œ±` give rise to continuous linear functionals on the space of
   bounded continuous nonnegative functions on `Œ±` via integration:
   `to_weak_dual_bcnn : finite_measure Œ± ‚Üí (weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0))`.
 * `tendsto_iff_forall_lintegral_tendsto`: Convergence of finite measures and probability measures
   is characterized by the convergence of integrals of all bounded continuous (nonnegative)
   functions. This essentially shows that the given definition of topology corresponds to the
   common textbook definition of weak convergence of measures.

TODO:
* Portmanteau theorem.

## Notations

No new notation is introduced.

## Implementation notes

The topology of weak convergence of finite Borel measures will be defined using a mapping from
`finite_measure Œ±` to `weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0)`, inheriting the topology from the latter.

The current implementation of `finite_measure Œ±` and `probability_measure Œ±` is directly as
subtypes of `measure Œ±`, and the coercion to a function is the composition `ennreal.to_nnreal`
and the coercion to function of `measure Œ±`. Another alternative would be to use a bijection
with `vector_measure Œ± ‚Ñù‚â•0` as an intermediate step. The choice of implementation should not have
drastic downstream effects, so it can be changed later if appropriate.

Potential advantages of using the `nnreal`-valued vector measure alternative:
 * The coercion to function would avoid need to compose with `ennreal.to_nnreal`, the
   `nnreal`-valued API could be more directly available.
Potential drawbacks of the vector measure alternative:
 * The coercion to function would lose monotonicity, as non-measurable sets would be defined to
   have measure 0.
 * No integration theory directly. E.g., the topology definition requires `lintegral` w.r.t.
   a coercion to `measure Œ±` in any case.

## References

* [Billingsley, *Convergence of probability measures*][billingsley1999]

## Tags

weak convergence of measures, finite measure, probability measure

-/

noncomputable theory
open measure_theory
open set
open filter
open bounded_continuous_function
open_locale topological_space ennreal nnreal bounded_continuous_function

namespace measure_theory

variables {Œ± : Type*} [measurable_space Œ±]

/-- Finite measures are defined as the subtype of measures that have the property of being finite
measures (i.e., their total mass is finite). -/
def finite_measure (Œ± : Type*) [measurable_space Œ±] : Type* :=
{Œº : measure Œ± // is_finite_measure Œº}

namespace finite_measure

/-- A finite measure can be interpreted as a measure. -/
instance : has_coe (finite_measure Œ±) (measure_theory.measure Œ±) := coe_subtype

instance is_finite_measure (Œº : finite_measure Œ±) :
  is_finite_measure (Œº : measure Œ±) := Œº.prop

instance : has_coe_to_fun (finite_measure Œ±) (Œª _, set Œ± ‚Üí ‚Ñù‚â•0) :=
‚ü®Œª Œº s, (Œº s).to_nnreal‚ü©

lemma coe_fn_eq_to_nnreal_coe_fn_to_measure (ŒΩ : finite_measure Œ±) :
  (ŒΩ : set Œ± ‚Üí ‚Ñù‚â•0) = Œª s, ((ŒΩ : measure Œ±) s).to_nnreal := rfl

@[simp] lemma ennreal_coe_fn_eq_coe_fn_to_measure (ŒΩ : finite_measure Œ±) (s : set Œ±) :
  (ŒΩ s : ‚Ñù‚â•0‚àû) = (ŒΩ : measure Œ±) s := ennreal.coe_to_nnreal (measure_lt_top ‚ÜëŒΩ s).ne

@[simp] lemma val_eq_to_measure (ŒΩ : finite_measure Œ±) : ŒΩ.val = (ŒΩ : measure Œ±) := rfl

lemma coe_injective : function.injective (coe : finite_measure Œ± ‚Üí measure Œ±) :=
subtype.coe_injective

/-- The (total) mass of a finite measure `Œº` is `Œº univ`, i.e., the cast to `nnreal` of
`(Œº : measure Œ±) univ`. -/
def mass (Œº : finite_measure Œ±) : ‚Ñù‚â•0 := Œº univ

@[simp] lemma ennreal_mass {Œº : finite_measure Œ±} :
  (Œº.mass : ‚Ñù‚â•0‚àû) = (Œº : measure Œ±) univ := ennreal_coe_fn_eq_coe_fn_to_measure Œº set.univ

instance has_zero : has_zero (finite_measure Œ±) :=
{ zero := ‚ü®0, measure_theory.is_finite_measure_zero‚ü© }

instance : inhabited (finite_measure Œ±) := ‚ü®0‚ü©

instance : has_add (finite_measure Œ±) :=
{ add := Œª Œº ŒΩ, ‚ü®Œº + ŒΩ, measure_theory.is_finite_measure_add‚ü© }

variables {R : Type*} [has_scalar R ‚Ñù‚â•0] [has_scalar R ‚Ñù‚â•0‚àû] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù‚â•0‚àû]
  [is_scalar_tower R ‚Ñù‚â•0‚àû ‚Ñù‚â•0‚àû]

instance : has_scalar R (finite_measure Œ±) :=
{ smul := Œª (c : R) Œº, ‚ü®c ‚Ä¢ Œº, measure_theory.is_finite_measure_smul_of_nnreal_tower‚ü©, }

@[simp, norm_cast] lemma coe_zero : (coe : finite_measure Œ± ‚Üí measure Œ±) 0 = 0 := rfl

@[simp, norm_cast] lemma coe_add (Œº ŒΩ : finite_measure Œ±) : ‚Üë(Œº + ŒΩ) = (‚ÜëŒº + ‚ÜëŒΩ : measure Œ±) := rfl

@[simp, norm_cast] lemma coe_smul (c : R) (Œº : finite_measure Œ±) :
  ‚Üë(c ‚Ä¢ Œº) = (c ‚Ä¢ ‚ÜëŒº : measure Œ±) := rfl

@[simp, norm_cast] lemma coe_fn_zero :
  (‚áë(0 : finite_measure Œ±) : set Œ± ‚Üí ‚Ñù‚â•0) = (0 : set Œ± ‚Üí ‚Ñù‚â•0) := by { funext, refl, }

@[simp, norm_cast] lemma coe_fn_add (Œº ŒΩ : finite_measure Œ±) :
  (‚áë(Œº + ŒΩ) : set Œ± ‚Üí ‚Ñù‚â•0) = (‚áëŒº + ‚áëŒΩ : set Œ± ‚Üí ‚Ñù‚â•0) :=
by { funext, simp [‚Üê ennreal.coe_eq_coe], }

@[simp, norm_cast] lemma coe_fn_smul [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù‚â•0] (c : R) (Œº : finite_measure Œ±) :
  (‚áë(c ‚Ä¢ Œº) : set Œ± ‚Üí ‚Ñù‚â•0) = c ‚Ä¢ (‚áëŒº : set Œ± ‚Üí ‚Ñù‚â•0) :=
by { funext, simp [‚Üê ennreal.coe_eq_coe, ennreal.coe_smul], }

instance : add_comm_monoid (finite_measure Œ±) :=
finite_measure.coe_injective.add_comm_monoid coe coe_zero coe_add (Œª _ _, coe_smul _ _)

/-- Coercion is an `add_monoid_hom`. -/
@[simps]
def coe_add_monoid_hom : finite_measure Œ± ‚Üí+ measure Œ± :=
{ to_fun := coe, map_zero' := coe_zero, map_add' := coe_add }

instance {Œ± : Type*} [measurable_space Œ±] : module ‚Ñù‚â•0 (finite_measure Œ±) :=
function.injective.module _ coe_add_monoid_hom finite_measure.coe_injective coe_smul

variables [topological_space Œ±]

/-- The pairing of a finite (Borel) measure `Œº` with a nonnegative bounded continuous
function is obtained by (Lebesgue) integrating the (test) function against the measure.
This is `finite_measure.test_against_nn`. -/
def test_against_nn (Œº : finite_measure Œ±) (f : Œ± ‚Üí·µá ‚Ñù‚â•0) : ‚Ñù‚â•0 :=
(‚à´‚Åª x, f x ‚àÇ(Œº : measure Œ±)).to_nnreal

lemma _root_.bounded_continuous_function.nnreal.to_ennreal_comp_measurable {Œ± : Type*}
  [topological_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±] (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  measurable (Œª x, (f x : ‚Ñù‚â•0‚àû)) :=
measurable_coe_nnreal_ennreal.comp f.continuous.measurable

lemma lintegral_lt_top_of_bounded_continuous_to_nnreal (Œº : finite_measure Œ±) (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  ‚à´‚Åª x, f x ‚àÇ(Œº : measure Œ±) < ‚àû :=
begin
  apply is_finite_measure.lintegral_lt_top_of_bounded_to_ennreal,
  use nndist f 0,
  intros x,
  have key := bounded_continuous_function.nnreal.upper_bound f x,
  rw ennreal.coe_le_coe,
  have eq : nndist f 0 = ‚ü®dist f 0, dist_nonneg‚ü©,
  { ext,
    simp only [real.coe_to_nnreal', max_eq_left_iff, subtype.coe_mk, coe_nndist], },
  rwa eq at key,
end

@[simp] lemma test_against_nn_coe_eq {Œº : finite_measure Œ±} {f : Œ± ‚Üí·µá ‚Ñù‚â•0} :
  (Œº.test_against_nn f : ‚Ñù‚â•0‚àû) = ‚à´‚Åª x, f x ‚àÇ(Œº : measure Œ±) :=
ennreal.coe_to_nnreal (lintegral_lt_top_of_bounded_continuous_to_nnreal Œº f).ne

lemma test_against_nn_const (Œº : finite_measure Œ±) (c : ‚Ñù‚â•0) :
  Œº.test_against_nn (bounded_continuous_function.const Œ± c) = c * Œº.mass :=
by simp [‚Üê ennreal.coe_eq_coe]

lemma test_against_nn_mono (Œº : finite_measure Œ±)
  {f g : Œ± ‚Üí·µá ‚Ñù‚â•0} (f_le_g : (f : Œ± ‚Üí ‚Ñù‚â•0) ‚â§ g) :
  Œº.test_against_nn f ‚â§ Œº.test_against_nn g :=
begin
  simp only [‚Üêennreal.coe_le_coe, test_against_nn_coe_eq],
  apply lintegral_mono,
  exact Œª x, ennreal.coe_mono (f_le_g x),
end

variables [opens_measurable_space Œ±]

lemma test_against_nn_add (Œº : finite_measure Œ±) (f‚ÇÅ f‚ÇÇ : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn (f‚ÇÅ + f‚ÇÇ) = Œº.test_against_nn f‚ÇÅ + Œº.test_against_nn f‚ÇÇ :=
begin
  simp only [‚Üêennreal.coe_eq_coe, bounded_continuous_function.coe_add, ennreal.coe_add,
             pi.add_apply, test_against_nn_coe_eq],
  apply lintegral_add;
  exact bounded_continuous_function.nnreal.to_ennreal_comp_measurable _,
end

lemma test_against_nn_smul [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù‚â•0] [pseudo_metric_space R] [has_zero R]
  [has_bounded_smul R ‚Ñù‚â•0]
  (Œº : finite_measure Œ±) (c : R) (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn (c ‚Ä¢ f) = c ‚Ä¢ Œº.test_against_nn f :=
begin
  simp only [‚Üêennreal.coe_eq_coe, bounded_continuous_function.coe_smul,
             test_against_nn_coe_eq, ennreal.coe_smul],
  simp_rw [‚Üêsmul_one_smul ‚Ñù‚â•0‚àû c (f _ : ‚Ñù‚â•0‚àû), ‚Üêsmul_one_smul ‚Ñù‚â•0‚àû c (lintegral _ _ : ‚Ñù‚â•0‚àû),
           smul_eq_mul],
  exact @lintegral_const_mul _ _ (Œº : measure Œ±) (c ‚Ä¢ 1)  _
                   (bounded_continuous_function.nnreal.to_ennreal_comp_measurable f),
end

lemma test_against_nn_lipschitz_estimate (Œº : finite_measure Œ±) (f g : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn f ‚â§ Œº.test_against_nn g + (nndist f g) * Œº.mass :=
begin
  simp only [‚ÜêŒº.test_against_nn_const (nndist f g), ‚Üêtest_against_nn_add, ‚Üêennreal.coe_le_coe,
             bounded_continuous_function.coe_add, const_apply, ennreal.coe_add, pi.add_apply,
             coe_nnreal_ennreal_nndist, test_against_nn_coe_eq],
  apply lintegral_mono,
  have le_dist : ‚àÄ x, dist (f x) (g x) ‚â§ nndist f g :=
  bounded_continuous_function.dist_coe_le_dist,
  intros x,
  have le' : f(x) ‚â§ g(x) + nndist f g,
  { apply (nnreal.le_add_nndist (f x) (g x)).trans,
    rw add_le_add_iff_left,
    exact dist_le_coe.mp (le_dist x), },
  have le : (f(x) : ‚Ñù‚â•0‚àû) ‚â§ (g(x) : ‚Ñù‚â•0‚àû) + (nndist f g),
  by { rw ‚Üêennreal.coe_add, exact ennreal.coe_mono le', },
  rwa [coe_nnreal_ennreal_nndist] at le,
end

lemma test_against_nn_lipschitz (Œº : finite_measure Œ±) :
  lipschitz_with Œº.mass (Œª (f : Œ± ‚Üí·µá ‚Ñù‚â•0), Œº.test_against_nn f) :=
begin
  rw lipschitz_with_iff_dist_le_mul,
  intros f‚ÇÅ f‚ÇÇ,
  suffices : abs (Œº.test_against_nn f‚ÇÅ - Œº.test_against_nn f‚ÇÇ : ‚Ñù) ‚â§ Œº.mass * (dist f‚ÇÅ f‚ÇÇ),
  { rwa nnreal.dist_eq, },
  apply abs_le.mpr,
  split,
  { have key' := Œº.test_against_nn_lipschitz_estimate f‚ÇÇ f‚ÇÅ,
    rw mul_comm at key',
    suffices : ‚Üë(Œº.test_against_nn f‚ÇÇ) ‚â§ ‚Üë(Œº.test_against_nn f‚ÇÅ) + ‚Üë(Œº.mass) * dist f‚ÇÅ f‚ÇÇ,
    { linarith, },
    have key := nnreal.coe_mono key',
    rwa [nnreal.coe_add, nnreal.coe_mul, nndist_comm] at key, },
  { have key' := Œº.test_against_nn_lipschitz_estimate f‚ÇÅ f‚ÇÇ,
    rw mul_comm at key',
    suffices : ‚Üë(Œº.test_against_nn f‚ÇÅ) ‚â§ ‚Üë(Œº.test_against_nn f‚ÇÇ) + ‚Üë(Œº.mass) * dist f‚ÇÅ f‚ÇÇ,
    { linarith, },
    have key := nnreal.coe_mono key',
    rwa [nnreal.coe_add, nnreal.coe_mul] at key, },
end

/-- Finite measures yield elements of the `weak_dual` of bounded continuous nonnegative
functions via `finite_measure.test_against_nn`, i.e., integration. -/
def to_weak_dual_bcnn (Œº : finite_measure Œ±) :
  weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0) :=
{ to_fun := Œª f, Œº.test_against_nn f,
  map_add' := test_against_nn_add Œº,
  map_smul' := test_against_nn_smul Œº,
  cont := Œº.test_against_nn_lipschitz.continuous, }

@[simp] lemma coe_to_weak_dual_bcnn (Œº : finite_measure Œ±) :
  ‚áëŒº.to_weak_dual_bcnn = Œº.test_against_nn := rfl

@[simp] lemma to_weak_dual_bcnn_apply (Œº : finite_measure Œ±) (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  Œº.to_weak_dual_bcnn f = (‚à´‚Åª x, f x ‚àÇ(Œº : measure Œ±)).to_nnreal := rfl

/-- The topology of weak convergence on `finite_measures Œ±` is inherited (induced) from the weak-*
topology on `weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0)` via the function `finite_measures.to_weak_dual_bcnn`. -/
instance : topological_space (finite_measure Œ±) :=
topological_space.induced to_weak_dual_bcnn infer_instance

lemma to_weak_dual_bcnn_continuous :
  continuous (@finite_measure.to_weak_dual_bcnn Œ± _ _ _) :=
continuous_induced_dom

/- Integration of (nonnegative bounded continuous) test functions against finite Borel measures
depends continuously on the measure. -/
lemma continuous_test_against_nn_eval (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  continuous (Œª (Œº : finite_measure Œ±), Œº.test_against_nn f) :=
(by apply (weak_bilin.eval_continuous _ _).comp to_weak_dual_bcnn_continuous :
  continuous ((Œª œÜ : weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0), œÜ f) ‚àò to_weak_dual_bcnn))

lemma tendsto_iff_weak_star_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measure Œ±} {Œº : finite_measure Œ±} :
  tendsto Œºs F (ùìù Œº) ‚Üî tendsto (Œª i, (Œºs(i)).to_weak_dual_bcnn) F (ùìù Œº.to_weak_dual_bcnn) :=
inducing.tendsto_nhds_iff ‚ü®rfl‚ü©

theorem tendsto_iff_forall_test_against_nn_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measure Œ±} {Œº : finite_measure Œ±} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ± ‚Üí·µá ‚Ñù‚â•0), tendsto (Œª i, (Œºs(i)).to_weak_dual_bcnn f) F (ùìù (Œº.to_weak_dual_bcnn f)) :=
by { rw [tendsto_iff_weak_star_tendsto, tendsto_iff_forall_eval_tendsto_top_dual_pairing], refl, }

theorem tendsto_iff_forall_lintegral_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measure Œ±} {Œº : finite_measure Œ±} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ± ‚Üí·µá ‚Ñù‚â•0),
    tendsto (Œª i, (‚à´‚Åª x, (f x) ‚àÇ(Œºs(i) : measure Œ±))) F (ùìù ((‚à´‚Åª x, (f x) ‚àÇ(Œº : measure Œ±)))) :=
begin
  rw tendsto_iff_forall_test_against_nn_tendsto,
  simp_rw [to_weak_dual_bcnn_apply _ _, ‚Üêtest_against_nn_coe_eq,
           ennreal.tendsto_coe, ennreal.to_nnreal_coe],
end

end finite_measure

/-- Probability measures are defined as the subtype of measures that have the property of being
probability measures (i.e., their total mass is one). -/
def probability_measure (Œ± : Type*) [measurable_space Œ±] : Type* :=
{Œº : measure Œ± // is_probability_measure Œº}

namespace probability_measure

instance [inhabited Œ±] : inhabited (probability_measure Œ±) :=
‚ü®‚ü®measure.dirac default, measure.dirac.is_probability_measure‚ü©‚ü©

/-- A probability measure can be interpreted as a measure. -/
instance : has_coe (probability_measure Œ±) (measure_theory.measure Œ±) := coe_subtype

instance : has_coe_to_fun (probability_measure Œ±) (Œª _, set Œ± ‚Üí ‚Ñù‚â•0) :=
‚ü®Œª Œº s, (Œº s).to_nnreal‚ü©

instance (Œº : probability_measure Œ±) : is_probability_measure (Œº : measure Œ±) := Œº.prop

lemma coe_fn_eq_to_nnreal_coe_fn_to_measure (ŒΩ : probability_measure Œ±) :
  (ŒΩ : set Œ± ‚Üí ‚Ñù‚â•0) = Œª s, ((ŒΩ : measure Œ±) s).to_nnreal := rfl

@[simp] lemma val_eq_to_measure (ŒΩ : probability_measure Œ±) : ŒΩ.val = (ŒΩ : measure Œ±) := rfl

lemma coe_injective : function.injective (coe : probability_measure Œ± ‚Üí measure Œ±) :=
subtype.coe_injective

@[simp] lemma coe_fn_univ (ŒΩ : probability_measure Œ±) : ŒΩ univ = 1 :=
congr_arg ennreal.to_nnreal ŒΩ.prop.measure_univ

/-- A probability measure can be interpreted as a finite measure. -/
def to_finite_measure (Œº : probability_measure Œ±) : finite_measure Œ± := ‚ü®Œº, infer_instance‚ü©

@[simp] lemma coe_comp_to_finite_measure_eq_coe (ŒΩ : probability_measure Œ±) :
  (ŒΩ.to_finite_measure : measure Œ±) = (ŒΩ : measure Œ±) := rfl

@[simp] lemma coe_fn_comp_to_finite_measure_eq_coe_fn (ŒΩ : probability_measure Œ±) :
  (ŒΩ.to_finite_measure : set Œ± ‚Üí ‚Ñù‚â•0) = (ŒΩ : set Œ± ‚Üí ‚Ñù‚â•0) := rfl

@[simp] lemma ennreal_coe_fn_eq_coe_fn_to_measure (ŒΩ : probability_measure Œ±) (s : set Œ±) :
  (ŒΩ s : ‚Ñù‚â•0‚àû) = (ŒΩ : measure Œ±) s :=
by { rw [‚Üê coe_fn_comp_to_finite_measure_eq_coe_fn,
     finite_measure.ennreal_coe_fn_eq_coe_fn_to_measure], refl, }

@[simp] lemma mass_to_finite_measure (Œº : probability_measure Œ±) :
  Œº.to_finite_measure.mass = 1 := Œº.coe_fn_univ

variables [topological_space Œ±]

lemma lintegral_lt_top_of_bounded_continuous_to_nnreal (Œº : probability_measure Œ±) (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  ‚à´‚Åª x, f x ‚àÇ(Œº : measure Œ±) < ‚àû :=
Œº.to_finite_measure.lintegral_lt_top_of_bounded_continuous_to_nnreal f

variables [opens_measurable_space Œ±]

lemma test_against_nn_lipschitz (Œº : probability_measure Œ±) :
  lipschitz_with 1 (Œª (f : Œ± ‚Üí·µá ‚Ñù‚â•0), Œº.to_finite_measure.test_against_nn f) :=
Œº.mass_to_finite_measure ‚ñ∏ Œº.to_finite_measure.test_against_nn_lipschitz

/-- The topology of weak convergence on `probability_measures Œ±`. This is inherited (induced) from
the weak-*  topology on `weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0)` via the function
`probability_measures.to_weak_dual_bcnn`. -/
instance : topological_space (probability_measure Œ±) :=
topological_space.induced to_finite_measure infer_instance

lemma to_finite_measure_continuous :
  continuous (to_finite_measure : probability_measure Œ± ‚Üí finite_measure Œ±) :=
continuous_induced_dom

/-- Probability measures yield elements of the `weak_dual` of bounded continuous nonnegative
functions via `finite_measure.test_against_nn`, i.e., integration. -/
def to_weak_dual_bcnn : probability_measure Œ± ‚Üí weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0) :=
finite_measure.to_weak_dual_bcnn ‚àò to_finite_measure

@[simp] lemma coe_to_weak_dual_bcnn (Œº : probability_measure Œ±) :
  ‚áëŒº.to_weak_dual_bcnn = Œº.to_finite_measure.test_against_nn := rfl

@[simp] lemma to_weak_dual_bcnn_apply (Œº : probability_measure Œ±) (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  Œº.to_weak_dual_bcnn f = (‚à´‚Åª x, f x ‚àÇ(Œº : measure Œ±)).to_nnreal := rfl

lemma to_weak_dual_bcnn_continuous :
  continuous (Œª (Œº : probability_measure Œ±), Œº.to_weak_dual_bcnn) :=
finite_measure.to_weak_dual_bcnn_continuous.comp to_finite_measure_continuous

/- Integration of (nonnegative bounded continuous) test functions against Borel probability
measures depends continuously on the measure. -/
lemma continuous_test_against_nn_eval (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  continuous (Œª (Œº : probability_measure Œ±), Œº.to_finite_measure.test_against_nn f) :=
(finite_measure.continuous_test_against_nn_eval f).comp to_finite_measure_continuous

/- The canonical mapping from probability measures to finite measures is an embedding. -/
lemma to_finite_measure_embedding (Œ± : Type*)
  [measurable_space Œ±] [topological_space Œ±] [opens_measurable_space Œ±] :
  embedding (to_finite_measure : probability_measure Œ± ‚Üí finite_measure Œ±) :=
{ induced := rfl,
  inj := Œª Œº ŒΩ h, subtype.eq (by convert congr_arg coe h) }

lemma tendsto_nhds_iff_to_finite_measures_tendsto_nhds {Œ¥ : Type*}
  (F : filter Œ¥) {Œºs : Œ¥ ‚Üí probability_measure Œ±} {Œº‚ÇÄ : probability_measure Œ±} :
  tendsto Œºs F (ùìù Œº‚ÇÄ) ‚Üî tendsto (to_finite_measure ‚àò Œºs) F (ùìù (Œº‚ÇÄ.to_finite_measure)) :=
embedding.tendsto_nhds_iff (probability_measure.to_finite_measure_embedding Œ±)

/-- The usual definition of weak convergence of probability measures is given in terms of sequences
of probability measures: it is the requirement that the integrals of all continuous bounded
functions against members of the sequence converge. This version is a characterization using
nonnegative bounded continuous functions. -/
theorem tendsto_iff_forall_lintegral_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí probability_measure Œ±} {Œº : probability_measure Œ±} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ± ‚Üí·µá ‚Ñù‚â•0), tendsto (Œª i, (‚à´‚Åª x, (f x) ‚àÇ(Œºs(i) : measure Œ±))) F
    (ùìù ((‚à´‚Åª x, (f x) ‚àÇ(Œº : measure Œ±)))) :=
begin
  rw tendsto_nhds_iff_to_finite_measures_tendsto_nhds,
  exact finite_measure.tendsto_iff_forall_lintegral_tendsto,
end

end probability_measure

end measure_theory
