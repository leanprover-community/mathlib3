/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, SÃ©bastien GouÃ«zel
-/
import measure_theory.measure.lebesgue.eq_haar
import measure_theory.integral.bochner

/-!
# Basic properties of Haar measures on real vector spaces

-/

noncomputable theory

open_locale nnreal ennreal pointwise big_operators topology
open has_inv set function measure_theory.measure filter
open measure finite_dimensional

namespace measure_theory

namespace measure

/- The instance `is_add_haar_measure.has_no_atoms` applies in particular to show that an additive
Haar measure on a nontrivial finite-dimensional real vector space has no atom. -/
example {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [nontrivial E]
  [finite_dimensional â„ E] [measurable_space E] [borel_space E] (Î¼ : measure E)
  [is_add_haar_measure Î¼] :
  has_no_atoms Î¼ := by apply_instance

section continuous_linear_equiv

variables {ğ•œ G H : Type*} [measurable_space G] [measurable_space H]
  [nontrivially_normed_field ğ•œ] [topological_space G] [topological_space H]
  [add_comm_group G] [add_comm_group H] [topological_add_group G] [topological_add_group H]
  [module ğ•œ G] [module ğ•œ H] (Î¼ : measure G) [is_add_haar_measure Î¼] [borel_space G] [borel_space H]
  [t2_space H]

instance map_continuous_linear_equiv.is_add_haar_measure (e : G â‰ƒL[ğ•œ] H) :
  is_add_haar_measure (Î¼.map e) :=
e.to_add_equiv.is_add_haar_measure_map _ e.continuous e.symm.continuous

variables [complete_space ğ•œ] [t2_space G] [finite_dimensional ğ•œ G] [has_continuous_smul ğ•œ G]
  [has_continuous_smul ğ•œ H]

instance map_linear_equiv.is_add_haar_measure (e : G â‰ƒâ‚—[ğ•œ] H) : is_add_haar_measure (Î¼.map e) :=
map_continuous_linear_equiv.is_add_haar_measure _ e.to_continuous_linear_equiv

end continuous_linear_equiv

variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E]
  [borel_space E] [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  {F : Type*} [normed_add_comm_group F] [normed_space â„ F] [complete_space F]
variables (Î¼) {s : set E}

/-- The integral of `f (R â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
lemma integral_comp_smul (f : E â†’ F) (R : â„) :
  âˆ« x, f (R â€¢ x) âˆ‚Î¼ = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« x, f x âˆ‚Î¼ :=
begin
  rcases eq_or_ne R 0 with rfl|hR,
  { simp only [zero_smul, integral_const],
    rcases nat.eq_zero_or_pos (finrank â„ E) with hE|hE,
    { haveI : subsingleton E, from finrank_zero_iff.1 hE,
      have : f = (Î» x, f 0), { ext x, rw subsingleton.elim x 0 },
      conv_rhs { rw this },
      simp only [hE, pow_zero, inv_one, abs_one, one_smul, integral_const] },
    { haveI : nontrivial E, from finrank_pos_iff.1 hE,
      simp only [zero_pow hE, measure_univ_of_is_add_left_invariant, ennreal.top_to_real, zero_smul,
        inv_zero, abs_zero]} },
  { calc âˆ« x, f (R â€¢ x) âˆ‚Î¼ = âˆ« y, f y âˆ‚(measure.map (Î» x, R â€¢ x) Î¼) :
      (integral_map_equiv (homeomorph.smul (is_unit_iff_ne_zero.2 hR).unit)
        .to_measurable_equiv f).symm
    ... = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« x, f x âˆ‚Î¼ :
      by simp only [map_add_haar_smul Î¼ hR, integral_smul_measure, ennreal.to_real_of_real,
                    abs_nonneg] }
end

/-- The integral of `f (R â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
lemma integral_comp_smul_of_nonneg (f : E â†’ F) (R : â„) {hR : 0 â‰¤ R} :
  âˆ« x, f (R â€¢ x) âˆ‚Î¼ = (R ^ finrank â„ E)â»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ :=
by rw [integral_comp_smul Î¼ f R, abs_of_nonneg (inv_nonneg.2 (pow_nonneg hR _))]

/-- The integral of `f (Râ»Â¹ â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
lemma integral_comp_inv_smul (f : E â†’ F) (R : â„) :
  âˆ« x, f (Râ»Â¹ â€¢ x) âˆ‚Î¼ = |(R ^ finrank â„ E)| â€¢ âˆ« x, f x âˆ‚Î¼ :=
by rw [integral_comp_smul Î¼ f (Râ»Â¹), inv_pow, inv_inv]

/-- The integral of `f (Râ»Â¹ â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
lemma integral_comp_inv_smul_of_nonneg (f : E â†’ F) {R : â„} (hR : 0 â‰¤ R) :
  âˆ« x, f (Râ»Â¹ â€¢ x) âˆ‚Î¼ = R ^ finrank â„ E â€¢ âˆ« x, f x âˆ‚Î¼ :=
by rw [integral_comp_inv_smul Î¼ f R, abs_of_nonneg ((pow_nonneg hR _))]

lemma integral_comp_mul_left (g : â„ â†’ F) (a : â„) :
  âˆ« x : â„, g (a * x) = |aâ»Â¹| â€¢ âˆ« y : â„, g y :=
by simp_rw [â†smul_eq_mul, measure.integral_comp_smul, finite_dimensional.finrank_self, pow_one]

lemma integral_comp_inv_mul_left (g : â„ â†’ F) (a : â„) :
  âˆ« x : â„, g (aâ»Â¹ * x) = |a| â€¢ âˆ« y : â„, g y :=
by simp_rw [â†smul_eq_mul, measure.integral_comp_inv_smul, finite_dimensional.finrank_self, pow_one]

lemma integral_comp_mul_right (g : â„ â†’ F) (a : â„) :
  âˆ« x : â„, g (x * a) = |aâ»Â¹| â€¢ âˆ« y : â„, g y :=
by simpa only [mul_comm] using integral_comp_mul_left g a

lemma integral_comp_inv_mul_right (g : â„ â†’ F) (a : â„) :
  âˆ« x : â„, g (x * aâ»Â¹) = |a| â€¢ âˆ« y : â„, g y :=
by simpa only [mul_comm] using integral_comp_inv_mul_left g a

lemma integral_comp_div (g : â„ â†’ F) (a : â„) :
  âˆ« x : â„, g (x / a) = |a| â€¢ âˆ« y : â„, g y :=
integral_comp_inv_mul_right g a

end measure

variables {F : Type*} [normed_add_comm_group F]

lemma integrable.comp_smul {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
  [measurable_space E] [borel_space E] [finite_dimensional â„ E]
  (Î¼ : measure E) [is_add_haar_measure Î¼] {f : E â†’ F} (hf : integrable f Î¼) {R : â„} (hR : R â‰  0) :
  integrable (Î» x, f (R â€¢ x)) Î¼ :=
begin
  let t := ((homeomorph.smul (is_unit_iff_ne_zero.2 hR).unit).to_measurable_equiv : E â‰ƒáµ E),
  refine (integrable_map_equiv t f).mp (_ : integrable f (map (has_smul.smul R) Î¼)),
  rwa [map_add_haar_smul Î¼ hR, integrable_smul_measure _ ennreal.of_real_ne_top],
  simpa only [ne.def, ennreal.of_real_eq_zero, not_le, abs_pos] using inv_ne_zero (pow_ne_zero _ hR)
end

lemma integrable.comp_mul_left {g : â„ â†’ F} (hg : integrable g) {R : â„} (hR : R â‰  0) :
  integrable (Î» x, g (R * x)) volume :=
by simpa only [smul_eq_mul] using hg.comp_smul volume hR

lemma integrable.comp_mul_right {g : â„ â†’ F} (hg : integrable g) {R : â„} (hR : R â‰  0) :
  integrable (Î» x, g (x * R)) volume :=
by simpa only [mul_comm, smul_eq_mul] using hg.comp_smul volume hR

lemma integrable.comp_div {g : â„ â†’ F} (hg : integrable g) {R : â„} (hR : R â‰  0) :
  integrable (Î» x, g (x / R)) volume :=
hg.comp_mul_right (inv_ne_zero hR)

end measure_theory
