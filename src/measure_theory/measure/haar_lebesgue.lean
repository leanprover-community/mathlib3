/-
Copyright (c) 2021 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, S√©bastien Gou√´zel
-/
import measure_theory.measure.lebesgue
import measure_theory.measure.haar
import linear_algebra.finite_dimensional
import analysis.normed_space.pointwise

/-!
# Relationship between the Haar and Lebesgue measures

We prove that the Haar measure and Lebesgue measure are equal on `‚Ñù` and on `‚Ñù^Œπ`, in
`measure_theory.add_haar_measure_eq_volume` and `measure_theory.add_haar_measure_eq_volume_pi`.

We deduce basic properties of any Haar measure on a finite dimensional real vector space:
* `map_linear_map_add_haar_eq_smul_add_haar`: a linear map rescales the Haar measure by the
  absolute value of its determinant.
* `add_haar_preimage_linear_map` : when `f` is a linear map with nonzero determinant, the measure
  of `f ‚Åª¬π' s` is the measure of `s` multiplied by the absolute value of the inverse of the
  determinant of `f`.
* `add_haar_image_linear_map` :  when `f` is a linear map, the measure of `f '' s` is the
  measure of `s` multiplied by the absolute value of the determinant of `f`.
* `add_haar_submodule` : a strict submodule has measure `0`.
* `add_haar_smul` : the measure of `r ‚Ä¢ s` is `|r| ^ dim * Œº s`.
* `add_haar_ball`: the measure of `ball x r` is `r ^ dim * Œº (ball 0 1)`.
* `add_haar_closed_ball`: the measure of `closed_ball x r` is `r ^ dim * Œº (ball 0 1)`.
* `add_haar_sphere`: spheres have zero measure.

-/

open topological_space set filter metric
open_locale ennreal pointwise topological_space

/-- The interval `[0,1]` as a compact set with non-empty interior. -/
def topological_space.positive_compacts.Icc01 : positive_compacts ‚Ñù :=
‚ü®Icc 0 1, is_compact_Icc, by simp_rw [interior_Icc, nonempty_Ioo, zero_lt_one]‚ü©

universe u

/-- The set `[0,1]^Œπ` as a compact set with non-empty interior. -/
def topological_space.positive_compacts.pi_Icc01 (Œπ : Type*) [fintype Œπ] :
  positive_compacts (Œπ ‚Üí ‚Ñù) :=
‚ü®set.pi set.univ (Œª i, Icc 0 1), is_compact_univ_pi (Œª i, is_compact_Icc),
by simp only [interior_pi_set, finite.of_fintype, interior_Icc, univ_pi_nonempty_iff, nonempty_Ioo,
  implies_true_iff, zero_lt_one]‚ü©

namespace measure_theory

open measure topological_space.positive_compacts finite_dimensional

/-!
### The Lebesgue measure is a Haar measure on `‚Ñù` and on `‚Ñù^Œπ`.
-/

lemma is_add_left_invariant_real_volume : is_add_left_invariant ‚áë(volume : measure ‚Ñù) :=
by simp [‚Üê map_add_left_eq_self, real.map_volume_add_left]

/-- The Haar measure equals the Lebesgue measure on `‚Ñù`. -/
lemma add_haar_measure_eq_volume : add_haar_measure Icc01 = volume :=
begin
  convert (add_haar_measure_unique _ Icc01).symm,
  { simp [Icc01] },
  { apply_instance },
  { exact is_add_left_invariant_real_volume }
end

instance : is_add_haar_measure (volume : measure ‚Ñù) :=
by { rw ‚Üê add_haar_measure_eq_volume, apply_instance }

lemma is_add_left_invariant_real_volume_pi (Œπ : Type*) [fintype Œπ] :
  is_add_left_invariant ‚áë(volume : measure (Œπ ‚Üí ‚Ñù)) :=
by simp [‚Üê map_add_left_eq_self, real.map_volume_pi_add_left]

/-- The Haar measure equals the Lebesgue measure on `‚Ñù^Œπ`. -/
lemma add_haar_measure_eq_volume_pi (Œπ : Type*) [fintype Œπ] :
  add_haar_measure (pi_Icc01 Œπ) = volume :=
begin
  convert (add_haar_measure_unique _ (pi_Icc01 Œπ)).symm,
  { simp only [pi_Icc01, volume_pi_pi (Œª i, Icc (0 : ‚Ñù) 1),
      finset.prod_const_one, ennreal.of_real_one, real.volume_Icc, one_smul, sub_zero] },
  { apply_instance },
  { exact is_add_left_invariant_real_volume_pi Œπ }
end

instance is_add_haar_measure_volume_pi (Œπ : Type*) [fintype Œπ] :
  is_add_haar_measure (volume : measure (Œπ ‚Üí ‚Ñù)) :=
by { rw ‚Üê add_haar_measure_eq_volume_pi, apply_instance }

namespace measure

/-!
### Strict subspaces have zero measure
-/

/-- If a set is disjoint of its translates by infinitely many bounded vectors, then it has measure
zero. This auxiliary lemma proves this assuming additionally that the set is bounded. -/
lemma add_haar_eq_zero_of_disjoint_translates_aux
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  {s : set E} (u : ‚Ñï ‚Üí E) (sb : bounded s) (hu : bounded (range u))
  (hs : pairwise (disjoint on (Œª n, {u n} + s))) (h's : measurable_set s) :
  Œº s = 0 :=
begin
  by_contra h,
  apply lt_irrefl ‚àû,
  calc
  ‚àû = ‚àë' (n : ‚Ñï), Œº s : (ennreal.tsum_const_eq_top_of_ne_zero h).symm
  ... = ‚àë' (n : ‚Ñï), Œº ({u n} + s) :
    by { congr' 1, ext1 n, simp only [image_add_left, add_haar_preimage_add, singleton_add] }
  ... = Œº (‚ãÉ n, {u n} + s) :
    by rw measure_Union hs
      (Œª n, by simpa only [image_add_left, singleton_add] using measurable_id.const_add _ h's)
  ... = Œº (range u + s) : by rw [‚Üê Union_add, Union_singleton_eq_range]
  ... < ‚àû : bounded.measure_lt_top (hu.add sb)
end

/-- If a set is disjoint of its translates by infinitely many bounded vectors, then it has measure
zero. -/
lemma add_haar_eq_zero_of_disjoint_translates
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  {s : set E} (u : ‚Ñï ‚Üí E) (hu : bounded (range u))
  (hs : pairwise (disjoint on (Œª n, {u n} + s))) (h's : measurable_set s) :
  Œº s = 0 :=
begin
  suffices H : ‚àÄ R, Œº (s ‚à© closed_ball 0 R) = 0,
  { apply le_antisymm _ (zero_le _),
    have : s ‚äÜ ‚ãÉ (n : ‚Ñï), s ‚à© closed_ball 0 n,
    { assume x hx,
      obtain ‚ü®n, hn‚ü© : ‚àÉ (n : ‚Ñï), ‚à•x‚à• ‚â§ n := exists_nat_ge (‚à•x‚à•),
      exact mem_Union.2 ‚ü®n, ‚ü®hx, mem_closed_ball_zero_iff.2 hn‚ü©‚ü© },
    calc Œº s ‚â§ Œº (‚ãÉ (n : ‚Ñï), s ‚à© closed_ball 0 n) : measure_mono this
    ... ‚â§ ‚àë' (n : ‚Ñï), Œº (s ‚à© closed_ball 0 n) : measure_Union_le _
    ... = 0 : by simp only [H, tsum_zero] },
  assume R,
  apply add_haar_eq_zero_of_disjoint_translates_aux Œº u
    (bounded.mono (inter_subset_right _ _) bounded_closed_ball) hu _
    (h's.inter (measurable_set_closed_ball)),
  rw ‚Üê pairwise_univ at ‚ä¢ hs,
  apply pairwise_disjoint.mono hs (Œª n, _),
  exact add_subset_add (subset.refl _) (inter_subset_left _ _)
end

/-- A strict vector subspace has measure zero. -/
lemma add_haar_submodule
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  (s : submodule ‚Ñù E) (hs : s ‚â† ‚ä§) : Œº s = 0 :=
begin
  obtain ‚ü®x, hx‚ü© : ‚àÉ x, x ‚àâ s,
    by simpa only [submodule.eq_top_iff', not_exists, ne.def, not_forall] using hs,
  obtain ‚ü®c, cpos, cone‚ü© : ‚àÉ (c : ‚Ñù), 0 < c ‚àß c < 1 := ‚ü®1/2, by norm_num, by norm_num‚ü©,
  have A : bounded (range (Œª (n : ‚Ñï), (c ^ n) ‚Ä¢ x)),
  { have : tendsto (Œª (n : ‚Ñï), (c ^ n) ‚Ä¢ x) at_top (ùìù ((0 : ‚Ñù) ‚Ä¢ x)) :=
      (tendsto_pow_at_top_nhds_0_of_lt_1 cpos.le cone).smul_const x,
    exact bounded_range_of_tendsto _ this },
  apply add_haar_eq_zero_of_disjoint_translates Œº _ A _
    (submodule.closed_of_finite_dimensional s).measurable_set,
  assume m n hmn,
  simp only [function.on_fun, image_add_left, singleton_add, disjoint_left, mem_preimage,
             set_like.mem_coe],
  assume y hym hyn,
  have A : (c ^ n - c ^ m) ‚Ä¢ x ‚àà s,
  { convert s.sub_mem hym hyn,
    simp only [sub_smul, neg_sub_neg, add_sub_add_right_eq_sub] },
  have H : c ^ n - c ^ m ‚â† 0,
    by simpa only [sub_eq_zero, ne.def] using (strict_anti_pow cpos cone).injective.ne hmn.symm,
  have : x ‚àà s,
  { convert s.smul_mem (c ^ n - c ^ m)‚Åª¬π A,
    rw [smul_smul, inv_mul_cancel H, one_smul] },
  exact hx this
end

/-!
### Applying a linear map rescales Haar measure by the determinant

We first prove this on `Œπ ‚Üí ‚Ñù`, using that this is already known for the product Lebesgue
measure (thanks to matrices computations). Then, we extend this to any finite-dimensional real
vector space by using a linear equiv with a space of the form `Œπ ‚Üí ‚Ñù`, and arguing that such a
linear equiv maps Haar measure to Haar measure.
-/

lemma map_linear_map_add_haar_pi_eq_smul_add_haar
  {Œπ : Type*} [fintype Œπ] {f : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] (Œπ ‚Üí ‚Ñù)} (hf : f.det ‚â† 0)
  (Œº : measure (Œπ ‚Üí ‚Ñù)) [is_add_haar_measure Œº] :
  measure.map f Œº = ennreal.of_real (abs (f.det)‚Åª¬π) ‚Ä¢ Œº :=
begin
  /- We have already proved the result for the Lebesgue product measure, using matrices.
  We deduce it for any Haar measure by uniqueness (up to scalar multiplication). -/
  have := add_haar_measure_unique (is_add_left_invariant_add_haar Œº) (pi_Icc01 Œπ),
  rw this,
  simp [add_haar_measure_eq_volume_pi, real.map_linear_map_volume_pi_eq_smul_volume_pi hf,
    smul_smul, mul_comm],
end

lemma map_linear_map_add_haar_eq_smul_add_haar
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  {f : E ‚Üí‚Çó[‚Ñù] E} (hf : f.det ‚â† 0) :
  measure.map f Œº = ennreal.of_real (abs (f.det)‚Åª¬π) ‚Ä¢ Œº :=
begin
  -- we reduce to the case of `E = Œπ ‚Üí ‚Ñù`, for which we have already proved the result using
  -- matrices in `map_linear_map_add_haar_pi_eq_smul_add_haar`.
  let Œπ := fin (finrank ‚Ñù E),
  haveI : finite_dimensional ‚Ñù (Œπ ‚Üí ‚Ñù) := by apply_instance,
  have : finrank ‚Ñù E = finrank ‚Ñù (Œπ ‚Üí ‚Ñù), by simp,
  have e : E ‚âÉ‚Çó[‚Ñù] Œπ ‚Üí ‚Ñù := linear_equiv.of_finrank_eq E (Œπ ‚Üí ‚Ñù) this,
  -- next line is to avoid `g` getting reduced by `simp`.
  obtain ‚ü®g, hg‚ü© : ‚àÉ g, g = (e : E ‚Üí‚Çó[‚Ñù] (Œπ ‚Üí ‚Ñù)).comp (f.comp (e.symm : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] E)) :=
    ‚ü®_, rfl‚ü©,
  have gdet : g.det = f.det, by { rw [hg], exact linear_map.det_conj f e },
  rw ‚Üê gdet at hf ‚ä¢,
  have fg : f = (e.symm : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] E).comp (g.comp (e : E ‚Üí‚Çó[‚Ñù] (Œπ ‚Üí ‚Ñù))),
  { ext x,
    simp only [linear_equiv.coe_coe, function.comp_app, linear_map.coe_comp,
      linear_equiv.symm_apply_apply, hg] },
  simp only [fg, linear_equiv.coe_coe, linear_map.coe_comp],
  have Ce : continuous e := (e : E ‚Üí‚Çó[‚Ñù] (Œπ ‚Üí ‚Ñù)).continuous_of_finite_dimensional,
  have Cg : continuous g := linear_map.continuous_of_finite_dimensional g,
  have Cesymm : continuous e.symm := (e.symm : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] E).continuous_of_finite_dimensional,
  rw [‚Üê map_map Cesymm.measurable (Cg.comp Ce).measurable, ‚Üê map_map Cg.measurable Ce.measurable],
  haveI : is_add_haar_measure (map e Œº) := is_add_haar_measure_map Œº e.to_add_equiv Ce Cesymm,
  have ecomp : (e.symm) ‚àò e = id,
    by { ext x, simp only [id.def, function.comp_app, linear_equiv.symm_apply_apply] },
  rw [map_linear_map_add_haar_pi_eq_smul_add_haar hf (map e Œº), linear_map.map_smul,
    map_map Cesymm.measurable Ce.measurable, ecomp, measure.map_id]
end

/-- The preimage of a set `s` under a linear map `f` with nonzero determinant has measure
equal to `Œº s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp] lemma add_haar_preimage_linear_map
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  {f : E ‚Üí‚Çó[‚Ñù] E} (hf : f.det ‚â† 0) (s : set E) :
  Œº (f ‚Åª¬π' s) = ennreal.of_real (abs (f.det)‚Åª¬π) * Œº s :=
calc Œº (f ‚Åª¬π' s) = measure.map f Œº s :
  ((f.equiv_of_det_ne_zero hf).to_continuous_linear_equiv.to_homeomorph
    .to_measurable_equiv.map_apply s).symm
... = ennreal.of_real (abs (f.det)‚Åª¬π) * Œº s :
  by { rw map_linear_map_add_haar_eq_smul_add_haar Œº hf, refl }

/-- The preimage of a set `s` under a continuous linear map `f` with nonzero determinant has measure
equal to `Œº s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp] lemma add_haar_preimage_continuous_linear_map
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  {f : E ‚ÜíL[‚Ñù] E} (hf : linear_map.det (f : E ‚Üí‚Çó[‚Ñù] E) ‚â† 0) (s : set E) :
  Œº (f ‚Åª¬π' s) = ennreal.of_real (abs (linear_map.det (f : E ‚Üí‚Çó[‚Ñù] E))‚Åª¬π) * Œº s :=
add_haar_preimage_linear_map Œº hf s

/-- The preimage of a set `s` under a linear equiv `f` has measure
equal to `Œº s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp] lemma add_haar_preimage_linear_equiv
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  (f : E ‚âÉ‚Çó[‚Ñù] E) (s : set E) :
  Œº (f ‚Åª¬π' s) = ennreal.of_real (abs (f.symm : E ‚Üí‚Çó[‚Ñù] E).det) * Œº s :=
begin
  have A : (f : E ‚Üí‚Çó[‚Ñù] E).det ‚â† 0 := (linear_equiv.is_unit_det' f).ne_zero,
  convert add_haar_preimage_linear_map Œº A s,
  simp only [linear_equiv.det_coe_symm]
end

/-- The preimage of a set `s` under a continuous linear equiv `f` has measure
equal to `Œº s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp] lemma add_haar_preimage_continuous_linear_equiv
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  (f : E ‚âÉL[‚Ñù] E) (s : set E) :
  Œº (f ‚Åª¬π' s) = ennreal.of_real (abs (f.symm : E ‚Üí‚Çó[‚Ñù] E).det) * Œº s :=
add_haar_preimage_linear_equiv Œº _ s

/-- The image of a set `s` under a linear map `f` has measure
equal to `Œº s` times the absolute value of the determinant of `f`. -/
@[simp] lemma add_haar_image_linear_map
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  (f : E ‚Üí‚Çó[‚Ñù] E) (s : set E) :
  Œº (f '' s) = ennreal.of_real (abs f.det) * Œº s :=
begin
  rcases ne_or_eq f.det 0 with hf|hf,
  { let g := (f.equiv_of_det_ne_zero hf).to_continuous_linear_equiv,
    change Œº (g '' s) = _,
    rw [continuous_linear_equiv.image_eq_preimage g s, add_haar_preimage_continuous_linear_equiv],
    congr,
    ext x,
    simp only [linear_equiv.of_is_unit_det_apply, linear_equiv.to_continuous_linear_equiv_apply,
      continuous_linear_equiv.symm_symm, continuous_linear_equiv.coe_coe,
      continuous_linear_map.coe_coe, linear_equiv.to_fun_eq_coe, coe_coe] },
  { simp only [hf, zero_mul, ennreal.of_real_zero, abs_zero],
    have : Œº f.range = 0 :=
      add_haar_submodule Œº _ (linear_map.range_lt_top_of_det_eq_zero hf).ne,
    exact le_antisymm (le_trans (measure_mono (image_subset_range _ _)) this.le) (zero_le _) }
end

/-- The image of a set `s` under a continuous linear map `f` has measure
equal to `Œº s` times the absolute value of the determinant of `f`. -/
@[simp] lemma add_haar_image_continuous_linear_map
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  (f : E ‚ÜíL[‚Ñù] E) (s : set E) :
  Œº (f '' s) = ennreal.of_real (abs (f : E ‚Üí‚Çó[‚Ñù] E).det) * Œº s :=
add_haar_image_linear_map Œº _ s

/-- The image of a set `s` under a continuous linear equiv `f` has measure
equal to `Œº s` times the absolute value of the determinant of `f`. -/
@[simp] lemma add_haar_image_continuous_linear_equiv
  {E : Type*} [normed_group E] [normed_space ‚Ñù E] [measurable_space E] [borel_space E]
  [finite_dimensional ‚Ñù E] (Œº : measure E) [is_add_haar_measure Œº]
  (f : E ‚âÉL[‚Ñù] E) (s : set E) :
  Œº (f '' s) = ennreal.of_real (abs (f : E ‚Üí‚Çó[‚Ñù] E).det) * Œº s :=
add_haar_image_linear_map Œº _ s

/-!
### Basic properties of Haar measures on real vector spaces
-/

variables {E : Type*} [normed_group E] [measurable_space E] [normed_space ‚Ñù E]
  [finite_dimensional ‚Ñù E] [borel_space E] (Œº : measure E) [is_add_haar_measure Œº]

lemma map_add_haar_smul {r : ‚Ñù} (hr : r ‚â† 0) :
  measure.map ((‚Ä¢) r) Œº = ennreal.of_real (abs (r ^ (finrank ‚Ñù E))‚Åª¬π) ‚Ä¢ Œº :=
begin
  let f : E ‚Üí‚Çó[‚Ñù] E := r ‚Ä¢ 1,
  change measure.map f Œº = _,
  have hf : f.det ‚â† 0,
  { simp only [mul_one, linear_map.det_smul, ne.def, monoid_hom.map_one],
    assume h,
    exact hr (pow_eq_zero h) },
  simp only [map_linear_map_add_haar_eq_smul_add_haar Œº hf, mul_one, linear_map.det_smul,
    monoid_hom.map_one],
end

@[simp] lemma add_haar_preimage_smul {r : ‚Ñù} (hr : r ‚â† 0) (s : set E) :
  Œº (((‚Ä¢) r) ‚Åª¬π' s) = ennreal.of_real (abs (r ^ (finrank ‚Ñù E))‚Åª¬π) * Œº s :=
calc Œº (((‚Ä¢) r) ‚Åª¬π' s) = measure.map ((‚Ä¢) r) Œº s :
  ((homeomorph.smul (is_unit_iff_ne_zero.2 hr).unit).to_measurable_equiv.map_apply s).symm
... = ennreal.of_real (abs (r^(finrank ‚Ñù E))‚Åª¬π) * Œº s : by { rw map_add_haar_smul Œº hr, refl }

/-- Rescaling a set by a factor `r` multiplies its measure by `abs (r ^ dim)`. -/
@[simp] lemma add_haar_smul (r : ‚Ñù) (s : set E) :
  Œº (r ‚Ä¢ s) = ennreal.of_real (abs (r ^ (finrank ‚Ñù E))) * Œº s :=
begin
  rcases ne_or_eq r 0 with h|rfl,
  { rw [‚Üê preimage_smul_inv‚ÇÄ h, add_haar_preimage_smul Œº (inv_ne_zero h), inv_pow‚ÇÄ, inv_inv‚ÇÄ] },
  rcases eq_empty_or_nonempty s with rfl|hs,
  { simp only [measure_empty, mul_zero, smul_set_empty] },
  rw [zero_smul_set hs, ‚Üê singleton_zero],
  by_cases h : finrank ‚Ñù E = 0,
  { haveI : subsingleton E := finrank_zero_iff.1 h,
    simp only [h, one_mul, ennreal.of_real_one, abs_one, subsingleton.eq_univ_of_nonempty hs,
      pow_zero, subsingleton.eq_univ_of_nonempty (singleton_nonempty (0 : E))] },
  { haveI : nontrivial E := nontrivial_of_finrank_pos (bot_lt_iff_ne_bot.2 h),
    simp only [h, zero_mul, ennreal.of_real_zero, abs_zero, ne.def, not_false_iff, zero_pow',
      measure_singleton] }
end

/-! We don't need to state `map_add_haar_neg` here, because it has already been proved for
general Haar measures on general commutative groups. -/

/-! ### Measure of balls -/

lemma add_haar_ball_center
  {E : Type*} [normed_group E] [measurable_space E]
  [borel_space E] (Œº : measure E) [is_add_haar_measure Œº] (x : E) (r : ‚Ñù) :
  Œº (ball x r) = Œº (ball (0 : E) r) :=
begin
  have : ball (0 : E) r = ((+) x) ‚Åª¬π' (ball x r), by simp [preimage_add_ball],
  rw [this, add_haar_preimage_add]
end

lemma add_haar_closed_ball_center
  {E : Type*} [normed_group E] [measurable_space E]
  [borel_space E] (Œº : measure E) [is_add_haar_measure Œº] (x : E) (r : ‚Ñù) :
  Œº (closed_ball x r) = Œº (closed_ball (0 : E) r) :=
begin
  have : closed_ball (0 : E) r = ((+) x) ‚Åª¬π' (closed_ball x r), by simp [preimage_add_closed_ball],
  rw [this, add_haar_preimage_add]
end

lemma add_haar_ball_pos {E : Type*} [normed_group E] [measurable_space E]
  (Œº : measure E) [is_add_haar_measure Œº] (x : E) {r : ‚Ñù} (hr : 0 < r) :
  0 < Œº (ball x r) :=
is_open_ball.add_haar_pos Œº (nonempty_ball.2 hr)

lemma add_haar_closed_ball_pos {E : Type*} [normed_group E] [measurable_space E]
  (Œº : measure E) [is_add_haar_measure Œº] (x : E) {r : ‚Ñù} (hr : 0 < r) :
  0 < Œº (closed_ball x r) :=
lt_of_lt_of_le (add_haar_ball_pos Œº x hr) (measure_mono ball_subset_closed_ball)

lemma add_haar_ball_of_pos (x : E) {r : ‚Ñù} (hr : 0 < r) :
  Œº (ball x r) = ennreal.of_real (r ^ (finrank ‚Ñù E)) * Œº (ball 0 1) :=
begin
  have : ball (0 : E) r = r ‚Ä¢ ball 0 1,
    by simp [smul_ball hr.ne' (0 : E) 1, real.norm_eq_abs, abs_of_nonneg hr.le],
  simp [this, add_haar_smul, abs_of_nonneg hr.le, add_haar_ball_center],
end

lemma add_haar_ball [nontrivial E] (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
  Œº (ball x r) = ennreal.of_real (r ^ (finrank ‚Ñù E)) * Œº (ball 0 1) :=
begin
  rcases has_le.le.eq_or_lt hr with h|h,
  { simp [‚Üê h, zero_pow finrank_pos] },
  { exact add_haar_ball_of_pos Œº x h }
end

/-- The measure of a closed ball can be expressed in terms of the measure of the closed unit ball.
Use instead `add_haar_closed_ball`, which uses the measure of the open unit ball as a standard
form. -/
lemma add_haar_closed_ball' (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
  Œº (closed_ball x r) = ennreal.of_real (r ^ (finrank ‚Ñù E)) * Œº (closed_ball 0 1) :=
begin
  have : closed_ball (0 : E) r = r ‚Ä¢ closed_ball 0 1,
    by simp [smul_closed_ball r (0 : E) zero_le_one, real.norm_eq_abs, abs_of_nonneg hr],
  simp [this, add_haar_smul, abs_of_nonneg hr, add_haar_closed_ball_center],
end

lemma add_haar_closed_unit_ball_eq_add_haar_unit_ball :
  Œº (closed_ball (0 : E) 1) = Œº (ball 0 1) :=
begin
  apply le_antisymm _ (measure_mono ball_subset_closed_ball),
  have A : tendsto (Œª (r : ‚Ñù), ennreal.of_real (r ^ (finrank ‚Ñù E)) * Œº (closed_ball (0 : E) 1))
    (ùìù[<] 1) (ùìù (ennreal.of_real (1 ^ (finrank ‚Ñù E)) * Œº (closed_ball (0 : E) 1))),
  { refine ennreal.tendsto.mul _ (by simp) tendsto_const_nhds (by simp),
    exact ennreal.tendsto_of_real ((tendsto_id' nhds_within_le_nhds).pow _) },
  simp only [one_pow, one_mul, ennreal.of_real_one] at A,
  refine le_of_tendsto A _,
  refine mem_nhds_within_Iio_iff_exists_Ioo_subset.2 ‚ü®(0 : ‚Ñù), by simp, Œª r hr, _‚ü©,
  dsimp,
  rw ‚Üê add_haar_closed_ball' Œº (0 : E) hr.1.le,
  exact measure_mono (closed_ball_subset_ball hr.2)
end

lemma add_haar_closed_ball (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
  Œº (closed_ball x r) = ennreal.of_real (r ^ (finrank ‚Ñù E)) * Œº (ball 0 1) :=
by rw [add_haar_closed_ball' Œº x hr, add_haar_closed_unit_ball_eq_add_haar_unit_ball]

lemma add_haar_sphere_of_ne_zero (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
  Œº (sphere x r) = 0 :=
begin
  rcases lt_trichotomy r 0 with h|rfl|h,
  { simp only [empty_diff, measure_empty, ‚Üê closed_ball_diff_ball, closed_ball_eq_empty.2 h] },
  { exact (hr rfl).elim },
  { rw [‚Üê closed_ball_diff_ball,
        measure_diff ball_subset_closed_ball measurable_set_closed_ball measurable_set_ball
          measure_ball_lt_top.ne,
        add_haar_ball_of_pos Œº _ h, add_haar_closed_ball Œº _ h.le, tsub_self];
    apply_instance }
end

lemma add_haar_sphere [nontrivial E] (x : E) (r : ‚Ñù) :
  Œº (sphere x r) = 0 :=
begin
  rcases eq_or_ne r 0 with rfl|h,
  { simp only [‚Üê closed_ball_diff_ball, diff_empty, closed_ball_zero,
               ball_zero, measure_singleton] },
  { exact add_haar_sphere_of_ne_zero Œº x h }
end

end measure

end measure_theory
