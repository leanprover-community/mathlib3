/-
Copyright (c) 2021 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, SÃ©bastien GouÃ«zel
-/
import measure_theory.measure.lebesgue
import measure_theory.measure.haar
import linear_algebra.finite_dimensional
import analysis.normed_space.pointwise
import measure_theory.group.pointwise
import measure_theory.measure.doubling

/-!
# Relationship between the Haar and Lebesgue measures

We prove that the Haar measure and Lebesgue measure are equal on `â„` and on `â„^Î¹`, in
`measure_theory.add_haar_measure_eq_volume` and `measure_theory.add_haar_measure_eq_volume_pi`.

We deduce basic properties of any Haar measure on a finite dimensional real vector space:
* `map_linear_map_add_haar_eq_smul_add_haar`: a linear map rescales the Haar measure by the
  absolute value of its determinant.
* `add_haar_preimage_linear_map` : when `f` is a linear map with nonzero determinant, the measure
  of `f â»Â¹' s` is the measure of `s` multiplied by the absolute value of the inverse of the
  determinant of `f`.
* `add_haar_image_linear_map` : when `f` is a linear map, the measure of `f '' s` is the
  measure of `s` multiplied by the absolute value of the determinant of `f`.
* `add_haar_submodule` : a strict submodule has measure `0`.
* `add_haar_smul` : the measure of `r â€¢ s` is `|r| ^ dim * Î¼ s`.
* `add_haar_ball`: the measure of `ball x r` is `r ^ dim * Î¼ (ball 0 1)`.
* `add_haar_closed_ball`: the measure of `closed_ball x r` is `r ^ dim * Î¼ (ball 0 1)`.
* `add_haar_sphere`: spheres have zero measure.

We also show that a Lebesgue density point `x` of a set `s` (with respect to closed balls) has
density one for the rescaled copies `{x} + r â€¢ t` of a given set `t` with positive measure, in
`tendsto_add_haar_inter_smul_one_of_density_one`. In particular, `s` intersects `{x} + r â€¢ t` for
small `r`, see `eventually_nonempty_inter_smul_of_density_one`.
-/

namespace linear_map
variables {ğ•œ Î± Î² Î³ : Type*} [semiring ğ•œ] [add_comm_monoid Î±] [add_comm_monoid Î²] [add_comm_monoid Î³]
  [module ğ•œ Î±] [module ğ•œ Î²] [module ğ•œ Î³]

lemma eq_comp_symm (e : Î± â‰ƒâ‚—[ğ•œ] Î²) (f : Î² â†’â‚—[ğ•œ] Î³) (g : Î± â†’â‚—[ğ•œ] Î³) :
  f = g.comp (e.symm : Î² â†’â‚—[ğ•œ] Î±) â†” f.comp (e : Î± â†’â‚—[ğ•œ] Î²) = g :=
by { simp_rw fun_like.ext'_iff, exact e.to_equiv.eq_comp_symm _ _ }

lemma comp_symm_eq (e : Î± â‰ƒâ‚—[ğ•œ] Î²) (f : Î² â†’â‚—[ğ•œ] Î³) (g : Î± â†’â‚—[ğ•œ] Î³) :
  g.comp (e.symm : Î² â†’â‚—[ğ•œ] Î±) = f â†” g = f.comp (e : Î± â†’â‚—[ğ•œ] Î²) :=
by { simp_rw fun_like.ext'_iff, exact e.to_equiv.comp_symm_eq _ _ }

lemma eq_symm_comp (e : Î± â‰ƒâ‚—[ğ•œ] Î²) (f : Î³ â†’â‚—[ğ•œ] Î±) (g : Î³ â†’â‚—[ğ•œ] Î²) :
  f = (e.symm : Î² â†’â‚—[ğ•œ] Î±).comp g â†” (e : Î± â†’â‚—[ğ•œ] Î²).comp f = g :=
by { simp_rw fun_like.ext'_iff, exact e.to_equiv.eq_symm_comp _ _ }

lemma symm_comp_eq (e : Î± â‰ƒâ‚—[ğ•œ] Î²) (f : Î³ â†’â‚—[ğ•œ] Î±) (g : Î³ â†’â‚—[ğ•œ] Î²) :
  (e.symm : Î² â†’â‚—[ğ•œ] Î±).comp g = f â†” g = (e : Î± â†’â‚—[ğ•œ] Î²).comp f :=
by { simp_rw fun_like.ext'_iff, exact e.to_equiv.symm_comp_eq _ _ }

end linear_map

namespace continuous_linear_map
variables {ğ•œ Î± Î² Î³ : Type*} [semiring ğ•œ] [add_comm_monoid Î±] [add_comm_monoid Î²] [add_comm_monoid Î³]
  [module ğ•œ Î±] [module ğ•œ Î²] [module ğ•œ Î³]
  [topological_space Î±] [topological_space Î²] [topological_space Î³]

lemma eq_comp_symm (e : Î± â‰ƒL[ğ•œ] Î²) (f : Î² â†’L[ğ•œ] Î³) (g : Î± â†’L[ğ•œ] Î³) :
  f = g.comp (e.symm : Î² â†’L[ğ•œ] Î±) â†” f.comp (e : Î± â†’L[ğ•œ] Î²) = g :=
by { simp_rw fun_like.ext'_iff, exact e.to_equiv.eq_comp_symm _ _ }

lemma comp_symm_eq (e : Î± â‰ƒL[ğ•œ] Î²) (f : Î² â†’L[ğ•œ] Î³) (g : Î± â†’L[ğ•œ] Î³) :
  g.comp (e.symm : Î² â†’L[ğ•œ] Î±) = f â†” g = f.comp (e : Î± â†’L[ğ•œ] Î²) :=
by { simp_rw fun_like.ext'_iff, exact e.to_equiv.comp_symm_eq _ _ }

lemma eq_symm_comp (e : Î± â‰ƒL[ğ•œ] Î²) (f : Î³ â†’L[ğ•œ] Î±) (g : Î³ â†’L[ğ•œ] Î²) :
  f = (e.symm : Î² â†’L[ğ•œ] Î±).comp g â†” (e : Î± â†’L[ğ•œ] Î²).comp f = g :=
by { simp_rw fun_like.ext'_iff, exact e.to_equiv.eq_symm_comp _ _ }

lemma symm_comp_eq (e : Î± â‰ƒL[ğ•œ] Î²) (f : Î³ â†’L[ğ•œ] Î±) (g : Î³ â†’L[ğ•œ] Î²) :
  (e.symm : Î² â†’L[ğ•œ] Î±).comp g = f â†” g = (e : Î± â†’L[ğ•œ] Î²).comp f :=
by { simp_rw fun_like.ext'_iff, exact e.to_equiv.symm_comp_eq _ _ }

end continuous_linear_map

namespace linear_equiv
variables {ğ•œ Î± Î² Î³ Î´ : Type*} [semiring ğ•œ] [add_comm_monoid Î±] [add_comm_monoid Î²]
  [add_comm_monoid Î³] [add_comm_monoid Î´] [module ğ•œ Î±] [module ğ•œ Î²] [module ğ•œ Î³] [module ğ•œ Î´]

@[simp] lemma symm_comp_self (e : Î± â‰ƒâ‚—[ğ•œ] Î²) : e.symm âˆ˜ e = id := e.to_equiv.symm_comp_self
@[simp] lemma self_comp_symm (e : Î± â‰ƒâ‚—[ğ•œ] Î²) : e âˆ˜ e.symm = id := e.to_equiv.self_comp_symm

lemma trans_assoc (e : Î± â‰ƒâ‚—[ğ•œ] Î²) (f : Î² â‰ƒâ‚—[ğ•œ] Î³) (g : Î³ â‰ƒâ‚—[ğ•œ] Î´) :
  (e.trans f).trans g = e.trans (f.trans g) := rfl

end linear_equiv

namespace continuous_linear_equiv
variables {ğ•œ Î± Î² Î³ Î´ : Type*} [semiring ğ•œ] [add_comm_monoid Î±] [add_comm_monoid Î²]
  [add_comm_monoid Î³] [add_comm_monoid Î´] [module ğ•œ Î±] [module ğ•œ Î²] [module ğ•œ Î³] [module ğ•œ Î´]
  [topological_space Î±] [topological_space Î²] [topological_space Î³] [topological_space Î´]

lemma trans_assoc (e : Î± â‰ƒL[ğ•œ] Î²) (f : Î² â‰ƒL[ğ•œ] Î³) (g : Î³ â‰ƒL[ğ•œ] Î´) :
  (e.trans f).trans g = e.trans (f.trans g) := rfl

end continuous_linear_equiv

instance linear_map.can_lift_continuous_linear_map {ğ•œ E F : Type*} [nontrivially_normed_field ğ•œ]
  [add_comm_group E] [module ğ•œ E] [topological_space E] [topological_add_group E]
  [has_continuous_smul ğ•œ E] [add_comm_group F] [module ğ•œ F] [topological_space F]
  [topological_add_group F] [has_continuous_smul ğ•œ F] [complete_space ğ•œ] [t2_space E]
  [finite_dimensional ğ•œ E] :
  can_lift (E â†’â‚—[ğ•œ] F) (E â†’L[ğ•œ] F) coe âŠ¤ :=
âŸ¨Î» f _, âŸ¨f.to_continuous_linear_map, rflâŸ©âŸ©

instance linear_equiv.can_lift_continuous_linear_equiv {ğ•œ E F : Type*} [nontrivially_normed_field ğ•œ]
  [add_comm_group E] [module ğ•œ E] [topological_space E] [topological_add_group E]
  [has_continuous_smul ğ•œ E] [add_comm_group F] [module ğ•œ F] [topological_space F]
  [topological_add_group F] [has_continuous_smul ğ•œ F] [complete_space ğ•œ] [t2_space E] [t2_space F]
  [finite_dimensional ğ•œ E] :
  can_lift (E â‰ƒâ‚—[ğ•œ] F) (E â‰ƒL[ğ•œ] F) continuous_linear_equiv.to_linear_equiv âŠ¤ :=
âŸ¨Î» f _, âŸ¨_, f.to_linear_equiv_to_continuous_linear_equivâŸ©âŸ©

open topological_space set filter metric
open_locale ennreal pointwise topological_space nnreal

/-- The interval `[0,1]` as a compact set with non-empty interior. -/
def topological_space.positive_compacts.Icc01 : positive_compacts â„ :=
{ carrier := Icc 0 1,
  is_compact' := is_compact_Icc,
  interior_nonempty' := by simp_rw [interior_Icc, nonempty_Ioo, zero_lt_one] }

universe u

/-- The set `[0,1]^Î¹` as a compact set with non-empty interior. -/
def topological_space.positive_compacts.pi_Icc01 (Î¹ : Type*) [fintype Î¹] :
  positive_compacts (Î¹ â†’ â„) :=
{ carrier := pi univ (Î» i, Icc 0 1),
  is_compact' := is_compact_univ_pi (Î» i, is_compact_Icc),
  interior_nonempty' := by simp only [interior_pi_set, set.to_finite, interior_Icc,
    univ_pi_nonempty_iff, nonempty_Ioo, implies_true_iff, zero_lt_one] }

namespace measure_theory

open measure topological_space.positive_compacts finite_dimensional

/-!
### The Lebesgue measure is a Haar measure on `â„` and on `â„^Î¹`.
-/

/-- The Haar measure equals the Lebesgue measure on `â„`. -/
lemma add_haar_measure_eq_volume : add_haar_measure Icc01 = volume :=
by { convert (add_haar_measure_unique volume Icc01).symm, simp [Icc01] }

instance : is_add_haar_measure (volume : measure â„) :=
by { rw â† add_haar_measure_eq_volume, apply_instance }

/-- The Haar measure equals the Lebesgue measure on `â„^Î¹`. -/
lemma add_haar_measure_eq_volume_pi (Î¹ : Type*) [fintype Î¹] :
  add_haar_measure (pi_Icc01 Î¹) = volume :=
begin
  convert (add_haar_measure_unique volume (pi_Icc01 Î¹)).symm,
  simp only [pi_Icc01, volume_pi_pi (Î» i, Icc (0 : â„) 1), positive_compacts.coe_mk,
    compacts.coe_mk, finset.prod_const_one, ennreal.of_real_one, real.volume_Icc, one_smul,
    sub_zero],
end

instance is_add_haar_measure_volume_pi (Î¹ : Type*) [fintype Î¹] :
  is_add_haar_measure (volume : measure (Î¹ â†’ â„)) :=
by { rw â† add_haar_measure_eq_volume_pi, apply_instance }

namespace measure

/-!
### Strict subspaces have zero measure
-/

/-- If a set is disjoint of its translates by infinitely many bounded vectors, then it has measure
zero. This auxiliary lemma proves this assuming additionally that the set is bounded. -/
lemma add_haar_eq_zero_of_disjoint_translates_aux
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  {s : set E} (u : â„• â†’ E) (sb : bounded s) (hu : bounded (range u))
  (hs : pairwise (disjoint on (Î» n, {u n} + s))) (h's : measurable_set s) :
  Î¼ s = 0 :=
begin
  by_contra h,
  apply lt_irrefl âˆ,
  calc
  âˆ = âˆ‘' (n : â„•), Î¼ s : (ennreal.tsum_const_eq_top_of_ne_zero h).symm
  ... = âˆ‘' (n : â„•), Î¼ ({u n} + s) :
    by { congr' 1, ext1 n, simp only [image_add_left, measure_preimage_add, singleton_add] }
  ... = Î¼ (â‹ƒ n, {u n} + s) :
    by rw measure_Union hs
      (Î» n, by simpa only [image_add_left, singleton_add] using measurable_id.const_add _ h's)
  ... = Î¼ (range u + s) : by rw [â† Union_add, Union_singleton_eq_range]
  ... < âˆ : bounded.measure_lt_top (hu.add sb)
end

/-- If a set is disjoint of its translates by infinitely many bounded vectors, then it has measure
zero. -/
lemma add_haar_eq_zero_of_disjoint_translates
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  {s : set E} (u : â„• â†’ E) (hu : bounded (range u))
  (hs : pairwise (disjoint on (Î» n, {u n} + s))) (h's : measurable_set s) :
  Î¼ s = 0 :=
begin
  suffices H : âˆ€ R, Î¼ (s âˆ© closed_ball 0 R) = 0,
  { apply le_antisymm _ (zero_le _),
    calc Î¼ s â‰¤ âˆ‘' (n : â„•), Î¼ (s âˆ© closed_ball 0 n) :
      by { conv_lhs { rw â† Union_inter_closed_ball_nat s 0 }, exact measure_Union_le _ }
    ... = 0 : by simp only [H, tsum_zero] },
  assume R,
  apply add_haar_eq_zero_of_disjoint_translates_aux Î¼ u
    (bounded.mono (inter_subset_right _ _) bounded_closed_ball) hu _
    (h's.inter (measurable_set_closed_ball)),
  apply pairwise_disjoint.mono hs (Î» n, _),
  exact add_subset_add (subset.refl _) (inter_subset_left _ _)
end

/-- A strict vector subspace has measure zero. -/
lemma add_haar_submodule
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  (s : submodule â„ E) (hs : s â‰  âŠ¤) : Î¼ s = 0 :=
begin
  obtain âŸ¨x, hxâŸ© : âˆƒ x, x âˆ‰ s,
    by simpa only [submodule.eq_top_iff', not_exists, ne.def, not_forall] using hs,
  obtain âŸ¨c, cpos, coneâŸ© : âˆƒ (c : â„), 0 < c âˆ§ c < 1 := âŸ¨1/2, by norm_num, by norm_numâŸ©,
  have A : bounded (range (Î» (n : â„•), (c ^ n) â€¢ x)),
  { have : tendsto (Î» (n : â„•), (c ^ n) â€¢ x) at_top (ğ“ ((0 : â„) â€¢ x)) :=
      (tendsto_pow_at_top_nhds_0_of_lt_1 cpos.le cone).smul_const x,
    exact bounded_range_of_tendsto _ this },
  apply add_haar_eq_zero_of_disjoint_translates Î¼ _ A _
    (submodule.closed_of_finite_dimensional s).measurable_set,
  assume m n hmn,
  simp only [function.on_fun, image_add_left, singleton_add, disjoint_left, mem_preimage,
             set_like.mem_coe],
  assume y hym hyn,
  have A : (c ^ n - c ^ m) â€¢ x âˆˆ s,
  { convert s.sub_mem hym hyn,
    simp only [sub_smul, neg_sub_neg, add_sub_add_right_eq_sub] },
  have H : c ^ n - c ^ m â‰  0,
    by simpa only [sub_eq_zero, ne.def] using (strict_anti_pow cpos cone).injective.ne hmn.symm,
  have : x âˆˆ s,
  { convert s.smul_mem (c ^ n - c ^ m)â»Â¹ A,
    rw [smul_smul, inv_mul_cancel H, one_smul] },
  exact hx this
end

/-- A strict affine subspace has measure zero. -/
lemma add_haar_affine_subspace
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  (s : affine_subspace â„ E) (hs : s â‰  âŠ¤) : Î¼ s = 0 :=
begin
  rcases s.eq_bot_or_nonempty with rfl|hne,
  { rw [affine_subspace.bot_coe, measure_empty] },
  rw [ne.def, â† affine_subspace.direction_eq_top_iff_of_nonempty hne] at hs,
  rcases hne with âŸ¨x, hx : x âˆˆ sâŸ©,
  simpa only [affine_subspace.coe_direction_eq_vsub_set_right hx, vsub_eq_sub,
    sub_eq_add_neg, image_add_right, neg_neg, measure_preimage_add_right]
    using add_haar_submodule Î¼ s.direction hs
end

/-!
### Applying a linear map rescales Haar measure by the determinant

We first prove this on `Î¹ â†’ â„`, using that this is already known for the product Lebesgue
measure (thanks to matrices computations). Then, we extend this to any finite-dimensional real
vector space by using a linear equiv with a space of the form `Î¹ â†’ â„`, and arguing that such a
linear equiv maps Haar measure to Haar measure.
-/

lemma map_linear_map_add_haar_pi_eq_smul_add_haar
  {Î¹ : Type*} [finite Î¹] {f : (Î¹ â†’ â„) â†’â‚—[â„] (Î¹ â†’ â„)} (hf : f.det â‰  0)
  (Î¼ : measure (Î¹ â†’ â„)) [is_add_haar_measure Î¼] :
  measure.map f Î¼ = ennreal.of_real (abs (f.det)â»Â¹) â€¢ Î¼ :=
begin
  casesI nonempty_fintype Î¹,
  /- We have already proved the result for the Lebesgue product measure, using matrices.
  We deduce it for any Haar measure by uniqueness (up to scalar multiplication). -/
  have := add_haar_measure_unique Î¼ (pi_Icc01 Î¹),
  rw [this, add_haar_measure_eq_volume_pi, measure.map_smul,
    real.map_linear_map_volume_pi_eq_smul_volume_pi hf, smul_comm],
end

lemma map_linear_map_add_haar_eq_smul_add_haar
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  {f : E â†’â‚—[â„] E} (hf : f.det â‰  0) :
  measure.map f Î¼ = ennreal.of_real (abs (f.det)â»Â¹) â€¢ Î¼ :=
begin
  lift f to E â†’L[â„] E using trivial,
  -- we reduce to the case of `E = Î¹ â†’ â„`, for which we have already proved the result using
  -- matrices in `map_linear_map_add_haar_pi_eq_smul_add_haar`.
  let Î¹ := fin (finrank â„ E),
  haveI : finite_dimensional â„ (Î¹ â†’ â„) := by apply_instance,
  have : finrank â„ E = finrank â„ (Î¹ â†’ â„), by simp,
  have e : E â‰ƒL[â„] Î¹ â†’ â„ := continuous_linear_equiv.of_finrank_eq this,
  set g := (e : E â†’L[â„] (Î¹ â†’ â„)).comp (f.comp (e.symm : (Î¹ â†’ â„) â†’L[â„] E)) with hg,
  have gdet : g.det = f.det := by exact linear_map.det_conj _ _,
  rw [â†continuous_linear_map.symm_comp_eq, continuous_linear_map.eq_comp_symm] at hg,
  clear_value g,
  subst hg,
  rw [â†continuous_linear_map.det, â†gdet] at âŠ¢ hf,
  simp only [continuous_linear_map.coe_comp, linear_map.coe_comp, continuous_linear_map.coe_coe,
    continuous_linear_equiv.coe_coe],
  have me : measurable e := e.continuous.measurable,
  have mg : measurable g := g.continuous.measurable,
  have mesymm : measurable e.symm := e.symm.continuous.measurable,
  rw [â† map_map mesymm (mg.comp me), â† map_map mg me, â†g.coe_coe,
    map_linear_map_add_haar_pi_eq_smul_add_haar hf (map e Î¼), measure.map_smul, map_map mesymm me,
    e.symm_comp_self, measure.map_id],
end

/-- The preimage of a set `s` under a linear map `f` with nonzero determinant has measure
equal to `Î¼ s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp] lemma add_haar_preimage_linear_map
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  {f : E â†’â‚—[â„] E} (hf : f.det â‰  0) (s : set E) :
  Î¼ (f â»Â¹' s) = ennreal.of_real (abs (f.det)â»Â¹) * Î¼ s :=
calc Î¼ (f â»Â¹' s) = measure.map f Î¼ s :
  ((f.equiv_of_det_ne_zero hf).to_continuous_linear_equiv.to_homeomorph
    .to_measurable_equiv.map_apply s).symm
... = ennreal.of_real (abs (f.det)â»Â¹) * Î¼ s :
  by { rw map_linear_map_add_haar_eq_smul_add_haar Î¼ hf, refl }

/-- The preimage of a set `s` under a continuous linear map `f` with nonzero determinant has measure
equal to `Î¼ s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp] lemma add_haar_preimage_continuous_linear_map
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  {f : E â†’L[â„] E} (hf : linear_map.det (f : E â†’â‚—[â„] E) â‰  0) (s : set E) :
  Î¼ (f â»Â¹' s) = ennreal.of_real (abs (linear_map.det (f : E â†’â‚—[â„] E))â»Â¹) * Î¼ s :=
add_haar_preimage_linear_map Î¼ hf s

/-- The preimage of a set `s` under a linear equiv `f` has measure
equal to `Î¼ s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp] lemma add_haar_preimage_linear_equiv
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  (f : E â‰ƒâ‚—[â„] E) (s : set E) :
  Î¼ (f â»Â¹' s) = ennreal.of_real (abs (f.symm : E â†’â‚—[â„] E).det) * Î¼ s :=
begin
  have A : (f : E â†’â‚—[â„] E).det â‰  0 := (linear_equiv.is_unit_det' f).ne_zero,
  convert add_haar_preimage_linear_map Î¼ A s,
  simp only [linear_equiv.det_coe_symm]
end

/-- The preimage of a set `s` under a continuous linear equiv `f` has measure
equal to `Î¼ s` times the absolute value of the inverse of the determinant of `f`. -/
@[simp] lemma add_haar_preimage_continuous_linear_equiv
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  (f : E â‰ƒL[â„] E) (s : set E) :
  Î¼ (f â»Â¹' s) = ennreal.of_real (abs (f.symm : E â†’â‚—[â„] E).det) * Î¼ s :=
add_haar_preimage_linear_equiv Î¼ _ s

/-- The image of a set `s` under a linear map `f` has measure
equal to `Î¼ s` times the absolute value of the determinant of `f`. -/
@[simp] lemma add_haar_image_linear_map
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  (f : E â†’â‚—[â„] E) (s : set E) :
  Î¼ (f '' s) = ennreal.of_real (abs f.det) * Î¼ s :=
begin
  rcases ne_or_eq f.det 0 with hf|hf,
  { let g := (f.equiv_of_det_ne_zero hf).to_continuous_linear_equiv,
    change Î¼ (g '' s) = _,
    rw [continuous_linear_equiv.image_eq_preimage g s, add_haar_preimage_continuous_linear_equiv],
    congr,
    ext x,
    simp only [linear_equiv.coe_to_continuous_linear_equiv, linear_equiv.of_is_unit_det_apply,
               linear_equiv.coe_coe, continuous_linear_equiv.symm_symm], },
  { simp only [hf, zero_mul, ennreal.of_real_zero, abs_zero],
    have : Î¼ f.range = 0 :=
      add_haar_submodule Î¼ _ (linear_map.range_lt_top_of_det_eq_zero hf).ne,
    exact le_antisymm (le_trans (measure_mono (image_subset_range _ _)) this.le) (zero_le _) }
end

/-- The image of a set `s` under a continuous linear map `f` has measure
equal to `Î¼ s` times the absolute value of the determinant of `f`. -/
@[simp] lemma add_haar_image_continuous_linear_map
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  (f : E â†’L[â„] E) (s : set E) :
  Î¼ (f '' s) = ennreal.of_real (abs (f : E â†’â‚—[â„] E).det) * Î¼ s :=
add_haar_image_linear_map Î¼ _ s

/-- The image of a set `s` under a continuous linear equiv `f` has measure
equal to `Î¼ s` times the absolute value of the determinant of `f`. -/
@[simp] lemma add_haar_image_continuous_linear_equiv
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  (f : E â‰ƒL[â„] E) (s : set E) :
  Î¼ (f '' s) = ennreal.of_real (abs (f : E â†’â‚—[â„] E).det) * Î¼ s :=
Î¼.add_haar_image_linear_map (f : E â†’â‚—[â„] E) s

/-!
### Basic properties of Haar measures on real vector spaces
-/

variables {E : Type*} [normed_add_comm_group E] [measurable_space E] [normed_space â„ E]
  [finite_dimensional â„ E] [borel_space E] (Î¼ : measure E) [is_add_haar_measure Î¼]
  {F : Type*} [normed_add_comm_group F] [normed_space â„ F] [complete_space F]

lemma map_add_haar_smul {r : â„} (hr : r â‰  0) :
  measure.map ((â€¢) r) Î¼ = ennreal.of_real (abs (r ^ (finrank â„ E))â»Â¹) â€¢ Î¼ :=
begin
  let f : E â†’â‚—[â„] E := r â€¢ 1,
  change measure.map f Î¼ = _,
  have hf : f.det â‰  0,
  { simp only [mul_one, linear_map.det_smul, ne.def, monoid_hom.map_one],
    assume h,
    exact hr (pow_eq_zero h) },
  simp only [map_linear_map_add_haar_eq_smul_add_haar Î¼ hf, mul_one, linear_map.det_smul,
    monoid_hom.map_one],
end

@[simp] lemma add_haar_preimage_smul {r : â„} (hr : r â‰  0) (s : set E) :
  Î¼ (((â€¢) r) â»Â¹' s) = ennreal.of_real (abs (r ^ (finrank â„ E))â»Â¹) * Î¼ s :=
calc Î¼ (((â€¢) r) â»Â¹' s) = measure.map ((â€¢) r) Î¼ s :
  ((homeomorph.smul (is_unit_iff_ne_zero.2 hr).unit).to_measurable_equiv.map_apply s).symm
... = ennreal.of_real (abs (r^(finrank â„ E))â»Â¹) * Î¼ s : by { rw map_add_haar_smul Î¼ hr, refl }

/-- Rescaling a set by a factor `r` multiplies its measure by `abs (r ^ dim)`. -/
@[simp] lemma add_haar_smul (r : â„) (s : set E) :
  Î¼ (r â€¢ s) = ennreal.of_real (abs (r ^ (finrank â„ E))) * Î¼ s :=
begin
  rcases ne_or_eq r 0 with h|rfl,
  { rw [â† preimage_smul_invâ‚€ h, add_haar_preimage_smul Î¼ (inv_ne_zero h), inv_pow, inv_inv] },
  rcases eq_empty_or_nonempty s with rfl|hs,
  { simp only [measure_empty, mul_zero, smul_set_empty] },
  rw [zero_smul_set hs, â† singleton_zero],
  by_cases h : finrank â„ E = 0,
  { haveI : subsingleton E := finrank_zero_iff.1 h,
    simp only [h, one_mul, ennreal.of_real_one, abs_one, subsingleton.eq_univ_of_nonempty hs,
      pow_zero, subsingleton.eq_univ_of_nonempty (singleton_nonempty (0 : E))] },
  { haveI : nontrivial E := nontrivial_of_finrank_pos (bot_lt_iff_ne_bot.2 h),
    simp only [h, zero_mul, ennreal.of_real_zero, abs_zero, ne.def, not_false_iff, zero_pow',
      measure_singleton] }
end

@[simp] lemma add_haar_image_homothety (x : E) (r : â„) (s : set E) :
  Î¼ (affine_map.homothety x r '' s) = ennreal.of_real (abs (r ^ (finrank â„ E))) * Î¼ s :=
calc Î¼ (affine_map.homothety x r '' s) = Î¼ ((Î» y, y + x) '' (r â€¢ ((Î» y, y + (-x)) '' s))) :
  by { simp only [â† image_smul, image_image, â† sub_eq_add_neg], refl }
... = ennreal.of_real (abs (r ^ (finrank â„ E))) * Î¼ s :
  by simp only [image_add_right, measure_preimage_add_right, add_haar_smul]

/-- The integral of `f (R â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
lemma integral_comp_smul (f : E â†’ F) (R : â„) :
  âˆ« x, f (R â€¢ x) âˆ‚Î¼ = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« x, f x âˆ‚Î¼ :=
begin
  rcases eq_or_ne R 0 with rfl|hR,
  { simp only [zero_smul, integral_const],
    rcases nat.eq_zero_or_pos (finrank â„ E) with hE|hE,
    { haveI : subsingleton E, from finrank_zero_iff.1 hE,
      have : f = (Î» x, f 0), { ext x, rw subsingleton.elim x 0 },
      conv_rhs { rw this },
      simp only [hE, pow_zero, inv_one, abs_one, one_smul, integral_const] },
    { haveI : nontrivial E, from finrank_pos_iff.1 hE,
      simp only [zero_pow hE, measure_univ_of_is_add_left_invariant, ennreal.top_to_real, zero_smul,
        inv_zero, abs_zero]} },
  { calc âˆ« x, f (R â€¢ x) âˆ‚Î¼ = âˆ« y, f y âˆ‚(measure.map (Î» x, R â€¢ x) Î¼) :
      (integral_map_equiv (homeomorph.smul (is_unit_iff_ne_zero.2 hR).unit)
        .to_measurable_equiv f).symm
    ... = |(R ^ finrank â„ E)â»Â¹| â€¢ âˆ« x, f x âˆ‚Î¼ :
      by simp only [map_add_haar_smul Î¼ hR, integral_smul_measure, ennreal.to_real_of_real,
                    abs_nonneg] }
end

/-- The integral of `f (R â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
lemma integral_comp_smul_of_nonneg (f : E â†’ F) (R : â„) {hR : 0 â‰¤ R} :
  âˆ« x, f (R â€¢ x) âˆ‚Î¼ = (R ^ finrank â„ E)â»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ :=
by rw [integral_comp_smul Î¼ f R, abs_of_nonneg (inv_nonneg.2 (pow_nonneg hR _))]

/-- The integral of `f (Râ»Â¹ â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
lemma integral_comp_inv_smul (f : E â†’ F) (R : â„) :
  âˆ« x, f (Râ»Â¹ â€¢ x) âˆ‚Î¼ = |(R ^ finrank â„ E)| â€¢ âˆ« x, f x âˆ‚Î¼ :=
by rw [integral_comp_smul Î¼ f (Râ»Â¹), inv_pow, inv_inv]

/-- The integral of `f (Râ»Â¹ â€¢ x)` with respect to an additive Haar measure is a multiple of the
integral of `f`. The formula we give works even when `f` is not integrable or `R = 0`
thanks to the convention that a non-integrable function has integral zero. -/
lemma integral_comp_inv_smul_of_nonneg (f : E â†’ F) {R : â„} (hR : 0 â‰¤ R) :
  âˆ« x, f (Râ»Â¹ â€¢ x) âˆ‚Î¼ = R ^ finrank â„ E â€¢ âˆ« x, f x âˆ‚Î¼ :=
by rw [integral_comp_inv_smul Î¼ f R, abs_of_nonneg ((pow_nonneg hR _))]

/-! We don't need to state `map_add_haar_neg` here, because it has already been proved for
general Haar measures on general commutative groups. -/

/-! ### Measure of balls -/

lemma add_haar_ball_center
  {E : Type*} [normed_add_comm_group E] [measurable_space E]
  [borel_space E] (Î¼ : measure E) [is_add_haar_measure Î¼] (x : E) (r : â„) :
  Î¼ (ball x r) = Î¼ (ball (0 : E) r) :=
begin
  have : ball (0 : E) r = ((+) x) â»Â¹' (ball x r), by simp [preimage_add_ball],
  rw [this, measure_preimage_add]
end

lemma add_haar_closed_ball_center
  {E : Type*} [normed_add_comm_group E] [measurable_space E]
  [borel_space E] (Î¼ : measure E) [is_add_haar_measure Î¼] (x : E) (r : â„) :
  Î¼ (closed_ball x r) = Î¼ (closed_ball (0 : E) r) :=
begin
  have : closed_ball (0 : E) r = ((+) x) â»Â¹' (closed_ball x r), by simp [preimage_add_closed_ball],
  rw [this, measure_preimage_add]
end

lemma add_haar_ball_mul_of_pos (x : E) {r : â„} (hr : 0 < r) (s : â„) :
  Î¼ (ball x (r * s)) = ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (ball 0 s) :=
begin
  have : ball (0 : E) (r * s) = r â€¢ ball 0 s,
    by simp only [smul_ball hr.ne' (0 : E) s, real.norm_eq_abs, abs_of_nonneg hr.le, smul_zero],
  simp only [this, add_haar_smul, abs_of_nonneg hr.le, add_haar_ball_center, abs_pow],
end

lemma add_haar_ball_of_pos (x : E) {r : â„} (hr : 0 < r) :
  Î¼ (ball x r) = ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (ball 0 1) :=
by rw [â† add_haar_ball_mul_of_pos Î¼ x hr, mul_one]

lemma add_haar_ball_mul [nontrivial E] (x : E) {r : â„} (hr : 0 â‰¤ r) (s : â„) :
  Î¼ (ball x (r * s)) = ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (ball 0 s) :=
begin
  rcases has_le.le.eq_or_lt hr with h|h,
  { simp only [â† h, zero_pow finrank_pos, measure_empty, zero_mul, ennreal.of_real_zero,
               ball_zero] },
  { exact add_haar_ball_mul_of_pos Î¼ x h s }
end

lemma add_haar_ball [nontrivial E] (x : E) {r : â„} (hr : 0 â‰¤ r) :
  Î¼ (ball x r) = ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (ball 0 1) :=
by rw [â† add_haar_ball_mul Î¼ x hr, mul_one]

lemma add_haar_closed_ball_mul_of_pos (x : E) {r : â„} (hr : 0 < r) (s : â„) :
  Î¼ (closed_ball x (r * s)) = ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (closed_ball 0 s) :=
begin
  have : closed_ball (0 : E) (r * s) = r â€¢ closed_ball 0 s,
    by simp [smul_closed_ball' hr.ne' (0 : E), abs_of_nonneg hr.le],
  simp only [this, add_haar_smul, abs_of_nonneg hr.le, add_haar_closed_ball_center, abs_pow],
end

lemma add_haar_closed_ball_mul (x : E) {r : â„} (hr : 0 â‰¤ r) {s : â„} (hs : 0 â‰¤ s) :
  Î¼ (closed_ball x (r * s)) = ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (closed_ball 0 s) :=
begin
  have : closed_ball (0 : E) (r * s) = r â€¢ closed_ball 0 s,
    by simp [smul_closed_ball r (0 : E) hs, abs_of_nonneg hr],
  simp only [this, add_haar_smul, abs_of_nonneg hr, add_haar_closed_ball_center, abs_pow],
end

/-- The measure of a closed ball can be expressed in terms of the measure of the closed unit ball.
Use instead `add_haar_closed_ball`, which uses the measure of the open unit ball as a standard
form. -/
lemma add_haar_closed_ball' (x : E) {r : â„} (hr : 0 â‰¤ r) :
  Î¼ (closed_ball x r) = ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (closed_ball 0 1) :=
by rw [â† add_haar_closed_ball_mul Î¼ x hr zero_le_one, mul_one]

lemma add_haar_closed_unit_ball_eq_add_haar_unit_ball :
  Î¼ (closed_ball (0 : E) 1) = Î¼ (ball 0 1) :=
begin
  apply le_antisymm _ (measure_mono ball_subset_closed_ball),
  have A : tendsto (Î» (r : â„), ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (closed_ball (0 : E) 1))
    (ğ“[<] 1) (ğ“ (ennreal.of_real (1 ^ (finrank â„ E)) * Î¼ (closed_ball (0 : E) 1))),
  { refine ennreal.tendsto.mul _ (by simp) tendsto_const_nhds (by simp),
    exact ennreal.tendsto_of_real ((tendsto_id'.2 nhds_within_le_nhds).pow _) },
  simp only [one_pow, one_mul, ennreal.of_real_one] at A,
  refine le_of_tendsto A _,
  refine mem_nhds_within_Iio_iff_exists_Ioo_subset.2 âŸ¨(0 : â„), by simp, Î» r hr, _âŸ©,
  dsimp,
  rw â† add_haar_closed_ball' Î¼ (0 : E) hr.1.le,
  exact measure_mono (closed_ball_subset_ball hr.2)
end

lemma add_haar_closed_ball (x : E) {r : â„} (hr : 0 â‰¤ r) :
  Î¼ (closed_ball x r) = ennreal.of_real (r ^ (finrank â„ E)) * Î¼ (ball 0 1) :=
by rw [add_haar_closed_ball' Î¼ x hr, add_haar_closed_unit_ball_eq_add_haar_unit_ball]

lemma add_haar_closed_ball_eq_add_haar_ball [nontrivial E] (x : E) (r : â„) :
  Î¼ (closed_ball x r) = Î¼ (ball x r) :=
begin
  by_cases h : r < 0,
  { rw [metric.closed_ball_eq_empty.mpr h, metric.ball_eq_empty.mpr h.le] },
  push_neg at h,
  rw [add_haar_closed_ball Î¼ x h, add_haar_ball Î¼ x h],
end

lemma add_haar_sphere_of_ne_zero (x : E) {r : â„} (hr : r â‰  0) :
  Î¼ (sphere x r) = 0 :=
begin
  rcases hr.lt_or_lt with h|h,
  { simp only [empty_diff, measure_empty, â† closed_ball_diff_ball, closed_ball_eq_empty.2 h] },
  { rw [â† closed_ball_diff_ball,
        measure_diff ball_subset_closed_ball measurable_set_ball measure_ball_lt_top.ne,
        add_haar_ball_of_pos Î¼ _ h, add_haar_closed_ball Î¼ _ h.le, tsub_self];
    apply_instance }
end

lemma add_haar_sphere [nontrivial E] (x : E) (r : â„) :
  Î¼ (sphere x r) = 0 :=
begin
  rcases eq_or_ne r 0 with rfl|h,
  { rw [sphere_zero, measure_singleton] },
  { exact add_haar_sphere_of_ne_zero Î¼ x h }
end

lemma add_haar_singleton_add_smul_div_singleton_add_smul
  {r : â„} (hr : r â‰  0) (x y : E) (s t : set E) :
  Î¼ ({x} + r â€¢ s) / Î¼ ({y} + r â€¢ t) = Î¼ s / Î¼ t :=
calc
Î¼ ({x} + r â€¢ s) / Î¼ ({y} + r â€¢ t)
    = ennreal.of_real (|r| ^ finrank â„ E) * Î¼ s * (ennreal.of_real (|r| ^ finrank â„ E) * Î¼ t)â»Â¹ :
  by simp only [div_eq_mul_inv, add_haar_smul, image_add_left, measure_preimage_add, abs_pow,
    singleton_add]
... = ennreal.of_real (|r| ^ finrank â„ E) * (ennreal.of_real (|r| ^ finrank â„ E))â»Â¹ *
        (Î¼ s * (Î¼ t)â»Â¹) :
  begin
    rw ennreal.mul_inv,
    { ring },
    { simp only [pow_pos (abs_pos.mpr hr), ennreal.of_real_eq_zero, not_le, ne.def, true_or] },
    { simp only [ennreal.of_real_ne_top, true_or, ne.def, not_false_iff] },
  end
... = Î¼ s / Î¼ t :
  begin
    rw [ennreal.mul_inv_cancel, one_mul, div_eq_mul_inv],
    { simp only [pow_pos (abs_pos.mpr hr), ennreal.of_real_eq_zero, not_le, ne.def], },
    { simp only [ennreal.of_real_ne_top, ne.def, not_false_iff] }
  end

@[priority 100] instance is_doubling_measure_of_is_add_haar_measure : is_doubling_measure Î¼ :=
begin
  refine âŸ¨âŸ¨(2 : â„â‰¥0) ^ (finrank â„ E), _âŸ©âŸ©,
  filter_upwards [self_mem_nhds_within] with r hr x,
  rw [add_haar_closed_ball_mul Î¼ x zero_le_two (le_of_lt hr), add_haar_closed_ball_center Î¼ x,
    ennreal.of_real, real.to_nnreal_pow zero_le_two],
  simp only [real.to_nnreal_bit0, real.to_nnreal_one, le_refl],
end

/-!
### Density points

Besicovitch covering theorem ensures that, for any locally finite measure on a finite-dimensional
real vector space, almost every point of a set `s` is a density point, i.e.,
`Î¼ (s âˆ© closed_ball x r) / Î¼ (closed_ball x r)` tends to `1` as `r` tends to `0`
(see `besicovitch.ae_tendsto_measure_inter_div`).
When `Î¼` is a Haar measure, one can deduce the same property for any rescaling sequence of sets,
of the form `{x} + r â€¢ t` where `t` is a set with positive finite measure, instead of the sequence
of closed balls.

We argue first for the dual property, i.e., if `s` has density `0` at `x`, then
`Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ t)` tends to `0`. First when `t` is contained in the ball
of radius `1`, in `tendsto_add_haar_inter_smul_zero_of_density_zero_aux1`,
(by arguing by inclusion). Then when `t` is bounded, reducing to the previous one by rescaling, in
`tendsto_add_haar_inter_smul_zero_of_density_zero_aux2`.
Then for a general set `t`, by cutting it into a bounded part and a part with small measure, in
`tendsto_add_haar_inter_smul_zero_of_density_zero`.
Going to the complement, one obtains the desired property at points of density `1`, first when
`s` is measurable in `tendsto_add_haar_inter_smul_one_of_density_one_aux`, and then without this
assumption in `tendsto_add_haar_inter_smul_one_of_density_one` by applying the previous lemma to
the measurable hull `to_measurable Î¼ s`
-/

lemma tendsto_add_haar_inter_smul_zero_of_density_zero_aux1
  (s : set E) (x : E)
  (h : tendsto (Î» r, Î¼ (s âˆ© closed_ball x r) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 0))
  (t : set E) (u : set E) (h'u : Î¼ u â‰  0) (t_bound : t âŠ† closed_ball 0 1) :
  tendsto (Î» (r : â„), Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ u)) (ğ“[>] 0) (ğ“ 0) :=
begin
  have A : tendsto (Î» (r : â„), Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 0),
  { apply tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds h
      (eventually_of_forall (Î» b, zero_le _)),
    filter_upwards [self_mem_nhds_within],
    rintros r (rpos : 0 < r),
    apply ennreal.mul_le_mul (measure_mono (inter_subset_inter_right _ _)) le_rfl,
    assume y hy,
    have : y - x âˆˆ r â€¢ closed_ball (0 : E) 1,
    { apply smul_set_mono t_bound,
      simpa [neg_add_eq_sub] using hy },
    simpa only [smul_closed_ball _ _ zero_le_one, real.norm_of_nonneg rpos.le,
      mem_closed_ball_iff_norm, mul_one, sub_zero, smul_zero] },
  have B : tendsto (Î» (r : â„), Î¼ (closed_ball x r) / Î¼ ({x} + r â€¢ u)) (ğ“[>] 0)
    (ğ“ (Î¼ (closed_ball x 1) / Î¼ ({x} + u))),
  { apply tendsto_const_nhds.congr' _,
    filter_upwards [self_mem_nhds_within],
    rintros r (rpos : 0 < r),
    have : closed_ball x r = {x} + r â€¢ closed_ball 0 1,
      by simp only [smul_closed_ball, real.norm_of_nonneg rpos.le, zero_le_one, add_zero, mul_one,
        singleton_add_closed_ball, smul_zero],
    simp only [this, add_haar_singleton_add_smul_div_singleton_add_smul Î¼ rpos.ne'],
    simp only [add_haar_closed_ball_center, image_add_left, measure_preimage_add, singleton_add] },
  have C : tendsto (Î» (r : â„),
    (Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ (closed_ball x r)) * (Î¼ (closed_ball x r) / Î¼ ({x} + r â€¢ u)))
    (ğ“[>] 0) (ğ“ (0 * (Î¼ (closed_ball x 1) / Î¼ ({x} + u)))),
  { apply ennreal.tendsto.mul A _ B (or.inr ennreal.zero_ne_top),
    simp only [ennreal.div_eq_top, h'u, measure_closed_ball_lt_top.ne, false_or, image_add_left,
      eq_self_iff_true, not_true, ne.def, not_false_iff, measure_preimage_add, singleton_add,
      and_false, false_and] },
  simp only [zero_mul] at C,
  apply C.congr' _,
  filter_upwards [self_mem_nhds_within],
  rintros r (rpos : 0 < r),
  calc Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ (closed_ball x r) * (Î¼ (closed_ball x r) / Î¼ ({x} + r â€¢ u))
    = (Î¼ (closed_ball x r) * (Î¼ (closed_ball x r))â»Â¹) * (Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ u)) :
      by { simp only [div_eq_mul_inv], ring }
    ... = Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ u) :
      by rw [ennreal.mul_inv_cancel (measure_closed_ball_pos Î¼ x rpos).ne'
          measure_closed_ball_lt_top.ne, one_mul],
end

lemma tendsto_add_haar_inter_smul_zero_of_density_zero_aux2
  (s : set E) (x : E)
  (h : tendsto (Î» r, Î¼ (s âˆ© closed_ball x r) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 0))
  (t : set E) (u : set E) (h'u : Î¼ u â‰  0)
  (R : â„) (Rpos : 0 < R) (t_bound : t âŠ† closed_ball 0 R) :
  tendsto (Î» (r : â„), Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ u)) (ğ“[>] 0) (ğ“ 0) :=
begin
  set t' := Râ»Â¹ â€¢ t with ht',
  set u' := Râ»Â¹ â€¢ u with hu',
  have A : tendsto (Î» (r : â„), Î¼ (s âˆ© ({x} + r â€¢ t')) / Î¼ ({x} + r â€¢ u')) (ğ“[>] 0) (ğ“ 0),
  { apply tendsto_add_haar_inter_smul_zero_of_density_zero_aux1 Î¼ s x h
      t' u',
    { simp only [h'u, (pow_pos Rpos _).ne', abs_nonpos_iff, add_haar_smul, not_false_iff,
        ennreal.of_real_eq_zero, inv_eq_zero, inv_pow, ne.def, or_self, mul_eq_zero] },
    { convert smul_set_mono t_bound,
      rw [smul_closed_ball _ _ Rpos.le, smul_zero, real.norm_of_nonneg (inv_nonneg.2 Rpos.le),
        inv_mul_cancel Rpos.ne'] } },
  have B : tendsto (Î» (r : â„), R * r) (ğ“[>] 0) (ğ“[>] (R * 0)),
  { apply tendsto_nhds_within_of_tendsto_nhds_of_eventually_within,
    { exact (tendsto_const_nhds.mul tendsto_id).mono_left nhds_within_le_nhds },
    { filter_upwards [self_mem_nhds_within],
      assume r rpos,
      rw mul_zero,
      exact mul_pos Rpos rpos } },
  rw mul_zero at B,
  apply (A.comp B).congr' _,
  filter_upwards [self_mem_nhds_within],
  rintros r (rpos : 0 < r),
  have T : (R * r) â€¢ t' = r â€¢ t,
    by rw [mul_comm, ht', smul_smul, mul_assoc, mul_inv_cancel Rpos.ne', mul_one],
  have U : (R * r) â€¢ u' = r â€¢ u,
    by rw [mul_comm, hu', smul_smul, mul_assoc, mul_inv_cancel Rpos.ne', mul_one],
  dsimp,
  rw [T, U],
end

/-- Consider a point `x` at which a set `s` has density zero, with respect to closed balls. Then it
also has density zero with respect to any measurable set `t`: the proportion of points in `s`
belonging to a rescaled copy `{x} + r â€¢ t` of `t` tends to zero as `r` tends to zero. -/
lemma tendsto_add_haar_inter_smul_zero_of_density_zero
  (s : set E) (x : E)
  (h : tendsto (Î» r, Î¼ (s âˆ© closed_ball x r) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 0))
  (t : set E) (ht : measurable_set t) (h''t : Î¼ t â‰  âˆ) :
  tendsto (Î» (r : â„), Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ t)) (ğ“[>] 0) (ğ“ 0) :=
begin
  refine tendsto_order.2 âŸ¨Î» a' ha', (ennreal.not_lt_zero ha').elim, Î» Îµ (Îµpos : 0 < Îµ), _âŸ©,
  rcases eq_or_ne (Î¼ t) 0 with h't|h't,
  { apply eventually_of_forall (Î» r, _),
    suffices H : Î¼ (s âˆ© ({x} + r â€¢ t)) = 0,
      by { rw H, simpa only [ennreal.zero_div] using Îµpos },
    apply le_antisymm _ (zero_le _),
    calc Î¼ (s âˆ© ({x} + r â€¢ t)) â‰¤ Î¼ ({x} + r â€¢ t) : measure_mono (inter_subset_right _ _)
    ... = 0 : by simp only [h't, add_haar_smul, image_add_left, measure_preimage_add,
      singleton_add, mul_zero] },
  obtain âŸ¨n, npos, hnâŸ© : âˆƒ (n : â„•), 0 < n âˆ§ Î¼ (t \ closed_ball 0 n) < (Îµ / 2) * Î¼ t,
  { have A : tendsto (Î» (n : â„•), Î¼ (t \ closed_ball 0 n)) at_top
      (ğ“ (Î¼ (â‹‚ (n : â„•), t \ closed_ball 0 n))),
    { have N : âˆƒ (n : â„•), Î¼ (t \ closed_ball 0 n) â‰  âˆ :=
        âŸ¨0, ((measure_mono (diff_subset t _)).trans_lt h''t.lt_top).neâŸ©,
      refine tendsto_measure_Inter (Î» n, ht.diff measurable_set_closed_ball) (Î» m n hmn, _) N,
      exact diff_subset_diff subset.rfl (closed_ball_subset_closed_ball (nat.cast_le.2 hmn)) },
    have : (â‹‚ (n : â„•), t \ closed_ball 0 n) = âˆ…,
      by simp_rw [diff_eq, â† inter_Inter, Inter_eq_compl_Union_compl, compl_compl,
          Union_closed_ball_nat, compl_univ, inter_empty],
    simp only [this, measure_empty] at A,
    have I : 0 < (Îµ / 2) * Î¼ t := ennreal.mul_pos (ennreal.half_pos Îµpos.ne').ne' h't,
    exact (eventually.and (Ioi_mem_at_top 0) ((tendsto_order.1 A).2 _ I)).exists },
  have L : tendsto (Î» (r : â„), Î¼ (s âˆ© ({x} + r â€¢ (t âˆ© closed_ball 0 n))) / Î¼ ({x} + r â€¢ t))
    (ğ“[>] 0) (ğ“ 0) :=
      tendsto_add_haar_inter_smul_zero_of_density_zero_aux2 Î¼ s x h
        _ t h't n (nat.cast_pos.2 npos) (inter_subset_right _ _),
  filter_upwards [(tendsto_order.1 L).2 _ (ennreal.half_pos Îµpos.ne'), self_mem_nhds_within],
  rintros r hr (rpos : 0 < r),
  have I : Î¼ (s âˆ© ({x} + r â€¢ t)) â‰¤
    Î¼ (s âˆ© ({x} + r â€¢ (t âˆ© closed_ball 0 n))) + Î¼ ({x} + r â€¢ (t \ closed_ball 0 n)) := calc
  Î¼ (s âˆ© ({x} + r â€¢ t))
      = Î¼ ((s âˆ© ({x} + r â€¢ (t âˆ© closed_ball 0 n))) âˆª (s âˆ© ({x} + r â€¢ (t \ closed_ball 0 n)))) :
    by rw [â† inter_union_distrib_left, â† add_union, â† smul_set_union, inter_union_diff]
  ... â‰¤ Î¼ (s âˆ© ({x} + r â€¢ (t âˆ© closed_ball 0 n))) + Î¼ (s âˆ© ({x} + r â€¢ (t \ closed_ball 0 n))) :
    measure_union_le _ _
  ... â‰¤ Î¼ (s âˆ© ({x} + r â€¢ (t âˆ© closed_ball 0 n))) + Î¼ ({x} + r â€¢ (t \ closed_ball 0 n)) :
    add_le_add le_rfl (measure_mono (inter_subset_right _ _)),
  calc Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ t)
  â‰¤ (Î¼ (s âˆ© ({x} + r â€¢ (t âˆ© closed_ball 0 n))) + Î¼ ({x} + r â€¢ (t \ closed_ball 0 n))) /
      Î¼ ({x} + r â€¢ t) : ennreal.mul_le_mul I le_rfl
  ... < Îµ / 2 + Îµ / 2 :
    begin
      rw ennreal.add_div,
      apply ennreal.add_lt_add hr _,
      rwa [add_haar_singleton_add_smul_div_singleton_add_smul Î¼ rpos.ne',
           ennreal.div_lt_iff (or.inl h't) (or.inl h''t)],
    end
  ... = Îµ : ennreal.add_halves _
end

lemma tendsto_add_haar_inter_smul_one_of_density_one_aux
  (s : set E) (hs : measurable_set s) (x : E)
  (h : tendsto (Î» r, Î¼ (s âˆ© closed_ball x r) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 1))
  (t : set E) (ht : measurable_set t) (h't : Î¼ t â‰  0) (h''t : Î¼ t â‰  âˆ) :
  tendsto (Î» (r : â„), Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ t)) (ğ“[>] 0) (ğ“ 1) :=
begin
  have I : âˆ€ u v, Î¼ u â‰  0 â†’ Î¼ u â‰  âˆ â†’ measurable_set v â†’
    Î¼ u / Î¼ u - Î¼ (vá¶œ âˆ© u) / Î¼ u = Î¼ (v âˆ© u) / Î¼ u,
  { assume u v uzero utop vmeas,
    simp_rw [div_eq_mul_inv],
    rw â† ennreal.sub_mul, swap,
    { simp only [uzero, ennreal.inv_eq_top, implies_true_iff, ne.def, not_false_iff] },
    congr' 1,
    apply ennreal.sub_eq_of_add_eq
      (ne_top_of_le_ne_top utop (measure_mono (inter_subset_right _ _))),
    rw [inter_comm _ u, inter_comm _ u],
    exact measure_inter_add_diff u vmeas },
  have L : tendsto (Î» r, Î¼ (sá¶œ âˆ© closed_ball x r) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 0),
  { have A : tendsto (Î» r, Î¼ (closed_ball x r) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 1),
    { apply tendsto_const_nhds.congr' _,
      filter_upwards [self_mem_nhds_within],
      assume r hr,
      rw [div_eq_mul_inv, ennreal.mul_inv_cancel],
      { exact (measure_closed_ball_pos Î¼ _ hr).ne' },
      { exact measure_closed_ball_lt_top.ne } },
    have B := ennreal.tendsto.sub A h (or.inl ennreal.one_ne_top),
    simp only [tsub_self] at B,
    apply B.congr' _,
    filter_upwards [self_mem_nhds_within],
    rintros r (rpos : 0 < r),
    convert I (closed_ball x r) sá¶œ (measure_closed_ball_pos Î¼ _ rpos).ne'
      (measure_closed_ball_lt_top).ne hs.compl,
    rw compl_compl },
  have L' : tendsto (Î» (r : â„), Î¼ (sá¶œ âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ t)) (ğ“[>] 0) (ğ“ 0) :=
    tendsto_add_haar_inter_smul_zero_of_density_zero Î¼ sá¶œ x L t ht h''t,
  have L'' : tendsto (Î» (r : â„), Î¼ ({x} + r â€¢ t) / Î¼ ({x} + r â€¢ t)) (ğ“[>] 0) (ğ“ 1),
  { apply tendsto_const_nhds.congr' _,
    filter_upwards [self_mem_nhds_within],
    rintros r (rpos : 0 < r),
    rw [add_haar_singleton_add_smul_div_singleton_add_smul Î¼ rpos.ne', ennreal.div_self h't h''t] },
  have := ennreal.tendsto.sub L'' L' (or.inl ennreal.one_ne_top),
  simp only [tsub_zero] at this,
  apply this.congr' _,
  filter_upwards [self_mem_nhds_within],
  rintros r (rpos : 0 < r),
  refine I ({x} + r â€¢ t) s _ _ hs,
  { simp only [h't, abs_of_nonneg rpos.le, pow_pos rpos, add_haar_smul, image_add_left,
      ennreal.of_real_eq_zero, not_le, or_false, ne.def, measure_preimage_add, abs_pow,
      singleton_add, mul_eq_zero] },
  { simp only [h''t, ennreal.of_real_ne_top, add_haar_smul, image_add_left, with_top.mul_eq_top_iff,
      ne.def, not_false_iff, measure_preimage_add, singleton_add, and_false, false_and, or_self] }
end

/-- Consider a point `x` at which a set `s` has density one, with respect to closed balls (i.e.,
a Lebesgue density point of `s`). Then `s` has also density one at `x` with respect to any
measurable set `t`: the proportion of points in `s` belonging to a rescaled copy `{x} + r â€¢ t`
of `t` tends to one as `r` tends to zero. -/
lemma tendsto_add_haar_inter_smul_one_of_density_one
  (s : set E) (x : E)
  (h : tendsto (Î» r, Î¼ (s âˆ© closed_ball x r) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 1))
  (t : set E) (ht : measurable_set t) (h't : Î¼ t â‰  0) (h''t : Î¼ t â‰  âˆ) :
  tendsto (Î» (r : â„), Î¼ (s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ t)) (ğ“[>] 0) (ğ“ 1) :=
begin
  have : tendsto (Î» (r : â„), Î¼ (to_measurable Î¼ s âˆ© ({x} + r â€¢ t)) / Î¼ ({x} + r â€¢ t))
    (ğ“[>] 0) (ğ“ 1),
  { apply tendsto_add_haar_inter_smul_one_of_density_one_aux Î¼ _
      (measurable_set_to_measurable _ _) _ _ t ht h't h''t,
    apply tendsto_of_tendsto_of_tendsto_of_le_of_le' h tendsto_const_nhds,
    { apply eventually_of_forall (Î» r, _),
      apply ennreal.mul_le_mul _ le_rfl,
      exact measure_mono (inter_subset_inter_left _ (subset_to_measurable _ _)) },
    { filter_upwards [self_mem_nhds_within],
      rintros r (rpos : 0 < r),
      apply ennreal.div_le_of_le_mul,
      rw one_mul,
      exact measure_mono (inter_subset_right _ _) } },
  apply this.congr (Î» r, _),
  congr' 1,
  apply measure_to_measurable_inter_of_sigma_finite,
  simp only [image_add_left, singleton_add],
  apply (continuous_add_left (-x)).measurable (ht.const_smulâ‚€ r)
end

/-- Consider a point `x` at which a set `s` has density one, with respect to closed balls (i.e.,
a Lebesgue density point of `s`). Then `s` intersects the rescaled copies `{x} + r â€¢ t` of a given
set `t` with positive measure, for any small enough `r`. -/
lemma eventually_nonempty_inter_smul_of_density_one (s : set E) (x : E)
  (h : tendsto (Î» r, Î¼ (s âˆ© closed_ball x r) / Î¼ (closed_ball x r)) (ğ“[>] 0) (ğ“ 1))
  (t : set E) (ht : measurable_set t) (h't : Î¼ t â‰  0) :
  âˆ€á¶  r in ğ“[>] (0 : â„), (s âˆ© ({x} + r â€¢ t)).nonempty :=
begin
  obtain âŸ¨t', t'_meas, t't, t'pos, t'topâŸ© :
    âˆƒ t', measurable_set t' âˆ§ t' âŠ† t âˆ§ 0 < Î¼ t' âˆ§ Î¼ t' < âŠ¤ :=
      exists_subset_measure_lt_top ht h't.bot_lt,
  filter_upwards [(tendsto_order.1
    (tendsto_add_haar_inter_smul_one_of_density_one Î¼ s x h t'
      t'_meas t'pos.ne' t'top.ne)).1 0 ennreal.zero_lt_one],
  assume r hr,
  have : Î¼ (s âˆ© ({x} + r â€¢ t')) â‰  0 :=
    Î» h', by simpa only [ennreal.not_lt_zero, ennreal.zero_div, h'] using hr,
  have : (s âˆ© ({x} + r â€¢ t')).nonempty := nonempty_of_measure_ne_zero this,
  apply this.mono (inter_subset_inter subset.rfl _),
  exact add_subset_add subset.rfl (smul_set_mono t't),
end

end measure

end measure_theory
