/-
Copyright (c) 2022 Alex Kontorovich and Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth
-/

import measure_theory.measure.haar
import measure_theory.group.fundamental_domain
import topology.compact_open
import algebra.group.opposite

/-!
# Haar quotient measure

In this file, we consider properties of fundamental domains and measures for the action of a
subgroup of a group `G` on `G` itself.

## Main results

* `measure_theory.is_fundamental_domain.smul_invariant_measure_map `: given a subgroup `Î“` of a
  topological group `G`, the pushforward to the coset space `G â§¸ Î“` of the restriction of a both
  left- and right-invariant measure on `G` to a fundamental domain `ğ“•` is a `G`-invariant measure
  on `G â§¸ Î“`.

* `measure_theory.is_fundamental_domain.is_mul_left_invariant_map `: given a normal subgroup `Î“` of
  a topological group `G`, the pushforward to the quotient group `G â§¸ Î“` of the restriction of
  a both left- and right-invariant measure on `G` to a fundamental domain `ğ“•` is a left-invariant
  measure on `G â§¸ Î“`.

Note that a group `G` with Haar measure that is both left and right invariant is called
**unimodular**.
-/

open set measure_theory topological_space measure_theory.measure

variables {G : Type*} [group G] [measurable_space G] [topological_space G]
  [topological_group G] [borel_space G]
  {Î¼ : measure G}
  {Î“ : subgroup G}

/-- Given a subgroup `Î“` of `G` and a right invariant measure `Î¼` on `G`, the measure is also
  invariant under the action of `Î“` on `G` by **right** multiplication. -/
@[to_additive "Given a subgroup `Î“` of an additive group `G` and a right invariant measure `Î¼` on
  `G`, the measure is also invariant under the action of `Î“` on `G` by **right** addition."]
lemma subgroup.smul_invariant_measure [Î¼.is_mul_right_invariant] :
  smul_invariant_measure Î“.opposite G Î¼ :=
{ measure_preimage_smul :=
begin
  rintros âŸ¨c, hcâŸ© s hs,
  dsimp [(â€¢)],
  refine measure_preimage_mul_right Î¼ (mul_opposite.unop c) s,
end}

/-- Measurability of the action of the topological group `G` on the left-coset space `G/Î“`. -/
@[to_additive "Measurability of the action of the additive topological group `G` on the left-coset
  space `G/Î“`."]
instance quotient_group.has_measurable_smul [measurable_space (G â§¸ Î“)] [borel_space (G â§¸ Î“)] :
  has_measurable_smul G (G â§¸ Î“) :=
{ measurable_const_smul := Î» g, (continuous_const_smul g).measurable,
  measurable_smul_const := Î» x, (quotient_group.continuous_smulâ‚ x).measurable }

variables {ğ“• : set G} (hğ“• : is_fundamental_domain Î“.opposite ğ“• Î¼)
include hğ“•

/-- If `ğ“•` is a fundamental domain for the action by right multiplication of a subgroup `Î“` of a
  topological group `G`, then its left-translate by an element of `g` is also a fundamental
  domain. -/
@[to_additive "If `ğ“•` is a fundamental domain for the action by right addition of a subgroup `Î“`
  of an additive topological group `G`, then its left-translate by an element of `g` is also a
  fundamental domain."]
lemma measure_theory.is_fundamental_domain.smul (g : G) [Î¼.is_mul_left_invariant] :
  is_fundamental_domain â†¥Î“.opposite (has_mul.mul g â»Â¹' ğ“•) Î¼ :=
{ measurable_set := measurable_set_preimage (measurable_const_mul g) (hğ“•.measurable_set),
  ae_covers := begin
    let s := {x : G | Â¬âˆƒ (Î³ : â†¥(Î“.opposite)), Î³ â€¢ x âˆˆ ğ“•},
    have Î¼s_eq_zero : Î¼ s = 0 := hğ“•.2,
    change Î¼ {x : G | Â¬âˆƒ (Î³ : â†¥(Î“.opposite)), g * Î³ â€¢ x âˆˆ ğ“•} = 0,
    have : {x : G | Â¬âˆƒ (Î³ : â†¥(Î“.opposite)), g * Î³ â€¢ x âˆˆ ğ“•} = has_mul.mul g â»Â¹' s,
    { ext,
      simp [s, subgroup.smul_opposite_mul], },
    rw [this, measure_preimage_mul Î¼ g s, Î¼s_eq_zero],
  end,
  ae_disjoint := begin
    intros Î³ Î³_ne_one,
    have Î¼s_eq_zero : Î¼ (((Î» x, Î³ â€¢ x) '' ğ“•) âˆ© ğ“•) = 0 := hğ“•.3 Î³ Î³_ne_one,
    change Î¼ (((Î» x, Î³ â€¢ x) '' (has_mul.mul g â»Â¹' ğ“•)) âˆ© (has_mul.mul g â»Â¹' ğ“•)) = 0,
    rw [subgroup.smul_opposite_image_mul_preimage, â† preimage_inter, measure_preimage_mul Î¼ g _,
      Î¼s_eq_zero],
  end }

variables [encodable Î“] [measurable_space (G â§¸ Î“)] [borel_space (G â§¸ Î“)]

/-- The pushforward to the coset space `G â§¸ Î“` of the restriction of a both left- and right-
  invariant measure on `G` to a fundamental domain `ğ“•` is a `G`-invariant measure on `G â§¸ Î“`. -/
@[to_additive "The pushforward to the coset space `G â§¸ Î“` of the restriction of a both left- and
  right-invariant measure on an additive topological group `G` to a fundamental domain `ğ“•` is a
  `G`-invariant measure on `G â§¸ Î“`."]
lemma measure_theory.is_fundamental_domain.smul_invariant_measure_map
  [Î¼.is_mul_left_invariant] [Î¼.is_mul_right_invariant] :
  smul_invariant_measure G (G â§¸ Î“) (measure.map quotient_group.mk (Î¼.restrict ğ“•)) :=
{ measure_preimage_smul :=
  begin
    let Ï€ : G â†’ G â§¸ Î“ := quotient_group.mk,
    have meas_Ï€ : measurable Ï€ :=
      continuous_quotient_mk.measurable,
    have ğ“•meas : measurable_set ğ“• := hğ“•.measurable_set,
    intros g A hA,
    have meas_Ï€A : measurable_set (Ï€ â»Â¹' A) := measurable_set_preimage meas_Ï€ hA,
    rw [measure.map_apply meas_Ï€ hA,
      measure.map_apply meas_Ï€ (measurable_set_preimage (measurable_const_smul g) hA),
      measure.restrict_apply' ğ“•meas, measure.restrict_apply' ğ“•meas],
    set Ï€_preA := Ï€ â»Â¹' A,
    have : (quotient_group.mk â»Â¹' ((Î» (x : G â§¸ Î“), g â€¢ x) â»Â¹' A)) = has_mul.mul g â»Â¹' Ï€_preA,
    { ext1, simp },
    rw this,
    have : Î¼ (has_mul.mul g â»Â¹' Ï€_preA âˆ© ğ“•) = Î¼ (Ï€_preA âˆ© has_mul.mul (gâ»Â¹) â»Â¹' ğ“•),
    { transitivity Î¼ (has_mul.mul g â»Â¹' (Ï€_preA âˆ© has_mul.mul gâ»Â¹ â»Â¹' ğ“•)),
      { rw preimage_inter,
        congr,
        rw [â† preimage_comp, comp_mul_left, mul_left_inv],
        ext,
        simp, },
      rw measure_preimage_mul, },
    rw this,
    have hğ“•_translate_fundom : is_fundamental_domain Î“.opposite (has_mul.mul gâ»Â¹ â»Â¹' ğ“•) Î¼ :=
      hğ“•.smul (gâ»Â¹),
    haveI : smul_invariant_measure â†¥(Î“.opposite) G Î¼ := subgroup.smul_invariant_measure,
    rw hğ“•.measure_set_eq hğ“•_translate_fundom meas_Ï€A,
    rintros âŸ¨Î³, Î³_in_Î“âŸ©,
    ext,
    have : Ï€ (x * (mul_opposite.unop Î³)) = Ï€ (x) := by simpa [quotient_group.eq'] using Î³_in_Î“,
    simp [(â€¢), this],
  end }

/-- Assuming `Î“` is a normal subgroup of a topological group `G`, the pushforward to the quotient
  group `G â§¸ Î“` of the restriction of a both left- and right-invariant measure on `G` to a
  fundamental domain `ğ“•` is a left-invariant measure on `G â§¸ Î“`. -/
@[to_additive "Assuming `Î“` is a normal subgroup of an additive topological group `G`, the
  pushforward to the quotient group `G â§¸ Î“` of the restriction of a both left- and right-invariant
  measure on `G` to a fundamental domain `ğ“•` is a left-invariant measure on `G â§¸ Î“`."]
lemma measure_theory.is_fundamental_domain.is_mul_left_invariant_map [subgroup.normal Î“]
  [Î¼.is_mul_left_invariant] [Î¼.is_mul_right_invariant] :
  (measure.map (quotient_group.mk' Î“) (Î¼.restrict ğ“•)).is_mul_left_invariant :=
{ map_mul_left_eq_self := begin
    intros x,
    apply measure.ext,
    intros A hA,
    obtain âŸ¨xâ‚, _âŸ© := @quotient.exists_rep _ (quotient_group.left_rel Î“) x,
    haveI := hğ“•.smul_invariant_measure_map,
    convert measure_preimage_smul xâ‚ ((measure.map quotient_group.mk) (Î¼.restrict ğ“•)) A using 1,
    rw [â† h, measure.map_apply],
    { refl, },
    { exact measurable_const_mul _, },
    { exact hA, },
  end }

variables [t2_space (G â§¸ Î“)] [second_countable_topology (G â§¸ Î“)] (K : positive_compacts (G â§¸ Î“))

/-- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the pushforward to the quotient
  group `G â§¸ Î“` of the restriction of `Î¼` to `ğ“•` is a multiple of Haar measure on `G â§¸ Î“`. -/
@[to_additive "Given a normal subgroup `Î“` of an additive topological group `G` with Haar measure
  `Î¼`, which is also right-invariant, and a finite volume fundamental domain `ğ“•`, the pushforward
  to the quotient group `G â§¸ Î“` of the restriction of `Î¼` to `ğ“•` is a multiple of Haar measure on
  `G â§¸ Î“`."]
lemma measure_theory.is_fundamental_domain.map_restrict_quotient [subgroup.normal Î“]
  [measure_theory.measure.is_haar_measure Î¼] [Î¼.is_mul_right_invariant]
  (hğ“•_finite : Î¼ ğ“• < âŠ¤) : measure.map (quotient_group.mk' Î“) (Î¼.restrict ğ“•)
  = (Î¼ (ğ“• âˆ© (quotient_group.mk' Î“) â»Â¹' K)) â€¢ (measure_theory.measure.haar_measure K) :=
begin
  let Ï€ : G â†’* G â§¸ Î“ := quotient_group.mk' Î“,
  have meas_Ï€ : measurable Ï€ := continuous_quotient_mk.measurable,
  have ğ“•meas : measurable_set ğ“• := hğ“•.measurable_set,
  haveI : is_finite_measure (Î¼.restrict ğ“•) :=
    âŸ¨by { rw [measure.restrict_apply' ğ“•meas, univ_inter], exact hğ“•_finite }âŸ©,
  -- the measure is left-invariant, so by the uniqueness of Haar measure it's enough to show that
  -- it has the stated size on the reference compact set `K`.
  haveI : (measure.map (quotient_group.mk' Î“) (Î¼.restrict ğ“•)).is_mul_left_invariant :=
    hğ“•.is_mul_left_invariant_map,
  rw [measure.haar_measure_unique (measure.map (quotient_group.mk' Î“) (Î¼.restrict ğ“•)) K,
    measure.map_apply meas_Ï€, measure.restrict_apply' ğ“•meas, inter_comm],
  exact K.compact.measurable_set,
end
