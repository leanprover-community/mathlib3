/-
Copyright (c) 2022 Alex Kontorovich and Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth
-/

import measure_theory.measure.haar
import measure_theory.group.fundamental_domain
import algebra.group.opposite

/-!
# Haar quotient measure

In this file, we consider properties of fundamental domains and measures for the action of a
subgroup of a group `G` on `G` itself.

## Main results

* `measure_theory.is_fundamental_domain.smul_invariant_measure_map `: given a subgroup `Î“` of a
  topological group `G`, the pushforward to the coset space `G â§¸ Î“` of the restriction of a both
  left- and right-invariant measure on `G` to a fundamental domain `ğ“•` is a `G`-invariant measure
  on `G â§¸ Î“`.

* `measure_theory.is_fundamental_domain.is_mul_left_invariant_map `: given a normal subgroup `Î“` of
  a topological group `G`, the pushforward to the quotient group `G â§¸ Î“` of the restriction of
  a both left- and right-invariant measure on `G` to a fundamental domain `ğ“•` is a left-invariant
  measure on `G â§¸ Î“`.

Note that a group `G` with Haar measure that is both left and right invariant is called
**unimodular**.
-/

noncomputable theory

open set measure_theory topological_space measure_theory.measure quotient_group
open_locale pointwise measure_theory topology big_operators nnreal ennreal

variables {G : Type*} [group G] [measurable_space G] [topological_space G]
  [topological_group G] [borel_space G]
  {Î¼ : measure G}
  {Î“ : subgroup G}

/-- Measurability of the action of the topological group `G` on the left-coset space `G/Î“`. -/
@[to_additive "Measurability of the action of the additive topological group `G` on the left-coset
  space `G/Î“`."]
instance quotient_group.has_measurable_smul [measurable_space (G â§¸ Î“)] [borel_space (G â§¸ Î“)] :
  has_measurable_smul G (G â§¸ Î“) :=
{ measurable_const_smul := Î» g, (continuous_const_smul g).measurable,
  measurable_smul_const := Î» x, (quotient_group.continuous_smulâ‚ x).measurable }

variables {ğ“• : set G} (hğ“• : is_fundamental_domain Î“.opposite ğ“• Î¼)
include hğ“•

variables [countable Î“] [measurable_space (G â§¸ Î“)] [borel_space (G â§¸ Î“)]

/-- The pushforward to the coset space `G â§¸ Î“` of the restriction of a both left- and right-
  invariant measure on `G` to a fundamental domain `ğ“•` is a `G`-invariant measure on `G â§¸ Î“`. -/
@[to_additive "The pushforward to the coset space `G â§¸ Î“` of the restriction of a both left- and
  right-invariant measure on an additive topological group `G` to a fundamental domain `ğ“•` is a
  `G`-invariant measure on `G â§¸ Î“`."]
lemma measure_theory.is_fundamental_domain.smul_invariant_measure_map
  [Î¼.is_mul_left_invariant] [Î¼.is_mul_right_invariant] :
  smul_invariant_measure G (G â§¸ Î“) (measure.map quotient_group.mk (Î¼.restrict ğ“•)) :=
{ measure_preimage_smul :=
  begin
    let Ï€ : G â†’ G â§¸ Î“ := quotient_group.mk,
    have meas_Ï€ : measurable Ï€ :=
      continuous_quotient_mk.measurable,
    have ğ“•meas : null_measurable_set ğ“• Î¼ := hğ“•.null_measurable_set,
    intros g A hA,
    have meas_Ï€A : measurable_set (Ï€ â»Â¹' A) := measurable_set_preimage meas_Ï€ hA,
    rw [measure.map_apply meas_Ï€ hA,
      measure.map_apply meas_Ï€ (measurable_set_preimage (measurable_const_smul g) hA),
      measure.restrict_applyâ‚€' ğ“•meas, measure.restrict_applyâ‚€' ğ“•meas],
    set Ï€_preA := Ï€ â»Â¹' A,
    have : (quotient_group.mk â»Â¹' ((Î» (x : G â§¸ Î“), g â€¢ x) â»Â¹' A)) = has_mul.mul g â»Â¹' Ï€_preA,
    { ext1, simp },
    rw this,
    have : Î¼ (has_mul.mul g â»Â¹' Ï€_preA âˆ© ğ“•) = Î¼ (Ï€_preA âˆ© has_mul.mul (gâ»Â¹) â»Â¹' ğ“•),
    { transitivity Î¼ (has_mul.mul g â»Â¹' (Ï€_preA âˆ© has_mul.mul gâ»Â¹ â»Â¹' ğ“•)),
      { rw preimage_inter,
        congr,
        rw [â† preimage_comp, comp_mul_left, mul_left_inv],
        ext,
        simp, },
      rw measure_preimage_mul, },
    rw this,
    have hğ“•_translate_fundom : is_fundamental_domain Î“.opposite (g â€¢ ğ“•) Î¼ := hğ“•.smul_of_comm g,
    rw [hğ“•.measure_set_eq hğ“•_translate_fundom meas_Ï€A, â† preimage_smul_inv], refl,
    rintros âŸ¨Î³, Î³_in_Î“âŸ©,
    ext,
    have : Ï€ (x * (mul_opposite.unop Î³)) = Ï€ (x) := by simpa [quotient_group.eq'] using Î³_in_Î“,
    simp [(â€¢), this],
  end }

/-- Assuming `Î“` is a normal subgroup of a topological group `G`, the pushforward to the quotient
  group `G â§¸ Î“` of the restriction of a both left- and right-invariant measure on `G` to a
  fundamental domain `ğ“•` is a left-invariant measure on `G â§¸ Î“`. -/
@[to_additive "Assuming `Î“` is a normal subgroup of an additive topological group `G`, the
  pushforward to the quotient group `G â§¸ Î“` of the restriction of a both left- and right-invariant
  measure on `G` to a fundamental domain `ğ“•` is a left-invariant measure on `G â§¸ Î“`."]
lemma measure_theory.is_fundamental_domain.is_mul_left_invariant_map [subgroup.normal Î“]
  [Î¼.is_mul_left_invariant] [Î¼.is_mul_right_invariant] :
  (measure.map (quotient_group.mk' Î“) (Î¼.restrict ğ“•)).is_mul_left_invariant :=
{ map_mul_left_eq_self := begin
    intros x,
    apply measure.ext,
    intros A hA,
    obtain âŸ¨xâ‚, _âŸ© := @quotient.exists_rep _ (quotient_group.left_rel Î“) x,
    haveI := hğ“•.smul_invariant_measure_map,
    convert measure_preimage_smul xâ‚ ((measure.map quotient_group.mk) (Î¼.restrict ğ“•)) A using 1,
    rw [â† h, measure.map_apply],
    { refl, },
    { exact measurable_const_mul _, },
    { exact hA, },
  end }

/-- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the pushforward to the quotient
  group `G â§¸ Î“` of the restriction of `Î¼` to `ğ“•` is a multiple of Haar measure on `G â§¸ Î“`. -/
@[to_additive "Given a normal subgroup `Î“` of an additive topological group `G` with Haar measure
  `Î¼`, which is also right-invariant, and a finite volume fundamental domain `ğ“•`, the pushforward
  to the quotient group `G â§¸ Î“` of the restriction of `Î¼` to `ğ“•` is a multiple of Haar measure on
  `G â§¸ Î“`."]
lemma measure_theory.is_fundamental_domain.map_restrict_quotient [t2_space (G â§¸ Î“)]
  [second_countable_topology (G â§¸ Î“)] (K : positive_compacts (G â§¸ Î“)) [subgroup.normal Î“]
  [measure_theory.measure.is_haar_measure Î¼] [Î¼.is_mul_right_invariant]
  (hğ“•_finite : Î¼ ğ“• < âŠ¤) : measure.map (quotient_group.mk' Î“) (Î¼.restrict ğ“•)
  = (Î¼ (ğ“• âˆ© (quotient_group.mk' Î“) â»Â¹' K)) â€¢ (measure_theory.measure.haar_measure K) :=
begin
  let Ï€ : G â†’* G â§¸ Î“ := quotient_group.mk' Î“,
  have meas_Ï€ : measurable Ï€ := continuous_quotient_mk.measurable,
  have ğ“•meas : null_measurable_set ğ“• Î¼ := hğ“•.null_measurable_set,
  haveI : is_finite_measure (Î¼.restrict ğ“•) :=
    âŸ¨by { rw [measure.restrict_applyâ‚€' ğ“•meas, univ_inter], exact hğ“•_finite }âŸ©,
  -- the measure is left-invariant, so by the uniqueness of Haar measure it's enough to show that
  -- it has the stated size on the reference compact set `K`.
  haveI : (measure.map (quotient_group.mk' Î“) (Î¼.restrict ğ“•)).is_mul_left_invariant :=
    hğ“•.is_mul_left_invariant_map,
  rw [measure.haar_measure_unique (measure.map (quotient_group.mk' Î“) (Î¼.restrict ğ“•)) K,
    measure.map_apply meas_Ï€, measure.restrict_applyâ‚€' ğ“•meas, inter_comm],
  exact K.is_compact.measurable_set,
end

/-- Given a normal subgroup `Î“` of a topological group `G` with Haar measure `Î¼`, which is also
  right-invariant, and a finite volume fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“` is
  measure-preserving between appropriate multiples of Haar measure on `G` and `G â§¸ Î“`. -/
@[to_additive measure_preserving_quotient_add_group.mk' "Given a normal subgroup `Î“` of an additive
  topological group `G` with Haar measure `Î¼`, which is also right-invariant, and a finite volume
  fundamental domain `ğ“•`, the quotient map to `G â§¸ Î“` is measure-preserving between appropriate
  multiples of Haar measure on `G` and `G â§¸ Î“`."]
lemma measure_preserving_quotient_group.mk' [t2_space (G â§¸ Î“)] [second_countable_topology (G â§¸ Î“)]
  (K : positive_compacts (G â§¸ Î“)) [subgroup.normal Î“] [measure_theory.measure.is_haar_measure Î¼]
  [Î¼.is_mul_right_invariant] (hğ“•_finite : Î¼ ğ“• < âŠ¤) (c : â„â‰¥0)
  (h : Î¼ (ğ“• âˆ© (quotient_group.mk' Î“) â»Â¹' K) = c) :
  measure_preserving
    (quotient_group.mk' Î“)
    (Î¼.restrict ğ“•)
    (c â€¢ (measure_theory.measure.haar_measure K)) :=
{ measurable := continuous_quotient_mk.measurable,
  map_eq := by rw [hğ“•.map_restrict_quotient K hğ“•_finite, h]; refl }

section

local notation `Î¼_ğ“•` := measure.map (@quotient_group.mk G _ Î“) (Î¼.restrict ğ“•)

/-- The `ess_sup` of a function `g` on the quotient space `G â§¸ Î“` with respect to the pushforward
  of the restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` to a fundamental domain `ğ“•`, is the
  same as the `ess_sup` of `g`'s lift to the universal cover `G` with respect to `Î¼`. -/
@[to_additive "The `ess_sup` of a function `g` on the additive quotient space `G â§¸ Î“` with respect
  to the pushforward of the restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` to a fundamental
  domain `ğ“•`, is the same as the `ess_sup` of `g`'s lift to the universal cover `G` with respect
  to `Î¼`."]
lemma ess_sup_comp_quotient_group_mk [Î¼.is_mul_right_invariant] {g : G â§¸ Î“ â†’ â„â‰¥0âˆ}
  (g_ae_measurable : ae_measurable g Î¼_ğ“•) :
  ess_sup g Î¼_ğ“• = ess_sup (Î» (x : G), g x) Î¼ :=
begin
  have hÏ€ : measurable (quotient_group.mk : G â†’ G â§¸ Î“) := continuous_quotient_mk.measurable,
  rw ess_sup_map_measure g_ae_measurable hÏ€.ae_measurable,
  refine hğ“•.ess_sup_measure_restrict _,
  rintros âŸ¨Î³, hÎ³âŸ© x,
  dsimp,
  congr' 1,
  exact quotient_group.mk_mul_of_mem x hÎ³,
end

/-- Given a quotient space `G â§¸ Î“` where `Î“` is `countable`, and the restriction,
  `Î¼_ğ“•`, of a right-invariant measure `Î¼` on `G` to a fundamental domain `ğ“•`, a set
  in the quotient which has `Î¼_ğ“•`-measure zero, also has measure zero under the
  folding of `Î¼` under the quotient. Note that, if `Î“` is infinite, then the folded map
  will take the value `âˆ` on any open set in the quotient! -/
@[to_additive "Given an additive quotient space `G â§¸ Î“` where `Î“` is `countable`, and the
  restriction, `Î¼_ğ“•`, of a right-invariant measure `Î¼` on `G` to a fundamental domain `ğ“•`, a set
  in the quotient which has `Î¼_ğ“•`-measure zero, also has measure zero under the
  folding of `Î¼` under the quotient. Note that, if `Î“` is infinite, then the folded map
  will take the value `âˆ` on any open set in the quotient!"]
lemma _root_.measure_theory.is_fundamental_domain.absolutely_continuous_map
  [Î¼.is_mul_right_invariant] :
  map (quotient_group.mk : G â†’ G â§¸ Î“) Î¼ â‰ª map (quotient_group.mk : G â†’ G â§¸ Î“) (Î¼.restrict ğ“•) :=
begin
  set Ï€ : G â†’ G â§¸ Î“ := quotient_group.mk,
  have meas_Ï€ : measurable Ï€ := continuous_quotient_mk.measurable,
  apply absolutely_continuous.mk,
  intros s s_meas hs,
  rw map_apply meas_Ï€ s_meas at hs âŠ¢,
  rw measure.restrict_apply at hs,
  apply hğ“•.measure_zero_of_invariant _ _ hs,
  { intros Î³,
    ext g,
    rw set.mem_smul_set_iff_inv_smul_mem,
    rw mem_preimage,
    rw mem_preimage,
    congrm _ âˆˆ s,
    convert quotient_group.mk_mul_of_mem g (Î³â»Â¹).2, },
  exact measurable_set_preimage meas_Ï€ s_meas,
end

local attribute [-instance] quotient.measurable_space

/- Move to commit message in PR: question: how to deduce
  `ae_strongly_measurable (quotient_group.automorphize f) Î¼_ğ“•`? -/

/-- This is a simple version of the **Unfolding Trick**: Given a subgroup `Î“` of a group `G`, the
  integral of a function `f` on `G` with respect to a right-invariant measure `Î¼` is equal to the
  integral over the quotient `G â§¸ Î“` of the automorphization of `f`. -/
@[to_additive "This is a simple version of the **Unfolding Trick**: Given a subgroup `Î“` of an
  additive  group `G`, the integral of a function `f` on `G` with respect to a right-invariant
  measure `Î¼` is equal to the integral over the quotient `G â§¸ Î“` of the automorphization of `f`."]
lemma quotient_group.integral_eq_integral_automorphize {E : Type*} [normed_add_comm_group E]
  [complete_space E] [normed_space â„ E] [Î¼.is_mul_right_invariant] {f : G â†’ E}
  (hfâ‚ : integrable f Î¼) (hfâ‚‚ : ae_strongly_measurable (automorphize f) Î¼_ğ“•) :
  âˆ« x : G, f x âˆ‚Î¼ = âˆ« x : G â§¸ Î“, automorphize f x âˆ‚Î¼_ğ“• :=
calc âˆ« x : G, f x âˆ‚Î¼  = âˆ‘' Î³ : Î“.opposite, âˆ« x in ğ“•, f (Î³ â€¢ x) âˆ‚Î¼ : hğ“•.integral_eq_tsum'' f hfâ‚
... = âˆ« x in ğ“•, âˆ‘' Î³ : Î“.opposite, f (Î³ â€¢ x) âˆ‚Î¼ :
  begin
    rw integral_tsum,
    { exact Î» i, (hfâ‚.1.comp_quasi_measure_preserving
        (measure_preserving_smul i Î¼).quasi_measure_preserving).restrict, },
    { rw â† hğ“•.lintegral_eq_tsum'' (Î» x, â€–f xâ€–â‚Š),
      exact ne_of_lt hfâ‚.2, },
  end
... = âˆ« x : G â§¸ Î“, automorphize f x âˆ‚Î¼_ğ“• :
  (integral_map continuous_quotient_mk.ae_measurable hfâ‚‚).symm

/-- This is the **Unfolding Trick**: Given a subgroup `Î“` of a group `G`, the integral of a
  function `f` on `G` times the lift to `G` of a function `g` on the quotient `G â§¸ Î“` with respect
  to a right-invariant measure `Î¼` on `G`, is equal to the integral over the quotient of the
  automorphization of `f` times `g`. -/
lemma quotient_group.integral_mul_eq_integral_automorphize_mul {K : Type*} [normed_field K]
  [complete_space K] [normed_space â„ K] [Î¼.is_mul_right_invariant] {f : G â†’ K}
  (f_â„’_1 : integrable f Î¼) {g : G â§¸ Î“ â†’ K} (hg : ae_strongly_measurable g Î¼_ğ“•)
  (g_â„’_infinity : ess_sup (Î» x, â†‘â€–g xâ€–â‚Š) Î¼_ğ“• â‰  âˆ)
  (F_ae_measurable : ae_strongly_measurable (quotient_group.automorphize f) Î¼_ğ“•) :
  âˆ« x : G, g (x : G â§¸ Î“) * (f x) âˆ‚Î¼ = âˆ« x : G â§¸ Î“, g x * (quotient_group.automorphize f x) âˆ‚Î¼_ğ“• :=
begin
  let Ï€ : G â†’ G â§¸ Î“ := quotient_group.mk,
  have Hâ‚€ : quotient_group.automorphize ((g âˆ˜ Ï€) * f) = g * (quotient_group.automorphize f) :=
    quotient_group.automorphize_smul_left f g,
  calc âˆ« (x : G), g (Ï€ x) * f x âˆ‚Î¼ =
       âˆ« (x : G â§¸ Î“), quotient_group.automorphize ((g âˆ˜ Ï€) * f) x âˆ‚Î¼_ğ“• : _
  ... = âˆ« (x : G â§¸ Î“), g x * (quotient_group.automorphize f x) âˆ‚Î¼_ğ“• : by simp [Hâ‚€],
  have meas_Ï€ : measurable Ï€ := continuous_quotient_mk.measurable,
  have Hâ‚ : integrable ((g âˆ˜ Ï€) * f) Î¼,
  { have : ae_strongly_measurable (Î» x : G, g (x : G â§¸ Î“)) Î¼,
    { refine (ae_strongly_measurable_of_absolutely_continuous _ _ hg).comp_measurable meas_Ï€,
      exact hğ“•.absolutely_continuous_map },
    refine integrable.ess_sup_smul f_â„’_1 this _,
    { have hg' : ae_strongly_measurable (Î» x, â†‘â€–g xâ€–â‚Š) Î¼_ğ“• :=
        (ennreal.continuous_coe.comp continuous_nnnorm).comp_ae_strongly_measurable hg,
      rw [â† ess_sup_comp_quotient_group_mk hğ“• hg'.ae_measurable],
      exact g_â„’_infinity } },
  have Hâ‚‚ : ae_strongly_measurable (quotient_group.automorphize ((g âˆ˜ Ï€) * f)) Î¼_ğ“•,
  { simp_rw [Hâ‚€],
    exact hg.mul F_ae_measurable },
  apply quotient_group.integral_eq_integral_automorphize hğ“• Hâ‚ Hâ‚‚,
end

end

---- trying to additivize 4/14/23
 --- ALEX HOMEWORK

section

variables {G' : Type*} [add_group G'] [measurable_space G'] [topological_space G']
  [topological_add_group G'] [borel_space G']
  {Î¼' : measure G'}
  {Î“' : add_subgroup G'}
  {ğ“•' : set G'} (hğ“•' : is_add_fundamental_domain Î“'.opposite ğ“•' Î¼')

local notation `Î¼_ğ“•` := measure.map (@quotient_add_group.mk G' _ Î“') (Î¼'.restrict ğ“•')

/-- This is the **Unfolding Trick**: Given an additive subgroup `Î“'` of an additive group `G'`,

**** FIx

 the integral of a
  function `f` on `G'` times the lift to `G'` of a function `g` on the quotient `G' â§¸ Î“'` with respect
  to a right-invariant measure `Î¼` on `G'`, is equal to the integral over the quotient of the
  automorphization of `f` times `g`. -/
lemma quotient_add_group.integral_mul_eq_integral_automorphize_mul
{K : Type*} [normed_field K]
  [complete_space K] [normed_space â„ K] [Î¼'.is_add_right_invariant] {f : G' â†’ K}
  (f_â„’_1 : integrable f Î¼') {g : G' â§¸ Î“' â†’ K} (hg : ae_strongly_measurable g Î¼_ğ“•)
  (g_â„’_infinity : ess_sup (Î» x, â†‘â€–g xâ€–â‚Š) Î¼_ğ“• â‰  âˆ)
  (F_ae_measurable : ae_strongly_measurable (quotient_add_group.automorphize f) Î¼_ğ“•) :
  âˆ« x : G', g (x : G' â§¸ Î“') * (f x) âˆ‚Î¼' = âˆ« x : G' â§¸ Î“', g x * (quotient_add_group.automorphize f x) âˆ‚Î¼_ğ“• :=
begin
  let Ï€ : G' â†’ G' â§¸ Î“' := quotient_add_group.mk,
  have Hâ‚€ : quotient_add_group.automorphize ((g âˆ˜ Ï€) * f) = g * (quotient_add_group.automorphize f) :=
    quotient_add_group.automorphize_smul_left f g,
  calc âˆ« (x : G'), g (Ï€ x) * f x âˆ‚Î¼' =
       âˆ« (x : G' â§¸ Î“'), quotient_add_group.automorphize ((g âˆ˜ Ï€) * f) x âˆ‚Î¼_ğ“• : _
  ... = âˆ« (x : G' â§¸ Î“'), g x * (quotient_add_group.automorphize f x) âˆ‚Î¼_ğ“• : by simp [Hâ‚€],
  have meas_Ï€ : measurable Ï€ := continuous_quotient_mk.measurable,
  have Hâ‚ : integrable ((g âˆ˜ Ï€) * f) Î¼',
  { have : ae_strongly_measurable (Î» x : G', g (x : G' â§¸ Î“')) Î¼',
    { refine (ae_strongly_measurable_of_absolutely_continuous _ _ hg).comp_measurable meas_Ï€,
      exact hğ“•'.absolutely_continuous_map },
    refine integrable.ess_sup_smul f_â„’_1 this _,
    { have hg' : ae_strongly_measurable (Î» x, â†‘â€–g xâ€–â‚Š) Î¼_ğ“• :=
        (ennreal.continuous_coe.comp continuous_nnnorm).comp_ae_strongly_measurable hg,
      rw [â† ess_sup_comp_quotient_add_group_mk hğ“•' hg'.ae_measurable],
      exact g_â„’_infinity } },
  have Hâ‚‚ : ae_strongly_measurable (quotient_add_group.automorphize ((g âˆ˜ Ï€) * f)) Î¼_ğ“•,
  { simp_rw [Hâ‚€],
    exact hg.mul F_ae_measurable },
  apply quotient_add_group.integral_eq_integral_automorphize hğ“•' Hâ‚ Hâ‚‚,
end

end

-- add `attribute` pairing line (ALEX HOMEWORK)
