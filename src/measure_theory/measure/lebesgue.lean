/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Yury Kudryashov
-/
import measure_theory.constructions.pi
import measure_theory.measure.stieltjes
import linear_algebra.matrix.transvection
import dynamics.ergodic.measure_preserving
import linear_algebra.determinant

/-!
# Lebesgue measure on the real line and on `‚Ñù‚Åø`

We construct Lebesgue measure on the real line, as a particular case of Stieltjes measure associated
to the function `x ‚Ü¶ x`. We obtain as a consequence Lebesgue measure on `‚Ñù‚Åø`. We prove that they
are translation invariant.

We show that, on `‚Ñù‚Åø`, a linear map acts on Lebesgue measure by rescaling it through the absolute
value of its determinant, in `real.map_linear_map_volume_pi_eq_smul_volume_pi`.

More properties of the Lebesgue measure are deduced from this in `haar_lebesgue.lean`, where they
are proved more generally for any additive Haar measure on a finite-dimensional real vector space.
-/

noncomputable theory
open classical set filter measure_theory
open ennreal (of_real)
open_locale big_operators ennreal nnreal topological_space

/-!
### Definition of the Lebesgue measure and lengths of intervals
-/

/-- Lebesgue measure on the Borel sigma algebra, giving measure `b - a` to the interval `[a, b]`. -/
instance real.measure_space : measure_space ‚Ñù :=
‚ü®stieltjes_function.id.measure‚ü©

namespace real

variables {Œπ : Type*} [fintype Œπ]

open_locale topological_space

theorem volume_val (s) : volume s = stieltjes_function.id.measure s := rfl

@[simp] lemma volume_Ico {a b : ‚Ñù} : volume (Ico a b) = of_real (b - a) :=
by simp [volume_val]

@[simp] lemma volume_Icc {a b : ‚Ñù} : volume (Icc a b) = of_real (b - a) :=
by simp [volume_val]

@[simp] lemma volume_Ioo {a b : ‚Ñù} : volume (Ioo a b) = of_real (b - a) :=
by simp [volume_val]

@[simp] lemma volume_Ioc {a b : ‚Ñù} : volume (Ioc a b) = of_real (b - a) :=
by simp [volume_val]

@[simp] lemma volume_singleton {a : ‚Ñù} : volume ({a} : set ‚Ñù) = 0 :=
by simp [volume_val]

@[simp] lemma volume_univ : volume (univ : set ‚Ñù) = ‚àû :=
ennreal.eq_top_of_forall_nnreal_le $ Œª r,
  calc (r : ‚Ñù‚â•0‚àû) = volume (Icc (0 : ‚Ñù) r) : by simp
              ... ‚â§ volume univ            : measure_mono (subset_univ _)

@[simp] lemma volume_ball (a r : ‚Ñù) :
  volume (metric.ball a r) = of_real (2 * r) :=
by rw [ball_eq, volume_Ioo, ‚Üê sub_add, add_sub_cancel', two_mul]

@[simp] lemma volume_closed_ball (a r : ‚Ñù) :
  volume (metric.closed_ball a r) = of_real (2 * r) :=
by rw [closed_ball_eq, volume_Icc, ‚Üê sub_add, add_sub_cancel', two_mul]

@[simp] lemma volume_emetric_ball (a : ‚Ñù) (r : ‚Ñù‚â•0‚àû) :
  volume (emetric.ball a r) = 2 * r :=
begin
  rcases eq_or_ne r ‚àû with rfl|hr,
  { rw [metric.emetric_ball_top, volume_univ, two_mul, ennreal.top_add] },
  { lift r to ‚Ñù‚â•0 using hr,
    rw [metric.emetric_ball_nnreal, volume_ball, two_mul, ‚Üê nnreal.coe_add,
      ennreal.of_real_coe_nnreal, ennreal.coe_add, two_mul] }
end

@[simp] lemma volume_emetric_closed_ball (a : ‚Ñù) (r : ‚Ñù‚â•0‚àû) :
  volume (emetric.closed_ball a r) = 2 * r :=
begin
  rcases eq_or_ne r ‚àû with rfl|hr,
  { rw [emetric.closed_ball_top, volume_univ, two_mul, ennreal.top_add] },
  { lift r to ‚Ñù‚â•0 using hr,
    rw [metric.emetric_closed_ball_nnreal, volume_closed_ball, two_mul, ‚Üê nnreal.coe_add,
      ennreal.of_real_coe_nnreal, ennreal.coe_add, two_mul] }
end

instance has_no_atoms_volume : has_no_atoms (volume : measure ‚Ñù) :=
‚ü®Œª x, volume_singleton‚ü©

@[simp] lemma volume_interval {a b : ‚Ñù} : volume (interval a b) = of_real (|b - a|) :=
by rw [interval, volume_Icc, max_sub_min_eq_abs]

@[simp] lemma volume_Ioi {a : ‚Ñù} : volume (Ioi a) = ‚àû :=
top_unique $ le_of_tendsto' ennreal.tendsto_nat_nhds_top $ Œª n,
calc (n : ‚Ñù‚â•0‚àû) = volume (Ioo a (a + n)) : by simp
... ‚â§ volume (Ioi a) : measure_mono Ioo_subset_Ioi_self

@[simp] lemma volume_Ici {a : ‚Ñù} : volume (Ici a) = ‚àû :=
by simp [‚Üê measure_congr Ioi_ae_eq_Ici]

@[simp] lemma volume_Iio {a : ‚Ñù} : volume (Iio a) = ‚àû :=
top_unique $ le_of_tendsto' ennreal.tendsto_nat_nhds_top $ Œª n,
calc (n : ‚Ñù‚â•0‚àû) = volume (Ioo (a - n) a) : by simp
... ‚â§ volume (Iio a) : measure_mono Ioo_subset_Iio_self

@[simp] lemma volume_Iic {a : ‚Ñù} : volume (Iic a) = ‚àû :=
by simp [‚Üê measure_congr Iio_ae_eq_Iic]

instance locally_finite_volume : is_locally_finite_measure (volume : measure ‚Ñù) :=
‚ü®Œª x, ‚ü®Ioo (x - 1) (x + 1),
  is_open.mem_nhds is_open_Ioo ‚ü®sub_lt_self _ zero_lt_one, lt_add_of_pos_right _ zero_lt_one‚ü©,
  by simp only [real.volume_Ioo, ennreal.of_real_lt_top]‚ü©‚ü©

instance is_finite_measure_restrict_Icc (x y : ‚Ñù) : is_finite_measure (volume.restrict (Icc x y)) :=
‚ü®by simp‚ü©

instance is_finite_measure_restrict_Ico (x y : ‚Ñù) : is_finite_measure (volume.restrict (Ico x y)) :=
‚ü®by simp‚ü©

instance is_finite_measure_restrict_Ioc (x y : ‚Ñù) : is_finite_measure (volume.restrict (Ioc x y)) :=
 ‚ü®by simp‚ü©

instance is_finite_measure_restrict_Ioo (x y : ‚Ñù) : is_finite_measure (volume.restrict (Ioo x y)) :=
‚ü®by simp‚ü©

/-!
### Volume of a box in `‚Ñù‚Åø`
-/

lemma volume_Icc_pi {a b : Œπ ‚Üí ‚Ñù} : volume (Icc a b) = ‚àè i, ennreal.of_real (b i - a i) :=
begin
  rw [‚Üê pi_univ_Icc, volume_pi_pi],
  { simp only [real.volume_Icc] },
  { exact Œª i, measurable_set_Icc }
end

@[simp] lemma volume_Icc_pi_to_real {a b : Œπ ‚Üí ‚Ñù} (h : a ‚â§ b) :
  (volume (Icc a b)).to_real = ‚àè i, (b i - a i) :=
by simp only [volume_Icc_pi, ennreal.to_real_prod, ennreal.to_real_of_real (sub_nonneg.2 (h _))]

lemma volume_pi_Ioo {a b : Œπ ‚Üí ‚Ñù} :
  volume (pi univ (Œª i, Ioo (a i) (b i))) = ‚àè i, ennreal.of_real (b i - a i) :=
(measure_congr measure.univ_pi_Ioo_ae_eq_Icc).trans volume_Icc_pi

@[simp] lemma volume_pi_Ioo_to_real {a b : Œπ ‚Üí ‚Ñù} (h : a ‚â§ b) :
  (volume (pi univ (Œª i, Ioo (a i) (b i)))).to_real = ‚àè i, (b i - a i) :=
by simp only [volume_pi_Ioo, ennreal.to_real_prod, ennreal.to_real_of_real (sub_nonneg.2 (h _))]

lemma volume_pi_Ioc {a b : Œπ ‚Üí ‚Ñù} :
  volume (pi univ (Œª i, Ioc (a i) (b i))) = ‚àè i, ennreal.of_real (b i - a i) :=
(measure_congr measure.univ_pi_Ioc_ae_eq_Icc).trans volume_Icc_pi

@[simp] lemma volume_pi_Ioc_to_real {a b : Œπ ‚Üí ‚Ñù} (h : a ‚â§ b) :
  (volume (pi univ (Œª i, Ioc (a i) (b i)))).to_real = ‚àè i, (b i - a i) :=
by simp only [volume_pi_Ioc, ennreal.to_real_prod, ennreal.to_real_of_real (sub_nonneg.2 (h _))]

lemma volume_pi_Ico {a b : Œπ ‚Üí ‚Ñù} :
  volume (pi univ (Œª i, Ico (a i) (b i))) = ‚àè i, ennreal.of_real (b i - a i) :=
(measure_congr measure.univ_pi_Ico_ae_eq_Icc).trans volume_Icc_pi

@[simp] lemma volume_pi_Ico_to_real {a b : Œπ ‚Üí ‚Ñù} (h : a ‚â§ b) :
  (volume (pi univ (Œª i, Ico (a i) (b i)))).to_real = ‚àè i, (b i - a i) :=
by simp only [volume_pi_Ico, ennreal.to_real_prod, ennreal.to_real_of_real (sub_nonneg.2 (h _))]

@[simp] lemma volume_pi_ball (a : Œπ ‚Üí ‚Ñù) {r : ‚Ñù} (hr : 0 < r) :
  volume (metric.ball a r) = ennreal.of_real ((2 * r) ^ fintype.card Œπ) :=
begin
  simp only [volume_pi_ball a hr, volume_ball, finset.prod_const],
  exact (ennreal.of_real_pow (mul_nonneg zero_le_two hr.le) _).symm
end

@[simp] lemma volume_pi_closed_ball (a : Œπ ‚Üí ‚Ñù) {r : ‚Ñù} (hr : 0 ‚â§ r) :
  volume (metric.closed_ball a r) = ennreal.of_real ((2 * r) ^ fintype.card Œπ) :=
begin
  simp only [volume_pi_closed_ball a hr, volume_closed_ball, finset.prod_const],
  exact (ennreal.of_real_pow (mul_nonneg zero_le_two hr) _).symm
end

lemma volume_le_diam (s : set ‚Ñù) : volume s ‚â§ emetric.diam s :=
begin
  by_cases hs : metric.bounded s,
  { rw [real.ediam_eq hs, ‚Üê volume_Icc],
    exact volume.mono (real.subset_Icc_Inf_Sup_of_bounded hs) },
  { rw metric.ediam_of_unbounded hs, exact le_top }
end

lemma volume_pi_le_prod_diam (s : set (Œπ ‚Üí ‚Ñù)) :
  volume s ‚â§ ‚àè i : Œπ, emetric.diam (function.eval i '' s) :=
calc volume s ‚â§ volume (pi univ (Œª i, closure (function.eval i '' s))) :
  volume.mono $ subset.trans (subset_pi_eval_image univ s) $ pi_mono $ Œª i hi, subset_closure
          ... = ‚àè i, volume (closure $ function.eval i '' s) :
  volume_pi_pi _ $ Œª i, measurable_set_closure
          ... ‚â§ ‚àè i : Œπ, emetric.diam (function.eval i '' s) :
  finset.prod_le_prod' $ Œª i hi, (volume_le_diam _).trans_eq (emetric.diam_closure _)

lemma volume_pi_le_diam_pow (s : set (Œπ ‚Üí ‚Ñù)) :
  volume s ‚â§ emetric.diam s ^ fintype.card Œπ :=
calc volume s ‚â§ ‚àè i : Œπ, emetric.diam (function.eval i '' s) : volume_pi_le_prod_diam s
          ... ‚â§ ‚àè i : Œπ, (1 : ‚Ñù‚â•0) * emetric.diam s                      :
  finset.prod_le_prod' $ Œª i hi, (lipschitz_with.eval i).ediam_image_le s
          ... = emetric.diam s ^ fintype.card Œπ              :
  by simp only [ennreal.coe_one, one_mul, finset.prod_const, fintype.card]

/-!
### Images of the Lebesgue measure under translation/multiplication in ‚Ñù
-/

lemma map_volume_add_left (a : ‚Ñù) : measure.map ((+) a) volume = volume :=
eq.symm $ real.measure_ext_Ioo_rat $ Œª p q,
  by simp [measure.map_apply (measurable_const_add a) measurable_set_Ioo, sub_sub_sub_cancel_right]

@[simp] lemma volume_preimage_add_left (a : ‚Ñù) (s : set ‚Ñù) : volume (((+) a) ‚Åª¬π' s) = volume s :=
calc volume (((+) a) ‚Åª¬π' s) = measure.map ((+) a) volume s :
  ((homeomorph.add_left a).to_measurable_equiv.map_apply s).symm
... = volume s : by rw map_volume_add_left

lemma map_volume_add_right (a : ‚Ñù) : measure.map (+ a) volume = volume :=
by simpa only [add_comm] using real.map_volume_add_left a

@[simp] lemma volume_preimage_add_right (a : ‚Ñù) (s : set ‚Ñù) : volume ((+ a) ‚Åª¬π' s) = volume s :=
calc volume ((+ a) ‚Åª¬π' s) = measure.map (+ a) volume s :
  ((homeomorph.add_right a).to_measurable_equiv.map_apply s).symm
... = volume s : by rw map_volume_add_right

lemma smul_map_volume_mul_left {a : ‚Ñù} (h : a ‚â† 0) :
  ennreal.of_real (|a|) ‚Ä¢ measure.map ((*) a) volume = volume :=
begin
  refine (real.measure_ext_Ioo_rat $ Œª p q, _).symm,
  cases lt_or_gt_of_ne h with h h,
  { simp only [real.volume_Ioo, measure.smul_apply, ‚Üê ennreal.of_real_mul (le_of_lt $ neg_pos.2 h),
      measure.map_apply (measurable_const_mul a) measurable_set_Ioo, neg_sub_neg,
      ‚Üê neg_mul_eq_neg_mul, preimage_const_mul_Ioo_of_neg _ _ h, abs_of_neg h, mul_sub,
      mul_div_cancel' _ (ne_of_lt h)] },
  { simp only [real.volume_Ioo, measure.smul_apply, ‚Üê ennreal.of_real_mul (le_of_lt h),
      measure.map_apply (measurable_const_mul a) measurable_set_Ioo, preimage_const_mul_Ioo _ _ h,
      abs_of_pos h, mul_sub, mul_div_cancel' _ (ne_of_gt h)] }
end

lemma map_volume_mul_left {a : ‚Ñù} (h : a ‚â† 0) :
  measure.map ((*) a) volume = ennreal.of_real (|a‚Åª¬π|) ‚Ä¢ volume :=
by conv_rhs { rw [‚Üê real.smul_map_volume_mul_left h, smul_smul,
  ‚Üê ennreal.of_real_mul (abs_nonneg _), ‚Üê abs_mul, inv_mul_cancel h, abs_one, ennreal.of_real_one,
  one_smul] }

@[simp] lemma volume_preimage_mul_left {a : ‚Ñù} (h : a ‚â† 0) (s : set ‚Ñù) :
  volume (((*) a) ‚Åª¬π' s) = ennreal.of_real (abs a‚Åª¬π) * volume s :=
calc volume (((*) a) ‚Åª¬π' s) = measure.map ((*) a) volume s :
  ((homeomorph.mul_left‚ÇÄ a h).to_measurable_equiv.map_apply s).symm
... = ennreal.of_real (abs a‚Åª¬π) * volume s : by { rw map_volume_mul_left h, refl }

lemma smul_map_volume_mul_right {a : ‚Ñù} (h : a ‚â† 0) :
  ennreal.of_real (|a|) ‚Ä¢ measure.map (* a) volume = volume :=
by simpa only [mul_comm] using real.smul_map_volume_mul_left h

lemma map_volume_mul_right {a : ‚Ñù} (h : a ‚â† 0) :
  measure.map (* a) volume = ennreal.of_real (|a‚Åª¬π|) ‚Ä¢ volume :=
by simpa only [mul_comm] using real.map_volume_mul_left h

@[simp] lemma volume_preimage_mul_right {a : ‚Ñù} (h : a ‚â† 0) (s : set ‚Ñù) :
  volume ((* a) ‚Åª¬π' s) = ennreal.of_real (abs a‚Åª¬π) * volume s :=
calc volume ((* a) ‚Åª¬π' s) = measure.map (* a) volume s :
  ((homeomorph.mul_right‚ÇÄ a h).to_measurable_equiv.map_apply s).symm
... = ennreal.of_real (abs a‚Åª¬π) * volume s : by { rw map_volume_mul_right h, refl }

@[simp] lemma map_volume_neg : measure.map has_neg.neg (volume : measure ‚Ñù) = volume :=
eq.symm $ real.measure_ext_Ioo_rat $ Œª p q,
  by simp [show measure.map has_neg.neg volume (Ioo (p : ‚Ñù) q) = _,
    from measure.map_apply measurable_neg measurable_set_Ioo]

/-!
### Images of the Lebesgue measure under translation/linear maps in ‚Ñù‚Åø
-/

lemma map_volume_pi_add_left (a : Œπ ‚Üí ‚Ñù) : measure.map ((+) a) volume = volume :=
begin
  refine (measure.pi_eq (Œª s hs, _)).symm,
  have A : has_add.add a ‚Åª¬π' (set.pi univ (Œª (i : Œπ), s i))
    = set.pi univ (Œª (i : Œπ), ((+) (a i)) ‚Åª¬π' (s i)), by { ext, simp },
  rw [measure.map_apply (measurable_const_add a) (measurable_set.univ_pi_fintype hs), A,
      volume_pi_pi],
  { simp only [volume_preimage_add_left] },
  { exact Œª i, measurable_const_add (a i) (hs i) }
end

@[simp] lemma volume_pi_preimage_add_left (a : Œπ ‚Üí ‚Ñù) (s : set (Œπ ‚Üí ‚Ñù)) :
  volume (((+) a) ‚Åª¬π' s) = volume s :=
calc volume (((+) a) ‚Åª¬π' s) = measure.map ((+) a) volume s :
  ((homeomorph.add_left a).to_measurable_equiv.map_apply s).symm
... = volume s : by rw map_volume_pi_add_left

open matrix

/-- A diagonal matrix rescales Lebesgue according to its determinant. This is a special case of
`real.map_matrix_volume_pi_eq_smul_volume_pi`, that one should use instead (and whose proof
uses this particular case). -/
lemma smul_map_diagonal_volume_pi [decidable_eq Œπ] {D : Œπ ‚Üí ‚Ñù} (h : det (diagonal D) ‚â† 0) :
  ennreal.of_real (abs (det (diagonal D))) ‚Ä¢ measure.map ((diagonal D).to_lin') volume = volume :=
begin
  refine (measure.pi_eq (Œª s hs, _)).symm,
  simp only [det_diagonal, measure.coe_smul, algebra.id.smul_eq_mul, pi.smul_apply],
  rw [measure.map_apply _ (measurable_set.univ_pi_fintype hs)],
  swap, { exact continuous.measurable (linear_map.continuous_on_pi _) },
  have : (matrix.to_lin' (diagonal D)) ‚Åª¬π' (set.pi set.univ (Œª (i : Œπ), s i))
    = set.pi set.univ (Œª (i : Œπ), ((*) (D i)) ‚Åª¬π' (s i)),
  { ext f,
    simp only [linear_map.coe_proj, algebra.id.smul_eq_mul, linear_map.smul_apply, mem_univ_pi,
      mem_preimage, linear_map.pi_apply, diagonal_to_lin'] },
  have B : ‚àÄ i, of_real (abs (D i)) * volume (has_mul.mul (D i) ‚Åª¬π' s i) = volume (s i),
  { assume i,
    have A : D i ‚â† 0,
    { simp only [det_diagonal, ne.def] at h,
      exact finset.prod_ne_zero_iff.1 h i (finset.mem_univ i) },
    rw [volume_preimage_mul_left A, ‚Üê mul_assoc, ‚Üê ennreal.of_real_mul (abs_nonneg _), ‚Üê abs_mul,
      mul_inv_cancel A, abs_one, ennreal.of_real_one, one_mul] },
  rw [this, volume_pi_pi, finset.abs_prod,
    ennreal.of_real_prod_of_nonneg (Œª i hi, abs_nonneg (D i)), ‚Üê finset.prod_mul_distrib],
  { simp only [B] },
  { exact Œª i, measurable_const_mul _ (hs i) },
end

/-- A transvection preserves Lebesgue measure. -/
lemma map_transvection_volume_pi [decidable_eq Œπ] (t : transvection_struct Œπ ‚Ñù) :
  measure.map (t.to_matrix.to_lin') volume = volume :=
begin
  /- We separate the coordinate along which there is a shearing from the other ones, and apply
  Fubini. Along this coordinate (and when all the other coordinates are fixed), it acts like a
  translation, and therefore preserves Lebesgue. -/
  suffices H : measure_preserving t.to_matrix.to_lin' volume volume, by exact H.2,
  let p : Œπ ‚Üí Prop := Œª i, i ‚â† t.i,
  let Œ± : Type* := {x // p x},
  let Œ≤ : Type* := {x // ¬¨ (p x)},
  let g : (Œ± ‚Üí ‚Ñù) ‚Üí (Œ≤ ‚Üí ‚Ñù) ‚Üí (Œ≤ ‚Üí ‚Ñù) := Œª a b, (Œª x, t.c * a ‚ü®t.j, t.hij.symm‚ü©) + b,
  let F : (Œ± ‚Üí ‚Ñù) √ó (Œ≤ ‚Üí ‚Ñù) ‚Üí (Œ± ‚Üí ‚Ñù) √ó (Œ≤ ‚Üí ‚Ñù) :=
    Œª p, (id p.1, g p.1 p.2),
  let e := equiv.pi_equiv_pi_subtype_prod p (Œª (i : Œπ), ‚Ñù),
  have : (t.to_matrix.to_lin' : (Œπ ‚Üí ‚Ñù) ‚Üí (Œπ ‚Üí ‚Ñù)) = e.symm ‚àò F ‚àò e,
  { cases t,
    ext f k,
    simp only [linear_equiv.map_smul, dite_eq_ite, linear_map.id_coe, p, ite_not,
      algebra.id.smul_eq_mul, one_mul, dot_product, std_basis_matrix,
      equiv.pi_equiv_pi_subtype_prod_symm_apply, id.def, transvection,
      pi.add_apply, zero_mul, linear_map.smul_apply, function.comp_app,
      equiv.pi_equiv_pi_subtype_prod_apply, matrix.transvection_struct.to_matrix_mk,
      matrix.mul_vec, linear_equiv.map_add, ite_mul, e, matrix.to_lin'_apply,
      pi.smul_apply, subtype.coe_mk, g, linear_map.add_apply, finset.sum_congr, matrix.to_lin'_one],
    by_cases h : t_i = k,
    { simp only [h, true_and, finset.mem_univ, if_true, eq_self_iff_true, finset.sum_ite_eq,
        one_apply, boole_mul, add_comm], },
    { simp only [h, ne.symm h, add_zero, if_false, finset.sum_const_zero, false_and, mul_zero] } },
  rw this,
  have A : measure_preserving e volume volume :=
  ‚ü® measurable_pi_equiv_pi_subtype_prod (Œª i, ‚Ñù) _,
    (measure.map_pi_equiv_pi_subtype_prod (Œª i, (volume : measure ‚Ñù)) p : _) ‚ü©,
  have B : measure_preserving F volume volume,
  { have g_meas : measurable (function.uncurry g),
    { have : measurable (Œª (c : (Œ± ‚Üí ‚Ñù)), c ‚ü®t.j, t.hij.symm‚ü©) :=
        measurable_pi_apply ‚ü®t.j, t.hij.symm‚ü©,
      refine measurable.add (measurable_pi_lambda _ (Œª i, measurable.const_mul _ _)) measurable_snd,
      exact this.comp measurable_fst },
    exact measure_preserving.skew_product (measure_preserving.id _) g_meas
      (eventually_of_forall (Œª a, map_volume_pi_add_left _)) },
  have C : measure_preserving e.symm volume volume :=
  ‚ü® (measurable_pi_equiv_pi_subtype_prod_symm (Œª (i : Œπ), ‚Ñù) p : _),
    (measure.map_pi_equiv_pi_subtype_prod_symm (Œª (i : Œπ), volume) p : _) ‚ü©,
  exact (C.comp B).comp A,
end

/-- Any invertible matrix rescales Lebesgue measure through the absolute value of its
determinant. -/
lemma map_matrix_volume_pi_eq_smul_volume_pi [decidable_eq Œπ] {M : matrix Œπ Œπ ‚Ñù} (hM : det M ‚â† 0) :
  measure.map (M.to_lin') volume = ennreal.of_real (abs (det M)‚Åª¬π) ‚Ä¢ volume :=
begin
  -- This follows from the cases we have already proved, of diagonal matrices and transvections,
  -- as these matrices generate all invertible matrices.
  apply diagonal_transvection_induction_of_det_ne_zero _ M hM (Œª D hD, _) (Œª t, _)
    (Œª A B hA hB IHA IHB, _),
  { conv_rhs { rw [‚Üê smul_map_diagonal_volume_pi hD] },
    rw [smul_smul, ‚Üê ennreal.of_real_mul (abs_nonneg _), ‚Üê abs_mul, inv_mul_cancel hD, abs_one,
      ennreal.of_real_one, one_smul] },
  { simp only [matrix.transvection_struct.det, ennreal.of_real_one, map_transvection_volume_pi,
      one_smul, _root_.inv_one, abs_one] },
  { rw [to_lin'_mul, det_mul, linear_map.coe_comp, ‚Üê measure.map_map, IHB, linear_map.map_smul,
      IHA, smul_smul, ‚Üê ennreal.of_real_mul (abs_nonneg _), ‚Üê abs_mul, mul_comm, mul_inv‚ÇÄ],
    { apply continuous.measurable,
      apply linear_map.continuous_on_pi },
    { apply continuous.measurable,
      apply linear_map.continuous_on_pi } }
end

/-- Any invertible linear map rescales Lebesgue measure through the absolute value of its
determinant. -/
lemma map_linear_map_volume_pi_eq_smul_volume_pi {f : (Œπ ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] (Œπ ‚Üí ‚Ñù)} (hf : f.det ‚â† 0) :
  measure.map f volume = ennreal.of_real (abs (f.det)‚Åª¬π) ‚Ä¢ volume :=
begin
  -- this is deduced from the matrix case
  classical,
  let M := f.to_matrix',
  have A : f.det = det M, by simp only [linear_map.det_to_matrix'],
  have B : f = M.to_lin', by simp only [to_lin'_to_matrix'],
  rw [A, B],
  apply map_matrix_volume_pi_eq_smul_volume_pi,
  rwa A at hf
end

end real

open_locale topological_space

lemma filter.eventually.volume_pos_of_nhds_real {p : ‚Ñù ‚Üí Prop} {a : ‚Ñù} (h : ‚àÄ·∂† x in ùìù a, p x) :
  (0 : ‚Ñù‚â•0‚àû) < volume {x | p x} :=
begin
  rcases h.exists_Ioo_subset with ‚ü®l, u, hx, hs‚ü©,
  refine lt_of_lt_of_le _ (measure_mono hs),
  simpa [-mem_Ioo] using hx.1.trans hx.2
end

section region_between

open_locale classical

variable {Œ± : Type*}

/-- The region between two real-valued functions on an arbitrary set. -/
def region_between (f g : Œ± ‚Üí ‚Ñù) (s : set Œ±) : set (Œ± √ó ‚Ñù) :=
{p : Œ± √ó ‚Ñù | p.1 ‚àà s ‚àß p.2 ‚àà Ioo (f p.1) (g p.1)}

lemma region_between_subset (f g : Œ± ‚Üí ‚Ñù) (s : set Œ±) : region_between f g s ‚äÜ s.prod univ :=
by simpa only [prod_univ, region_between, set.preimage, set_of_subset_set_of] using Œª a, and.left

variables [measurable_space Œ±] {Œº : measure Œ±} {f g : Œ± ‚Üí ‚Ñù} {s : set Œ±}

/-- The region between two measurable functions on a measurable set is measurable. -/
lemma measurable_set_region_between
  (hf : measurable f) (hg : measurable g) (hs : measurable_set s) :
  measurable_set (region_between f g s) :=
begin
  dsimp only [region_between, Ioo, mem_set_of_eq, set_of_and],
  refine measurable_set.inter _ ((measurable_set_lt (hf.comp measurable_fst) measurable_snd).inter
    (measurable_set_lt measurable_snd (hg.comp measurable_fst))),
  convert hs.prod measurable_set.univ,
  simp only [and_true, mem_univ],
end

theorem volume_region_between_eq_lintegral'
  (hf : measurable f) (hg : measurable g) (hs : measurable_set s) :
  Œº.prod volume (region_between f g s) = ‚à´‚Åª y in s, ennreal.of_real ((g - f) y) ‚àÇŒº :=
begin
  rw measure.prod_apply,
  { have h : (Œª x, volume {a | x ‚àà s ‚àß a ‚àà Ioo (f x) (g x)})
            = s.indicator (Œª x, ennreal.of_real (g x - f x)),
    { funext x,
      rw indicator_apply,
      split_ifs,
      { have hx : {a | x ‚àà s ‚àß a ‚àà Ioo (f x) (g x)} = Ioo (f x) (g x) := by simp [h, Ioo],
        simp only [hx, real.volume_Ioo, sub_zero] },
      { have hx : {a | x ‚àà s ‚àß a ‚àà Ioo (f x) (g x)} = ‚àÖ := by simp [h],
        simp only [hx, measure_empty] } },
    dsimp only [region_between, preimage_set_of_eq],
    rw [h, lintegral_indicator];
    simp only [hs, pi.sub_apply] },
  { exact measurable_set_region_between hf hg hs },
end

/-- The volume of the region between two almost everywhere measurable functions on a measurable set
    can be represented as a Lebesgue integral. -/
theorem volume_region_between_eq_lintegral [sigma_finite Œº]
  (hf : ae_measurable f (Œº.restrict s)) (hg : ae_measurable g (Œº.restrict s))
  (hs : measurable_set s) :
  Œº.prod volume (region_between f g s) = ‚à´‚Åª y in s, ennreal.of_real ((g - f) y) ‚àÇŒº :=
begin
  have h‚ÇÅ : (Œª y, ennreal.of_real ((g - f) y))
          =·µê[Œº.restrict s]
              Œª y, ennreal.of_real ((ae_measurable.mk g hg - ae_measurable.mk f hf) y) :=
    (hg.ae_eq_mk.sub hf.ae_eq_mk).fun_comp _,
  have h‚ÇÇ : (Œº.restrict s).prod volume (region_between f g s) =
    (Œº.restrict s).prod volume (region_between (ae_measurable.mk f hf) (ae_measurable.mk g hg) s),
  { apply measure_congr,
    apply eventually_eq.rfl.inter,
    exact
      ((ae_eq_comp' measurable_fst hf.ae_eq_mk measure.prod_fst_absolutely_continuous).comp‚ÇÇ _
        eventually_eq.rfl).inter
      (eventually_eq.rfl.comp‚ÇÇ _
        (ae_eq_comp' measurable_fst hg.ae_eq_mk measure.prod_fst_absolutely_continuous)) },
  rw [lintegral_congr_ae h‚ÇÅ,
      ‚Üê volume_region_between_eq_lintegral' hf.measurable_mk hg.measurable_mk hs],
  convert h‚ÇÇ using 1,
  { rw measure.restrict_prod_eq_prod_univ,
    exacts [ (measure.restrict_eq_self' (hs.prod measurable_set.univ)
      (region_between_subset f g s)).symm, hs], },
  { rw measure.restrict_prod_eq_prod_univ,
    exacts [(measure.restrict_eq_self' (hs.prod measurable_set.univ)
      (region_between_subset (ae_measurable.mk f hf) (ae_measurable.mk g hg) s)).symm, hs] },
end


theorem volume_region_between_eq_integral' [sigma_finite Œº]
  (f_int : integrable_on f s Œº) (g_int : integrable_on g s Œº)
  (hs : measurable_set s) (hfg : f ‚â§·µê[Œº.restrict s] g ) :
  Œº.prod volume (region_between f g s) = ennreal.of_real (‚à´ y in s, (g - f) y ‚àÇŒº) :=
begin
  have h : g - f =·µê[Œº.restrict s] (Œª x, real.to_nnreal (g x - f x)),
    from hfg.mono (Œª x hx, (real.coe_to_nnreal _ $ sub_nonneg.2 hx).symm),
  rw [volume_region_between_eq_lintegral f_int.ae_measurable g_int.ae_measurable hs,
    integral_congr_ae h, lintegral_congr_ae,
    lintegral_coe_eq_integral _ ((integrable_congr h).mp (g_int.sub f_int))],
  simpa only,
end

/-- If two functions are integrable on a measurable set, and one function is less than
    or equal to the other on that set, then the volume of the region
    between the two functions can be represented as an integral. -/
theorem volume_region_between_eq_integral [sigma_finite Œº]
  (f_int : integrable_on f s Œº) (g_int : integrable_on g s Œº)
  (hs : measurable_set s) (hfg : ‚àÄ x ‚àà s, f x ‚â§ g x) :
  Œº.prod volume (region_between f g s) = ennreal.of_real (‚à´ y in s, (g - f) y ‚àÇŒº) :=
volume_region_between_eq_integral' f_int g_int hs
  ((ae_restrict_iff' hs).mpr (eventually_of_forall hfg))

end region_between

/-
section vitali

def vitali_aux_h (x : ‚Ñù) (h : x ‚àà Icc (0:‚Ñù) 1) :
  ‚àÉ y ‚àà Icc (0:‚Ñù) 1, ‚àÉ q:‚Ñö, ‚Üëq = x - y :=
‚ü®x, h, 0, by simp‚ü©

def vitali_aux (x : ‚Ñù) (h : x ‚àà Icc (0:‚Ñù) 1) : ‚Ñù :=
classical.some (vitali_aux_h x h)

theorem vitali_aux_mem (x : ‚Ñù) (h : x ‚àà Icc (0:‚Ñù) 1) : vitali_aux x h ‚àà Icc (0:‚Ñù) 1 :=
Exists.fst (classical.some_spec (vitali_aux_h x h):_)

theorem vitali_aux_rel (x : ‚Ñù) (h : x ‚àà Icc (0:‚Ñù) 1) :
 ‚àÉ q:‚Ñö, ‚Üëq = x - vitali_aux x h :=
Exists.snd (classical.some_spec (vitali_aux_h x h):_)

def vitali : set ‚Ñù := {x | ‚àÉ h, x = vitali_aux x h}

theorem vitali_nonmeasurable : ¬¨ null_measurable_set measure_space.Œº vitali :=
sorry

end vitali
-/
