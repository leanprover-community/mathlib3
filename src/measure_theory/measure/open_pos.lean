/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import measure_theory.measure.measure_space

/-!
# Measures positive on nonempty opens

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define a typeclass for measures that are positive on nonempty opens, see
`measure_theory.measure.is_open_pos_measure`. Examples include (additive) Haar measures, as well as
measures that have positive density with respect to a Haar measure. We also prove some basic facts
about these measures.

-/

open_locale topology ennreal measure_theory
open set function filter

namespace measure_theory

namespace measure

section basic

variables {X Y : Type*} [topological_space X] {m : measurable_space X}
  [topological_space Y] [t2_space Y] (Î¼ Î½ : measure X)

/-- A measure is said to be `is_open_pos_measure` if it is positive on nonempty open sets. -/
class is_open_pos_measure : Prop :=
(open_pos : âˆ€ (U : set X), is_open U â†’ U.nonempty â†’ Î¼ U â‰  0)

variables [is_open_pos_measure Î¼] {s U : set X} {x : X}

lemma _root_.is_open.measure_ne_zero (hU : is_open U) (hne : U.nonempty) : Î¼ U â‰  0 :=
is_open_pos_measure.open_pos U hU hne

lemma _root_.is_open.measure_pos (hU : is_open U) (hne : U.nonempty) : 0 < Î¼ U :=
(hU.measure_ne_zero Î¼ hne).bot_lt

lemma _root_.is_open.measure_pos_iff (hU : is_open U) : 0 < Î¼ U â†” U.nonempty :=
âŸ¨Î» h, nonempty_iff_ne_empty.2 $ Î» he, h.ne' $ he.symm â–¸ measure_empty, hU.measure_pos Î¼âŸ©

lemma _root_.is_open.measure_eq_zero_iff (hU : is_open U) : Î¼ U = 0 â†” U = âˆ… :=
by simpa only [not_lt, nonpos_iff_eq_zero, not_nonempty_iff_eq_empty]
  using not_congr (hU.measure_pos_iff Î¼)

lemma measure_pos_of_nonempty_interior (h : (interior s).nonempty) : 0 < Î¼ s :=
(is_open_interior.measure_pos Î¼ h).trans_le (measure_mono interior_subset)

lemma measure_pos_of_mem_nhds (h : s âˆˆ ð“ x) : 0 < Î¼ s :=
measure_pos_of_nonempty_interior _ âŸ¨x, mem_interior_iff_mem_nhds.2 hâŸ©

lemma is_open_pos_measure_smul {c : â„â‰¥0âˆž} (h : c â‰  0) : is_open_pos_measure (c â€¢ Î¼) :=
âŸ¨Î» U Uo Une, mul_ne_zero h (Uo.measure_ne_zero Î¼ Une)âŸ©

variables {Î¼ Î½}

protected lemma absolutely_continuous.is_open_pos_measure (h : Î¼ â‰ª Î½) :
  is_open_pos_measure Î½ :=
âŸ¨Î» U ho hne hâ‚€, ho.measure_ne_zero Î¼ hne (h hâ‚€)âŸ©

lemma _root_.has_le.le.is_open_pos_measure (h : Î¼ â‰¤ Î½) :
  is_open_pos_measure Î½ :=
h.absolutely_continuous.is_open_pos_measure

lemma _root_.is_open.eq_empty_of_measure_zero (hU : is_open U) (hâ‚€ : Î¼ U = 0) :
  U = âˆ… :=
(hU.measure_eq_zero_iff Î¼).mp hâ‚€

lemma interior_eq_empty_of_null (hs : Î¼ s = 0) : interior s = âˆ… :=
is_open_interior.eq_empty_of_measure_zero $ measure_mono_null interior_subset hs

/-- If two functions are a.e. equal on an open set and are continuous on this set, then they are
equal on this set. -/
lemma eq_on_open_of_ae_eq {f g : X â†’ Y} (h : f =áµ[Î¼.restrict U] g) (hU : is_open U)
  (hf : continuous_on f U) (hg : continuous_on g U) :
  eq_on f g U :=
begin
  replace h := ae_imp_of_ae_restrict h,
  simp only [eventually_eq, ae_iff, not_imp] at h,
  have : is_open (U âˆ© {a | f a â‰  g a}),
  { refine is_open_iff_mem_nhds.mpr (Î» a ha, inter_mem (hU.mem_nhds ha.1) _),
    rcases ha with âŸ¨ha : a âˆˆ U, ha' : (f a, g a) âˆˆ (diagonal Y)á¶œâŸ©,
    exact (hf.continuous_at (hU.mem_nhds ha)).prod_mk_nhds (hg.continuous_at (hU.mem_nhds ha))
      (is_closed_diagonal.is_open_compl.mem_nhds ha') },
  replace := (this.eq_empty_of_measure_zero h).le,
  exact Î» x hx, not_not.1 (Î» h, this âŸ¨hx, hâŸ©)
end

/-- If two continuous functions are a.e. equal, then they are equal. -/
lemma eq_of_ae_eq {f g : X â†’ Y} (h : f =áµ[Î¼] g) (hf : continuous f) (hg : continuous g) : f = g :=
suffices eq_on f g univ, from funext (Î» x, this trivial),
eq_on_open_of_ae_eq (ae_restrict_of_ae h) is_open_univ hf.continuous_on hg.continuous_on

lemma eq_on_of_ae_eq {f g : X â†’ Y} (h : f =áµ[Î¼.restrict s] g) (hf : continuous_on f s)
  (hg : continuous_on g s) (hU : s âŠ† closure (interior s)) :
  eq_on f g s :=
have interior s âŠ† s, from interior_subset,
(eq_on_open_of_ae_eq (ae_restrict_of_ae_restrict_of_subset this h) is_open_interior
  (hf.mono this) (hg.mono this)).of_subset_closure hf hg this hU

variable (Î¼)

lemma _root_.continuous.ae_eq_iff_eq {f g : X â†’ Y} (hf : continuous f) (hg : continuous g) :
  f =áµ[Î¼] g â†” f = g :=
âŸ¨Î» h, eq_of_ae_eq h hf hg, Î» h, h â–¸ eventually_eq.rflâŸ©

end basic

section linear_order

variables {X Y : Type*} [topological_space X] [linear_order X] [order_topology X]
  {m : measurable_space X} [topological_space Y] [t2_space Y] (Î¼ : measure X)
  [is_open_pos_measure Î¼]

lemma measure_Ioi_pos [no_max_order X] (a : X) : 0 < Î¼ (Ioi a) :=
is_open_Ioi.measure_pos Î¼ nonempty_Ioi

lemma measure_Iio_pos [no_min_order X] (a : X) : 0 < Î¼ (Iio a) :=
is_open_Iio.measure_pos Î¼ nonempty_Iio

lemma measure_Ioo_pos [densely_ordered X] {a b : X} : 0 < Î¼ (Ioo a b) â†” a < b :=
(is_open_Ioo.measure_pos_iff Î¼).trans nonempty_Ioo

lemma measure_Ioo_eq_zero [densely_ordered X] {a b : X} : Î¼ (Ioo a b) = 0 â†” b â‰¤ a :=
(is_open_Ioo.measure_eq_zero_iff Î¼).trans (Ioo_eq_empty_iff.trans not_lt)

lemma eq_on_Ioo_of_ae_eq {a b : X} {f g : X â†’ Y} (hfg : f =áµ[Î¼.restrict (Ioo a b)] g)
  (hf : continuous_on f (Ioo a b)) (hg : continuous_on g (Ioo a b)) : eq_on f g (Ioo a b) :=
eq_on_of_ae_eq hfg hf hg Ioo_subset_closure_interior

lemma eq_on_Ioc_of_ae_eq [densely_ordered X] {a b : X} {f g : X â†’ Y}
  (hfg : f =áµ[Î¼.restrict (Ioc a b)] g) (hf : continuous_on f (Ioc a b))
  (hg : continuous_on g (Ioc a b)) : eq_on f g (Ioc a b) :=
eq_on_of_ae_eq hfg hf hg (Ioc_subset_closure_interior _ _)

lemma eq_on_Ico_of_ae_eq [densely_ordered X] {a b : X} {f g : X â†’ Y}
  (hfg : f =áµ[Î¼.restrict (Ico a b)] g) (hf : continuous_on f (Ico a b))
  (hg : continuous_on g (Ico a b)) : eq_on f g (Ico a b) :=
eq_on_of_ae_eq hfg hf hg (Ico_subset_closure_interior _ _)

lemma eq_on_Icc_of_ae_eq [densely_ordered X] {a b : X} (hne : a â‰  b) {f g : X â†’ Y}
  (hfg : f =áµ[Î¼.restrict (Icc a b)] g) (hf : continuous_on f (Icc a b))
  (hg : continuous_on g (Icc a b)) : eq_on f g (Icc a b) :=
eq_on_of_ae_eq hfg hf hg (closure_interior_Icc hne).symm.subset

end linear_order

end measure

end measure_theory

open measure_theory measure_theory.measure

namespace metric

variables {X : Type*} [pseudo_metric_space X] {m : measurable_space X} (Î¼ : measure X)
  [is_open_pos_measure Î¼]

lemma measure_ball_pos (x : X) {r : â„} (hr : 0 < r) : 0 < Î¼ (ball x r) :=
is_open_ball.measure_pos Î¼ (nonempty_ball.2 hr)

lemma measure_closed_ball_pos (x : X) {r : â„} (hr : 0 < r) :
  0 < Î¼ (closed_ball x r) :=
(measure_ball_pos Î¼ x hr).trans_le (measure_mono ball_subset_closed_ball)

end metric

namespace emetric

variables {X : Type*} [pseudo_emetric_space X] {m : measurable_space X} (Î¼ : measure X)
  [is_open_pos_measure Î¼]

lemma measure_ball_pos (x : X) {r : â„â‰¥0âˆž} (hr : r â‰  0) : 0 < Î¼ (ball x r) :=
is_open_ball.measure_pos Î¼ âŸ¨x, mem_ball_self hr.bot_ltâŸ©

lemma measure_closed_ball_pos (x : X) {r : â„â‰¥0âˆž} (hr : r â‰  0) :
  0 < Î¼ (closed_ball x r) :=
(measure_ball_pos Î¼ x hr).trans_le (measure_mono ball_subset_closed_ball)

end emetric
