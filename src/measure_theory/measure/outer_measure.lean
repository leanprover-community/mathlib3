/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import analysis.specific_limits
import measure_theory.pi_system
import data.matrix.notation
import topology.algebra.infinite_sum

/-!
# Outer Measures

An outer measure is a function `Œº : set Œ± ‚Üí ‚Ñù‚â•0‚àû`, from the powerset of a type to the extended
nonnegative real numbers that satisfies the following conditions:
1. `Œº ‚àÖ = 0`;
2. `Œº` is monotone;
3. `Œº` is countably subadditive. This means that the outer measure of a countable union is at most
   the sum of the outer measure on the individual sets.

Note that we do not need `Œ±` to be measurable to define an outer measure.

The outer measures on a type `Œ±` form a complete lattice.

Given an arbitrary function `m : set Œ± ‚Üí ‚Ñù‚â•0‚àû` that sends `‚àÖ` to `0` we can define an outer
measure on `Œ±` that on `s` is defined to be the infimum of `‚àë·µ¢, m (s·µ¢)` for all collections of sets
`s·µ¢` that cover `s`. This is the unique maximal outer measure that is at most the given function.
We also define this for functions `m` defined on a subset of `set Œ±`, by treating the function as
having value `‚àû` outside its domain.

Given an outer measure `m`, the Carath√©odory-measurable sets are the sets `s` such that
for all sets `t` we have `m t = m (t ‚à© s) + m (t \ s)`. This forms a measurable space.

## Main definitions and statements

* `outer_measure.bounded_by` is the greatest outer measure that is at most the given function.
  If you know that the given functions sends `‚àÖ` to `0`, then `outer_measure.of_function` is a
  special case.
* `caratheodory` is the Carath√©odory-measurable space of an outer measure.
* `Inf_eq_of_function_Inf_gen` is a characterization of the infimum of outer measures.
* `induced_outer_measure` is the measure induced by a function on a subset of `set Œ±`

## References

* <https://en.wikipedia.org/wiki/Outer_measure>
* <https://en.wikipedia.org/wiki/Carath%C3%A9odory%27s_criterion>

## Tags

outer measure, Carath√©odory-measurable, Carath√©odory's criterion
-/

noncomputable theory

open set finset function filter encodable
open_locale classical big_operators nnreal topological_space ennreal

namespace measure_theory

/-- An outer measure is a countably subadditive monotone function that sends `‚àÖ` to `0`. -/
structure outer_measure (Œ± : Type*) :=
(measure_of : set Œ± ‚Üí ‚Ñù‚â•0‚àû)
(empty : measure_of ‚àÖ = 0)
(mono : ‚àÄ{s‚ÇÅ s‚ÇÇ}, s‚ÇÅ ‚äÜ s‚ÇÇ ‚Üí measure_of s‚ÇÅ ‚â§ measure_of s‚ÇÇ)
(Union_nat : ‚àÄ(s:‚Ñï ‚Üí set Œ±), measure_of (‚ãÉi, s i) ‚â§ ‚àë'i, measure_of (s i))

namespace outer_measure

section basic

variables {Œ± : Type*} {Œ≤ : Type*} {ms : set (outer_measure Œ±)} {m : outer_measure Œ±}

instance : has_coe_to_fun (outer_measure Œ±) (Œª _, set Œ± ‚Üí ‚Ñù‚â•0‚àû) := ‚ü®Œª m, m.measure_of‚ü©

@[simp] lemma measure_of_eq_coe (m : outer_measure Œ±) : m.measure_of = m := rfl

@[simp] theorem empty' (m : outer_measure Œ±) : m ‚àÖ = 0 := m.empty

theorem mono' (m : outer_measure Œ±) {s‚ÇÅ s‚ÇÇ}
  (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : m s‚ÇÅ ‚â§ m s‚ÇÇ := m.mono h

protected theorem Union (m : outer_measure Œ±)
  {Œ≤} [encodable Œ≤] (s : Œ≤ ‚Üí set Œ±) :
  m (‚ãÉi, s i) ‚â§ ‚àë'i, m (s i) :=
rel_supr_tsum m m.empty (‚â§) m.Union_nat s

lemma Union_null (m : outer_measure Œ±)
  {Œ≤} [encodable Œ≤] {s : Œ≤ ‚Üí set Œ±} (h : ‚àÄ i, m (s i) = 0) : m (‚ãÉi, s i) = 0 :=
by simpa [h] using m.Union s

protected lemma Union_finset (m : outer_measure Œ±) (s : Œ≤ ‚Üí set Œ±) (t : finset Œ≤) :
  m (‚ãÉi ‚àà t, s i) ‚â§ ‚àë i in t, m (s i) :=
rel_supr_sum m m.empty (‚â§) m.Union_nat s t

protected lemma union (m : outer_measure Œ±) (s‚ÇÅ s‚ÇÇ : set Œ±) :
  m (s‚ÇÅ ‚à™ s‚ÇÇ) ‚â§ m s‚ÇÅ + m s‚ÇÇ :=
rel_sup_add m m.empty (‚â§) m.Union_nat s‚ÇÅ s‚ÇÇ

/-- If `s : Œπ ‚Üí set Œ±` is a sequence of sets, `S = ‚ãÉ n, s n`, and `m (S \ s n)` tends to zero along
some nontrivial filter (usually `at_top` on `Œ± = ‚Ñï`), then `m S = ‚®Ü n, m (s n)`. -/
lemma Union_of_tendsto_zero {Œπ} (m : outer_measure Œ±) {s : Œπ ‚Üí set Œ±}
  (l : filter Œπ) [ne_bot l] (h0 : tendsto (Œª k, m ((‚ãÉ n, s n) \ s k)) l (ùìù 0)) :
  m (‚ãÉ n, s n) = ‚®Ü n, m (s n) :=
begin
  set S := ‚ãÉ n, s n,
  set M := ‚®Ü n, m (s n),
  have hsS : ‚àÄ {k}, s k ‚äÜ S, from Œª k, subset_Union _ _,
  refine le_antisymm _ (supr_le $ Œª n, m.mono hsS),
  have A : ‚àÄ k, m S ‚â§ M + m (S \ s k), from Œª k,
  calc m S = m (s k ‚à™ S \ s k) : by rw [union_diff_self, union_eq_self_of_subset_left hsS]
  ... ‚â§ m (s k) + m (S \ s k) : m.union _ _
  ... ‚â§ M + m (S \ s k) : add_le_add_right (le_supr _ k) _,
  have B : tendsto (Œª k, M + m (S \ s k)) l (ùìù (M + 0)), from tendsto_const_nhds.add h0,
  rw add_zero at B,
  exact ge_of_tendsto' B A
end

/-- If `s : ‚Ñï ‚Üí set Œ±` is a monotone sequence of sets such that `‚àë' k, m (s (k + 1) \ s k) ‚â† ‚àû`,
then `m (‚ãÉ n, s n) = ‚®Ü n, m (s n)`. -/
lemma Union_nat_of_monotone_of_tsum_ne_top (m : outer_measure Œ±) {s : ‚Ñï ‚Üí set Œ±}
  (h_mono : ‚àÄ n, s n ‚äÜ s (n + 1)) (h0 : ‚àë' k, m (s (k + 1) \ s k) ‚â† ‚àû) :
  m (‚ãÉ n, s n) = ‚®Ü n, m (s n) :=
begin
  refine m.Union_of_tendsto_zero at_top _,
  refine tendsto_nhds_bot_mono' (ennreal.tendsto_sum_nat_add _ h0) (Œª n, _),
  refine (m.mono _).trans (m.Union _),
  /- Current goal: `(‚ãÉ k, s k) \ s n ‚äÜ ‚ãÉ k, s (k + n + 1) \ s (k + n)` -/
  have h' : monotone s := @monotone_nat_of_le_succ (set Œ±) _ _ h_mono,
  simp only [diff_subset_iff, Union_subset_iff],
  intros i x hx,
  rcases nat.find_x ‚ü®i, hx‚ü© with ‚ü®j, hj, hlt‚ü©, clear hx i,
  cases le_or_lt j n with hjn hnj, { exact or.inl (h' hjn hj) },
  have : j - (n + 1) + n + 1 = j,
    by rw [add_assoc, tsub_add_cancel_of_le hnj.nat_succ_le],
  refine or.inr (mem_Union.2 ‚ü®j - (n + 1), _, hlt _ _‚ü©),
  { rwa this },
  { rw [‚Üê nat.succ_le_iff, nat.succ_eq_add_one, this] }
end

lemma le_inter_add_diff {m : outer_measure Œ±} {t : set Œ±} (s : set Œ±) :
  m t ‚â§ m (t ‚à© s) + m (t \ s) :=
by { convert m.union _ _, rw inter_union_diff t s }

lemma diff_null (m : outer_measure Œ±) (s : set Œ±) {t : set Œ±} (ht : m t = 0) :
  m (s \ t) = m s :=
begin
  refine le_antisymm (m.mono $ diff_subset _ _) _,
  calc m s ‚â§ m (s ‚à© t) + m (s \ t) : le_inter_add_diff _
       ... ‚â§ m t + m (s \ t)       : add_le_add_right (m.mono $ inter_subset_right _ _) _
       ... = m (s \ t)             : by rw [ht, zero_add]
end

lemma union_null (m : outer_measure Œ±) {s‚ÇÅ s‚ÇÇ : set Œ±}
  (h‚ÇÅ : m s‚ÇÅ = 0) (h‚ÇÇ : m s‚ÇÇ = 0) : m (s‚ÇÅ ‚à™ s‚ÇÇ) = 0 :=
by simpa [h‚ÇÅ, h‚ÇÇ] using m.union s‚ÇÅ s‚ÇÇ

lemma coe_fn_injective : injective (Œª (Œº : outer_measure Œ±) (s : set Œ±), Œº s) :=
Œª Œº‚ÇÅ Œº‚ÇÇ h, by { cases Œº‚ÇÅ, cases Œº‚ÇÇ, congr, exact h }

@[ext] lemma ext {Œº‚ÇÅ Œº‚ÇÇ : outer_measure Œ±} (h : ‚àÄ s, Œº‚ÇÅ s = Œº‚ÇÇ s) : Œº‚ÇÅ = Œº‚ÇÇ :=
coe_fn_injective $ funext h

/-- A version of `measure_theory.outer_measure.ext` that assumes `Œº‚ÇÅ s = Œº‚ÇÇ s` on all *nonempty*
sets `s`, and gets `Œº‚ÇÅ ‚àÖ = Œº‚ÇÇ ‚àÖ` from `measure_theory.outer_measure.empty'`. -/
lemma ext_nonempty {Œº‚ÇÅ Œº‚ÇÇ : outer_measure Œ±} (h : ‚àÄ s : set Œ±, s.nonempty ‚Üí Œº‚ÇÅ s = Œº‚ÇÇ s) :
  Œº‚ÇÅ = Œº‚ÇÇ :=
ext $ Œª s, s.eq_empty_or_nonempty.elim (Œª he, by rw [he, empty', empty']) (h s)

instance : has_zero (outer_measure Œ±) :=
‚ü®{ measure_of := Œª_, 0,
   empty      := rfl,
   mono       := assume _ _ _, le_refl 0,
   Union_nat  := assume s, zero_le _ }‚ü©

@[simp] theorem coe_zero : ‚áë(0 : outer_measure Œ±) = 0 := rfl

instance : inhabited (outer_measure Œ±) := ‚ü®0‚ü©

instance : has_add (outer_measure Œ±) :=
‚ü®Œªm‚ÇÅ m‚ÇÇ,
  { measure_of := Œªs, m‚ÇÅ s + m‚ÇÇ s,
    empty      := show m‚ÇÅ ‚àÖ + m‚ÇÇ ‚àÖ = 0, by simp [outer_measure.empty],
    mono       := assume s‚ÇÅ s‚ÇÇ h, add_le_add (m‚ÇÅ.mono h) (m‚ÇÇ.mono h),
    Union_nat  := assume s,
      calc m‚ÇÅ (‚ãÉi, s i) + m‚ÇÇ (‚ãÉi, s i) ‚â§
          (‚àë'i, m‚ÇÅ (s i)) + (‚àë'i, m‚ÇÇ (s i)) :
          add_le_add (m‚ÇÅ.Union_nat s) (m‚ÇÇ.Union_nat s)
        ... = _ : ennreal.tsum_add.symm}‚ü©

@[simp] theorem coe_add (m‚ÇÅ m‚ÇÇ : outer_measure Œ±) : ‚áë(m‚ÇÅ + m‚ÇÇ) = m‚ÇÅ + m‚ÇÇ := rfl

theorem add_apply (m‚ÇÅ m‚ÇÇ : outer_measure Œ±) (s : set Œ±) : (m‚ÇÅ + m‚ÇÇ) s = m‚ÇÅ s + m‚ÇÇ s := rfl

instance add_comm_monoid : add_comm_monoid (outer_measure Œ±) :=
{ zero      := 0,
  add       := (+),
  .. injective.add_comm_monoid (show outer_measure Œ± ‚Üí set Œ± ‚Üí ‚Ñù‚â•0‚àû, from coe_fn)
    coe_fn_injective rfl (Œª _ _, rfl) }

instance : has_scalar ‚Ñù‚â•0‚àû (outer_measure Œ±) :=
‚ü®Œª c m,
  { measure_of := Œª s, c * m s,
    empty      := by simp,
    mono       := Œª s t h, ennreal.mul_left_mono $ m.mono h,
    Union_nat  := Œª s, by { rw [ennreal.tsum_mul_left], exact ennreal.mul_left_mono (m.Union _) } }‚ü©

@[simp] lemma coe_smul (c : ‚Ñù‚â•0‚àû) (m : outer_measure Œ±) : ‚áë(c ‚Ä¢ m) = c ‚Ä¢ m := rfl

lemma smul_apply (c : ‚Ñù‚â•0‚àû) (m : outer_measure Œ±) (s : set Œ±) : (c ‚Ä¢ m) s = c * m s := rfl

instance : module ‚Ñù‚â•0‚àû (outer_measure Œ±) :=
{ smul := (‚Ä¢),
  .. injective.module ‚Ñù‚â•0‚àû ‚ü®show outer_measure Œ± ‚Üí set Œ± ‚Üí ‚Ñù‚â•0‚àû, from coe_fn, coe_zero,
    coe_add‚ü© coe_fn_injective coe_smul }

instance : has_bot (outer_measure Œ±) := ‚ü®0‚ü©

instance outer_measure.order_bot : order_bot (outer_measure Œ±) :=
{ le          := Œªm‚ÇÅ m‚ÇÇ, ‚àÄs, m‚ÇÅ s ‚â§ m‚ÇÇ s,
  bot         := 0,
  le_refl     := assume a s, le_refl _,
  le_trans    := assume a b c hab hbc s, le_trans (hab s) (hbc s),
  le_antisymm := assume a b hab hba, ext $ assume s, le_antisymm (hab s) (hba s),
  bot_le      := assume a s, zero_le _ }

section supremum

instance : has_Sup (outer_measure Œ±) :=
‚ü®Œªms, {
  measure_of := Œªs, ‚®Ü m ‚àà ms, (m : outer_measure Œ±) s,
  empty      := nonpos_iff_eq_zero.1 $ bsupr_le $ Œª m h, le_of_eq m.empty,
  mono       := assume s‚ÇÅ s‚ÇÇ hs, bsupr_le_bsupr $ assume m hm, m.mono hs,
  Union_nat  := assume f, bsupr_le $ assume m hm,
    calc m (‚ãÉi, f i) ‚â§ ‚àë' (i : ‚Ñï), m (f i) : m.Union_nat _
      ... ‚â§ ‚àë'i, (‚®Ü m ‚àà ms, (m : outer_measure Œ±) (f i)) :
        ennreal.tsum_le_tsum $ assume i, le_bsupr m hm }‚ü©

instance : complete_lattice (outer_measure Œ±) :=
{ .. outer_measure.order_bot, .. complete_lattice_of_Sup (outer_measure Œ±)
    (Œª ms, ‚ü®Œª m hm s, le_bsupr m hm, Œª m hm s, bsupr_le (Œª m' hm', hm hm' s)‚ü©) }

@[simp] theorem Sup_apply (ms : set (outer_measure Œ±)) (s : set Œ±) :
  (Sup ms) s = ‚®Ü m ‚àà ms, (m : outer_measure Œ±) s := rfl

@[simp] theorem supr_apply {Œπ} (f : Œπ ‚Üí outer_measure Œ±) (s : set Œ±) :
  (‚®Ü i : Œπ, f i) s = ‚®Ü i, f i s :=
by rw [supr, Sup_apply, supr_range, supr]

@[norm_cast] theorem coe_supr {Œπ} (f : Œπ ‚Üí outer_measure Œ±) :
  ‚áë(‚®Ü i, f i) = ‚®Ü i, f i :=
funext $ Œª s, by rw [supr_apply, _root_.supr_apply]

@[simp] theorem sup_apply (m‚ÇÅ m‚ÇÇ : outer_measure Œ±) (s : set Œ±) :
  (m‚ÇÅ ‚äî m‚ÇÇ) s = m‚ÇÅ s ‚äî m‚ÇÇ s :=
by have := supr_apply (Œª b, cond b m‚ÇÅ m‚ÇÇ) s;
  rwa [supr_bool_eq, supr_bool_eq] at this

theorem smul_supr {Œπ} (f : Œπ ‚Üí outer_measure Œ±) (c : ‚Ñù‚â•0‚àû) :
  c ‚Ä¢ (‚®Ü i, f i) = ‚®Ü i, c ‚Ä¢ f i :=
ext $ Œª s, by simp only [smul_apply, supr_apply, ennreal.mul_supr]

end supremum

@[mono] lemma mono'' {m‚ÇÅ m‚ÇÇ : outer_measure Œ±} {s‚ÇÅ s‚ÇÇ : set Œ±} (hm : m‚ÇÅ ‚â§ m‚ÇÇ) (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) :
  m‚ÇÅ s‚ÇÅ ‚â§ m‚ÇÇ s‚ÇÇ :=
(hm s‚ÇÅ).trans (m‚ÇÇ.mono hs)

/-- The pushforward of `m` along `f`. The outer measure on `s` is defined to be `m (f ‚Åª¬π' s)`. -/
def map {Œ≤} (f : Œ± ‚Üí Œ≤) : outer_measure Œ± ‚Üí‚Çó[‚Ñù‚â•0‚àû] outer_measure Œ≤ :=
{ to_fun := Œª m,
    { measure_of := Œªs, m (f ‚Åª¬π' s),
      empty := m.empty,
      mono := Œª s t h, m.mono (preimage_mono h),
      Union_nat := Œª s, by rw [preimage_Union]; exact
        m.Union_nat (Œª i, f ‚Åª¬π' s i) },
  map_add' := Œª m‚ÇÅ m‚ÇÇ, coe_fn_injective rfl,
  map_smul' := Œª c m, coe_fn_injective rfl }

@[simp] theorem map_apply {Œ≤} (f : Œ± ‚Üí Œ≤)
  (m : outer_measure Œ±) (s : set Œ≤) : map f m s = m (f ‚Åª¬π' s) := rfl

@[simp] theorem map_id (m : outer_measure Œ±) : map id m = m :=
ext $ Œª s, rfl

@[simp] theorem map_map {Œ≤ Œ≥} (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥)
  (m : outer_measure Œ±) : map g (map f m) = map (g ‚àò f) m :=
ext $ Œª s, rfl

@[mono] theorem map_mono {Œ≤} (f : Œ± ‚Üí Œ≤) : monotone (map f) :=
Œª m m' h s, h _

@[simp] theorem map_sup {Œ≤} (f : Œ± ‚Üí Œ≤) (m m' : outer_measure Œ±) :
  map f (m ‚äî m') = map f m ‚äî map f m' :=
ext $ Œª s, by simp only [map_apply, sup_apply]

@[simp] theorem map_supr {Œ≤ Œπ} (f : Œ± ‚Üí Œ≤) (m : Œπ ‚Üí outer_measure Œ±) :
  map f (‚®Ü i, m i) = ‚®Ü i, map f (m i) :=
ext $ Œª s, by simp only [map_apply, supr_apply]

instance : functor outer_measure := {map := Œª Œ± Œ≤ f, map f}

instance : is_lawful_functor outer_measure :=
{ id_map := Œª Œ±, map_id,
  comp_map := Œª Œ± Œ≤ Œ≥ f g m, (map_map f g m).symm }

/-- The dirac outer measure. -/
def dirac (a : Œ±) : outer_measure Œ± :=
{ measure_of := Œªs, indicator s (Œª _, 1) a,
  empty := by simp,
  mono := Œª s t h, indicator_le_indicator_of_subset h (Œª _, zero_le _) a,
  Union_nat := Œª s,
    if hs : a ‚àà ‚ãÉ n, s n then let ‚ü®i, hi‚ü© := mem_Union.1 hs in
      calc indicator (‚ãÉ n, s n) (Œª _, (1 : ‚Ñù‚â•0‚àû)) a = 1 : indicator_of_mem hs _
      ... = indicator (s i) (Œª _, 1) a : (indicator_of_mem hi _).symm
      ... ‚â§ ‚àë' n, indicator (s n) (Œª _, 1) a : ennreal.le_tsum _
    else by simp only [indicator_of_not_mem hs, zero_le]}

@[simp] theorem dirac_apply (a : Œ±) (s : set Œ±) :
  dirac a s = indicator s (Œª _, 1) a := rfl

/-- The sum of an (arbitrary) collection of outer measures. -/
def sum {Œπ} (f : Œπ ‚Üí outer_measure Œ±) : outer_measure Œ± :=
{ measure_of := Œªs, ‚àë' i, f i s,
  empty := by simp,
  mono := Œª s t h, ennreal.tsum_le_tsum (Œª i, (f i).mono' h),
  Union_nat := Œª s, by rw ennreal.tsum_comm; exact
    ennreal.tsum_le_tsum (Œª i, (f i).Union_nat _) }

@[simp] theorem sum_apply {Œπ} (f : Œπ ‚Üí outer_measure Œ±) (s : set Œ±) :
  sum f s = ‚àë' i, f i s := rfl

theorem smul_dirac_apply (a : ‚Ñù‚â•0‚àû) (b : Œ±) (s : set Œ±) :
  (a ‚Ä¢ dirac b) s = indicator s (Œª _, a) b :=
by simp only [smul_apply, dirac_apply, ‚Üê indicator_mul_right _ (Œª _, a), mul_one]

/-- Pullback of an `outer_measure`: `comap f Œº s = Œº (f '' s)`. -/
def comap {Œ≤} (f : Œ± ‚Üí Œ≤) : outer_measure Œ≤ ‚Üí‚Çó[‚Ñù‚â•0‚àû] outer_measure Œ± :=
{ to_fun := Œª m,
    { measure_of := Œª s, m (f '' s),
      empty := by simp,
      mono := Œª s t h, m.mono $ image_subset f h,
      Union_nat := Œª s, by { rw [image_Union], apply m.Union_nat } },
  map_add' := Œª m‚ÇÅ m‚ÇÇ, rfl,
  map_smul' := Œª c m, rfl }

@[simp] lemma comap_apply {Œ≤} (f : Œ± ‚Üí Œ≤) (m : outer_measure Œ≤) (s : set Œ±) :
  comap f m s = m (f '' s) :=
rfl

@[mono] lemma comap_mono {Œ≤} (f : Œ± ‚Üí Œ≤) :
  monotone (comap f) :=
Œª m m' h s, h _

@[simp] theorem comap_supr {Œ≤ Œπ} (f : Œ± ‚Üí Œ≤) (m : Œπ ‚Üí outer_measure Œ≤) :
  comap f (‚®Ü i, m i) = ‚®Ü i, comap f (m i) :=
ext $ Œª s, by simp only [comap_apply, supr_apply]

/-- Restrict an `outer_measure` to a set. -/
def restrict (s : set Œ±) : outer_measure Œ± ‚Üí‚Çó[‚Ñù‚â•0‚àû] outer_measure Œ± :=
(map coe).comp (comap (coe : s ‚Üí Œ±))

@[simp] lemma restrict_apply (s t : set Œ±) (m : outer_measure Œ±) :
  restrict s m t = m (t ‚à© s) :=
by simp [restrict]

@[mono] lemma restrict_mono {s t : set Œ±} (h : s ‚äÜ t) {m m' : outer_measure Œ±} (hm : m ‚â§ m') :
  restrict s m ‚â§ restrict t m' :=
Œª u, by { simp only [restrict_apply], exact (hm _).trans (m'.mono $ inter_subset_inter_right _ h) }

@[simp] lemma restrict_univ (m : outer_measure Œ±) : restrict univ m = m := ext $ Œª s, by simp

@[simp] lemma restrict_empty (m : outer_measure Œ±) : restrict ‚àÖ m = 0 := ext $ Œª s, by simp

@[simp] lemma restrict_supr {Œπ} (s : set Œ±) (m : Œπ ‚Üí outer_measure Œ±) :
  restrict s (‚®Ü i, m i) = ‚®Ü i, restrict s (m i) :=
by simp [restrict]

lemma map_comap {Œ≤} (f : Œ± ‚Üí Œ≤) (m : outer_measure Œ≤) :
  map f (comap f m) = restrict (range f) m :=
ext $ Œª s, congr_arg m $ by simp only [image_preimage_eq_inter_range, subtype.range_coe]

lemma map_comap_le {Œ≤} (f : Œ± ‚Üí Œ≤) (m : outer_measure Œ≤) :
  map f (comap f m) ‚â§ m :=
Œª s, m.mono $ image_preimage_subset _ _

lemma restrict_le_self (m : outer_measure Œ±) (s : set Œ±) :
  restrict s m ‚â§ m :=
map_comap_le _ _

@[simp] lemma map_le_restrict_range {Œ≤} {ma : outer_measure Œ±} {mb : outer_measure Œ≤} {f : Œ± ‚Üí Œ≤} :
  map f ma ‚â§ restrict (range f) mb ‚Üî map f ma ‚â§ mb :=
‚ü®Œª h, h.trans (restrict_le_self _ _), Œª h s, by simpa using h (s ‚à© range f)‚ü©

lemma map_comap_of_surjective {Œ≤} {f : Œ± ‚Üí Œ≤} (hf : surjective f) (m : outer_measure Œ≤) :
  map f (comap f m) = m :=
ext $ Œª s, by rw [map_apply, comap_apply, hf.image_preimage]

lemma le_comap_map {Œ≤} (f : Œ± ‚Üí Œ≤) (m : outer_measure Œ±) :
  m ‚â§ comap f (map f m) :=
Œª s, m.mono $ subset_preimage_image _ _

lemma comap_map {Œ≤} {f : Œ± ‚Üí Œ≤} (hf : injective f) (m : outer_measure Œ±) :
  comap f (map f m) = m :=
ext $ Œª s, by rw [comap_apply, map_apply, hf.preimage_image]

@[simp] theorem top_apply {s : set Œ±} (h : s.nonempty) : (‚ä§ : outer_measure Œ±) s = ‚àû :=
let ‚ü®a, as‚ü© := h in
top_unique $ le_trans (by simp [smul_dirac_apply, as]) (le_bsupr (‚àû ‚Ä¢ dirac a) trivial)

theorem top_apply' (s : set Œ±) : (‚ä§ : outer_measure Œ±) s = ‚®Ö (h : s = ‚àÖ), 0 :=
s.eq_empty_or_nonempty.elim (Œª h, by simp [h]) (Œª h, by simp [h, h.ne_empty])

@[simp] theorem comap_top (f : Œ± ‚Üí Œ≤) : comap f ‚ä§ = ‚ä§ :=
ext_nonempty $ Œª s hs, by rw [comap_apply, top_apply hs, top_apply (hs.image _)]

theorem map_top (f : Œ± ‚Üí Œ≤) : map f ‚ä§ = restrict (range f) ‚ä§ :=
ext $ Œª s, by rw [map_apply, restrict_apply, ‚Üê image_preimage_eq_inter_range,
  top_apply', top_apply', set.image_eq_empty]

theorem map_top_of_surjective (f : Œ± ‚Üí Œ≤) (hf : surjective f) : map f ‚ä§ = ‚ä§ :=
by rw [map_top, hf.range_eq, restrict_univ]

end basic

section of_function
set_option eqn_compiler.zeta true

variables {Œ± : Type*} (m : set Œ± ‚Üí ‚Ñù‚â•0‚àû) (m_empty : m ‚àÖ = 0)
include m_empty

/-- Given any function `m` assigning measures to sets satisying `m ‚àÖ = 0`, there is
  a unique maximal outer measure `Œº` satisfying `Œº s ‚â§ m s` for all `s : set Œ±`. -/
protected def of_function : outer_measure Œ± :=
let Œº := Œªs, ‚®Ö{f : ‚Ñï ‚Üí set Œ±} (h : s ‚äÜ ‚ãÉi, f i), ‚àë'i, m (f i) in
{ measure_of := Œº,
  empty      := le_antisymm
    (infi_le_of_le (Œª_, ‚àÖ) $ infi_le_of_le (empty_subset _) $ by simp [m_empty])
    (zero_le _),
  mono       := assume s‚ÇÅ s‚ÇÇ hs, infi_le_infi $ assume f,
    infi_le_infi2 $ assume hb, ‚ü®subset.trans hs hb, le_refl _‚ü©,
  Union_nat := assume s, ennreal.le_of_forall_pos_le_add $ begin
    assume Œµ hŒµ (hb : ‚àë'i, Œº (s i) < ‚àû),
    rcases ennreal.exists_pos_sum_of_encodable (ennreal.coe_pos.2 hŒµ).ne' ‚Ñï with ‚ü®Œµ', hŒµ', hl‚ü©,
    refine le_trans _ (add_le_add_left (le_of_lt hl) _),
    rw ‚Üê ennreal.tsum_add,
    choose f hf using show
      ‚àÄi, ‚àÉf:‚Ñï ‚Üí set Œ±, s i ‚äÜ (‚ãÉi, f i) ‚àß ‚àë'i, m (f i) < Œº (s i) + Œµ' i,
    { intro,
      have : Œº (s i) < Œº (s i) + Œµ' i :=
        ennreal.lt_add_right
          (ne_top_of_le_ne_top hb.ne $ ennreal.le_tsum _)
          (by simpa using (hŒµ' i).ne'),
      simpa [Œº, infi_lt_iff] },
    refine le_trans _ (ennreal.tsum_le_tsum $ Œª i, le_of_lt (hf i).2),
    rw [‚Üê ennreal.tsum_prod, ‚Üê equiv.nat_prod_nat_equiv_nat.symm.tsum_eq],
    swap, {apply_instance},
    refine infi_le_of_le _ (infi_le _ _),
    exact Union_subset (Œª i, subset.trans (hf i).1 $
      Union_subset $ Œª j, subset.trans (by simp) $
      subset_Union _ $ equiv.nat_prod_nat_equiv_nat (i, j)),
  end }

lemma of_function_apply (s : set Œ±) :
  outer_measure.of_function m m_empty s =
  (‚®Ö (t : ‚Ñï ‚Üí set Œ±) (h : s ‚äÜ Union t), ‚àë' n, m (t n)) := rfl

variables {m m_empty}
theorem of_function_le (s : set Œ±) : outer_measure.of_function m m_empty s ‚â§ m s :=
let f : ‚Ñï ‚Üí set Œ± := Œªi, nat.cases_on i s (Œª _, ‚àÖ) in
infi_le_of_le f $ infi_le_of_le (subset_Union f 0) $ le_of_eq $
tsum_eq_single 0 $ by rintro (_|i); simp [f, m_empty]

theorem of_function_eq (s : set Œ±) (m_mono : ‚àÄ ‚¶Ét : set Œ±‚¶Ñ, s ‚äÜ t ‚Üí m s ‚â§ m t)
  (m_subadd : ‚àÄ (s : ‚Ñï ‚Üí set Œ±), m (‚ãÉi, s i) ‚â§ ‚àë'i, m (s i)) :
  outer_measure.of_function m m_empty s = m s :=
le_antisymm (of_function_le s) $ le_infi $ Œª f, le_infi $ Œª hf, le_trans (m_mono hf) (m_subadd f)

theorem le_of_function {Œº : outer_measure Œ±} :
  Œº ‚â§ outer_measure.of_function m m_empty ‚Üî ‚àÄ s, Œº s ‚â§ m s :=
‚ü®Œª H s, le_trans (H s) (of_function_le s),
 Œª H s, le_infi $ Œª f, le_infi $ Œª hs,
  le_trans (Œº.mono hs) $ le_trans (Œº.Union f) $
  ennreal.tsum_le_tsum $ Œª i, H _‚ü©

lemma is_greatest_of_function :
  is_greatest {Œº : outer_measure Œ± | ‚àÄ s, Œº s ‚â§ m s} (outer_measure.of_function m m_empty) :=
‚ü®Œª s, of_function_le _, Œª Œº, le_of_function.2‚ü©

lemma of_function_eq_Sup : outer_measure.of_function m m_empty = Sup {Œº | ‚àÄ s, Œº s ‚â§ m s} :=
(@is_greatest_of_function Œ± m m_empty).is_lub.Sup_eq.symm

/-- If `m u = ‚àû` for any set `u` that has nonempty intersection both with `s` and `t`, then
`Œº (s ‚à™ t) = Œº s + Œº t`, where `Œº = measure_theory.outer_measure.of_function m m_empty`.

E.g., if `Œ±` is an (e)metric space and `m u = ‚àû` on any set of diameter `‚â• r`, then this lemma
implies that `Œº (s ‚à™ t) = Œº s + Œº t` on any two sets such that `r ‚â§ edist x y` for all `x ‚àà s`
and `y ‚àà t`.  -/
lemma of_function_union_of_top_of_nonempty_inter {s t : set Œ±}
  (h : ‚àÄ u, (s ‚à© u).nonempty ‚Üí (t ‚à© u).nonempty ‚Üí m u = ‚àû) :
  outer_measure.of_function m m_empty (s ‚à™ t) =
    outer_measure.of_function m m_empty s + outer_measure.of_function m m_empty t :=
begin
  refine le_antisymm (outer_measure.union _ _ _) (le_infi $ Œª f, le_infi $ Œª hf, _),
  set Œº := outer_measure.of_function m m_empty,
  rcases em (‚àÉ i, (s ‚à© f i).nonempty ‚àß (t ‚à© f i).nonempty) with ‚ü®i, hs, ht‚ü©|he,
  { calc Œº s + Œº t ‚â§ ‚àû : le_top
    ... = m (f i) : (h (f i) hs ht).symm
    ... ‚â§ ‚àë' i, m (f i) : ennreal.le_tsum i },
  set I := Œª s, {i : ‚Ñï | (s ‚à© f i).nonempty},
  have hd : disjoint (I s) (I t), from Œª i hi, he ‚ü®i, hi‚ü©,
  have hI : ‚àÄ u ‚äÜ s ‚à™ t, Œº u ‚â§ ‚àë'  i : I u, Œº (f i), from Œª u hu,
  calc Œº u ‚â§ Œº (‚ãÉ i : I u, f i) :
    Œº.mono (Œª x hx, let ‚ü®i, hi‚ü© := mem_Union.1 (hf (hu hx)) in mem_Union.2 ‚ü®‚ü®i, ‚ü®x, hx, hi‚ü©‚ü©, hi‚ü©)
  ... ‚â§ ‚àë' i : I u, Œº (f i) : Œº.Union _,
  calc Œº s + Œº t ‚â§ (‚àë' i : I s, Œº (f i)) + (‚àë' i : I t, Œº (f i)) :
    add_le_add (hI _ $ subset_union_left _ _) (hI _ $ subset_union_right _ _)
  ... = ‚àë' i : I s ‚à™ I t, Œº (f i) :
    (@tsum_union_disjoint _ _ _ _ _ (Œª i, Œº (f i)) _ _ _ hd ennreal.summable ennreal.summable).symm
  ... ‚â§ ‚àë' i, Œº (f i) :
    tsum_le_tsum_of_inj coe subtype.coe_injective (Œª _ _, zero_le _) (Œª _, le_rfl)
      ennreal.summable ennreal.summable
  ... ‚â§ ‚àë' i, m (f i) : ennreal.tsum_le_tsum (Œª i, of_function_le _)
end

lemma comap_of_function {Œ≤} (f : Œ≤ ‚Üí Œ±) (h : monotone m ‚à® surjective f) :
  comap f (outer_measure.of_function m m_empty) =
    outer_measure.of_function (Œª s, m (f '' s)) (by rwa set.image_empty) :=
begin
  refine le_antisymm (le_of_function.2 $ Œª s, _) (Œª s, _),
  { rw comap_apply, apply of_function_le },
  { rw [comap_apply, of_function_apply, of_function_apply],
    refine infi_le_infi2 (Œª t, ‚ü®Œª k, f ‚Åª¬π' (t k), _‚ü©),
    refine infi_le_infi2 (Œª ht, _),
    rw [set.image_subset_iff, preimage_Union] at ht,
    refine ‚ü®ht, ennreal.tsum_le_tsum $ Œª n, _‚ü©,
    cases h,
    exacts [h (image_preimage_subset _ _), (congr_arg m (h.image_preimage (t n))).le] }
end

lemma map_of_function_le {Œ≤} (f : Œ± ‚Üí Œ≤) :
  map f (outer_measure.of_function m m_empty) ‚â§
    outer_measure.of_function (Œª s, m (f ‚Åª¬π' s)) m_empty :=
le_of_function.2 $ Œª s, by { rw map_apply, apply of_function_le }

lemma map_of_function {Œ≤} {f : Œ± ‚Üí Œ≤} (hf : injective f) :
  map f (outer_measure.of_function m m_empty) =
    outer_measure.of_function (Œª s, m (f ‚Åª¬π' s)) m_empty :=
begin
  refine (map_of_function_le _).antisymm (Œª s, _),
  simp only [of_function_apply, map_apply, le_infi_iff],
  intros t ht,
  refine infi_le_of_le (Œª n, (range f)·∂ú ‚à™ f '' (t n)) (infi_le_of_le _ _),
  { rw [‚Üê union_Union, ‚Üê inter_subset, ‚Üê image_preimage_eq_inter_range, ‚Üê image_Union],
    exact image_subset _ ht },
  { refine ennreal.tsum_le_tsum (Œª n, le_of_eq _),
    simp [hf.preimage_image] }
end

lemma restrict_of_function (s : set Œ±) (hm : monotone m) :
  restrict s (outer_measure.of_function m m_empty) =
    outer_measure.of_function (Œª t, m (t ‚à© s)) (by rwa set.empty_inter) :=
by simp only [restrict, linear_map.comp_apply, comap_of_function _ (or.inl hm),
  map_of_function subtype.coe_injective, subtype.image_preimage_coe]

lemma smul_of_function {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† ‚àû) :
  c ‚Ä¢ outer_measure.of_function m m_empty = outer_measure.of_function (c ‚Ä¢ m) (by simp [m_empty]) :=
begin
  ext1 s,
  haveI : nonempty {t : ‚Ñï ‚Üí set Œ± // s ‚äÜ ‚ãÉ i, t i} := ‚ü®‚ü®Œª _, s, subset_Union (Œª _, s) 0‚ü©‚ü©,
  simp only [smul_apply, of_function_apply, ennreal.tsum_mul_left, pi.smul_apply, smul_eq_mul,
    infi_subtype', ennreal.infi_mul_left (Œª h, (hc h).elim)],
end

end of_function

section bounded_by

variables {Œ± : Type*} (m : set Œ± ‚Üí ‚Ñù‚â•0‚àû)

/-- Given any function `m` assigning measures to sets, there is a unique maximal outer measure `Œº`
  satisfying `Œº s ‚â§ m s` for all `s : set Œ±`. This is the same as `outer_measure.of_function`,
  except that it doesn't require `m ‚àÖ = 0`. -/
def bounded_by : outer_measure Œ± :=
outer_measure.of_function (Œª s, ‚®Ü (h : s.nonempty), m s) (by simp [empty_not_nonempty])

variables {m}
theorem bounded_by_le (s : set Œ±) : bounded_by m s ‚â§ m s :=
(of_function_le _).trans supr_const_le

theorem bounded_by_eq_of_function (m_empty : m ‚àÖ = 0) (s : set Œ±) :
  bounded_by m s = outer_measure.of_function m m_empty s :=
begin
  have : (Œª s : set Œ±, ‚®Ü (h : s.nonempty), m s) = m,
  { ext1 t, cases t.eq_empty_or_nonempty with h h; simp [h, empty_not_nonempty, m_empty] },
  simp [bounded_by, this]
end
theorem bounded_by_apply (s : set Œ±) :
  bounded_by m s = ‚®Ö (t : ‚Ñï ‚Üí set Œ±) (h : s ‚äÜ Union t), ‚àë' n, ‚®Ü (h : (t n).nonempty), m (t n) :=
by simp [bounded_by, of_function_apply]

theorem bounded_by_eq (s : set Œ±) (m_empty : m ‚àÖ = 0) (m_mono : ‚àÄ ‚¶Ét : set Œ±‚¶Ñ, s ‚äÜ t ‚Üí m s ‚â§ m t)
  (m_subadd : ‚àÄ (s : ‚Ñï ‚Üí set Œ±), m (‚ãÉi, s i) ‚â§ ‚àë'i, m (s i)) : bounded_by m s = m s :=
by rw [bounded_by_eq_of_function m_empty, of_function_eq s m_mono m_subadd]

@[simp] theorem bounded_by_eq_self (m : outer_measure Œ±) : bounded_by m = m :=
ext $ Œª s, bounded_by_eq _ m.empty' (Œª t ht, m.mono' ht) m.Union

theorem le_bounded_by {Œº : outer_measure Œ±} : Œº ‚â§ bounded_by m ‚Üî ‚àÄ s, Œº s ‚â§ m s :=
begin
  rw [bounded_by, le_of_function, forall_congr], intro s,
  cases s.eq_empty_or_nonempty with h h; simp [h, empty_not_nonempty]
end

theorem le_bounded_by' {Œº : outer_measure Œ±} :
  Œº ‚â§ bounded_by m ‚Üî ‚àÄ s : set Œ±, s.nonempty ‚Üí Œº s ‚â§ m s :=
by { rw [le_bounded_by, forall_congr], intro s, cases s.eq_empty_or_nonempty with h h; simp [h] }

lemma smul_bounded_by {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† ‚àû) : c ‚Ä¢ bounded_by m = bounded_by (c ‚Ä¢ m) :=
begin
  simp only [bounded_by, smul_of_function hc],
  congr' 1 with s : 1,
  rcases s.eq_empty_or_nonempty with rfl|hs; simp *
end

lemma comap_bounded_by {Œ≤} (f : Œ≤ ‚Üí Œ±)
  (h : monotone (Œª s : {s : set Œ± // s.nonempty}, m s) ‚à® surjective f) :
  comap f (bounded_by m) = bounded_by (Œª s, m (f '' s)) :=
begin
  refine (comap_of_function _ _).trans _,
  { refine h.imp (Œª H s t hst, supr_le $ Œª hs, _) id,
    have ht : t.nonempty := hs.mono hst,
    exact (@H ‚ü®s, hs‚ü© ‚ü®t, ht‚ü© hst).trans (le_supr (Œª h : t.nonempty, m t) ht) },
  { dunfold bounded_by,
    congr' with s : 1,
    rw nonempty_image_iff }
end

/-- If `m u = ‚àû` for any set `u` that has nonempty intersection both with `s` and `t`, then
`Œº (s ‚à™ t) = Œº s + Œº t`, where `Œº = measure_theory.outer_measure.bounded_by m`.

E.g., if `Œ±` is an (e)metric space and `m u = ‚àû` on any set of diameter `‚â• r`, then this lemma
implies that `Œº (s ‚à™ t) = Œº s + Œº t` on any two sets such that `r ‚â§ edist x y` for all `x ‚àà s`
and `y ‚àà t`.  -/
lemma bounded_by_union_of_top_of_nonempty_inter {s t : set Œ±}
  (h : ‚àÄ u, (s ‚à© u).nonempty ‚Üí (t ‚à© u).nonempty ‚Üí m u = ‚àû) :
  bounded_by m (s ‚à™ t) = bounded_by m s + bounded_by m t :=
of_function_union_of_top_of_nonempty_inter $ Œª u hs ht,
  top_unique $ (h u hs ht).ge.trans $ le_supr (Œª h, m u) (hs.mono $ inter_subset_right s u)

end bounded_by

section caratheodory_measurable
universe u
parameters {Œ± : Type u} (m : outer_measure Œ±)
include m

local attribute [simp] set.inter_comm set.inter_left_comm set.inter_assoc

variables {s s‚ÇÅ s‚ÇÇ : set Œ±}

/-- A set `s` is Carath√©odory-measurable for an outer measure `m` if for all sets `t` we have
  `m t = m (t ‚à© s) + m (t \ s)`. -/
def is_caratheodory (s : set Œ±) : Prop := ‚àÄt, m t = m (t ‚à© s) + m (t \ s)

lemma is_caratheodory_iff_le' {s : set Œ±} : is_caratheodory s ‚Üî ‚àÄt, m (t ‚à© s) + m (t \ s) ‚â§ m t :=
forall_congr $ Œª t, le_antisymm_iff.trans $ and_iff_right $ le_inter_add_diff _

@[simp] lemma is_caratheodory_empty : is_caratheodory ‚àÖ :=
by simp [is_caratheodory, m.empty, diff_empty]

lemma is_caratheodory_compl : is_caratheodory s‚ÇÅ ‚Üí is_caratheodory s‚ÇÅ·∂ú :=
by simp [is_caratheodory, diff_eq, add_comm]

@[simp] lemma is_caratheodory_compl_iff : is_caratheodory s·∂ú ‚Üî is_caratheodory s :=
‚ü®Œª h, by simpa using is_caratheodory_compl m h, is_caratheodory_compl‚ü©

lemma is_caratheodory_union (h‚ÇÅ : is_caratheodory s‚ÇÅ) (h‚ÇÇ : is_caratheodory s‚ÇÇ) :
  is_caratheodory (s‚ÇÅ ‚à™ s‚ÇÇ) :=
Œª t, begin
  rw [h‚ÇÅ t, h‚ÇÇ (t ‚à© s‚ÇÅ), h‚ÇÇ (t \ s‚ÇÅ), h‚ÇÅ (t ‚à© (s‚ÇÅ ‚à™ s‚ÇÇ)),
    inter_diff_assoc _ _ s‚ÇÅ, set.inter_assoc _ _ s‚ÇÅ,
    inter_eq_self_of_subset_right (set.subset_union_left _ _),
    union_diff_left, h‚ÇÇ (t ‚à© s‚ÇÅ)],
  simp [diff_eq, add_assoc]
end

lemma measure_inter_union (h : s‚ÇÅ ‚à© s‚ÇÇ ‚äÜ ‚àÖ) (h‚ÇÅ : is_caratheodory s‚ÇÅ) {t : set Œ±} :
  m (t ‚à© (s‚ÇÅ ‚à™ s‚ÇÇ)) = m (t ‚à© s‚ÇÅ) + m (t ‚à© s‚ÇÇ) :=
by rw [h‚ÇÅ, set.inter_assoc, set.union_inter_cancel_left,
  inter_diff_assoc, union_diff_cancel_left h]

lemma is_caratheodory_Union_lt {s : ‚Ñï ‚Üí set Œ±} :
  ‚àÄ{n:‚Ñï}, (‚àÄi<n, is_caratheodory (s i)) ‚Üí is_caratheodory (‚ãÉi<n, s i)
| 0       h := by simp [nat.not_lt_zero]
| (n + 1) h := by rw bUnion_lt_succ; exact is_caratheodory_union m
    (is_caratheodory_Union_lt $ assume i hi, h i $ lt_of_lt_of_le hi $ nat.le_succ _)
    (h n (le_refl (n + 1)))

lemma is_caratheodory_inter (h‚ÇÅ : is_caratheodory s‚ÇÅ) (h‚ÇÇ : is_caratheodory s‚ÇÇ) :
  is_caratheodory (s‚ÇÅ ‚à© s‚ÇÇ) :=
by { rw [‚Üê is_caratheodory_compl_iff, compl_inter],
  exact is_caratheodory_union _ (is_caratheodory_compl _ h‚ÇÅ) (is_caratheodory_compl _ h‚ÇÇ) }

lemma is_caratheodory_sum {s : ‚Ñï ‚Üí set Œ±} (h : ‚àÄi, is_caratheodory (s i))
  (hd : pairwise (disjoint on s)) {t : set Œ±} :
  ‚àÄ {n}, ‚àë i in finset.range n, m (t ‚à© s i) = m (t ‚à© ‚ãÉi<n, s i)
| 0            := by simp [nat.not_lt_zero, m.empty]
| (nat.succ n) := begin
  rw [bUnion_lt_succ, finset.sum_range_succ, set.union_comm, is_caratheodory_sum,
    m.measure_inter_union _ (h n), add_comm],
  intro a,
  simpa using Œª (h‚ÇÅ : a ‚àà s n) i (hi : i < n) h‚ÇÇ, hd _ _ (ne_of_gt hi) ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
end

lemma is_caratheodory_Union_nat {s : ‚Ñï ‚Üí set Œ±} (h : ‚àÄi, is_caratheodory (s i))
  (hd : pairwise (disjoint on s)) : is_caratheodory (‚ãÉi, s i) :=
is_caratheodory_iff_le'.2 $ Œª t, begin
  have hp : m (t ‚à© ‚ãÉi, s i) ‚â§ (‚®Ün, m (t ‚à© ‚ãÉi<n, s i)),
  { convert m.Union (Œª i, t ‚à© s i),
    { rw inter_Union },
    { simp [ennreal.tsum_eq_supr_nat, is_caratheodory_sum m h hd] } },
  refine le_trans (add_le_add_right hp _) _,
  rw ennreal.supr_add,
  refine supr_le (Œª n, le_trans (add_le_add_left _ _)
    (ge_of_eq (is_caratheodory_Union_lt m (Œª i _, h i) _))),
  refine m.mono (diff_subset_diff_right _),
  exact bUnion_subset (Œª i _, subset_Union _ i),
end

lemma f_Union {s : ‚Ñï ‚Üí set Œ±} (h : ‚àÄi, is_caratheodory (s i))
  (hd : pairwise (disjoint on s)) : m (‚ãÉi, s i) = ‚àë'i, m (s i) :=
begin
  refine le_antisymm (m.Union_nat s) _,
  rw ennreal.tsum_eq_supr_nat,
  refine supr_le (Œª n, _),
  have := @is_caratheodory_sum _ m _ h hd univ n,
  simp at this, simp [this],
  exact m.mono (bUnion_subset (Œª i _, subset_Union _ i)),
end

/-- The Carath√©odory-measurable sets for an outer measure `m` form a Dynkin system.  -/
def caratheodory_dynkin : measurable_space.dynkin_system Œ± :=
{ has := is_caratheodory,
  has_empty := is_caratheodory_empty,
  has_compl := assume s, is_caratheodory_compl,
  has_Union_nat := assume f hf hn, is_caratheodory_Union_nat hn hf }

/-- Given an outer measure `Œº`, the Carath√©odory-measurable space is
  defined such that `s` is measurable if `‚àÄt, Œº t = Œº (t ‚à© s) + Œº (t \ s)`. -/
protected def caratheodory : measurable_space Œ± :=
caratheodory_dynkin.to_measurable_space $ assume s‚ÇÅ s‚ÇÇ, is_caratheodory_inter

lemma is_caratheodory_iff {s : set Œ±} :
  caratheodory.measurable_set' s ‚Üî ‚àÄt, m t = m (t ‚à© s) + m (t \ s) :=
iff.rfl

lemma is_caratheodory_iff_le {s : set Œ±} :
  caratheodory.measurable_set' s ‚Üî ‚àÄt, m (t ‚à© s) + m (t \ s) ‚â§ m t :=
is_caratheodory_iff_le'

protected lemma Union_eq_of_caratheodory {s : ‚Ñï ‚Üí set Œ±}
  (h : ‚àÄi, caratheodory.measurable_set' (s i)) (hd : pairwise (disjoint on s)) :
  m (‚ãÉi, s i) = ‚àë'i, m (s i) :=
f_Union h hd

end caratheodory_measurable

variables {Œ± : Type*}

lemma of_function_caratheodory {m : set Œ± ‚Üí ‚Ñù‚â•0‚àû} {s : set Œ±}
  {h‚ÇÄ : m ‚àÖ = 0} (hs : ‚àÄt, m (t ‚à© s) + m (t \ s) ‚â§ m t) :
  (outer_measure.of_function m h‚ÇÄ).caratheodory.measurable_set' s :=
begin
  apply (is_caratheodory_iff_le _).mpr,
  refine Œª t, le_infi (Œª f, le_infi $ Œª hf, _),
  refine le_trans (add_le_add
    (infi_le_of_le (Œªi, f i ‚à© s) $ infi_le _ _)
    (infi_le_of_le (Œªi, f i \ s) $ infi_le _ _)) _,
  { rw ‚Üê Union_inter, exact inter_subset_inter_left _ hf },
  { rw ‚Üê Union_diff, exact diff_subset_diff_left hf },
  { rw ‚Üê ennreal.tsum_add, exact ennreal.tsum_le_tsum (Œª i, hs _) }
end

lemma bounded_by_caratheodory {m : set Œ± ‚Üí ‚Ñù‚â•0‚àû} {s : set Œ±}
  (hs : ‚àÄt, m (t ‚à© s) + m (t \ s) ‚â§ m t) : (bounded_by m).caratheodory.measurable_set' s :=
begin
  apply of_function_caratheodory, intro t,
  cases t.eq_empty_or_nonempty with h h,
  { simp [h, empty_not_nonempty] },
  { convert le_trans _ (hs t), { simp [h] }, exact add_le_add supr_const_le supr_const_le }
end

@[simp] theorem zero_caratheodory : (0 : outer_measure Œ±).caratheodory = ‚ä§ :=
top_unique $ Œª s _ t, (add_zero _).symm

theorem top_caratheodory : (‚ä§ : outer_measure Œ±).caratheodory = ‚ä§ :=
top_unique $ assume s hs, (is_caratheodory_iff_le _).2 $ assume t,
  t.eq_empty_or_nonempty.elim (Œª ht, by simp [ht])
    (Œª ht, by simp only [ht, top_apply, le_top])

theorem le_add_caratheodory (m‚ÇÅ m‚ÇÇ : outer_measure Œ±) :
  m‚ÇÅ.caratheodory ‚äì m‚ÇÇ.caratheodory ‚â§ (m‚ÇÅ + m‚ÇÇ : outer_measure Œ±).caratheodory :=
Œª s ‚ü®hs‚ÇÅ, hs‚ÇÇ‚ü© t, by simp [hs‚ÇÅ t, hs‚ÇÇ t, add_left_comm, add_assoc]

theorem le_sum_caratheodory {Œπ} (m : Œπ ‚Üí outer_measure Œ±) :
  (‚®Ö i, (m i).caratheodory) ‚â§ (sum m).caratheodory :=
Œª s h t, by simp [Œª i,
  measurable_space.measurable_set_infi.1 h i t, ennreal.tsum_add]

theorem le_smul_caratheodory (a : ‚Ñù‚â•0‚àû) (m : outer_measure Œ±) :
  m.caratheodory ‚â§ (a ‚Ä¢ m).caratheodory :=
Œª s h t, by simp [h t, mul_add]

@[simp] theorem dirac_caratheodory (a : Œ±) : (dirac a).caratheodory = ‚ä§ :=
top_unique $ Œª s _ t, begin
  by_cases ht : a ‚àà t, swap, by simp [ht],
  by_cases hs : a ‚àà s; simp*
end

section Inf_gen

/-- Given a set of outer measures, we define a new function that on a set `s` is defined to be the
  infimum of `Œº(s)` for the outer measures `Œº` in the collection. We ensure that this
  function is defined to be `0` on `‚àÖ`, even if the collection of outer measures is empty.
  The outer measure generated by this function is the infimum of the given outer measures. -/
def Inf_gen (m : set (outer_measure Œ±)) (s : set Œ±) : ‚Ñù‚â•0‚àû :=
‚®Ö (Œº : outer_measure Œ±) (h : Œº ‚àà m), Œº s

lemma Inf_gen_def (m : set (outer_measure Œ±)) (t : set Œ±) :
  Inf_gen m t = (‚®Ö (Œº : outer_measure Œ±) (h : Œº ‚àà m), Œº t) :=
rfl

lemma Inf_eq_bounded_by_Inf_gen (m : set (outer_measure Œ±)) :
  Inf m = outer_measure.bounded_by (Inf_gen m) :=
begin
  refine le_antisymm _ _,
  { refine (le_bounded_by.2 $ Œª s, _), refine le_binfi _,
    intros Œº hŒº, refine (show Inf m ‚â§ Œº, from Inf_le hŒº) s },
  { refine le_Inf _, intros Œº hŒº t, refine le_trans (bounded_by_le t) (binfi_le Œº hŒº) }
end

lemma supr_Inf_gen_nonempty {m : set (outer_measure Œ±)} (h : m.nonempty) (t : set Œ±) :
  (‚®Ü (h : t.nonempty), Inf_gen m t) = (‚®Ö (Œº : outer_measure Œ±) (h : Œº ‚àà m), Œº t) :=
begin
  rcases t.eq_empty_or_nonempty with rfl|ht,
  { rcases h with ‚ü®Œº, hŒº‚ü©,
    rw [eq_false_intro empty_not_nonempty, supr_false, eq_comm],
    simp_rw [empty'],
    apply bot_unique,
    refine infi_le_of_le Œº (infi_le _ hŒº) },
  { simp [ht, Inf_gen_def] }
end

/-- The value of the Infimum of a nonempty set of outer measures on a set is not simply
the minimum value of a measure on that set: it is the infimum sum of measures of countable set of
sets that covers that set, where a different measure can be used for each set in the cover. -/
lemma Inf_apply {m : set (outer_measure Œ±)} {s : set Œ±} (h : m.nonempty) :
  Inf m s = ‚®Ö (t : ‚Ñï ‚Üí set Œ±) (h2 : s ‚äÜ Union t),
    ‚àë' n, ‚®Ö (Œº : outer_measure Œ±) (h3 : Œº ‚àà m), Œº (t n) :=
by simp_rw [Inf_eq_bounded_by_Inf_gen, bounded_by_apply, supr_Inf_gen_nonempty h]

/-- The value of the Infimum of a set of outer measures on a nonempty set is not simply
the minimum value of a measure on that set: it is the infimum sum of measures of countable set of
sets that covers that set, where a different measure can be used for each set in the cover. -/
lemma Inf_apply' {m : set (outer_measure Œ±)} {s : set Œ±} (h : s.nonempty) :
  Inf m s = ‚®Ö (t : ‚Ñï ‚Üí set Œ±) (h2 : s ‚äÜ Union t),
    ‚àë' n, ‚®Ö (Œº : outer_measure Œ±) (h3 : Œº ‚àà m), Œº (t n) :=
m.eq_empty_or_nonempty.elim (Œª hm, by simp [hm, h]) Inf_apply

/-- The value of the Infimum of a nonempty family of outer measures on a set is not simply
the minimum value of a measure on that set: it is the infimum sum of measures of countable set of
sets that covers that set, where a different measure can be used for each set in the cover. -/
lemma infi_apply {Œπ} [nonempty Œπ] (m : Œπ ‚Üí outer_measure Œ±) (s : set Œ±) :
  (‚®Ö i, m i) s = ‚®Ö (t : ‚Ñï ‚Üí set Œ±) (h2 : s ‚äÜ Union t), ‚àë' n, ‚®Ö i, m i (t n) :=
by { rw [infi, Inf_apply (range_nonempty m)], simp only [infi_range] }

/-- The value of the Infimum of a family of outer measures on a nonempty set is not simply
the minimum value of a measure on that set: it is the infimum sum of measures of countable set of
sets that covers that set, where a different measure can be used for each set in the cover. -/
lemma infi_apply' {Œπ} (m : Œπ ‚Üí outer_measure Œ±) {s : set Œ±} (hs : s.nonempty) :
  (‚®Ö i, m i) s = ‚®Ö (t : ‚Ñï ‚Üí set Œ±) (h2 : s ‚äÜ Union t), ‚àë' n, ‚®Ö i, m i (t n) :=
by { rw [infi, Inf_apply' hs], simp only [infi_range] }

/-- The value of the Infimum of a nonempty family of outer measures on a set is not simply
the minimum value of a measure on that set: it is the infimum sum of measures of countable set of
sets that covers that set, where a different measure can be used for each set in the cover. -/
lemma binfi_apply {Œπ} {I : set Œπ} (hI : I.nonempty) (m : Œπ ‚Üí outer_measure Œ±) (s : set Œ±) :
  (‚®Ö i ‚àà I, m i) s = ‚®Ö (t : ‚Ñï ‚Üí set Œ±) (h2 : s ‚äÜ Union t), ‚àë' n, ‚®Ö i ‚àà I, m i (t n) :=
by { haveI := hI.to_subtype, simp only [‚Üê infi_subtype'', infi_apply] }

/-- The value of the Infimum of a nonempty family of outer measures on a set is not simply
the minimum value of a measure on that set: it is the infimum sum of measures of countable set of
sets that covers that set, where a different measure can be used for each set in the cover. -/
lemma binfi_apply' {Œπ} (I : set Œπ) (m : Œπ ‚Üí outer_measure Œ±) {s : set Œ±} (hs : s.nonempty) :
  (‚®Ö i ‚àà I, m i) s = ‚®Ö (t : ‚Ñï ‚Üí set Œ±) (h2 : s ‚äÜ Union t), ‚àë' n, ‚®Ö i ‚àà I, m i (t n) :=
by { simp only [‚Üê infi_subtype'', infi_apply' _ hs] }

lemma map_infi_le {Œπ Œ≤} (f : Œ± ‚Üí Œ≤) (m : Œπ ‚Üí outer_measure Œ±) :
  map f (‚®Ö i, m i) ‚â§ ‚®Ö i, map f (m i) :=
(map_mono f).map_infi_le

lemma comap_infi {Œπ Œ≤} (f : Œ± ‚Üí Œ≤) (m : Œπ ‚Üí outer_measure Œ≤) :
  comap f (‚®Ö i, m i) = ‚®Ö i, comap f (m i) :=
begin
  refine ext_nonempty (Œª s hs, _),
  refine ((comap_mono f).map_infi_le s).antisymm _,
  simp only [comap_apply, infi_apply' _ hs, infi_apply' _ (hs.image _),
    le_infi_iff, set.image_subset_iff, preimage_Union],
  refine Œª t ht, infi_le_of_le _ (infi_le_of_le ht $ ennreal.tsum_le_tsum $ Œª k, _),
  exact infi_le_infi (Œª i, (m i).mono (image_preimage_subset _ _))
end

lemma map_infi {Œπ Œ≤} {f : Œ± ‚Üí Œ≤} (hf : injective f) (m : Œπ ‚Üí outer_measure Œ±) :
  map f (‚®Ö i, m i) = restrict (range f) (‚®Ö i, map f (m i)) :=
begin
  refine eq.trans _ (map_comap _ _),
  simp only [comap_infi, comap_map hf]
end

lemma map_infi_comap {Œπ Œ≤} [nonempty Œπ] {f : Œ± ‚Üí Œ≤} (m : Œπ ‚Üí outer_measure Œ≤) :
  map f (‚®Ö i, comap f (m i)) = ‚®Ö i, map f (comap f (m i)) :=
begin
  refine (map_infi_le _ _).antisymm (Œª s, _),
  simp only [map_apply, comap_apply, infi_apply, le_infi_iff],
  refine Œª t ht, infi_le_of_le (Œª n, f '' (t n) ‚à™ (range f)·∂ú) (infi_le_of_le _ _),
  { rw [‚Üê Union_union, set.union_comm, ‚Üê inter_subset, ‚Üê image_Union,
      ‚Üê image_preimage_eq_inter_range],
    exact image_subset _ ht },
  { refine ennreal.tsum_le_tsum (Œª n, infi_le_infi (Œª i, (m i).mono _)),
    simp }
end

lemma map_binfi_comap {Œπ Œ≤} {I : set Œπ} (hI : I.nonempty) {f : Œ± ‚Üí Œ≤} (m : Œπ ‚Üí outer_measure Œ≤) :
  map f (‚®Ö i ‚àà I, comap f (m i)) = ‚®Ö i ‚àà I, map f (comap f (m i)) :=
by { haveI := hI.to_subtype, rw [‚Üê infi_subtype'', ‚Üê infi_subtype''], exact map_infi_comap _ }

lemma restrict_infi_restrict {Œπ} (s : set Œ±) (m : Œπ ‚Üí outer_measure Œ±) :
  restrict s (‚®Ö i, restrict s (m i)) = restrict s (‚®Ö i, m i) :=
calc restrict s (‚®Ö i, restrict s (m i)) = restrict (range (coe : s ‚Üí Œ±)) (‚®Ö i, restrict s (m i)) :
  by rw [subtype.range_coe]
... = map (coe : s ‚Üí Œ±) (‚®Ö i, comap coe (m i)) : (map_infi subtype.coe_injective _).symm
... = restrict s (‚®Ö i, m i) : congr_arg (map coe) (comap_infi _ _).symm

lemma restrict_infi {Œπ} [nonempty Œπ] (s : set Œ±) (m : Œπ ‚Üí outer_measure Œ±) :
  restrict s (‚®Ö i, m i) = ‚®Ö i, restrict s (m i) :=
(congr_arg (map coe) (comap_infi _ _)).trans (map_infi_comap _)

lemma restrict_binfi {Œπ} {I : set Œπ} (hI : I.nonempty) (s : set Œ±) (m : Œπ ‚Üí outer_measure Œ±) :
  restrict s (‚®Ö i ‚àà I, m i) = ‚®Ö i ‚àà I, restrict s (m i) :=
by { haveI := hI.to_subtype, rw [‚Üê infi_subtype'', ‚Üê infi_subtype''], exact restrict_infi _ _ }

/-- This proves that Inf and restrict commute for outer measures, so long as the set of
outer measures is nonempty. -/
lemma restrict_Inf_eq_Inf_restrict
  (m : set (outer_measure Œ±)) {s : set Œ±} (hm : m.nonempty) :
  restrict s (Inf m) = Inf ((restrict s) '' m) :=
by simp only [Inf_eq_infi, restrict_binfi, hm, infi_image]

end Inf_gen

end outer_measure
open outer_measure

/-! ### Induced Outer Measure

  We can extend a function defined on a subset of `set Œ±` to an outer measure.
  The underlying function is called `extend`, and the measure it induces is called
  `induced_outer_measure`.

  Some lemmas below are proven twice, once in the general case, and one where the function `m`
  is only defined on measurable sets (i.e. when `P = measurable_set`). In the latter cases, we can
  remove some hypotheses in the statement. The general version has the same name, but with a prime
  at the end. -/
section extend
variables {Œ± : Type*} {P : Œ± ‚Üí Prop}
variables (m : Œ† (s : Œ±), P s ‚Üí ‚Ñù‚â•0‚àû)

/-- We can trivially extend a function defined on a subclass of objects (with codomain `‚Ñù‚â•0‚àû`)
  to all objects by defining it to be `‚àû` on the objects not in the class. -/
def extend (s : Œ±) : ‚Ñù‚â•0‚àû := ‚®Ö h : P s, m s h

lemma extend_eq {s : Œ±} (h : P s) : extend m s = m s h :=
by simp [extend, h]

lemma extend_eq_top {s : Œ±} (h : ¬¨P s) : extend m s = ‚àû :=
by simp [extend, h]

lemma le_extend {s : Œ±} (h : P s) : m s h ‚â§ extend m s :=
by { simp only [extend, le_infi_iff], intro, refl' }

-- TODO: why this is a bad `congr` lemma?
lemma extend_congr {Œ≤ : Type*} {Pb : Œ≤ ‚Üí Prop} {mb : Œ† s : Œ≤, Pb s ‚Üí ‚Ñù‚â•0‚àû}
  {sa : Œ±} {sb : Œ≤} (hP : P sa ‚Üî Pb sb) (hm : ‚àÄ (ha : P sa) (hb : Pb sb), m sa ha = mb sb hb) :
  extend m sa = extend mb sb :=
infi_congr_Prop hP (Œª h, hm _ _)

end extend

section extend_set

variables {Œ± : Type*} {P : set Œ± ‚Üí Prop}
variables {m : Œ† (s : set Œ±), P s ‚Üí ‚Ñù‚â•0‚àû}
variables (P0 : P ‚àÖ) (m0 : m ‚àÖ P0 = 0)
variables (PU : ‚àÄ{{f : ‚Ñï ‚Üí set Œ±}} (hm : ‚àÄi, P (f i)), P (‚ãÉi, f i))
variables (mU : ‚àÄ {{f : ‚Ñï ‚Üí set Œ±}} (hm : ‚àÄi, P (f i)), pairwise (disjoint on f) ‚Üí
  m (‚ãÉi, f i) (PU hm) = ‚àë'i, m (f i) (hm i))
variables (msU : ‚àÄ {{f : ‚Ñï ‚Üí set Œ±}} (hm : ‚àÄi, P (f i)),
  m (‚ãÉi, f i) (PU hm) ‚â§ ‚àë'i, m (f i) (hm i))
variables (m_mono : ‚àÄ‚¶És‚ÇÅ s‚ÇÇ : set Œ±‚¶Ñ (hs‚ÇÅ : P s‚ÇÅ) (hs‚ÇÇ : P s‚ÇÇ), s‚ÇÅ ‚äÜ s‚ÇÇ ‚Üí m s‚ÇÅ hs‚ÇÅ ‚â§ m s‚ÇÇ hs‚ÇÇ)

lemma extend_empty : extend m ‚àÖ = 0 :=
(extend_eq _ P0).trans m0

lemma extend_Union_nat
  {f : ‚Ñï ‚Üí set Œ±} (hm : ‚àÄi, P (f i))
  (mU : m (‚ãÉi, f i) (PU hm) = ‚àë'i, m (f i) (hm i)) :
  extend m (‚ãÉi, f i) = ‚àë'i, extend m (f i) :=
(extend_eq _ _).trans $ mU.trans $ by { congr' with i, rw extend_eq }

section subadditive
include PU msU
lemma extend_Union_le_tsum_nat'
  (s : ‚Ñï ‚Üí set Œ±) : extend m (‚ãÉi, s i) ‚â§ ‚àë'i, extend m (s i) :=
begin
  by_cases h : ‚àÄi, P (s i),
  { rw [extend_eq _ (PU h), congr_arg tsum _],
    { apply msU h },
    funext i, apply extend_eq _ (h i) },
  { cases not_forall.1 h with i hi,
    exact le_trans (le_infi $ Œª h, hi.elim h) (ennreal.le_tsum i) }
end
end subadditive

section mono
include m_mono
lemma extend_mono'
  ‚¶És‚ÇÅ s‚ÇÇ : set Œ±‚¶Ñ (h‚ÇÅ : P s‚ÇÅ) (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) : extend m s‚ÇÅ ‚â§ extend m s‚ÇÇ :=
by { refine le_infi _, intro h‚ÇÇ, rw [extend_eq m h‚ÇÅ], exact m_mono h‚ÇÅ h‚ÇÇ hs }
end mono

section unions
include P0 m0 PU mU
lemma extend_Union {Œ≤} [encodable Œ≤] {f : Œ≤ ‚Üí set Œ±}
  (hd : pairwise (disjoint on f)) (hm : ‚àÄi, P (f i)) :
  extend m (‚ãÉi, f i) = ‚àë'i, extend m (f i) :=
begin
  rw [‚Üê encodable.Union_decode‚ÇÇ, ‚Üê tsum_Union_decode‚ÇÇ],
  { exact extend_Union_nat PU
      (Œª n, encodable.Union_decode‚ÇÇ_cases P0 hm)
      (mU _ (encodable.Union_decode‚ÇÇ_disjoint_on hd)) },
  { exact extend_empty P0 m0 }
end

lemma extend_union {s‚ÇÅ s‚ÇÇ : set Œ±} (hd : disjoint s‚ÇÅ s‚ÇÇ) (h‚ÇÅ : P s‚ÇÅ) (h‚ÇÇ : P s‚ÇÇ) :
  extend m (s‚ÇÅ ‚à™ s‚ÇÇ) = extend m s‚ÇÅ + extend m s‚ÇÇ :=
begin
  rw [union_eq_Union, extend_Union P0 m0 PU mU
      (pairwise_disjoint_on_bool.2 hd) (bool.forall_bool.2 ‚ü®h‚ÇÇ, h‚ÇÅ‚ü©), tsum_fintype],
  simp
end

end unions

variable (m)
/-- Given an arbitrary function on a subset of sets, we can define the outer measure corresponding
  to it (this is the unique maximal outer measure that is at most `m` on the domain of `m`). -/
def induced_outer_measure : outer_measure Œ± :=
outer_measure.of_function (extend m) (extend_empty P0 m0)
variables {m P0 m0}

lemma le_induced_outer_measure {Œº : outer_measure Œ±} :
  Œº ‚â§ induced_outer_measure m P0 m0 ‚Üî ‚àÄ s (hs : P s), Œº s ‚â§ m s hs :=
le_of_function.trans $ forall_congr $ Œª s, le_infi_iff

/-- If `P u` is `false` for any set `u` that has nonempty intersection both with `s` and `t`, then
`Œº (s ‚à™ t) = Œº s + Œº t`, where `Œº = induced_outer_measure m P0 m0`.

E.g., if `Œ±` is an (e)metric space and `P u = diam u < r`, then this lemma implies that
`Œº (s ‚à™ t) = Œº s + Œº t` on any two sets such that `r ‚â§ edist x y` for all `x ‚àà s` and `y ‚àà t`. -/
lemma induced_outer_measure_union_of_false_of_nonempty_inter {s t : set Œ±}
  (h : ‚àÄ u, (s ‚à© u).nonempty ‚Üí (t ‚à© u).nonempty ‚Üí ¬¨P u) :
  induced_outer_measure m P0 m0 (s ‚à™ t) =
     induced_outer_measure m P0 m0 s + induced_outer_measure m P0 m0 t :=
of_function_union_of_top_of_nonempty_inter $ Œª u hsu htu, @infi_of_empty _ _ _ ‚ü®h u hsu htu‚ü© _

include msU m_mono
lemma induced_outer_measure_eq_extend' {s : set Œ±} (hs : P s) :
  induced_outer_measure m P0 m0 s = extend m s :=
of_function_eq s (Œª t, extend_mono' m_mono hs) (extend_Union_le_tsum_nat' PU msU)

lemma induced_outer_measure_eq' {s : set Œ±} (hs : P s) :
  induced_outer_measure m P0 m0 s = m s hs :=
(induced_outer_measure_eq_extend' PU msU m_mono hs).trans $ extend_eq _ _

lemma induced_outer_measure_eq_infi (s : set Œ±) :
  induced_outer_measure m P0 m0 s = ‚®Ö (t : set Œ±) (ht : P t) (h : s ‚äÜ t), m t ht :=
begin
  apply le_antisymm,
  { simp only [le_infi_iff], intros t ht, simp only [le_infi_iff], intro hs,
    refine le_trans (mono' _ hs) _,
    exact le_of_eq (induced_outer_measure_eq' _ msU m_mono _) },
  { refine le_infi _, intro f, refine le_infi _, intro hf,
    refine le_trans _ (extend_Union_le_tsum_nat' _ msU _),
    refine le_infi _, intro h2f,
    refine infi_le_of_le _ (infi_le_of_le h2f $ infi_le _ hf) }
end

lemma induced_outer_measure_preimage (f : Œ± ‚âÉ Œ±) (Pm : ‚àÄ (s : set Œ±), P (f ‚Åª¬π' s) ‚Üî P s)
  (mm : ‚àÄ (s : set Œ±) (hs : P s), m (f ‚Åª¬π' s) ((Pm _).mpr hs) = m s hs)
  {A : set Œ±} : induced_outer_measure m P0 m0 (f ‚Åª¬π' A) = induced_outer_measure m P0 m0 A :=
begin
  simp only [induced_outer_measure_eq_infi _ msU m_mono], symmetry,
  refine infi_congr (preimage f) f.injective.preimage_surjective _, intro s,
  refine infi_congr_Prop (Pm s) _, intro hs,
  refine infi_congr_Prop f.surjective.preimage_subset_preimage_iff _,
  intro h2s, exact mm s hs
end

lemma induced_outer_measure_exists_set {s : set Œ±}
  (hs : induced_outer_measure m P0 m0 s ‚â† ‚àû) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) :
  ‚àÉ (t : set Œ±) (ht : P t), s ‚äÜ t ‚àß
    induced_outer_measure m P0 m0 t ‚â§ induced_outer_measure m P0 m0 s + Œµ :=
begin
  have := ennreal.lt_add_right hs hŒµ,
  conv at this {to_lhs, rw induced_outer_measure_eq_infi _ msU m_mono },
  simp only [infi_lt_iff] at this,
  rcases this with ‚ü®t, h1t, h2t, h3t‚ü©,
  exact ‚ü®t, h1t, h2t,
    le_trans (le_of_eq $ induced_outer_measure_eq' _ msU m_mono h1t) (le_of_lt h3t)‚ü©
end

/-- To test whether `s` is Carath√©odory-measurable we only need to check the sets `t` for which
  `P t` holds. See `of_function_caratheodory` for another way to show the Carath√©odory-measurability
  of `s`.
-/
lemma induced_outer_measure_caratheodory (s : set Œ±) :
  (induced_outer_measure m P0 m0).caratheodory.measurable_set' s ‚Üî ‚àÄ (t : set Œ±), P t ‚Üí
  induced_outer_measure m P0 m0 (t ‚à© s) + induced_outer_measure m P0 m0 (t \ s) ‚â§
    induced_outer_measure m P0 m0 t :=
begin
  rw is_caratheodory_iff_le,
  split,
  { intros h t ht, exact h t },
  { intros h u, conv_rhs { rw induced_outer_measure_eq_infi _ msU m_mono },
    refine le_infi _, intro t, refine le_infi _, intro ht, refine le_infi _, intro h2t,
    refine le_trans _ (le_trans (h t ht) $ le_of_eq $ induced_outer_measure_eq' _ msU m_mono ht),
    refine add_le_add (mono' _ $ set.inter_subset_inter_left _ h2t)
      (mono' _  $ diff_subset_diff_left h2t) }
end

end extend_set

/-! If `P` is `measurable_set` for some measurable space, then we can remove some hypotheses of the
  above lemmas. -/
section measurable_space

variables {Œ± : Type*} [measurable_space Œ±]
variables {m : Œ† (s : set Œ±), measurable_set s ‚Üí ‚Ñù‚â•0‚àû}
variables (m0 : m ‚àÖ measurable_set.empty = 0)
variable (mU : ‚àÄ {{f : ‚Ñï ‚Üí set Œ±}} (hm : ‚àÄi, measurable_set (f i)), pairwise (disjoint on f) ‚Üí
  m (‚ãÉi, f i) (measurable_set.Union hm) = ‚àë'i, m (f i) (hm i))
include m0 mU

lemma extend_mono {s‚ÇÅ s‚ÇÇ : set Œ±} (h‚ÇÅ : measurable_set s‚ÇÅ) (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) :
  extend m s‚ÇÅ ‚â§ extend m s‚ÇÇ :=
begin
  refine le_infi _, intro h‚ÇÇ,
  have := extend_union measurable_set.empty m0 measurable_set.Union mU disjoint_diff
    h‚ÇÅ (h‚ÇÇ.diff h‚ÇÅ),
  rw union_diff_cancel hs at this,
  rw ‚Üê extend_eq m,
  exact le_iff_exists_add.2 ‚ü®_, this‚ü©,
end

lemma extend_Union_le_tsum_nat : ‚àÄ (s : ‚Ñï ‚Üí set Œ±), extend m (‚ãÉi, s i) ‚â§ ‚àë'i, extend m (s i) :=
begin
  refine extend_Union_le_tsum_nat' measurable_set.Union _, intros f h,
  simp [Union_disjointed.symm] {single_pass := tt},
  rw [mU (measurable_set.disjointed h) (disjoint_disjointed _)],
  refine ennreal.tsum_le_tsum (Œª i, _),
  rw [‚Üê extend_eq m, ‚Üê extend_eq m],
  exact extend_mono m0 mU (measurable_set.disjointed h _) (disjointed_le f _),
end

lemma induced_outer_measure_eq_extend {s : set Œ±} (hs : measurable_set s) :
  induced_outer_measure m measurable_set.empty m0 s = extend m s :=
of_function_eq s (Œª t, extend_mono m0 mU hs) (extend_Union_le_tsum_nat m0 mU)

lemma induced_outer_measure_eq {s : set Œ±} (hs : measurable_set s) :
  induced_outer_measure m measurable_set.empty m0 s = m s hs :=
(induced_outer_measure_eq_extend m0 mU hs).trans $ extend_eq _ _

end measurable_space

namespace outer_measure
variables {Œ± : Type*} [measurable_space Œ±] (m : outer_measure Œ±)

/-- Given an outer measure `m` we can forget its value on non-measurable sets, and then consider
  `m.trim`, the unique maximal outer measure less than that function. -/
def trim : outer_measure Œ± :=
induced_outer_measure (Œª s _, m s) measurable_set.empty m.empty

theorem le_trim : m ‚â§ m.trim :=
le_of_function.mpr $ Œª s, le_infi $ Œª _, le_refl _

theorem trim_eq {s : set Œ±} (hs : measurable_set s) : m.trim s = m s :=
induced_outer_measure_eq' measurable_set.Union (Œª f hf, m.Union_nat f) (Œª _ _ _ _ h, m.mono h) hs

theorem trim_congr {m‚ÇÅ m‚ÇÇ : outer_measure Œ±}
  (H : ‚àÄ {s : set Œ±}, measurable_set s ‚Üí m‚ÇÅ s = m‚ÇÇ s) :
  m‚ÇÅ.trim = m‚ÇÇ.trim :=
by { unfold trim, congr, funext s hs, exact H hs }

@[mono] theorem trim_mono : monotone (trim : outer_measure Œ± ‚Üí outer_measure Œ±) :=
Œª m‚ÇÅ m‚ÇÇ H s, binfi_le_binfi $ Œª f hs, ennreal.tsum_le_tsum $ Œª b, infi_le_infi $ Œª hf, H _

theorem le_trim_iff {m‚ÇÅ m‚ÇÇ : outer_measure Œ±} :
  m‚ÇÅ ‚â§ m‚ÇÇ.trim ‚Üî ‚àÄ s, measurable_set s ‚Üí m‚ÇÅ s ‚â§ m‚ÇÇ s :=
le_of_function.trans $ forall_congr $ Œª s, le_infi_iff

theorem trim_le_trim_iff {m‚ÇÅ m‚ÇÇ : outer_measure Œ±} :
  m‚ÇÅ.trim ‚â§ m‚ÇÇ.trim ‚Üî ‚àÄ s, measurable_set s ‚Üí m‚ÇÅ s ‚â§ m‚ÇÇ s :=
le_trim_iff.trans $ forall_congr $ Œª s, forall_congr $ Œª hs, by rw [trim_eq _ hs]

theorem trim_eq_trim_iff {m‚ÇÅ m‚ÇÇ : outer_measure Œ±} :
  m‚ÇÅ.trim = m‚ÇÇ.trim ‚Üî ‚àÄ s, measurable_set s ‚Üí m‚ÇÅ s = m‚ÇÇ s :=
by simp only [le_antisymm_iff, trim_le_trim_iff, forall_and_distrib]

theorem trim_eq_infi (s : set Œ±) : m.trim s = ‚®Ö t (st : s ‚äÜ t) (ht : measurable_set t), m t :=
by { simp only [infi_comm] {single_pass := tt}, exact induced_outer_measure_eq_infi
    measurable_set.Union (Œª f _, m.Union_nat f) (Œª _ _ _ _ h, m.mono h) s }

theorem trim_eq_infi' (s : set Œ±) : m.trim s = ‚®Ö t : {t // s ‚äÜ t ‚àß measurable_set t}, m t :=
by simp [infi_subtype, infi_and, trim_eq_infi]

theorem trim_trim (m : outer_measure Œ±) : m.trim.trim = m.trim :=
trim_eq_trim_iff.2 $ Œª s, m.trim_eq

@[simp] theorem trim_zero : (0 : outer_measure Œ±).trim = 0 :=
ext $ Œª s, le_antisymm
  (le_trans ((trim 0).mono (subset_univ s)) $
    le_of_eq $ trim_eq _ measurable_set.univ)
  (zero_le _)

theorem trim_sum_ge {Œπ} (m : Œπ ‚Üí outer_measure Œ±) : sum (Œª i, (m i).trim) ‚â§ (sum m).trim :=
Œª s, by simp [trim_eq_infi]; exact
Œª t st ht, ennreal.tsum_le_tsum (Œª i,
  infi_le_of_le t $ infi_le_of_le st $ infi_le _ ht)

lemma exists_measurable_superset_eq_trim (m : outer_measure Œ±) (s : set Œ±) :
  ‚àÉ t, s ‚äÜ t ‚àß measurable_set t ‚àß m t = m.trim s :=
begin
  simp only [trim_eq_infi], set ms := ‚®Ö (t : set Œ±) (st : s ‚äÜ t) (ht : measurable_set t), m t,
  by_cases hs : ms = ‚àû,
  { simp only [hs],
    simp only [infi_eq_top] at hs,
    exact ‚ü®univ, subset_univ s, measurable_set.univ, hs _ (subset_univ s) measurable_set.univ‚ü© },
  { have : ‚àÄ r > ms, ‚àÉ t, s ‚äÜ t ‚àß measurable_set t ‚àß m t < r,
    { intros r hs,
      simpa [infi_lt_iff] using hs },
    have : ‚àÄ n : ‚Ñï, ‚àÉ t, s ‚äÜ t ‚àß measurable_set t ‚àß m t < ms + n‚Åª¬π,
    { assume n,
      refine this _ (ennreal.lt_add_right hs _),
      simp },
    choose t hsub hm hm',
    refine ‚ü®‚ãÇ n, t n, subset_Inter hsub, measurable_set.Inter hm, _‚ü©,
    have : tendsto (Œª n : ‚Ñï, ms + n‚Åª¬π) at_top (ùìù (ms + 0)),
      from tendsto_const_nhds.add ennreal.tendsto_inv_nat_nhds_zero,
    rw add_zero at this,
    refine le_antisymm (ge_of_tendsto' this $ Œª n, _) _,
    { exact le_trans (m.mono' $ Inter_subset t n) (hm' n).le },
    { refine infi_le_of_le (‚ãÇ n, t n) _,
      refine infi_le_of_le (subset_Inter hsub) _,
      refine infi_le _ (measurable_set.Inter hm) } }
end

lemma exists_measurable_superset_of_trim_eq_zero
  {m : outer_measure Œ±} {s : set Œ±} (h : m.trim s = 0) :
  ‚àÉt, s ‚äÜ t ‚àß measurable_set t ‚àß m t = 0 :=
begin
  rcases exists_measurable_superset_eq_trim m s with ‚ü®t, hst, ht, hm‚ü©,
  exact ‚ü®t, hst, ht, h ‚ñ∏ hm‚ü©
end

/-- If `Œº i` is a countable family of outer measures, then for every set `s` there exists
a measurable set `t ‚äá s` such that `Œº i t = (Œº i).trim s` for all `i`. -/
lemma exists_measurable_superset_forall_eq_trim {Œπ} [encodable Œπ] (Œº : Œπ ‚Üí outer_measure Œ±)
  (s : set Œ±) : ‚àÉ t, s ‚äÜ t ‚àß measurable_set t ‚àß ‚àÄ i, Œº i t = (Œº i).trim s :=
begin
  choose t hst ht hŒºt using Œª i, (Œº i).exists_measurable_superset_eq_trim s,
  replace hst := subset_Inter hst,
  replace ht := measurable_set.Inter ht,
  refine ‚ü®‚ãÇ i, t i, hst, ht, Œª i, le_antisymm _ _‚ü©,
  exacts [hŒºt i ‚ñ∏ (Œº i).mono (Inter_subset _ _),
    (mono' _ hst).trans_eq ((Œº i).trim_eq ht)]
end

/-- If `m‚ÇÅ s = op (m‚ÇÇ s) (m‚ÇÉ s)` for all `s`, then the same is true for `m‚ÇÅ.trim`, `m‚ÇÇ.trim`,
and `m‚ÇÉ s`. -/
theorem trim_binop {m‚ÇÅ m‚ÇÇ m‚ÇÉ : outer_measure Œ±} {op : ‚Ñù‚â•0‚àû ‚Üí ‚Ñù‚â•0‚àû ‚Üí ‚Ñù‚â•0‚àû}
  (h : ‚àÄ s, m‚ÇÅ s = op (m‚ÇÇ s) (m‚ÇÉ s)) (s : set Œ±) :
  m‚ÇÅ.trim s = op (m‚ÇÇ.trim s) (m‚ÇÉ.trim s) :=
begin
  rcases exists_measurable_superset_forall_eq_trim (![m‚ÇÅ, m‚ÇÇ, m‚ÇÉ]) s
    with ‚ü®t, hst, ht, htm‚ü©,
  simp only [fin.forall_fin_succ, matrix.cons_val_zero, matrix.cons_val_succ] at htm,
  rw [‚Üê htm.1, ‚Üê htm.2.1, ‚Üê htm.2.2.1, h]
end

/-- If `m‚ÇÅ s = op (m‚ÇÇ s)` for all `s`, then the same is true for `m‚ÇÅ.trim` and `m‚ÇÇ.trim`. -/
theorem trim_op {m‚ÇÅ m‚ÇÇ : outer_measure Œ±} {op : ‚Ñù‚â•0‚àû ‚Üí ‚Ñù‚â•0‚àû}
  (h : ‚àÄ s, m‚ÇÅ s = op (m‚ÇÇ s)) (s : set Œ±) :
  m‚ÇÅ.trim s = op (m‚ÇÇ.trim s) :=
@trim_binop Œ± _ m‚ÇÅ m‚ÇÇ 0 (Œª a b, op a) h s

/-- `trim` is additive. -/
theorem trim_add (m‚ÇÅ m‚ÇÇ : outer_measure Œ±) : (m‚ÇÅ + m‚ÇÇ).trim = m‚ÇÅ.trim + m‚ÇÇ.trim :=
ext $ trim_binop (add_apply m‚ÇÅ m‚ÇÇ)

/-- `trim` respects scalar multiplication. -/
theorem trim_smul (c : ‚Ñù‚â•0‚àû) (m : outer_measure Œ±) :
  (c ‚Ä¢ m).trim = c ‚Ä¢ m.trim :=
ext $ trim_op (smul_apply c m)

/-- `trim` sends the supremum of two outer measures to the supremum of the trimmed measures. -/
theorem trim_sup (m‚ÇÅ m‚ÇÇ : outer_measure Œ±) : (m‚ÇÅ ‚äî m‚ÇÇ).trim = m‚ÇÅ.trim ‚äî m‚ÇÇ.trim :=
ext $ Œª s, (trim_binop (sup_apply m‚ÇÅ m‚ÇÇ) s).trans (sup_apply _ _ _).symm

/-- `trim` sends the supremum of a countable family of outer measures to the supremum
of the trimmed measures. -/
lemma trim_supr {Œπ} [encodable Œπ] (Œº : Œπ ‚Üí outer_measure Œ±) :
  trim (‚®Ü i, Œº i) = ‚®Ü i, trim (Œº i) :=
begin
  ext1 s,
  rcases exists_measurable_superset_forall_eq_trim (Œª o, option.elim o (supr Œº) Œº) s
    with ‚ü®t, hst, ht, hŒºt‚ü©,
  simp only [option.forall, option.elim] at hŒºt,
  simp only [supr_apply, ‚Üê hŒºt.1, ‚Üê hŒºt.2]
end

/-- The trimmed property of a measure Œº states that `Œº.to_outer_measure.trim = Œº.to_outer_measure`.
This theorem shows that a restricted trimmed outer measure is a trimmed outer measure. -/
lemma restrict_trim {Œº : outer_measure Œ±} {s : set Œ±} (hs : measurable_set s) :
  (restrict s Œº).trim = restrict s Œº.trim :=
begin
  refine le_antisymm (Œª t, _) (le_trim_iff.2 $ Œª t ht, _),
  { rw restrict_apply,
    rcases Œº.exists_measurable_superset_eq_trim (t ‚à© s) with ‚ü®t', htt', ht', hŒºt'‚ü©,
    rw [‚Üê hŒºt'], rw inter_subset at htt',
    refine (mono' _ htt').trans _,
    rw [trim_eq _ (hs.compl.union ht'), restrict_apply, union_inter_distrib_right,
      compl_inter_self, set.empty_union],
    exact Œº.mono' (inter_subset_left _ _) },
  { rw [restrict_apply, trim_eq _ (ht.inter hs), restrict_apply],
    exact le_rfl }
end

end outer_measure

end measure_theory
