/-
Copyright (c) 2021 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import measure_theory.measure.probability_measure
import measure_theory.measure.lebesgue

/-!
# Characterizations of weak convergence of finite measures and probability measures

This file will provide portmanteau characterizations of the weak convergence of finite measures
and of probability measures, i.e., the standard characterizations of convergence in distribution.

## Main definitions

This file does not introduce substantial new definitions: the topologies of weak convergence on
the types of finite measures and probability measures are already defined in their corresponding
files.

## Main results

The main result will be the portmanteau theorem providing various characterizations of the
weak convergence of measures. The separate implications are:
 * `measure_theory.finite_measure.limsup_measure_closed_le_of_tendsto` proves that weak convergence
   implies a limsup-condition for closed sets.
 * `measure_theory.limsup_measure_closed_le_iff_liminf_measure_open_ge` proves for probability
   measures the equivalence of the limsup condition for closed sets and the liminf condition for
   open sets.
 * `measure_theory.tendsto_measure_of_null_frontier` proves that the liminf condition for open
   sets (which is equivalent to the limsup condition for closed sets) implies the convergence of
   probabilities of sets whose boundary carries no mass under the limit measure.
 * `measure_theory.probability_measure.tendsto_measure_of_null_frontier_of_tendsto` is a
   combination of earlier implications, which shows that weak convergence of probability measures
   implies the convergence of probabilities of sets whose boundary carries no mass under the
   limit measure.

TODO:
 * Prove the rest of the implications.

## Implementation notes

Many of the characterizations of weak convergence hold for finite measures and are proven in that
generality and then specialized to probability measures. Some implications hold with slightly
weaker assumptions than usually stated. The full portmanteau theorem, however, is most convenient
for probability measures on metrizable spaces with their Borel sigmas.

Some specific considerations on the assumptions in the different implications:
 * `measure_theory.finite_measure.limsup_measure_closed_le_of_tendsto` assumes
   `pseudo_emetric_space`. The only reason is to have bounded continuous pointwise approximations
   to the indicator function of a closed set. Clearly for example metrizability or
   pseudo-emetrizability would be sufficient assumptions. The typeclass assumptions should be later
   adjusted in a way that takes into account use cases, but the proof will presumably remain
   essentially the same.
 * Where formulations are currently only provided for probability measures, one can obtain the
   finite measure formulations using the characterization of convergence of finite measures by
   their total masses and their probability-normalized versions, i.e., by
   `measure_theory.finite_measure.tendsto_normalize_iff_tendsto`.

## References

* [Billingsley, *Convergence of probability measures*][billingsley1999]

## Tags

weak convergence of measures, convergence in distribution, convergence in law, finite measure,
probability measure

-/

noncomputable theory
open measure_theory
open set
open filter
open bounded_continuous_function
open_locale topology ennreal nnreal bounded_continuous_function

namespace measure_theory

section limsup_closed_le_and_le_liminf_open
/-! ### Portmanteau: limsup condition for closed sets iff liminf condition for open sets

In this section we prove that for a sequence of Borel probability measures on a topological space
and its candidate limit measure, the following two conditions are equivalent:
  (C) For any closed set `F` in `Œ©` the limsup of the measures of `F` is at most the limit
      measure of `F`.
  (O) For any open set `G` in `Œ©` the liminf of the measures of `G` is at least the limit
      measure of `G`.
Either of these will later be shown to be equivalent to the weak convergence of the sequence
of measures.
-/

variables {Œ© : Type*} [measurable_space Œ©]

lemma le_measure_compl_liminf_of_limsup_measure_le
  {Œπ : Type*} {L : filter Œπ} {Œº : measure Œ©} {Œºs : Œπ ‚Üí measure Œ©}
  [is_probability_measure Œº] [‚àÄ i, is_probability_measure (Œºs i)]
  {E : set Œ©} (E_mble : measurable_set E) (h : L.limsup (Œª i, Œºs i E) ‚â§ Œº E) :
  Œº E·∂ú ‚â§ L.liminf (Œª i, Œºs i E·∂ú) :=
begin
  by_cases L_bot : L = ‚ä•,
  { simp only [L_bot, le_top,
      (show liminf (Œª i, Œºs i E·∂ú) ‚ä• = ‚ä§, by simp only [liminf, filter.map_bot, Liminf_bot])], },
  haveI : L.ne_bot, from {ne' := L_bot},
  have meas_Ec : Œº E·∂ú = 1 - Œº E,
  { simpa only [measure_univ] using measure_compl E_mble (measure_lt_top Œº E).ne, },
  have meas_i_Ec : ‚àÄ i, Œºs i E·∂ú = 1 - Œºs i E,
  { intro i,
    simpa only [measure_univ] using measure_compl E_mble (measure_lt_top (Œºs i) E).ne, },
  simp_rw [meas_Ec, meas_i_Ec],
  have obs : L.liminf (Œª (i : Œπ), 1 - Œºs i E) = L.liminf ((Œª x, 1 - x) ‚àò (Œª (i : Œπ), Œºs i E)),
    by refl,
  rw obs,
  simp_rw ‚Üê antitone_const_tsub.map_limsup_of_continuous_at (Œª i, Œºs i E)
            (ennreal.continuous_sub_left ennreal.one_ne_top).continuous_at,
  exact antitone_const_tsub h,
end

lemma le_measure_liminf_of_limsup_measure_compl_le
  {Œπ : Type*} {L : filter Œπ} {Œº : measure Œ©} {Œºs : Œπ ‚Üí measure Œ©}
  [is_probability_measure Œº] [‚àÄ i, is_probability_measure (Œºs i)]
  {E : set Œ©} (E_mble : measurable_set E) (h : L.limsup (Œª i, Œºs i E·∂ú) ‚â§ Œº E·∂ú) :
  Œº E ‚â§ L.liminf (Œª i, Œºs i E) :=
compl_compl E ‚ñ∏ (le_measure_compl_liminf_of_limsup_measure_le (measurable_set.compl E_mble) h)

lemma limsup_measure_compl_le_of_le_liminf_measure
  {Œπ : Type*} {L : filter Œπ} {Œº : measure Œ©} {Œºs : Œπ ‚Üí measure Œ©}
  [is_probability_measure Œº] [‚àÄ i, is_probability_measure (Œºs i)]
  {E : set Œ©} (E_mble : measurable_set E) (h : Œº E ‚â§ L.liminf (Œª i, Œºs i E)) :
  L.limsup (Œª i, Œºs i E·∂ú) ‚â§ Œº E·∂ú :=
begin
  by_cases L_bot : L = ‚ä•,
  { simp only [L_bot, bot_le,
      (show limsup (Œª i, Œºs i E·∂ú) ‚ä• = ‚ä•, by simp only [limsup, filter.map_bot, Limsup_bot])], },
  haveI : L.ne_bot, from {ne' := L_bot},
  have meas_Ec : Œº E·∂ú = 1 - Œº E,
  { simpa only [measure_univ] using measure_compl E_mble (measure_lt_top Œº E).ne, },
  have meas_i_Ec : ‚àÄ i, Œºs i E·∂ú = 1 - Œºs i E,
  { intro i,
    simpa only [measure_univ] using measure_compl E_mble (measure_lt_top (Œºs i) E).ne, },
  simp_rw [meas_Ec, meas_i_Ec],
  have obs : L.limsup (Œª (i : Œπ), 1 - Œºs i E) = L.limsup ((Œª x, 1 - x) ‚àò (Œª (i : Œπ), Œºs i E)),
    by refl,
  rw obs,
  simp_rw ‚Üê antitone_const_tsub.map_liminf_of_continuous_at (Œª i, Œºs i E)
            (ennreal.continuous_sub_left ennreal.one_ne_top).continuous_at,
  exact antitone_const_tsub h,
end

lemma limsup_measure_le_of_le_liminf_measure_compl
  {Œπ : Type*} {L : filter Œπ} {Œº : measure Œ©} {Œºs : Œπ ‚Üí measure Œ©}
  [is_probability_measure Œº] [‚àÄ i, is_probability_measure (Œºs i)]
  {E : set Œ©} (E_mble : measurable_set E) (h : Œº E·∂ú ‚â§ L.liminf (Œª i, Œºs i E·∂ú)) :
  L.limsup (Œª i, Œºs i E) ‚â§ Œº E :=
compl_compl E ‚ñ∏ (limsup_measure_compl_le_of_le_liminf_measure (measurable_set.compl E_mble) h)

variables [topological_space Œ©] [opens_measurable_space Œ©]

/-- One pair of implications of the portmanteau theorem:
For a sequence of Borel probability measures, the following two are equivalent:

(C) The limsup of the measures of any closed set is at most the measure of the closed set
under a candidate limit measure.

(O) The liminf of the measures of any open set is at least the measure of the open set
under a candidate limit measure.
-/
lemma limsup_measure_closed_le_iff_liminf_measure_open_ge
  {Œπ : Type*} {L : filter Œπ} {Œº : measure Œ©} {Œºs : Œπ ‚Üí measure Œ©}
  [is_probability_measure Œº] [‚àÄ i, is_probability_measure (Œºs i)] :
  (‚àÄ F, is_closed F ‚Üí L.limsup (Œª i, Œºs i F) ‚â§ Œº F)
    ‚Üî (‚àÄ G, is_open G ‚Üí Œº G ‚â§ L.liminf (Œª i, Œºs i G)) :=
begin
  split,
  { intros h G G_open,
    exact le_measure_liminf_of_limsup_measure_compl_le
          G_open.measurable_set (h G·∂ú (is_closed_compl_iff.mpr G_open)), },
  { intros h F F_closed,
    exact limsup_measure_le_of_le_liminf_measure_compl
          F_closed.measurable_set (h F·∂ú (is_open_compl_iff.mpr F_closed)), },
end

end limsup_closed_le_and_le_liminf_open -- section

section tendsto_of_null_frontier
/-! ### Portmanteau: limit of measures of Borel sets whose boundary carries no mass in the limit

In this section we prove that for a sequence of Borel probability measures on a topological space
and its candidate limit measure, either of the following equivalent conditions:
  (C) For any closed set `F` in `Œ©` the limsup of the measures of `F` is at most the limit
      measure of `F`
  (O) For any open set `G` in `Œ©` the liminf of the measures of `G` is at least the limit
      measure of `G`
implies that
  (B) For any Borel set `E` in `Œ©` whose boundary `‚àÇE` carries no mass under the candidate limit
      measure, we have that the limit of measures of `E` is the measure of `E` under the
      candidate limit measure.
-/

variables {Œ© : Type*} [measurable_space Œ©]

lemma tendsto_measure_of_le_liminf_measure_of_limsup_measure_le
  {Œπ : Type*} {L : filter Œπ} {Œº : measure Œ©} {Œºs : Œπ ‚Üí measure Œ©}
  {E‚ÇÄ E E‚ÇÅ : set Œ©} (E‚ÇÄ_subset : E‚ÇÄ ‚äÜ E) (subset_E‚ÇÅ : E ‚äÜ E‚ÇÅ) (nulldiff : Œº (E‚ÇÅ \ E‚ÇÄ) = 0)
  (h_E‚ÇÄ : Œº E‚ÇÄ ‚â§ L.liminf (Œª i, Œºs i E‚ÇÄ)) (h_E‚ÇÅ : L.limsup (Œª i, Œºs i E‚ÇÅ) ‚â§ Œº E‚ÇÅ) :
  L.tendsto (Œª i, Œºs i E) (ùìù (Œº E)) :=
begin
  apply tendsto_of_le_liminf_of_limsup_le,
  { have E‚ÇÄ_ae_eq_E : E‚ÇÄ =·µê[Œº] E,
      from eventually_le.antisymm E‚ÇÄ_subset.eventually_le
            (subset_E‚ÇÅ.eventually_le.trans (ae_le_set.mpr nulldiff)),
    calc  Œº(E)
        = Œº(E‚ÇÄ)                      : measure_congr E‚ÇÄ_ae_eq_E.symm
    ... ‚â§ L.liminf (Œª i, Œºs i E‚ÇÄ)    : h_E‚ÇÄ
    ... ‚â§ L.liminf (Œª i, Œºs i E)     : _,
    { refine liminf_le_liminf (eventually_of_forall (Œª _, measure_mono E‚ÇÄ_subset)) _,
      apply_auto_param, }, },
  { have E_ae_eq_E‚ÇÅ : E =·µê[Œº] E‚ÇÅ,
      from eventually_le.antisymm subset_E‚ÇÅ.eventually_le
            ((ae_le_set.mpr nulldiff).trans E‚ÇÄ_subset.eventually_le),
    calc  L.limsup (Œª i, Œºs i E)
        ‚â§ L.limsup (Œª i, Œºs i E‚ÇÅ)    : _
    ... ‚â§ Œº E‚ÇÅ                       : h_E‚ÇÅ
    ... = Œº E                        : measure_congr E_ae_eq_E‚ÇÅ.symm,
    { refine limsup_le_limsup (eventually_of_forall (Œª _, measure_mono subset_E‚ÇÅ)) _,
      apply_auto_param, }, },
end

variables [topological_space Œ©] [opens_measurable_space Œ©]

/-- One implication of the portmanteau theorem:
For a sequence of Borel probability measures, if the liminf of the measures of any open set is at
least the measure of the open set under a candidate limit measure, then for any set whose
boundary carries no probability mass under the candidate limit measure, then its measures under the
sequence converge to its measure under the candidate limit measure.
-/
lemma tendsto_measure_of_null_frontier
  {Œπ : Type*} {L : filter Œπ} {Œº : measure Œ©} {Œºs : Œπ ‚Üí measure Œ©}
  [is_probability_measure Œº] [‚àÄ i, is_probability_measure (Œºs i)]
  (h_opens : ‚àÄ G, is_open G ‚Üí Œº G ‚â§ L.liminf (Œª i, Œºs i G))
  {E : set Œ©} (E_nullbdry : Œº (frontier E) = 0) :
  L.tendsto (Œª i, Œºs i E) (ùìù (Œº E)) :=
begin
  have h_closeds : ‚àÄ F, is_closed F ‚Üí L.limsup (Œª i, Œºs i F) ‚â§ Œº F,
    from limsup_measure_closed_le_iff_liminf_measure_open_ge.mpr h_opens,
  exact tendsto_measure_of_le_liminf_measure_of_limsup_measure_le
        interior_subset subset_closure E_nullbdry
        (h_opens _ is_open_interior) (h_closeds _ is_closed_closure),
end

end tendsto_of_null_frontier --section

section convergence_implies_limsup_closed_le
/-! ### Portmanteau implication: weak convergence implies a limsup condition for closed sets

In this section we prove, under the assumption that the underlying topological space `Œ©` is
pseudo-emetrizable, that the weak convergence of measures on `measure_theory.finite_measure Œ©`
implies that for any closed set `F` in `Œ©` the limsup of the measures of `F` is at most the
limit measure of `F`. This is one implication of the portmanteau theorem characterizing weak
convergence of measures.

Combining with an earlier implication we also get that weak convergence implies that for any Borel
set `E` in `Œ©` whose boundary `‚àÇE` carries no mass under the limit measure, the limit of measures
of `E` is the measure of `E` under the limit measure.
-/

variables {Œ© : Type*} [measurable_space Œ©]

/-- If bounded continuous functions tend to the indicator of a measurable set and are
uniformly bounded, then their integrals against a finite measure tend to the measure of the set.
This formulation assumes:
 * the functions tend to a limit along a countably generated filter;
 * the limit is in the almost everywhere sense;
 * boundedness holds almost everywhere.
-/
lemma measure_of_cont_bdd_of_tendsto_filter_indicator {Œπ : Type*} {L : filter Œπ}
  [L.is_countably_generated] [topological_space Œ©] [opens_measurable_space Œ©]
  (Œº : measure Œ©) [is_finite_measure Œº] {c : ‚Ñù‚â•0} {E : set Œ©} (E_mble : measurable_set E)
  (fs : Œπ ‚Üí (Œ© ‚Üí·µá ‚Ñù‚â•0)) (fs_bdd : ‚àÄ·∂† i in L, ‚àÄ·µê (œâ : Œ©) ‚àÇŒº, fs i œâ ‚â§ c)
  (fs_lim : ‚àÄ·µê (œâ : Œ©) ‚àÇŒº,
            tendsto (Œª (i : Œπ), (coe_fn : (Œ© ‚Üí·µá ‚Ñù‚â•0) ‚Üí (Œ© ‚Üí ‚Ñù‚â•0)) (fs i) œâ) L
                    (ùìù (indicator E (Œª x, (1 : ‚Ñù‚â•0)) œâ))) :
  tendsto (Œª n, lintegral Œº (Œª œâ, fs n œâ)) L (ùìù (Œº E)) :=
begin
  convert finite_measure.tendsto_lintegral_nn_filter_of_le_const Œº fs_bdd fs_lim,
  have aux : ‚àÄ œâ, indicator E (Œª œâ, (1 : ‚Ñù‚â•0‚àû)) œâ = ‚Üë(indicator E (Œª œâ, (1 : ‚Ñù‚â•0)) œâ),
  from Œª œâ, by simp only [ennreal.coe_indicator, ennreal.coe_one],
  simp_rw [‚Üêaux, lintegral_indicator _ E_mble],
  simp only [lintegral_one, measure.restrict_apply, measurable_set.univ, univ_inter],
end

/-- If a sequence of bounded continuous functions tends to the indicator of a measurable set and
the functions are uniformly bounded, then their integrals against a finite measure tend to the
measure of the set.

A similar result with more general assumptions is
`measure_theory.measure_of_cont_bdd_of_tendsto_filter_indicator`.
-/
lemma measure_of_cont_bdd_of_tendsto_indicator
  [topological_space Œ©] [opens_measurable_space Œ©]
  (Œº : measure Œ©) [is_finite_measure Œº] {c : ‚Ñù‚â•0} {E : set Œ©} (E_mble : measurable_set E)
  (fs : ‚Ñï ‚Üí (Œ© ‚Üí·µá ‚Ñù‚â•0)) (fs_bdd : ‚àÄ n œâ, fs n œâ ‚â§ c)
  (fs_lim : tendsto (Œª (n : ‚Ñï), (coe_fn : (Œ© ‚Üí·µá ‚Ñù‚â•0) ‚Üí (Œ© ‚Üí ‚Ñù‚â•0)) (fs n))
            at_top (ùìù (indicator E (Œª x, (1 : ‚Ñù‚â•0))))) :
  tendsto (Œª n, lintegral Œº (Œª œâ, fs n œâ)) at_top (ùìù (Œº E)) :=
begin
  have fs_lim' : ‚àÄ œâ, tendsto (Œª (n : ‚Ñï), (fs n œâ : ‚Ñù‚â•0))
                 at_top (ùìù (indicator E (Œª x, (1 : ‚Ñù‚â•0)) œâ)),
  by { rw tendsto_pi_nhds at fs_lim, exact Œª œâ, fs_lim œâ, },
  apply measure_of_cont_bdd_of_tendsto_filter_indicator Œº E_mble fs
      (eventually_of_forall (Œª n, eventually_of_forall (fs_bdd n))) (eventually_of_forall fs_lim'),
end

/-- The integrals of thickened indicators of a closed set against a finite measure tend to the
measure of the closed set if the thickening radii tend to zero.
-/
lemma tendsto_lintegral_thickened_indicator_of_is_closed
  {Œ© : Type*} [measurable_space Œ©] [pseudo_emetric_space Œ©] [opens_measurable_space Œ©]
  (Œº : measure Œ©) [is_finite_measure Œº] {F : set Œ©} (F_closed : is_closed F) {Œ¥s : ‚Ñï ‚Üí ‚Ñù}
  (Œ¥s_pos : ‚àÄ n, 0 < Œ¥s n) (Œ¥s_lim : tendsto Œ¥s at_top (ùìù 0)) :
  tendsto (Œª n, lintegral Œº (Œª œâ, (thickened_indicator (Œ¥s_pos n) F œâ : ‚Ñù‚â•0‚àû)))
          at_top (ùìù (Œº F)) :=
begin
  apply measure_of_cont_bdd_of_tendsto_indicator Œº F_closed.measurable_set
          (Œª n, thickened_indicator (Œ¥s_pos n) F)
          (Œª n œâ, thickened_indicator_le_one (Œ¥s_pos n) F œâ),
  have key := thickened_indicator_tendsto_indicator_closure Œ¥s_pos Œ¥s_lim F,
  rwa F_closed.closure_eq at key,
end

/-- One implication of the portmanteau theorem:
Weak convergence of finite measures implies that the limsup of the measures of any closed set is
at most the measure of the closed set under the limit measure.
-/
lemma finite_measure.limsup_measure_closed_le_of_tendsto
  {Œ© Œπ : Type*} {L : filter Œπ}
  [measurable_space Œ©] [pseudo_emetric_space Œ©] [opens_measurable_space Œ©]
  {Œº : finite_measure Œ©} {Œºs : Œπ ‚Üí finite_measure Œ©}
  (Œºs_lim : tendsto Œºs L (ùìù Œº)) {F : set Œ©} (F_closed : is_closed F) :
  L.limsup (Œª i, (Œºs i : measure Œ©) F) ‚â§ (Œº : measure Œ©) F :=
begin
  by_cases L = ‚ä•,
  { simp only [h, limsup, filter.map_bot, Limsup_bot, ennreal.bot_eq_zero, zero_le], },
  apply ennreal.le_of_forall_pos_le_add,
  intros Œµ Œµ_pos Œº_F_finite,
  set Œ¥s := Œª (n : ‚Ñï), (1 : ‚Ñù) / (n+1) with def_Œ¥s,
  have Œ¥s_pos : ‚àÄ n, 0 < Œ¥s n, from Œª n, nat.one_div_pos_of_nat,
  have Œ¥s_lim : tendsto Œ¥s at_top (ùìù 0), from tendsto_one_div_add_at_top_nhds_0_nat,
  have key‚ÇÅ := tendsto_lintegral_thickened_indicator_of_is_closed
                  (Œº : measure Œ©) F_closed Œ¥s_pos Œ¥s_lim,
  have room‚ÇÅ : (Œº : measure Œ©) F < (Œº : measure Œ©) F + Œµ / 2,
  { apply ennreal.lt_add_right (measure_lt_top (Œº : measure Œ©) F).ne
          ((ennreal.div_pos_iff.mpr
              ‚ü®(ennreal.coe_pos.mpr Œµ_pos).ne.symm, ennreal.two_ne_top‚ü©).ne.symm), },
  rcases eventually_at_top.mp (eventually_lt_of_tendsto_lt room‚ÇÅ key‚ÇÅ) with ‚ü®M, hM‚ü©,
  have key‚ÇÇ := finite_measure.tendsto_iff_forall_lintegral_tendsto.mp
                Œºs_lim (thickened_indicator (Œ¥s_pos M) F),
  have room‚ÇÇ : lintegral (Œº : measure Œ©) (Œª a, thickened_indicator (Œ¥s_pos M) F a)
                < lintegral (Œº : measure Œ©) (Œª a, thickened_indicator (Œ¥s_pos M) F a) + Œµ / 2,
  { apply ennreal.lt_add_right
          (lintegral_lt_top_of_bounded_continuous_to_nnreal (Œº : measure Œ©) _).ne
          ((ennreal.div_pos_iff.mpr
              ‚ü®(ennreal.coe_pos.mpr Œµ_pos).ne.symm, ennreal.two_ne_top‚ü©).ne.symm), },
  have ev_near := eventually.mono (eventually_lt_of_tendsto_lt room‚ÇÇ key‚ÇÇ) (Œª n, le_of_lt),
  have aux := Œª n, le_trans (measure_le_lintegral_thickened_indicator
                            (Œºs n : measure Œ©) F_closed.measurable_set (Œ¥s_pos M)),
  have ev_near' := eventually.mono ev_near aux,
  apply (filter.limsup_le_limsup ev_near').trans,
  haveI : ne_bot L, from ‚ü®h‚ü©,
  rw limsup_const,
  apply le_trans (add_le_add (hM M rfl.le).le (le_refl (Œµ/2 : ‚Ñù‚â•0‚àû))),
  simp only [add_assoc, ennreal.add_halves, le_refl],
end

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that the limsup of the measures of any closed
set is at most the measure of the closed set under the limit probability measure.
-/
lemma probability_measure.limsup_measure_closed_le_of_tendsto
  {Œ© Œπ : Type*} {L : filter Œπ}
  [measurable_space Œ©] [pseudo_emetric_space Œ©] [opens_measurable_space Œ©]
  {Œº : probability_measure Œ©} {Œºs : Œπ ‚Üí probability_measure Œ©}
  (Œºs_lim : tendsto Œºs L (ùìù Œº)) {F : set Œ©} (F_closed : is_closed F) :
  L.limsup (Œª i, (Œºs i : measure Œ©) F) ‚â§ (Œº : measure Œ©) F :=
by apply finite_measure.limsup_measure_closed_le_of_tendsto
         ((probability_measure.tendsto_nhds_iff_to_finite_measures_tendsto_nhds L).mp Œºs_lim)
         F_closed

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that the liminf of the measures of any open set
is at least the measure of the open set under the limit probability measure.
-/
lemma probability_measure.le_liminf_measure_open_of_tendsto
  {Œ© Œπ : Type*} {L : filter Œπ}
  [measurable_space Œ©] [pseudo_emetric_space Œ©] [opens_measurable_space Œ©]
  {Œº : probability_measure Œ©} {Œºs : Œπ ‚Üí probability_measure Œ©}
  (Œºs_lim : tendsto Œºs L (ùìù Œº)) {G : set Œ©} (G_open : is_open G) :
  (Œº : measure Œ©) G ‚â§ L.liminf (Œª i, (Œºs i : measure Œ©) G) :=
begin
  have h_closeds : ‚àÄ F, is_closed F ‚Üí L.limsup (Œª i, (Œºs i : measure Œ©) F) ‚â§ (Œº : measure Œ©) F,
    from Œª F F_closed, probability_measure.limsup_measure_closed_le_of_tendsto Œºs_lim F_closed,
  exact le_measure_liminf_of_limsup_measure_compl_le
        G_open.measurable_set (h_closeds _ (is_closed_compl_iff.mpr G_open)),
end

lemma probability_measure.tendsto_measure_of_null_frontier_of_tendsto'
  {Œ© Œπ : Type*} {L : filter Œπ}
  [measurable_space Œ©] [pseudo_emetric_space Œ©] [opens_measurable_space Œ©]
  {Œº : probability_measure Œ©} {Œºs : Œπ ‚Üí probability_measure Œ©}
  (Œºs_lim : tendsto Œºs L (ùìù Œº)) {E : set Œ©} (E_nullbdry : (Œº : measure Œ©) (frontier E) = 0) :
  tendsto (Œª i, (Œºs i : measure Œ©) E) L (ùìù ((Œº : measure Œ©) E)) :=
begin
  have h_opens : ‚àÄ G, is_open G ‚Üí (Œº : measure Œ©) G ‚â§ L.liminf (Œª i, (Œºs i : measure Œ©) G),
    from Œª G G_open, probability_measure.le_liminf_measure_open_of_tendsto Œºs_lim G_open,
  exact tendsto_measure_of_null_frontier h_opens E_nullbdry,
end

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that if the boundary of a Borel set
carries no probability mass under the limit measure, then the limit of the measures of the set
equals the measure of the set under the limit probability measure.

A version with coercions to ordinary `‚Ñù‚â•0‚àû`-valued measures is
`measure_theory.probability_measure.tendsto_measure_of_null_frontier_of_tendsto'`.
-/
lemma probability_measure.tendsto_measure_of_null_frontier_of_tendsto
  {Œ© Œπ : Type*} {L : filter Œπ}
  [measurable_space Œ©] [pseudo_emetric_space Œ©] [opens_measurable_space Œ©]
  {Œº : probability_measure Œ©} {Œºs : Œπ ‚Üí probability_measure Œ©}
  (Œºs_lim : tendsto Œºs L (ùìù Œº)) {E : set Œ©} (E_nullbdry : Œº (frontier E) = 0) :
  tendsto (Œª i, Œºs i E) L (ùìù (Œº E)) :=
begin
  have E_nullbdry' : (Œº : measure Œ©) (frontier E) = 0,
    by rw [‚Üê probability_measure.ennreal_coe_fn_eq_coe_fn_to_measure, E_nullbdry, ennreal.coe_zero],
  have key := probability_measure.tendsto_measure_of_null_frontier_of_tendsto' Œºs_lim E_nullbdry',
  exact (ennreal.tendsto_to_nnreal (measure_ne_top ‚ÜëŒº E)).comp key,
end

end convergence_implies_limsup_closed_le --section

section limit_borel_implies_limsup_closed_le
/-! ### Portmanteau implication: limit condition for Borel sets implies limsup for closed sets

TODO: The proof of the implication is not yet here. Add it.
-/

variables {Œ© : Type*} [pseudo_emetric_space Œ©] [measurable_space Œ©] [opens_measurable_space Œ©]

lemma exists_null_frontier_thickening
  (Œº : measure Œ©) [sigma_finite Œº] (s : set Œ©) {a b : ‚Ñù} (hab : a < b) :
  ‚àÉ r ‚àà Ioo a b, Œº (frontier (metric.thickening r s)) = 0 :=
begin
  have mbles : ‚àÄ (r : ‚Ñù), measurable_set (frontier (metric.thickening r s)),
    from Œª r, (is_closed_frontier).measurable_set,
  have disjs := metric.frontier_thickening_disjoint s,
  have key := @measure.countable_meas_pos_of_disjoint_Union Œ© _ _ Œº _ _ mbles disjs,
  have aux := @measure_diff_null ‚Ñù _ volume (Ioo a b) _ (set.countable.measure_zero key volume),
  have len_pos : 0 < ennreal.of_real (b - a), by simp only [hab, ennreal.of_real_pos, sub_pos],
  rw [‚Üê real.volume_Ioo, ‚Üê aux] at len_pos,
  rcases nonempty_of_measure_ne_zero len_pos.ne.symm with ‚ü®r, ‚ü®r_in_Ioo, hr‚ü©‚ü©,
  refine ‚ü®r, r_in_Ioo, _‚ü©,
  simpa only [mem_set_of_eq, not_lt, le_zero_iff] using hr,
end

lemma exists_null_frontiers_thickening (Œº : measure Œ©) [sigma_finite Œº] (s : set Œ©) :
  ‚àÉ (rs : ‚Ñï ‚Üí ‚Ñù), tendsto rs at_top (ùìù 0) ‚àß
                  ‚àÄ n, 0 < rs n ‚àß Œº (frontier (metric.thickening (rs n) s)) = 0 :=
begin
  rcases exists_seq_strict_anti_tendsto (0 : ‚Ñù) with ‚ü®Rs, ‚ü®rubbish, ‚ü®Rs_pos, Rs_lim‚ü©‚ü©‚ü©,
  have obs := Œª (n : ‚Ñï), exists_null_frontier_thickening Œº s (Rs_pos n),
  refine ‚ü®(Œª (n : ‚Ñï), (obs n).some), ‚ü®_, _‚ü©‚ü©,
  { exact tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds Rs_lim
              (Œª n, (obs n).some_spec.some.1.le) (Œª n, (obs n).some_spec.some.2.le), },
  { exact Œª n, ‚ü®(obs n).some_spec.some.1, (obs n).some_spec.some_spec‚ü©, },
end

end limit_borel_implies_limsup_closed_le --section

end measure_theory --namespace
