/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import measure_theory.measure.probability_measure

noncomputable theory
open measure_theory
open set
open filter
open bounded_continuous_function
open_locale topological_space ennreal nnreal bounded_continuous_function

namespace measure_theory

section limsup_closed_le_and_le_liminf_open
/-! ### Portmanteau: limsup condition for closed sets iff liminf condition for open sets

In this section we prove that for a sequence of Borel probability measures on a topological space
and its candidate limit measure, the following two conditions are equivalent:
  (C) For any closed set `F` in `Î©` the limsup of the measures of `F` is at most the limit
      measure of `F`.
  (O) For any open set `G` in `Î©` the liminf of the measures of `G` is at least the limit
      measure of `G`.
Either of these will later be shown to be equivalent to the weak convergence of the sequence
of measures.
-/

variables {Î© : Type*} [measurable_space Î©]

lemma le_measure_compl_liminf_of_limsup_measure_le
  {Î¹ : Type*} {L : filter Î¹} {Î¼ : measure Î©} {Î¼s : Î¹ â†’ measure Î©}
  [is_probability_measure Î¼] [âˆ€ i, is_probability_measure (Î¼s i)]
  {E : set Î©} (E_mble : measurable_set E) (h : L.limsup (Î» i, Î¼s i E) â‰¤ Î¼ E) :
  Î¼ Eá¶œ â‰¤ L.liminf (Î» i, Î¼s i Eá¶œ) :=
begin
  by_cases L_bot : L = âŠ¥,
  { simp only [L_bot, le_top,
      (show liminf (Î» i, Î¼s i Eá¶œ) âŠ¥ = âŠ¤, by simp only [liminf, filter.map_bot, Liminf_bot])], },
  haveI : L.ne_bot, from {ne' := L_bot},
  have meas_Ec : Î¼ Eá¶œ = 1 - Î¼ E,
  { simpa only [measure_univ] using measure_compl E_mble (measure_lt_top Î¼ E).ne, },
  have meas_i_Ec : âˆ€ i, Î¼s i Eá¶œ = 1 - Î¼s i E,
  { intro i,
    simpa only [measure_univ] using measure_compl E_mble (measure_lt_top (Î¼s i) E).ne, },
  simp_rw [meas_Ec, meas_i_Ec],
  have obs : L.liminf (Î» (i : Î¹), 1 - Î¼s i E) = L.liminf ((Î» x, 1 - x) âˆ˜ (Î» (i : Î¹), Î¼s i E)),
    by refl,
  rw obs,
  simp_rw â† antitone_const_tsub.map_limsup_of_continuous_at (Î» i, Î¼s i E)
            (ennreal.continuous_sub_left ennreal.one_ne_top).continuous_at,
  exact antitone_const_tsub h,
end

lemma le_measure_liminf_of_limsup_measure_compl_le
  {Î¹ : Type*} {L : filter Î¹} {Î¼ : measure Î©} {Î¼s : Î¹ â†’ measure Î©}
  [is_probability_measure Î¼] [âˆ€ i, is_probability_measure (Î¼s i)]
  {E : set Î©} (E_mble : measurable_set E) (h : L.limsup (Î» i, Î¼s i Eá¶œ) â‰¤ Î¼ Eá¶œ) :
  Î¼ E â‰¤ L.liminf (Î» i, Î¼s i E) :=
compl_compl E â–¸ (le_measure_compl_liminf_of_limsup_measure_le (measurable_set.compl E_mble) h)

lemma limsup_measure_compl_le_of_le_liminf_measure
  {Î¹ : Type*} {L : filter Î¹} {Î¼ : measure Î©} {Î¼s : Î¹ â†’ measure Î©}
  [is_probability_measure Î¼] [âˆ€ i, is_probability_measure (Î¼s i)]
  {E : set Î©} (E_mble : measurable_set E) (h : Î¼ E â‰¤ L.liminf (Î» i, Î¼s i E)) :
  L.limsup (Î» i, Î¼s i Eá¶œ) â‰¤ Î¼ Eá¶œ :=
begin
  by_cases L_bot : L = âŠ¥,
  { simp only [L_bot, bot_le,
      (show limsup (Î» i, Î¼s i Eá¶œ) âŠ¥ = âŠ¥, by simp only [limsup, filter.map_bot, Limsup_bot])], },
  haveI : L.ne_bot, from {ne' := L_bot},
  have meas_Ec : Î¼ Eá¶œ = 1 - Î¼ E,
  { simpa only [measure_univ] using measure_compl E_mble (measure_lt_top Î¼ E).ne, },
  have meas_i_Ec : âˆ€ i, Î¼s i Eá¶œ = 1 - Î¼s i E,
  { intro i,
    simpa only [measure_univ] using measure_compl E_mble (measure_lt_top (Î¼s i) E).ne, },
  simp_rw [meas_Ec, meas_i_Ec],
  have obs : L.limsup (Î» (i : Î¹), 1 - Î¼s i E) = L.limsup ((Î» x, 1 - x) âˆ˜ (Î» (i : Î¹), Î¼s i E)),
    by refl,
  rw obs,
  simp_rw â† antitone_const_tsub.map_liminf_of_continuous_at (Î» i, Î¼s i E)
            (ennreal.continuous_sub_left ennreal.one_ne_top).continuous_at,
  exact antitone_const_tsub h,
end

lemma limsup_measure_le_of_le_liminf_measure_compl
  {Î¹ : Type*} {L : filter Î¹} {Î¼ : measure Î©} {Î¼s : Î¹ â†’ measure Î©}
  [is_probability_measure Î¼] [âˆ€ i, is_probability_measure (Î¼s i)]
  {E : set Î©} (E_mble : measurable_set E) (h : Î¼ Eá¶œ â‰¤ L.liminf (Î» i, Î¼s i Eá¶œ)) :
  L.limsup (Î» i, Î¼s i E) â‰¤ Î¼ E :=
compl_compl E â–¸ (limsup_measure_compl_le_of_le_liminf_measure (measurable_set.compl E_mble) h)

variables [topological_space Î©] [opens_measurable_space Î©]

/-- One pair of implications of the portmanteau theorem:
For a sequence of Borel probability measures, the following two are equivalent:

(C) The limsup of the measures of any closed set is at most the measure of the closed set
under a candidate limit measure.

(O) The liminf of the measures of any open set is at least the measure of the open set
under a candidate limit measure.
-/
lemma limsup_measure_closed_le_iff_liminf_measure_open_ge
  {Î¹ : Type*} {L : filter Î¹} {Î¼ : measure Î©} {Î¼s : Î¹ â†’ measure Î©}
  [is_probability_measure Î¼] [âˆ€ i, is_probability_measure (Î¼s i)] :
  (âˆ€ F, is_closed F â†’ L.limsup (Î» i, Î¼s i F) â‰¤ Î¼ F)
    â†” (âˆ€ G, is_open G â†’ Î¼ G â‰¤ L.liminf (Î» i, Î¼s i G)) :=
begin
  split,
  { intros h G G_open,
    exact le_measure_liminf_of_limsup_measure_compl_le
          G_open.measurable_set (h Gá¶œ (is_closed_compl_iff.mpr G_open)), },
  { intros h F F_closed,
    exact limsup_measure_le_of_le_liminf_measure_compl
          F_closed.measurable_set (h Fá¶œ (is_open_compl_iff.mpr F_closed)), },
end

end limsup_closed_le_and_le_liminf_open -- section

section tendsto_of_null_frontier
/-! ### Portmanteau: limit of measures of Borel sets whose boundary carries no mass in the limit

In this section we prove that for a sequence of Borel probability measures on a topological space
and its candidate limit measure, either of the following equivalent conditions:
  (C) For any closed set `F` in `Î©` the limsup of the measures of `F` is at most the limit
      measure of `F`
  (O) For any open set `G` in `Î©` the liminf of the measures of `G` is at least the limit
      measure of `G`
implies that
  (B) For any Borel set `E` in `Î©` whose boundary `âˆ‚E` carries no mass under the candidate limit
      measure, we have that the limit of measures of `E` is the measure of `E` under the
      candidate limit measure.
-/

variables {Î© : Type*} [measurable_space Î©]

lemma tendsto_measure_of_le_liminf_measure_of_limsup_measure_le
  {Î¹ : Type*} {L : filter Î¹} {Î¼ : measure Î©} {Î¼s : Î¹ â†’ measure Î©}
  {Eâ‚€ E Eâ‚ : set Î©} (Eâ‚€_subset : Eâ‚€ âŠ† E) (subset_Eâ‚ : E âŠ† Eâ‚) (nulldiff : Î¼ (Eâ‚ \ Eâ‚€) = 0)
  (h_Eâ‚€ : Î¼ Eâ‚€ â‰¤ L.liminf (Î» i, Î¼s i Eâ‚€)) (h_Eâ‚ : L.limsup (Î» i, Î¼s i Eâ‚) â‰¤ Î¼ Eâ‚) :
  L.tendsto (Î» i, Î¼s i E) (ğ“ (Î¼ E)) :=
begin
  apply tendsto_of_le_liminf_of_limsup_le,
  { have Eâ‚€_ae_eq_E : Eâ‚€ =áµ[Î¼] E,
      from eventually_le.antisymm Eâ‚€_subset.eventually_le
            (subset_Eâ‚.eventually_le.trans (ae_le_set.mpr nulldiff)),
    calc  Î¼(E)
        = Î¼(Eâ‚€)                      : measure_congr Eâ‚€_ae_eq_E.symm
    ... â‰¤ L.liminf (Î» i, Î¼s i Eâ‚€)    : h_Eâ‚€
    ... â‰¤ L.liminf (Î» i, Î¼s i E)     : _,
    { refine liminf_le_liminf (eventually_of_forall (Î» _, measure_mono Eâ‚€_subset)) _,
      apply_auto_param, }, },
  { have E_ae_eq_Eâ‚ : E =áµ[Î¼] Eâ‚,
      from eventually_le.antisymm subset_Eâ‚.eventually_le
            ((ae_le_set.mpr nulldiff).trans Eâ‚€_subset.eventually_le),
    calc  L.limsup (Î» i, Î¼s i E)
        â‰¤ L.limsup (Î» i, Î¼s i Eâ‚)    : _
    ... â‰¤ Î¼ Eâ‚                       : h_Eâ‚
    ... = Î¼ E                        : measure_congr E_ae_eq_Eâ‚.symm,
    { refine limsup_le_limsup (eventually_of_forall (Î» _, measure_mono subset_Eâ‚)) _,
      apply_auto_param, }, },
end

variables [topological_space Î©] [opens_measurable_space Î©]

/-- One implication of the portmanteau theorem:
For a sequence of Borel probability measures, if the liminf of the measures of any open set is at
least the measure of the open set under a candidate limit measure, then for any set whose
boundary carries no probability mass under the candidate limit measure, then its measures under the
sequence converge to its measure under the candidate limit measure.
-/
lemma tendsto_measure_of_null_frontier
  {Î¹ : Type*} {L : filter Î¹} {Î¼ : measure Î©} {Î¼s : Î¹ â†’ measure Î©}
  [is_probability_measure Î¼] [âˆ€ i, is_probability_measure (Î¼s i)]
  (h_opens : âˆ€ G, is_open G â†’ Î¼ G â‰¤ L.liminf (Î» i, Î¼s i G))
  {E : set Î©} (E_nullbdry : Î¼ (frontier E) = 0) :
  L.tendsto (Î» i, Î¼s i E) (ğ“ (Î¼ E)) :=
begin
  have h_closeds : âˆ€ F, is_closed F â†’ L.limsup (Î» i, Î¼s i F) â‰¤ Î¼ F,
    from limsup_measure_closed_le_iff_liminf_measure_open_ge.mpr h_opens,
  exact tendsto_measure_of_le_liminf_measure_of_limsup_measure_le
        interior_subset subset_closure E_nullbdry
        (h_opens _ is_open_interior) (h_closeds _ is_closed_closure),
end

end tendsto_of_null_frontier --section

section convergence_implies_limsup_closed_le
/-! ### Portmanteau implication: weak convergence implies a limsup condition for closed sets

In this section we prove, under the assumption that the underlying topological space `Î©` is
pseudo-emetrizable, that the weak convergence of measures on `measure_theory.finite_measure Î©`
implies that for any closed set `F` in `Î©` the limsup of the measures of `F` is at most the
limit measure of `F`. This is one implication of the portmanteau theorem characterizing weak
convergence of measures.

Combining with an earlier implication we also get that weak convergence implies that for any Borel
set `E` in `Î©` whose boundary `âˆ‚E` carries no mass under the limit measure, the limit of measures
of `E` is the measure of `E` under the limit measure.
-/

variables {Î© : Type*} [measurable_space Î©]

/-- If bounded continuous functions tend to the indicator of a measurable set and are
uniformly bounded, then their integrals against a finite measure tend to the measure of the set.
This formulation assumes:
 * the functions tend to a limit along a countably generated filter;
 * the limit is in the almost everywhere sense;
 * boundedness holds almost everywhere.
-/
lemma measure_of_cont_bdd_of_tendsto_filter_indicator {Î¹ : Type*} {L : filter Î¹}
  [L.is_countably_generated] [topological_space Î©] [opens_measurable_space Î©]
  (Î¼ : measure Î©) [is_finite_measure Î¼] {c : â„â‰¥0} {E : set Î©} (E_mble : measurable_set E)
  (fs : Î¹ â†’ (Î© â†’áµ‡ â„â‰¥0)) (fs_bdd : âˆ€á¶  i in L, âˆ€áµ (Ï‰ : Î©) âˆ‚Î¼, fs i Ï‰ â‰¤ c)
  (fs_lim : âˆ€áµ (Ï‰ : Î©) âˆ‚Î¼,
            tendsto (Î» (i : Î¹), (coe_fn : (Î© â†’áµ‡ â„â‰¥0) â†’ (Î© â†’ â„â‰¥0)) (fs i) Ï‰) L
                    (ğ“ (indicator E (Î» x, (1 : â„â‰¥0)) Ï‰))) :
  tendsto (Î» n, lintegral Î¼ (Î» Ï‰, fs n Ï‰)) L (ğ“ (Î¼ E)) :=
begin
  convert finite_measure.tendsto_lintegral_nn_filter_of_le_const Î¼ fs_bdd fs_lim,
  have aux : âˆ€ Ï‰, indicator E (Î» Ï‰, (1 : â„â‰¥0âˆ)) Ï‰ = â†‘(indicator E (Î» Ï‰, (1 : â„â‰¥0)) Ï‰),
  from Î» Ï‰, by simp only [ennreal.coe_indicator, ennreal.coe_one],
  simp_rw [â†aux, lintegral_indicator _ E_mble],
  simp only [lintegral_one, measure.restrict_apply, measurable_set.univ, univ_inter],
end

/-- If a sequence of bounded continuous functions tends to the indicator of a measurable set and
the functions are uniformly bounded, then their integrals against a finite measure tend to the
measure of the set.

A similar result with more general assumptions is
`measure_theory.measure_of_cont_bdd_of_tendsto_filter_indicator`.
-/
lemma measure_of_cont_bdd_of_tendsto_indicator
  [topological_space Î©] [opens_measurable_space Î©]
  (Î¼ : measure Î©) [is_finite_measure Î¼] {c : â„â‰¥0} {E : set Î©} (E_mble : measurable_set E)
  (fs : â„• â†’ (Î© â†’áµ‡ â„â‰¥0)) (fs_bdd : âˆ€ n Ï‰, fs n Ï‰ â‰¤ c)
  (fs_lim : tendsto (Î» (n : â„•), (coe_fn : (Î© â†’áµ‡ â„â‰¥0) â†’ (Î© â†’ â„â‰¥0)) (fs n))
            at_top (ğ“ (indicator E (Î» x, (1 : â„â‰¥0))))) :
  tendsto (Î» n, lintegral Î¼ (Î» Ï‰, fs n Ï‰)) at_top (ğ“ (Î¼ E)) :=
begin
  have fs_lim' : âˆ€ Ï‰, tendsto (Î» (n : â„•), (fs n Ï‰ : â„â‰¥0))
                 at_top (ğ“ (indicator E (Î» x, (1 : â„â‰¥0)) Ï‰)),
  by { rw tendsto_pi_nhds at fs_lim, exact Î» Ï‰, fs_lim Ï‰, },
  apply measure_of_cont_bdd_of_tendsto_filter_indicator Î¼ E_mble fs
      (eventually_of_forall (Î» n, eventually_of_forall (fs_bdd n))) (eventually_of_forall fs_lim'),
end

/-- The integrals of thickened indicators of a closed set against a finite measure tend to the
measure of the closed set if the thickening radii tend to zero.
-/
lemma tendsto_lintegral_thickened_indicator_of_is_closed
  {Î© : Type*} [measurable_space Î©] [pseudo_emetric_space Î©] [opens_measurable_space Î©]
  (Î¼ : measure Î©) [is_finite_measure Î¼] {F : set Î©} (F_closed : is_closed F) {Î´s : â„• â†’ â„}
  (Î´s_pos : âˆ€ n, 0 < Î´s n) (Î´s_lim : tendsto Î´s at_top (ğ“ 0)) :
  tendsto (Î» n, lintegral Î¼ (Î» Ï‰, (thickened_indicator (Î´s_pos n) F Ï‰ : â„â‰¥0âˆ)))
          at_top (ğ“ (Î¼ F)) :=
begin
  apply measure_of_cont_bdd_of_tendsto_indicator Î¼ F_closed.measurable_set
          (Î» n, thickened_indicator (Î´s_pos n) F)
          (Î» n Ï‰, thickened_indicator_le_one (Î´s_pos n) F Ï‰),
  have key := thickened_indicator_tendsto_indicator_closure Î´s_pos Î´s_lim F,
  rwa F_closed.closure_eq at key,
end

/-- One implication of the portmanteau theorem:
Weak convergence of finite measures implies that the limsup of the measures of any closed set is
at most the measure of the closed set under the limit measure.
-/
lemma finite_measure.limsup_measure_closed_le_of_tendsto
  {Î© Î¹ : Type*} {L : filter Î¹}
  [measurable_space Î©] [pseudo_emetric_space Î©] [opens_measurable_space Î©]
  {Î¼ : finite_measure Î©} {Î¼s : Î¹ â†’ finite_measure Î©}
  (Î¼s_lim : tendsto Î¼s L (ğ“ Î¼)) {F : set Î©} (F_closed : is_closed F) :
  L.limsup (Î» i, (Î¼s i : measure Î©) F) â‰¤ (Î¼ : measure Î©) F :=
begin
  by_cases L = âŠ¥,
  { simp only [h, limsup, filter.map_bot, Limsup_bot, ennreal.bot_eq_zero, zero_le], },
  apply ennreal.le_of_forall_pos_le_add,
  intros Îµ Îµ_pos Î¼_F_finite,
  set Î´s := Î» (n : â„•), (1 : â„) / (n+1) with def_Î´s,
  have Î´s_pos : âˆ€ n, 0 < Î´s n, from Î» n, nat.one_div_pos_of_nat,
  have Î´s_lim : tendsto Î´s at_top (ğ“ 0), from tendsto_one_div_add_at_top_nhds_0_nat,
  have keyâ‚ := tendsto_lintegral_thickened_indicator_of_is_closed
                  (Î¼ : measure Î©) F_closed Î´s_pos Î´s_lim,
  have roomâ‚ : (Î¼ : measure Î©) F < (Î¼ : measure Î©) F + Îµ / 2,
  { apply ennreal.lt_add_right (measure_lt_top (Î¼ : measure Î©) F).ne
          ((ennreal.div_pos_iff.mpr
              âŸ¨(ennreal.coe_pos.mpr Îµ_pos).ne.symm, ennreal.two_ne_topâŸ©).ne.symm), },
  rcases eventually_at_top.mp (eventually_lt_of_tendsto_lt roomâ‚ keyâ‚) with âŸ¨M, hMâŸ©,
  have keyâ‚‚ := finite_measure.tendsto_iff_forall_lintegral_tendsto.mp
                Î¼s_lim (thickened_indicator (Î´s_pos M) F),
  have roomâ‚‚ : lintegral (Î¼ : measure Î©) (Î» a, thickened_indicator (Î´s_pos M) F a)
                < lintegral (Î¼ : measure Î©) (Î» a, thickened_indicator (Î´s_pos M) F a) + Îµ / 2,
  { apply ennreal.lt_add_right
          (lintegral_lt_top_of_bounded_continuous_to_nnreal (Î¼ : measure Î©) _).ne
          ((ennreal.div_pos_iff.mpr
              âŸ¨(ennreal.coe_pos.mpr Îµ_pos).ne.symm, ennreal.two_ne_topâŸ©).ne.symm), },
  have ev_near := eventually.mono (eventually_lt_of_tendsto_lt roomâ‚‚ keyâ‚‚) (Î» n, le_of_lt),
  have aux := Î» n, le_trans (measure_le_lintegral_thickened_indicator
                            (Î¼s n : measure Î©) F_closed.measurable_set (Î´s_pos M)),
  have ev_near' := eventually.mono ev_near aux,
  apply (filter.limsup_le_limsup ev_near').trans,
  haveI : ne_bot L, from âŸ¨hâŸ©,
  rw limsup_const,
  apply le_trans (add_le_add (hM M rfl.le).le (le_refl (Îµ/2 : â„â‰¥0âˆ))),
  simp only [add_assoc, ennreal.add_halves, le_refl],
end

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that the limsup of the measures of any closed
set is at most the measure of the closed set under the limit probability measure.
-/
lemma probability_measure.limsup_measure_closed_le_of_tendsto
  {Î© Î¹ : Type*} {L : filter Î¹}
  [measurable_space Î©] [pseudo_emetric_space Î©] [opens_measurable_space Î©]
  {Î¼ : probability_measure Î©} {Î¼s : Î¹ â†’ probability_measure Î©}
  (Î¼s_lim : tendsto Î¼s L (ğ“ Î¼)) {F : set Î©} (F_closed : is_closed F) :
  L.limsup (Î» i, (Î¼s i : measure Î©) F) â‰¤ (Î¼ : measure Î©) F :=
by apply finite_measure.limsup_measure_closed_le_of_tendsto
         ((probability_measure.tendsto_nhds_iff_to_finite_measures_tendsto_nhds L).mp Î¼s_lim)
         F_closed

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that the liminf of the measures of any open set
is at least the measure of the open set under the limit probability measure.
-/
lemma probability_measure.le_liminf_measure_open_of_tendsto
  {Î© Î¹ : Type*} {L : filter Î¹}
  [measurable_space Î©] [pseudo_emetric_space Î©] [opens_measurable_space Î©]
  {Î¼ : probability_measure Î©} {Î¼s : Î¹ â†’ probability_measure Î©}
  (Î¼s_lim : tendsto Î¼s L (ğ“ Î¼)) {G : set Î©} (G_open : is_open G) :
  (Î¼ : measure Î©) G â‰¤ L.liminf (Î» i, (Î¼s i : measure Î©) G) :=
begin
  have h_closeds : âˆ€ F, is_closed F â†’ L.limsup (Î» i, (Î¼s i : measure Î©) F) â‰¤ (Î¼ : measure Î©) F,
    from Î» F F_closed, probability_measure.limsup_measure_closed_le_of_tendsto Î¼s_lim F_closed,
  exact le_measure_liminf_of_limsup_measure_compl_le
        G_open.measurable_set (h_closeds _ (is_closed_compl_iff.mpr G_open)),
end

lemma probability_measure.tendsto_measure_of_null_frontier_of_tendsto'
  {Î© Î¹ : Type*} {L : filter Î¹}
  [measurable_space Î©] [pseudo_emetric_space Î©] [opens_measurable_space Î©]
  {Î¼ : probability_measure Î©} {Î¼s : Î¹ â†’ probability_measure Î©}
  (Î¼s_lim : tendsto Î¼s L (ğ“ Î¼)) {E : set Î©} (E_nullbdry : (Î¼ : measure Î©) (frontier E) = 0) :
  tendsto (Î» i, (Î¼s i : measure Î©) E) L (ğ“ ((Î¼ : measure Î©) E)) :=
begin
  have h_opens : âˆ€ G, is_open G â†’ (Î¼ : measure Î©) G â‰¤ L.liminf (Î» i, (Î¼s i : measure Î©) G),
    from Î» G G_open, probability_measure.le_liminf_measure_open_of_tendsto Î¼s_lim G_open,
  exact tendsto_measure_of_null_frontier h_opens E_nullbdry,
end

/-- One implication of the portmanteau theorem:
Weak convergence of probability measures implies that if the boundary of a Borel set
carries no probability mass under the limit measure, then the limit of the measures of the set
equals the measure of the set under the limit probability measure.

A version with coercions to ordinary `â„â‰¥0âˆ`-valued measures is
`measure_theory.probability_measure.tendsto_measure_of_null_frontier_of_tendsto'`.
-/
lemma probability_measure.tendsto_measure_of_null_frontier_of_tendsto
  {Î© Î¹ : Type*} {L : filter Î¹}
  [measurable_space Î©] [pseudo_emetric_space Î©] [opens_measurable_space Î©]
  {Î¼ : probability_measure Î©} {Î¼s : Î¹ â†’ probability_measure Î©}
  (Î¼s_lim : tendsto Î¼s L (ğ“ Î¼)) {E : set Î©} (E_nullbdry : Î¼ (frontier E) = 0) :
  tendsto (Î» i, Î¼s i E) L (ğ“ (Î¼ E)) :=
begin
  have E_nullbdry' : (Î¼ : measure Î©) (frontier E) = 0,
    by rw [â† probability_measure.ennreal_coe_fn_eq_coe_fn_to_measure, E_nullbdry, ennreal.coe_zero],
  have key := probability_measure.tendsto_measure_of_null_frontier_of_tendsto' Î¼s_lim E_nullbdry',
  exact (ennreal.tendsto_to_nnreal (measure_ne_top â†‘Î¼ E)).comp key,
end

end convergence_implies_limsup_closed_le --section

end measure_theory --namespace
