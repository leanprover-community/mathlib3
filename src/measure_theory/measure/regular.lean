/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris Van Doorn, Yury Kudryashov
-/

import measure_theory.constructions.borel_space

/-!
# Regular measures

A measure is `outer_regular` if the measure of any measurable set `A` is the infimum of `Î¼ U` over
all open sets `U` containing `A`.

A measure is `regular` if it satisfies the following properties:
* it is finite on compact sets;
* it is outer regular;
* it is inner regular for open sets with respect to compacts sets: the measure of any open set `U`
  is the supremum of `Î¼ K` over all compact sets `K` contained in `U`.

A measure is `weakly_regular` if it satisfies the following properties:
* it is outer regular;
* it is inner regular for open sets with respect to closed sets: the measure of any open set `U`
  is the supremum of `Î¼ F` over all compact sets `F` contained in `U`.

In a Hausdorff topological space, regularity implies weak regularity. These three conditions are
registered as typeclasses for a measure `Î¼`, and this implication is recorded as an instance.

In order to avoid code duplication, we also define a measure `Î¼` to be `inner_regular` for sets
satisfying a predicate `q` with respect to sets satisfying a predicate `p` if for any set
`U âˆˆ {U | q U}` and a number `r < Î¼ U` there exists `F âŠ† U` such that `p F` and `r < Î¼ F`.

We prove that inner regularity for open sets with respect to compact sets or closed sets implies
inner regularity for all measurable sets of finite measure (with respect to
compact sets or closed sets respectively), and register some corollaries for (weakly) regular
measures.

Note that a similar statement for measurable sets of infinite mass can fail. For a counterexample,
consider the group `â„ Ã— â„` where the first factor has the discrete topology and the second one the
usual topology. It is a locally compact Hausdorff topological group, with Haar measure equal to
Lebesgue measure on each vertical fiber. The set `â„ Ã— {0}` has infinite measure (by outer
regularity), but any compact set it contains has zero measure (as it is finite).

Several authors require as a definition of regularity that all measurable sets are inner regular.
We have opted for the slightly weaker definition above as it holds for all Haar measures, it is
enough for essentially all applications, and it is equivalent to the other definition when the
measure is finite.

The interest of the notion of weak regularity is that it is enough for many applications, and it
is automatically satisfied by any finite measure on a metric space.

## Main definitions

* `measure_theory.measure.outer_regular Î¼`: a typeclass registering that a measure `Î¼` on a
  topological space is outer regular.
* `measure_theory.measure.regular Î¼`: a typeclass registering that a measure `Î¼` on a topological
  space is regular.
* `measure_theory.measure.weakly_regular Î¼`: a typeclass registering that a measure `Î¼` on a
  topological space is weakly regular.
* `measure_theory.measure.inner_regular Î¼ p q`: a non-typeclass predicate saying that a measure `Î¼`
  is inner regular for sets satisfying `q` with respect to sets satisfying `p`.

## Main results

### Outer regular measures

* `set.measure_eq_infi_is_open` asserts that, when `Î¼` is outer regular, the measure of a
  set is the infimum of the measure of open sets containing it.
* `set.exists_is_open_lt_of_lt'` asserts that, when `Î¼` is outer regular, for every set `s`
  and `r > Î¼ s` there exists an open superset `U âŠ‡ s` of measure less than `r`.
* push forward of an outer regular measure is outer regular, and scalar multiplication of a regular
  measure by a finite number is outer regular.
* `measure_theory.measure.outer_regular.of_sigma_compact_space_of_is_locally_finite_measure`:
  a locally finite measure on a `Ïƒ`-compact metric (or even pseudo emetric) space is outer regular.

### Weakly regular measures

* `is_open.measure_eq_supr_is_closed` asserts that the measure of an open set is the supremum of
  the measure of closed sets it contains.
* `is_open.exists_lt_is_closed`: for an open set `U` and `r < Î¼ U`, there exists a closed `F âŠ† U`
  of measure greater than `r`;
* `measurable_set.measure_eq_supr_is_closed_of_ne_top` asserts that the measure of a measurable set
  of finite measure is the supremum of the measure of closed sets it contains.
*  `measurable_set.exists_lt_is_closed_of_ne_top` and `measurable_set.exists_is_closed_lt_add`:
  a measurable set of finite measure can be approximated by a closed subset (stated as
  `r < Î¼ F` and `Î¼ s < Î¼ F + Îµ`, respectively).
* `measure_theory.measure.weakly_regular.of_pseudo_emetric_space_of_is_finite_measure` is an
  instance registering that a finite measure on a metric space is weakly regular (in fact, a pseudo
  emetric space is enough);
* `measure_theory.measure.weakly_regular.of_pseudo_emetric_sigma_compact_space_of_locally_finite`
  is an instance registering that a locally finite measure on a `Ïƒ`-compact metric space (or even
  a pseudo emetric space) is weakly regular.

### Regular measures

* `is_open.measure_eq_supr_is_compact` asserts that the measure of an open set is the supremum of
  the measure of compact sets it contains.
* `is_open.exists_lt_is_compact`: for an open set `U` and `r < Î¼ U`, there exists a compact `K âŠ† U`
  of measure greater than `r`;
* `measurable_set.measure_eq_supr_is_compact_of_ne_top` asserts that the measure of a measurable set
  of finite measure is the supremum of the measure of compact sets it contains.
*  `measurable_set.exists_lt_is_compact_of_ne_top` and `measurable_set.exists_is_compact_lt_add`:
  a measurable set of finite measure can be approximated by a compact subset (stated as
  `r < Î¼ K` and `Î¼ s < Î¼ K + Îµ`, respectively).
* `measure_theory.measure.regular.of_sigma_compact_space_of_is_locally_finite_measure` is an
  instance registering that a locally finite measure on a `Ïƒ`-compact metric space is regular (in
  fact, an emetric space is enough).

## Implementation notes

The main nontrivial statement is `measure_theory.measure.inner_regular.weakly_regular_of_finite`,
expressing that in a finite measure space, if every open set can be approximated from inside by
closed sets, then the measure is in fact weakly regular. To prove that we show that any measurable
set can be approximated from inside by closed sets and from outside by open sets. This statement is
proved by measurable induction, starting from open sets and checking that it is stable by taking
complements (this is the point of this condition, being symmetrical between inside and outside) and
countable disjoint unions.

Once this statement is proved, one deduces results for `Ïƒ`-finite measures from this statement, by
restricting them to finite measure sets (and proving that this restriction is weakly regular, using
again the same statement).

## References

[Halmos, Measure Theory, Â§52][halmos1950measure]. Note that Halmos uses an unusual definition of
Borel sets (for him, they are elements of the `Ïƒ`-algebra generated by compact sets!), so his
proofs or statements do not apply directly.

[Billingsley, Convergence of Probability Measures][billingsley1999]
-/

open set filter
open_locale ennreal topological_space nnreal big_operators

namespace measure_theory
namespace measure

/-- We say that a measure `Î¼` is *inner regular* with respect to predicates `p q : set Î± â†’ Prop`,
if for every `U` such that `q U` and `r < Î¼ U`, there exists a subset `K âŠ† U` satisfying `p K`
of measure greater than `r`.

This definition is used to prove some facts about regular and weakly regular measures without
repeating the proofs. -/
def inner_regular {Î±} {m : measurable_space Î±} (Î¼ : measure Î±) (p q : set Î± â†’ Prop) :=
âˆ€ â¦ƒUâ¦„, q U â†’ âˆ€ r < Î¼ U, âˆƒ K âŠ† U, p K âˆ§ r < Î¼ K

namespace inner_regular

variables {Î± : Type*} {m : measurable_space Î±} {Î¼ : measure Î±} {p q : set Î± â†’ Prop}
  {U : set Î±} {Îµ : â„â‰¥0âˆž}

lemma measure_eq_supr (H : inner_regular Î¼ p q) (hU : q U) : Î¼ U = â¨† (K âŠ† U) (hK : p K), Î¼ K :=
begin
  refine le_antisymm (le_of_forall_lt (Î» r hr, _)) (bsupr_le $ Î» K hK, supr_le $ Î» _, Î¼.mono hK),
  simpa only [lt_supr_iff, exists_prop] using H hU r hr
end

lemma exists_subset_lt_add (H : inner_regular Î¼ p q) (h0 : p âˆ…) (hU : q U) (hÎ¼U : Î¼ U â‰  âˆž)
  (hÎµ : Îµ â‰  0) :
  âˆƒ K âŠ† U, p K âˆ§ Î¼ U < Î¼ K + Îµ :=
begin
  cases eq_or_ne (Î¼ U) 0 with hâ‚€ hâ‚€,
  { refine âŸ¨âˆ…, empty_subset _, h0, _âŸ©,
    rwa [measure_empty, hâ‚€, zero_add, pos_iff_ne_zero] },
  { rcases H hU _ (ennreal.sub_lt_self hÎ¼U hâ‚€ hÎµ) with âŸ¨K, hKU, hKc, hrKâŸ©,
    exact âŸ¨K, hKU, hKc, ennreal.lt_add_of_sub_lt (or.inl hÎ¼U) hrKâŸ© }
end

lemma map {Î± Î²} [measurable_space Î±] [measurable_space Î²] {Î¼ : measure Î±} {pa qa : set Î± â†’ Prop}
  (H : inner_regular Î¼ pa qa) (f : Î± â‰ƒ Î²) (hf : measurable f)
  {pb qb : set Î² â†’ Prop} (hAB : âˆ€ U, qb U â†’ qa (f â»Â¹' U)) (hAB' : âˆ€ K, pa K â†’ pb (f '' K))
  (hBâ‚ : âˆ€ K, pb K â†’ measurable_set K) (hBâ‚‚ : âˆ€ U, qb U â†’ measurable_set U) :
  inner_regular (map f Î¼) pb qb :=
begin
  intros U hU r hr,
  rw [map_apply hf (hBâ‚‚ _ hU)] at hr,
  rcases H (hAB U hU) r hr with âŸ¨K, hKU, hKc, hKâŸ©,
  refine âŸ¨f '' K, image_subset_iff.2 hKU, hAB' _ hKc, _âŸ©,
  rwa [map_apply hf (hBâ‚ _ $ hAB' _ hKc), f.preimage_image]
end

lemma smul (H : inner_regular Î¼ p q) (c : â„â‰¥0âˆž) : inner_regular (c â€¢ Î¼) p q :=
begin
  intros U hU r hr,
  rw [smul_apply, H.measure_eq_supr hU] at hr,
  simpa only [ennreal.mul_supr, lt_supr_iff, exists_prop] using hr
end

lemma trans {q' : set Î± â†’ Prop} (H : inner_regular Î¼ p q) (H' : inner_regular Î¼ q q') :
  inner_regular Î¼ p q' :=
begin
  intros U hU r hr,
  rcases H' hU r hr with âŸ¨F, hFU, hqF, hFâŸ©, rcases H hqF _ hF with âŸ¨K, hKF, hpK, hrKâŸ©,
  exact âŸ¨K, hKF.trans hFU, hpK, hrKâŸ©
end

end inner_regular

variables {Î± Î² : Type*} [measurable_space Î±] [topological_space Î±] {Î¼ : measure Î±}

/-- A measure `Î¼` is outer regular if `Î¼(A) = inf {Î¼(U) | A âŠ† U open}` for a measurable set `A`.

This definition implies the same equality for any (not necessarily measurable) set, see
`set.measure_eq_infi_is_open`. -/
@[protect_proj] class outer_regular (Î¼ : measure Î±) : Prop :=
(outer_regular : âˆ€ â¦ƒA : set Î±â¦„, measurable_set A â†’ âˆ€ r > Î¼ A, âˆƒ U âŠ‡ A, is_open U âˆ§ Î¼ U < r)

/-- A measure `Î¼` is regular if
  - it is finite on all compact sets;
  - it is outer regular: `Î¼(A) = inf {Î¼(U) | A âŠ† U open}` for `A` measurable;
  - it is inner regular for open sets, using compact sets:
    `Î¼(U) = sup {Î¼(K) | K âŠ† U compact}` for `U` open. -/
@[protect_proj] class regular (Î¼ : measure Î±) extends outer_regular Î¼ : Prop :=
(lt_top_of_is_compact : âˆ€ â¦ƒK : set Î±â¦„, is_compact K â†’ Î¼ K < âˆž)
(inner_regular : inner_regular Î¼ is_compact is_open)

/-- A measure `Î¼` is weakly regular if
  - it is outer regular: `Î¼(A) = inf { Î¼(U) | A âŠ† U open }` for `A` measurable;
  - it is inner regular for open sets, using closed sets:
    `Î¼(U) = sup {Î¼(F) | F âŠ† U compact}` for `U` open. -/
@[protect_proj] class weakly_regular (Î¼ : measure Î±) extends outer_regular Î¼ : Prop :=
(inner_regular : inner_regular Î¼ is_closed is_open)

/-- A regular measure is weakly regular. -/
@[priority 100] -- see Note [lower instance priority]
instance regular.weakly_regular [t2_space Î±] [regular Î¼] : weakly_regular Î¼ :=
{ inner_regular := Î» U hU r hr, let âŸ¨K, hKU, hcK, hKâŸ© := regular.inner_regular hU r hr
  in âŸ¨K, hKU, hcK.is_closed, hKâŸ© }

namespace outer_regular

instance zero : outer_regular (0 : measure Î±) :=
âŸ¨Î» A hA r hr, âŸ¨univ, subset_univ A, is_open_univ, hrâŸ©âŸ©

/-- Given `r` larger than the measure of a set `A`, there exists an open superset of `A` with
measure less than `r`. -/
lemma _root_.set.exists_is_open_lt_of_lt [outer_regular Î¼] (A : set Î±) (r : â„â‰¥0âˆž) (hr : Î¼ A < r) :
  âˆƒ U âŠ‡ A, is_open U âˆ§ Î¼ U < r :=
begin
  rcases outer_regular.outer_regular (measurable_set_to_measurable Î¼ A) r
    (by rwa measure_to_measurable) with âŸ¨U, hAU, hUo, hUâŸ©,
  exact âŸ¨U, (subset_to_measurable _ _).trans hAU, hUo, hUâŸ©
end

/-- For an outer regular measure, the measure of a set is the infimum of the measures of open sets
containing it. -/
lemma _root_.set.measure_eq_infi_is_open (A : set Î±) (Î¼ : measure Î±) [outer_regular Î¼] :
  Î¼ A = (â¨… (U : set Î±) (h : A âŠ† U) (h2 : is_open U), Î¼ U) :=
begin
  refine le_antisymm (le_binfi $ Î» s hs, le_infi $ Î» h2s, Î¼.mono hs) _,
  refine le_of_forall_lt' (Î» r hr, _),
  simpa only [infi_lt_iff, exists_prop] using A.exists_is_open_lt_of_lt r hr
end

lemma _root_.set.exists_is_open_lt_add [outer_regular Î¼] (A : set Î±) (hA : Î¼ A â‰  âˆž)
  {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ U âŠ‡ A, is_open U âˆ§ Î¼ U < Î¼ A + Îµ :=
A.exists_is_open_lt_of_lt _ (ennreal.lt_add_right hA hÎµ)

lemma _root_.measurable_set.exists_is_open_diff_lt [opens_measurable_space Î±]
  [outer_regular Î¼] {A : set Î±} (hA : measurable_set A)
  (hA' : Î¼ A â‰  âˆž) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ U âŠ‡ A, is_open U âˆ§ Î¼ U < âˆž âˆ§ Î¼ (U \ A) < Îµ :=
begin
  rcases A.exists_is_open_lt_add hA' hÎµ with âŸ¨U, hAU, hUo, hUâŸ©,
  use [U, hAU, hUo, hU.trans_le le_top],
  exact measure_diff_lt_of_lt_add hA hUo.measurable_set hAU hA' hU,
end

protected lemma map [opens_measurable_space Î±] [measurable_space Î²] [topological_space Î²]
  [borel_space Î²] (f : Î± â‰ƒâ‚œ Î²) (Î¼ : measure Î±) [outer_regular Î¼] :
  (measure.map f Î¼).outer_regular :=
begin
  refine âŸ¨Î» A hA r hr, _âŸ©,
  rw [map_apply f.measurable hA, â† f.image_symm] at hr,
  rcases set.exists_is_open_lt_of_lt _ r hr with âŸ¨U, hAU, hUo, hUâŸ©,
  have : is_open (f.symm â»Â¹' U), from hUo.preimage f.symm.continuous,
  refine âŸ¨f.symm â»Â¹' U, image_subset_iff.1 hAU, this, _âŸ©,
  rwa [map_apply f.measurable this.measurable_set, f.preimage_symm, f.preimage_image],
end

protected lemma smul (Î¼ : measure Î±) [outer_regular Î¼] {x : â„â‰¥0âˆž} (hx : x â‰  âˆž) :
  (x â€¢ Î¼).outer_regular :=
begin
  rcases eq_or_ne x 0 with rfl|h0,
  { rw zero_smul, exact outer_regular.zero },
  { refine âŸ¨Î» A hA r hr, _âŸ©,
    rw [smul_apply, A.measure_eq_infi_is_open] at hr,
    simpa only [ennreal.mul_infi_of_ne h0 hx, gt_iff_lt, infi_lt_iff, exists_prop] using hr }
end

end outer_regular

/-- If a measure `Î¼` admits finite spanning open sets such that the restriction of `Î¼` to each set
is outer regular, then the original measure is outer regular as well. -/
protected lemma finite_spanning_sets_in.outer_regular [opens_measurable_space Î±] {Î¼ : measure Î±}
  (s : Î¼.finite_spanning_sets_in {U | is_open U âˆ§ outer_regular (Î¼.restrict U)}) :
  outer_regular Î¼ :=
begin
  refine âŸ¨Î» A hA r hr, _âŸ©,
  have hm : âˆ€ n, measurable_set (s.set n), from Î» n, (s.set_mem n).1.measurable_set,
  haveI : âˆ€ n, outer_regular (Î¼.restrict (s.set n)) := Î» n, (s.set_mem n).2,
  -- Note that `A = â‹ƒ n, A âˆ© disjointed s n`. We replace `A` with this sequence.
  obtain âŸ¨A, hAm, hAs, hAd, rflâŸ© : âˆƒ A' : â„• â†’ set Î±, (âˆ€ n, measurable_set (A' n)) âˆ§
    (âˆ€ n, A' n âŠ† s.set n) âˆ§ pairwise (disjoint on A') âˆ§ A = â‹ƒ n, A' n,
  { refine âŸ¨Î» n, A âˆ© disjointed s.set n, Î» n, hA.inter (measurable_set.disjointed hm _),
      Î» n, (inter_subset_right _ _).trans (disjointed_subset _ _),
      (disjoint_disjointed s.set).mono (Î» k l hkl, hkl.mono inf_le_right inf_le_right), _âŸ©,
    rw [â† inter_Union, Union_disjointed, s.spanning, inter_univ] },
  rcases ennreal.exists_pos_sum_of_encodable' (ennreal.sub_pos.2 hr).ne' â„• with âŸ¨Î´, Î´0, hÎ´ÎµâŸ©,
  rw [lt_tsub_iff_right, add_comm] at hÎ´Îµ,
  have : âˆ€ n, âˆƒ U âŠ‡ A n, is_open U âˆ§ Î¼ U < Î¼ (A n) + Î´ n,
  { intro n,
    have Hâ‚ : âˆ€ t, Î¼.restrict (s.set n) t = Î¼ (t âˆ© s.set n), from Î» t, restrict_apply' (hm n),
    have Ht : Î¼.restrict (s.set n) (A n) â‰  âŠ¤,
    { rw Hâ‚, exact ((measure_mono $ inter_subset_right _ _).trans_lt (s.finite n)).ne },
    rcases (A n).exists_is_open_lt_add Ht (Î´0 n).ne' with âŸ¨U, hAU, hUo, hUâŸ©,
    rw [Hâ‚, Hâ‚, inter_eq_self_of_subset_left (hAs _)] at hU,
    exact âŸ¨U âˆ© s.set n, subset_inter hAU (hAs _), hUo.inter (s.set_mem n).1, hUâŸ© },
  choose U hAU hUo hU,
  refine âŸ¨â‹ƒ n, U n, Union_subset_Union hAU, is_open_Union hUo, _âŸ©,
  calc Î¼ (â‹ƒ n, U n) â‰¤ âˆ‘' n, Î¼ (U n)             : measure_Union_le _
                ... â‰¤ âˆ‘' n, (Î¼ (A n) + Î´ n)     : ennreal.tsum_le_tsum (Î» n, (hU n).le)
                ... = âˆ‘' n, Î¼ (A n) + âˆ‘' n, Î´ n : ennreal.tsum_add
                ... = Î¼ (â‹ƒ n, A n) + âˆ‘' n, Î´ n  : congr_arg2 (+) (measure_Union hAd hAm).symm rfl
                ... < r                         : hÎ´Îµ
end

namespace inner_regular

variables {p q : set Î± â†’ Prop} {U s : set Î±} {Îµ r : â„â‰¥0âˆž}

/-- If a measure is inner regular (using closed or compact sets), then every measurable set of
finite measure can by approximated by a (closed or compact) subset. -/
lemma measurable_set_of_open [opens_measurable_space Î±] [outer_regular Î¼]
  (H : inner_regular Î¼ p is_open) (h0 : p âˆ…) (hd : âˆ€ â¦ƒs Uâ¦„, p s â†’ is_open U â†’ p (s \ U)) :
  inner_regular Î¼ p (Î» s, measurable_set s âˆ§ Î¼ s â‰  âˆž) :=
begin
  rintros s âŸ¨hs, hÎ¼sâŸ© r hr,
  obtain âŸ¨Îµ, hÎµ, hÎµs, rflâŸ© : âˆƒ Îµ â‰  0, Îµ + Îµ â‰¤ Î¼ s âˆ§ r = Î¼ s - (Îµ + Îµ),
  { use (Î¼ s - r) / 2, simp [*, hr.le, ennreal.add_halves, ennreal.sub_sub_cancel, le_add_right] },
  rcases hs.exists_is_open_diff_lt hÎ¼s hÎµ with âŸ¨U, hsU, hUo, hUt, hÎ¼UâŸ©,
  rcases (U \ s).exists_is_open_lt_of_lt _ hÎ¼U with âŸ¨U', hsU', hU'o, hÎ¼U'âŸ©,
  replace hsU' := diff_subset_comm.1 hsU',
  rcases H.exists_subset_lt_add h0 hUo hUt.ne hÎµ with âŸ¨K, hKU, hKc, hKrâŸ©,
  refine âŸ¨K \ U', Î» x hx, hsU' âŸ¨hKU hx.1, hx.2âŸ©, hd hKc hU'o, ennreal.sub_lt_of_lt_add hÎµs _âŸ©,
  calc Î¼ s â‰¤ Î¼ U                   : Î¼.mono hsU
       ... < Î¼ K + Îµ               : hKr
       ... â‰¤ Î¼ (K \ U') + Î¼ U' + Îµ :
    add_le_add_right (tsub_le_iff_right.1 le_measure_diff) _
       ... â‰¤ Î¼ (K \ U') + Îµ + Îµ    : by { mono*, exacts [hÎ¼U'.le, le_rfl] }
       ... = Î¼ (K \ U') + (Îµ + Îµ)  : add_assoc _ _ _
end

open finset

/-- In a finite measure space, assume that any open set can be approximated from inside by closed
sets. Then the measure is weakly regular. -/
lemma weakly_regular_of_finite [borel_space Î±] (Î¼ : measure Î±) [is_finite_measure Î¼]
  (H : inner_regular Î¼ is_closed is_open) : weakly_regular Î¼ :=
begin
  have hfin : âˆ€ {s}, Î¼ s â‰  âŠ¤ := measure_ne_top Î¼,
  suffices : âˆ€ s, measurable_set s â†’ âˆ€ Îµ â‰  0,
    âˆƒ (F âŠ† s) (U âŠ‡ s), is_closed F âˆ§ is_open U âˆ§ Î¼ s â‰¤ Î¼ F + Îµ âˆ§ Î¼ U â‰¤ Î¼ s + Îµ,
  { refine { outer_regular := Î» s hs r hr, _, inner_regular := H },
    rcases exists_between hr with âŸ¨r', hsr', hr'râŸ©,
    rcases this s hs _ (ennreal.sub_pos.2 hsr').ne' with âŸ¨-, -, U, hsU, -, hUo, -, HâŸ©,
    refine âŸ¨U, hsU, hUo, _âŸ©,
    rw [ennreal.add_sub_cancel_of_le hsr'.le] at H, exact H.trans_lt hr'r },
  refine measurable_set.induction_on_open _ _ _,
  /- The proof is by measurable induction: we should check that the property is true for the empty
  set, for open sets, and is stable by taking the complement and by taking countable disjoint
  unions. The point of the property we are proving is that it is stable by taking complements
  (exchanging the roles of closed and open sets and thanks to the finiteness of the measure). -/
  -- check for open set
  { intros U hU Îµ hÎµ,
    rcases H.exists_subset_lt_add is_closed_empty hU hfin hÎµ with âŸ¨F, hsF, hFc, hFâŸ©,
    exact âŸ¨F, hsF, U, subset.rfl, hFc, hU, hF.le, le_self_addâŸ© },
  -- check for complements
  { rintros s hs H Îµ hÎµ,
    rcases H Îµ hÎµ with âŸ¨F, hFs, U, hsU, hFc, hUo, hF, hUâŸ©,
    refine âŸ¨Uá¶œ, compl_subset_compl.2 hsU, Fá¶œ, compl_subset_compl.2 hFs,
      hUo.is_closed_compl, hFc.is_open_compl, _âŸ©,
    simp only [measure_compl_le_add_iff, *, hUo.measurable_set, hFc.measurable_set, true_and] },
  -- check for disjoint unions
  { intros s hsd hsm H Îµ Îµ0, have Îµ0' : Îµ / 2 â‰  0, from (ennreal.half_pos Îµ0).ne',
    rcases ennreal.exists_pos_sum_of_encodable' Îµ0' â„• with âŸ¨Î´, Î´0, hÎ´ÎµâŸ©,
    choose F hFs U hsU hFc hUo hF hU using Î» n, H n (Î´ n) (Î´0 n).ne',
    -- the approximating closed set is constructed by considering finitely many sets `s i`, which
    -- cover all the measure up to `Îµ/2`, approximating each of these by a closed set `F i`, and
    -- taking the union of these (finitely many) `F i`.
    have : tendsto (Î» t, âˆ‘ k in t, Î¼ (s k) + Îµ / 2) at_top (ð“ $ Î¼ (â‹ƒ n, s n) + Îµ / 2),
    { rw measure_Union hsd hsm, exact tendsto.add ennreal.summable.has_sum tendsto_const_nhds },
    rcases (this.eventually $ lt_mem_nhds $ ennreal.lt_add_right hfin Îµ0').exists with âŸ¨t, htâŸ©,
    -- the approximating open set is constructed by taking for each `s n` an approximating open set
    -- `U n` with measure at most `Î¼ (s n) + Î´ n` for a summable `Î´`, and taking the union of these.
    refine âŸ¨â‹ƒ k âˆˆ t, F k, Union_subset_Union $ Î» k, Union_subset $ Î» _, hFs _,
      â‹ƒ n, U n, Union_subset_Union hsU, is_closed_bUnion t.finite_to_set $ Î» k _, hFc k,
      is_open_Union hUo, ht.le.trans _, _âŸ©,
    { calc âˆ‘ k in t, Î¼ (s k) + Îµ / 2 â‰¤ âˆ‘ k in t, Î¼ (F k) + âˆ‘ k in t, Î´ k + Îµ / 2 :
        by { rw â† sum_add_distrib, exact add_le_add_right (sum_le_sum $ Î» k hk, hF k) _ }
      ... â‰¤ âˆ‘ k in t, Î¼ (F k) + Îµ / 2 + Îµ / 2 :
        add_le_add_right (add_le_add_left ((ennreal.sum_le_tsum _).trans hÎ´Îµ.le) _) _
      ... = Î¼ (â‹ƒ k âˆˆ t, F k) + Îµ : _,
      rw [measure_bUnion_finset, add_assoc, ennreal.add_halves],
      exacts [Î» k _ n _ hkn, (hsd k n hkn).mono (hFs k) (hFs n), Î» k hk, (hFc k).measurable_set] },
    { calc Î¼ (â‹ƒ n, U n) â‰¤ âˆ‘' n, Î¼ (U n) : measure_Union_le _
      ... â‰¤ âˆ‘' n, (Î¼ (s n) + Î´ n) : ennreal.tsum_le_tsum hU
      ... = Î¼ (â‹ƒ n, s n) + âˆ‘' n, Î´ n : by rw [measure_Union hsd hsm, ennreal.tsum_add]
      ... â‰¤ Î¼ (â‹ƒ n, s n) + Îµ : add_le_add_left (hÎ´Îµ.le.trans ennreal.half_le_self) _ } }
end

/-- In a metric space (or even a pseudo emetric space), an open set can be approximated from inside
by closed sets. -/
lemma of_pseudo_emetric_space {X : Type*} [pseudo_emetric_space X] [measurable_space X]
  [opens_measurable_space X] (Î¼ : measure X) :
  inner_regular Î¼ is_closed is_open :=
begin
  intros U hU r hr,
  rcases hU.exists_Union_is_closed with âŸ¨F, F_closed, -, rfl, F_monoâŸ©,
  rw measure_Union_eq_supr (Î» n, (F_closed n).measurable_set) F_mono.directed_le at hr,
  rcases lt_supr_iff.1 hr with âŸ¨n, hnâŸ©,
  exact âŸ¨F n, subset_Union _ _, F_closed n, hnâŸ©
end

/-- In a `Ïƒ`-compact space, any closed set can be approximated by a compact subset. -/
lemma is_compact_is_closed {X : Type*} [topological_space X] [t2_space X]
  [sigma_compact_space X] [measurable_space X] [opens_measurable_space X] (Î¼ : measure X) :
  inner_regular Î¼ is_compact is_closed :=
begin
  intros F hF r hr,
  set B : â„• â†’ set X := compact_covering X,
  have hBc : âˆ€ n, is_compact (F âˆ© B n), from Î» n, (is_compact_compact_covering X n).inter_left hF,
  have hBU : (â‹ƒ n, F âˆ© B n) = F, by rw [â† inter_Union, Union_compact_covering, set.inter_univ],
  have : Î¼ F = â¨† n, Î¼ (F âˆ© B n),
  { rw [â† measure_Union_eq_supr, hBU],
    exacts [Î» n, (hBc n).measurable_set, monotone.directed_le $
      Î» m n h, inter_subset_inter_right _ (compact_covering_subset _ h)] },
  rw this at hr, rcases lt_supr_iff.1 hr with âŸ¨n, hnâŸ©,
  exact âŸ¨_, inter_subset_left _ _, hBc n, hnâŸ©
end

end inner_regular

namespace regular

instance zero : regular (0 : measure Î±) :=
âŸ¨Î» K hK, ennreal.coe_lt_top, Î» U hU r hr, âŸ¨âˆ…, empty_subset _, is_compact_empty, hrâŸ©âŸ©

/-- If `Î¼` is a regular measure, then any open set can be approximated by a compact subset. -/
lemma _root_.is_open.exists_lt_is_compact [regular Î¼] â¦ƒU : set Î±â¦„ (hU : is_open U)
  {r : â„â‰¥0âˆž} (hr : r < Î¼ U) :
  âˆƒ K âŠ† U, is_compact K âˆ§ r < Î¼ K :=
regular.inner_regular hU r hr

/-- The measure of an open set is the supremum of the measures of compact sets it contains. -/
lemma _root_.is_open.measure_eq_supr_is_compact â¦ƒU : set Î±â¦„ (hU : is_open U)
  (Î¼ : measure Î±) [regular Î¼] :
  Î¼ U = (â¨† (K : set Î±) (h : K âŠ† U) (h2 : is_compact K), Î¼ K) :=
regular.inner_regular.measure_eq_supr hU

lemma exists_compact_not_null [regular Î¼] : (âˆƒ K, is_compact K âˆ§ Î¼ K â‰  0) â†” Î¼ â‰  0 :=
by simp_rw [ne.def, â† measure_univ_eq_zero, is_open_univ.measure_eq_supr_is_compact,
    ennreal.supr_eq_zero, not_forall, exists_prop, subset_univ, true_and]

/-- If `Î¼` is a regular measure, then any measurable set of finite measure can be approximated by a
compact subset. See also `measurable_set.exists_is_compact_lt_add` and
`measurable_set.exists_lt_is_compact_of_ne_top`. -/
lemma inner_regular_measurable [opens_measurable_space Î±] [regular Î¼] :
  inner_regular Î¼ is_compact (Î» s, measurable_set s âˆ§ Î¼ s â‰  âˆž) :=
regular.inner_regular.measurable_set_of_open is_compact_empty (Î» _ _, is_compact.diff)

/-- If `Î¼` is a regular measure, then any measurable set of finite measure can be approximated by a
compact subset. See also `measurable_set.exists_lt_is_compact_of_ne_top`. -/
lemma _root_.measurable_set.exists_is_compact_lt_add [opens_measurable_space Î±]
  [regular Î¼] â¦ƒA : set Î±â¦„ (hA : measurable_set A) (h'A : Î¼ A â‰  âˆž) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ K âŠ† A, is_compact K âˆ§ Î¼ A < Î¼ K + Îµ :=
regular.inner_regular_measurable.exists_subset_lt_add is_compact_empty âŸ¨hA, h'AâŸ© h'A hÎµ

/-- If `Î¼` is a regular measure, then any measurable set of finite measure can be approximated by a
compact subset. See also `measurable_set.exists_is_compact_lt_add` and
`measurable_set.exists_lt_is_compact_of_ne_top`. -/
lemma _root_.measurable_set.exists_is_compact_diff_lt [opens_measurable_space Î±] [t2_space Î±]
  [regular Î¼] â¦ƒA : set Î±â¦„ (hA : measurable_set A) (h'A : Î¼ A â‰  âˆž) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ K âŠ† A, is_compact K âˆ§ Î¼ (A \ K) < Îµ :=
begin
  rcases hA.exists_is_compact_lt_add h'A hÎµ with âŸ¨K, hKA, hKc, hKâŸ©,
  exact âŸ¨K, hKA, hKc, measure_diff_lt_of_lt_add hKc.measurable_set hA hKA
    (ne_top_of_le_ne_top h'A $ measure_mono hKA) hKâŸ©
end

/-- If `Î¼` is a regular measure, then any measurable set of finite measure can be approximated by a
compact subset. See also `measurable_set.exists_is_compact_lt_add`. -/
lemma _root_.measurable_set.exists_lt_is_compact_of_ne_top [regular Î¼]
  [opens_measurable_space Î±] â¦ƒA : set Î±â¦„ (hA : measurable_set A) (h'A : Î¼ A â‰  âˆž)
  {r : â„â‰¥0âˆž} (hr : r < Î¼ A) :
  âˆƒ K âŠ† A, is_compact K âˆ§ r < Î¼ K :=
regular.inner_regular_measurable âŸ¨hA, h'AâŸ© _ hr

/-- Given a regular measure, any measurable set of finite mass can be approximated from
inside by compact sets. -/
lemma _root_.measurable_set.measure_eq_supr_is_compact_of_ne_top
  [opens_measurable_space Î±] [regular Î¼]
  â¦ƒA : set Î±â¦„ (hA : measurable_set A) (h'A : Î¼ A â‰  âˆž) :
  Î¼ A = (â¨† (K âŠ† A) (h : is_compact K), Î¼ K) :=
regular.inner_regular_measurable.measure_eq_supr âŸ¨hA, h'AâŸ©

protected lemma map [opens_measurable_space Î±] [measurable_space Î²] [topological_space Î²]
  [t2_space Î²] [borel_space Î²] [regular Î¼] (f : Î± â‰ƒâ‚œ Î²) :
  (measure.map f Î¼).regular :=
begin
  haveI := outer_regular.map f Î¼,
  split,
  { intros K hK, rw [map_apply f.measurable hK.measurable_set],
    apply regular.lt_top_of_is_compact,
    rwa f.compact_preimage },
  { exact regular.inner_regular.map f.to_equiv f.measurable (Î» U hU, hU.preimage f.continuous)
      (Î» K hK, hK.image f.continuous) (Î» K hK, hK.measurable_set) (Î» U hU, hU.measurable_set) }
end

protected lemma smul [regular Î¼] {x : â„â‰¥0âˆž} (hx : x â‰  âˆž) :
  (x â€¢ Î¼).regular :=
begin
  haveI := outer_regular.smul Î¼ hx,
  exact âŸ¨Î» K hK, ennreal.mul_lt_top hx (regular.lt_top_of_is_compact hK).ne,
    regular.inner_regular.smul xâŸ©
end

/-- A regular measure in a Ïƒ-compact space is Ïƒ-finite. -/
@[priority 100] -- see Note [lower instance priority]
instance sigma_finite [sigma_compact_space Î±] [regular Î¼] : sigma_finite Î¼ :=
âŸ¨âŸ¨{ set := compact_covering Î±,
  set_mem := Î» n, trivial,
  finite := Î» n, regular.lt_top_of_is_compact $ is_compact_compact_covering Î± n,
  spanning := Union_compact_covering Î± }âŸ©âŸ©

end regular

namespace weakly_regular

/-- If `Î¼` is a weakly regular measure, then any open set can be approximated by a closed subset. -/
lemma _root_.is_open.exists_lt_is_closed [weakly_regular Î¼] â¦ƒU : set Î±â¦„ (hU : is_open U)
  {r : â„â‰¥0âˆž} (hr : r < Î¼ U) :
  âˆƒ F âŠ† U, is_closed F âˆ§ r < Î¼ F :=
weakly_regular.inner_regular hU r hr

/-- If `Î¼` is a weakly regular measure, then any open set can be approximated by a closed subset. -/
lemma _root_.is_open.measure_eq_supr_is_closed â¦ƒU : set Î±â¦„ (hU : is_open U)
  (Î¼ : measure Î±) [weakly_regular Î¼] :
  Î¼ U = (â¨† (F âŠ† U) (h : is_closed F), Î¼ F) :=
weakly_regular.inner_regular.measure_eq_supr hU

lemma inner_regular_measurable [opens_measurable_space Î±] [weakly_regular Î¼] :
  inner_regular Î¼ is_closed (Î» s, measurable_set s âˆ§ Î¼ s â‰  âˆž) :=
weakly_regular.inner_regular.measurable_set_of_open is_closed_empty
  (Î» _ _ hâ‚ hâ‚‚, hâ‚.inter hâ‚‚.is_closed_compl)

/-- If `s` is a measurable set, a weakly regular measure `Î¼` is finite on `s`, and `Îµ` is a positive
number, then there exist a closed set `K âŠ† s` such that `Î¼ s < Î¼ K + Îµ`. -/
lemma _root_.measurable_set.exists_is_closed_lt_add [weakly_regular Î¼]
  [opens_measurable_space Î±] {s : set Î±} (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆž)
  {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ K âŠ† s, is_closed K âˆ§ Î¼ s < Î¼ K + Îµ :=
inner_regular_measurable.exists_subset_lt_add is_closed_empty âŸ¨hs, hÎ¼sâŸ© hÎ¼s hÎµ

lemma _root_.measurable_set.exists_is_closed_diff_lt [opens_measurable_space Î±]
  [weakly_regular Î¼] â¦ƒA : set Î±â¦„ (hA : measurable_set A) (h'A : Î¼ A â‰  âˆž) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ F âŠ† A, is_closed F âˆ§ Î¼ (A \ F) < Îµ :=
begin
  rcases hA.exists_is_closed_lt_add h'A hÎµ with âŸ¨F, hFA, hFc, hFâŸ©,
  exact âŸ¨F, hFA, hFc, measure_diff_lt_of_lt_add hFc.measurable_set hA hFA
    (ne_top_of_le_ne_top h'A $ measure_mono hFA) hFâŸ©
end

/-- Given a weakly regular measure, any measurable set of finite mass can be approximated from
inside by closed sets. -/
lemma _root_.measurable_set.exists_lt_is_closed_of_ne_top [weakly_regular Î¼]
  [opens_measurable_space Î±] â¦ƒA : set Î±â¦„ (hA : measurable_set A) (h'A : Î¼ A â‰  âˆž)
  {r : â„â‰¥0âˆž} (hr : r < Î¼ A) :
  âˆƒ K âŠ† A, is_closed K âˆ§ r < Î¼ K :=
inner_regular_measurable âŸ¨hA, h'AâŸ© _ hr

/-- Given a weakly regular measure, any measurable set of finite mass can be approximated from
inside by closed sets. -/
lemma _root_.measurable_set.measure_eq_supr_is_closed_of_ne_top [opens_measurable_space Î±]
  [weakly_regular Î¼] â¦ƒA : set Î±â¦„ (hA : measurable_set A) (h'A : Î¼ A â‰  âˆž) :
  Î¼ A = (â¨† (K âŠ† A) (h : is_closed K), Î¼ K) :=
inner_regular_measurable.measure_eq_supr âŸ¨hA, h'AâŸ©

/-- The restriction of a weakly regular measure to a measurable set of finite measure is
weakly regular. -/
lemma restrict_of_measurable_set [borel_space Î±] [weakly_regular Î¼] (A : set Î±)
  (hA : measurable_set A) (h'A : Î¼ A â‰  âˆž) : weakly_regular (Î¼.restrict A) :=
begin
  haveI : fact (Î¼ A < âˆž) := âŸ¨h'A.lt_topâŸ©,
  refine inner_regular.weakly_regular_of_finite _ (Î» V V_open, _),
  simp only [restrict_apply' hA], intros r hr,
  have : Î¼ (V âˆ© A) â‰  âˆž, from ne_top_of_le_ne_top h'A (measure_mono $ inter_subset_right _ _),
  rcases (V_open.measurable_set.inter hA).exists_lt_is_closed_of_ne_top this hr
    with âŸ¨F, hFVA, hFc, hFâŸ©,
  refine âŸ¨F, hFVA.trans (inter_subset_left _ _), hFc, _âŸ©,
  rwa inter_eq_self_of_subset_left (hFVA.trans $ inter_subset_right _ _)
end

/-- Any finite measure on a metric space (or even a pseudo emetric space) is weakly regular. -/
@[priority 100] -- see Note [lower instance priority]
instance of_pseudo_emetric_space_of_is_finite_measure {X : Type*} [pseudo_emetric_space X]
  [measurable_space X] [borel_space X] (Î¼ : measure X) [is_finite_measure Î¼] :
  weakly_regular Î¼ :=
(inner_regular.of_pseudo_emetric_space Î¼).weakly_regular_of_finite Î¼

/-- Any locally finite measure on a `Ïƒ`-compact metric space (or even a pseudo emetric space) is
weakly regular. -/
@[priority 100] -- see Note [lower instance priority]
instance of_pseudo_emetric_sigma_compact_space_of_locally_finite {X : Type*}
  [pseudo_emetric_space X] [sigma_compact_space X] [measurable_space X] [borel_space X]
  (Î¼ : measure X) [is_locally_finite_measure Î¼] :
  weakly_regular Î¼ :=
begin
  haveI : outer_regular Î¼,
  { refine (Î¼.finite_spanning_sets_in_open.mono' $ Î» U hU, _).outer_regular,
    haveI : fact (Î¼ U < âˆž), from âŸ¨hU.2âŸ©,
    exact âŸ¨hU.1, infer_instanceâŸ© },
  exact âŸ¨inner_regular.of_pseudo_emetric_space Î¼âŸ©
end

end weakly_regular

/-- Any locally finite measure on a `Ïƒ`-compact (e)metric space is regular. -/
@[priority 100] -- see Note [lower instance priority]
instance regular.of_sigma_compact_space_of_is_locally_finite_measure {X : Type*}
  [emetric_space X] [sigma_compact_space X] [measurable_space X] [borel_space X] (Î¼ : measure X)
  [is_locally_finite_measure Î¼] : regular Î¼ :=
{ lt_top_of_is_compact := Î» K hK, hK.measure_lt_top,
  inner_regular := (inner_regular.is_compact_is_closed Î¼).trans
    (inner_regular.of_pseudo_emetric_space Î¼) }

end measure
end measure_theory
