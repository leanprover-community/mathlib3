/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris Van Doorn
-/

import measure_theory.constructions.borel_space

/-!
# Regular measures

A measure is `regular` if it satisfies the following properties:
* it is finite on compact sets;
* it is outer regular for measurable sets with respect to open sets: the measure of any measurable
  set `A` is the infimum of `Œº U` over all open sets `U` containing `A`;
* it is inner regular for open sets with respect to compacts sets: the measure of any open set `U`
  is the supremum of `Œº K` over all compact sets `K` contained in `U`.

A measure is `weakly_regular` if it satisfies the following properties:
* it is outer regular for measurable sets with respect to open sets: the measure of any measurable
  set `A` is the infimum of `Œº U` over all open sets `U` containing `A`;
* it is inner regular for open sets with respect to closed sets: the measure of any open set `U`
  is the supremum of `Œº F` over all compact sets `F` contained in `U`.

Regularity implies weak regularity. Both these conditions are registered as typeclasses for a
measure `Œº`, and this implication is recorded as an instance.

These conditions imply inner regularity for all measurable sets of finite measure (with respect to
compact sets or closed sets respectively), but in general not for all sets. For a counterexample,
consider the group `‚Ñù √ó ‚Ñù` where the first factor has the discrete topology and the second one the
usual topology. It is a locally compact Hausdorff topological group, with Haar measure equal to
Lebesgue measure on each vertical fiber. The set `‚Ñù √ó {0}` has infinite measure (by outer
regularity), but any compact set it contains has zero measure (as it is finite).

Several authors require as a definition of regularity that all measurable sets are inner regular.
We have opted for the slightly weaker definition above as it holds for all Haar measures, it is
enough for essentially all applications, and it is equivalent to the other definition when the
measure is finite.

The interest of the notion of weak regularity is that it is enough for many applications, and it
is automatically satisfied by any finite measure on a metric space.

## Main definitions and results

* `regular Œº`: a typeclass registering that a measure `Œº` on a topological space is regular.
* `measurable_set.measure_eq_infi_is_open'` asserts that, when `Œº` is regular, the measure of a
  measurable set is the infimum of the measure of open sets containing it. The unprimed version
  is reserved for weakly regular measures, as it should apply most of the time. Such open sets
  with suitable measures can be constructed with `measurable_set.exists_is_open_lt_of_lt'`.
* `is_open.measure_eq_supr_is_compact` asserts that the measure of an open set is the supremum of
  the measure of compact sets it contains. Such a compact set with suitable measure can be
  constructed with `is_open.exists_lt_is_compact`.
* `measurable_set.measure_eq_supr_is_compact_of_lt_top` asserts that the measure of a measurable set
  of finite measure is the supremum of the measure of compact sets it contains. Such a compact set
  with suitable measure can be constructed with `measurable_set.exists_lt_is_compact_of_lt_top` or
  `measurable_set.exists_lt_is_compact_of_lt_top_of_pos`.
* `regular.of_sigma_compact_space_of_is_locally_finite_measure` is an instance registering that a
  locally finite measure on a locally compact metric space is regular (in fact, an emetric space
  is enough).

* `weakly_regular Œº`: a typeclass registering that a measure `Œº` on a topological space is
  weakly regular.
* `measurable_set.measure_eq_infi_is_open`, `measurable_set.exists_is_open_lt_of_lt`,
  `is_open.measure_eq_supr_is_closed`, `is_open.exists_lt_is_closed`,
  `measurable_set.measure_eq_supr_is_closed_of_lt_top`,
  `measurable_set.exists_lt_is_closed_of_lt_top` and
  `measurable_set.exists_lt_is_closed_of_lt_top_of_pos`
  state the corresponding properties for weakly regular measures.
* `weakly_regular.of_pseudo_emetric_space_of_is_finite_measure` is an instance registering that a
  finite measure on a metric space is weakly regular (in fact, a pseudo emetric space is enough).

## Implementation notes

The main nontrivial statement is `weakly_regular.exists_closed_subset_self_subset_open`, expressing
that in a finite measure space, if every open set can be approximated from inside by closed sets,
then any measurable set can be approximated from inside by closed sets and from outside by open
sets. This statement is proved by measurable induction, starting from open sets and checking that
it is stable by taking complements (this is the point of this condition, being symmetrical between
inside and outside) and countable disjoint unions.

Once this statement is proved, one deduces results for general measures from this statement, by
restricting them to finite measure sets (and proving that this restriction is weakly regular,
using again the same statement).

## References

[Halmos, Measure Theory, ¬ß52][halmos1950measure]. Note that Halmos uses an unusual definition of
Borel sets (for him, they are elements of the `œÉ`-algebra generated by compact sets!), so his
proofs or statements do not apply directly.

[Billingsley, Convergence of Probability Measures][billingsley1999]
-/

open set filter
open_locale ennreal topological_space nnreal big_operators

namespace measure_theory
namespace measure

variables {Œ± Œ≤ : Type*} [measurable_space Œ±] [topological_space Œ±] {Œº : measure Œ±}

/-- A measure `Œº` is outer regular if `Œº(A) = inf {Œº(U) | A ‚äÜ U open}` for a measurable set `A`. -/
@[protect_proj] class outer_regular (Œº : measure Œ±) : Prop :=
(outer_regular : ‚àÄ ‚¶ÉA : set Œ±‚¶Ñ, measurable_set A ‚Üí
  (‚®Ö (U : set Œ±) (h : is_open U) (h2 : A ‚äÜ U), Œº U) ‚â§ Œº A)

/-- A measure `Œº` is regular if
  - it is finite on all compact sets;
  - it is outer regular: `Œº(A) = inf {Œº(U) | A ‚äÜ U open}` for `A` measurable;
  - it is inner regular for open sets, using compact sets:
    `Œº(U) = sup {Œº(K) | K ‚äÜ U compact}` for `U` open. -/
class regular (Œº : measure Œ±) extends outer_regular Œº : Prop :=
(lt_top_of_is_compact : ‚àÄ ‚¶ÉK : set Œ±‚¶Ñ, is_compact K ‚Üí Œº K < ‚àû)
(inner_regular : ‚àÄ ‚¶ÉU : set Œ±‚¶Ñ, is_open U ‚Üí
  Œº U ‚â§ ‚®Ü (K : set Œ±) (h : is_compact K) (h2 : K ‚äÜ U), Œº K)

/-- A measure `Œº` is weakly regular if
  - it is outer regular: `Œº(A) = inf { Œº(U) | A ‚äÜ U open }` for `A` measurable;
  - it is inner regular for open sets, using closed sets:
    `Œº(U) = sup {Œº(F) | F ‚äÜ U compact}` for `U` open. -/
class weakly_regular (Œº : measure Œ±) extends outer_regular Œº : Prop :=
(inner_regular : ‚àÄ ‚¶ÉU : set Œ±‚¶Ñ, is_open U ‚Üí
  Œº U ‚â§ ‚®Ü (F : set Œ±) (h : is_closed F) (h2 : F ‚äÜ U), Œº F)

/-- A regular measure is weakly regular. -/
@[priority 100] -- see Note [lower instance priority]
instance regular.weakly_regular [t2_space Œ±] [regular Œº] : weakly_regular Œº :=
{ inner_regular := Œª U hU, calc
    Œº U ‚â§ ‚®Ü (K : set Œ±) (h : is_compact K) (h2 : K ‚äÜ U), Œº K : regular.inner_regular hU
    ... ‚â§ ‚®Ü (F : set Œ±) (h : is_closed F) (h2 : F ‚äÜ U), Œº F :
      by { simp only [supr_and'], exact bsupr_le_bsupr' (Œª i hi, ‚ü®hi.1.is_closed, hi.2‚ü©) } }

namespace outer_regular

instance zero : outer_regular (0 : measure Œ±) :=
‚ü®Œª A hA, binfi_le_of_le univ is_open_univ $ infi_le _ (subset_univ A)‚ü©

/-- For an outer regular measure, the measure of a set is the infimum of the measures of open sets
containing it. -/
lemma _root_.set.measure_eq_infi_is_open (A : set Œ±) (Œº : measure Œ±) [outer_regular Œº] :
  Œº A = (‚®Ö (U : set Œ±) (h : is_open U) (h2 : A ‚äÜ U), Œº U) :=
begin
  refine le_antisymm (le_infi $ Œª s, le_infi $ Œª hs, le_infi $ Œª h2s, Œº.mono h2s) _,
  rw ‚Üê @measure_to_measurable _ _ Œº A,
  refine le_trans _ (outer_regular.outer_regular (measurable_set_to_measurable Œº A)),
  exact infi_le_infi (Œª U, infi_le_infi $ Œª hU, infi_le_infi2 $
    Œª hAU, ‚ü®(subset_to_measurable _ _).trans hAU, le_rfl‚ü©)
end

/-- Given `r` larger than the measure of a set `A`, there exists an open superset of `A` with
measure less than `r`. -/
lemma _root_.set.exists_is_open_lt_of_lt [outer_regular Œº] (A : set Œ±) (r : ‚Ñù‚â•0‚àû) (hr : Œº A < r) :
  ‚àÉ U, is_open U ‚àß A ‚äÜ U ‚àß Œº U < r :=
begin
  rw A.measure_eq_infi_is_open Œº at hr,
  simpa only [infi_lt_iff, exists_prop] using hr
end

/-- Given an outer regular measure, any finite measure set is contained in a finite measure open
set.-/
lemma exists_subset_is_open_measure_lt_top [outer_regular Œº] {A : set Œ±} (h'A : Œº A ‚â† ‚àû) :
  ‚àÉ U, is_open U ‚àß A ‚äÜ U ‚àß Œº U < ‚àû :=
begin
  rcases A.exists_is_open_lt_of_lt _ (ennreal.lt_add_right h'A ennreal.zero_lt_one)
    with ‚ü®U, hUo, hBU, hŒºU‚ü©,
  exact ‚ü®U, hUo, hBU, hŒºU.trans_le le_top‚ü©
end

protected lemma map [opens_measurable_space Œ±] [measurable_space Œ≤] [topological_space Œ≤]
  [t2_space Œ≤] [borel_space Œ≤] (f : Œ± ‚âÉ‚Çú Œ≤) (Œº : measure Œ±) [outer_regular Œº] :
  (measure.map f Œº).outer_regular :=
begin
  refine ‚ü®Œª A hA, le_of_eq _‚ü©,
  simp only [map_apply f.measurable hA, (f ‚Åª¬π' A).measure_eq_infi_is_open],
  refine infi_congr (preimage f) f.injective.preimage_surjective (Œª U, _),
  refine infi_congr_Prop f.is_open_preimage (Œª hU, _),
  refine infi_congr_Prop f.surjective.preimage_subset_preimage_iff (Œª h2U, _),
  rw [map_apply f.measurable hU.measurable_set]
end

protected lemma smul (Œº : measure Œ±) [outer_regular Œº] {x : ‚Ñù‚â•0‚àû} (hx : x ‚â† ‚àû) :
  (x ‚Ä¢ Œº).outer_regular :=
begin
  rcases eq_or_ne x 0 with rfl|h0,
  { rw zero_smul, exact outer_regular.zero },
  { refine ‚ü®Œª A hA, _‚ü©,
    simp only [smul_apply],
    refine eq.trans_le _ (ennreal.mul_left_mono $ outer_regular.outer_regular hA),
    simp only [ennreal.mul_infi_of_ne h0 hx] },
end

end outer_regular

namespace regular

instance zero : regular (0 : measure Œ±) :=
‚ü®Œª K hK, ennreal.coe_lt_top, Œª U hU, zero_le _‚ü©

/-- The measure of an open set is the supremum of the measures of compact sets it contains. -/
lemma _root_.is_open.measure_eq_supr_is_compact ‚¶ÉU : set Œ±‚¶Ñ (hU : is_open U)
  (Œº : measure Œ±) [regular Œº] :
  Œº U = (‚®Ü (K : set Œ±) (h : is_compact K) (h2 : K ‚äÜ U), Œº K) :=
le_antisymm (regular.inner_regular hU) (supr_le $ Œª s, supr_le $ Œª hs, supr_le $ Œª h2s, Œº.mono h2s)

lemma _root_.is_open.exists_lt_is_compact [regular Œº] ‚¶ÉU : set Œ±‚¶Ñ (hU : is_open U)
  {r : ‚Ñù‚â•0‚àû} (hr : r < Œº U) :
  ‚àÉ K, is_compact K ‚àß K ‚äÜ U ‚àß r < Œº K :=
begin
  rw hU.measure_eq_supr_is_compact at hr,
  simpa only [lt_supr_iff, exists_prop] using hr
end

lemma _root_.is_open.exists_is_compact_lt_add [regular Œº] ‚¶ÉU : set Œ±‚¶Ñ (hU : is_open U)
  (hŒºU : Œº U ‚â† ‚àû) {r : ‚Ñù‚â•0‚àû} (hr : 0 < r) :
  ‚àÉ K, is_compact K ‚àß K ‚äÜ U ‚àß Œº U < Œº K + r :=
begin
  cases eq_or_ne (Œº U) 0 with h‚ÇÄ h‚ÇÄ,
  { refine ‚ü®‚àÖ, is_compact_empty, empty_subset _, _‚ü©,
    rwa [measure_empty, h‚ÇÄ, zero_add] },
  { rcases hU.exists_lt_is_compact (ennreal.sub_lt_self hŒºU h‚ÇÄ hr) with ‚ü®K, hKc, hKU, hrK‚ü©,
    exact ‚ü®K, hKc, hKU, ennreal.lt_add_of_sub_lt (or.inl hŒºU) hrK‚ü© }
end

lemma exists_compact_not_null [regular Œº] : (‚àÉ K, is_compact K ‚àß Œº K ‚â† 0) ‚Üî Œº ‚â† 0 :=
by simp_rw [ne.def, ‚Üê measure_univ_eq_zero, is_open_univ.measure_eq_supr_is_compact,
    ennreal.supr_eq_zero, not_forall, exists_prop, subset_univ, true_and]

/-- If `s` is a measurable set, a regular measure `Œº` is finite on `s`, and `r` is a positive
number, then there exist a compact set `K ‚äÜ s` and an open set `U ‚äá s` such that `Œº U < Œº K + r`. -/
lemma _root_.measurable_set.exists_is_compact_is_open_lt_add [regular Œº]
  [opens_measurable_space Œ±] {s : set Œ±} (hs : measurable_set s) (hŒºs : Œº s ‚â† ‚àû)
  {r : ‚Ñù‚â•0‚àû} (hr : 0 < r) :
  ‚àÉ K U, is_compact K ‚àß is_open U ‚àß K ‚äÜ s ‚àß s ‚äÜ U ‚àß Œº U < Œº K + r :=
begin
  rcases s.exists_is_open_lt_of_lt (Œº s + r / 2) (ennreal.lt_add_right hŒºs (ennreal.half_pos hr))
    with ‚ü®U, hUo, hsU, hŒºU‚ü©,
  have : Œº (U \ s) < r / 2,
  { rw [measure_diff hsU hUo.measurable_set hs hŒºs],
    refine ennreal.sub_lt_of_lt_add (measure_mono hsU) _, rwa add_comm },
  rcases (U \ s).exists_is_open_lt_of_lt _ this with ‚ü®U', hU'o, hsU', hŒºU'‚ü©,
  rw diff_subset_comm at hsU',
  rcases hUo.exists_is_compact_lt_add (ne_top_of_lt hŒºU) (ennreal.half_pos hr)
    with ‚ü®K, hKc, hKU, hKr‚ü©,
  refine ‚ü®K \ U', U, hKc.diff hU'o, hUo, Œª x hx, hsU' ‚ü®hKU hx.1, hx.2‚ü©, hsU, _‚ü©,
  rw [‚Üê ennreal.add_halves r, ‚Üê add_assoc],
  refine hKr.trans_le (add_le_add_right _ _),
  calc Œº K ‚â§ Œº K - Œº U' + Œº U' : ennreal.le_sub_add_self
       ... ‚â§ _                 : add_le_add le_measure_diff hŒºU'.le,
end

lemma _root_.measurable_set.exists_lt_is_compact_of_ne_top_of_pos [opens_measurable_space Œ±]
  [t2_space Œ±] [regular Œº]
  ‚¶ÉA : set Œ±‚¶Ñ (hA : measurable_set A) (h'A : Œº A ‚â† ‚àû) {Œµ : ‚Ñù‚â•0‚àû} (Œµpos : 0 < Œµ) :
  ‚àÉ K, is_compact K ‚àß K ‚äÜ A ‚àß Œº A < Œº K + Œµ :=
begin
  rcases hA.exists_is_compact_is_open_lt_add h'A Œµpos with ‚ü®K, U, hK, hU, KA, AU, hŒº‚ü©,
  exact ‚ü®K, hK, KA, (measure_mono AU).trans_lt hŒº‚ü©
end

lemma _root_.measurable_set.exists_lt_is_compact_of_ne_top [t2_space Œ±] [regular Œº]
  [opens_measurable_space Œ±] ‚¶ÉA : set Œ±‚¶Ñ (hA : measurable_set A) (h'A : Œº A ‚â† ‚àû)
  {r : ‚Ñù‚â•0‚àû} (hr : r < Œº A) :
  ‚àÉ K, is_compact K ‚àß K ‚äÜ A ‚àß r < Œº K :=
begin
  rcases hA.exists_is_compact_is_open_lt_add h'A (ennreal.zero_lt_sub_iff_lt.2 hr)
    with ‚ü®K, U, hKc, hUo, hKA, hAU, hKU‚ü©,
  refine ‚ü®K, hKc, hKA, lt_of_add_lt_add_right (lt_of_le_of_lt _ hKU)‚ü©,
  rw ennreal.add_sub_cancel_of_le hr.le, exact measure_mono hAU
end

/-- Given a regular measure, any measurable set of finite mass can be approximated from
inside by compact sets. -/
lemma _root_.measurable_set.measure_eq_supr_is_compact_of_ne_top
  [opens_measurable_space Œ±] [t2_space Œ±] [regular Œº]
  ‚¶ÉA : set Œ±‚¶Ñ (hA : measurable_set A) (h'A : Œº A ‚â† ‚àû) :
  Œº A = (‚®Ü (K : set Œ±) (h : is_compact K) (h2 : K ‚äÜ A), Œº K) :=
begin
  refine le_antisymm (le_of_forall_lt $ Œª r hr, _) _,
  { simpa only [lt_supr_iff, exists_prop] using hA.exists_lt_is_compact_of_ne_top h'A hr },
  { simp only [supr_le_iff], exact Œª K _ hKA, measure_mono hKA }
end

protected lemma map [opens_measurable_space Œ±] [measurable_space Œ≤] [topological_space Œ≤]
  [t2_space Œ≤] [borel_space Œ≤] [regular Œº] (f : Œ± ‚âÉ‚Çú Œ≤) :
  (measure.map f Œº).regular :=
begin
  have hf := f.measurable,
  have h2f := f.to_equiv.injective.preimage_surjective,
  have h3f := f.to_equiv.surjective,
  haveI := outer_regular.map f Œº,
  split,
  { intros K hK, rw [map_apply hf hK.measurable_set],
    apply regular.lt_top_of_is_compact,
    rwa f.compact_preimage },
  { intros U hU,
    rw [map_apply hf hU.measurable_set, (hU.preimage f.continuous).measure_eq_supr_is_compact],
    refine ge_of_eq _,
    apply supr_congr (preimage f) h2f,
    intro K,
    apply supr_congr_Prop f.compact_preimage,
    intro hK,
    apply supr_congr_Prop h3f.preimage_subset_preimage_iff,
    intro h2U,
    rw [map_apply hf hK.measurable_set] }
end

protected lemma smul [regular Œº] {x : ‚Ñù‚â•0‚àû} (hx : x ‚â† ‚àû) :
  (x ‚Ä¢ Œº).regular :=
begin
  haveI := outer_regular.smul Œº hx,
  split,
  { exact Œª K hK, ennreal.mul_lt_top (lt_top_iff_ne_top.2 hx) (regular.lt_top_of_is_compact hK) },
  { intros U hU, simp only [smul_apply],
    simpa only [ennreal.mul_supr] using (ennreal.mul_left_mono $ regular.inner_regular hU) }
end

/-- A regular measure in a œÉ-compact space is œÉ-finite. -/
@[priority 100] -- see Note [lower instance priority]
instance sigma_finite [opens_measurable_space Œ±] [t2_space Œ±] [sigma_compact_space Œ±]
  [regular Œº] : sigma_finite Œº :=
‚ü®‚ü®{ set := compact_covering Œ±,
  set_mem := Œª n, (is_compact_compact_covering Œ± n).measurable_set,
  finite := Œª n, regular.lt_top_of_is_compact $ is_compact_compact_covering Œ± n,
  spanning := Union_compact_covering Œ± }‚ü©‚ü©

end regular

namespace weakly_regular

lemma _root_.is_open.measure_eq_supr_is_closed ‚¶ÉU : set Œ±‚¶Ñ (hU : is_open U)
  (Œº : measure Œ±) [weakly_regular Œº] :
  Œº U = (‚®Ü (F : set Œ±) (h : is_closed F) (h2 : F ‚äÜ U), Œº F) :=
le_antisymm (weakly_regular.inner_regular hU)
  (supr_le $ Œª s, supr_le $ Œª hs, supr_le $ Œª h2s, Œº.mono h2s)

lemma _root_.is_open.exists_lt_is_closed_of_gt
  [weakly_regular Œº] ‚¶ÉU : set Œ±‚¶Ñ (hU : is_open U) (r : ‚Ñù‚â•0‚àû) (hr : r < Œº U) :
  ‚àÉ F, is_closed F ‚àß F ‚äÜ U ‚àß r < Œº F :=
begin
  rw hU.measure_eq_supr_is_closed at hr,
  simpa only [lt_supr_iff, exists_prop] using hr,
end

lemma _root_.is_open.exists_is_closed_lt_add [weakly_regular Œº] [opens_measurable_space Œ±]
  ‚¶ÉU : set Œ±‚¶Ñ (hU : is_open U) (hŒºU : Œº U ‚â† ‚àû) {r : ‚Ñù‚â•0‚àû} (hr : 0 < r) :
  ‚àÉ K, is_closed K ‚àß K ‚äÜ U ‚àß Œº U < Œº K + r :=
begin
  cases eq_or_ne (Œº U) 0 with h‚ÇÄ h‚ÇÄ,
  { refine ‚ü®‚àÖ, is_closed_empty, empty_subset _, _‚ü©,
    rwa [measure_empty, h‚ÇÄ, zero_add] },
  { rcases hU.exists_lt_is_closed_of_gt _ (ennreal.sub_lt_self hŒºU h‚ÇÄ hr) with ‚ü®K, hKc, hKU, hrK‚ü©,
    exact ‚ü®K, hKc, hKU, ennreal.lt_add_of_sub_lt (or.inl hŒºU) hrK‚ü© }
end

/-- If `s` is a measurable set, a weakly regular measure `Œº` is finite on `s`, and `r` is a positive
number, then there exist a closed set `K ‚äÜ s` and an open set `U ‚äá s` such that `Œº U < Œº K + r`. -/
lemma _root_.measurable_set.exists_is_closed_is_open_lt_add [weakly_regular Œº]
  [opens_measurable_space Œ±] {s : set Œ±} (hs : measurable_set s) (hŒºs : Œº s ‚â† ‚àû)
  {r : ‚Ñù‚â•0‚àû} (hr : 0 < r) :
  ‚àÉ K U, is_closed K ‚àß is_open U ‚àß K ‚äÜ s ‚àß s ‚äÜ U ‚àß Œº U < Œº K + r :=
begin
  rcases s.exists_is_open_lt_of_lt (Œº s + r / 2) (ennreal.lt_add_right hŒºs (ennreal.half_pos hr))
    with ‚ü®U, hUo, hsU, hŒºU‚ü©,
  have : Œº (U \ s) < r / 2,
  { rw [measure_diff hsU hUo.measurable_set hs hŒºs],
    refine ennreal.sub_lt_of_lt_add (measure_mono hsU) _, rwa add_comm },
  rcases (U \ s).exists_is_open_lt_of_lt _ this with ‚ü®U', hU'o, hsU', hŒºU'‚ü©,
  rw diff_subset_comm at hsU',
  rcases hUo.exists_is_closed_lt_add (ne_top_of_lt hŒºU) (ennreal.half_pos hr)
    with ‚ü®K, hKc, hKU, hKr‚ü©,
  refine ‚ü®K \ U', U, hKc.inter hU'o.is_closed_compl, hUo, Œª x hx, hsU' ‚ü®hKU hx.1, hx.2‚ü©, hsU, _‚ü©,
  rw [‚Üê ennreal.add_halves r, ‚Üê add_assoc],
  refine hKr.trans_le (add_le_add_right _ _),
  calc Œº K ‚â§ Œº K - Œº U' + Œº U' : ennreal.le_sub_add_self
       ... ‚â§ _                 : add_le_add le_measure_diff hŒºU'.le,
end

/-- Given a weakly regular measure, any measurable set of finite mass can be approximated from
inside by closed sets. -/
lemma _root_.measurable_set.exists_lt_is_closed_of_ne_top [weakly_regular Œº]
  [opens_measurable_space Œ±] ‚¶ÉA : set Œ±‚¶Ñ (hA : measurable_set A) (h'A : Œº A ‚â† ‚àû)
  {r : ‚Ñù‚â•0‚àû} (hr : r < Œº A) :
  ‚àÉ K, is_closed K ‚àß K ‚äÜ A ‚àß r < Œº K :=
begin
  rcases hA.exists_is_closed_is_open_lt_add h'A (ennreal.zero_lt_sub_iff_lt.2 hr)
    with ‚ü®K, U, hKc, hUo, hKA, hAU, hKU‚ü©,
  refine ‚ü®K, hKc, hKA, lt_of_add_lt_add_right (lt_of_le_of_lt _ hKU)‚ü©,
  rw ennreal.add_sub_cancel_of_le hr.le, exact measure_mono hAU
end

/-- Given a weakly regular measure, any measurable set of finite mass can be approximated from
inside by closed sets. -/
lemma _root_.measurable_set.measure_eq_supr_is_closed_of_ne_top
  [opens_measurable_space Œ±] [weakly_regular Œº]
  ‚¶ÉA : set Œ±‚¶Ñ (hA : measurable_set A) (h'A : Œº A ‚â† ‚àû) :
  Œº A = (‚®Ü (K : set Œ±) (h : is_closed K) (h2 : K ‚äÜ A), Œº K) :=
begin
  refine le_antisymm (le_of_forall_lt $ Œª r hr, _) _,
  { simpa only [lt_supr_iff, exists_prop] using hA.exists_lt_is_closed_of_ne_top h'A hr },
  { simp only [supr_le_iff], exact Œª K _ hKA, measure_mono hKA }
end

/-- In a finite measure space, assume that any open set can be approximated from inside by closed
sets. Then any measurable set can be approximated from inside by closed sets, and from outside
by open sets. -/
lemma exists_closed_subset_self_subset_open_of_pos [borel_space Œ±]
  (Œº : measure Œ±) [is_finite_measure Œº]
  (h0 : ‚àÄ (U : set Œ±), is_open U ‚Üí Œº U ‚â§ ‚®Ü (F : set Œ±) (hF : is_closed F) (FU : F ‚äÜ U), Œº F) :
  ‚àÄ ‚¶És : set Œ±‚¶Ñ (hs : measurable_set s),
  ‚àÄ Œµ > 0, (‚àÉ (U : set Œ±), is_open U ‚àß s ‚äÜ U ‚àß Œº U ‚â§ Œº s + Œµ)
    ‚àß (‚àÉ (F : set Œ±), is_closed F ‚àß F ‚äÜ s ‚àß Œº s ‚â§ Œº F + Œµ) :=
begin
  refine measurable_space.induction_on_inter borel_space.measurable_eq is_pi_system_is_open _ _ _ _,
  /- The proof is by measurable induction: we should check that the property is true for the empty
  set, for open sets, and is stable by taking the complement and by taking countable disjoint
  unions. The point of the property we are proving is that it is stable by taking complements
  (exchanging the roles of closed and open sets and thanks to the finiteness of the measure). -/
  -- check for empty set
  { assume Œµ hŒµ,
    exact ‚ü®‚ü®‚àÖ, is_open_empty, subset.refl _, by simp only [measure_empty, zero_le]‚ü©,
            ‚ü®‚àÖ, is_closed_empty, subset.refl _, by simp only [measure_empty, zero_le]‚ü©‚ü© },
  -- check for open sets. This is essentially our assumption `h0`.
  { assume U hU Œµ hŒµ,
    refine ‚ü®‚ü®U, hU, subset.refl _, le_self_add‚ü©, _‚ü©,
    have : Œº U + Œµ ‚â§ ‚®Ü (F : set Œ±) (hF : is_closed F) (FU : F ‚äÜ U), (Œº F + Œµ),
    { haveI : nonempty {i // is_closed i ‚àß i ‚äÜ U} := ‚ü®‚ü®‚àÖ, is_closed_empty, empty_subset _‚ü©‚ü©,
      simp_rw [supr_and', supr_subtype', ‚Üê ennreal.supr_add],
      refine add_le_add _ (le_refl _),
      convert h0 U hU using 1,
      simp_rw [supr_and', supr_subtype'], },
    have : Œº U < (‚®Ü (F : set Œ±) (hF : is_closed F) (FU : F ‚äÜ U), (Œº F + Œµ)),
    { apply lt_of_lt_of_le _ this,
      simpa using (ennreal.add_lt_add_iff_left (measure_ne_top Œº U)).2 hŒµ },
    simp only [lt_supr_iff] at this,
    rcases this with ‚ü®F, F_closed, FU, ŒºF‚ü©,
    exact ‚ü®F, F_closed, FU, ŒºF.le‚ü© },
  -- check for complements
  { assume s hs h Œµ Œµpos,
    rcases h Œµ Œµpos with ‚ü®‚ü®U, U_open, U_subset, nu_U‚ü©, ‚ü®F, F_closed, F_subset, nu_F‚ü©‚ü©,
    refine ‚ü®‚ü®F·∂ú, is_open_compl_iff.2 F_closed, compl_subset_compl.2 F_subset, _‚ü©,
            ‚ü®U·∂ú, is_closed_compl_iff.2 U_open, compl_subset_compl.2 U_subset, _‚ü©‚ü©,
    { apply ennreal.le_of_add_le_add_left (measure_ne_top Œº F),
      calc
        Œº F + Œº F·∂ú = Œº s + Œº s·∂ú :
          by rw [measure_add_measure_compl hs, measure_add_measure_compl F_closed.measurable_set]
        ... ‚â§ (Œº F + Œµ) + Œº s·∂ú : add_le_add_right nu_F _
        ... = Œº F + (Œº s·∂ú + Œµ) : by ac_refl },
    { apply ennreal.le_of_add_le_add_left (measure_ne_top Œº s),
      calc
        Œº s + Œº s·∂ú = Œº U + Œº U·∂ú :
          by rw [measure_add_measure_compl hs, measure_add_measure_compl U_open.measurable_set]
        ... ‚â§ (Œº s + Œµ) + Œº U·∂ú : add_le_add nu_U (le_refl _)
        ... = Œº s + (Œº U·∂ú + Œµ) : by abel } },
  -- check for disjoint unions
  { assume s s_disj s_meas hs Œµ Œµpos,
    split,
    -- the approximating open set is constructed by taking for each `s n` an approximating open set
    -- `U n` with measure at most `Œº (s n) + Œ¥ n` for a summable `Œ¥`, and taking the union of these.
    { rcases ennreal.exists_pos_sum_of_encodable' Œµpos.lt.ne' ‚Ñï with ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü©,
      have : ‚àÄ n, ‚àÉ (U : set Œ±), is_open U ‚àß s n ‚äÜ U ‚àß Œº U ‚â§ Œº (s n) + Œ¥ n :=
        Œª n, (hs n _ (Œ¥pos n).gt).1,
      choose U hU using this,
      refine ‚ü®(‚ãÉ n, U n), is_open_Union (Œª n, (hU n).1), Union_subset_Union (Œª n, (hU n).2.1), _‚ü©,
      calc
      Œº (‚ãÉ (n : ‚Ñï), U n)
          ‚â§ ‚àë' n, Œº (U n) : measure_Union_le _
      ... ‚â§ ‚àë' n, (Œº (s n) + Œ¥ n) : ennreal.tsum_le_tsum (Œª n, (hU n).2.2)
      ... ‚â§ Œº (‚ãÉ (i : ‚Ñï), s i) + Œµ :
      begin
        rw [ennreal.tsum_add],
        refine add_le_add (le_of_eq _) hŒ¥.le,
        exact (measure_Union s_disj s_meas).symm,
      end },
    -- the approximating closed set is constructed by considering finitely many sets `s i`, which
    -- cover all the measure up to `Œµ/2`, approximating each of these by a closed set `F i`, and
    -- taking the union of these (finitely many) `F i`.
    { set Œ¥ := Œµ / 2 with hŒ¥,
      have Œ¥pos : 0 < Œ¥ := ennreal.half_pos Œµpos.lt.ne',
      have L : tendsto (Œª n, ‚àë i in finset.range n, Œº (s i) + Œ¥) at_top (ùìù (Œº (‚ãÉ i, s i) + Œ¥)),
      { rw measure_Union s_disj s_meas,
        refine tendsto.add (ennreal.tendsto_nat_tsum _) tendsto_const_nhds },
      have nu_lt : Œº (‚ãÉ i, s i) < Œº (‚ãÉ i, s i) + Œ¥,
        by simpa only [add_zero] using (ennreal.add_lt_add_iff_left (measure_ne_top Œº _)).mpr Œ¥pos,
      obtain ‚ü®n, hn, npos‚ü© :
        ‚àÉ n, (Œº (‚ãÉ (i : ‚Ñï), s i) < ‚àë (i : ‚Ñï) in finset.range n, Œº (s i) + Œ¥) ‚àß (0 < n) :=
      (((tendsto_order.1 L).1 _ nu_lt).and (eventually_gt_at_top 0)).exists,
      have : ‚àÄ i, ‚àÉ (F : set Œ±), is_closed F ‚àß F ‚äÜ s i ‚àß Œº (s i) ‚â§ Œº F + Œ¥ / n :=
        Œª i, (hs i _ (ennreal.div_pos_iff.2 ‚ü®ne_of_gt Œ¥pos, ennreal.nat_ne_top n‚ü©)).2,
      choose F hF using this,
      have F_disj: pairwise (disjoint on F) :=
        s_disj.mono (Œª i j hij, disjoint.mono (hF i).2.1 (hF j).2.1 hij),
      refine ‚ü®‚ãÉ i ‚àà finset.range n, F i, _, _, _‚ü©,
      { exact is_closed_bUnion (by simpa using finite_lt_nat n) (Œª i hi, (hF i).1) },
      { assume x hx,
        simp only [exists_prop, mem_Union, finset.mem_range] at hx,
        rcases hx with ‚ü®i, i_lt, hi‚ü©,
        simp only [mem_Union],
        exact ‚ü®i, (hF i).2.1 hi‚ü© },
      { calc
        Œº (‚ãÉ (i : ‚Ñï), s i)
            ‚â§ ‚àë (i : ‚Ñï) in finset.range n, Œº (s i) + Œ¥ : hn.le
        ... ‚â§ (‚àë (i : ‚Ñï) in finset.range n, (Œº (F i) + Œ¥ / n)) + Œ¥ :
          add_le_add (finset.sum_le_sum (Œª i hi, (hF i).2.2)) (le_refl _)
        ... = Œº (‚ãÉ i ‚àà finset.range n, F i) + Œµ :
        begin
          simp only [finset.sum_add_distrib, finset.sum_const, nsmul_eq_mul, finset.card_range],
          rw [ennreal.mul_div_cancel' _ (ennreal.nat_ne_top n),
              measure_bUnion_finset (F_disj.pairwise_on _) (Œª i hi, (hF i).1.measurable_set),
              hŒ¥, add_assoc, ennreal.add_halves],
          simpa only [ne.def, nat.cast_eq_zero] using ne_of_gt npos
        end } } }
end

/-- In a finite measure space, if every open set can be approximated from inside by closed sets,
then the measure is weakly regular -/
theorem weakly_regular_of_inner_regular_of_is_finite_measure [borel_space Œ±]
  (Œº : measure Œ±) [is_finite_measure Œº]
  (h0 : ‚àÄ (U : set Œ±), is_open U ‚Üí Œº U ‚â§ ‚®Ü (F : set Œ±) (hF : is_closed F) (FU : F ‚äÜ U), Œº F) :
  weakly_regular Œº :=
{ outer_regular := begin
    assume s hs,
    apply ennreal.le_of_forall_pos_le_add (Œª Œµ Œµpos h, le_of_lt _),
    rcases exists_between (ennreal.coe_lt_coe.2 Œµpos) with ‚ü®Œ¥, Œ¥pos, Œ¥Œµ‚ü©,
    simp only [infi_lt_iff],
    rcases (exists_closed_subset_self_subset_open_of_pos Œº h0 hs Œ¥ Œ¥pos).1
      with ‚ü®U, U_open, sU, ŒºU‚ü©,
    refine ‚ü®U, U_open, sU, ŒºU.trans_lt _‚ü©,
    rwa ennreal.add_lt_add_iff_left (measure_ne_top Œº s),
  end,
  inner_regular := h0 }

/-- The restriction of a weakly regular measure to an open set of finite measure is
weakly regular. Superseded by `restrict_of_measurable_set`, proving the same statement for
measurable sets instead of open sets. -/
lemma restrict_of_is_open [borel_space Œ±] [weakly_regular Œº]
  (U : set Œ±) (hU : is_open U) (h'U : Œº U < ‚àû) : weakly_regular (Œº.restrict U) :=
begin
  haveI : fact (Œº U < ‚àû) := ‚ü®h'U‚ü©,
  refine weakly_regular_of_inner_regular_of_is_finite_measure _ (Œª V V_open, _),
  simp only [restrict_apply' hU.measurable_set],
  refine le_trans (weakly_regular.inner_regular (V_open.inter hU)) _,
  simp only [and_imp, supr_le_iff, subset_inter_iff],
  assume F F_closed FV FU,
  have : F = F ‚à© U :=
    subset.antisymm (by simp [subset.refl, FU]) (inter_subset_left _ _),
  conv_lhs {rw this},
  simp_rw [supr_and', supr_subtype'],
  exact le_supr (Œª s : {s // is_closed s ‚àß s ‚äÜ V}, Œº (s ‚à© U)) ‚ü®F, F_closed, FV‚ü©,
end

lemma _root_.measurable_set.exists_lt_is_closed_of_ne_top_of_pos [opens_measurable_space Œ±]
  [weakly_regular Œº] ‚¶ÉA : set Œ±‚¶Ñ (hA : measurable_set A) (h'A : Œº A ‚â† ‚àû) {Œµ : ‚Ñù‚â•0‚àû} (Œµpos : 0 < Œµ) :
  ‚àÉ F, is_closed F ‚àß F ‚äÜ A ‚àß Œº A < Œº F + Œµ :=
begin
  have : Œº A < ‚®Ü (F : set Œ±) (h : is_closed F) (h2 : F ‚äÜ A), (Œº F + Œµ),
  { haveI : nonempty {F // is_closed F ‚àß F ‚äÜ A} := ‚ü®‚ü®‚àÖ, is_closed_empty, empty_subset _‚ü©‚ü©,
    simp_rw [hA.measure_eq_supr_is_closed_of_ne_top h'A, supr_and',
      supr_subtype', ‚Üê ennreal.supr_add],
    simpa only [add_zero, ennreal.coe_zero] using (ennreal.add_lt_add_iff_left _).mpr Œµpos,
    convert h'A,
    simp_rw [hA.measure_eq_supr_is_closed_of_ne_top h'A, supr_and', supr_subtype'] },
  simpa only [lt_supr_iff, exists_prop],
end

/-- The restriction of a weakly regular measure to a measurable set of finite measure is
weakly regular. -/
lemma restrict_of_measurable_set [borel_space Œ±] [weakly_regular Œº]
  (A : set Œ±) (hA : measurable_set A) (h'A : Œº A ‚â† ‚àû) : weakly_regular (Œº.restrict A) :=
begin
  haveI : fact (Œº A < ‚àû) := ‚ü®lt_top_iff_ne_top.2 h'A‚ü©,
  refine weakly_regular_of_inner_regular_of_is_finite_measure _ (Œª V V_open, _),
  simp only [restrict_apply' hA],
  rw (V_open.measurable_set.inter hA).measure_eq_supr_is_closed_of_ne_top,
  { simp only [and_imp, supr_le_iff, subset_inter_iff],
    assume F F_closed FV FU,
    have : F = F ‚à© A :=
      subset.antisymm (by simp [subset.refl, FU]) (inter_subset_left _ _),
    conv_lhs {rw this},
    simp_rw [supr_and', supr_subtype'],
    exact le_supr (Œª s : {s // is_closed s ‚àß s ‚äÜ V}, Œº (s ‚à© A)) ‚ü®F, F_closed, FV‚ü© },
  { exact ne_top_of_le_ne_top h'A (measure_mono (inter_subset_right _ _)) },
  { apply_instance }
end

/-- In a metric space (or even a pseudo emetric space), an open set can be approximated from inside
by closed sets. -/
lemma inner_regular_of_pseudo_emetric_space {X : Type*}
  [pseudo_emetric_space X] [measurable_space X] [borel_space X] (Œº : measure X)
  (U : set X) (U_open : is_open U) :
  Œº U ‚â§ ‚®Ü (F : set X) (hF : is_closed F) (FU : F ‚äÜ U), Œº F :=
begin
  rcases U_open.exists_Union_is_closed with ‚ü®F, F_closed, F_subset, F_Union, F_mono‚ü©,
  conv_lhs { rw ‚Üê F_Union },
  rw measure_Union_eq_supr (Œª n, (F_closed n).measurable_set) F_mono.directed_le,
  simp only [supr_le_iff],
  assume n,
  simp_rw [supr_and', supr_subtype'],
  exact le_supr (Œª s : {s // is_closed s ‚àß s ‚äÜ U}, Œº s) ‚ü®F n, F_closed n, F_subset n‚ü©,
end

/-- Any finite measure on a metric space (or even a pseudo emetric space) is weakly regular. -/
@[priority 100] -- see Note [lower instance priority]
instance of_pseudo_emetric_space_of_is_finite_measure {X : Type*} [pseudo_emetric_space X]
  [measurable_space X] [borel_space X] (Œº : measure X) [is_finite_measure Œº] :
  weakly_regular Œº :=
weakly_regular_of_inner_regular_of_is_finite_measure Œº $ inner_regular_of_pseudo_emetric_space Œº

end weakly_regular

namespace regular

/-- Any locally finite measure on a sigma compact locally compact metric space is regular. -/
@[priority 100] -- see Note [lower instance priority]
instance of_sigma_compact_space_of_is_locally_finite_measure {X : Type*}
  [emetric_space X] [sigma_compact_space X] [locally_compact_space X]
  [measurable_space X] [borel_space X] (Œº : measure X)
  [is_locally_finite_measure Œº] : regular Œº :=
{ lt_top_of_is_compact := Œª K hK, hK.measure_lt_top,
  inner_regular :=
  begin
    /- Given an open set, it can be approximated from inside by closed sets thanks to
    `inner_regular_of_pseudo_emetric_space`. Each such closed set `F` can be approximated by a
    compact set `B n ‚à© F`, using a compact exhaustion `B n` of the space. -/
    assume U U_open,
    refine (weakly_regular.inner_regular_of_pseudo_emetric_space Œº U U_open).trans _,
    simp only [supr_le_iff],
    assume F F_closed FU,
    have B : compact_exhaustion X := default _,
    have : F = ‚ãÉ n, B n ‚à© F, by rw [‚Üê Union_inter, B.Union_eq, univ_inter],
    rw [this, measure_Union_eq_supr
      (Œª n, (B.is_compact n).measurable_set.inter F_closed.measurable_set)],
    { simp_rw [supr_le_iff, supr_and', supr_subtype'],
      assume n,
      exact le_supr (Œª K : {K // is_compact K ‚àß K ‚äÜ U}, Œº K)
        ‚ü®B n ‚à© F, is_compact.inter_right (B.is_compact n) F_closed,
          subset.trans (inter_subset_right _ _) FU‚ü© },
    { apply (monotone_nat_of_le_succ (Œª n, _)).directed_le,
      exact inter_subset_inter_left _ (B.subset_succ n) },
  end,
  outer_regular :=
  begin
    /- let `B n` be a compact exhaustion of the space, and let `C n = B n \ B (n-1)`. Consider a
    measurable set `A`. Then `A ‚à© C n` is a set of finite measure, that can be approximated by an
    open set `U n` (by using the fact that the measure restricted to `B (n+1)` is finite, and
    therefore weakly regular). The union of the `U n` is an open set containing `A`, with measure
    arbitrarily close to that of `A`.
    -/
    assume A hA,
    apply ennreal.le_of_forall_pos_le_add (Œª Œµ Œµpos ŒºA, le_of_lt _),
    rcases ennreal.exists_pos_sum_of_encodable' (ennreal.coe_pos.2 Œµpos).ne' ‚Ñï with ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü©,
    have B : compact_exhaustion X := default _,
    let C := disjointed (Œª n, B n),
    have C_meas : ‚àÄ n, measurable_set (C n) :=
      measurable_set.disjointed (Œª n, (B.is_compact n).measurable_set),
    have A_eq : A = (‚ãÉ n, A ‚à© C n),
      by simp_rw [‚Üê inter_Union, C, Union_disjointed, compact_exhaustion.Union_eq, inter_univ],
    have ŒºA_eq : Œº A = ‚àë' n, Œº (A ‚à© C n),
    { conv_lhs { rw A_eq },
      rw measure_Union,
      { assume m n hmn,
        exact disjoint.mono (inter_subset_right _ _) (inter_subset_right _ _)
          (disjoint_disjointed _ m n hmn), },
      { exact (Œª n, hA.inter (C_meas n)) } },
    have : ‚àÄ n, ‚àÉ U, is_open U ‚àß (A ‚à© C n ‚äÜ U) ‚àß (Œº U ‚â§ Œº (A ‚à© C n) + Œ¥ n),
    { assume n,
      set ŒΩ := Œº.restrict (B (n+1)) with hŒΩ,
      haveI : is_finite_measure ŒΩ :=
      ‚ü®begin
        rw [restrict_apply measurable_set.univ, univ_inter],
        exact is_compact.measure_lt_top (B.is_compact _),
      end‚ü©,
      have : (‚®Ö (U : set X) (h : is_open U) (h2 : A ‚à© C n ‚äÜ U), ŒΩ U) < ŒΩ (A ‚à© C n) + Œ¥ n :=
      begin
        refine (outer_regular.outer_regular (hA.inter (C_meas n))).trans_lt _,
        simpa only [add_zero] using (ennreal.add_lt_add_iff_left (measure_ne_top ŒΩ _)).mpr (Œ¥pos n),
      end,
      simp only [infi_lt_iff] at this,
      rcases this with ‚ü®U, U_open, UA, ŒΩU‚ü©,
      refine ‚ü®U ‚à© interior (B (n+1)), U_open.inter is_open_interior, _, _‚ü©,
      { simp only [UA, true_and, subset_inter_iff],
        refine (inter_subset_right _ _).trans _,
        exact (disjointed_subset _ n).trans (B.subset_interior_succ _) },
      { simp only [hŒΩ, restrict_apply' (B.is_compact _).measurable_set] at ŒΩU,
        calc Œº (U ‚à© interior (B (n + 1))) ‚â§ Œº (U ‚à© B (n + 1)) :
          measure_mono (inter_subset_inter_right _ interior_subset)
        ... ‚â§ Œº (A ‚à© C n ‚à© B (n + 1)) + Œ¥ n : ŒΩU.le
        ... ‚â§ Œº (A ‚à© C n) + Œ¥ n :
          add_le_add (measure_mono (inter_subset_left _ _)) (le_refl _) } },
    choose U hU using this,
    simp_rw [infi_lt_iff],
    refine ‚ü®‚ãÉ n, U n, is_open_Union (Œª n, (hU n).1), _, _‚ü©,
    { rw A_eq, exact Union_subset_Union (Œª n, (hU n).2.1) },
    { calc Œº (‚ãÉ (n : ‚Ñï), U n)
          ‚â§ ‚àë' n, Œº (U n) : measure_Union_le _
      ... ‚â§ ‚àë' n, (Œº (A ‚à© C n) + Œ¥ n) : ennreal.tsum_le_tsum (Œª n, (hU n).2.2)
      ... < Œº A + Œµ :
        by { rw [ennreal.tsum_add, ŒºA_eq.symm], exact (ennreal.add_lt_add_iff_left ŒºA.ne).2 hŒ¥ } }
  end }

end regular

end measure
end measure_theory
