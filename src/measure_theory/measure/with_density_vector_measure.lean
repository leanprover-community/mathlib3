/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import measure_theory.measure.vector_measure
import measure_theory.function.ae_eq_of_integral

/-!

# Vector measure defined by an integral

Given a measure `Î¼` and an integrable function `f : Î± â†’ E`, we can define a vector measure `v` such
that for all measurable set `s`, `v i = âˆ« x in s, f x âˆ‚Î¼`. This definition is useful for
the Radon-Nikodym theorem for signed measures.

## Main definitions

* `measure_theory.measure.with_densityáµ¥`: the vector measure formed by integrating a function `f`
  with respect to a measure `Î¼` on some set if `f` is integrable, and `0` otherwise.

-/

noncomputable theory
open_locale classical measure_theory nnreal ennreal

variables {Î± Î² : Type*} {m : measurable_space Î±}

namespace measure_theory

open topological_space

variables {Î¼ Î½ : measure Î±}
variables {E : Type*} [normed_group E] [measurable_space E] [second_countable_topology E]
  [normed_space â„ E] [complete_space E] [borel_space E]

/-- Given a measure `Î¼` and an integrable function `f`, `Î¼.with_densityáµ¥ f` is
the vector measure which maps the set `s` to `âˆ«â‚› f âˆ‚Î¼`. -/
def measure.with_densityáµ¥ {m : measurable_space Î±} (Î¼ : measure Î±) (f : Î± â†’ E) :
  vector_measure Î± E :=
if hf : integrable f Î¼ then
{ measure_of' := Î» s, if measurable_set s then âˆ« x in s, f x âˆ‚Î¼ else 0,
  empty' := by simp,
  not_measurable' := Î» s hs, if_neg hs,
  m_Union' := Î» s hsâ‚ hsâ‚‚,
  begin
    convert has_sum_integral_Union hsâ‚ hsâ‚‚ hf,
    { ext n, rw if_pos (hsâ‚ n) },
    { rw if_pos (measurable_set.Union hsâ‚) }
  end }
else 0

open measure

include m

variables {f g : Î± â†’ E}

lemma with_densityáµ¥_apply (hf : integrable f Î¼) {s : set Î±} (hs : measurable_set s) :
  Î¼.with_densityáµ¥ f s = âˆ« x in s, f x âˆ‚Î¼ :=
by { rw [with_densityáµ¥, dif_pos hf], exact dif_pos hs }

@[simp] lemma with_densityáµ¥_zero : Î¼.with_densityáµ¥ (0 : Î± â†’ E) = 0 :=
by { ext1 s hs, erw [with_densityáµ¥_apply (integrable_zero Î± E Î¼) hs], simp, }

@[simp] lemma with_densityáµ¥_neg : Î¼.with_densityáµ¥ (-f) = -Î¼.with_densityáµ¥ f :=
begin
  by_cases hf : integrable f Î¼,
  { ext1 i hi,
    rw [vector_measure.neg_apply, with_densityáµ¥_apply hf hi,
        â† integral_neg, with_densityáµ¥_apply hf.neg hi],
    refl },
  { rw [with_densityáµ¥, with_densityáµ¥, dif_neg hf, dif_neg, neg_zero],
    rwa integrable_neg_iff }
end

lemma with_densityáµ¥_neg' : Î¼.with_densityáµ¥ (Î» x, -f x) = -Î¼.with_densityáµ¥ f :=
with_densityáµ¥_neg

@[simp] lemma with_densityáµ¥_add (hf : integrable f Î¼) (hg : integrable g Î¼) :
  Î¼.with_densityáµ¥ (f + g) = Î¼.with_densityáµ¥ f + Î¼.with_densityáµ¥ g :=
begin
  ext1 i hi,
  rw [with_densityáµ¥_apply (hf.add hg) hi, vector_measure.add_apply,
      with_densityáµ¥_apply hf hi, with_densityáµ¥_apply hg hi],
  simp_rw [pi.add_apply],
  rw integral_add; rw â† integrable_on_univ,
  { exact hf.integrable_on.restrict measurable_set.univ },
  { exact hg.integrable_on.restrict measurable_set.univ }
end

lemma with_densityáµ¥_add' (hf : integrable f Î¼) (hg : integrable g Î¼) :
  Î¼.with_densityáµ¥ (Î» x, f x + g x) = Î¼.with_densityáµ¥ f + Î¼.with_densityáµ¥ g :=
with_densityáµ¥_add hf hg

@[simp] lemma with_densityáµ¥_sub (hf : integrable f Î¼) (hg : integrable g Î¼) :
  Î¼.with_densityáµ¥ (f - g) = Î¼.with_densityáµ¥ f - Î¼.with_densityáµ¥ g :=
by rw [sub_eq_add_neg, sub_eq_add_neg, with_densityáµ¥_add hf hg.neg, with_densityáµ¥_neg]

lemma with_densityáµ¥_sub' (hf : integrable f Î¼) (hg : integrable g Î¼) :
  Î¼.with_densityáµ¥ (Î» x, f x - g x) = Î¼.with_densityáµ¥ f - Î¼.with_densityáµ¥ g :=
with_densityáµ¥_sub hf hg

@[simp] lemma with_densityáµ¥_smul {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] [normed_space ğ•œ E]
  [smul_comm_class â„ ğ•œ E] [measurable_space ğ•œ] [opens_measurable_space ğ•œ] (r : ğ•œ) :
  Î¼.with_densityáµ¥ (r â€¢ f) = r â€¢ Î¼.with_densityáµ¥ f :=
begin
  by_cases hf : integrable f Î¼,
  { ext1 i hi,
    rw [with_densityáµ¥_apply (hf.smul r) hi, vector_measure.smul_apply,
        with_densityáµ¥_apply hf hi, â† integral_smul r f],
    refl },
  { by_cases hr : r = 0,
    { rw [hr, zero_smul, zero_smul, with_densityáµ¥_zero] },
    { rw [with_densityáµ¥, with_densityáµ¥, dif_neg hf, dif_neg, smul_zero],
      rwa integrable_smul_iff hr f } }
end

lemma with_densityáµ¥_smul' {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] [normed_space ğ•œ E]
  [smul_comm_class â„ ğ•œ E] [measurable_space ğ•œ] [opens_measurable_space ğ•œ] (r : ğ•œ) :
  Î¼.with_densityáµ¥ (Î» x, r â€¢ f x) = r â€¢ Î¼.with_densityáµ¥ f :=
with_densityáµ¥_smul r

lemma measure.with_densityáµ¥_absolutely_continuous (Î¼ : measure Î±) (f : Î± â†’ â„) :
  Î¼.with_densityáµ¥ f â‰ª Î¼.to_ennreal_vector_measure :=
begin
  by_cases hf : integrable f Î¼,
  { refine vector_measure.absolutely_continuous.mk (Î» i hiâ‚ hiâ‚‚, _),
    rw to_ennreal_vector_measure_apply_measurable hiâ‚ at hiâ‚‚,
    rw [with_densityáµ¥_apply hf hiâ‚, measure.restrict_zero_set hiâ‚‚, integral_zero_measure] },
  { rw [with_densityáµ¥, dif_neg hf],
    exact vector_measure.absolutely_continuous.zero _ }
end

/-- Having the same density implies the underlying functions are equal almost everywhere. -/
lemma integrable.ae_eq_of_with_densityáµ¥_eq {f g : Î± â†’ E} (hf : integrable f Î¼) (hg : integrable g Î¼)
  (hfg : Î¼.with_densityáµ¥ f = Î¼.with_densityáµ¥ g) :
  f =áµ[Î¼] g :=
begin
  refine hf.ae_eq_of_forall_set_integral_eq f g hg (Î» i hi _, _),
  rw [â† with_densityáµ¥_apply hf hi, hfg, with_densityáµ¥_apply hg hi]
end

lemma with_densityáµ¥_eq.congr_ae {f g : Î± â†’ E} (h : f =áµ[Î¼] g) :
  Î¼.with_densityáµ¥ f = Î¼.with_densityáµ¥ g :=
begin
  by_cases hf : integrable f Î¼,
  { ext i hi,
    rw [with_densityáµ¥_apply hf hi, with_densityáµ¥_apply (hf.congr h) hi],
    exact integral_congr_ae (ae_restrict_of_ae h) },
  { have hg : Â¬ integrable g Î¼,
    { intro hg, exact hf (hg.congr h.symm) },
    rw [with_densityáµ¥, with_densityáµ¥, dif_neg hf, dif_neg hg] }
end

lemma integrable.with_densityáµ¥_eq_iff {f g : Î± â†’ E}
  (hf : integrable f Î¼) (hg : integrable g Î¼) :
  Î¼.with_densityáµ¥ f = Î¼.with_densityáµ¥ g â†” f =áµ[Î¼] g :=
âŸ¨Î» hfg, hf.ae_eq_of_with_densityáµ¥_eq hg hfg, Î» h, with_densityáµ¥_eq.congr_ae hâŸ©

section signed_measure

lemma with_densityáµ¥_to_real {f : Î± â†’ â„â‰¥0âˆ} (hfm : ae_measurable f Î¼) (hf : âˆ«â» x, f x âˆ‚Î¼ â‰  âˆ) :
  Î¼.with_densityáµ¥ (Î» x, (f x).to_real) =
  @to_signed_measure Î± _ (Î¼.with_density f) (is_finite_measure_with_density hf) :=
begin
  have hfi := integrable_to_real_of_lintegral_ne_top hfm hf,
  ext i hi,
  rw [with_densityáµ¥_apply hfi hi, to_signed_measure_apply_measurable hi,
      with_density_apply _ hi, integral_to_real hfm.restrict],
  refine ae_lt_top' hfm.restrict (ne_top_of_le_ne_top hf _),
  conv_rhs { rw â† set_lintegral_univ },
  exact lintegral_mono_set (set.subset_univ _),
end

lemma with_densityáµ¥_eq_with_density_pos_part_sub_with_density_neg_part
  {f : Î± â†’ â„} (hfi : integrable f Î¼) :
  Î¼.with_densityáµ¥ f =
  @to_signed_measure Î± _ (Î¼.with_density (Î» x, ennreal.of_real $ f x))
    (is_finite_measure_with_density_of_real hfi.2) -
  @to_signed_measure Î± _ (Î¼.with_density (Î» x, ennreal.of_real $ -f x))
    (is_finite_measure_with_density_of_real hfi.neg.2) :=
begin
  ext i hi,
  rw [with_densityáµ¥_apply hfi hi,
      integral_eq_lintegral_pos_part_sub_lintegral_neg_part hfi.integrable_on,
      vector_measure.sub_apply, to_signed_measure_apply_measurable hi,
      to_signed_measure_apply_measurable hi, with_density_apply _ hi, with_density_apply _ hi],
end

end signed_measure

end measure_theory
