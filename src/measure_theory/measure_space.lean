/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import measure_theory.outer_measure
import order.filter.countable_Inter

/-!
# Measure spaces

Given a measurable space `Î±`, a measure on `Î±` is a function that sends measurable sets to the
extended nonnegative reals that satisfies the following conditions:
1. `Î¼ âˆ… = 0`;
2. `Î¼` is countably additive. This means that the measure of a countable union of pairwise disjoint
   sets is equal to the measure of the individual sets.

Every measure can be canonically extended to an outer measure, so that it assigns values to
all subsets, not just the measurable subsets. On the other hand, a measure that is countably
additive on measurable sets can be restricted to measurable sets to obtain a measure.
In this file a measure is defined to be an outer measure that is countably additive on
measurable sets, with the additional assumption that the outer measure is the canonical
extension of the restricted measure.

In the first part of the file we define operations on arbitrary functions defined on measurable
sets.

Measures on `Î±` form a complete lattice.

Given a measure, the null sets are the sets where `Î¼ s = 0`, where `Î¼` denotes the corresponding
outer measure (so `s` might not be measurable). We can then define the completion of `Î¼` as the
measure on the least `Ïƒ`-algebra that also contains all null sets, by defining the measure to be `0`
on the null sets.

## Main statements

* `completion` is the completion of a measure to all null measurable sets.
* `measure.of_measurable` and `outer_measure.to_measure` are two important ways to define a measure.

## Implementation notes

Given `Î¼ : measure Î±`, `Î¼ s` is the value of the *outer measure* applied to `s`.
This conveniently allows us to apply the measure to sets without proving that they are measurable.
We get countable subadditivity for all sets, but only countable additivity for measurable sets.

You often don't want to define a measure via its constructor.
Two ways that are sometimes more convenient:
* `measure.of_measurable` is a way to define a measure by only giving its value on measurable sets
  and proving the properties (1) and (2) mentioned above.
* `outer_measure.to_measure` is a way of obtaining a measure from an outer measure by showing that
  all measurable sets in the measurable space are CarathÃ©odory measurable.

A `measure_space` is a class that is a measurable space with a canonical measure.
The measure is denoted `volume`.

## References

* <https://en.wikipedia.org/wiki/Measure_(mathematics)>
* <https://en.wikipedia.org/wiki/Complete_measure>
* <https://en.wikipedia.org/wiki/Almost_everywhere>

## Tags

measure, almost everywhere, measure space, completion, null set, null measurable set
-/

noncomputable theory

open classical set filter finset function
open_locale classical topological_space big_operators

universes u v w x

namespace measure_theory

/- We first consider operations on arbitrary functions defined on measurable sets. -/
section of_measurable
parameters {Î± : Type*} [measurable_space Î±]
parameters (m : Î  (s : set Î±), is_measurable s â†’ ennreal)
parameters (m0 : m âˆ… is_measurable.empty = 0)
include m0

/-- We can trivially extend a function defined on measurable sets to all sets by defining it to be
 `âˆ` on the non-measurable sets.

`measure'` is mainly used to derive the outer measure, for the main `measure` projection. -/
def measure' (s : set Î±) : ennreal := â¨… h : is_measurable s, m s h

lemma measure'_eq {s} (h : is_measurable s) : measure' s = m s h :=
by simp [measure', h]

lemma measure'_empty : measure' âˆ… = 0 :=
(measure'_eq is_measurable.empty).trans m0

lemma measure'_Union_nat
  {f : â„• â†’ set Î±}
  (hm : âˆ€i, is_measurable (f i))
  (mU : m (â‹ƒi, f i) (is_measurable.Union hm) = (âˆ‘'i, m (f i) (hm i))) :
  measure' (â‹ƒi, f i) = (âˆ‘'i, measure' (f i)) :=
(measure'_eq _).trans $ mU.trans $
by congr; funext i; rw measure'_eq

/-- Given an arbitrary function on measurable sets, we can define the outer measure corresponding to
  it (this is the unique maximal outer measure that is at most `m` on measurable sets). -/
def outer_measure' : outer_measure Î± :=
outer_measure.of_function measure' measure'_empty

lemma measure'_Union_le_tsum_nat'
  (mU : âˆ€ {f : â„• â†’ set Î±} (hm : âˆ€i, is_measurable (f i)),
    m (â‹ƒi, f i) (is_measurable.Union hm) â‰¤ (âˆ‘'i, m (f i) (hm i)))
  (s : â„• â†’ set Î±) :
  measure' (â‹ƒi, s i) â‰¤ (âˆ‘'i, measure' (s i)) :=
begin
  by_cases h : âˆ€i, is_measurable (s i),
  { rw [measure'_eq _ _ (is_measurable.Union h),
        congr_arg tsum _], {apply mU h},
    funext i, apply measure'_eq _ _ (h i) },
  { cases not_forall.1 h with i hi,
    exact le_trans (le_infi $ Î» h, hi.elim h) (ennreal.le_tsum i) }
end

parameter (mU : âˆ€ {f : â„• â†’ set Î±} (hm : âˆ€i, is_measurable (f i)),
  pairwise (disjoint on f) â†’
  m (â‹ƒi, f i) (is_measurable.Union hm) = (âˆ‘'i, m (f i) (hm i)))
include mU

lemma measure'_Union
  {Î²} [encodable Î²] {f : Î² â†’ set Î±}
  (hd : pairwise (disjoint on f)) (hm : âˆ€i, is_measurable (f i)) :
  measure' (â‹ƒi, f i) = (âˆ‘'i, measure' (f i)) :=
begin
  rw [encodable.Union_decode2, outer_measure.Union_aux],
  { exact measure'_Union_nat _ _
      (Î» n, encodable.Union_decode2_cases is_measurable.empty hm)
      (mU _ (measurable_space.Union_decode2_disjoint_on hd)) },
  { apply measure'_empty },
end

lemma measure'_union {sâ‚ sâ‚‚ : set Î±}
  (hd : disjoint sâ‚ sâ‚‚) (hâ‚ : is_measurable sâ‚) (hâ‚‚ : is_measurable sâ‚‚) :
  measure' (sâ‚ âˆª sâ‚‚) = measure' sâ‚ + measure' sâ‚‚ :=
begin
  rw [union_eq_Union, measure'_Union _ _ @mU
      (pairwise_disjoint_on_bool.2 hd) (bool.forall_bool.2 âŸ¨hâ‚‚, hâ‚âŸ©),
    tsum_fintype],
  change _+_ = _, simp
end

lemma measure'_mono {sâ‚ sâ‚‚ : set Î±} (hâ‚ : is_measurable sâ‚) (hs : sâ‚ âŠ† sâ‚‚) :
  measure' sâ‚ â‰¤ measure' sâ‚‚ :=
le_infi $ Î» hâ‚‚, begin
  have := measure'_union _ _ @mU disjoint_diff hâ‚ (hâ‚‚.diff hâ‚),
  rw union_diff_cancel hs at this,
  rw â† measure'_eq m m0 _,
  exact le_iff_exists_add.2 âŸ¨_, thisâŸ©
end

lemma measure'_Union_le_tsum_nat : âˆ€ (s : â„• â†’ set Î±),
  measure' (â‹ƒi, s i) â‰¤ (âˆ‘'i, measure' (s i)) :=
measure'_Union_le_tsum_nat' $ Î» f h, begin
  simp [Union_disjointed.symm] {single_pass := tt},
  rw [mU (is_measurable.disjointed h) disjoint_disjointed],
  refine ennreal.tsum_le_tsum (Î» i, _),
  rw [â† measure'_eq m m0, â† measure'_eq m m0],
  exact measure'_mono _ _ @mU (is_measurable.disjointed h _) (inter_subset_left _ _)
end

lemma outer_measure'_eq {s : set Î±} (hs : is_measurable s) :
  outer_measure' s = m s hs :=
by rw â† measure'_eq m m0 hs; exact
(le_antisymm (outer_measure.of_function_le _ _ _) $
  le_infi $ Î» f, le_infi $ Î» hf,
  le_trans (measure'_mono _ _ @mU hs hf) $
  measure'_Union_le_tsum_nat _ _ @mU _)

lemma outer_measure'_eq_measure' {s : set Î±} (hs : is_measurable s) :
  outer_measure' s = measure' s :=
by rw [measure'_eq m m0 hs, outer_measure'_eq m m0 @mU hs]

end of_measurable

namespace outer_measure
variables {Î± : Type*} [measurable_space Î±] (m : outer_measure Î±)

/-- Given an outer measure `m` we can forget its value on non-measurable sets, and then consider
  `m.trim`, the unique maximal outer measure less than that function. -/
def trim : outer_measure Î± :=
outer_measure' (Î» s _, m s) m.empty

theorem trim_ge : m â‰¤ m.trim :=
Î» s, le_infi $ Î» f, le_infi $ Î» hs,
le_trans (m.mono hs) $ le_trans (m.Union_nat f) $
ennreal.tsum_le_tsum $ Î» i, le_infi $ Î» hf, le_refl _

theorem trim_eq {s : set Î±} (hs : is_measurable s) : m.trim s = m s :=
le_antisymm (le_trans (of_function_le _ _ _) (infi_le _ hs)) (trim_ge _ _)

theorem trim_congr {mâ‚ mâ‚‚ : outer_measure Î±}
  (H : âˆ€ {s : set Î±}, is_measurable s â†’ mâ‚ s = mâ‚‚ s) :
  mâ‚.trim = mâ‚‚.trim :=
by unfold trim; congr; funext s hs; exact H hs

theorem trim_le_trim {mâ‚ mâ‚‚ : outer_measure Î±} (H : mâ‚ â‰¤ mâ‚‚) : mâ‚.trim â‰¤ mâ‚‚.trim :=
Î» s, infi_le_infi $ Î» f, infi_le_infi $ Î» hs,
ennreal.tsum_le_tsum $ Î» b, infi_le_infi $ Î» hf, H _

theorem le_trim_iff {mâ‚ mâ‚‚ : outer_measure Î±} : mâ‚ â‰¤ mâ‚‚.trim â†”
  âˆ€ s, is_measurable s â†’ mâ‚ s â‰¤ mâ‚‚ s :=
le_of_function.trans $ forall_congr $ Î» s, le_infi_iff

theorem trim_eq_infi (s : set Î±) : m.trim s = â¨… t (st : s âŠ† t) (ht : is_measurable t), m t :=
begin
  refine le_antisymm
    (le_infi $ Î» t, le_infi $ Î» st, le_infi $ Î» ht, _)
    (le_infi $ Î» f, le_infi $ Î» hf, _),
  { rw â† trim_eq m ht, exact (trim m).mono st },
  { by_cases h : âˆ€i, is_measurable (f i),
    { refine infi_le_of_le _ (infi_le_of_le hf $
        infi_le_of_le (is_measurable.Union h) _),
      rw congr_arg tsum _, {exact m.Union_nat _},
      funext i, exact measure'_eq _ _ (h i) },
    { cases not_forall.1 h with i hi,
      exact le_trans (le_infi $ Î» h, hi.elim h) (ennreal.le_tsum i) } }
end

theorem trim_eq_infi' (s : set Î±) : m.trim s = â¨… t : {t // s âŠ† t âˆ§ is_measurable t}, m t.1 :=
by simp [infi_subtype, infi_and, trim_eq_infi]

theorem trim_trim (m : outer_measure Î±) : m.trim.trim = m.trim :=
le_antisymm (le_trim_iff.2 $ Î» s hs, by simp [trim_eq _ hs, le_refl]) (trim_ge _)

theorem trim_zero : (0 : outer_measure Î±).trim = 0 :=
ext $ Î» s, le_antisymm
  (le_trans ((trim 0).mono (subset_univ s)) $
    le_of_eq $ trim_eq _ is_measurable.univ)
  (zero_le _)

theorem trim_add (mâ‚ mâ‚‚ : outer_measure Î±) : (mâ‚ + mâ‚‚).trim = mâ‚.trim + mâ‚‚.trim :=
ext $ Î» s, begin
  simp [trim_eq_infi'],
  rw ennreal.infi_add_infi,
  rintro âŸ¨tâ‚, stâ‚, htâ‚âŸ© âŸ¨tâ‚‚, stâ‚‚, htâ‚‚âŸ©,
  exact âŸ¨âŸ¨_, subset_inter_iff.2 âŸ¨stâ‚, stâ‚‚âŸ©, htâ‚.inter htâ‚‚âŸ©,
    add_le_add
      (mâ‚.mono' (inter_subset_left _ _))
      (mâ‚‚.mono' (inter_subset_right _ _))âŸ©,
end

theorem trim_sum_ge {Î¹} (m : Î¹ â†’ outer_measure Î±) : sum (Î» i, (m i).trim) â‰¤ (sum m).trim :=
Î» s, by simp [trim_eq_infi]; exact
Î» t st ht, ennreal.tsum_le_tsum (Î» i,
  infi_le_of_le t $ infi_le_of_le st $ infi_le _ ht)

end outer_measure

/-- A measure is defined to be an outer measure that is countably additive on
measurable sets, with the additional assumption that the outer measure is the canonical
extension of the restricted measure. -/
structure measure (Î± : Type*) [measurable_space Î±] extends outer_measure Î± :=
(m_Union {f : â„• â†’ set Î±} :
  (âˆ€i, is_measurable (f i)) â†’ pairwise (disjoint on f) â†’
  measure_of (â‹ƒi, f i) = (âˆ‘'i, measure_of (f i)))
(trimmed : to_outer_measure.trim = to_outer_measure)

/-- Measure projections for a measure space.

For measurable sets this returns the measure assigned by the `measure_of` field in `measure`.
But we can extend this to _all_ sets, but using the outer measure. This gives us monotonicity and
subadditivity for all sets.
-/
instance measure.has_coe_to_fun {Î±} [measurable_space Î±] : has_coe_to_fun (measure Î±) :=
âŸ¨Î» _, set Î± â†’ ennreal, Î» m, m.to_outer_measureâŸ©

namespace measure

/-- Obtain a measure by giving a countably additive function that sends `âˆ…` to `0`. -/
def of_measurable {Î±} [measurable_space Î±]
  (m : Î  (s : set Î±), is_measurable s â†’ ennreal)
  (m0 : m âˆ… is_measurable.empty = 0)
  (mU : âˆ€ {f : â„• â†’ set Î±} (h : âˆ€i, is_measurable (f i)),
    pairwise (disjoint on f) â†’
    m (â‹ƒi, f i) (is_measurable.Union h) = (âˆ‘'i, m (f i) (h i))) :
  measure Î± :=
{ m_Union := Î» f hf hd,
  show outer_measure' m m0 (Union f) =
      âˆ‘' i, outer_measure' m m0 (f i), begin
    rw [outer_measure'_eq m m0 @mU, mU hf hd],
    congr, funext n, rw outer_measure'_eq m m0 @mU
  end,
  trimmed :=
  show (outer_measure' m m0).trim = outer_measure' m m0, begin
    unfold outer_measure.trim,
    congr, funext s hs,
    exact outer_measure'_eq m m0 @mU hs
  end,
  ..outer_measure' m m0 }

lemma of_measurable_apply {Î±} [measurable_space Î±]
  {m : Î  (s : set Î±), is_measurable s â†’ ennreal}
  {m0 : m âˆ… is_measurable.empty = 0}
  {mU : âˆ€ {f : â„• â†’ set Î±} (h : âˆ€i, is_measurable (f i)),
    pairwise (disjoint on f) â†’
    m (â‹ƒi, f i) (is_measurable.Union h) = (âˆ‘'i, m (f i) (h i))}
  (s : set Î±) (hs : is_measurable s) :
  of_measurable m m0 @mU s = m s hs :=
outer_measure'_eq m m0 @mU hs

@[ext] lemma ext {Î±} [measurable_space Î±] :
  âˆ€ {Î¼â‚ Î¼â‚‚ : measure Î±}, (âˆ€s, is_measurable s â†’ Î¼â‚ s = Î¼â‚‚ s) â†’ Î¼â‚ = Î¼â‚‚
| âŸ¨mâ‚, uâ‚, hâ‚âŸ© âŸ¨mâ‚‚, uâ‚‚, hâ‚‚âŸ© h := by congr; rw [â† hâ‚, â† hâ‚‚];
  exact outer_measure.trim_congr h

end measure

section
variables {Î± : Type*} {Î² : Type*} {Î¹ : Type*} [measurable_space Î±] {Î¼ Î¼â‚ Î¼â‚‚ : measure Î±}
  {s sâ‚ sâ‚‚ : set Î±}

@[simp] lemma to_outer_measure_apply (s) : Î¼.to_outer_measure s = Î¼ s := rfl

lemma measure_eq_trim (s) : Î¼ s = Î¼.to_outer_measure.trim s :=
by rw Î¼.trimmed; refl

lemma measure_eq_infi (s) : Î¼ s = â¨… t (st : s âŠ† t) (ht : is_measurable t), Î¼ t :=
by rw [measure_eq_trim, outer_measure.trim_eq_infi]; refl

lemma measure_eq_outer_measure' :
  Î¼ s = outer_measure' (Î» s _, Î¼ s) Î¼.empty s :=
measure_eq_trim _

lemma to_outer_measure_eq_outer_measure' :
  Î¼.to_outer_measure = outer_measure' (Î» s _, Î¼ s) Î¼.empty :=
Î¼.trimmed.symm

lemma measure_eq_measure' (hs : is_measurable s) :
  Î¼ s = measure' (Î» s _, Î¼ s) Î¼.empty s :=
by rw [measure_eq_outer_measure',
  outer_measure'_eq_measure' (Î» s _, Î¼ s) _ Î¼.m_Union hs]

@[simp] lemma measure_empty : Î¼ âˆ… = 0 := Î¼.empty

lemma measure_mono (h : sâ‚ âŠ† sâ‚‚) : Î¼ sâ‚ â‰¤ Î¼ sâ‚‚ := Î¼.mono h

lemma measure_mono_null (h : sâ‚ âŠ† sâ‚‚) (hâ‚‚ : Î¼ sâ‚‚ = 0) : Î¼ sâ‚ = 0 :=
by rw [â† le_zero_iff_eq, â† hâ‚‚]; exact measure_mono h

lemma exists_is_measurable_superset_of_measure_eq_zero {s : set Î±} (h : Î¼ s = 0) :
  âˆƒt, s âŠ† t âˆ§ is_measurable t âˆ§ Î¼ t = 0 :=
begin
  rw [measure_eq_infi] at h,
  have h := (infi_eq_bot _).1 h,
  choose t ht using show âˆ€n:â„•, âˆƒt, s âŠ† t âˆ§ is_measurable t âˆ§ Î¼ t < nâ»Â¹,
  { assume n,
    have : (0 : ennreal) < nâ»Â¹ :=
      (zero_lt_iff_ne_zero.2 $ ennreal.inv_ne_zero.2 $ ennreal.nat_ne_top _),
    rcases h _ this with âŸ¨t, htâŸ©,
    use [t],
    simpa [(>), infi_lt_iff, -add_comm] using ht },
  refine âŸ¨â‹‚n, t n, subset_Inter (Î»n, (ht n).1), is_measurable.Inter (Î»n, (ht n).2.1), _âŸ©,
  refine eq_of_le_of_forall_le_of_dense bot_le (assume r hr, _),
  rcases ennreal.exists_inv_nat_lt (ne_of_gt hr) with âŸ¨n, hnâŸ©,
  calc Î¼ (â‹‚n, t n) â‰¤ Î¼ (t n) : measure_mono (Inter_subset _ _)
    ... â‰¤ nâ»Â¹ : le_of_lt (ht n).2.2
    ... â‰¤ r : le_of_lt hn
end

theorem measure_Union_le {Î²} [encodable Î²] (s : Î² â†’ set Î±) : Î¼ (â‹ƒi, s i) â‰¤ (âˆ‘'i, Î¼ (s i)) :=
Î¼.to_outer_measure.Union _

lemma measure_Union_null {Î²} [encodable Î²] {s : Î² â†’ set Î±} :
  (âˆ€ i, Î¼ (s i) = 0) â†’ Î¼ (â‹ƒi, s i) = 0 :=
Î¼.to_outer_measure.Union_null

theorem measure_union_le (sâ‚ sâ‚‚ : set Î±) : Î¼ (sâ‚ âˆª sâ‚‚) â‰¤ Î¼ sâ‚ + Î¼ sâ‚‚ :=
Î¼.to_outer_measure.union _ _

lemma measure_union_null {sâ‚ sâ‚‚ : set Î±} : Î¼ sâ‚ = 0 â†’ Î¼ sâ‚‚ = 0 â†’ Î¼ (sâ‚ âˆª sâ‚‚) = 0 :=
Î¼.to_outer_measure.union_null

lemma measure_Union {Î²} [encodable Î²] {f : Î² â†’ set Î±}
  (hn : pairwise (disjoint on f)) (h : âˆ€i, is_measurable (f i)) :
  Î¼ (â‹ƒi, f i) = (âˆ‘'i, Î¼ (f i)) :=
by rw [measure_eq_measure' (is_measurable.Union h),
     measure'_Union (Î» s _, Î¼ s) _ Î¼.m_Union hn h];
   simp [measure_eq_measure', h]

lemma measure_union (hd : disjoint sâ‚ sâ‚‚) (hâ‚ : is_measurable sâ‚) (hâ‚‚ : is_measurable sâ‚‚) :
  Î¼ (sâ‚ âˆª sâ‚‚) = Î¼ sâ‚ + Î¼ sâ‚‚ :=
by rw [measure_eq_measure' (hâ‚.union hâ‚‚),
     measure'_union (Î» s _, Î¼ s) _ Î¼.m_Union hd hâ‚ hâ‚‚];
   simp [measure_eq_measure', hâ‚, hâ‚‚]

lemma measure_bUnion {s : set Î²} {f : Î² â†’ set Î±} (hs : countable s)
  (hd : pairwise_on s (disjoint on f)) (h : âˆ€bâˆˆs, is_measurable (f b)) :
  Î¼ (â‹ƒbâˆˆs, f b) = âˆ‘'p:s, Î¼ (f p) :=
begin
  haveI := hs.to_encodable,
  rw [â† measure_Union, bUnion_eq_Union],
  { rintro âŸ¨i, hiâŸ© âŸ¨j, hjâŸ© ij x âŸ¨hâ‚, hâ‚‚âŸ©,
    exact hd i hi j hj (mt subtype.ext_val ij:_) âŸ¨hâ‚, hâ‚‚âŸ© },
  { simpa }
end

lemma measure_sUnion {S : set (set Î±)} (hs : countable S)
  (hd : pairwise_on S disjoint) (h : âˆ€sâˆˆS, is_measurable s) :
  Î¼ (â‹ƒâ‚€ S) = âˆ‘' s:S, Î¼ s :=
by rw [sUnion_eq_bUnion, measure_bUnion hs hd h]

lemma measure_bUnion_finset {s : finset Î¹} {f : Î¹ â†’ set Î±} (hd : pairwise_on â†‘s (disjoint on f))
  (hm : âˆ€bâˆˆs, is_measurable (f b)) :
  Î¼ (â‹ƒbâˆˆs, f b) = âˆ‘ p in s, Î¼ (f p) :=
begin
  rw [â† finset.sum_attach, finset.attach_eq_univ, â† tsum_fintype],
  exact measure_bUnion s.countable_to_set hd hm
end

lemma measure_diff {sâ‚ sâ‚‚ : set Î±} (h : sâ‚‚ âŠ† sâ‚)
  (hâ‚ : is_measurable sâ‚) (hâ‚‚ : is_measurable sâ‚‚)
  (h_fin : Î¼ sâ‚‚ < âŠ¤) : Î¼ (sâ‚ \ sâ‚‚) = Î¼ sâ‚ - Î¼ sâ‚‚ :=
begin
  refine (ennreal.add_sub_self' h_fin).symm.trans _,
  rw [â† measure_union disjoint_diff hâ‚‚ (hâ‚.diff hâ‚‚), union_diff_cancel h]
end

lemma sum_measure_le_measure_univ {s : finset Î¹} {t : Î¹ â†’ set Î±} (h : âˆ€ i âˆˆ s, is_measurable (t i))
  (H : pairwise_on â†‘s (disjoint on t)) :
  âˆ‘ i in s, Î¼ (t i) â‰¤ Î¼ (univ : set Î±) :=
by { rw â† measure_bUnion_finset H h, exact measure_mono (subset_univ _) }

lemma tsum_measure_le_measure_univ {s : Î¹ â†’ set Î±} (hs : âˆ€ i, is_measurable (s i))
  (H : pairwise (disjoint on s)) :
  (âˆ‘' i, Î¼ (s i)) â‰¤ Î¼ (univ : set Î±) :=
begin
  rw [ennreal.tsum_eq_supr_sum],
  exact supr_le (Î» s, sum_measure_le_measure_univ (Î» i hi, hs i) (Î» i hi j hj hij, H i j hij))
end

/-- Pigeonhole principle for measure spaces: if `âˆ‘' i, Î¼ (s i) > Î¼ univ`, then
one of the intersections `s i âˆ© s j` is not empty. -/
lemma exists_nonempty_inter_of_measure_univ_lt_tsum_measure (Î¼ : measure Î±) {s : Î¹ â†’ set Î±}
  (hs : âˆ€ i, is_measurable (s i)) (H : Î¼ (univ : set Î±) < âˆ‘' i, Î¼ (s i)) :
  âˆƒ i j (h : i â‰  j), (s i âˆ© s j).nonempty :=
begin
  contrapose! H,
  apply tsum_measure_le_measure_univ hs,
  exact Î» i j hij x hx, H i j hij âŸ¨x, hxâŸ©
end

/-- Pigeonhole principle for measure spaces: if `s` is a `finset` and
`âˆ‘ i in s, Î¼ (t i) > Î¼ univ`, then one of the intersections `t i âˆ© t j` is not empty. -/
lemma exists_nonempty_inter_of_measure_univ_lt_sum_measure (Î¼ : measure Î±) {s : finset Î¹}
  {t : Î¹ â†’ set Î±} (h : âˆ€ i âˆˆ s, is_measurable (t i)) (H : Î¼ (univ : set Î±) < âˆ‘ i in s, Î¼ (t i)) :
  âˆƒ (i âˆˆ s) (j âˆˆ s) (h : i â‰  j), (t i âˆ© t j).nonempty :=
begin
  contrapose! H,
  apply sum_measure_le_measure_univ h,
  exact Î» i hi j hj hij x hx, H i hi j hj hij âŸ¨x, hxâŸ©
end

lemma measure_Union_eq_supr_nat {s : â„• â†’ set Î±} (h : âˆ€i, is_measurable (s i)) (hs : monotone s) :
  Î¼ (â‹ƒi, s i) = (â¨†i, Î¼ (s i)) :=
begin
  have : âˆ€ t : finset â„•, âˆƒ n, t âŠ† finset.range (n + 1),
    from Î» t, (finset.exists_nat_subset_range t).imp (Î» n hn, finset.subset.trans hn $
      finset.range_mono $ (le_add_iff_nonneg_right _).2 (zero_le 1)),
  rw [â† Union_disjointed, measure_Union disjoint_disjointed (is_measurable.disjointed h),
    ennreal.tsum_eq_supr_sum' _ this],
  congr' 1, ext1 n,
  rw [â† measure_bUnion_finset (disjoint_disjointed.pairwise_on _)
    (Î» n _, is_measurable.disjointed h n)],
  convert congr_arg Î¼ (Union_disjointed_of_mono hs n),
  ext, simp
end

lemma measure_Inter_eq_infi_nat {s : â„• â†’ set Î±}
  (h : âˆ€i, is_measurable (s i)) (hs : âˆ€i j, i â‰¤ j â†’ s j âŠ† s i)
  (hfin : âˆƒi, Î¼ (s i) < âŠ¤) :
  Î¼ (â‹‚i, s i) = (â¨…i, Î¼ (s i)) :=
begin
  rcases hfin with âŸ¨k, hkâŸ©,
  rw [â† ennreal.sub_sub_cancel (by exact hk) (infi_le _ k),
    ennreal.sub_infi,
    â† ennreal.sub_sub_cancel (by exact hk) (measure_mono (Inter_subset _ k)),
    â† measure_diff (Inter_subset _ k) (h k) (is_measurable.Inter h)
      (lt_of_le_of_lt (measure_mono (Inter_subset _ k)) hk),
    diff_Inter, measure_Union_eq_supr_nat],
  { congr, funext i,
    cases le_total k i with ik ik,
    { exact measure_diff (hs _ _ ik) (h k) (h i)
        (lt_of_le_of_lt (measure_mono (hs _ _ ik)) hk) },
    { rw [diff_eq_empty.2 (hs _ _ ik), measure_empty,
      ennreal.sub_eq_zero_of_le (measure_mono (hs _ _ ik))] } },
  { exact Î» i, (h k).diff (h i) },
  { exact Î» i j ij, diff_subset_diff_right (hs _ _ ij) }
end

lemma measure_eq_inter_diff {Î¼ : measure Î±} {s t : set Î±}
  (hs : is_measurable s) (ht : is_measurable t) :
  Î¼ s = Î¼ (s âˆ© t) + Î¼ (s \ t) :=
have hd : disjoint (s âˆ© t) (s \ t) := assume a âŸ¨âŸ¨_, hsâŸ©, _, hnsâŸ©, hns hs ,
by rw [â† measure_union hd (hs.inter ht) (hs.diff ht), inter_union_diff s t]

lemma tendsto_measure_Union {Î¼ : measure Î±} {s : â„• â†’ set Î±}
  (hs : âˆ€n, is_measurable (s n)) (hm : monotone s) :
  tendsto (Î¼ âˆ˜ s) at_top (ğ“ (Î¼ (â‹ƒn, s n))) :=
begin
  rw measure_Union_eq_supr_nat hs hm,
  exact tendsto_at_top_supr_nat (Î¼ âˆ˜ s) (assume n m hnm, measure_mono $ hm $ hnm)
end

lemma tendsto_measure_Inter {Î¼ : measure Î±} {s : â„• â†’ set Î±}
  (hs : âˆ€n, is_measurable (s n)) (hm : âˆ€n m, n â‰¤ m â†’ s m âŠ† s n) (hf : âˆƒi, Î¼ (s i) < âŠ¤) :
  tendsto (Î¼ âˆ˜ s) at_top (ğ“ (Î¼ (â‹‚n, s n))) :=
begin
  rw measure_Inter_eq_infi_nat hs hm hf,
  exact tendsto_at_top_infi_nat (Î¼ âˆ˜ s) (assume n m hnm, measure_mono $ hm _ _ $ hnm),
end

end
/-- Obtain a measure by giving an outer measure where all sets in the Ïƒ-algebra are
  CarathÃ©odory measurable. -/
def outer_measure.to_measure {Î±} (m : outer_measure Î±)
  [ms : measurable_space Î±] (h : ms â‰¤ m.caratheodory) :
  measure Î± :=
measure.of_measurable (Î» s _, m s) m.empty
  (Î» f hf hd, m.Union_eq_of_caratheodory (Î» i, h _ (hf i)) hd)

lemma le_to_outer_measure_caratheodory {Î±} [ms : measurable_space Î±]
  (Î¼ : measure Î±) : ms â‰¤ Î¼.to_outer_measure.caratheodory :=
begin
  assume s hs,
  rw to_outer_measure_eq_outer_measure',
  refine outer_measure.caratheodory_is_measurable (Î» t, le_infi $ Î» ht, _),
  rw [â† measure_eq_measure' (ht.inter hs),
    â† measure_eq_measure' (ht.diff hs),
    â† measure_union _ (ht.inter hs) (ht.diff hs),
    inter_union_diff],
  exact le_refl _,
  exact Î» x âŸ¨âŸ¨_, hâ‚âŸ©, _, hâ‚‚âŸ©, hâ‚‚ hâ‚
end

lemma to_measure_to_outer_measure {Î±} (m : outer_measure Î±)
  [ms : measurable_space Î±] (h : ms â‰¤ m.caratheodory) :
  (m.to_measure h).to_outer_measure = m.trim := rfl

@[simp] lemma to_measure_apply {Î±} (m : outer_measure Î±)
  [ms : measurable_space Î±] (h : ms â‰¤ m.caratheodory)
  {s : set Î±} (hs : is_measurable s) :
  m.to_measure h s = m s := m.trim_eq hs

lemma to_outer_measure_to_measure {Î± : Type*} [ms : measurable_space Î±] {Î¼ : measure Î±} :
  Î¼.to_outer_measure.to_measure (le_to_outer_measure_caratheodory _) = Î¼ :=
measure.ext $ Î» s, Î¼.to_outer_measure.trim_eq

namespace measure
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*}
  [measurable_space Î±] [measurable_space Î²] [measurable_space Î³]

instance : has_zero (measure Î±) :=
âŸ¨{ to_outer_measure := 0,
   m_Union := Î» f hf hd, tsum_zero.symm,
   trimmed := outer_measure.trim_zero }âŸ©

@[simp] theorem zero_to_outer_measure :
  (0 : measure Î±).to_outer_measure = 0 := rfl

@[simp] theorem zero_apply (s : set Î±) : (0 : measure Î±) s = 0 := rfl

instance : inhabited (measure Î±) := âŸ¨0âŸ©

instance : has_add (measure Î±) :=
âŸ¨Î»Î¼â‚ Î¼â‚‚, {
  to_outer_measure := Î¼â‚.to_outer_measure + Î¼â‚‚.to_outer_measure,
  m_Union := Î»s hs hd,
    show Î¼â‚ (â‹ƒ i, s i) + Î¼â‚‚ (â‹ƒ i, s i) = âˆ‘' i, Î¼â‚ (s i) + Î¼â‚‚ (s i),
    by rw [ennreal.tsum_add, measure_Union hd hs, measure_Union hd hs],
  trimmed := by rw [outer_measure.trim_add, Î¼â‚.trimmed, Î¼â‚‚.trimmed] }âŸ©

@[simp] theorem add_to_outer_measure (Î¼â‚ Î¼â‚‚ : measure Î±) :
  (Î¼â‚ + Î¼â‚‚).to_outer_measure = Î¼â‚.to_outer_measure + Î¼â‚‚.to_outer_measure := rfl

@[simp] theorem add_apply (Î¼â‚ Î¼â‚‚ : measure Î±) (s : set Î±) :
  (Î¼â‚ + Î¼â‚‚) s = Î¼â‚ s + Î¼â‚‚ s := rfl

instance add_comm_monoid : add_comm_monoid (measure Î±) :=
{ zero      := 0,
  add       := (+),
  add_assoc := assume a b c, ext $ assume s hs, add_assoc _ _ _,
  add_comm  := assume a b, ext $ assume s hs, add_comm _ _,
  zero_add  := assume a, ext $ by simp,
  add_zero  := assume a, ext $ assume s hs, add_zero _ }

instance : partial_order (measure Î±) :=
{ le          := Î»mâ‚ mâ‚‚, âˆ€ s, is_measurable s â†’ mâ‚ s â‰¤ mâ‚‚ s,
  le_refl     := assume m s hs, le_refl _,
  le_trans    := assume mâ‚ mâ‚‚ mâ‚ƒ hâ‚ hâ‚‚ s hs, le_trans (hâ‚ s hs) (hâ‚‚ s hs),
  le_antisymm := assume mâ‚ mâ‚‚ hâ‚ hâ‚‚, ext $
    assume s hs, le_antisymm (hâ‚ s hs) (hâ‚‚ s hs) }

theorem le_iff {Î¼â‚ Î¼â‚‚ : measure Î±} :
  Î¼â‚ â‰¤ Î¼â‚‚ â†” âˆ€ s, is_measurable s â†’ Î¼â‚ s â‰¤ Î¼â‚‚ s := iff.rfl

theorem to_outer_measure_le {Î¼â‚ Î¼â‚‚ : measure Î±} :
  Î¼â‚.to_outer_measure â‰¤ Î¼â‚‚.to_outer_measure â†” Î¼â‚ â‰¤ Î¼â‚‚ :=
by rw [â† Î¼â‚‚.trimmed, outer_measure.le_trim_iff]; refl

theorem le_iff' {Î¼â‚ Î¼â‚‚ : measure Î±} :
  Î¼â‚ â‰¤ Î¼â‚‚ â†” âˆ€ s, Î¼â‚ s â‰¤ Î¼â‚‚ s :=
to_outer_measure_le.symm

section
variables {m : set (measure Î±)} {Î¼ : measure Î±}

lemma Inf_caratheodory (s : set Î±) (hs : is_measurable s) :
  (Inf (measure.to_outer_measure '' m)).caratheodory.is_measurable s :=
begin
  rw [outer_measure.Inf_eq_of_function_Inf_gen],
  refine outer_measure.caratheodory_is_measurable (assume t, _),
  cases t.eq_empty_or_nonempty with ht ht, by simp [ht],
  simp only [outer_measure.Inf_gen_nonempty1 _ _ ht, le_infi_iff, ball_image_iff,
    to_outer_measure_apply, measure_eq_infi t],
  assume Î¼ hÎ¼ u htu hu,
  have hm : âˆ€{s t}, s âŠ† t â†’ outer_measure.Inf_gen (to_outer_measure '' m) s â‰¤ Î¼ t,
  { assume s t hst,
    rw [outer_measure.Inf_gen_nonempty2 _ _ (mem_image_of_mem _ hÎ¼)],
    refine infi_le_of_le (Î¼.to_outer_measure) (infi_le_of_le (mem_image_of_mem _ hÎ¼) _),
    rw [to_outer_measure_apply],
    refine measure_mono hst },
  rw [measure_eq_inter_diff hu hs],
  refine add_le_add (hm $ inter_subset_inter_left _ htu) (hm $ diff_subset_diff_left htu)
end

instance : has_Inf (measure Î±) :=
âŸ¨Î»m, (Inf (to_outer_measure '' m)).to_measure $ Inf_caratheodoryâŸ©

lemma Inf_apply {m : set (measure Î±)} {s : set Î±} (hs : is_measurable s) :
  Inf m s = Inf (to_outer_measure '' m) s :=
to_measure_apply _ _ hs

private lemma Inf_le (h : Î¼ âˆˆ m) : Inf m â‰¤ Î¼ :=
have Inf (to_outer_measure '' m) â‰¤ Î¼.to_outer_measure := Inf_le (mem_image_of_mem _ h),
assume s hs, by rw [Inf_apply hs, â† to_outer_measure_apply]; exact this s

private lemma le_Inf (h : âˆ€Î¼' âˆˆ m, Î¼ â‰¤ Î¼') : Î¼ â‰¤ Inf m :=
have Î¼.to_outer_measure â‰¤ Inf (to_outer_measure '' m) :=
  le_Inf $ ball_image_of_ball $ assume Î¼ hÎ¼, to_outer_measure_le.2 $ h _ hÎ¼,
assume s hs, by rw [Inf_apply hs, â† to_outer_measure_apply]; exact this s

instance : complete_lattice (measure Î±) :=
{ bot := 0,
  bot_le := assume a s hs, by exact bot_le,
/- Adding an explicit `top` makes `leanchecker` fail, see lean#364, disable for now

  top := (âŠ¤ : outer_measure Î±).to_measure (by rw [outer_measure.top_caratheodory]; exact le_top),
  le_top := assume a s hs,
    by cases s.eq_empty_or_nonempty with h  h;
      simp [h, to_measure_apply âŠ¤ _ hs, outer_measure.top_apply],
-/
  .. complete_lattice_of_Inf (measure Î±) (Î» ms, âŸ¨Î» _, Inf_le, Î» _, le_InfâŸ©) }

end

/-- The pushforward of a measure. It is defined to be `0` if `f` is not a measurable function. -/
def map (f : Î± â†’ Î²) (Î¼ : measure Î±) : measure Î² :=
if hf : measurable f then
  (Î¼.to_outer_measure.map f).to_measure $ Î» s hs t,
  le_to_outer_measure_caratheodory Î¼ _ (hf _ hs) (f â»Â¹' t)
else 0

variables {Î¼ Î½ : measure Î±}

@[simp] theorem map_apply {f : Î± â†’ Î²} (hf : measurable f)
  {s : set Î²} (hs : is_measurable s) :
  (map f Î¼ : measure Î²) s = Î¼ (f â»Â¹' s) :=
by rw [map, dif_pos hf, to_measure_apply _ _ hs]; refl

@[simp] lemma map_id : map id Î¼ = Î¼ :=
ext $ Î» s, map_apply measurable_id

lemma map_map {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : measurable g) (hf : measurable f) :
  map g (map f Î¼) = map (g âˆ˜ f) Î¼ :=
ext $ Î» s hs,
by simp [hf, hg, hs, hg.preimage hs, hg.comp hf];
   rw â† preimage_comp

/-- The dirac measure. -/
def dirac (a : Î±) : measure Î± :=
(outer_measure.dirac a).to_measure (by simp)

@[simp] lemma dirac_apply (a : Î±) {s : set Î±} (hs : is_measurable s) :
  (dirac a : measure Î±) s = â¨† h : a âˆˆ s, 1 :=
to_measure_apply _ _ hs

/-- Sum of an indexed family of measures. -/
def sum {Î¹ : Type*} (f : Î¹ â†’ measure Î±) : measure Î± :=
(outer_measure.sum (Î» i, (f i).to_outer_measure)).to_measure $
le_trans
  (by exact le_infi (Î» i, le_to_outer_measure_caratheodory _))
  (outer_measure.le_sum_caratheodory _)

/-- Counting measure on any measurable space. -/
def count : measure Î± := sum dirac

/-- A measure is complete if every null set is also measurable.
  A null set is a subset of a measurable set with measure `0`.
  Since every measure is defined as a special case of an outer measure, we can more simply state
  that a set `s` is null if `Î¼ s = 0`. -/
@[class] def is_complete {Î±} {_:measurable_space Î±} (Î¼ : measure Î±) : Prop :=
âˆ€ s, Î¼ s = 0 â†’ is_measurable s

/-- The "almost everywhere" filter of co-null sets. -/
def ae (Î¼ : measure Î±) : filter Î± :=
{ sets := {s | Î¼ sá¶œ = 0},
  univ_sets := by simp [measure_empty],
  inter_sets := Î» s t hs ht, by simp [compl_inter]; exact measure_union_null hs ht,
  sets_of_superset := Î» s t hs hst, measure_mono_null (set.compl_subset_compl.2 hst) hs }

end measure

variables {Î± : Type*} {Î² : Type*} [measurable_space Î±] {Î¼ : measure Î±}

notation `âˆ€â‚˜` binders `âˆ‚` Î¼ `, ` r:(scoped P, Î¼.ae.eventually P) := r

lemma mem_ae_iff (s : set Î±) : s âˆˆ Î¼.ae.sets â†” Î¼ sá¶œ = 0 := iff.rfl

lemma ae_iff {p : Î± â†’ Prop} : (âˆ€â‚˜ a âˆ‚ Î¼, p a) â†” Î¼ { a | Â¬ p a } = 0 := iff.rfl

lemma measure_zero_iff_ae_nmem {s : set Î±} : Î¼ s = 0 â†” âˆ€â‚˜ a âˆ‚ Î¼, a âˆ‰ s :=
by simp only [ae_iff, not_not, set_of_mem_eq]

lemma ae_of_all {p : Î± â†’ Prop} (Î¼ : measure Î±) : (âˆ€a, p a) â†’ âˆ€â‚˜ a âˆ‚ Î¼, p a :=
eventually_of_forall _

instance : countable_Inter_filter Î¼.ae :=
âŸ¨begin
  intros S hSc hS,
  simp only [mem_ae_iff, compl_sInter, sUnion_image, bUnion_eq_Union] at hS âŠ¢,
  haveI := hSc.to_encodable,
  exact measure_Union_null (subtype.forall.2 hS)
endâŸ©

lemma ae_all_iff {Î¹ : Type*} [encodable Î¹] {p : Î± â†’ Î¹ â†’ Prop} :
  (âˆ€â‚˜ a âˆ‚ Î¼, âˆ€i, p a i) â†” (âˆ€i, âˆ€â‚˜ a âˆ‚ Î¼, p a i) :=
eventually_countable_forall

lemma ae_ball_iff {Î¹} {S : set Î¹} (hS : countable S) {p : Î  (x : Î±) (i âˆˆ S), Prop} :
  (âˆ€â‚˜ x âˆ‚ Î¼, âˆ€ i âˆˆ S, p x i â€¹_â€º) â†” âˆ€ i âˆˆ S, âˆ€â‚˜ x âˆ‚ Î¼, p x i â€¹_â€º :=
eventually_countable_ball hS

lemma ae_eq_refl (f : Î± â†’ Î²) : âˆ€â‚˜ a âˆ‚ Î¼, f a = f a :=
ae_of_all Î¼ $ Î» a, rfl

lemma ae_eq_symm {f g : Î± â†’ Î²} (h : âˆ€â‚˜ a âˆ‚ Î¼, f a = g a) : (âˆ€â‚˜ a âˆ‚ Î¼, g a = f a) :=
h.mono $ Î» a, eq.symm

lemma ae_eq_trans {f g h: Î± â†’ Î²} (hâ‚ : âˆ€â‚˜ a âˆ‚ Î¼, f a = g a) (hâ‚‚ : âˆ€â‚˜ a âˆ‚ Î¼, g a = h a) :
  âˆ€â‚˜ a âˆ‚ Î¼, f a = h a :=
by { filter_upwards [hâ‚, hâ‚‚], intro a, exact eq.trans }

end measure_theory

section is_complete
open measure_theory

variables {Î± : Type*} [measurable_space Î±] (Î¼ : measure Î±)

/-- A set is null measurable if it is the union of a null set and a measurable set. -/
def is_null_measurable (s : set Î±) : Prop :=
âˆƒ t z, s = t âˆª z âˆ§ is_measurable t âˆ§ Î¼ z = 0

theorem is_null_measurable_iff {Î¼ : measure Î±} {s : set Î±} :
  is_null_measurable Î¼ s â†”
  âˆƒ t, t âŠ† s âˆ§ is_measurable t âˆ§ Î¼ (s \ t) = 0 :=
begin
  split,
  { rintro âŸ¨t, z, rfl, ht, hzâŸ©,
    refine âŸ¨t, set.subset_union_left _ _, ht, measure_mono_null _ hzâŸ©,
    simp [union_diff_left, diff_subset] },
  { rintro âŸ¨t, st, ht, hzâŸ©,
    exact âŸ¨t, _, (union_diff_cancel st).symm, ht, hzâŸ© }
end

theorem is_null_measurable_measure_eq {Î¼ : measure Î±} {s t : set Î±}
  (st : t âŠ† s) (hz : Î¼ (s \ t) = 0) : Î¼ s = Î¼ t :=
begin
  refine le_antisymm _ (measure_mono st),
  have := measure_union_le t (s \ t),
  rw [union_diff_cancel st, hz] at this, simpa
end

theorem is_measurable.is_null_measurable
  {s : set Î±} (hs : is_measurable s) : is_null_measurable Î¼ s :=
âŸ¨s, âˆ…, by simp, hs, Î¼.emptyâŸ©

theorem is_null_measurable_of_complete [c : Î¼.is_complete]
  {s : set Î±} : is_null_measurable Î¼ s â†” is_measurable s :=
âŸ¨by rintro âŸ¨t, z, rfl, ht, hzâŸ©; exact
  is_measurable.union ht (c _ hz),
 Î» h, h.is_null_measurable _âŸ©

variables {Î¼}
theorem is_null_measurable.union_null {s z : set Î±}
  (hs : is_null_measurable Î¼ s) (hz : Î¼ z = 0) :
  is_null_measurable Î¼ (s âˆª z) :=
begin
  rcases hs with âŸ¨t, z', rfl, ht, hz'âŸ©,
  exact âŸ¨t, z' âˆª z, set.union_assoc _ _ _, ht, le_zero_iff_eq.1
    (le_trans (measure_union_le _ _) $ by simp [hz, hz'])âŸ©
end

theorem null_is_null_measurable {z : set Î±}
  (hz : Î¼ z = 0) : is_null_measurable Î¼ z :=
by simpa using (is_measurable.empty.is_null_measurable _).union_null hz

theorem is_null_measurable.Union_nat {s : â„• â†’ set Î±}
  (hs : âˆ€ i, is_null_measurable Î¼ (s i)) :
  is_null_measurable Î¼ (Union s) :=
begin
  choose t ht using assume i, is_null_measurable_iff.1 (hs i),
  simp [forall_and_distrib] at ht,
  rcases ht with âŸ¨st, ht, hzâŸ©,
  refine is_null_measurable_iff.2
    âŸ¨Union t, Union_subset_Union st, is_measurable.Union ht,
      measure_mono_null _ (measure_Union_null hz)âŸ©,
  rw [diff_subset_iff, â† Union_union_distrib],
  exact Union_subset_Union (Î» i, by rw â† diff_subset_iff)
end

theorem is_measurable.diff_null {s z : set Î±}
  (hs : is_measurable s) (hz : Î¼ z = 0) :
  is_null_measurable Î¼ (s \ z) :=
begin
  rw measure_eq_infi at hz,
  choose f hf using show âˆ€ q : {q:â„š//q>0}, âˆƒ t:set Î±,
    z âŠ† t âˆ§ is_measurable t âˆ§ Î¼ t < (nnreal.of_real q.1 : ennreal),
  { rintro âŸ¨Îµ, Îµ0âŸ©,
    have : 0 < (nnreal.of_real Îµ : ennreal), { simpa using Îµ0 },
    rw â† hz at this, simpa [infi_lt_iff] },
  refine is_null_measurable_iff.2 âŸ¨s \ Inter f,
    diff_subset_diff_right (subset_Inter (Î» i, (hf i).1)),
    hs.diff (is_measurable.Inter (Î» i, (hf i).2.1)),
    measure_mono_null _ (le_zero_iff_eq.1 $ le_of_not_lt $ Î» h, _)âŸ©,
  { exact Inter f },
  { rw [diff_subset_iff, diff_union_self],
    exact subset.trans (diff_subset _ _) (subset_union_left _ _) },
  rcases ennreal.lt_iff_exists_rat_btwn.1 h with âŸ¨Îµ, Îµ0', Îµ0, hâŸ©,
  simp at Îµ0,
  apply not_le_of_lt (lt_trans (hf âŸ¨Îµ, Îµ0âŸ©).2.2 h),
  exact measure_mono (Inter_subset _ _)
end

theorem is_null_measurable.diff_null {s z : set Î±}
  (hs : is_null_measurable Î¼ s) (hz : Î¼ z = 0) :
  is_null_measurable Î¼ (s \ z) :=
begin
  rcases hs with âŸ¨t, z', rfl, ht, hz'âŸ©,
  rw [set.union_diff_distrib],
  exact (ht.diff_null hz).union_null (measure_mono_null (diff_subset _ _) hz')
end

theorem is_null_measurable.compl {s : set Î±}
  (hs : is_null_measurable Î¼ s) :
  is_null_measurable Î¼ sá¶œ :=
begin
  rcases hs with âŸ¨t, z, rfl, ht, hzâŸ©,
  rw compl_union,
  exact ht.compl.diff_null hz
end

/-- The measurable space of all null measurable sets. -/
def null_measurable {Î± : Type u} [measurable_space Î±]
  (Î¼ : measure Î±) : measurable_space Î± :=
{ is_measurable := is_null_measurable Î¼,
  is_measurable_empty := is_measurable.empty.is_null_measurable _,
  is_measurable_compl := Î» s hs, hs.compl,
  is_measurable_Union := Î» f, is_null_measurable.Union_nat }

/-- Given a measure we can complete it to a (complete) measure on all null measurable sets. -/
def completion {Î± : Type u} [measurable_space Î±] (Î¼ : measure Î±) :
  @measure_theory.measure Î± (null_measurable Î¼) :=
{ to_outer_measure := Î¼.to_outer_measure,
  m_Union := Î» s hs hd, show Î¼ (Union s) = âˆ‘' i, Î¼ (s i), begin
    choose t ht using assume i, is_null_measurable_iff.1 (hs i),
    simp [forall_and_distrib] at ht, rcases ht with âŸ¨st, ht, hzâŸ©,
    rw is_null_measurable_measure_eq (Union_subset_Union st),
    { rw measure_Union _ ht,
      { congr, funext i,
        exact (is_null_measurable_measure_eq (st i) (hz i)).symm },
      { rintro i j ij x âŸ¨hâ‚, hâ‚‚âŸ©,
        exact hd i j ij âŸ¨st i hâ‚, st j hâ‚‚âŸ© } },
    { refine measure_mono_null _ (measure_Union_null hz),
      rw [diff_subset_iff, â† Union_union_distrib],
      exact Union_subset_Union (Î» i, by rw â† diff_subset_iff) }
  end,
  trimmed := begin
    letI := null_measurable Î¼,
    refine le_antisymm (Î» s, _) (outer_measure.trim_ge _),
    rw outer_measure.trim_eq_infi,
    dsimp,
    clear _inst,
    resetI,
    rw measure_eq_infi s,
    exact infi_le_infi (Î» t, infi_le_infi $ Î» st,
      infi_le_infi2 $ Î» ht, âŸ¨ht.is_null_measurable _, le_refl _âŸ©)
  end }

instance completion.is_complete {Î± : Type u} [measurable_space Î±] (Î¼ : measure Î±) :
  (completion Î¼).is_complete :=
Î» z hz, null_is_null_measurable hz

end is_complete

namespace measure_theory

section prio
set_option default_priority 100 -- see Note [default priority]
/-- A measure space is a measurable space equipped with a
  measure, referred to as `volume`. -/
class measure_space (Î± : Type*) extends measurable_space Î± :=
(volume : measure Î±)
end prio

export measure_space (volume)

/-- `volume` is the canonical  measure on `Î±`. -/
add_decl_doc volume

section measure_space
variables {Î± : Type*} {Î¹ : Type*} [measure_space Î±] {sâ‚ sâ‚‚ : set Î±}

notation `âˆ€â‚˜` binders `, ` r:(scoped P, volume.ae.eventually P) := r

end measure_space

end measure_theory
