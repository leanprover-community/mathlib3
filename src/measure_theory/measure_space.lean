/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import measure_theory.outer_measure
import order.filter.countable_Inter

/-!
# Measure spaces

Given a measurable space `Œ±`, a measure on `Œ±` is a function that sends measurable sets to the
extended nonnegative reals that satisfies the following conditions:
1. `Œº ‚àÖ = 0`;
2. `Œº` is countably additive. This means that the measure of a countable union of pairwise disjoint
   sets is equal to the measure of the individual sets.

Every measure can be canonically extended to an outer measure, so that it assigns values to
all subsets, not just the measurable subsets. On the other hand, a measure that is countably
additive on measurable sets can be restricted to measurable sets to obtain a measure.
In this file a measure is defined to be an outer measure that is countably additive on
measurable sets, with the additional assumption that the outer measure is the canonical
extension of the restricted measure.

In the first part of the file we define operations on arbitrary functions defined on measurable
sets.

Measures on `Œ±` form a complete lattice, and are closed under scalar multiplication with `ennreal`.

Given a measure, the null sets are the sets where `Œº s = 0`, where `Œº` denotes the corresponding
outer measure (so `s` might not be measurable). We can then define the completion of `Œº` as the
measure on the least `œÉ`-algebra that also contains all null sets, by defining the measure to be `0`
on the null sets.

## Main statements

* `completion` is the completion of a measure to all null measurable sets.
* `measure.of_measurable` and `outer_measure.to_measure` are two important ways to define a measure.

## Implementation notes

Given `Œº : measure Œ±`, `Œº s` is the value of the *outer measure* applied to `s`.
This conveniently allows us to apply the measure to sets without proving that they are measurable.
We get countable subadditivity for all sets, but only countable additivity for measurable sets.

You often don't want to define a measure via its constructor.
Two ways that are sometimes more convenient:
* `measure.of_measurable` is a way to define a measure by only giving its value on measurable sets
  and proving the properties (1) and (2) mentioned above.
* `outer_measure.to_measure` is a way of obtaining a measure from an outer measure by showing that
  all measurable sets in the measurable space are Carath√©odory measurable.

A `measure_space` is a class that is a measurable space with a canonical measure.
The measure is denoted `volume`.

## References

* <https://en.wikipedia.org/wiki/Measure_(mathematics)>
* <https://en.wikipedia.org/wiki/Complete_measure>
* <https://en.wikipedia.org/wiki/Almost_everywhere>

## Tags

measure, almost everywhere, measure space, completion, null set, null measurable set
-/

noncomputable theory

open classical set filter finset function
open_locale classical topological_space big_operators

universes u v w x

namespace measure_theory

/- We first consider operations on arbitrary functions defined on measurable sets. -/
section of_measurable
parameters {Œ± : Type*} [measurable_space Œ±]
parameters (m : Œ† (s : set Œ±), is_measurable s ‚Üí ennreal)
parameters (m0 : m ‚àÖ is_measurable.empty = 0)
include m0

/-- We can trivially extend a function defined on measurable sets to all sets by defining it to be
 `‚àû` on the non-measurable sets.

`measure'` is mainly used to derive the outer measure, for the main `measure` projection. -/
def measure' (s : set Œ±) : ennreal := ‚®Ö h : is_measurable s, m s h

lemma measure'_eq {s} (h : is_measurable s) : measure' s = m s h :=
by simp [measure', h]

lemma measure'_empty : measure' ‚àÖ = 0 :=
(measure'_eq is_measurable.empty).trans m0

lemma measure'_Union_nat
  {f : ‚Ñï ‚Üí set Œ±}
  (hm : ‚àÄi, is_measurable (f i))
  (mU : m (‚ãÉi, f i) (is_measurable.Union hm) = (‚àë'i, m (f i) (hm i))) :
  measure' (‚ãÉi, f i) = (‚àë'i, measure' (f i)) :=
(measure'_eq _).trans $ mU.trans $
by congr; funext i; rw measure'_eq

/-- Given an arbitrary function on measurable sets, we can define the outer measure corresponding to
  it (this is the unique maximal outer measure that is at most `m` on measurable sets). -/
def outer_measure' : outer_measure Œ± :=
outer_measure.of_function measure' measure'_empty

lemma measure'_Union_le_tsum_nat'
  (mU : ‚àÄ {f : ‚Ñï ‚Üí set Œ±} (hm : ‚àÄi, is_measurable (f i)),
    m (‚ãÉi, f i) (is_measurable.Union hm) ‚â§ (‚àë'i, m (f i) (hm i)))
  (s : ‚Ñï ‚Üí set Œ±) :
  measure' (‚ãÉi, s i) ‚â§ (‚àë'i, measure' (s i)) :=
begin
  by_cases h : ‚àÄi, is_measurable (s i),
  { rw [measure'_eq _ _ (is_measurable.Union h),
        congr_arg tsum _], {apply mU h},
    funext i, apply measure'_eq _ _ (h i) },
  { cases not_forall.1 h with i hi,
    exact le_trans (le_infi $ Œª h, hi.elim h) (ennreal.le_tsum i) }
end

parameter (mU : ‚àÄ {f : ‚Ñï ‚Üí set Œ±} (hm : ‚àÄi, is_measurable (f i)),
  pairwise (disjoint on f) ‚Üí
  m (‚ãÉi, f i) (is_measurable.Union hm) = (‚àë'i, m (f i) (hm i)))
include mU

lemma measure'_Union
  {Œ≤} [encodable Œ≤] {f : Œ≤ ‚Üí set Œ±}
  (hd : pairwise (disjoint on f)) (hm : ‚àÄi, is_measurable (f i)) :
  measure' (‚ãÉi, f i) = (‚àë'i, measure' (f i)) :=
begin
  rw [encodable.Union_decode2, ‚Üê tsum_Union_decode2],
  { exact measure'_Union_nat _ _
      (Œª n, encodable.Union_decode2_cases is_measurable.empty hm)
      (mU _ (encodable.Union_decode2_disjoint_on hd)) },
  { apply measure'_empty },
end

lemma measure'_union {s‚ÇÅ s‚ÇÇ : set Œ±}
  (hd : disjoint s‚ÇÅ s‚ÇÇ) (h‚ÇÅ : is_measurable s‚ÇÅ) (h‚ÇÇ : is_measurable s‚ÇÇ) :
  measure' (s‚ÇÅ ‚à™ s‚ÇÇ) = measure' s‚ÇÅ + measure' s‚ÇÇ :=
begin
  rw [union_eq_Union, measure'_Union _ _ @mU
      (pairwise_disjoint_on_bool.2 hd) (bool.forall_bool.2 ‚ü®h‚ÇÇ, h‚ÇÅ‚ü©),
    tsum_fintype],
  change _+_ = _, simp
end

lemma measure'_mono {s‚ÇÅ s‚ÇÇ : set Œ±} (h‚ÇÅ : is_measurable s‚ÇÅ) (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) :
  measure' s‚ÇÅ ‚â§ measure' s‚ÇÇ :=
le_infi $ Œª h‚ÇÇ, begin
  have := measure'_union _ _ @mU disjoint_diff h‚ÇÅ (h‚ÇÇ.diff h‚ÇÅ),
  rw union_diff_cancel hs at this,
  rw ‚Üê measure'_eq m m0 _,
  exact le_iff_exists_add.2 ‚ü®_, this‚ü©
end

lemma measure'_Union_le_tsum_nat : ‚àÄ (s : ‚Ñï ‚Üí set Œ±),
  measure' (‚ãÉi, s i) ‚â§ (‚àë'i, measure' (s i)) :=
measure'_Union_le_tsum_nat' $ Œª f h, begin
  simp [Union_disjointed.symm] {single_pass := tt},
  rw [mU (is_measurable.disjointed h) disjoint_disjointed],
  refine ennreal.tsum_le_tsum (Œª i, _),
  rw [‚Üê measure'_eq m m0, ‚Üê measure'_eq m m0],
  exact measure'_mono _ _ @mU (is_measurable.disjointed h _) (inter_subset_left _ _)
end

lemma outer_measure'_eq {s : set Œ±} (hs : is_measurable s) :
  outer_measure' s = m s hs :=
by rw ‚Üê measure'_eq m m0 hs; exact
(le_antisymm (outer_measure.of_function_le _ _ _) $
  le_infi $ Œª f, le_infi $ Œª hf,
  le_trans (measure'_mono _ _ @mU hs hf) $
  measure'_Union_le_tsum_nat _ _ @mU _)

lemma outer_measure'_eq_measure' {s : set Œ±} (hs : is_measurable s) :
  outer_measure' s = measure' s :=
by rw [measure'_eq m m0 hs, outer_measure'_eq m m0 @mU hs]

end of_measurable

namespace outer_measure
variables {Œ± : Type*} [measurable_space Œ±] (m : outer_measure Œ±)

/-- Given an outer measure `m` we can forget its value on non-measurable sets, and then consider
  `m.trim`, the unique maximal outer measure less than that function. -/
def trim : outer_measure Œ± :=
outer_measure' (Œª s _, m s) m.empty

theorem trim_ge : m ‚â§ m.trim :=
Œª s, le_infi $ Œª f, le_infi $ Œª hs,
le_trans (m.mono hs) $ le_trans (m.Union_nat f) $
ennreal.tsum_le_tsum $ Œª i, le_infi $ Œª hf, le_refl _

theorem trim_eq {s : set Œ±} (hs : is_measurable s) : m.trim s = m s :=
le_antisymm (le_trans (of_function_le _ _ _) (infi_le _ hs)) (trim_ge _ _)

theorem trim_congr {m‚ÇÅ m‚ÇÇ : outer_measure Œ±}
  (H : ‚àÄ {s : set Œ±}, is_measurable s ‚Üí m‚ÇÅ s = m‚ÇÇ s) :
  m‚ÇÅ.trim = m‚ÇÇ.trim :=
by unfold trim; congr; funext s hs; exact H hs

theorem trim_le_trim {m‚ÇÅ m‚ÇÇ : outer_measure Œ±} (H : m‚ÇÅ ‚â§ m‚ÇÇ) : m‚ÇÅ.trim ‚â§ m‚ÇÇ.trim :=
Œª s, infi_le_infi $ Œª f, infi_le_infi $ Œª hs,
ennreal.tsum_le_tsum $ Œª b, infi_le_infi $ Œª hf, H _

theorem le_trim_iff {m‚ÇÅ m‚ÇÇ : outer_measure Œ±} : m‚ÇÅ ‚â§ m‚ÇÇ.trim ‚Üî
  ‚àÄ s, is_measurable s ‚Üí m‚ÇÅ s ‚â§ m‚ÇÇ s :=
le_of_function.trans $ forall_congr $ Œª s, le_infi_iff

theorem trim_eq_infi (s : set Œ±) : m.trim s = ‚®Ö t (st : s ‚äÜ t) (ht : is_measurable t), m t :=
begin
  refine le_antisymm
    (le_infi $ Œª t, le_infi $ Œª st, le_infi $ Œª ht, _)
    (le_infi $ Œª f, le_infi $ Œª hf, _),
  { rw ‚Üê trim_eq m ht, exact (trim m).mono st },
  { by_cases h : ‚àÄi, is_measurable (f i),
    { refine infi_le_of_le _ (infi_le_of_le hf $
        infi_le_of_le (is_measurable.Union h) _),
      rw congr_arg tsum _, {exact m.Union_nat _},
      funext i, exact measure'_eq _ _ (h i) },
    { cases not_forall.1 h with i hi,
      exact le_trans (le_infi $ Œª h, hi.elim h) (ennreal.le_tsum i) } }
end

theorem trim_eq_infi' (s : set Œ±) : m.trim s = ‚®Ö t : {t // s ‚äÜ t ‚àß is_measurable t}, m t.1 :=
by simp [infi_subtype, infi_and, trim_eq_infi]

theorem trim_trim (m : outer_measure Œ±) : m.trim.trim = m.trim :=
le_antisymm (le_trim_iff.2 $ Œª s hs, by simp [trim_eq _ hs, le_refl]) (trim_ge _)

@[simp] theorem trim_zero : (0 : outer_measure Œ±).trim = 0 :=
ext $ Œª s, le_antisymm
  (le_trans ((trim 0).mono (subset_univ s)) $
    le_of_eq $ trim_eq _ is_measurable.univ)
  (zero_le _)

theorem trim_add (m‚ÇÅ m‚ÇÇ : outer_measure Œ±) : (m‚ÇÅ + m‚ÇÇ).trim = m‚ÇÅ.trim + m‚ÇÇ.trim :=
ext $ Œª s, begin
  simp [trim_eq_infi'],
  rw ennreal.infi_add_infi,
  rintro ‚ü®t‚ÇÅ, st‚ÇÅ, ht‚ÇÅ‚ü© ‚ü®t‚ÇÇ, st‚ÇÇ, ht‚ÇÇ‚ü©,
  exact ‚ü®‚ü®_, subset_inter_iff.2 ‚ü®st‚ÇÅ, st‚ÇÇ‚ü©, ht‚ÇÅ.inter ht‚ÇÇ‚ü©,
    add_le_add
      (m‚ÇÅ.mono' (inter_subset_left _ _))
      (m‚ÇÇ.mono' (inter_subset_right _ _))‚ü©,
end

theorem trim_sum_ge {Œπ} (m : Œπ ‚Üí outer_measure Œ±) : sum (Œª i, (m i).trim) ‚â§ (sum m).trim :=
Œª s, by simp [trim_eq_infi]; exact
Œª t st ht, ennreal.tsum_le_tsum (Œª i,
  infi_le_of_le t $ infi_le_of_le st $ infi_le _ ht)

lemma exists_is_measurable_superset_of_trim_eq_zero
  {m : outer_measure Œ±} {s : set Œ±} (h : m.trim s = 0) :
  ‚àÉt, s ‚äÜ t ‚àß is_measurable t ‚àß m t = 0 :=
begin
  rw [trim_eq_infi] at h,
  have h := (infi_eq_bot _).1 h,
  choose t ht using show ‚àÄn:‚Ñï, ‚àÉt, s ‚äÜ t ‚àß is_measurable t ‚àß m t < n‚Åª¬π,
  { assume n,
    have : (0 : ennreal) < n‚Åª¬π :=
      (zero_lt_iff_ne_zero.2 $ ennreal.inv_ne_zero.2 $ ennreal.nat_ne_top _),
    rcases h _ this with ‚ü®t, ht‚ü©,
    use [t],
    simpa [(>), infi_lt_iff, -add_comm] using ht },
  refine ‚ü®‚ãÇn, t n, subset_Inter (Œªn, (ht n).1), is_measurable.Inter (Œªn, (ht n).2.1), _‚ü©,
  refine eq_of_le_of_forall_le_of_dense bot_le (assume r hr, _),
  rcases ennreal.exists_inv_nat_lt (ne_of_gt hr) with ‚ü®n, hn‚ü©,
  calc m (‚ãÇn, t n) ‚â§ m (t n) : m.mono' (Inter_subset _ _)
    ... ‚â§ n‚Åª¬π : le_of_lt (ht n).2.2
    ... ‚â§ r : le_of_lt hn
end

/- Can this proof be simplified? Currently it's pretty ugly. -/
lemma trim_smul (m : outer_measure Œ±) (x : ennreal) : (x ‚Ä¢ m).trim = x ‚Ä¢ m.trim :=
begin
  ext1 s,
  haveI : nonempty {t : set Œ± // s ‚äÜ t ‚àß is_measurable t} :=
  ‚ü®‚ü®set.univ, subset_univ s, is_measurable.univ‚ü©‚ü©,
  by_cases h : x = 0,
  { simp only [h, zero_smul, zero_apply, trim_zero] },
  simp only [smul_apply],
  by_cases h2 : m.trim s = 0,
  { rcases exists_is_measurable_superset_of_trim_eq_zero h2 with ‚ü®t, h1t, h2t, h3t‚ü©,
    simp only [h2, mul_zero, ‚Üêle_zero_iff_eq],
    refine le_trans (outer_measure.mono _ h1t) _,
    simp only [trim_eq _ h2t, smul_apply, le_zero_iff_eq, measure_of_eq_coe, h3t, mul_zero] },
  by_cases h3 : x = ‚ä§,
  { simp only [h3, h2, with_top.top_mul, ne.def, not_false_iff],
    simp only [trim_eq_infi, true_and, infi_eq_top, smul_apply, with_top.mul_eq_top_iff,
      eq_self_iff_true, not_false_iff, ennreal.top_ne_zero, ‚Üê zero_lt_iff_ne_zero, true_and,
      with_top.zero_lt_top],
    intros t ht h2t, right, refine lt_of_lt_of_le (zero_lt_iff_ne_zero.mpr h2) _,
    rw [‚Üê trim_eq m h2t], exact m.trim.mono ht },
  simp only [trim_eq_infi, infi_and'],
  simp only [infi_subtype'],
  rw [ennreal.mul_infi], refl, exact h3
end

end outer_measure

/-- A measure is defined to be an outer measure that is countably additive on
measurable sets, with the additional assumption that the outer measure is the canonical
extension of the restricted measure. -/
structure measure (Œ± : Type*) [measurable_space Œ±] extends outer_measure Œ± :=
(m_Union {f : ‚Ñï ‚Üí set Œ±} :
  (‚àÄi, is_measurable (f i)) ‚Üí pairwise (disjoint on f) ‚Üí
  measure_of (‚ãÉi, f i) = (‚àë'i, measure_of (f i)))
(trimmed : to_outer_measure.trim = to_outer_measure)

/-- Measure projections for a measure space.

For measurable sets this returns the measure assigned by the `measure_of` field in `measure`.
But we can extend this to _all_ sets, but using the outer measure. This gives us monotonicity and
subadditivity for all sets.
-/
instance measure.has_coe_to_fun {Œ±} [measurable_space Œ±] : has_coe_to_fun (measure Œ±) :=
‚ü®Œª _, set Œ± ‚Üí ennreal, Œª m, m.to_outer_measure‚ü©

namespace measure

/-- Obtain a measure by giving a countably additive function that sends `‚àÖ` to `0`. -/
def of_measurable {Œ±} [measurable_space Œ±]
  (m : Œ† (s : set Œ±), is_measurable s ‚Üí ennreal)
  (m0 : m ‚àÖ is_measurable.empty = 0)
  (mU : ‚àÄ {f : ‚Ñï ‚Üí set Œ±} (h : ‚àÄi, is_measurable (f i)),
    pairwise (disjoint on f) ‚Üí
    m (‚ãÉi, f i) (is_measurable.Union h) = (‚àë'i, m (f i) (h i))) :
  measure Œ± :=
{ m_Union := Œª f hf hd,
  show outer_measure' m m0 (Union f) =
      ‚àë' i, outer_measure' m m0 (f i), begin
    rw [outer_measure'_eq m m0 @mU, mU hf hd],
    congr, funext n, rw outer_measure'_eq m m0 @mU
  end,
  trimmed :=
  show (outer_measure' m m0).trim = outer_measure' m m0, begin
    unfold outer_measure.trim,
    congr, funext s hs,
    exact outer_measure'_eq m m0 @mU hs
  end,
  ..outer_measure' m m0 }

lemma of_measurable_apply {Œ±} [measurable_space Œ±]
  {m : Œ† (s : set Œ±), is_measurable s ‚Üí ennreal}
  {m0 : m ‚àÖ is_measurable.empty = 0}
  {mU : ‚àÄ {f : ‚Ñï ‚Üí set Œ±} (h : ‚àÄi, is_measurable (f i)),
    pairwise (disjoint on f) ‚Üí
    m (‚ãÉi, f i) (is_measurable.Union h) = (‚àë'i, m (f i) (h i))}
  (s : set Œ±) (hs : is_measurable s) :
  of_measurable m m0 @mU s = m s hs :=
outer_measure'_eq m m0 @mU hs

@[ext] lemma ext {Œ±} [measurable_space Œ±] :
  ‚àÄ {Œº‚ÇÅ Œº‚ÇÇ : measure Œ±}, (‚àÄs, is_measurable s ‚Üí Œº‚ÇÅ s = Œº‚ÇÇ s) ‚Üí Œº‚ÇÅ = Œº‚ÇÇ
| ‚ü®m‚ÇÅ, u‚ÇÅ, h‚ÇÅ‚ü© ‚ü®m‚ÇÇ, u‚ÇÇ, h‚ÇÇ‚ü© h := by congr; rw [‚Üê h‚ÇÅ, ‚Üê h‚ÇÇ];
  exact outer_measure.trim_congr h

end measure

section
variables {Œ± : Type*} {Œ≤ : Type*} {Œπ : Type*} [measurable_space Œ±] {Œº Œº‚ÇÅ Œº‚ÇÇ : measure Œ±}
  {s s‚ÇÅ s‚ÇÇ : set Œ±}

@[simp] lemma to_outer_measure_apply (s) : Œº.to_outer_measure s = Œº s := rfl

lemma measure_eq_trim (s) : Œº s = Œº.to_outer_measure.trim s :=
by rw Œº.trimmed; refl

lemma measure_eq_infi (s) : Œº s = ‚®Ö t (st : s ‚äÜ t) (ht : is_measurable t), Œº t :=
by rw [measure_eq_trim, outer_measure.trim_eq_infi]; refl

lemma measure_eq_outer_measure' :
  Œº s = outer_measure' (Œª s _, Œº s) Œº.empty s :=
measure_eq_trim _

lemma to_outer_measure_eq_outer_measure' :
  Œº.to_outer_measure = outer_measure' (Œª s _, Œº s) Œº.empty :=
Œº.trimmed.symm

lemma measure_eq_measure' (hs : is_measurable s) :
  Œº s = measure' (Œª s _, Œº s) Œº.empty s :=
by rw [measure_eq_outer_measure',
  outer_measure'_eq_measure' (Œª s _, Œº s) _ Œº.m_Union hs]

@[simp] lemma measure_empty : Œº ‚àÖ = 0 := Œº.empty

lemma measure_mono (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : Œº s‚ÇÅ ‚â§ Œº s‚ÇÇ := Œº.mono h

lemma measure_mono_null (h : s‚ÇÅ ‚äÜ s‚ÇÇ) (h‚ÇÇ : Œº s‚ÇÇ = 0) : Œº s‚ÇÅ = 0 :=
by rw [‚Üê le_zero_iff_eq, ‚Üê h‚ÇÇ]; exact measure_mono h

lemma exists_is_measurable_superset_of_measure_eq_zero {s : set Œ±} (h : Œº s = 0) :
  ‚àÉt, s ‚äÜ t ‚àß is_measurable t ‚àß Œº t = 0 :=
outer_measure.exists_is_measurable_superset_of_trim_eq_zero (by rw [‚Üê measure_eq_trim, h])

theorem measure_Union_le {Œ≤} [encodable Œ≤] (s : Œ≤ ‚Üí set Œ±) : Œº (‚ãÉi, s i) ‚â§ (‚àë'i, Œº (s i)) :=
Œº.to_outer_measure.Union _

lemma measure_Union_null {Œ≤} [encodable Œ≤] {s : Œ≤ ‚Üí set Œ±} :
  (‚àÄ i, Œº (s i) = 0) ‚Üí Œº (‚ãÉi, s i) = 0 :=
Œº.to_outer_measure.Union_null

theorem measure_union_le (s‚ÇÅ s‚ÇÇ : set Œ±) : Œº (s‚ÇÅ ‚à™ s‚ÇÇ) ‚â§ Œº s‚ÇÅ + Œº s‚ÇÇ :=
Œº.to_outer_measure.union _ _

lemma measure_union_null {s‚ÇÅ s‚ÇÇ : set Œ±} : Œº s‚ÇÅ = 0 ‚Üí Œº s‚ÇÇ = 0 ‚Üí Œº (s‚ÇÅ ‚à™ s‚ÇÇ) = 0 :=
Œº.to_outer_measure.union_null

lemma measure_Union {Œ≤} [encodable Œ≤] {f : Œ≤ ‚Üí set Œ±}
  (hn : pairwise (disjoint on f)) (h : ‚àÄi, is_measurable (f i)) :
  Œº (‚ãÉi, f i) = (‚àë'i, Œº (f i)) :=
by rw [measure_eq_measure' (is_measurable.Union h),
     measure'_Union (Œª s _, Œº s) _ Œº.m_Union hn h];
   simp [measure_eq_measure', h]

lemma measure_union (hd : disjoint s‚ÇÅ s‚ÇÇ) (h‚ÇÅ : is_measurable s‚ÇÅ) (h‚ÇÇ : is_measurable s‚ÇÇ) :
  Œº (s‚ÇÅ ‚à™ s‚ÇÇ) = Œº s‚ÇÅ + Œº s‚ÇÇ :=
by rw [measure_eq_measure' (h‚ÇÅ.union h‚ÇÇ),
     measure'_union (Œª s _, Œº s) _ Œº.m_Union hd h‚ÇÅ h‚ÇÇ];
   simp [measure_eq_measure', h‚ÇÅ, h‚ÇÇ]

lemma measure_bUnion {s : set Œ≤} {f : Œ≤ ‚Üí set Œ±} (hs : countable s)
  (hd : pairwise_on s (disjoint on f)) (h : ‚àÄb‚ààs, is_measurable (f b)) :
  Œº (‚ãÉb‚ààs, f b) = ‚àë'p:s, Œº (f p) :=
begin
  haveI := hs.to_encodable,
  rw [‚Üê measure_Union, bUnion_eq_Union],
  { rintro ‚ü®i, hi‚ü© ‚ü®j, hj‚ü© ij x ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
    exact hd i hi j hj (mt subtype.ext_val ij:_) ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© },
  { simpa }
end

lemma measure_sUnion {S : set (set Œ±)} (hs : countable S)
  (hd : pairwise_on S disjoint) (h : ‚àÄs‚ààS, is_measurable s) :
  Œº (‚ãÉ‚ÇÄ S) = ‚àë' s:S, Œº s :=
by rw [sUnion_eq_bUnion, measure_bUnion hs hd h]

lemma measure_bUnion_finset {s : finset Œπ} {f : Œπ ‚Üí set Œ±} (hd : pairwise_on ‚Üës (disjoint on f))
  (hm : ‚àÄb‚ààs, is_measurable (f b)) :
  Œº (‚ãÉb‚ààs, f b) = ‚àë p in s, Œº (f p) :=
begin
  rw [‚Üê finset.sum_attach, finset.attach_eq_univ, ‚Üê tsum_fintype],
  exact measure_bUnion s.countable_to_set hd hm
end

lemma measure_diff {s‚ÇÅ s‚ÇÇ : set Œ±} (h : s‚ÇÇ ‚äÜ s‚ÇÅ)
  (h‚ÇÅ : is_measurable s‚ÇÅ) (h‚ÇÇ : is_measurable s‚ÇÇ)
  (h_fin : Œº s‚ÇÇ < ‚ä§) : Œº (s‚ÇÅ \ s‚ÇÇ) = Œº s‚ÇÅ - Œº s‚ÇÇ :=
begin
  refine (ennreal.add_sub_self' h_fin).symm.trans _,
  rw [‚Üê measure_union disjoint_diff h‚ÇÇ (h‚ÇÅ.diff h‚ÇÇ), union_diff_cancel h]
end

lemma sum_measure_le_measure_univ {s : finset Œπ} {t : Œπ ‚Üí set Œ±} (h : ‚àÄ i ‚àà s, is_measurable (t i))
  (H : pairwise_on ‚Üës (disjoint on t)) :
  ‚àë i in s, Œº (t i) ‚â§ Œº (univ : set Œ±) :=
by { rw ‚Üê measure_bUnion_finset H h, exact measure_mono (subset_univ _) }

lemma tsum_measure_le_measure_univ {s : Œπ ‚Üí set Œ±} (hs : ‚àÄ i, is_measurable (s i))
  (H : pairwise (disjoint on s)) :
  (‚àë' i, Œº (s i)) ‚â§ Œº (univ : set Œ±) :=
begin
  rw [ennreal.tsum_eq_supr_sum],
  exact supr_le (Œª s, sum_measure_le_measure_univ (Œª i hi, hs i) (Œª i hi j hj hij, H i j hij))
end

/-- Pigeonhole principle for measure spaces: if `‚àë' i, Œº (s i) > Œº univ`, then
one of the intersections `s i ‚à© s j` is not empty. -/
lemma exists_nonempty_inter_of_measure_univ_lt_tsum_measure (Œº : measure Œ±) {s : Œπ ‚Üí set Œ±}
  (hs : ‚àÄ i, is_measurable (s i)) (H : Œº (univ : set Œ±) < ‚àë' i, Œº (s i)) :
  ‚àÉ i j (h : i ‚â† j), (s i ‚à© s j).nonempty :=
begin
  contrapose! H,
  apply tsum_measure_le_measure_univ hs,
  exact Œª i j hij x hx, H i j hij ‚ü®x, hx‚ü©
end

/-- Pigeonhole principle for measure spaces: if `s` is a `finset` and
`‚àë i in s, Œº (t i) > Œº univ`, then one of the intersections `t i ‚à© t j` is not empty. -/
lemma exists_nonempty_inter_of_measure_univ_lt_sum_measure (Œº : measure Œ±) {s : finset Œπ}
  {t : Œπ ‚Üí set Œ±} (h : ‚àÄ i ‚àà s, is_measurable (t i)) (H : Œº (univ : set Œ±) < ‚àë i in s, Œº (t i)) :
  ‚àÉ (i ‚àà s) (j ‚àà s) (h : i ‚â† j), (t i ‚à© t j).nonempty :=
begin
  contrapose! H,
  apply sum_measure_le_measure_univ h,
  exact Œª i hi j hj hij x hx, H i hi j hj hij ‚ü®x, hx‚ü©
end

lemma measure_Union_eq_supr_nat {s : ‚Ñï ‚Üí set Œ±} (h : ‚àÄi, is_measurable (s i)) (hs : monotone s) :
  Œº (‚ãÉi, s i) = (‚®Üi, Œº (s i)) :=
begin
  have : ‚àÄ t : finset ‚Ñï, ‚àÉ n, t ‚äÜ finset.range (n + 1),
    from Œª t, (finset.exists_nat_subset_range t).imp (Œª n hn, finset.subset.trans hn $
      finset.range_mono $ (le_add_iff_nonneg_right _).2 (zero_le 1)),
  rw [‚Üê Union_disjointed, measure_Union disjoint_disjointed (is_measurable.disjointed h),
    ennreal.tsum_eq_supr_sum' _ this],
  congr' 1, ext1 n,
  rw [‚Üê measure_bUnion_finset (disjoint_disjointed.pairwise_on _)
    (Œª n _, is_measurable.disjointed h n)],
  convert congr_arg Œº (Union_disjointed_of_mono hs n),
  ext, simp
end

lemma measure_Inter_eq_infi_nat {s : ‚Ñï ‚Üí set Œ±}
  (h : ‚àÄi, is_measurable (s i)) (hs : ‚àÄi j, i ‚â§ j ‚Üí s j ‚äÜ s i)
  (hfin : ‚àÉi, Œº (s i) < ‚ä§) :
  Œº (‚ãÇi, s i) = (‚®Öi, Œº (s i)) :=
begin
  rcases hfin with ‚ü®k, hk‚ü©,
  rw [‚Üê ennreal.sub_sub_cancel (by exact hk) (infi_le _ k),
    ennreal.sub_infi,
    ‚Üê ennreal.sub_sub_cancel (by exact hk) (measure_mono (Inter_subset _ k)),
    ‚Üê measure_diff (Inter_subset _ k) (h k) (is_measurable.Inter h)
      (lt_of_le_of_lt (measure_mono (Inter_subset _ k)) hk),
    diff_Inter, measure_Union_eq_supr_nat],
  { congr, funext i,
    cases le_total k i with ik ik,
    { exact measure_diff (hs _ _ ik) (h k) (h i)
        (lt_of_le_of_lt (measure_mono (hs _ _ ik)) hk) },
    { rw [diff_eq_empty.2 (hs _ _ ik), measure_empty,
      ennreal.sub_eq_zero_of_le (measure_mono (hs _ _ ik))] } },
  { exact Œª i, (h k).diff (h i) },
  { exact Œª i j ij, diff_subset_diff_right (hs _ _ ij) }
end

lemma measure_eq_inter_diff {Œº : measure Œ±} {s t : set Œ±}
  (hs : is_measurable s) (ht : is_measurable t) :
  Œº s = Œº (s ‚à© t) + Œº (s \ t) :=
have hd : disjoint (s ‚à© t) (s \ t) := assume a ‚ü®‚ü®_, hs‚ü©, _, hns‚ü©, hns hs ,
by rw [‚Üê measure_union hd (hs.inter ht) (hs.diff ht), inter_union_diff s t]

lemma tendsto_measure_Union {Œº : measure Œ±} {s : ‚Ñï ‚Üí set Œ±}
  (hs : ‚àÄn, is_measurable (s n)) (hm : monotone s) :
  tendsto (Œº ‚àò s) at_top (ùìù (Œº (‚ãÉn, s n))) :=
begin
  rw measure_Union_eq_supr_nat hs hm,
  exact tendsto_at_top_supr_nat (Œº ‚àò s) (assume n m hnm, measure_mono $ hm $ hnm)
end

lemma tendsto_measure_Inter {Œº : measure Œ±} {s : ‚Ñï ‚Üí set Œ±}
  (hs : ‚àÄn, is_measurable (s n)) (hm : ‚àÄn m, n ‚â§ m ‚Üí s m ‚äÜ s n) (hf : ‚àÉi, Œº (s i) < ‚ä§) :
  tendsto (Œº ‚àò s) at_top (ùìù (Œº (‚ãÇn, s n))) :=
begin
  rw measure_Inter_eq_infi_nat hs hm hf,
  exact tendsto_at_top_infi_nat (Œº ‚àò s) (assume n m hnm, measure_mono $ hm _ _ $ hnm),
end

end
/-- Obtain a measure by giving an outer measure where all sets in the œÉ-algebra are
  Carath√©odory measurable. -/
def outer_measure.to_measure {Œ±} (m : outer_measure Œ±)
  [ms : measurable_space Œ±] (h : ms ‚â§ m.caratheodory) :
  measure Œ± :=
measure.of_measurable (Œª s _, m s) m.empty
  (Œª f hf hd, m.Union_eq_of_caratheodory (Œª i, h _ (hf i)) hd)

lemma le_to_outer_measure_caratheodory {Œ±} [ms : measurable_space Œ±]
  (Œº : measure Œ±) : ms ‚â§ Œº.to_outer_measure.caratheodory :=
begin
  assume s hs,
  rw to_outer_measure_eq_outer_measure',
  refine outer_measure.caratheodory_is_measurable (Œª t, le_infi $ Œª ht, _),
  rw [‚Üê measure_eq_measure' (ht.inter hs),
    ‚Üê measure_eq_measure' (ht.diff hs),
    ‚Üê measure_union _ (ht.inter hs) (ht.diff hs),
    inter_union_diff],
  exact le_refl _,
  exact Œª x ‚ü®‚ü®_, h‚ÇÅ‚ü©, _, h‚ÇÇ‚ü©, h‚ÇÇ h‚ÇÅ
end

lemma to_measure_to_outer_measure {Œ±} (m : outer_measure Œ±)
  [ms : measurable_space Œ±] (h : ms ‚â§ m.caratheodory) :
  (m.to_measure h).to_outer_measure = m.trim := rfl

@[simp] lemma to_measure_apply {Œ±} (m : outer_measure Œ±)
  [ms : measurable_space Œ±] (h : ms ‚â§ m.caratheodory)
  {s : set Œ±} (hs : is_measurable s) :
  m.to_measure h s = m s := m.trim_eq hs

lemma to_outer_measure_to_measure {Œ± : Type*} [ms : measurable_space Œ±] {Œº : measure Œ±} :
  Œº.to_outer_measure.to_measure (le_to_outer_measure_caratheodory _) = Œº :=
measure.ext $ Œª s, Œº.to_outer_measure.trim_eq

namespace measure
variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [measurable_space Œ±] [measurable_space Œ≤] [measurable_space Œ≥]

instance : has_zero (measure Œ±) :=
‚ü®{ to_outer_measure := 0,
   m_Union := Œª f hf hd, tsum_zero.symm,
   trimmed := outer_measure.trim_zero }‚ü©

@[simp] theorem zero_to_outer_measure :
  (0 : measure Œ±).to_outer_measure = 0 := rfl

@[simp] theorem zero_apply (s : set Œ±) : (0 : measure Œ±) s = 0 := rfl

instance : inhabited (measure Œ±) := ‚ü®0‚ü©

instance : has_add (measure Œ±) :=
‚ü®ŒªŒº‚ÇÅ Œº‚ÇÇ, {
  to_outer_measure := Œº‚ÇÅ.to_outer_measure + Œº‚ÇÇ.to_outer_measure,
  m_Union := Œªs hs hd,
    show Œº‚ÇÅ (‚ãÉ i, s i) + Œº‚ÇÇ (‚ãÉ i, s i) = ‚àë' i, Œº‚ÇÅ (s i) + Œº‚ÇÇ (s i),
    by rw [ennreal.tsum_add, measure_Union hd hs, measure_Union hd hs],
  trimmed := by rw [outer_measure.trim_add, Œº‚ÇÅ.trimmed, Œº‚ÇÇ.trimmed] }‚ü©

@[simp] theorem add_to_outer_measure (Œº‚ÇÅ Œº‚ÇÇ : measure Œ±) :
  (Œº‚ÇÅ + Œº‚ÇÇ).to_outer_measure = Œº‚ÇÅ.to_outer_measure + Œº‚ÇÇ.to_outer_measure := rfl

@[simp] theorem add_apply (Œº‚ÇÅ Œº‚ÇÇ : measure Œ±) (s : set Œ±) :
  (Œº‚ÇÅ + Œº‚ÇÇ) s = Œº‚ÇÅ s + Œº‚ÇÇ s := rfl

instance add_comm_monoid : add_comm_monoid (measure Œ±) :=
{ zero      := 0,
  add       := (+),
  add_assoc := assume a b c, ext $ assume s hs, add_assoc _ _ _,
  add_comm  := assume a b, ext $ assume s hs, add_comm _ _,
  zero_add  := assume a, ext $ by simp,
  add_zero  := assume a, ext $ assume s hs, add_zero _ }

instance : partial_order (measure Œ±) :=
{ le          := Œªm‚ÇÅ m‚ÇÇ, ‚àÄ s, is_measurable s ‚Üí m‚ÇÅ s ‚â§ m‚ÇÇ s,
  le_refl     := assume m s hs, le_refl _,
  le_trans    := assume m‚ÇÅ m‚ÇÇ m‚ÇÉ h‚ÇÅ h‚ÇÇ s hs, le_trans (h‚ÇÅ s hs) (h‚ÇÇ s hs),
  le_antisymm := assume m‚ÇÅ m‚ÇÇ h‚ÇÅ h‚ÇÇ, ext $
    assume s hs, le_antisymm (h‚ÇÅ s hs) (h‚ÇÇ s hs) }

theorem le_iff {Œº‚ÇÅ Œº‚ÇÇ : measure Œ±} :
  Œº‚ÇÅ ‚â§ Œº‚ÇÇ ‚Üî ‚àÄ s, is_measurable s ‚Üí Œº‚ÇÅ s ‚â§ Œº‚ÇÇ s := iff.rfl

theorem to_outer_measure_le {Œº‚ÇÅ Œº‚ÇÇ : measure Œ±} :
  Œº‚ÇÅ.to_outer_measure ‚â§ Œº‚ÇÇ.to_outer_measure ‚Üî Œº‚ÇÅ ‚â§ Œº‚ÇÇ :=
by rw [‚Üê Œº‚ÇÇ.trimmed, outer_measure.le_trim_iff]; refl

theorem le_iff' {Œº‚ÇÅ Œº‚ÇÇ : measure Œ±} :
  Œº‚ÇÅ ‚â§ Œº‚ÇÇ ‚Üî ‚àÄ s, Œº‚ÇÅ s ‚â§ Œº‚ÇÇ s :=
to_outer_measure_le.symm

section
variables {m : set (measure Œ±)} {Œº : measure Œ±}

lemma Inf_caratheodory (s : set Œ±) (hs : is_measurable s) :
  (Inf (measure.to_outer_measure '' m)).caratheodory.is_measurable s :=
begin
  rw [outer_measure.Inf_eq_of_function_Inf_gen],
  refine outer_measure.caratheodory_is_measurable (assume t, _),
  cases t.eq_empty_or_nonempty with ht ht, by simp [ht],
  simp only [outer_measure.Inf_gen_nonempty1 _ _ ht, le_infi_iff, ball_image_iff,
    to_outer_measure_apply, measure_eq_infi t],
  assume Œº hŒº u htu hu,
  have hm : ‚àÄ{s t}, s ‚äÜ t ‚Üí outer_measure.Inf_gen (to_outer_measure '' m) s ‚â§ Œº t,
  { assume s t hst,
    rw [outer_measure.Inf_gen_nonempty2 _ _ (mem_image_of_mem _ hŒº)],
    refine infi_le_of_le (Œº.to_outer_measure) (infi_le_of_le (mem_image_of_mem _ hŒº) _),
    rw [to_outer_measure_apply],
    refine measure_mono hst },
  rw [measure_eq_inter_diff hu hs],
  refine add_le_add (hm $ inter_subset_inter_left _ htu) (hm $ diff_subset_diff_left htu)
end

instance : has_Inf (measure Œ±) :=
‚ü®Œªm, (Inf (to_outer_measure '' m)).to_measure $ Inf_caratheodory‚ü©

lemma Inf_apply {m : set (measure Œ±)} {s : set Œ±} (hs : is_measurable s) :
  Inf m s = Inf (to_outer_measure '' m) s :=
to_measure_apply _ _ hs

private lemma Inf_le (h : Œº ‚àà m) : Inf m ‚â§ Œº :=
have Inf (to_outer_measure '' m) ‚â§ Œº.to_outer_measure := Inf_le (mem_image_of_mem _ h),
assume s hs, by rw [Inf_apply hs, ‚Üê to_outer_measure_apply]; exact this s

private lemma le_Inf (h : ‚àÄŒº' ‚àà m, Œº ‚â§ Œº') : Œº ‚â§ Inf m :=
have Œº.to_outer_measure ‚â§ Inf (to_outer_measure '' m) :=
  le_Inf $ ball_image_of_ball $ assume Œº hŒº, to_outer_measure_le.2 $ h _ hŒº,
assume s hs, by rw [Inf_apply hs, ‚Üê to_outer_measure_apply]; exact this s

instance : complete_lattice (measure Œ±) :=
{ bot := 0,
  bot_le := assume a s hs, by exact bot_le,
/- Adding an explicit `top` makes `leanchecker` fail, see lean#364, disable for now

  top := (‚ä§ : outer_measure Œ±).to_measure (by rw [outer_measure.top_caratheodory]; exact le_top),
  le_top := assume a s hs,
    by cases s.eq_empty_or_nonempty with h  h;
      simp [h, to_measure_apply ‚ä§ _ hs, outer_measure.top_apply],
-/
  .. complete_lattice_of_Inf (measure Œ±) (Œª ms, ‚ü®Œª _, Inf_le, Œª _, le_Inf‚ü©) }

open outer_measure

instance : has_scalar ennreal (measure Œ±) :=
‚ü®Œª x m, {
  m_Union := Œª s hs h2s, by { simp only [measure_of_eq_coe, to_outer_measure_apply, smul_apply,
    ennreal.tsum_mul_left, measure_Union h2s hs] },
  trimmed := by { convert m.to_outer_measure.trim_smul x, ext1 s,
    simp only [m.trimmed, smul_apply, measure_of_eq_coe] },
  ..x ‚Ä¢ m.to_outer_measure }‚ü©

@[simp] theorem smul_apply (a : ennreal) (m : measure Œ±) (s : set Œ±) : (a ‚Ä¢ m) s = a * m s := rfl

instance : semimodule ennreal (measure Œ±) :=
{ smul_add := Œª a m‚ÇÅ m‚ÇÇ, ext $ Œª s hs, mul_add _ _ _,
  add_smul := Œª a b m, ext $ Œª s hs, add_mul _ _ _,
  mul_smul := Œª a b m, ext $ Œª s hs, mul_assoc _ _ _,
  one_smul := Œª m, ext $ Œª s hs, one_mul _,
  zero_smul := Œª m, ext $ Œª s hs, zero_mul _,
  smul_zero := Œª a, ext $ Œª s hs, mul_zero _,
  ..measure.has_scalar }

end

/-- The pushforward of a measure. It is defined to be `0` if `f` is not a measurable function. -/
def map (f : Œ± ‚Üí Œ≤) (Œº : measure Œ±) : measure Œ≤ :=
if hf : measurable f then
  (Œº.to_outer_measure.map f).to_measure $ Œª s hs t,
  le_to_outer_measure_caratheodory Œº _ (hf _ hs) (f ‚Åª¬π' t)
else 0

variables {Œº ŒΩ : measure Œ±}

@[simp] theorem map_apply {f : Œ± ‚Üí Œ≤} (hf : measurable f)
  {s : set Œ≤} (hs : is_measurable s) :
  (map f Œº : measure Œ≤) s = Œº (f ‚Åª¬π' s) :=
by rw [map, dif_pos hf, to_measure_apply _ _ hs]; refl

@[simp] lemma map_id : map id Œº = Œº :=
ext $ Œª s, map_apply measurable_id

lemma map_map {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : measurable g) (hf : measurable f) :
  map g (map f Œº) = map (g ‚àò f) Œº :=
ext $ Œª s hs,
by simp [hf, hg, hs, hg.preimage hs, hg.comp hf];
   rw ‚Üê preimage_comp

/-- The dirac measure. -/
def dirac (a : Œ±) : measure Œ± :=
(outer_measure.dirac a).to_measure (by simp)

@[simp] lemma dirac_apply (a : Œ±) {s : set Œ±} (hs : is_measurable s) :
  (dirac a : measure Œ±) s = ‚®Ü h : a ‚àà s, 1 :=
to_measure_apply _ _ hs

/-- Sum of an indexed family of measures. -/
def sum {Œπ : Type*} (f : Œπ ‚Üí measure Œ±) : measure Œ± :=
(outer_measure.sum (Œª i, (f i).to_outer_measure)).to_measure $
le_trans
  (by exact le_infi (Œª i, le_to_outer_measure_caratheodory _))
  (outer_measure.le_sum_caratheodory _)

/-- Counting measure on any measurable space. -/
def count : measure Œ± := sum dirac

/-- A measure is complete if every null set is also measurable.
  A null set is a subset of a measurable set with measure `0`.
  Since every measure is defined as a special case of an outer measure, we can more simply state
  that a set `s` is null if `Œº s = 0`. -/
@[class] def is_complete {Œ±} {_:measurable_space Œ±} (Œº : measure Œ±) : Prop :=
‚àÄ s, Œº s = 0 ‚Üí is_measurable s

/-- The "almost everywhere" filter of co-null sets. -/
def ae (Œº : measure Œ±) : filter Œ± :=
{ sets := {s | Œº s·∂ú = 0},
  univ_sets := by simp [measure_empty],
  inter_sets := Œª s t hs ht, by simp [compl_inter]; exact measure_union_null hs ht,
  sets_of_superset := Œª s t hs hst, measure_mono_null (set.compl_subset_compl.2 hst) hs }

end measure

variables {Œ± : Type*} {Œ≤ : Type*} [measurable_space Œ±] {Œº : measure Œ±}

notation `‚àÄ‚Çò` binders `‚àÇ` Œº `, ` r:(scoped P, Œº.ae.eventually P) := r

lemma mem_ae_iff (s : set Œ±) : s ‚àà Œº.ae.sets ‚Üî Œº s·∂ú = 0 := iff.rfl

lemma ae_iff {p : Œ± ‚Üí Prop} : (‚àÄ‚Çò a ‚àÇ Œº, p a) ‚Üî Œº { a | ¬¨ p a } = 0 := iff.rfl

lemma measure_zero_iff_ae_nmem {s : set Œ±} : Œº s = 0 ‚Üî ‚àÄ‚Çò a ‚àÇ Œº, a ‚àâ s :=
by simp only [ae_iff, not_not, set_of_mem_eq]

lemma ae_of_all {p : Œ± ‚Üí Prop} (Œº : measure Œ±) : (‚àÄa, p a) ‚Üí ‚àÄ‚Çò a ‚àÇ Œº, p a :=
eventually_of_forall

instance : countable_Inter_filter Œº.ae :=
‚ü®begin
  intros S hSc hS,
  simp only [mem_ae_iff, compl_sInter, sUnion_image, bUnion_eq_Union] at hS ‚ä¢,
  haveI := hSc.to_encodable,
  exact measure_Union_null (subtype.forall.2 hS)
end‚ü©

lemma ae_all_iff {Œπ : Type*} [encodable Œπ] {p : Œ± ‚Üí Œπ ‚Üí Prop} :
  (‚àÄ‚Çò a ‚àÇ Œº, ‚àÄi, p a i) ‚Üî (‚àÄi, ‚àÄ‚Çò a ‚àÇ Œº, p a i) :=
eventually_countable_forall

lemma ae_ball_iff {Œπ} {S : set Œπ} (hS : countable S) {p : Œ† (x : Œ±) (i ‚àà S), Prop} :
  (‚àÄ‚Çò x ‚àÇ Œº, ‚àÄ i ‚àà S, p x i ‚Äπ_‚Ä∫) ‚Üî ‚àÄ i ‚àà S, ‚àÄ‚Çò x ‚àÇ Œº, p x i ‚Äπ_‚Ä∫ :=
eventually_countable_ball hS

lemma ae_eq_refl (f : Œ± ‚Üí Œ≤) : ‚àÄ‚Çò a ‚àÇ Œº, f a = f a :=
ae_of_all Œº $ Œª a, rfl

lemma ae_eq_symm {f g : Œ± ‚Üí Œ≤} (h : ‚àÄ‚Çò a ‚àÇ Œº, f a = g a) : (‚àÄ‚Çò a ‚àÇ Œº, g a = f a) :=
h.mono $ Œª a, eq.symm

lemma ae_eq_trans {f g h: Œ± ‚Üí Œ≤} (h‚ÇÅ : ‚àÄ‚Çò a ‚àÇ Œº, f a = g a) (h‚ÇÇ : ‚àÄ‚Çò a ‚àÇ Œº, g a = h a) :
  ‚àÄ‚Çò a ‚àÇ Œº, f a = h a :=
by { filter_upwards [h‚ÇÅ, h‚ÇÇ], intro a, exact eq.trans }

end measure_theory

section is_complete
open measure_theory

variables {Œ± : Type*} [measurable_space Œ±] (Œº : measure Œ±)

/-- A set is null measurable if it is the union of a null set and a measurable set. -/
def is_null_measurable (s : set Œ±) : Prop :=
‚àÉ t z, s = t ‚à™ z ‚àß is_measurable t ‚àß Œº z = 0

theorem is_null_measurable_iff {Œº : measure Œ±} {s : set Œ±} :
  is_null_measurable Œº s ‚Üî
  ‚àÉ t, t ‚äÜ s ‚àß is_measurable t ‚àß Œº (s \ t) = 0 :=
begin
  split,
  { rintro ‚ü®t, z, rfl, ht, hz‚ü©,
    refine ‚ü®t, set.subset_union_left _ _, ht, measure_mono_null _ hz‚ü©,
    simp [union_diff_left, diff_subset] },
  { rintro ‚ü®t, st, ht, hz‚ü©,
    exact ‚ü®t, _, (union_diff_cancel st).symm, ht, hz‚ü© }
end

theorem is_null_measurable_measure_eq {Œº : measure Œ±} {s t : set Œ±}
  (st : t ‚äÜ s) (hz : Œº (s \ t) = 0) : Œº s = Œº t :=
begin
  refine le_antisymm _ (measure_mono st),
  have := measure_union_le t (s \ t),
  rw [union_diff_cancel st, hz] at this, simpa
end

theorem is_measurable.is_null_measurable
  {s : set Œ±} (hs : is_measurable s) : is_null_measurable Œº s :=
‚ü®s, ‚àÖ, by simp, hs, Œº.empty‚ü©

theorem is_null_measurable_of_complete [c : Œº.is_complete]
  {s : set Œ±} : is_null_measurable Œº s ‚Üî is_measurable s :=
‚ü®by rintro ‚ü®t, z, rfl, ht, hz‚ü©; exact
  is_measurable.union ht (c _ hz),
 Œª h, h.is_null_measurable _‚ü©

variables {Œº}
theorem is_null_measurable.union_null {s z : set Œ±}
  (hs : is_null_measurable Œº s) (hz : Œº z = 0) :
  is_null_measurable Œº (s ‚à™ z) :=
begin
  rcases hs with ‚ü®t, z', rfl, ht, hz'‚ü©,
  exact ‚ü®t, z' ‚à™ z, set.union_assoc _ _ _, ht, le_zero_iff_eq.1
    (le_trans (measure_union_le _ _) $ by simp [hz, hz'])‚ü©
end

theorem null_is_null_measurable {z : set Œ±}
  (hz : Œº z = 0) : is_null_measurable Œº z :=
by simpa using (is_measurable.empty.is_null_measurable _).union_null hz

theorem is_null_measurable.Union_nat {s : ‚Ñï ‚Üí set Œ±}
  (hs : ‚àÄ i, is_null_measurable Œº (s i)) :
  is_null_measurable Œº (Union s) :=
begin
  choose t ht using assume i, is_null_measurable_iff.1 (hs i),
  simp [forall_and_distrib] at ht,
  rcases ht with ‚ü®st, ht, hz‚ü©,
  refine is_null_measurable_iff.2
    ‚ü®Union t, Union_subset_Union st, is_measurable.Union ht,
      measure_mono_null _ (measure_Union_null hz)‚ü©,
  rw [diff_subset_iff, ‚Üê Union_union_distrib],
  exact Union_subset_Union (Œª i, by rw ‚Üê diff_subset_iff)
end

theorem is_measurable.diff_null {s z : set Œ±}
  (hs : is_measurable s) (hz : Œº z = 0) :
  is_null_measurable Œº (s \ z) :=
begin
  rw measure_eq_infi at hz,
  choose f hf using show ‚àÄ q : {q:‚Ñö//q>0}, ‚àÉ t:set Œ±,
    z ‚äÜ t ‚àß is_measurable t ‚àß Œº t < (nnreal.of_real q.1 : ennreal),
  { rintro ‚ü®Œµ, Œµ0‚ü©,
    have : 0 < (nnreal.of_real Œµ : ennreal), { simpa using Œµ0 },
    rw ‚Üê hz at this, simpa [infi_lt_iff] },
  refine is_null_measurable_iff.2 ‚ü®s \ Inter f,
    diff_subset_diff_right (subset_Inter (Œª i, (hf i).1)),
    hs.diff (is_measurable.Inter (Œª i, (hf i).2.1)),
    measure_mono_null _ (le_zero_iff_eq.1 $ le_of_not_lt $ Œª h, _)‚ü©,
  { exact Inter f },
  { rw [diff_subset_iff, diff_union_self],
    exact subset.trans (diff_subset _ _) (subset_union_left _ _) },
  rcases ennreal.lt_iff_exists_rat_btwn.1 h with ‚ü®Œµ, Œµ0', Œµ0, h‚ü©,
  simp at Œµ0,
  apply not_le_of_lt (lt_trans (hf ‚ü®Œµ, Œµ0‚ü©).2.2 h),
  exact measure_mono (Inter_subset _ _)
end

theorem is_null_measurable.diff_null {s z : set Œ±}
  (hs : is_null_measurable Œº s) (hz : Œº z = 0) :
  is_null_measurable Œº (s \ z) :=
begin
  rcases hs with ‚ü®t, z', rfl, ht, hz'‚ü©,
  rw [set.union_diff_distrib],
  exact (ht.diff_null hz).union_null (measure_mono_null (diff_subset _ _) hz')
end

theorem is_null_measurable.compl {s : set Œ±}
  (hs : is_null_measurable Œº s) :
  is_null_measurable Œº s·∂ú :=
begin
  rcases hs with ‚ü®t, z, rfl, ht, hz‚ü©,
  rw compl_union,
  exact ht.compl.diff_null hz
end

/-- The measurable space of all null measurable sets. -/
def null_measurable {Œ± : Type u} [measurable_space Œ±]
  (Œº : measure Œ±) : measurable_space Œ± :=
{ is_measurable := is_null_measurable Œº,
  is_measurable_empty := is_measurable.empty.is_null_measurable _,
  is_measurable_compl := Œª s hs, hs.compl,
  is_measurable_Union := Œª f, is_null_measurable.Union_nat }

/-- Given a measure we can complete it to a (complete) measure on all null measurable sets. -/
def completion {Œ± : Type u} [measurable_space Œ±] (Œº : measure Œ±) :
  @measure_theory.measure Œ± (null_measurable Œº) :=
{ to_outer_measure := Œº.to_outer_measure,
  m_Union := Œª s hs hd, show Œº (Union s) = ‚àë' i, Œº (s i), begin
    choose t ht using assume i, is_null_measurable_iff.1 (hs i),
    simp [forall_and_distrib] at ht, rcases ht with ‚ü®st, ht, hz‚ü©,
    rw is_null_measurable_measure_eq (Union_subset_Union st),
    { rw measure_Union _ ht,
      { congr, funext i,
        exact (is_null_measurable_measure_eq (st i) (hz i)).symm },
      { rintro i j ij x ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
        exact hd i j ij ‚ü®st i h‚ÇÅ, st j h‚ÇÇ‚ü© } },
    { refine measure_mono_null _ (measure_Union_null hz),
      rw [diff_subset_iff, ‚Üê Union_union_distrib],
      exact Union_subset_Union (Œª i, by rw ‚Üê diff_subset_iff) }
  end,
  trimmed := begin
    letI := null_measurable Œº,
    refine le_antisymm (Œª s, _) (outer_measure.trim_ge _),
    rw outer_measure.trim_eq_infi,
    dsimp,
    clear _inst,
    resetI,
    rw measure_eq_infi s,
    exact infi_le_infi (Œª t, infi_le_infi $ Œª st,
      infi_le_infi2 $ Œª ht, ‚ü®ht.is_null_measurable _, le_refl _‚ü©)
  end }

instance completion.is_complete {Œ± : Type u} [measurable_space Œ±] (Œº : measure Œ±) :
  (completion Œº).is_complete :=
Œª z hz, null_is_null_measurable hz

end is_complete

namespace measure_theory

section prio
set_option default_priority 100 -- see Note [default priority]
/-- A measure space is a measurable space equipped with a
  measure, referred to as `volume`. -/
class measure_space (Œ± : Type*) extends measurable_space Œ± :=
(volume : measure Œ±)
end prio

export measure_space (volume)

/-- `volume` is the canonical  measure on `Œ±`. -/
add_decl_doc volume

section measure_space
variables {Œ± : Type*} {Œπ : Type*} [measure_space Œ±] {s‚ÇÅ s‚ÇÇ : set Œ±}

notation `‚àÄ‚Çò` binders `, ` r:(scoped P, volume.ae.eventually P) := r

end measure_space

end measure_theory
