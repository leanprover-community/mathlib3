/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import measure_theory.outer_measure
import order.filter.countable_Inter
import data.set.accumulate

/-!
# Measure spaces

Given a measurable space `Î±`, a measure on `Î±` is a function that sends measurable sets to the
extended nonnegative reals that satisfies the following conditions:
1. `Î¼ âˆ… = 0`;
2. `Î¼` is countably additive. This means that the measure of a countable union of pairwise disjoint
   sets is equal to the measure of the individual sets.

Every measure can be canonically extended to an outer measure, so that it assigns values to
all subsets, not just the measurable subsets. On the other hand, a measure that is countably
additive on measurable sets can be restricted to measurable sets to obtain a measure.
In this file a measure is defined to be an outer measure that is countably additive on
measurable sets, with the additional assumption that the outer measure is the canonical
extension of the restricted measure.

Measures on `Î±` form a complete lattice, and are closed under scalar multiplication with `ennreal`.

We introduce the following typeclasses for measures:

* `probability_measure Î¼`: `Î¼ univ = 1`;
* `finite_measure Î¼`: `Î¼ univ < âŠ¤`;
* `sigma_finite Î¼`: there exists a countable collection of measurable sets that cover `univ`
  where `Î¼` is finite;
* `locally_finite_measure Î¼` : `âˆ€ x, âˆƒ s âˆˆ ğ“ x, Î¼ s < âŠ¤`;
* `has_no_atoms Î¼` : `âˆ€ x, Î¼ {x} = 0`; possibly should be redefined as
  `âˆ€ s, 0 < Î¼ s â†’ âˆƒ t âŠ† s, 0 < Î¼ t âˆ§ Î¼ t < Î¼ s`.

Given a measure, the null sets are the sets where `Î¼ s = 0`, where `Î¼` denotes the corresponding
outer measure (so `s` might not be measurable). We can then define the completion of `Î¼` as the
measure on the least `Ïƒ`-algebra that also contains all null sets, by defining the measure to be `0`
on the null sets.

## Main statements

* `completion` is the completion of a measure to all null measurable sets.
* `measure.of_measurable` and `outer_measure.to_measure` are two important ways to define a measure.

## Implementation notes

Given `Î¼ : measure Î±`, `Î¼ s` is the value of the *outer measure* applied to `s`.
This conveniently allows us to apply the measure to sets without proving that they are measurable.
We get countable subadditivity for all sets, but only countable additivity for measurable sets.

You often don't want to define a measure via its constructor.
Two ways that are sometimes more convenient:
* `measure.of_measurable` is a way to define a measure by only giving its value on measurable sets
  and proving the properties (1) and (2) mentioned above.
* `outer_measure.to_measure` is a way of obtaining a measure from an outer measure by showing that
  all measurable sets in the measurable space are CarathÃ©odory measurable.

To prove that two measures are equal, there are multiple options:
* `ext`: two measures are equal if they are equal on all measurable sets.
* `ext_of_generate_from_of_Union`: two measures are equal if they are equal on a Ï€-system generating
  the measurable sets, if the Ï€-system contains a spanning increasing sequence of sets where the
  measures take finite value (in particular the measures are Ïƒ-finite). This is a special case of the
  more general `ext_of_generate_from_of_cover`
* `ext_of_generate_finite`: two finite measures are equal if they are equal on a Ï€-system
  generating the measurable sets. This is a special case of `ext_of_generate_from_of_Union` using
  `C âˆª {univ}`, but is easier to work with.

A `measure_space` is a class that is a measurable space with a canonical measure.
The measure is denoted `volume`.

## References

* <https://en.wikipedia.org/wiki/Measure_(mathematics)>
* <https://en.wikipedia.org/wiki/Complete_measure>
* <https://en.wikipedia.org/wiki/Almost_everywhere>

## Tags

measure, almost everywhere, measure space, completion, null set, null measurable set
-/

noncomputable theory

open classical set filter function
open_locale classical topological_space big_operators filter

universes u v w x

namespace measure_theory

/-- A measure is defined to be an outer measure that is countably additive on
measurable sets, with the additional assumption that the outer measure is the canonical
extension of the restricted measure. -/
structure measure (Î± : Type*) [measurable_space Î±] extends outer_measure Î± :=
(m_Union â¦ƒf : â„• â†’ set Î±â¦„ :
  (âˆ€i, is_measurable (f i)) â†’ pairwise (disjoint on f) â†’
  measure_of (â‹ƒi, f i) = (âˆ‘'i, measure_of (f i)))
(trimmed : to_outer_measure.trim = to_outer_measure)

/-- Measure projections for a measure space.

For measurable sets this returns the measure assigned by the `measure_of` field in `measure`.
But we can extend this to _all_ sets, but using the outer measure. This gives us monotonicity and
subadditivity for all sets.
-/
instance measure.has_coe_to_fun {Î±} [measurable_space Î±] : has_coe_to_fun (measure Î±) :=
âŸ¨Î» _, set Î± â†’ ennreal, Î» m, m.to_outer_measureâŸ©

namespace measure

/-! ### General facts about measures -/

/-- Obtain a measure by giving a countably additive function that sends `âˆ…` to `0`. -/
def of_measurable {Î±} [measurable_space Î±]
  (m : Î  (s : set Î±), is_measurable s â†’ ennreal)
  (m0 : m âˆ… is_measurable.empty = 0)
  (mU : âˆ€ {{f : â„• â†’ set Î±}} (h : âˆ€i, is_measurable (f i)),
    pairwise (disjoint on f) â†’
    m (â‹ƒi, f i) (is_measurable.Union h) = (âˆ‘'i, m (f i) (h i))) :
  measure Î± :=
{ m_Union := Î» f hf hd,
  show induced_outer_measure m _ m0 (Union f) =
      âˆ‘' i, induced_outer_measure m _ m0 (f i), begin
    rw [induced_outer_measure_eq m0 mU, mU hf hd],
    congr, funext n, rw induced_outer_measure_eq m0 mU
  end,
  trimmed :=
  show (induced_outer_measure m _ m0).trim = induced_outer_measure m _ m0, begin
    unfold outer_measure.trim,
    congr, funext s hs,
    exact induced_outer_measure_eq m0 mU hs
  end,
  ..induced_outer_measure m _ m0 }

lemma of_measurable_apply {Î±} [measurable_space Î±]
  {m : Î  (s : set Î±), is_measurable s â†’ ennreal}
  {m0 : m âˆ… is_measurable.empty = 0}
  {mU : âˆ€ {{f : â„• â†’ set Î±}} (h : âˆ€i, is_measurable (f i)),
    pairwise (disjoint on f) â†’
    m (â‹ƒi, f i) (is_measurable.Union h) = (âˆ‘'i, m (f i) (h i))}
  (s : set Î±) (hs : is_measurable s) :
  of_measurable m m0 mU s = m s hs :=
induced_outer_measure_eq m0 mU hs

lemma to_outer_measure_injective {Î±} [measurable_space Î±] :
  injective (to_outer_measure : measure Î± â†’ outer_measure Î±) :=
Î» âŸ¨mâ‚, uâ‚, hâ‚âŸ© âŸ¨mâ‚‚, uâ‚‚, hâ‚‚âŸ© h, by { congr, exact h }

@[ext] lemma ext {Î±} [measurable_space Î±] {Î¼â‚ Î¼â‚‚ : measure Î±}
  (h : âˆ€s, is_measurable s â†’ Î¼â‚ s = Î¼â‚‚ s) :
  Î¼â‚ = Î¼â‚‚ :=
to_outer_measure_injective $ by rw [â† trimmed, outer_measure.trim_congr h, trimmed]

lemma ext_iff {Î±} [measurable_space Î±] {Î¼â‚ Î¼â‚‚ : measure Î±} :
  Î¼â‚ = Î¼â‚‚ â†” âˆ€s, is_measurable s â†’ Î¼â‚ s = Î¼â‚‚ s :=
âŸ¨by { rintro rfl s hs, refl }, measure.extâŸ©

end measure

section
variables {Î± : Type*} {Î² : Type*} {Î¹ : Type*} [measurable_space Î±] {Î¼ Î¼â‚ Î¼â‚‚ : measure Î±}
  {s sâ‚ sâ‚‚ : set Î±}

@[simp] lemma coe_to_outer_measure : â‡‘Î¼.to_outer_measure = Î¼ := rfl

lemma to_outer_measure_apply (s) : Î¼.to_outer_measure s = Î¼ s := rfl

lemma measure_eq_trim (s) : Î¼ s = Î¼.to_outer_measure.trim s :=
by rw Î¼.trimmed; refl

lemma measure_eq_infi (s) : Î¼ s = â¨… t (st : s âŠ† t) (ht : is_measurable t), Î¼ t :=
by rw [measure_eq_trim, outer_measure.trim_eq_infi]; refl

lemma measure_eq_induced_outer_measure :
  Î¼ s = induced_outer_measure (Î» s _, Î¼ s) is_measurable.empty Î¼.empty s :=
measure_eq_trim _

lemma to_outer_measure_eq_induced_outer_measure :
  Î¼.to_outer_measure = induced_outer_measure (Î» s _, Î¼ s) is_measurable.empty Î¼.empty :=
Î¼.trimmed.symm

lemma measure_eq_extend (hs : is_measurable s) :
  Î¼ s = extend (Î» t (ht : is_measurable t), Î¼ t) s :=
by { rw [measure_eq_induced_outer_measure, induced_outer_measure_eq_extend _ _ hs],
  exact Î¼.m_Union }

@[simp] lemma measure_empty : Î¼ âˆ… = 0 := Î¼.empty

lemma nonempty_of_measure_ne_zero (h : Î¼ s â‰  0) : s.nonempty :=
ne_empty_iff_nonempty.1 $ Î» h', h $ h'.symm â–¸ measure_empty

lemma measure_mono (h : sâ‚ âŠ† sâ‚‚) : Î¼ sâ‚ â‰¤ Î¼ sâ‚‚ := Î¼.mono h

lemma measure_mono_null (h : sâ‚ âŠ† sâ‚‚) (hâ‚‚ : Î¼ sâ‚‚ = 0) : Î¼ sâ‚ = 0 :=
le_zero_iff_eq.1 $ hâ‚‚ â–¸ measure_mono h

lemma measure_mono_top (h : sâ‚ âŠ† sâ‚‚) (hâ‚ : Î¼ sâ‚ = âŠ¤) : Î¼ sâ‚‚ = âŠ¤ :=
top_unique $ hâ‚ â–¸ measure_mono h

lemma exists_is_measurable_superset_of_measure_eq_zero {s : set Î±} (h : Î¼ s = 0) :
  âˆƒt, s âŠ† t âˆ§ is_measurable t âˆ§ Î¼ t = 0 :=
outer_measure.exists_is_measurable_superset_of_trim_eq_zero (by rw [â† measure_eq_trim, h])

lemma exists_is_measurable_superset_iff_measure_eq_zero {s : set Î±} :
  (âˆƒ t, s âŠ† t âˆ§ is_measurable t âˆ§ Î¼ t = 0) â†” Î¼ s = 0 :=
âŸ¨Î» âŸ¨t, hst, _, htâŸ©, measure_mono_null hst ht, exists_is_measurable_superset_of_measure_eq_zeroâŸ©

theorem measure_Union_le {Î²} [encodable Î²] (s : Î² â†’ set Î±) : Î¼ (â‹ƒi, s i) â‰¤ (âˆ‘'i, Î¼ (s i)) :=
Î¼.to_outer_measure.Union _

lemma measure_bUnion_le {s : set Î²} (hs : countable s) (f : Î² â†’ set Î±) :
  Î¼ (â‹ƒbâˆˆs, f b) â‰¤ âˆ‘'p:s, Î¼ (f p) :=
begin
  haveI := hs.to_encodable,
  rw [bUnion_eq_Union],
  apply measure_Union_le
end

lemma measure_bUnion_finset_le (s : finset Î²) (f : Î² â†’ set Î±) :
  Î¼ (â‹ƒbâˆˆs, f b) â‰¤ âˆ‘ p in s, Î¼ (f p) :=
begin
  rw [â† finset.sum_attach, finset.attach_eq_univ, â† tsum_fintype],
  exact measure_bUnion_le s.countable_to_set f
end

lemma measure_bUnion_lt_top {s : set Î²} {f : Î² â†’ set Î±} (hs : finite s)
  (hfin : âˆ€ i âˆˆ s, Î¼ (f i) < âŠ¤) : Î¼ (â‹ƒ i âˆˆ s, f i) < âŠ¤ :=
begin
  convert (measure_bUnion_finset_le hs.to_finset f).trans_lt _,
  { ext, rw [finite.mem_to_finset] },
  apply ennreal.sum_lt_top, simpa only [finite.mem_to_finset]
end

lemma measure_Union_null {Î²} [encodable Î²] {s : Î² â†’ set Î±} :
  (âˆ€ i, Î¼ (s i) = 0) â†’ Î¼ (â‹ƒi, s i) = 0 :=
Î¼.to_outer_measure.Union_null

lemma measure_Union_null_iff {Î¹} [encodable Î¹] {s : Î¹ â†’ set Î±} :
  Î¼ (â‹ƒ i, s i) = 0 â†” âˆ€ i, Î¼ (s i) = 0 :=
âŸ¨Î» h i, measure_mono_null (subset_Union _ _) h, measure_Union_nullâŸ©

theorem measure_union_le (sâ‚ sâ‚‚ : set Î±) : Î¼ (sâ‚ âˆª sâ‚‚) â‰¤ Î¼ sâ‚ + Î¼ sâ‚‚ :=
Î¼.to_outer_measure.union _ _

lemma measure_union_null {sâ‚ sâ‚‚ : set Î±} : Î¼ sâ‚ = 0 â†’ Î¼ sâ‚‚ = 0 â†’ Î¼ (sâ‚ âˆª sâ‚‚) = 0 :=
Î¼.to_outer_measure.union_null

lemma measure_union_null_iff {sâ‚ sâ‚‚ : set Î±} : Î¼ (sâ‚ âˆª sâ‚‚) = 0 â†” Î¼ sâ‚ = 0 âˆ§ Î¼ sâ‚‚ = 0:=
âŸ¨Î» h, âŸ¨measure_mono_null (subset_union_left _ _) h, measure_mono_null (subset_union_right _ _) hâŸ©,
  Î» h, measure_union_null h.1 h.2âŸ©

lemma measure_Union {Î²} [encodable Î²] {f : Î² â†’ set Î±}
  (hn : pairwise (disjoint on f)) (h : âˆ€i, is_measurable (f i)) :
  Î¼ (â‹ƒi, f i) = (âˆ‘'i, Î¼ (f i)) :=
begin
  rw [measure_eq_extend (is_measurable.Union h),
    extend_Union is_measurable.empty _ is_measurable.Union _ hn h],
  { simp [measure_eq_extend, h] },
  { exact Î¼.empty },
  { exact Î¼.m_Union }
end

lemma measure_union (hd : disjoint sâ‚ sâ‚‚) (hâ‚ : is_measurable sâ‚) (hâ‚‚ : is_measurable sâ‚‚) :
  Î¼ (sâ‚ âˆª sâ‚‚) = Î¼ sâ‚ + Î¼ sâ‚‚ :=
begin
  rw [union_eq_Union, measure_Union, tsum_fintype, fintype.sum_bool, cond, cond],
  exacts [pairwise_disjoint_on_bool.2 hd, Î» b, bool.cases_on b hâ‚‚ hâ‚]
end

lemma measure_bUnion {s : set Î²} {f : Î² â†’ set Î±} (hs : countable s)
  (hd : pairwise_on s (disjoint on f)) (h : âˆ€bâˆˆs, is_measurable (f b)) :
  Î¼ (â‹ƒbâˆˆs, f b) = âˆ‘'p:s, Î¼ (f p) :=
begin
  haveI := hs.to_encodable,
  rw bUnion_eq_Union,
  exact measure_Union (hd.on_injective subtype.coe_injective $ Î» x, x.2) (Î» x, h x x.2)
end

lemma measure_sUnion {S : set (set Î±)} (hs : countable S)
  (hd : pairwise_on S disjoint) (h : âˆ€sâˆˆS, is_measurable s) :
  Î¼ (â‹ƒâ‚€ S) = âˆ‘' s:S, Î¼ s :=
by rw [sUnion_eq_bUnion, measure_bUnion hs hd h]

lemma measure_bUnion_finset {s : finset Î¹} {f : Î¹ â†’ set Î±} (hd : pairwise_on â†‘s (disjoint on f))
  (hm : âˆ€bâˆˆs, is_measurable (f b)) :
  Î¼ (â‹ƒbâˆˆs, f b) = âˆ‘ p in s, Î¼ (f p) :=
begin
  rw [â† finset.sum_attach, finset.attach_eq_univ, â† tsum_fintype],
  exact measure_bUnion s.countable_to_set hd hm
end

/-- If `s` is a countable set, then the measure of its preimage can be found as the sum of measures
of the fibers `f â»Â¹' {y}`. -/
lemma tsum_measure_preimage_singleton {s : set Î²} (hs : countable s) {f : Î± â†’ Î²}
  (hf : âˆ€ y âˆˆ s, is_measurable (f â»Â¹' {y})) :
  (âˆ‘' b : s, Î¼ (f â»Â¹' {â†‘b})) = Î¼ (f â»Â¹' s) :=
by rw [â† set.bUnion_preimage_singleton, measure_bUnion hs (pairwise_on_disjoint_fiber _ _) hf]

/-- If `s` is a `finset`, then the measure of its preimage can be found as the sum of measures
of the fibers `f â»Â¹' {y}`. -/
lemma sum_measure_preimage_singleton (s : finset Î²) {f : Î± â†’ Î²}
  (hf : âˆ€ y âˆˆ s, is_measurable (f â»Â¹' {y})) :
  âˆ‘ b in s, Î¼ (f â»Â¹' {b}) = Î¼ (f â»Â¹' â†‘s) :=
by simp only [â† measure_bUnion_finset (pairwise_on_disjoint_fiber _ _) hf,
  finset.bUnion_preimage_singleton]

lemma measure_diff {sâ‚ sâ‚‚ : set Î±} (h : sâ‚‚ âŠ† sâ‚) (hâ‚ : is_measurable sâ‚) (hâ‚‚ : is_measurable sâ‚‚)
  (h_fin : Î¼ sâ‚‚ < âŠ¤) :
  Î¼ (sâ‚ \ sâ‚‚) = Î¼ sâ‚ - Î¼ sâ‚‚ :=
begin
  refine (ennreal.add_sub_self' h_fin).symm.trans _,
  rw [â† measure_union disjoint_diff hâ‚‚ (hâ‚.diff hâ‚‚), union_diff_cancel h]
end

lemma measure_compl {Î¼ : measure Î±} {s : set Î±} (hâ‚ : is_measurable s) (h_fin : Î¼ s < âŠ¤) :
  Î¼ (sá¶œ) = Î¼ univ - Î¼ s :=
by { rw compl_eq_univ_diff, exact measure_diff (subset_univ s) is_measurable.univ hâ‚ h_fin }

lemma sum_measure_le_measure_univ {s : finset Î¹} {t : Î¹ â†’ set Î±} (h : âˆ€ i âˆˆ s, is_measurable (t i))
  (H : pairwise_on â†‘s (disjoint on t)) :
  âˆ‘ i in s, Î¼ (t i) â‰¤ Î¼ (univ : set Î±) :=
by { rw â† measure_bUnion_finset H h, exact measure_mono (subset_univ _) }

lemma tsum_measure_le_measure_univ {s : Î¹ â†’ set Î±} (hs : âˆ€ i, is_measurable (s i))
  (H : pairwise (disjoint on s)) :
  (âˆ‘' i, Î¼ (s i)) â‰¤ Î¼ (univ : set Î±) :=
begin
  rw [ennreal.tsum_eq_supr_sum],
  exact supr_le (Î» s, sum_measure_le_measure_univ (Î» i hi, hs i) (Î» i hi j hj hij, H i j hij))
end

/-- Pigeonhole principle for measure spaces: if `âˆ‘' i, Î¼ (s i) > Î¼ univ`, then
one of the intersections `s i âˆ© s j` is not empty. -/
lemma exists_nonempty_inter_of_measure_univ_lt_tsum_measure (Î¼ : measure Î±) {s : Î¹ â†’ set Î±}
  (hs : âˆ€ i, is_measurable (s i)) (H : Î¼ (univ : set Î±) < âˆ‘' i, Î¼ (s i)) :
  âˆƒ i j (h : i â‰  j), (s i âˆ© s j).nonempty :=
begin
  contrapose! H,
  apply tsum_measure_le_measure_univ hs,
  exact Î» i j hij x hx, H i j hij âŸ¨x, hxâŸ©
end

/-- Pigeonhole principle for measure spaces: if `s` is a `finset` and
`âˆ‘ i in s, Î¼ (t i) > Î¼ univ`, then one of the intersections `t i âˆ© t j` is not empty. -/
lemma exists_nonempty_inter_of_measure_univ_lt_sum_measure (Î¼ : measure Î±) {s : finset Î¹}
  {t : Î¹ â†’ set Î±} (h : âˆ€ i âˆˆ s, is_measurable (t i)) (H : Î¼ (univ : set Î±) < âˆ‘ i in s, Î¼ (t i)) :
  âˆƒ (i âˆˆ s) (j âˆˆ s) (h : i â‰  j), (t i âˆ© t j).nonempty :=
begin
  contrapose! H,
  apply sum_measure_le_measure_univ h,
  exact Î» i hi j hj hij x hx, H i hi j hj hij âŸ¨x, hxâŸ©
end

/-- Continuity from below: the measure of the union of a directed sequence of measurable sets
is the supremum of the measures. -/
lemma measure_Union_eq_supr [encodable Î¹] {s : Î¹ â†’ set Î±} (h : âˆ€ i, is_measurable (s i))
  (hd : directed (âŠ†) s) : Î¼ (â‹ƒ i, s i) = â¨† i, Î¼ (s i) :=
begin
  by_cases hÎ¹ : nonempty Î¹, swap,
  { simp only [supr_of_empty hÎ¹, Union], exact measure_empty },
  resetI,
  refine le_antisymm _ (supr_le $ Î» i, measure_mono $ subset_Union _ _),
  have : âˆ€ n, is_measurable (disjointed (Î» n, â‹ƒ b âˆˆ encodable.decode2 Î¹ n, s b) n) :=
    is_measurable.disjointed (is_measurable.bUnion_decode2 h),
  rw [â† encodable.Union_decode2, â† Union_disjointed, measure_Union disjoint_disjointed this,
    ennreal.tsum_eq_supr_nat],
  simp only [â† measure_bUnion_finset (disjoint_disjointed.pairwise_on _) (Î» n _, this n)],
  refine supr_le (Î» n, _),
  refine le_trans (_ : _ â‰¤ Î¼ (â‹ƒ (k âˆˆ finset.range n) (i âˆˆ encodable.decode2 Î¹ k), s i)) _,
  exact measure_mono (bUnion_subset_bUnion_right (Î» k hk, disjointed_subset)),
  simp only [â† finset.bUnion_option_to_finset, â† finset.bUnion_bind],
  generalize : (finset.range n).bind (Î» k, (encodable.decode2 Î¹ k).to_finset) = t,
  rcases hd.finset_le t with âŸ¨i, hiâŸ©,
  exact le_supr_of_le i (measure_mono $ bUnion_subset hi)
end

lemma measure_bUnion_eq_supr {s : Î¹ â†’ set Î±} {t : set Î¹} (ht : countable t)
  (h : âˆ€ i âˆˆ t, is_measurable (s i)) (hd : directed_on ((âŠ†) on s) t) :
  Î¼ (â‹ƒ i âˆˆ t, s i) = â¨† i âˆˆ t, Î¼ (s i) :=
begin
  haveI := ht.to_encodable,
  rw [bUnion_eq_Union, measure_Union_eq_supr (set_coe.forall'.1 h) hd.directed_coe,
    supr_subtype'],
  refl
end

/-- Continuity from above: the measure of the intersection of a decreasing sequence of measurable
sets is the infimum of the measures. -/
lemma measure_Inter_eq_infi [encodable Î¹] {s : Î¹ â†’ set Î±}
  (h : âˆ€i, is_measurable (s i)) (hd : directed (âŠ‡) s)
  (hfin : âˆƒi, Î¼ (s i) < âŠ¤) :
  Î¼ (â‹‚ i, s i) = (â¨… i, Î¼ (s i)) :=
begin
  rcases hfin with âŸ¨k, hkâŸ©,
  rw [â† ennreal.sub_sub_cancel (by exact hk) (infi_le _ k), ennreal.sub_infi,
    â† ennreal.sub_sub_cancel (by exact hk) (measure_mono (Inter_subset _ k)),
    â† measure_diff (Inter_subset _ k) (h k) (is_measurable.Inter h)
      (lt_of_le_of_lt (measure_mono (Inter_subset _ k)) hk),
    diff_Inter, measure_Union_eq_supr],
  { congr' 1,
    refine le_antisymm (supr_le_supr2 $ Î» i, _) (supr_le_supr $ Î» i, _),
    { rcases hd i k with âŸ¨j, hji, hjkâŸ©,
      use j,
      rw [â† measure_diff hjk (h _) (h _) ((measure_mono hjk).trans_lt hk)],
      exact measure_mono (diff_subset_diff_right hji) },
    { rw [ennreal.sub_le_iff_le_add, â† measure_union disjoint_diff.symm ((h k).diff (h i)) (h i),
        set.union_comm],
      exact measure_mono (diff_subset_iff.1 $ subset.refl _) } },
  { exact Î» i, (h k).diff (h i) },
  { exact hd.mono_comp _ (Î» _ _, diff_subset_diff_right) }
end

lemma measure_eq_inter_diff {s t : set Î±} (hs : is_measurable s) (ht : is_measurable t) :
  Î¼ s = Î¼ (s âˆ© t) + Î¼ (s \ t) :=
have hd : disjoint (s âˆ© t) (s \ t) := assume a âŸ¨âŸ¨_, hsâŸ©, _, hnsâŸ©, hns hs ,
by rw [â† measure_union hd (hs.inter ht) (hs.diff ht), inter_union_diff s t]

lemma measure_union_add_inter {s t : set Î±} (hs : is_measurable s) (ht : is_measurable t) :
  Î¼ (s âˆª t) + Î¼ (s âˆ© t) = Î¼ s + Î¼ t :=
by { rw [measure_eq_inter_diff (hs.union ht) ht, set.union_inter_cancel_right,
  union_diff_right, measure_eq_inter_diff hs ht], ac_refl }

/-- Continuity from below: the measure of the union of an increasing sequence of measurable sets
is the limit of the measures. -/
lemma tendsto_measure_Union {Î¼ : measure Î±} {s : â„• â†’ set Î±}
  (hs : âˆ€n, is_measurable (s n)) (hm : monotone s) :
  tendsto (Î¼ âˆ˜ s) at_top (ğ“ (Î¼ (â‹ƒ n, s n))) :=
begin
  rw measure_Union_eq_supr hs (directed_of_sup hm),
  exact tendsto_at_top_supr (assume n m hnm, measure_mono $ hm hnm)
end

/-- Continuity from above: the measure of the intersection of a decreasing sequence of measurable
sets is the limit of the measures. -/
lemma tendsto_measure_Inter {Î¼ : measure Î±} {s : â„• â†’ set Î±}
  (hs : âˆ€n, is_measurable (s n)) (hm : âˆ€ â¦ƒn mâ¦„, n â‰¤ m â†’ s m âŠ† s n) (hf : âˆƒi, Î¼ (s i) < âŠ¤) :
  tendsto (Î¼ âˆ˜ s) at_top (ğ“ (Î¼ (â‹‚ n, s n))) :=
begin
  rw measure_Inter_eq_infi hs (directed_of_sup hm) hf,
  exact tendsto_at_top_infi (assume n m hnm, measure_mono $ hm hnm),
end

/-- One direction of the Borel-Cantelli lemma: if (sáµ¢) is a sequence of measurable sets such that
  âˆ‘ Î¼ sáµ¢ exists, then the limit superior of the sáµ¢ is a null set. -/
lemma measure_limsup_eq_zero {s : â„• â†’ set Î±} (hs : âˆ€ i, is_measurable (s i))
  (hs' : (âˆ‘' i, Î¼ (s i)) â‰  âŠ¤) : Î¼ (limsup at_top s) = 0 :=
begin
  rw limsup_eq_infi_supr_of_nat',
  -- We will show that both `Î¼ (â¨… n, â¨† i, s (i + n))` and `0` are the limit of `Î¼ (âŠ” i, s (i + n))`
  -- as `n` tends to infinity. For the former, we use continuity from above.
  refine tendsto_nhds_unique
    (tendsto_measure_Inter (Î» i, is_measurable.Union (Î» b, hs (b + i))) _
      âŸ¨0, lt_of_le_of_lt (measure_Union_le s) (ennreal.lt_top_iff_ne_top.2 hs')âŸ©) _,
  { intros n m hnm x,
    simp only [set.mem_Union],
    exact Î» âŸ¨i, hiâŸ©, âŸ¨i + (m - n), by simpa only [add_assoc, nat.sub_add_cancel hnm] using hiâŸ© },
  { -- For the latter, notice that, `Î¼ (â¨† i, s (i + n)) â‰¤ âˆ‘' s (i + n)`. Since the right hand side
    -- converges to `0` by hypothesis, so does the former and the proof is complete.
    exact (tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds
      (ennreal.tendsto_sum_nat_add (Î¼ âˆ˜ s) hs')
      (eventually_of_forall (by simp only [forall_const, zero_le]))
      (eventually_of_forall (Î» i, measure_Union_le _))) }
end

lemma measure_if {Î²} {x : Î²} {t : set Î²} {s : set Î±} {Î¼ : measure Î±} :
  Î¼ (if x âˆˆ t then s else âˆ…) = indicator t (Î» _, Î¼ s) x :=
by { split_ifs; simp [h] }

end

/-- Obtain a measure by giving an outer measure where all sets in the Ïƒ-algebra are
  CarathÃ©odory measurable. -/
def outer_measure.to_measure {Î±} (m : outer_measure Î±)
  [ms : measurable_space Î±] (h : ms â‰¤ m.caratheodory) :
  measure Î± :=
measure.of_measurable (Î» s _, m s) m.empty
  (Î» f hf hd, m.Union_eq_of_caratheodory (Î» i, h _ (hf i)) hd)

lemma le_to_outer_measure_caratheodory {Î±} [ms : measurable_space Î±]
  (Î¼ : measure Î±) : ms â‰¤ Î¼.to_outer_measure.caratheodory :=
begin
  assume s hs,
  rw to_outer_measure_eq_induced_outer_measure,
  refine outer_measure.of_function_caratheodory (Î» t, le_infi $ Î» ht, _),
  rw [â† measure_eq_extend (ht.inter hs),
    â† measure_eq_extend (ht.diff hs),
    â† measure_union _ (ht.inter hs) (ht.diff hs),
    inter_union_diff],
  exact le_refl _,
  exact Î» x âŸ¨âŸ¨_, hâ‚âŸ©, _, hâ‚‚âŸ©, hâ‚‚ hâ‚
end

@[simp] lemma to_measure_to_outer_measure {Î±} (m : outer_measure Î±)
  [ms : measurable_space Î±] (h : ms â‰¤ m.caratheodory) :
  (m.to_measure h).to_outer_measure = m.trim := rfl

@[simp] lemma to_measure_apply {Î±} (m : outer_measure Î±)
  [ms : measurable_space Î±] (h : ms â‰¤ m.caratheodory)
  {s : set Î±} (hs : is_measurable s) :
  m.to_measure h s = m s := m.trim_eq hs

lemma le_to_measure_apply {Î±} (m : outer_measure Î±) [ms : measurable_space Î±]
  (h : ms â‰¤ m.caratheodory) (s : set Î±) :
  m s â‰¤ m.to_measure h s :=
m.le_trim s

@[simp] lemma to_outer_measure_to_measure {Î± : Type*} [ms : measurable_space Î±] {Î¼ : measure Î±} :
  Î¼.to_outer_measure.to_measure (le_to_outer_measure_caratheodory _) = Î¼ :=
measure.ext $ Î» s, Î¼.to_outer_measure.trim_eq

namespace measure
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*}
variables [measurable_space Î±] [measurable_space Î²] [measurable_space Î³]
variables {Î¼ Î¼â‚ Î¼â‚‚ Î½ Î½' : measure Î±}

/-! ### The `ennreal`-module of measures -/

instance : has_zero (measure Î±) :=
âŸ¨{ to_outer_measure := 0,
   m_Union := Î» f hf hd, tsum_zero.symm,
   trimmed := outer_measure.trim_zero }âŸ©

@[simp] theorem zero_to_outer_measure : (0 : measure Î±).to_outer_measure = 0 := rfl

@[simp, norm_cast] theorem coe_zero : â‡‘(0 : measure Î±) = 0 := rfl

lemma eq_zero_of_not_nonempty (h : Â¬nonempty Î±) (Î¼ : measure Î±) : Î¼ = 0 :=
ext $ Î» s hs, by simp only [eq_empty_of_not_nonempty h s, measure_empty]

instance : inhabited (measure Î±) := âŸ¨0âŸ©

instance : has_add (measure Î±) :=
âŸ¨Î»Î¼â‚ Î¼â‚‚, {
  to_outer_measure := Î¼â‚.to_outer_measure + Î¼â‚‚.to_outer_measure,
  m_Union := Î»s hs hd,
    show Î¼â‚ (â‹ƒ i, s i) + Î¼â‚‚ (â‹ƒ i, s i) = âˆ‘' i, Î¼â‚ (s i) + Î¼â‚‚ (s i),
    by rw [ennreal.tsum_add, measure_Union hd hs, measure_Union hd hs],
  trimmed := by rw [outer_measure.trim_add, Î¼â‚.trimmed, Î¼â‚‚.trimmed] }âŸ©

@[simp] theorem add_to_outer_measure (Î¼â‚ Î¼â‚‚ : measure Î±) :
  (Î¼â‚ + Î¼â‚‚).to_outer_measure = Î¼â‚.to_outer_measure + Î¼â‚‚.to_outer_measure := rfl

@[simp, norm_cast] theorem coe_add (Î¼â‚ Î¼â‚‚ : measure Î±) : â‡‘(Î¼â‚ + Î¼â‚‚) = Î¼â‚ + Î¼â‚‚ := rfl

theorem add_apply (Î¼â‚ Î¼â‚‚ : measure Î±) (s) : (Î¼â‚ + Î¼â‚‚) s = Î¼â‚ s + Î¼â‚‚ s := rfl

instance add_comm_monoid : add_comm_monoid (measure Î±) :=
to_outer_measure_injective.add_comm_monoid to_outer_measure zero_to_outer_measure
  add_to_outer_measure

instance : has_scalar ennreal (measure Î±) :=
âŸ¨Î» c Î¼,
  { to_outer_measure := c â€¢ Î¼.to_outer_measure,
    m_Union := Î» s hs hd, by simp [measure_Union, *, ennreal.tsum_mul_left],
    trimmed := by rw [outer_measure.trim_smul, Î¼.trimmed] }âŸ©

@[simp] theorem smul_to_outer_measure (c : ennreal) (Î¼ : measure Î±) :
  (c â€¢ Î¼).to_outer_measure = c â€¢ Î¼.to_outer_measure :=
rfl

@[simp, norm_cast] theorem coe_smul (c : ennreal) (Î¼ : measure Î±) :
  â‡‘(c â€¢ Î¼) = c â€¢ Î¼ :=
rfl

theorem smul_apply (c : ennreal) (Î¼ : measure Î±) (s : set Î±) :
  (c â€¢ Î¼) s = c * Î¼ s :=
rfl

instance : semimodule ennreal (measure Î±) :=
injective.semimodule ennreal âŸ¨to_outer_measure, zero_to_outer_measure, add_to_outer_measureâŸ©
  to_outer_measure_injective smul_to_outer_measure

/-! ### The complete lattice of measures -/

instance : partial_order (measure Î±) :=
{ le          := Î»mâ‚ mâ‚‚, âˆ€ s, is_measurable s â†’ mâ‚ s â‰¤ mâ‚‚ s,
  le_refl     := assume m s hs, le_refl _,
  le_trans    := assume mâ‚ mâ‚‚ mâ‚ƒ hâ‚ hâ‚‚ s hs, le_trans (hâ‚ s hs) (hâ‚‚ s hs),
  le_antisymm := assume mâ‚ mâ‚‚ hâ‚ hâ‚‚, ext $
    assume s hs, le_antisymm (hâ‚ s hs) (hâ‚‚ s hs) }

theorem le_iff : Î¼â‚ â‰¤ Î¼â‚‚ â†” âˆ€ s, is_measurable s â†’ Î¼â‚ s â‰¤ Î¼â‚‚ s := iff.rfl

theorem to_outer_measure_le : Î¼â‚.to_outer_measure â‰¤ Î¼â‚‚.to_outer_measure â†” Î¼â‚ â‰¤ Î¼â‚‚ :=
by rw [â† Î¼â‚‚.trimmed, outer_measure.le_trim_iff]; refl

theorem le_iff' : Î¼â‚ â‰¤ Î¼â‚‚ â†” âˆ€ s, Î¼â‚ s â‰¤ Î¼â‚‚ s :=
to_outer_measure_le.symm

theorem lt_iff : Î¼ < Î½ â†” Î¼ â‰¤ Î½ âˆ§ âˆƒ s, is_measurable s âˆ§ Î¼ s < Î½ s :=
lt_iff_le_not_le.trans $ and_congr iff.rfl $ by simp only [le_iff, not_forall, not_le, exists_prop]

theorem lt_iff' : Î¼ < Î½ â†” Î¼ â‰¤ Î½ âˆ§ âˆƒ s, Î¼ s < Î½ s :=
lt_iff_le_not_le.trans $ and_congr iff.rfl $ by simp only [le_iff', not_forall, not_le]

-- TODO: add typeclasses for `âˆ€ c, monotone ((*) c)` and `âˆ€ c, monotone ((+) c)`

protected lemma add_le_add_left (Î½ : measure Î±) (hÎ¼ : Î¼â‚ â‰¤ Î¼â‚‚) : Î½ + Î¼â‚ â‰¤ Î½ + Î¼â‚‚ :=
Î» s hs, add_le_add_left (hÎ¼ s hs) _

protected lemma add_le_add_right (hÎ¼ : Î¼â‚ â‰¤ Î¼â‚‚) (Î½ : measure Î±) : Î¼â‚ + Î½ â‰¤ Î¼â‚‚ + Î½ :=
Î» s hs, add_le_add_right (hÎ¼ s hs) _

protected lemma add_le_add (hÎ¼ : Î¼â‚ â‰¤ Î¼â‚‚) {Î½â‚ Î½â‚‚ : measure Î±} (hÎ½ : Î½â‚ â‰¤ Î½â‚‚) :
  Î¼â‚ + Î½â‚ â‰¤ Î¼â‚‚ + Î½â‚‚ :=
Î» s hs, add_le_add (hÎ¼ s hs) (hÎ½ s hs)

protected lemma le_add_left (h : Î¼ â‰¤ Î½) : Î¼ â‰¤ Î½' + Î½ :=
Î» s hs, le_add_left (h s hs)

protected lemma le_add_right (h : Î¼ â‰¤ Î½) : Î¼ â‰¤ Î½ + Î½' :=
Î» s hs, le_add_right (h s hs)

section Inf
variables {m : set (measure Î±)}

lemma Inf_caratheodory (s : set Î±) (hs : is_measurable s) :
  (Inf (to_outer_measure '' m)).caratheodory.is_measurable' s :=
begin
  rw [outer_measure.Inf_eq_of_function_Inf_gen],
  refine outer_measure.of_function_caratheodory (assume t, _),
  cases t.eq_empty_or_nonempty with ht ht, by simp [ht],
  simp only [outer_measure.Inf_gen_nonempty1 _ _ ht, le_infi_iff, ball_image_iff,
    coe_to_outer_measure, measure_eq_infi t],
  assume Î¼ hÎ¼ u htu hu,
  have hm : âˆ€{s t}, s âŠ† t â†’ outer_measure.Inf_gen (to_outer_measure '' m) s â‰¤ Î¼ t,
  { assume s t hst,
    rw [outer_measure.Inf_gen_nonempty2 _ _ (mem_image_of_mem _ hÎ¼)],
    refine infi_le_of_le (Î¼.to_outer_measure) (infi_le_of_le (mem_image_of_mem _ hÎ¼) _),
    rw [to_outer_measure_apply],
    refine measure_mono hst },
  rw [measure_eq_inter_diff hu hs],
  refine add_le_add (hm $ inter_subset_inter_left _ htu) (hm $ diff_subset_diff_left htu)
end

instance : has_Inf (measure Î±) :=
âŸ¨Î»m, (Inf (to_outer_measure '' m)).to_measure $ Inf_caratheodoryâŸ©

lemma Inf_apply {m : set (measure Î±)} {s : set Î±} (hs : is_measurable s) :
  Inf m s = Inf (to_outer_measure '' m) s :=
to_measure_apply _ _ hs

private lemma Inf_le (h : Î¼ âˆˆ m) : Inf m â‰¤ Î¼ :=
have Inf (to_outer_measure '' m) â‰¤ Î¼.to_outer_measure := Inf_le (mem_image_of_mem _ h),
assume s hs, by rw [Inf_apply hs, â† to_outer_measure_apply]; exact this s

private lemma le_Inf (h : âˆ€Î¼' âˆˆ m, Î¼ â‰¤ Î¼') : Î¼ â‰¤ Inf m :=
have Î¼.to_outer_measure â‰¤ Inf (to_outer_measure '' m) :=
  le_Inf $ ball_image_of_ball $ assume Î¼ hÎ¼, to_outer_measure_le.2 $ h _ hÎ¼,
assume s hs, by rw [Inf_apply hs, â† to_outer_measure_apply]; exact this s

instance : complete_lattice (measure Î±) :=
{ bot := 0,
  bot_le := assume a s hs, by exact bot_le,
/- Adding an explicit `top` makes `leanchecker` fail, see lean#364, disable for now

  top := (âŠ¤ : outer_measure Î±).to_measure (by rw [outer_measure.top_caratheodory]; exact le_top),
  le_top := assume a s hs,
    by cases s.eq_empty_or_nonempty with h  h;
      simp [h, to_measure_apply âŠ¤ _ hs, outer_measure.top_apply],
-/
  .. complete_lattice_of_Inf (measure Î±) (Î» ms, âŸ¨Î» _, Inf_le, Î» _, le_InfâŸ©) }

end Inf

protected lemma zero_le (Î¼ : measure Î±) : 0 â‰¤ Î¼ := bot_le

lemma le_zero_iff_eq' : Î¼ â‰¤ 0 â†” Î¼ = 0 :=
Î¼.zero_le.le_iff_eq

@[simp] lemma measure_univ_eq_zero {Î¼ : measure Î±} : Î¼ univ = 0 â†” Î¼ = 0 :=
âŸ¨Î» h, bot_unique $ Î» s hs, trans_rel_left (â‰¤) (measure_mono (subset_univ s)) h, Î» h, h.symm â–¸ rflâŸ©

/-! ### Pushforward and pullback -/

/-- Lift a linear map between `outer_measure` spaces such that for each measure `Î¼` every measurable
set is caratheodory-measurable w.r.t. `f Î¼` to a linear map between `measure` spaces. -/
def lift_linear (f : outer_measure Î± â†’â‚—[ennreal] outer_measure Î²)
  (hf : âˆ€ Î¼ : measure Î±, â€¹_â€º â‰¤ (f Î¼.to_outer_measure).caratheodory) :
  measure Î± â†’â‚—[ennreal] measure Î² :=
{ to_fun := Î» Î¼, (f Î¼.to_outer_measure).to_measure (hf Î¼),
  map_add' := Î» Î¼â‚ Î¼â‚‚, ext $ Î» s hs, by simp [hs],
  map_smul' := Î» c Î¼, ext $ Î» s hs, by simp [hs] }

@[simp] lemma lift_linear_apply {f : outer_measure Î± â†’â‚—[ennreal] outer_measure Î²} (hf)
  {Î¼ : measure Î±} {s : set Î²} (hs : is_measurable s) :
  lift_linear f hf Î¼ s = f Î¼.to_outer_measure s :=
to_measure_apply _ _ hs

lemma le_lift_linear_apply {f : outer_measure Î± â†’â‚—[ennreal] outer_measure Î²} (hf)
  {Î¼ : measure Î±} (s : set Î²) :
  f Î¼.to_outer_measure s â‰¤ lift_linear f hf Î¼ s :=
le_to_measure_apply _ _ s

/-- The pushforward of a measure. It is defined to be `0` if `f` is not a measurable function. -/
def map (f : Î± â†’ Î²) : measure Î± â†’â‚—[ennreal] measure Î² :=
if hf : measurable f then
  lift_linear (outer_measure.map f) $ Î» Î¼ s hs t,
    le_to_outer_measure_caratheodory Î¼ _ (hf hs) (f â»Â¹' t)
else 0

@[simp] theorem map_apply {f : Î± â†’ Î²} (hf : measurable f) {s : set Î²} (hs : is_measurable s) :
  map f Î¼ s = Î¼ (f â»Â¹' s) :=
by simp [map, dif_pos hf, hs]

@[simp] lemma map_id : map id Î¼ = Î¼ :=
ext $ Î» s, map_apply measurable_id

lemma map_map {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : measurable g) (hf : measurable f) :
  map g (map f Î¼) = map (g âˆ˜ f) Î¼ :=
ext $ Î» s hs,
by simp [hf, hg, hs, hg hs, hg.comp hf, â† preimage_comp]

/-- Pullback of a `measure`. If `f` sends each `measurable` set to a `measurable` set, then for each
measurable set `s` we have `comap f Î¼ s = Î¼ (f '' s)`. -/
def comap (f : Î± â†’ Î²) : measure Î² â†’â‚—[ennreal] measure Î± :=
if hf : injective f âˆ§ âˆ€ s, is_measurable s â†’ is_measurable (f '' s) then
  lift_linear (outer_measure.comap f) $ Î» Î¼ s hs t,
  begin
    simp only [coe_to_outer_measure, outer_measure.comap_apply, â† image_inter hf.1,
      image_diff hf.1],
    apply le_to_outer_measure_caratheodory,
    exact hf.2 s hs
  end
else 0

lemma comap_apply (f : Î± â†’ Î²) (hfi : injective f)
  (hf : âˆ€ s, is_measurable s â†’ is_measurable (f '' s)) (Î¼ : measure Î²)
  {s : set Î±} (hs : is_measurable s) :
  comap f Î¼ s = Î¼ (f '' s) :=
begin
  rw [comap, dif_pos, lift_linear_apply _ hs, outer_measure.comap_apply, coe_to_outer_measure],
  exact âŸ¨hfi, hfâŸ©
end

/-! ### Restricting a measure -/

/-- Restrict a measure `Î¼` to a set `s` as an `ennreal`-linear map. -/
def restrictâ‚— (s : set Î±) : measure Î± â†’â‚—[ennreal] measure Î± :=
lift_linear (outer_measure.restrict s) $ Î» Î¼ s' hs' t,
begin
  suffices : Î¼ (s âˆ© t) = Î¼ (s âˆ© t âˆ© s') + Î¼ (s âˆ© t \ s'),
  { simpa [â† set.inter_assoc, set.inter_comm _ s, â† inter_diff_assoc] },
  exact le_to_outer_measure_caratheodory _ _ hs' _,
end

/-- Restrict a measure `Î¼` to a set `s`. -/
def restrict (Î¼ : measure Î±) (s : set Î±) : measure Î± := restrictâ‚— s Î¼

@[simp] lemma restrictâ‚—_apply (s : set Î±) (Î¼ : measure Î±) :
  restrictâ‚— s Î¼ = Î¼.restrict s :=
rfl

@[simp] lemma restrict_apply {s t : set Î±} (ht : is_measurable t) :
  Î¼.restrict s t = Î¼ (t âˆ© s) :=
by simp [â† restrictâ‚—_apply, restrictâ‚—, ht]

lemma restrict_apply_univ (s : set Î±) : Î¼.restrict s univ = Î¼ s :=
by rw [restrict_apply is_measurable.univ, set.univ_inter]

lemma le_restrict_apply (s t : set Î±) :
  Î¼ (t âˆ© s) â‰¤ Î¼.restrict s t :=
by { rw [restrict, restrictâ‚—], convert le_lift_linear_apply _ t, simp }

@[simp] lemma restrict_add (Î¼ Î½ : measure Î±) (s : set Î±) :
  (Î¼ + Î½).restrict s = Î¼.restrict s + Î½.restrict s :=
(restrictâ‚— s).map_add Î¼ Î½

@[simp] lemma restrict_zero (s : set Î±) : (0 : measure Î±).restrict s = 0 :=
(restrictâ‚— s).map_zero

@[simp] lemma restrict_smul (c : ennreal) (Î¼ : measure Î±) (s : set Î±) :
  (c â€¢ Î¼).restrict s = c â€¢ Î¼.restrict s :=
(restrictâ‚— s).map_smul c Î¼

@[simp] lemma restrict_restrict {s t : set Î±} (hs : is_measurable s) :
  (Î¼.restrict t).restrict s = Î¼.restrict (s âˆ© t) :=
ext $ Î» u hu, by simp [*, set.inter_assoc]

lemma restrict_apply_eq_zero {s t : set Î±} (ht : is_measurable t) :
  Î¼.restrict s t = 0 â†” Î¼ (t âˆ© s) = 0 :=
by rw [restrict_apply ht]

lemma restrict_apply_eq_zero' {s t : set Î±} (hs : is_measurable s) :
  Î¼.restrict s t = 0 â†” Î¼ (t âˆ© s) = 0 :=
begin
  refine âŸ¨Î» h, le_zero_iff_eq.1 (h â–¸ le_restrict_apply _ _), Î» h, _âŸ©,
  rcases exists_is_measurable_superset_of_measure_eq_zero h with âŸ¨t', htt', ht', ht'0âŸ©,
  apply measure_mono_null ((inter_subset _ _ _).1 htt'),
  rw [restrict_apply (hs.compl.union ht'), union_inter_distrib_right, compl_inter_self,
    set.empty_union],
  exact measure_mono_null (inter_subset_left _ _) ht'0
end

@[simp] lemma restrict_eq_zero {s} : Î¼.restrict s = 0 â†” Î¼ s = 0 :=
by rw [â† measure_univ_eq_zero, restrict_apply_univ]

@[simp] lemma restrict_empty : Î¼.restrict âˆ… = 0 := ext $ Î» s hs, by simp [hs]

@[simp] lemma restrict_univ : Î¼.restrict univ = Î¼ := ext $ Î» s hs, by simp [hs]

lemma restrict_union_apply {s s' t : set Î±} (h : disjoint (t âˆ© s) (t âˆ© s')) (hs : is_measurable s)
  (hs' : is_measurable s') (ht : is_measurable t) :
  Î¼.restrict (s âˆª s') t = Î¼.restrict s t + Î¼.restrict s' t :=
begin
  simp only [restrict_apply, ht, set.inter_union_distrib_left],
  exact measure_union h (ht.inter hs) (ht.inter hs'),
end

lemma restrict_union {s t : set Î±} (h : disjoint s t) (hs : is_measurable s)
  (ht : is_measurable t) :
  Î¼.restrict (s âˆª t) = Î¼.restrict s + Î¼.restrict t :=
ext $ Î» t' ht', restrict_union_apply (h.mono inf_le_right inf_le_right) hs ht ht'

lemma restrict_union_add_inter {s t : set Î±} (hs : is_measurable s) (ht : is_measurable t) :
  Î¼.restrict (s âˆª t) + Î¼.restrict (s âˆ© t) = Î¼.restrict s + Î¼.restrict t :=
begin
  ext1 u hu,
  simp only [add_apply, restrict_apply hu, inter_union_distrib_left],
  convert measure_union_add_inter (hu.inter hs) (hu.inter ht) using 3,
  rw [set.inter_left_comm (u âˆ© s), set.inter_assoc, â† set.inter_assoc u u, set.inter_self]
end

@[simp] lemma restrict_add_restrict_compl {s : set Î±} (hs : is_measurable s) :
  Î¼.restrict s + Î¼.restrict sá¶œ = Î¼ :=
by rw [â† restrict_union (disjoint_compl_right _) hs hs.compl, union_compl_self, restrict_univ]

@[simp] lemma restrict_compl_add_restrict {s : set Î±} (hs : is_measurable s) :
  Î¼.restrict sá¶œ + Î¼.restrict s = Î¼ :=
by rw [add_comm, restrict_add_restrict_compl hs]

lemma restrict_union_le (s s' : set Î±) : Î¼.restrict (s âˆª s') â‰¤ Î¼.restrict s + Î¼.restrict s' :=
begin
  intros t ht,
  suffices : Î¼ (t âˆ© s âˆª t âˆ© s') â‰¤ Î¼ (t âˆ© s) + Î¼ (t âˆ© s'),
    by simpa [ht, inter_union_distrib_left],
  apply measure_union_le
end

lemma restrict_Union_apply {Î¹} [encodable Î¹] {s : Î¹ â†’ set Î±} (hd : pairwise (disjoint on s))
  (hm : âˆ€ i, is_measurable (s i)) {t : set Î±} (ht : is_measurable t) :
  Î¼.restrict (â‹ƒ i, s i) t = âˆ‘' i, Î¼.restrict (s i) t :=
begin
  simp only [restrict_apply, ht, inter_Union],
  exact measure_Union (Î» i j hij, (hd i j hij).mono inf_le_right inf_le_right)
    (Î» i, ht.inter (hm i))
end

lemma restrict_Union_apply_eq_supr {Î¹} [encodable Î¹] {s : Î¹ â†’ set Î±}
  (hm : âˆ€ i, is_measurable (s i)) (hd : directed (âŠ†) s) {t : set Î±} (ht : is_measurable t) :
  Î¼.restrict (â‹ƒ i, s i) t = â¨† i, Î¼.restrict (s i) t :=
begin
  simp only [restrict_apply ht, inter_Union],
  rw [measure_Union_eq_supr],
  exacts [Î» i, ht.inter (hm i), hd.mono_comp _ (Î» sâ‚ sâ‚‚, inter_subset_inter_right _)]
end

lemma restrict_map {f : Î± â†’ Î²} (hf : measurable f) {s : set Î²} (hs : is_measurable s) :
  (map f Î¼).restrict s = map f (Î¼.restrict $ f â»Â¹' s) :=
ext $ Î» t ht, by simp [*, hf ht]

lemma map_comap_subtype_coe {s : set Î±} (hs : is_measurable s) :
  (map (coe : s â†’ Î±)).comp (comap coe) = restrictâ‚— s :=
linear_map.ext $ Î» Î¼, ext $ Î» t ht,
by rw [restrictâ‚—_apply, restrict_apply ht, linear_map.comp_apply,
  map_apply measurable_subtype_coe ht,
  comap_apply (coe : s â†’ Î±) subtype.val_injective (Î» _, hs.subtype_image) _
    (measurable_subtype_coe ht), subtype.image_preimage_coe]

/-- Restriction of a measure to a subset is monotone both in set and in measure. -/
@[mono] lemma restrict_mono â¦ƒs s' : set Î±â¦„ (hs : s âŠ† s') â¦ƒÎ¼ Î½ : measure Î±â¦„ (hÎ¼Î½ : Î¼ â‰¤ Î½) :
  Î¼.restrict s â‰¤ Î½.restrict s' :=
assume t ht,
calc Î¼.restrict s t = Î¼ (t âˆ© s) : restrict_apply ht
... â‰¤ Î¼ (t âˆ© s') : measure_mono $ inter_subset_inter_right _ hs
... â‰¤ Î½ (t âˆ© s') : le_iff'.1 hÎ¼Î½ (t âˆ© s')
... = Î½.restrict s' t : (restrict_apply ht).symm

lemma restrict_le_self {s} : Î¼.restrict s â‰¤ Î¼ :=
assume t ht,
calc Î¼.restrict s t = Î¼ (t âˆ© s) : restrict_apply ht
... â‰¤ Î¼ t : measure_mono $ inter_subset_left t s

lemma restrict_congr_meas {s} (hs : is_measurable s) :
  Î¼.restrict s = Î½.restrict s â†” âˆ€ t âŠ† s, is_measurable t â†’ Î¼ t = Î½ t :=
âŸ¨Î» H t hts ht,
   by rw [â† inter_eq_self_of_subset_left hts, â† restrict_apply ht, H, restrict_apply ht],
 Î» H, ext $ Î» t ht,
   by rw [restrict_apply ht, restrict_apply ht, H _ (inter_subset_right _ _) (ht.inter hs)]âŸ©

lemma restrict_congr_mono {s t} (hs : s âŠ† t) (hm : is_measurable s)
  (h : Î¼.restrict t = Î½.restrict t) :
  Î¼.restrict s = Î½.restrict s :=
by rw [â† inter_eq_self_of_subset_left hs, â† restrict_restrict hm, h, restrict_restrict hm]

/-- If two measures agree on all measurable subsets of `s` and `t`, then they agree on all
measurable subsets of `s âˆª t`. -/
lemma restrict_union_congr {s t : set Î±} (hsm : is_measurable s) (htm : is_measurable t) :
  Î¼.restrict (s âˆª t) = Î½.restrict (s âˆª t) â†”
    Î¼.restrict s = Î½.restrict s âˆ§ Î¼.restrict t = Î½.restrict t :=
begin
  refine âŸ¨Î» h, âŸ¨restrict_congr_mono (subset_union_left _ _) hsm h,
    restrict_congr_mono (subset_union_right _ _) htm hâŸ©, _âŸ©,
  simp only [restrict_congr_meas, hsm, htm, hsm.union htm],
  rintros âŸ¨hs, htâŸ© u hu hum,
  rw [measure_eq_inter_diff hum hsm, measure_eq_inter_diff hum hsm,
    hs _ (inter_subset_right _ _) (hum.inter hsm),
    ht _ (diff_subset_iff.2 hu) (hum.diff hsm)]
end

variables {Î¹ : Type*}

lemma restrict_finset_bUnion_congr {s : finset Î¹} {t : Î¹ â†’ set Î±}
  (htm : âˆ€ i âˆˆ s, is_measurable (t i)) :
  Î¼.restrict (â‹ƒ i âˆˆ s, t i) = Î½.restrict (â‹ƒ i âˆˆ s, t i) â†”
    âˆ€ i âˆˆ s, Î¼.restrict (t i) = Î½.restrict (t i) :=
begin
  induction s using finset.induction_on with i s hi hs, { simp },
  simp only [finset.mem_insert, or_imp_distrib, forall_and_distrib, forall_eq] at htm âŠ¢,
  simp only [finset.bUnion_insert, â† hs htm.2],
  exact restrict_union_congr htm.1 (s.is_measurable_bUnion htm.2)
end

lemma restrict_Union_congr [encodable Î¹] {s : Î¹ â†’ set Î±} (hm : âˆ€ i, is_measurable (s i)) :
  Î¼.restrict (â‹ƒ i, s i) = Î½.restrict (â‹ƒ i, s i) â†”
    âˆ€ i, Î¼.restrict (s i) = Î½.restrict (s i) :=
begin
  refine âŸ¨Î» h i, restrict_congr_mono (subset_Union _ _) (hm i) h, Î» h, _âŸ©,
  ext1 t ht,
  have M : âˆ€ t : finset Î¹, is_measurable (â‹ƒ i âˆˆ t, s i) :=
    Î» t, t.is_measurable_bUnion (Î» i _, hm i),
  have D : directed (âŠ†) (Î» t : finset Î¹, â‹ƒ i âˆˆ t, s i) :=
    directed_of_sup (Î» tâ‚ tâ‚‚ ht, bUnion_subset_bUnion_left ht),
  rw [Union_eq_Union_finset],
  simp only [restrict_Union_apply_eq_supr M D ht,
    (restrict_finset_bUnion_congr (Î» i hi, hm i)).2 (Î» i hi, h i)],
end

lemma restrict_bUnion_congr {s : set Î¹} {t : Î¹ â†’ set Î±} (hc : countable s)
  (htm : âˆ€ i âˆˆ s, is_measurable (t i)) :
  Î¼.restrict (â‹ƒ i âˆˆ s, t i) = Î½.restrict (â‹ƒ i âˆˆ s, t i) â†”
    âˆ€ i âˆˆ s, Î¼.restrict (t i) = Î½.restrict (t i) :=
begin
  simp only [bUnion_eq_Union, set_coe.forall'] at htm âŠ¢,
  haveI := hc.to_encodable,
  exact restrict_Union_congr htm
end

lemma restrict_sUnion_congr {S : set (set Î±)} (hc : countable S) (hm : âˆ€ s âˆˆ S, is_measurable s) :
  Î¼.restrict (â‹ƒâ‚€ S) = Î½.restrict (â‹ƒâ‚€ S) â†” âˆ€ s âˆˆ S, Î¼.restrict s = Î½.restrict s :=
by rw [sUnion_eq_bUnion, restrict_bUnion_congr hc hm]

/-! ### Extensionality results -/

/-- Two measures are equal if they have equal restrictions on a spanning collection of sets
  (formulated using `Union`). -/
lemma ext_iff_of_Union_eq_univ [encodable Î¹] {s : Î¹ â†’ set Î±}
  (hm : âˆ€ i, is_measurable (s i)) (hs : (â‹ƒ i, s i) = univ) :
  Î¼ = Î½ â†” âˆ€ i, Î¼.restrict (s i) = Î½.restrict (s i) :=
by rw [â† restrict_Union_congr hm, hs, restrict_univ, restrict_univ]

alias ext_iff_of_Union_eq_univ â†” _ measure_theory.measure.ext_of_Union_eq_univ

/-- Two measures are equal if they have equal restrictions on a spanning collection of sets
  (formulated using `bUnion`). -/
lemma ext_iff_of_bUnion_eq_univ {S : set Î¹} {s : Î¹ â†’ set Î±} (hc : countable S)
  (hm : âˆ€ i âˆˆ S, is_measurable (s i)) (hs : (â‹ƒ i âˆˆ S, s i) = univ) :
  Î¼ = Î½ â†” âˆ€ i âˆˆ S, Î¼.restrict (s i) = Î½.restrict (s i) :=
by rw [â† restrict_bUnion_congr hc hm, hs, restrict_univ, restrict_univ]

alias ext_iff_of_bUnion_eq_univ â†” _ measure_theory.measure.ext_of_bUnion_eq_univ

/-- Two measures are equal if they have equal restrictions on a spanning collection of sets
  (formulated using `sUnion`). -/
lemma ext_iff_of_sUnion_eq_univ {S : set (set Î±)} (hc : countable S)
  (hm : âˆ€ s âˆˆ S, is_measurable s) (hs : (â‹ƒâ‚€ S) = univ) :
  Î¼ = Î½ â†” âˆ€ s âˆˆ S, Î¼.restrict s = Î½.restrict s :=
ext_iff_of_bUnion_eq_univ hc hm $ by rwa â† sUnion_eq_bUnion

alias ext_iff_of_sUnion_eq_univ â†” _ measure_theory.measure.ext_of_sUnion_eq_univ

open measurable_space
lemma ext_of_generate_from_of_cover {S T : set (set Î±)}
  (h_gen : â€¹_â€º = generate_from S) (hc : countable T)
  (h_inter : is_pi_system S)
  (hm : âˆ€ t âˆˆ T, is_measurable t) (hU : â‹ƒâ‚€ T = univ) (htop : âˆ€ t âˆˆ T, Î¼ t < âŠ¤)
  (ST_eq : âˆ€ (t âˆˆ T) (s âˆˆ S), Î¼ (s âˆ© t) = Î½ (s âˆ© t)) (T_eq : âˆ€ t âˆˆ T, Î¼ t = Î½ t) :
  Î¼ = Î½ :=
begin
  refine ext_of_sUnion_eq_univ hc hm hU (Î» t ht, _),
  ext1 u hu,
  simp only [restrict_apply hu],
  refine induction_on_inter h_gen h_inter _ (ST_eq t ht) _ _ hu,
  { simp only [set.empty_inter, measure_empty] },
  { intros v hv hvt,
    have := T_eq t ht,
    rw [set.inter_comm] at hvt âŠ¢,
    rwa [measure_eq_inter_diff (hm _ ht) hv, measure_eq_inter_diff (hm _ ht) hv, â† hvt,
      ennreal.add_right_inj] at this,
    exact (measure_mono $ set.inter_subset_left _ _).trans_lt (htop t ht) },
  { intros f hfd hfm h_eq,
    have : pairwise (disjoint on Î» n, f n âˆ© t) :=
      Î» m n hmn, (hfd m n hmn).mono (inter_subset_left _ _) (inter_subset_left _ _),
    simp only [Union_inter, measure_Union this (Î» n, is_measurable.inter (hfm n) (hm t ht)), h_eq] }
end

/-- Two measures are equal if they are equal on the Ï€-system generating the Ïƒ-algebra,
  and they are both finite on a increasing spanning sequence of sets in the Ï€-system.
  This lemma is formulated using `sUnion`. -/
lemma ext_of_generate_from_of_cover_subset {S T : set (set Î±)}
  (h_gen : â€¹_â€º = generate_from S)
  (h_inter : is_pi_system S)
  (h_sub : T âŠ† S) (hc : countable T) (hU : â‹ƒâ‚€ T = univ) (htop : âˆ€ s âˆˆ T, Î¼ s < âŠ¤)
  (h_eq : âˆ€ s âˆˆ S, Î¼ s = Î½ s) :
  Î¼ = Î½ :=
begin
  refine ext_of_generate_from_of_cover h_gen hc h_inter _ hU htop _ (Î» t ht, h_eq t (h_sub ht)),
  { intros t ht, rw [h_gen], exact generate_measurable.basic _ (h_sub ht) },
  { intros t ht s hs, cases (s âˆ© t).eq_empty_or_nonempty with H H,
    { simp only [H, measure_empty] },
    { exact h_eq _ (h_inter _ _ hs (h_sub ht) H) } }
end

/-- Two measures are equal if they are equal on the Ï€-system generating the Ïƒ-algebra,
  and they are both finite on a increasing spanning sequence of sets in the Ï€-system.
  This lemma is formulated using `Union`. -/
lemma ext_of_generate_from_of_Union (C : set (set Î±))  (B : â„• â†’ set Î±)
  (hA : â€¹_â€º = generate_from C) (hC : is_pi_system C) (h1B : (â‹ƒ i, B i) = univ)
  (h2B : âˆ€ i, B i âˆˆ C) (hÎ¼B : âˆ€ i, Î¼ (B i) < âŠ¤) (h_eq : âˆ€ s âˆˆ C, Î¼ s = Î½ s) : Î¼ = Î½ :=
begin
  refine ext_of_generate_from_of_cover_subset hA hC _ (countable_range B) h1B _ h_eq,
  { rintro _ âŸ¨i, rflâŸ©, apply h2B },
  { rintro _ âŸ¨i, rflâŸ©, apply hÎ¼B }
end

/-- The dirac measure. -/
def dirac (a : Î±) : measure Î± :=
(outer_measure.dirac a).to_measure (by simp)

lemma dirac_apply' (a : Î±) {s : set Î±} (hs : is_measurable s) :
  dirac a s = â¨† h : a âˆˆ s, 1 :=
to_measure_apply _ _ hs

@[simp] lemma dirac_apply (a : Î±) {s : set Î±} (hs : is_measurable s) :
  dirac a s = s.indicator 1 a :=
(dirac_apply' a hs).trans $ by { by_cases h : a âˆˆ s; simp [h] }

lemma dirac_apply_of_mem {a : Î±} {s : set Î±} (h : a âˆˆ s) :
  dirac a s = 1 :=
begin
  rw [measure_eq_infi, infi_subtype', infi_subtype'],
  convert infi_const,
  { ext1 âŸ¨âŸ¨t, hstâŸ©, htâŸ©,
    dsimp only [subtype.coe_mk] at *,
    simp only [dirac_apply _ ht, indicator_of_mem (hst h), pi.one_apply] },
  { exact âŸ¨âŸ¨âŸ¨set.univ, subset_univ _âŸ©, is_measurable.univâŸ©âŸ© }
end

/-- Sum of an indexed family of measures. -/
def sum {Î¹ : Type*} (f : Î¹ â†’ measure Î±) : measure Î± :=
(outer_measure.sum (Î» i, (f i).to_outer_measure)).to_measure $
le_trans
  (by exact le_infi (Î» i, le_to_outer_measure_caratheodory _))
  (outer_measure.le_sum_caratheodory _)

@[simp] lemma sum_apply {Î¹ : Type*} (f : Î¹ â†’ measure Î±) {s : set Î±} (hs : is_measurable s) :
  sum f s = âˆ‘' i, f i s :=
to_measure_apply _ _ hs

lemma le_sum {Î¹ : Type*} (Î¼ : Î¹ â†’ measure Î±) (i : Î¹) : Î¼ i â‰¤ sum Î¼ :=
Î» s hs, by simp only [sum_apply Î¼ hs, ennreal.le_tsum i]

lemma restrict_Union {Î¹} [encodable Î¹] {s : Î¹ â†’ set Î±} (hd : pairwise (disjoint on s))
  (hm : âˆ€ i, is_measurable (s i)) :
  Î¼.restrict (â‹ƒ i, s i) = sum (Î» i, Î¼.restrict (s i)) :=
ext $ Î» t ht, by simp only [sum_apply _ ht, restrict_Union_apply hd hm ht]

lemma restrict_Union_le {Î¹} [encodable Î¹] {s : Î¹ â†’ set Î±} :
  Î¼.restrict (â‹ƒ i, s i) â‰¤ sum (Î» i, Î¼.restrict (s i)) :=
begin
  intros t ht,
  suffices : Î¼ (â‹ƒ i, t âˆ© s i) â‰¤ âˆ‘' i, Î¼ (t âˆ© s i), by simpa [ht, inter_Union],
  apply measure_Union_le
end

@[simp] lemma sum_bool (f : bool â†’ measure Î±) : sum f = f tt + f ff :=
ext $ Î» s hs, by simp [hs, tsum_fintype]

@[simp] lemma sum_cond (Î¼ Î½ : measure Î±) : sum (Î» b, cond b Î¼ Î½) = Î¼ + Î½ :=
sum_bool _

@[simp] lemma restrict_sum {Î¹ : Type*} (Î¼ : Î¹ â†’ measure Î±) {s : set Î±} (hs : is_measurable s) :
  (sum Î¼).restrict s = sum (Î» i, (Î¼ i).restrict s) :=
ext $ Î» t ht, by simp only [sum_apply, restrict_apply, ht, ht.inter hs]

/-- Counting measure on any measurable space. -/
def count : measure Î± := sum dirac

lemma count_apply {s : set Î±} (hs : is_measurable s) :
  count s = âˆ‘' i : s, 1 :=
by simp only [count, sum_apply, hs, dirac_apply, â† tsum_subtype s 1, pi.one_apply]

@[simp] lemma count_apply_finset [measurable_singleton_class Î±] (s : finset Î±) :
  count (â†‘s : set Î±) = s.card :=
calc count (â†‘s : set Î±) = âˆ‘' i : (â†‘s : set Î±), (1 : Î± â†’ ennreal) i : count_apply s.is_measurable
                    ... = âˆ‘ i in s, 1 : s.tsum_subtype 1
                    ... = s.card : by simp

lemma count_apply_finite [measurable_singleton_class Î±] (s : set Î±) (hs : finite s) :
  count s = hs.to_finset.card :=
by rw [â† count_apply_finset, finite.coe_to_finset]

/-- `count` measure evaluates to infinity at infinite sets. -/
lemma count_apply_infinite [measurable_singleton_class Î±] {s : set Î±} (hs : s.infinite) :
  count s = âŠ¤ :=
begin
  by_contra H,
  rcases ennreal.exists_nat_gt H with âŸ¨n, hnâŸ©,
  rcases hs.exists_subset_card_eq n with âŸ¨t, ht, rflâŸ©,
  have := lt_of_le_of_lt (measure_mono ht) hn,
  simpa [lt_irrefl] using this
end

@[simp] lemma count_apply_eq_top [measurable_singleton_class Î±] {s : set Î±} :
  count s = âŠ¤ â†” s.infinite :=
begin
  by_cases hs : s.finite,
  { simp [set.infinite, hs, count_apply_finite] },
  { change s.infinite at hs,
    simp [hs, count_apply_infinite] }
end

@[simp] lemma count_apply_lt_top [measurable_singleton_class Î±] {s : set Î±} :
  count s < âŠ¤ â†” s.finite :=
calc count s < âŠ¤ â†” count s â‰  âŠ¤ : lt_top_iff_ne_top
             ... â†” Â¬s.infinite : not_congr count_apply_eq_top
             ... â†” s.finite    : not_not

open measurable_space

/-! ### The almost everywhere filter -/

/-- The â€œalmost everywhereâ€ filter of co-null sets. -/
def ae (Î¼ : measure Î±) : filter Î± :=
{ sets := {s | Î¼ sá¶œ = 0},
  univ_sets := by simp,
  inter_sets := Î» s t hs ht, by simp only [compl_inter, mem_set_of_eq];
    exact measure_union_null hs ht,
  sets_of_superset := Î» s t hs hst, measure_mono_null (set.compl_subset_compl.2 hst) hs }

/-- The filter of sets `s` such that `sá¶œ` has finite measure. -/
def cofinite (Î¼ : measure Î±) : filter Î± :=
{ sets := {s | Î¼ sá¶œ < âŠ¤},
  univ_sets := by simp,
  inter_sets := Î» s t hs ht, by { simp only [compl_inter, mem_set_of_eq],
    calc Î¼ (sá¶œ âˆª tá¶œ) â‰¤ Î¼ sá¶œ + Î¼ tá¶œ : measure_union_le _ _
                ... < âŠ¤ : ennreal.add_lt_top.2 âŸ¨hs, htâŸ© },
  sets_of_superset := Î» s t hs hst, lt_of_le_of_lt (measure_mono $ compl_subset_compl.2 hst) hs }

lemma mem_cofinite {s : set Î±} : s âˆˆ Î¼.cofinite â†” Î¼ sá¶œ < âŠ¤ := iff.rfl

lemma compl_mem_cofinite {s : set Î±} : sá¶œ âˆˆ Î¼.cofinite â†” Î¼ s < âŠ¤ :=
by rw [mem_cofinite, compl_compl]

lemma eventually_cofinite {p : Î± â†’ Prop} : (âˆ€á¶  x in Î¼.cofinite, p x) â†” Î¼ {x | Â¬p x} < âŠ¤ := iff.rfl

end measure
open measure

variables {Î± : Type*} {Î² : Type*} [measurable_space Î±] {Î¼ : measure Î±}

notation `âˆ€áµ` binders ` âˆ‚` Î¼ `, ` r:(scoped P, filter.eventually P (measure.ae Î¼)) := r
notation f ` =áµ[`:50 Î¼:50 `] `:0 g:50 := f =á¶ [measure.ae Î¼] g
notation f ` â‰¤áµ[`:50 Î¼:50 `] `:0 g:50 := f â‰¤á¶ [measure.ae Î¼] g

lemma mem_ae_iff {s : set Î±} : s âˆˆ Î¼.ae â†” Î¼ sá¶œ = 0 := iff.rfl

lemma ae_iff {p : Î± â†’ Prop} : (âˆ€áµ a âˆ‚ Î¼, p a) â†” Î¼ { a | Â¬ p a } = 0 := iff.rfl

lemma compl_mem_ae_iff {s : set Î±} : sá¶œ âˆˆ Î¼.ae â†” Î¼ s = 0 := by simp only [mem_ae_iff, compl_compl]

lemma measure_zero_iff_ae_nmem {s : set Î±} : Î¼ s = 0 â†” âˆ€áµ a âˆ‚ Î¼, a âˆ‰ s :=
compl_mem_ae_iff.symm

@[simp] lemma ae_eq_bot : Î¼.ae = âŠ¥ â†” Î¼ = 0 :=
by rw [â† empty_in_sets_eq_bot, mem_ae_iff, compl_empty, measure_univ_eq_zero]

lemma ae_of_all {p : Î± â†’ Prop} (Î¼ : measure Î±) : (âˆ€a, p a) â†’ âˆ€áµ a âˆ‚ Î¼, p a :=
eventually_of_forall

@[mono] lemma ae_mono {Î¼ Î½ : measure Î±} (h : Î¼ â‰¤ Î½) : Î¼.ae â‰¤ Î½.ae :=
Î» s hs, bot_unique $ trans_rel_left (â‰¤) (measure.le_iff'.1 h _) hs

instance : countable_Inter_filter Î¼.ae :=
âŸ¨begin
  intros S hSc hS,
  simp only [mem_ae_iff, compl_sInter, sUnion_image, bUnion_eq_Union] at hS âŠ¢,
  haveI := hSc.to_encodable,
  exact measure_Union_null (subtype.forall.2 hS)
endâŸ©

instance ae_is_measurably_generated : is_measurably_generated Î¼.ae :=
âŸ¨Î» s hs, let âŸ¨t, hst, htm, htÎ¼âŸ© := exists_is_measurable_superset_of_measure_eq_zero hs in
  âŸ¨tá¶œ, compl_mem_ae_iff.2 htÎ¼, htm.compl, compl_subset_comm.1 hstâŸ©âŸ©

lemma ae_all_iff {Î¹ : Type*} [encodable Î¹] {p : Î± â†’ Î¹ â†’ Prop} :
  (âˆ€áµ a âˆ‚ Î¼, âˆ€i, p a i) â†” (âˆ€i, âˆ€áµ a âˆ‚ Î¼, p a i) :=
eventually_countable_forall

lemma ae_ball_iff {Î¹} {S : set Î¹} (hS : countable S) {p : Î  (x : Î±) (i âˆˆ S), Prop} :
  (âˆ€áµ x âˆ‚ Î¼, âˆ€ i âˆˆ S, p x i â€¹_â€º) â†” âˆ€ i âˆˆ S, âˆ€áµ x âˆ‚ Î¼, p x i â€¹_â€º :=
eventually_countable_ball hS

lemma ae_eq_refl (f : Î± â†’ Î²) : f =áµ[Î¼] f := eventually_eq.refl _ _

lemma ae_eq_symm {f g : Î± â†’ Î²} (h : f =áµ[Î¼] g) : g =áµ[Î¼] f :=
h.symm

lemma ae_eq_trans {f g h: Î± â†’ Î²} (hâ‚ : f =áµ[Î¼] g) (hâ‚‚ : g =áµ[Î¼] h) :
  f =áµ[Î¼] h :=
hâ‚.trans hâ‚‚

lemma ae_eq_empty {s : set Î±} : s =áµ[Î¼] (âˆ… : set Î±) â†” Î¼ s = 0 :=
eventually_eq_empty.trans $ by simp [ae_iff]

lemma ae_le_set {s t : set Î±} : s â‰¤áµ[Î¼] t â†” Î¼ (s \ t) = 0 :=
calc s â‰¤áµ[Î¼] t â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ x âˆˆ t : iff.rfl
           ... â†” Î¼ (s \ t) = 0          : by simp [ae_iff]; refl

lemma union_ae_eq_right {s t : set Î±} :
  (s âˆª t : set Î±) =áµ[Î¼] t â†” Î¼ (s \ t) = 0 :=
by simp [eventually_le_antisymm_iff, ae_le_set, union_diff_right,
  diff_eq_empty.2 (set.subset_union_right _ _)]

lemma diff_ae_eq_self {s t : set Î±} :
  (s \ t : set Î±) =áµ[Î¼] s â†” Î¼ (s âˆ© t) = 0 :=
by simp [eventually_le_antisymm_iff, ae_le_set, diff_diff_right,
  diff_diff, diff_eq_empty.2 (set.subset_union_right _ _)]

lemma mem_ae_map_iff [measurable_space Î²] {f : Î± â†’ Î²} (hf : measurable f)
  {s : set Î²} (hs : is_measurable s) :
  s âˆˆ (map f Î¼).ae â†” (f â»Â¹' s) âˆˆ Î¼.ae :=
by simp only [mem_ae_iff, map_apply hf hs.compl, preimage_compl]

lemma ae_map_iff [measurable_space Î²] {f : Î± â†’ Î²} (hf : measurable f)
  {p : Î² â†’ Prop} (hp : is_measurable {x | p x}) :
  (âˆ€áµ y âˆ‚ (map f Î¼), p y) â†” âˆ€áµ x âˆ‚ Î¼, p (f x) :=
mem_ae_map_iff hf hp

lemma ae_restrict_iff {s : set Î±} {p : Î± â†’ Prop} (hp : is_measurable {x | p x}) :
  (âˆ€áµ x âˆ‚(Î¼.restrict s), p x) â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ p x :=
begin
  simp only [ae_iff, â† compl_set_of, restrict_apply hp.compl],
  congr' with x, simp [and_comm]
end

lemma ae_smul_measure {p : Î± â†’ Prop} (h : âˆ€áµ x âˆ‚Î¼, p x) (c : ennreal) : âˆ€áµ x âˆ‚(c â€¢ Î¼), p x :=
ae_iff.2 $ by rw [smul_apply, ae_iff.1 h, mul_zero]

lemma ae_add_measure_iff {p : Î± â†’ Prop} {Î½} : (âˆ€áµ x âˆ‚Î¼ + Î½, p x) â†” (âˆ€áµ x âˆ‚Î¼, p x) âˆ§ âˆ€áµ x âˆ‚Î½, p x :=
add_eq_zero_iff

@[simp] lemma ae_restrict_eq {s : set Î±} (hs : is_measurable s):
  (Î¼.restrict s).ae = Î¼.ae âŠ“ ğ“Ÿ s :=
begin
  ext t,
  simp only [mem_inf_principal, mem_ae_iff, restrict_apply_eq_zero' hs, compl_set_of,
    not_imp, and_comm (_ âˆˆ s)],
  refl
end

@[simp] lemma ae_restrict_eq_bot {s} : (Î¼.restrict s).ae = âŠ¥ â†” Î¼ s = 0 :=
ae_eq_bot.trans restrict_eq_zero

@[simp] lemma ae_restrict_ne_bot {s} : (Î¼.restrict s).ae.ne_bot â†” 0 < Î¼ s :=
(not_congr ae_restrict_eq_bot).trans zero_lt_iff_ne_zero.symm

/-- A version of the Borel-Cantelli lemma: if sáµ¢ is a sequence of measurable sets such that
âˆ‘ Î¼ sáµ¢ exists, then for almost all x, x does not belong to almost all sáµ¢. -/
lemma ae_eventually_not_mem {s : â„• â†’ set Î±} (hs : âˆ€ i, is_measurable (s i))
  (hs' : (âˆ‘' i, Î¼ (s i)) â‰  âŠ¤) : âˆ€áµ x âˆ‚ Î¼, âˆ€á¶  n in at_top, x âˆ‰ s n :=
begin
  refine measure_mono_null _ (measure_limsup_eq_zero hs hs'),
  rw â†set.le_eq_subset,
  refine le_Inf (Î» t ht x hx, _),
  simp only [le_eq_subset, not_exists, eventually_map, exists_prop, ge_iff_le, mem_set_of_eq,
    eventually_at_top, mem_compl_eq, not_forall, not_not_mem] at hx ht,
  rcases ht with âŸ¨i, hiâŸ©,
  rcases hx i with âŸ¨j, âŸ¨hj, hj'âŸ©âŸ©,
  exact hi j hj hj'
end

lemma mem_dirac_ae_iff {a : Î±} {s : set Î±} (hs : is_measurable s) :
  s âˆˆ (dirac a).ae â†” a âˆˆ s :=
by by_cases a âˆˆ s; simp [mem_ae_iff, dirac_apply, hs.compl, indicator_apply, *]

lemma eventually_dirac {a : Î±} {p : Î± â†’ Prop} (hp : is_measurable {x | p x}) :
  (âˆ€áµ x âˆ‚(dirac a), p x) â†” p a :=
mem_dirac_ae_iff hp

lemma eventually_eq_dirac [measurable_space Î²] [measurable_singleton_class Î²] {a : Î±} {f : Î± â†’ Î²}
  (hf : measurable f) :
  f =áµ[dirac a] const Î± (f a) :=
(eventually_dirac $ show is_measurable (f â»Â¹' {f a}), from hf $ is_measurable_singleton _).2 rfl

lemma dirac_ae_eq [measurable_singleton_class Î±] (a : Î±) : (dirac a).ae = pure a :=
begin
  ext s,
  simp only [mem_ae_iff, mem_pure_sets],
  by_cases ha : a âˆˆ s,
  { simp only [ha, iff_true],
    rw [â† set.singleton_subset_iff, â† compl_subset_compl] at ha,
    refine measure_mono_null ha _,
    simp [dirac_apply a (is_measurable_singleton a).compl] },
  { simp only [ha, iff_false, dirac_apply_of_mem (mem_compl ha)],
    exact one_ne_zero }
end

lemma eventually_eq_dirac' [measurable_singleton_class Î±] {a : Î±} (f : Î± â†’ Î²) :
  f =áµ[dirac a] const Î± (f a) :=
by { rw [dirac_ae_eq], show f a = f a, refl }

lemma measure_diff_of_ae_le {s t : set Î±} (H : s â‰¤áµ[Î¼] t) :
  Î¼ (s \ t) = 0 :=
flip measure_mono_null H $ Î» x hx H, hx.2 (H hx.1)

/-- If `s âŠ† t` modulo a set of measure `0`, then `Î¼ s â‰¤ Î¼ t`. -/
lemma measure_mono_ae {s t : set Î±} (H : s â‰¤áµ[Î¼] t) :
  Î¼ s â‰¤ Î¼ t :=
calc Î¼ s â‰¤ Î¼ (s âˆª t)       : measure_mono $ subset_union_left s t
     ... = Î¼ (t âˆª s \ t)   : by rw [union_diff_self, set.union_comm]
     ... â‰¤ Î¼ t + Î¼ (s \ t) : measure_union_le _ _
     ... = Î¼ t             : by rw [measure_diff_of_ae_le H, add_zero]

alias measure_mono_ae â† filter.eventually_le.measure_le

/-- If two sets are equal modulo a set of measure zero, then `Î¼ s = Î¼ t`. -/
lemma measure_congr {s t : set Î±} (H : s =áµ[Î¼] t) : Î¼ s = Î¼ t :=
le_antisymm H.le.measure_le H.symm.le.measure_le

lemma restrict_mono_ae {s t : set Î±} (h : s â‰¤áµ[Î¼] t) : Î¼.restrict s â‰¤ Î¼.restrict t :=
begin
  intros u hu,
  simp only [restrict_apply hu],
  exact measure_mono_ae (h.mono $ Î» x hx, and.imp id hx)
end

lemma restrict_congr_set {s t : set Î±} (H : s =áµ[Î¼] t) : Î¼.restrict s = Î¼.restrict t :=
le_antisymm (restrict_mono_ae H.le) (restrict_mono_ae H.symm.le)

/-- A measure `Î¼` is called a probability measure if `Î¼ univ = 1`. -/
class probability_measure (Î¼ : measure Î±) : Prop := (measure_univ : Î¼ univ = 1)

instance measure.dirac.probability_measure {x : Î±} : probability_measure (dirac x) :=
âŸ¨dirac_apply_of_mem $ mem_univ xâŸ©

/-- A measure `Î¼` is called finite if `Î¼ univ < âŠ¤`. -/
class finite_measure (Î¼ : measure Î±) : Prop := (measure_univ_lt_top : Î¼ univ < âŠ¤)

instance restrict.finite_measure (Î¼ : measure Î±) {s : set Î±} [hs : fact (Î¼ s < âŠ¤)] :
  finite_measure (Î¼.restrict s) :=
âŸ¨by simp [hs.elim]âŸ©

/-- Measure `Î¼` *has no atoms* if the measure of each singleton is zero.

NB: Wikipedia assumes that for any measurable set `s` with positive `Î¼`-measure,
there exists a measurable `t âŠ† s` such that `0 < Î¼ t < Î¼ s`. While this implies `Î¼ {x} = 0`,
the converse is not true. -/
class has_no_atoms (Î¼ : measure Î±) : Prop :=
(measure_singleton : âˆ€ x, Î¼ {x} = 0)

export probability_measure (measure_univ) has_no_atoms (measure_singleton)

attribute [simp] measure_singleton

lemma measure_lt_top (Î¼ : measure Î±) [finite_measure Î¼] (s : set Î±) : Î¼ s < âŠ¤ :=
(measure_mono (subset_univ s)).trans_lt finite_measure.measure_univ_lt_top

lemma measure_ne_top (Î¼ : measure Î±) [finite_measure Î¼] (s : set Î±) : Î¼ s â‰  âŠ¤ :=
ne_of_lt (measure_lt_top Î¼ s)

/-- `le_of_add_le_add_left` is normally applicable to `ordered_cancel_add_comm_monoid`,
but it holds for measures with the additional assumption that Î¼ is finite. -/
lemma measure.le_of_add_le_add_left {Î¼ Î½â‚ Î½â‚‚ : measure Î±} [finite_measure Î¼] (A2 : Î¼ + Î½â‚ â‰¤ Î¼ + Î½â‚‚) : Î½â‚ â‰¤ Î½â‚‚ :=
Î» S B1, ennreal.le_of_add_le_add_left (measure_theory.measure_lt_top Î¼ S) (A2 S B1)

@[priority 100]
instance probability_measure.to_finite_measure (Î¼ : measure Î±) [probability_measure Î¼] :
  finite_measure Î¼ :=
âŸ¨by simp only [measure_univ, ennreal.one_lt_top]âŸ©

lemma probability_measure.ne_zero (Î¼ : measure Î±) [probability_measure Î¼] : Î¼ â‰  0 :=
mt measure_univ_eq_zero.2 $ by simp [measure_univ]

section no_atoms

variables [has_no_atoms Î¼]

lemma measure_countable {s : set Î±} (h : countable s) : Î¼ s = 0 :=
begin
  rw [â† bUnion_of_singleton s, â† le_zero_iff_eq],
  refine le_trans (measure_bUnion_le h _) _,
  simp
end

lemma measure_finite {s : set Î±} (h : s.finite) : Î¼ s = 0 :=
measure_countable h.countable

lemma measure_finset (s : finset Î±) : Î¼ â†‘s = 0 :=
measure_finite s.finite_to_set

lemma insert_ae_eq_self (a : Î±) (s : set Î±) :
  (insert a s : set Î±) =áµ[Î¼] s :=
union_ae_eq_right.2 $ measure_mono_null (diff_subset _ _) (measure_singleton _)

variables [partial_order Î±] {a b : Î±}

lemma Iio_ae_eq_Iic : Iio a =áµ[Î¼] Iic a :=
by simp only [â† Iic_diff_right, diff_ae_eq_self,
  measure_mono_null (set.inter_subset_right _ _) (measure_singleton a)]

lemma Ioi_ae_eq_Ici : Ioi a =áµ[Î¼] Ici a :=
@Iio_ae_eq_Iic (order_dual Î±) â€¹_â€º â€¹_â€º _ _ _

lemma Ioo_ae_eq_Ioc : Ioo a b =áµ[Î¼] Ioc a b :=
(ae_eq_refl _).inter Iio_ae_eq_Iic

lemma Ioc_ae_eq_Icc : Ioc a b =áµ[Î¼] Icc a b :=
Ioi_ae_eq_Ici.inter (ae_eq_refl _)

lemma Ioo_ae_eq_Ico : Ioo a b =áµ[Î¼] Ico a b :=
Ioi_ae_eq_Ici.inter (ae_eq_refl _)

lemma Ioo_ae_eq_Icc : Ioo a b =áµ[Î¼] Icc a b :=
Ioi_ae_eq_Ici.inter Iio_ae_eq_Iic

lemma Ico_ae_eq_Icc : Ico a b =áµ[Î¼] Icc a b :=
(ae_eq_refl _).inter Iio_ae_eq_Iic

lemma Ico_ae_eq_Ioc : Ico a b =áµ[Î¼] Ioc a b :=
Ioo_ae_eq_Ico.symm.trans Ioo_ae_eq_Ioc

end no_atoms

/-- A measure is called finite at filter `f` if it is finite at some set `s âˆˆ f`.
Equivalently, it is eventually finite at `s` in `f.lift' powerset`. -/
def measure.finite_at_filter (Î¼ : measure Î±) (f : filter Î±) : Prop := âˆƒ s âˆˆ f, Î¼ s < âŠ¤

lemma finite_at_filter_of_finite (Î¼ : measure Î±) [finite_measure Î¼] (f : filter Î±) :
  Î¼.finite_at_filter f :=
âŸ¨univ, univ_mem_sets, measure_lt_top Î¼ univâŸ©

lemma measure.finite_at_bot (Î¼ : measure Î±) : Î¼.finite_at_filter âŠ¥ :=
âŸ¨âˆ…, mem_bot_sets, by simp only [measure_empty, with_top.zero_lt_top]âŸ©

/-- A measure `Î¼` is called Ïƒ-finite if there is a countable collection of sets
  `{ A i | i âˆˆ â„• }` such that `Î¼ (A i) < âŠ¤` and `â‹ƒ i, A i = s`. -/
class sigma_finite (Î¼ : measure Î±) : Prop :=
(exists_finite_spanning_sets :
  âˆƒ s : â„• â†’ set Î±,
  (âˆ€ i, is_measurable (s i)) âˆ§
  (âˆ€ i, Î¼ (s i) < âŠ¤) âˆ§
  (â‹ƒ i, s i) = univ)

lemma exists_finite_spanning_sets (Î¼ : measure Î±) [sigma_finite Î¼] :
  âˆƒ s : â„• â†’ set Î±,
  (âˆ€ i, is_measurable (s i)) âˆ§
  (âˆ€ i, Î¼ (s i) < âŠ¤) âˆ§
  (â‹ƒ i, s i) = univ :=
sigma_finite.exists_finite_spanning_sets

/-- A noncomputable way to get a monotone collection of sets that span `univ` and have finite
  measure using `classical.some`. This definition satisfies monotonicity in addition to all other
  properties in `sigma_finite`. -/
def spanning_sets (Î¼ : measure Î±) [sigma_finite Î¼] (i : â„•) : set Î± :=
accumulate (classical.some $ exists_finite_spanning_sets Î¼) i

lemma monotone_spanning_sets (Î¼ : measure Î±) [sigma_finite Î¼] :
  monotone (spanning_sets Î¼) :=
monotone_accumulate

lemma is_measurable_spanning_sets (Î¼ : measure Î±) [sigma_finite Î¼] (i : â„•) :
  is_measurable (spanning_sets Î¼ i) :=
is_measurable.Union $ Î» j, is_measurable.Union_Prop $
  Î» hij, (classical.some_spec $ exists_finite_spanning_sets Î¼).1 j

lemma measure_spanning_sets_lt_top (Î¼ : measure Î±) [sigma_finite Î¼] (i : â„•) :
  Î¼ (spanning_sets Î¼ i) < âŠ¤ :=
measure_bUnion_lt_top (finite_le_nat i) $
  Î» j _, (classical.some_spec $ exists_finite_spanning_sets Î¼).2.1 j

lemma Union_spanning_sets (Î¼ : measure Î±) [sigma_finite Î¼] :
  (â‹ƒ i : â„•, spanning_sets Î¼ i) = univ :=
by simp_rw [spanning_sets, Union_accumulate,
  (classical.some_spec $ exists_finite_spanning_sets Î¼).2.2]

namespace measure

lemma supr_restrict_spanning_sets {Î¼ : measure Î±} [sigma_finite Î¼] {s : set Î±}
  (hs : is_measurable s) :
  (â¨† i, Î¼.restrict (spanning_sets Î¼ i) s) = Î¼ s :=
begin
  convert (restrict_Union_apply_eq_supr (is_measurable_spanning_sets Î¼) _ hs).symm,
  { simp [Union_spanning_sets] },
  { exact directed_of_sup (monotone_spanning_sets Î¼) }
end
end measure

/-- Every finite measure is Ïƒ-finite. -/
@[priority 100]
instance finite_measure.to_sigma_finite (Î¼ : measure Î±) [finite_measure Î¼] : sigma_finite Î¼ :=
âŸ¨âŸ¨Î» _, univ, Î» _, is_measurable.univ, Î» _, measure_lt_top Î¼ _, Union_const _âŸ©âŸ©

instance restrict.sigma_finite (Î¼ : measure Î±) [sigma_finite Î¼] (s : set Î±) :
  sigma_finite (Î¼.restrict s) :=
begin
  refine âŸ¨âŸ¨spanning_sets Î¼, is_measurable_spanning_sets Î¼, Î» i, _, Union_spanning_sets Î¼âŸ©âŸ©,
  rw [restrict_apply (is_measurable_spanning_sets Î¼ i)],
  exact (measure_mono $ inter_subset_left _ _).trans_lt (measure_spanning_sets_lt_top Î¼ i)
end

instance sum.sigma_finite {Î¹} [fintype Î¹] (Î¼ : Î¹ â†’ measure Î±) [âˆ€ i, sigma_finite (Î¼ i)] :
  sigma_finite (sum Î¼) :=
begin
  haveI : encodable Î¹ := (encodable.trunc_encodable_of_fintype Î¹).out,
  have : âˆ€ n, is_measurable (â‹‚ (i : Î¹), spanning_sets (Î¼ i) n) :=
  Î» n, is_measurable.Inter (Î» i, is_measurable_spanning_sets (Î¼ i) n),
  refine âŸ¨âŸ¨Î» n, â‹‚ i, spanning_sets (Î¼ i) n, this, Î» n, _, _âŸ©âŸ©,
  { rw [sum_apply _ (this n), tsum_fintype, ennreal.sum_lt_top_iff],
    rintro i -,
    exact (measure_mono $ Inter_subset _ i).trans_lt (measure_spanning_sets_lt_top (Î¼ i) n) },
  { rw [Union_Inter_of_monotone], simp_rw [Union_spanning_sets, Inter_univ],
    exact Î» i, monotone_spanning_sets (Î¼ i), }
end

instance add.sigma_finite (Î¼ Î½ : measure Î±) [sigma_finite Î¼] [sigma_finite Î½] :
  sigma_finite (Î¼ + Î½) :=
by { rw [â† sum_cond], refine @sum.sigma_finite _ _ _ _ _ (bool.rec _ _); simpa }

/-- A measure is called locally finite if it is finite in some neighborhood of each point. -/
class locally_finite_measure [topological_space Î±] (Î¼ : measure Î±) : Prop :=
(finite_at_nhds : âˆ€ x, Î¼.finite_at_filter (ğ“ x))

@[priority 100]
instance finite_measure.to_locally_finite_measure [topological_space Î±] (Î¼ : measure Î±)
  [finite_measure Î¼] :
  locally_finite_measure Î¼ :=
âŸ¨Î» x, finite_at_filter_of_finite _ _âŸ©

lemma measure.finite_at_nhds [topological_space Î±] (Î¼ : measure Î±)
  [locally_finite_measure Î¼] (x : Î±) :
  Î¼.finite_at_filter (ğ“ x) :=
locally_finite_measure.finite_at_nhds x

open measurable_space

/-- Two finite measures are equal if they are equal on the Ï€-system generating the Ïƒ-algebra
  (and `univ`). -/
lemma ext_of_generate_finite (C : set (set Î±)) (hA : _inst_1 = generate_from C)
  (hC : is_pi_system C) {Î¼ Î½ : measure Î±}
  [finite_measure Î¼] [finite_measure Î½] (hÎ¼Î½ : âˆ€ s âˆˆ C, Î¼ s = Î½ s) (h_univ : Î¼ univ = Î½ univ) :
  Î¼ = Î½ :=
begin
  ext1 s hs,
  refine induction_on_inter hA hC (by simp) hÎ¼Î½ _ _ hs,
  { rintros t h1t h2t, change is_measurable t at h1t, simp [measure_compl, measure_lt_top, *] },
  { rintros f h1f h2f h3f, simp [measure_Union, is_measurable.Union, *] }
end


namespace measure

namespace finite_at_filter

variables {Î½ : measure Î±} {f g : filter Î±}

lemma filter_mono (h : f â‰¤ g) : Î¼.finite_at_filter g â†’ Î¼.finite_at_filter f :=
Î» âŸ¨s, hs, hÎ¼âŸ©, âŸ¨s, h hs, hÎ¼âŸ©

lemma inf_of_left (h : Î¼.finite_at_filter f) : Î¼.finite_at_filter (f âŠ“ g) :=
h.filter_mono inf_le_left

lemma inf_of_right (h : Î¼.finite_at_filter g) : Î¼.finite_at_filter (f âŠ“ g) :=
h.filter_mono inf_le_right

@[simp] lemma inf_ae_iff : Î¼.finite_at_filter (f âŠ“ Î¼.ae) â†” Î¼.finite_at_filter f :=
begin
  refine âŸ¨_, Î» h, h.filter_mono inf_le_leftâŸ©,
  rintros âŸ¨s, âŸ¨t, ht, u, hu, hsâŸ©, hÎ¼âŸ©,
  suffices : Î¼ t â‰¤ Î¼ s, from âŸ¨t, ht, this.trans_lt hÎ¼âŸ©,
  exact measure_mono_ae (mem_sets_of_superset hu (Î» x hu ht, hs âŸ¨ht, huâŸ©))
end

alias inf_ae_iff â†” measure_theory.measure.finite_at_filter.of_inf_ae _

lemma filter_mono_ae (h : f âŠ“ Î¼.ae â‰¤ g) (hg : Î¼.finite_at_filter g) : Î¼.finite_at_filter f :=
inf_ae_iff.1 (hg.filter_mono h)

protected lemma measure_mono (h : Î¼ â‰¤ Î½) : Î½.finite_at_filter f â†’ Î¼.finite_at_filter f :=
Î» âŸ¨s, hs, hÎ½âŸ©, âŸ¨s, hs, (measure.le_iff'.1 h s).trans_lt hÎ½âŸ©

@[mono] protected lemma mono (hf : f â‰¤ g) (hÎ¼ : Î¼ â‰¤ Î½) :
  Î½.finite_at_filter g â†’ Î¼.finite_at_filter f :=
Î» h, (h.filter_mono hf).measure_mono hÎ¼

protected lemma eventually (h : Î¼.finite_at_filter f) : âˆ€á¶  s in f.lift' powerset, Î¼ s < âŠ¤ :=
(eventually_lift'_powerset' $ Î» s t hst ht, (measure_mono hst).trans_lt ht).2 h

lemma filter_sup : Î¼.finite_at_filter f â†’ Î¼.finite_at_filter g â†’ Î¼.finite_at_filter (f âŠ” g) :=
Î» âŸ¨s, hsf, hsÎ¼âŸ© âŸ¨t, htg, htÎ¼âŸ©,
 âŸ¨s âˆª t, union_mem_sup hsf htg, (measure_union_le s t).trans_lt (ennreal.add_lt_top.2 âŸ¨hsÎ¼, htÎ¼âŸ©)âŸ©

end finite_at_filter

lemma finite_at_nhds_within [topological_space Î±] (Î¼ : measure Î±) [locally_finite_measure Î¼]
  (x : Î±) (s : set Î±) :
  Î¼.finite_at_filter (ğ“[s] x) :=
(finite_at_nhds Î¼ x).inf_of_left

@[simp] lemma finite_at_principal {s : set Î±} : Î¼.finite_at_filter (ğ“Ÿ s) â†” Î¼ s < âŠ¤ :=
âŸ¨Î» âŸ¨t, ht, hÎ¼âŸ©, (measure_mono ht).trans_lt hÎ¼, Î» h, âŸ¨s, mem_principal_self s, hâŸ©âŸ©

/-! ### Subtraction of measures -/

/-- The measure `Î¼ - Î½` is defined to be the least measure `Ï„` such that `Î¼ â‰¤ Ï„ + Î½`.
It is the equivalent of `(Î¼ - Î½) âŠ” 0` if `Î¼` and `Î½` were signed measures.
Compare with `ennreal.has_sub`.
Specifically, note that if you have `Î± = {1,2}`, and  `Î¼ {1} = 2`, `Î¼ {2} = 0`, and 
`Î½ {2} = 2`, `Î½ {1} = 0`, then `(Î¼ - Î½) {1, 2} = 2`. However, if `Î¼ â‰¤ Î½`, and
`Î½ univ â‰  âŠ¤`, then `(Î¼ - Î½) + Î½ = Î¼`. -/
noncomputable instance has_sub {Î± : Type*} [measurable_space Î±] : has_sub (measure Î±) := 
âŸ¨Î» Î¼ Î½, Inf {Ï„ | Î¼ â‰¤ Ï„ + Î½} âŸ©

section measure_sub
variables {Î½ : measure_theory.measure Î±}

lemma sub_def : Î¼ - Î½ = Inf {d | Î¼ â‰¤ d + Î½} := rfl

lemma sub_eq_zero_of_le (h : Î¼ â‰¤ Î½) : Î¼ - Î½ = 0 :=
begin
  rw [â† le_zero_iff_eq', measure.sub_def],
  apply @Inf_le (measure Î±) _ _,
  simp [h],
end

/-- This application lemma only works in special circumstances. Given knowledge of
when `Î¼ â‰¤ Î½` and `Î½ â‰¤ Î¼`, a more general application lemma can be written. -/
lemma sub_apply {s : set Î±} [finite_measure Î½] (hâ‚ : is_measurable s) (hâ‚‚ : Î½ â‰¤ Î¼) :
  (Î¼ - Î½) s = Î¼ s - Î½ s :=
begin
  -- We begin by defining `measure_sub`, which will be equal to `(Î¼ - Î½)`.
  let measure_sub : measure Î± := @measure_theory.measure.of_measurable Î± _ 
    (Î» (t : set Î±) (h_t_is_measurable : is_measurable t), (Î¼ t - Î½ t))
    begin
      simp
    end
    begin
      intros g h_meas h_disj, simp only, rw ennreal.tsum_sub, 
      repeat { rw â† measure_theory.measure_Union h_disj h_meas },
      apply measure_theory.measure_lt_top, intro i, apply hâ‚‚, apply h_meas
    end,
  -- Now, we demonstrate `Î¼ - Î½ = measure_sub`, and apply it.
  begin
    have h_measure_sub_add : (Î½ + measure_sub = Î¼),
    { ext t h_t_is_measurable,
      simp only [pi.add_apply, coe_add],
      rw [measure_theory.measure.of_measurable_apply _ h_t_is_measurable, add_comm, 
        ennreal.sub_add_cancel_of_le (hâ‚‚ t h_t_is_measurable)] },
    have h_measure_sub_eq : (Î¼ - Î½) = measure_sub,
    { rw measure_theory.measure.sub_def, apply le_antisymm,
      { apply @Inf_le (measure Î±) (measure.complete_lattice), simp [le_refl, add_comm, h_measure_sub_add] },
      apply @le_Inf (measure Î±) (measure.complete_lattice),
      intros d h_d, rw [â† h_measure_sub_add, mem_set_of_eq, add_comm d] at h_d,
      apply measure.le_of_add_le_add_left h_d },
    rw h_measure_sub_eq,
    apply measure.of_measurable_apply _ hâ‚,
  end
end

lemma sub_add_cancel_of_le [finite_measure Î½] (hâ‚ : Î½ â‰¤ Î¼) : Î¼ - Î½ + Î½ = Î¼ :=
begin
  ext s h_s_meas,
  rw [add_apply, sub_apply h_s_meas hâ‚, ennreal.sub_add_cancel_of_le (hâ‚ s h_s_meas)],
end

end measure_sub

end measure

end measure_theory

open measure_theory measure_theory.measure

section is_complete

/-- A measure is complete if every null set is also measurable.
  A null set is a subset of a measurable set with measure `0`.
  Since every measure is defined as a special case of an outer measure, we can more simply state
  that a set `s` is null if `Î¼ s = 0`. -/
@[class] def measure_theory.measure.is_complete {Î±} {_:measurable_space Î±} (Î¼ : measure Î±) : Prop :=
âˆ€ s, Î¼ s = 0 â†’ is_measurable s

variables {Î± : Type*} [measurable_space Î±] (Î¼ : measure Î±)

/-- A set is null measurable if it is the union of a null set and a measurable set. -/
def is_null_measurable (s : set Î±) : Prop :=
âˆƒ t z, s = t âˆª z âˆ§ is_measurable t âˆ§ Î¼ z = 0

theorem is_null_measurable_iff {Î¼ : measure Î±} {s : set Î±} :
  is_null_measurable Î¼ s â†”
  âˆƒ t, t âŠ† s âˆ§ is_measurable t âˆ§ Î¼ (s \ t) = 0 :=
begin
  split,
  { rintro âŸ¨t, z, rfl, ht, hzâŸ©,
    refine âŸ¨t, set.subset_union_left _ _, ht, measure_mono_null _ hzâŸ©,
    simp [union_diff_left, diff_subset] },
  { rintro âŸ¨t, st, ht, hzâŸ©,
    exact âŸ¨t, _, (union_diff_cancel st).symm, ht, hzâŸ© }
end

theorem is_null_measurable_measure_eq {Î¼ : measure Î±} {s t : set Î±}
  (st : t âŠ† s) (hz : Î¼ (s \ t) = 0) : Î¼ s = Î¼ t :=
begin
  refine le_antisymm _ (measure_mono st),
  have := measure_union_le t (s \ t),
  rw [union_diff_cancel st, hz] at this, simpa
end

theorem is_measurable.is_null_measurable
  {s : set Î±} (hs : is_measurable s) : is_null_measurable Î¼ s :=
âŸ¨s, âˆ…, by simp, hs, Î¼.emptyâŸ©

theorem is_null_measurable_of_complete [c : Î¼.is_complete]
  {s : set Î±} : is_null_measurable Î¼ s â†” is_measurable s :=
âŸ¨by rintro âŸ¨t, z, rfl, ht, hzâŸ©; exact
  is_measurable.union ht (c _ hz),
 Î» h, h.is_null_measurable _âŸ©

variables {Î¼}
theorem is_null_measurable.union_null {s z : set Î±}
  (hs : is_null_measurable Î¼ s) (hz : Î¼ z = 0) :
  is_null_measurable Î¼ (s âˆª z) :=
begin
  rcases hs with âŸ¨t, z', rfl, ht, hz'âŸ©,
  exact âŸ¨t, z' âˆª z, set.union_assoc _ _ _, ht, le_zero_iff_eq.1
    (le_trans (measure_union_le _ _) $ by simp [hz, hz'])âŸ©
end

theorem null_is_null_measurable {z : set Î±}
  (hz : Î¼ z = 0) : is_null_measurable Î¼ z :=
by simpa using (is_measurable.empty.is_null_measurable _).union_null hz

theorem is_null_measurable.Union_nat {s : â„• â†’ set Î±}
  (hs : âˆ€ i, is_null_measurable Î¼ (s i)) :
  is_null_measurable Î¼ (Union s) :=
begin
  choose t ht using assume i, is_null_measurable_iff.1 (hs i),
  simp [forall_and_distrib] at ht,
  rcases ht with âŸ¨st, ht, hzâŸ©,
  refine is_null_measurable_iff.2
    âŸ¨Union t, Union_subset_Union st, is_measurable.Union ht,
      measure_mono_null _ (measure_Union_null hz)âŸ©,
  rw [diff_subset_iff, â† Union_union_distrib],
  exact Union_subset_Union (Î» i, by rw â† diff_subset_iff)
end

theorem is_measurable.diff_null {s z : set Î±}
  (hs : is_measurable s) (hz : Î¼ z = 0) :
  is_null_measurable Î¼ (s \ z) :=
begin
  rw measure_eq_infi at hz,
  choose f hf using show âˆ€ q : {q:â„š//q>0}, âˆƒ t:set Î±,
    z âŠ† t âˆ§ is_measurable t âˆ§ Î¼ t < (nnreal.of_real q.1 : ennreal),
  { rintro âŸ¨Îµ, Îµ0âŸ©,
    have : 0 < (nnreal.of_real Îµ : ennreal), { simpa using Îµ0 },
    rw â† hz at this, simpa [infi_lt_iff] },
  refine is_null_measurable_iff.2 âŸ¨s \ Inter f,
    diff_subset_diff_right (subset_Inter (Î» i, (hf i).1)),
    hs.diff (is_measurable.Inter (Î» i, (hf i).2.1)),
    measure_mono_null _ (le_zero_iff_eq.1 $ le_of_not_lt $ Î» h, _)âŸ©,
  { exact Inter f },
  { rw [diff_subset_iff, diff_union_self],
    exact subset.trans (diff_subset _ _) (subset_union_left _ _) },
  rcases ennreal.lt_iff_exists_rat_btwn.1 h with âŸ¨Îµ, Îµ0', Îµ0, hâŸ©,
  simp at Îµ0,
  apply not_le_of_lt (lt_trans (hf âŸ¨Îµ, Îµ0âŸ©).2.2 h),
  exact measure_mono (Inter_subset _ _)
end

theorem is_null_measurable.diff_null {s z : set Î±}
  (hs : is_null_measurable Î¼ s) (hz : Î¼ z = 0) :
  is_null_measurable Î¼ (s \ z) :=
begin
  rcases hs with âŸ¨t, z', rfl, ht, hz'âŸ©,
  rw [set.union_diff_distrib],
  exact (ht.diff_null hz).union_null (measure_mono_null (diff_subset _ _) hz')
end

theorem is_null_measurable.compl {s : set Î±}
  (hs : is_null_measurable Î¼ s) :
  is_null_measurable Î¼ sá¶œ :=
begin
  rcases hs with âŸ¨t, z, rfl, ht, hzâŸ©,
  rw compl_union,
  exact ht.compl.diff_null hz
end

/-- The measurable space of all null measurable sets. -/
def null_measurable {Î± : Type u} [measurable_space Î±]
  (Î¼ : measure Î±) : measurable_space Î± :=
{ is_measurable' := is_null_measurable Î¼,
  is_measurable_empty := is_measurable.empty.is_null_measurable _,
  is_measurable_compl := Î» s hs, hs.compl,
  is_measurable_Union := Î» f, is_null_measurable.Union_nat }

/-- Given a measure we can complete it to a (complete) measure on all null measurable sets. -/
def completion {Î± : Type u} [measurable_space Î±] (Î¼ : measure Î±) :
  @measure_theory.measure Î± (null_measurable Î¼) :=
{ to_outer_measure := Î¼.to_outer_measure,
  m_Union := Î» s hs hd, show Î¼ (Union s) = âˆ‘' i, Î¼ (s i), begin
    choose t ht using assume i, is_null_measurable_iff.1 (hs i),
    simp [forall_and_distrib] at ht, rcases ht with âŸ¨st, ht, hzâŸ©,
    rw is_null_measurable_measure_eq (Union_subset_Union st),
    { rw measure_Union _ ht,
      { congr, funext i,
        exact (is_null_measurable_measure_eq (st i) (hz i)).symm },
      { rintro i j ij x âŸ¨hâ‚, hâ‚‚âŸ©,
        exact hd i j ij âŸ¨st i hâ‚, st j hâ‚‚âŸ© } },
    { refine measure_mono_null _ (measure_Union_null hz),
      rw [diff_subset_iff, â† Union_union_distrib],
      exact Union_subset_Union (Î» i, by rw â† diff_subset_iff) }
  end,
  trimmed := begin
    letI := null_measurable Î¼,
    refine le_antisymm (Î» s, _) (outer_measure.le_trim _),
    rw outer_measure.trim_eq_infi,
    dsimp,
    clear _inst,
    resetI,
    rw measure_eq_infi s,
    exact infi_le_infi (Î» t, infi_le_infi $ Î» st,
      infi_le_infi2 $ Î» ht, âŸ¨ht.is_null_measurable _, le_refl _âŸ©)
  end }

instance completion.is_complete {Î± : Type u} [measurable_space Î±] (Î¼ : measure Î±) :
  (completion Î¼).is_complete :=
Î» z hz, null_is_null_measurable hz

end is_complete

namespace measure_theory

/-- A measure space is a measurable space equipped with a
  measure, referred to as `volume`. -/
class measure_space (Î± : Type*) extends measurable_space Î± :=
(volume : measure Î±)

export measure_space (volume)

/-- `volume` is the canonical  measure on `Î±`. -/
add_decl_doc volume

section measure_space
variables {Î± : Type*} {Î¹ : Type*} [measure_space Î±] {sâ‚ sâ‚‚ : set Î±}

notation `âˆ€áµ` binders `, ` r:(scoped P, filter.eventually P (measure.ae volume)) := r

/-- The tactic `exact volume`, to be used in optional (`auto_param`) arguments. -/
meta def volume_tac : tactic unit := `[exact measure_theory.measure_space.volume]

end measure_space

end measure_theory

namespace is_compact

variables {Î± : Type*} [topological_space Î±] [measurable_space Î±] {Î¼ : measure Î±} {s : set Î±}

lemma finite_measure_of_nhds_within (hs : is_compact s) :
  (âˆ€ a âˆˆ s, Î¼.finite_at_filter (ğ“[s] a)) â†’ Î¼ s < âŠ¤ :=
by simpa only [â† measure.compl_mem_cofinite, measure.finite_at_filter]
  using hs.compl_mem_sets_of_nhds_within

lemma finite_measure [locally_finite_measure Î¼] (hs : is_compact s) : Î¼ s < âŠ¤ :=
hs.finite_measure_of_nhds_within $ Î» a ha, Î¼.finite_at_nhds_within _ _

lemma measure_zero_of_nhds_within (hs : is_compact s) :
  (âˆ€ a âˆˆ s, âˆƒ t âˆˆ ğ“[s] a, Î¼ t = 0) â†’ Î¼ s = 0 :=
by simpa only [â† compl_mem_ae_iff] using hs.compl_mem_sets_of_nhds_within

end is_compact

lemma metric.bounded.finite_measure {Î± : Type*} [metric_space Î±] [proper_space Î±]
  [measurable_space Î±] {Î¼ : measure Î±} [locally_finite_measure Î¼] {s : set Î±}
  (hs : metric.bounded s) :
  Î¼ s < âŠ¤ :=
(measure_mono subset_closure).trans_lt (metric.compact_iff_closed_bounded.2
  âŸ¨is_closed_closure, metric.bounded_closure_of_bounded hsâŸ©).finite_measure
