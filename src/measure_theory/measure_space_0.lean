/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import measure_theory.outer_measure
import order.filter.countable_Inter
import data.set.accumulate
import measure_theory.measurable_space_0

/-!
# Measure spaces

Given a measurable space `Î±`, a measure on `Î±` is a function that sends measurable sets to the
extended nonnegative reals that satisfies the following conditions:
1. `Î¼ âˆ… = 0`;
2. `Î¼` is countably additive. This means that the measure of a countable union of pairwise disjoint
   sets is equal to the measure of the individual sets.

Every measure can be canonically extended to an outer measure, so that it assigns values to
all subsets, not just the measurable subsets. On the other hand, a measure that is countably
additive on measurable sets can be restricted to measurable sets to obtain a measure.
In this file a measure is defined to be an outer measure that is countably additive on
measurable sets, with the additional assumption that the outer measure is the canonical
extension of the restricted measure.

Measures on `Î±` form a complete lattice, and are closed under scalar multiplication with `â„â‰¥0âˆ`.

We introduce the following typeclasses for measures:

* `probability_measure Î¼`: `Î¼ univ = 1`;
* `finite_measure Î¼`: `Î¼ univ < âˆ`;
* `sigma_finite Î¼`: there exists a countable collection of measurable sets that cover `univ`
  where `Î¼` is finite;
* `locally_finite_measure Î¼` : `âˆ€ x, âˆƒ s âˆˆ ğ“ x, Î¼ s < âˆ`;
* `has_no_atoms Î¼` : `âˆ€ x, Î¼ {x} = 0`; possibly should be redefined as
  `âˆ€ s, 0 < Î¼ s â†’ âˆƒ t âŠ† s, 0 < Î¼ t âˆ§ Î¼ t < Î¼ s`.

Given a measure, the null sets are the sets where `Î¼ s = 0`, where `Î¼` denotes the corresponding
outer measure (so `s` might not be measurable). We can then define the completion of `Î¼` as the
measure on the least `Ïƒ`-algebra that also contains all null sets, by defining the measure to be `0`
on the null sets.

## Main statements

* `completion` is the completion of a measure to all null measurable sets.
* `measure.of_measurable` and `outer_measure.to_measure` are two important ways to define a measure.

## Implementation notes

Given `Î¼ : measure Î±`, `Î¼ s` is the value of the *outer measure* applied to `s`.
This conveniently allows us to apply the measure to sets without proving that they are measurable.
We get countable subadditivity for all sets, but only countable additivity for measurable sets.

You often don't want to define a measure via its constructor.
Two ways that are sometimes more convenient:
* `measure.of_measurable` is a way to define a measure by only giving its value on measurable sets
  and proving the properties (1) and (2) mentioned above.
* `outer_measure.to_measure` is a way of obtaining a measure from an outer measure by showing that
  all measurable sets in the measurable space are CarathÃ©odory measurable.

To prove that two measures are equal, there are multiple options:
* `ext`: two measures are equal if they are equal on all measurable sets.
* `ext_of_generate_from_of_Union`: two measures are equal if they are equal on a Ï€-system generating
  the measurable sets, if the Ï€-system contains a spanning increasing sequence of sets where the
  measures take finite value (in particular the measures are Ïƒ-finite). This is a special case of
  the more general `ext_of_generate_from_of_cover`
* `ext_of_generate_finite`: two finite measures are equal if they are equal on a Ï€-system
  generating the measurable sets. This is a special case of `ext_of_generate_from_of_Union` using
  `C âˆª {univ}`, but is easier to work with.

A `measure_space` is a class that is a measurable space with a canonical measure.
The measure is denoted `volume`.

## References

* <https://en.wikipedia.org/wiki/Measure_(mathematics)>
* <https://en.wikipedia.org/wiki/Complete_measure>
* <https://en.wikipedia.org/wiki/Almost_everywhere>

## Tags

measure, almost everywhere, measure space, completion, null set, null measurable set
-/

noncomputable theory

open classical set filter (hiding map) function measurable_space
open_locale classical topological_space big_operators filter ennreal nnreal

variables {Î± Î² Î³ Î´ Î¹ : Type*}

namespace measure_theory

/-- A measure is defined to be an outer measure that is countably additive on
measurable sets, with the additional assumption that the outer measure is the canonical
extension of the restricted measure. -/
structure measure (Î± : Type*) [measurable_space Î±] extends outer_measure Î± :=
(m_Union â¦ƒf : â„• â†’ set Î±â¦„ :
  (âˆ€ i, measurable_set (f i)) â†’ pairwise (disjoint on f) â†’
  measure_of (â‹ƒ i, f i) = âˆ‘' i, measure_of (f i))
(trimmed : to_outer_measure.trim = to_outer_measure)

/-- Measure projections for a measure space.

For measurable sets this returns the measure assigned by the `measure_of` field in `measure`.
But we can extend this to _all_ sets, but using the outer measure. This gives us monotonicity and
subadditivity for all sets.
-/
instance measure.has_coe_to_fun [measurable_space Î±] : has_coe_to_fun (measure Î±) :=
âŸ¨Î» _, set Î± â†’ â„â‰¥0âˆ, Î» m, m.to_outer_measureâŸ©

section

variables [measurable_space Î±] {Î¼ Î¼â‚ Î¼â‚‚ : measure Î±} {s sâ‚ sâ‚‚ t : set Î±}

namespace measure

/-! ### General facts about measures -/

/-- Obtain a measure by giving a countably additive function that sends `âˆ…` to `0`. -/
def of_measurable (m : Î  (s : set Î±), measurable_set s â†’ â„â‰¥0âˆ)
  (m0 : m âˆ… measurable_set.empty = 0)
  (mU : âˆ€ {{f : â„• â†’ set Î±}} (h : âˆ€ i, measurable_set (f i)), pairwise (disjoint on f) â†’
    m (â‹ƒ i, f i) (measurable_set.Union h) = âˆ‘' i, m (f i) (h i)) : measure Î± :=
{ m_Union := Î» f hf hd,
  show induced_outer_measure m _ m0 (Union f) =
      âˆ‘' i, induced_outer_measure m _ m0 (f i), begin
    rw [induced_outer_measure_eq m0 mU, mU hf hd],
    congr, funext n, rw induced_outer_measure_eq m0 mU
  end,
  trimmed :=
  show (induced_outer_measure m _ m0).trim = induced_outer_measure m _ m0, begin
    unfold outer_measure.trim,
    congr, funext s hs,
    exact induced_outer_measure_eq m0 mU hs
  end,
  ..induced_outer_measure m _ m0 }

lemma of_measurable_apply {m : Î  (s : set Î±), measurable_set s â†’ â„â‰¥0âˆ}
  {m0 : m âˆ… measurable_set.empty = 0}
  {mU : âˆ€ {{f : â„• â†’ set Î±}} (h : âˆ€ i, measurable_set (f i)), pairwise (disjoint on f) â†’
    m (â‹ƒ i, f i) (measurable_set.Union h) = âˆ‘' i, m (f i) (h i)}
  (s : set Î±) (hs : measurable_set s) : of_measurable m m0 mU s = m s hs :=
induced_outer_measure_eq m0 mU hs

lemma to_outer_measure_injective : injective (to_outer_measure : measure Î± â†’ outer_measure Î±) :=
Î» âŸ¨mâ‚, uâ‚, hâ‚âŸ© âŸ¨mâ‚‚, uâ‚‚, hâ‚‚âŸ© h, by { congr, exact h }

@[ext] lemma ext (h : âˆ€ s, measurable_set s â†’ Î¼â‚ s = Î¼â‚‚ s) : Î¼â‚ = Î¼â‚‚ :=
to_outer_measure_injective $ by rw [â† trimmed, outer_measure.trim_congr h, trimmed]

lemma ext_iff : Î¼â‚ = Î¼â‚‚ â†” âˆ€ s, measurable_set s â†’ Î¼â‚ s = Î¼â‚‚ s :=
âŸ¨by { rintro rfl s hs, refl }, measure.extâŸ©

end measure

@[simp] lemma coe_to_outer_measure : â‡‘Î¼.to_outer_measure = Î¼ := rfl

lemma to_outer_measure_apply (s : set Î±) : Î¼.to_outer_measure s = Î¼ s := rfl

lemma measure_eq_trim (s : set Î±) : Î¼ s = Î¼.to_outer_measure.trim s :=
by rw Î¼.trimmed; refl

lemma measure_eq_infi (s : set Î±) : Î¼ s = â¨… t (st : s âŠ† t) (ht : measurable_set t), Î¼ t :=
by rw [measure_eq_trim, outer_measure.trim_eq_infi]; refl

/-- A variant of `measure_eq_infi` which has a single `infi`. This is useful when applying a
  lemma next that only works for non-empty infima, in which case you can use
  `nonempty_measurable_superset`. -/
lemma measure_eq_infi' (Î¼ : measure Î±) (s : set Î±) :
  Î¼ s = â¨… t : { t // s âŠ† t âˆ§ measurable_set t}, Î¼ t :=
by simp_rw [infi_subtype, infi_and, subtype.coe_mk, â† measure_eq_infi]

lemma measure_eq_induced_outer_measure :
  Î¼ s = induced_outer_measure (Î» s _, Î¼ s) measurable_set.empty Î¼.empty s :=
measure_eq_trim _

lemma to_outer_measure_eq_induced_outer_measure :
  Î¼.to_outer_measure = induced_outer_measure (Î» s _, Î¼ s) measurable_set.empty Î¼.empty :=
Î¼.trimmed.symm

lemma measure_eq_extend (hs : measurable_set s) :
  Î¼ s = extend (Î» t (ht : measurable_set t), Î¼ t) s :=
by { rw [measure_eq_induced_outer_measure, induced_outer_measure_eq_extend _ _ hs],
  exact Î¼.m_Union }

@[simp] lemma measure_empty : Î¼ âˆ… = 0 := Î¼.empty

lemma nonempty_of_measure_ne_zero (h : Î¼ s â‰  0) : s.nonempty :=
ne_empty_iff_nonempty.1 $ Î» h', h $ h'.symm â–¸ measure_empty

lemma measure_mono (h : sâ‚ âŠ† sâ‚‚) : Î¼ sâ‚ â‰¤ Î¼ sâ‚‚ := Î¼.mono h

lemma measure_mono_null (h : sâ‚ âŠ† sâ‚‚) (hâ‚‚ : Î¼ sâ‚‚ = 0) : Î¼ sâ‚ = 0 :=
nonpos_iff_eq_zero.1 $ hâ‚‚ â–¸ measure_mono h

lemma measure_mono_top (h : sâ‚ âŠ† sâ‚‚) (hâ‚ : Î¼ sâ‚ = âˆ) : Î¼ sâ‚‚ = âˆ :=
top_unique $ hâ‚ â–¸ measure_mono h

/-- For every set there exists a measurable superset of the same measure. -/
lemma exists_measurable_superset (Î¼ : measure Î±) (s : set Î±) :
  âˆƒ t, s âŠ† t âˆ§ measurable_set t âˆ§ Î¼ t = Î¼ s :=
by simpa only [â† measure_eq_trim] using Î¼.to_outer_measure.exists_measurable_superset_eq_trim s

/-- For every set `s` and a countable collection of measures `Î¼ i` there exists a measurable
superset `t âŠ‡ s` such that each measure `Î¼ i` takes the same value on `s` and `t`. -/
lemma exists_measurable_superset_forall_eq {Î¹} [encodable Î¹] (Î¼ : Î¹ â†’ measure Î±) (s : set Î±) :
  âˆƒ t, s âŠ† t âˆ§ measurable_set t âˆ§ âˆ€ i, Î¼ i t = Î¼ i s :=
by simpa only [â† measure_eq_trim]
  using outer_measure.exists_measurable_superset_forall_eq_trim (Î» i, (Î¼ i).to_outer_measure) s

/-- A measurable set `t âŠ‡ s` such that `Î¼ t = Î¼ s`. -/
def to_measurable (Î¼ : measure Î±) (s : set Î±) : set Î± :=
classical.some (exists_measurable_superset Î¼ s)

lemma subset_to_measurable (Î¼ : measure Î±) (s : set Î±) : s âŠ† to_measurable Î¼ s :=
(classical.some_spec (exists_measurable_superset Î¼ s)).1

@[simp] lemma measurable_set_to_measurable (Î¼ : measure Î±) (s : set Î±) :
  measurable_set (to_measurable Î¼ s) :=
(classical.some_spec (exists_measurable_superset Î¼ s)).2.1

@[simp] lemma measure_to_measurable (s : set Î±) : Î¼ (to_measurable Î¼ s) = Î¼ s :=
(classical.some_spec (exists_measurable_superset Î¼ s)).2.2

lemma exists_measurable_superset_of_null (h : Î¼ s = 0) :
  âˆƒ t, s âŠ† t âˆ§ measurable_set t âˆ§ Î¼ t = 0 :=
outer_measure.exists_measurable_superset_of_trim_eq_zero (by rw [â† measure_eq_trim, h])

lemma exists_measurable_superset_iff_measure_eq_zero :
  (âˆƒ t, s âŠ† t âˆ§ measurable_set t âˆ§ Î¼ t = 0) â†” Î¼ s = 0 :=
âŸ¨Î» âŸ¨t, hst, _, htâŸ©, measure_mono_null hst ht, exists_measurable_superset_of_nullâŸ©

theorem measure_Union_le [encodable Î²] (s : Î² â†’ set Î±) : Î¼ (â‹ƒ i, s i) â‰¤ âˆ‘' i, Î¼ (s i) :=
Î¼.to_outer_measure.Union _

lemma measure_bUnion_le {s : set Î²} (hs : countable s) (f : Î² â†’ set Î±) :
  Î¼ (â‹ƒ b âˆˆ s, f b) â‰¤ âˆ‘' p : s, Î¼ (f p) :=
begin
  haveI := hs.to_encodable,
  rw [bUnion_eq_Union],
  apply measure_Union_le
end

lemma measure_bUnion_finset_le (s : finset Î²) (f : Î² â†’ set Î±) :
  Î¼ (â‹ƒ b âˆˆ s, f b) â‰¤ âˆ‘ p in s, Î¼ (f p) :=
begin
  rw [â† finset.sum_attach, finset.attach_eq_univ, â† tsum_fintype],
  exact measure_bUnion_le s.countable_to_set f
end

lemma measure_bUnion_lt_top {s : set Î²} {f : Î² â†’ set Î±} (hs : finite s)
  (hfin : âˆ€ i âˆˆ s, Î¼ (f i) < âˆ) : Î¼ (â‹ƒ i âˆˆ s, f i) < âˆ :=
begin
  convert (measure_bUnion_finset_le hs.to_finset f).trans_lt _,
  { ext, rw [finite.mem_to_finset] },
  apply ennreal.sum_lt_top, simpa only [finite.mem_to_finset]
end

lemma measure_Union_null [encodable Î²] {s : Î² â†’ set Î±} :
  (âˆ€ i, Î¼ (s i) = 0) â†’ Î¼ (â‹ƒ i, s i) = 0 :=
Î¼.to_outer_measure.Union_null

lemma measure_Union_null_iff [encodable Î¹] {s : Î¹ â†’ set Î±} :
  Î¼ (â‹ƒ i, s i) = 0 â†” âˆ€ i, Î¼ (s i) = 0 :=
âŸ¨Î» h i, measure_mono_null (subset_Union _ _) h, measure_Union_nullâŸ©

lemma measure_bUnion_null_iff {s : set Î¹} (hs : countable s) {t : Î¹ â†’ set Î±} :
  Î¼ (â‹ƒ i âˆˆ s, t i) = 0 â†” âˆ€ i âˆˆ s, Î¼ (t i) = 0 :=
by { haveI := hs.to_encodable, rw [â† Union_subtype, measure_Union_null_iff, set_coe.forall], refl }

theorem measure_union_le (sâ‚ sâ‚‚ : set Î±) : Î¼ (sâ‚ âˆª sâ‚‚) â‰¤ Î¼ sâ‚ + Î¼ sâ‚‚ :=
Î¼.to_outer_measure.union _ _

lemma measure_union_null : Î¼ sâ‚ = 0 â†’ Î¼ sâ‚‚ = 0 â†’ Î¼ (sâ‚ âˆª sâ‚‚) = 0 :=
Î¼.to_outer_measure.union_null

lemma measure_union_null_iff : Î¼ (sâ‚ âˆª sâ‚‚) = 0 â†” Î¼ sâ‚ = 0 âˆ§ Î¼ sâ‚‚ = 0:=
âŸ¨Î» h, âŸ¨measure_mono_null (subset_union_left _ _) h, measure_mono_null (subset_union_right _ _) hâŸ©,
  Î» h, measure_union_null h.1 h.2âŸ©

/-! ### The almost everywhere filter -/

/-- The â€œalmost everywhereâ€ filter of co-null sets. -/
def measure.ae (Î¼ : measure Î±) : filter Î± :=
{ sets := {s | Î¼ sá¶œ = 0},
  univ_sets := by simp,
  inter_sets := Î» s t hs ht, by simp only [compl_inter, mem_set_of_eq];
    exact measure_union_null hs ht,
  sets_of_superset := Î» s t hs hst, measure_mono_null (set.compl_subset_compl.2 hst) hs }

notation `âˆ€áµ` binders ` âˆ‚` Î¼ `, ` r:(scoped P, filter.eventually P (measure.ae Î¼)) := r
notation `âˆƒáµ` binders ` âˆ‚` Î¼ `, ` r:(scoped P, filter.frequently P (measure.ae Î¼)) := r
notation f ` =áµ[`:50 Î¼:50 `] `:0 g:50 := f =á¶ [measure.ae Î¼] g
notation f ` â‰¤áµ[`:50 Î¼:50 `] `:0 g:50 := f â‰¤á¶ [measure.ae Î¼] g

lemma mem_ae_iff {s : set Î±} : s âˆˆ Î¼.ae â†” Î¼ sá¶œ = 0 := iff.rfl

lemma ae_iff {p : Î± â†’ Prop} : (âˆ€áµ a âˆ‚ Î¼, p a) â†” Î¼ { a | Â¬ p a } = 0 := iff.rfl

lemma compl_mem_ae_iff {s : set Î±} : sá¶œ âˆˆ Î¼.ae â†” Î¼ s = 0 := by simp only [mem_ae_iff, compl_compl]

lemma frequently_ae_iff {p : Î± â†’ Prop} : (âˆƒáµ a âˆ‚Î¼, p a) â†” Î¼ {a | p a} â‰  0 :=
not_congr compl_mem_ae_iff

lemma frequently_ae_mem_iff {s : set Î±} : (âˆƒáµ a âˆ‚Î¼, a âˆˆ s) â†” Î¼ s â‰  0 :=
not_congr compl_mem_ae_iff

lemma measure_zero_iff_ae_nmem {s : set Î±} : Î¼ s = 0 â†” âˆ€áµ a âˆ‚ Î¼, a âˆ‰ s :=
compl_mem_ae_iff.symm

lemma ae_of_all {p : Î± â†’ Prop} (Î¼ : measure Î±) : (âˆ€ a, p a) â†’ âˆ€áµ a âˆ‚ Î¼, p a :=
eventually_of_forall

--instance ae_is_measurably_generated : is_measurably_generated Î¼.ae :=
--âŸ¨Î» s hs, let âŸ¨t, hst, htm, htÎ¼âŸ© := exists_measurable_superset_of_null hs in
--  âŸ¨tá¶œ, compl_mem_ae_iff.2 htÎ¼, htm.compl, compl_subset_comm.1 hstâŸ©âŸ©

instance : countable_Inter_filter Î¼.ae :=
âŸ¨begin
  intros S hSc hS,
  simp only [mem_ae_iff, compl_sInter, sUnion_image, bUnion_eq_Union] at hS âŠ¢,
  haveI := hSc.to_encodable,
  exact measure_Union_null (subtype.forall.2 hS)
endâŸ©

lemma ae_imp_iff {p : Î± â†’ Prop} {q : Prop} : (âˆ€áµ x âˆ‚Î¼, q â†’ p x) â†” (q â†’ âˆ€áµ x âˆ‚Î¼, p x) :=
filter.eventually_imp_distrib_left

lemma ae_all_iff [encodable Î¹] {p : Î± â†’ Î¹ â†’ Prop} :
  (âˆ€áµ a âˆ‚ Î¼, âˆ€ i, p a i) â†” (âˆ€ i, âˆ€áµ a âˆ‚ Î¼, p a i) :=
eventually_countable_forall

lemma ae_ball_iff {S : set Î¹} (hS : countable S) {p : Î  (x : Î±) (i âˆˆ S), Prop} :
  (âˆ€áµ x âˆ‚ Î¼, âˆ€ i âˆˆ S, p x i â€¹_â€º) â†” âˆ€ i âˆˆ S, âˆ€áµ x âˆ‚ Î¼, p x i â€¹_â€º :=
eventually_countable_ball hS

lemma ae_eq_refl (f : Î± â†’ Î´) : f =áµ[Î¼] f := eventually_eq.rfl

lemma ae_eq_symm {f g : Î± â†’ Î´} (h : f =áµ[Î¼] g) : g =áµ[Î¼] f :=
h.symm

lemma ae_eq_trans {f g h: Î± â†’ Î´} (hâ‚ : f =áµ[Î¼] g) (hâ‚‚ : g =áµ[Î¼] h) :
  f =áµ[Î¼] h :=
hâ‚.trans hâ‚‚

@[simp] lemma ae_eq_empty : s =áµ[Î¼] (âˆ… : set Î±) â†” Î¼ s = 0 :=
eventually_eq_empty.trans $ by simp [ae_iff]

lemma ae_le_set : s â‰¤áµ[Î¼] t â†” Î¼ (s \ t) = 0 :=
calc s â‰¤áµ[Î¼] t â†” âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ x âˆˆ t : iff.rfl
           ... â†” Î¼ (s \ t) = 0          : by simp [ae_iff]; refl

@[simp] lemma union_ae_eq_right : (s âˆª t : set Î±) =áµ[Î¼] t â†” Î¼ (s \ t) = 0 :=
by simp [eventually_le_antisymm_iff, ae_le_set, union_diff_right,
  diff_eq_empty.2 (set.subset_union_right _ _)]

lemma diff_ae_eq_self : (s \ t : set Î±) =áµ[Î¼] s â†” Î¼ (s âˆ© t) = 0 :=
by simp [eventually_le_antisymm_iff, ae_le_set, diff_diff_right,
  diff_diff, diff_eq_empty.2 (set.subset_union_right _ _)]

lemma ae_eq_set {s t : set Î±} :
  s =áµ[Î¼] t â†” Î¼ (s \ t) = 0 âˆ§ Î¼ (t \ s) = 0 :=
by simp [eventually_le_antisymm_iff, ae_le_set]

/-- If `s âŠ† t` modulo a set of measure `0`, then `Î¼ s â‰¤ Î¼ t`. -/
@[mono] lemma measure_mono_ae (H : s â‰¤áµ[Î¼] t) : Î¼ s â‰¤ Î¼ t :=
calc Î¼ s â‰¤ Î¼ (s âˆª t)       : measure_mono $ subset_union_left s t
     ... = Î¼ (t âˆª s \ t)   : by rw [union_diff_self, set.union_comm]
     ... â‰¤ Î¼ t + Î¼ (s \ t) : measure_union_le _ _
     ... = Î¼ t             : by rw [ae_le_set.1 H, add_zero]

alias measure_mono_ae â† filter.eventually_le.measure_le

/-- If two sets are equal modulo a set of measure zero, then `Î¼ s = Î¼ t`. -/
lemma measure_congr (H : s =áµ[Î¼] t) : Î¼ s = Î¼ t :=
le_antisymm H.le.measure_le H.symm.le.measure_le

/-- A measure space is a measurable space equipped with a
  measure, referred to as `volume`. -/
class measure_space (Î± : Type*) extends measurable_space Î± :=
(volume : measure Î±)

export measure_space (volume)

/-- `volume` is the canonical  measure on `Î±`. -/
add_decl_doc volume

section measure_space

notation `âˆ€áµ` binders `, ` r:(scoped P, filter.eventually P
  (measure_theory.measure.ae measure_theory.measure_space.volume)) := r

notation `âˆƒáµ` binders `, ` r:(scoped P, filter.frequently P
  (measure_theory.measure.ae measure_theory.measure_space.volume)) := r

/-- The tactic `exact volume`, to be used in optional (`auto_param`) arguments. -/
meta def volume_tac : tactic unit := `[exact measure_theory.measure_space.volume]

end measure_space

end

end measure_theory


section
open measure_theory

/-!
# Almost everywhere measurable functions

A function is almost everywhere measurable if it coincides almost everywhere with a measurable
function. We define this property, called `ae_measurable f Î¼`. It's properties are discussed in
`measure_theory.measure_space`.
-/

variables [measurable_space Î±] [measurable_space Î²]
  {f g : Î± â†’ Î²} {Î¼ Î½ : measure Î±}

/-- A function is almost everywhere measurable if it coincides almost everywhere with a measurable
function. -/
def ae_measurable (f : Î± â†’ Î²) (Î¼ : measure Î± . measure_theory.volume_tac) : Prop :=
âˆƒ g : Î± â†’ Î², measurable g âˆ§ f =áµ[Î¼] g

lemma measurable.ae_measurable (h : measurable f) : ae_measurable f Î¼ :=
âŸ¨f, h, ae_eq_refl fâŸ©


namespace ae_measurable

/-- Given an almost everywhere measurable function `f`, associate to it a measurable function
that coincides with it almost everywhere. `f` is explicit in the definition to make sure that
it shows in pretty-printing. -/
def mk (f : Î± â†’ Î²) (h : ae_measurable f Î¼) : Î± â†’ Î² := classical.some h

lemma measurable_mk (h : ae_measurable f Î¼) : measurable (h.mk f) :=
(classical.some_spec h).1

lemma ae_eq_mk (h : ae_measurable f Î¼) : f =áµ[Î¼] (h.mk f) :=
(classical.some_spec h).2

lemma congr (hf : ae_measurable f Î¼) (h : f =áµ[Î¼] g) : ae_measurable g Î¼ :=
âŸ¨hf.mk f, hf.measurable_mk, h.symm.trans hf.ae_eq_mkâŸ©

end ae_measurable

lemma ae_measurable_congr (h : f =áµ[Î¼] g) :
  ae_measurable f Î¼ â†” ae_measurable g Î¼ :=
âŸ¨Î» hf, ae_measurable.congr hf h, Î» hg, ae_measurable.congr hg h.symmâŸ©

@[simp] lemma ae_measurable_const {b : Î²} : ae_measurable (Î» a : Î±, b) Î¼ :=
measurable_const.ae_measurable

lemma measurable.comp_ae_measurable [measurable_space Î´] {f : Î± â†’ Î´} {g : Î´ â†’ Î²}
  (hg : measurable g) (hf : ae_measurable f Î¼) : ae_measurable (g âˆ˜ f) Î¼ :=
âŸ¨g âˆ˜ hf.mk f, hg.comp hf.measurable_mk, eventually_eq.fun_comp hf.ae_eq_mk _âŸ©


end
