import topology.metric_space.metric_separated
import measure_theory.borel_space

open_locale nnreal ennreal topological_space big_operators

open emetric set function filter

noncomputable theory

variables {Î¹ X Y : Type*} [emetric_space X] [emetric_space Y]

namespace measure_theory

namespace outer_measure

def is_metric (m : outer_measure X) : Prop :=
âˆ€ (s t : set X), is_metric_separated s t â†’ m (s âˆª t) = m s + m t

namespace is_metric

variables {m : outer_measure X}

lemma finset_Union_of_pairwise_separated (hm : is_metric m) {I : finset Î¹} {s : Î¹ â†’ set X}
  (hI : âˆ€ (i âˆˆ I) (j âˆˆ I), i â‰  j â†’ is_metric_separated (s i) (s j)) :
  m (â‹ƒ i âˆˆ I, s i) = âˆ‘ i in I, m (s i) :=
begin
  classical,
  induction I using finset.induction_on with i I hiI ihI hI, { simp },
  simp only [finset.mem_insert] at hI,
  rw [finset.set_bUnion_insert, hm, ihI, finset.sum_insert hiI],
  exacts [Î» i hi j hj hij, (hI i (or.inr hi) j (or.inr hj) hij),
    is_metric_separated.finset_Union_right
      (Î» j hj, hI i (or.inl rfl) j (or.inr hj) (ne_of_mem_of_not_mem hj hiI).symm)]
end

/-- If `m` is a metric outer measure, then every Borel measurable set `t` is Caratheodory
measurable: for any (not necessarily measurable) set `s` we have `m (s âˆ© t) + m (s \ t) = m s`. -/
lemma borel_le_caratheodory {m : outer_measure X} (hm : is_metric m) :
  borel X â‰¤ m.caratheodory :=
begin
  rw [borel_eq_generate_from_is_closed],
  refine measurable_space.generate_from_le (Î» t ht, m.is_caratheodory_iff_le.2 $ Î» s, _),
  set S : â„• â†’ set X := Î» n, {x âˆˆ s | (â†‘n)â»Â¹ â‰¤ inf_edist x t},
  have n0 : âˆ€ {n : â„•}, (nâ»Â¹ : â„â‰¥0âˆ) â‰  0, from Î» n, ennreal.inv_ne_zero.2 ennreal.coe_nat_ne_top,
  have Ssep : âˆ€ n, is_metric_separated (S n) t,
    from Î» n, âŸ¨nâ»Â¹, n0, Î» x hx y hy, hx.2.trans $ inf_edist_le_edist_of_mem hyâŸ©,
  have Ssep' : âˆ€ n, is_metric_separated (S n) (s âˆ© t),
    from Î» n, (Ssep n).mono subset.rfl (inter_subset_right _ _),
  have S_sub : âˆ€ n, S n âŠ† s \ t,
    from Î» n, subset_inter (inter_subset_left _ _) (Ssep n).subset_compl_right,
  have hSs : âˆ€ n, m (s âˆ© t) + m (S n) â‰¤ m s, from Î» n,
  calc m (s âˆ© t) + m (S n) = m (s âˆ© t âˆª S n) :
    eq.symm $ hm _ _ $ (Ssep' n).symm
  ... â‰¤ m (s âˆ© t âˆª s \ t)  : by { mono*, exact le_rfl }
  ... = m s : by rw [inter_union_diff],
  have Union_S : (â‹ƒ n, S n) = s \ t,
  { refine subset.antisymm (Union_subset S_sub) _,
    rintro x âŸ¨hxs, hxtâŸ©,
    rw mem_iff_ind_edist_zero_of_closed ht at hxt,
    rcases ennreal.exists_inv_nat_lt hxt with âŸ¨n, hnâŸ©,
    exact mem_Union.2 âŸ¨n, hxs, hn.leâŸ© },
  /- Now we have `âˆ€ n, m (s âˆ© t) + m (S n) â‰¤ m s` and we need to prove
  `m (s âˆ© t) + m (â‹ƒ n, S n) â‰¤ m s`. We can't pass to the limit because
  `m` is only an outer measure. -/
  by_cases htop : m (s \ t) = âˆ,
  { rw [htop, ennreal.add_top, â† htop],
    exact m.mono (diff_subset _ _) },
  suffices : m (â‹ƒ n, S n) â‰¤ â¨† n, m (S n),
  calc m (s âˆ© t) + m (s \ t) = m (s âˆ© t) + m (â‹ƒ n, S n) :
    by rw Union_S
  ... â‰¤ m (s âˆ© t) + â¨† n, m (S n) :
    add_le_add le_rfl this
  ... = â¨† n, m (s âˆ© t) + m (S n) : ennreal.add_supr
  ... â‰¤ m s : supr_le hSs,
  /- It suffices to show that `âˆ‘' k, m (S (k + 1) \ S k) â‰  âˆ`. Indeed, if we have this,
  then for all `N` we have `m (â‹ƒ n, S n) â‰¤ m (S N) + âˆ‘' k, m (S (N + k + 1) \ S (N + k))`
  and the second term tends to zero, see `outer_measure.Union_nat_of_monotone_of_tsum_ne_top`
  for details. -/
  refine (m.Union_nat_of_monotone_of_tsum_ne_top _ _).le,
  { exact Î» n x hx, âŸ¨hx.1, le_trans (ennreal.inv_le_inv.2 $
      ennreal.coe_nat_le_coe_nat.2 n.le_succ) hx.2âŸ© },
  /- While the sets `S (k + 1) \ S k` are not pairwise metric separated, the sets in each
  subsequence `S (2 * k + 1) \ S (2 * k)` and `S (2 * k + 2) \ S (2 * k)` are metric separated,
  so `m` is additive on each of those sequences. -/
  { rw [â† tsum_even_add_odd ennreal.summable ennreal.summable, ennreal.add_ne_top],
    suffices : âˆ€ a, (âˆ‘' (k : â„•), m (S (2 * k + 1 + a) \ S (2 * k + a))) â‰  âˆ,
      from âŸ¨by simpa using this 0, by simpa using this 1âŸ©,
    refine Î» r, ne_top_of_le_ne_top htop _,
    rw [â† Union_S, ennreal.tsum_eq_supr_nat, supr_le_iff],
    intro n,
    rw [â† hm.finset_Union_of_pairwise_separated],
    { exact m.mono (Union_subset $ Î» i, Union_subset $ Î» hi x hx, mem_Union.2 âŸ¨_, hx.1âŸ©) },
    suffices : âˆ€ i  j, i < j â†’ is_metric_separated (S (2 * i + 1 + r)) (s \ S (2 * j + r)),
      from Î» i _ j _ hij, hij.lt_or_lt.elim
        (Î» h, (this i j h).mono (inter_subset_left _ _) (Î» x hx, âŸ¨hx.1.1, hx.2âŸ©))
        (Î» h, (this j i h).symm.mono  (Î» x hx, âŸ¨hx.1.1, hx.2âŸ©) (inter_subset_left _ _)),
    intros i j hj,
    have A : ((â†‘(2 * j + r))â»Â¹ : â„â‰¥0âˆ) < (â†‘(2 * i + 1 + r))â»Â¹,
      by { rw [ennreal.inv_lt_inv, ennreal.coe_nat_lt_coe_nat], linarith },
    refine âŸ¨(â†‘(2 * i + 1 + r))â»Â¹ - (â†‘(2 * j + r))â»Â¹, by simpa using A, Î» x hx y hy, _âŸ©,
    have : inf_edist y t < (â†‘(2 * j + r))â»Â¹, from not_le.1 (Î» hle, hy.2 âŸ¨hy.1, hleâŸ©),
    rcases exists_edist_lt_of_inf_edist_lt this with âŸ¨z, hzt, hyzâŸ©,
    have hxz : (â†‘(2 * i + 1 + r))â»Â¹ â‰¤ edist x z, from le_inf_edist.1 hx.2 _ hzt,
    apply ennreal.le_of_add_le_add_right (hyz.trans_le le_top),
    refine le_trans _ (edist_triangle _ _ _),
    refine (add_le_add le_rfl hyz.le).trans (eq.trans_le _ hxz),
    rw [ennreal.sub_add_cancel_of_le A.le] }
end

lemma le_caratheodory [measurable_space X] [borel_space X]
  {m : outer_measure X} (hm : is_metric m) :
  â€¹_â€º â‰¤ m.caratheodory :=
by { rw @borel_space.measurable_eq X _ _, exact hm.borel_le_caratheodory }

end is_metric

/-- Auxiliary definition for `outer_measure.mk_metric'`: given a function on sets
`m : set X â†’ â„â‰¥0âˆ`, returns the maximal outer measure `Î¼` such that `Î¼ s â‰¤ m s`
for any set `s` of diamenter at most `r`.-/
def mk_metric'.pre (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) (r : â„â‰¥0âˆ) :
  outer_measure X :=
induced_outer_measure (Î» s (hs : diam s â‰¤ r), m s) (by { rw diam_empty, exact zero_le r }) hm

def mk_metric' (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) :
  outer_measure X :=
â¨† r > 0, mk_metric'.pre m hm r

def mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (hm : m 0 = 0) : outer_measure X :=
mk_metric' (Î» s, m (diam s)) $ by rwa diam_empty

namespace mk_metric'

variables {m : set X â†’ â„â‰¥0âˆ} {hm : m âˆ… = 0} {r : â„â‰¥0âˆ} {Î¼ : outer_measure X} {s : set X}

lemma le_pre : Î¼ â‰¤ pre m hm r â†” âˆ€ s, diam s â‰¤ r â†’ Î¼ s â‰¤ m s :=
by simp only [pre, le_induced_outer_measure]

lemma pre_le (hs : diam s â‰¤ r) : pre m hm r s â‰¤ m s :=
(of_function_le _).trans $ infi_le _ hs

lemma mono_pre (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) {r r' : â„â‰¥0âˆ} (h : r â‰¤ r') :
  pre m hm r' â‰¤ pre m hm r :=
le_pre.2 $ Î» s hs, pre_le (hs.trans h)

lemma mono_pre_nat (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) :
  monotone (Î» k : â„•, pre m hm kâ»Â¹) :=
Î» k l h, le_pre.2 $ Î» s hs, pre_le (hs.trans $ by simpa)

lemma tendsto_pre (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) (s : set X) :
  tendsto (Î» r, pre m hm r s) (ğ“[Ioi 0] 0) (ğ“ $ mk_metric' m hm s) :=
begin
  rw [â† map_coe_Ioi_at_bot, tendsto_map'_iff],
  simp only [mk_metric', outer_measure.supr_apply, supr_subtype'],
  exact tendsto_at_bot_supr (Î» r r' hr, _)
end

lemma tendsto_pre_nat (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) (s : set X) :
  tendsto (Î» n : â„•, pre m hm nâ»Â¹ s) at_top (ğ“ $ mk_metric' m hm s) :=
begin
  refine (tendsto_pre m hm s).comp (tendsto_inf.2 âŸ¨ennreal.tendsto_inv_nat_nhds_zero, _âŸ©),
  refine tendsto_principal.2 (eventually_of_forall $ Î» n, _),
  simp
end

lemma eq_supr_nat (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) :
  mk_metric' m hm = â¨† n : â„•, mk_metric'.pre m hm nâ»Â¹ :=
begin
  ext1 s,
  rw supr_apply,
  refine tendsto_nhds_unique (mk_metric'.tendsto_pre_nat m hm s)
    (tendsto_at_top_supr $ Î» k l hkl, mk_metric'.mono_pre_nat m hm hkl s)
end

lemma trim_pre [measurable_space X] [opens_measurable_space X]
  (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) {hm : m (diam (âˆ… : set X)) = 0} (r : â„â‰¥0âˆ) :
  (pre (Î» s : set X, m (diam s)) hm r).trim =
    pre (Î» s, m (diam s)) hm r :=
begin
  refine le_antisymm (le_pre.2 $ Î» s hs, _) (le_trim _),
  rw trim_eq_infi,
  refine (infi_le_of_le (closure s) $ infi_le_of_le subset_closure $
    infi_le_of_le measurable_set_closure ((pre_le _).trans_eq _)),
  { rwa diam_closure },
  { rw diam_closure }
end

end mk_metric'

/-- An outer measure constructed using `outer_measure.mk_metric'` is a metric outer measure. -/
lemma mk_metric'_is_metric (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) :
  (mk_metric' m hm).is_metric :=
begin
  rintros s t âŸ¨r, r0, hrâŸ©,
  refine tendsto_nhds_unique_of_eventually_eq
    (mk_metric'.tendsto_pre _ _ _)
    ((mk_metric'.tendsto_pre _ _ _).add (mk_metric'.tendsto_pre _ _ _)) _,
  rw [â† pos_iff_ne_zero] at r0,
  filter_upwards [Ioo_mem_nhds_within_Ioi âŸ¨le_rfl, r0âŸ©],
  rintro Îµ âŸ¨Îµ0, ÎµrâŸ©,
  refine of_function_union_of_separated _,
  rintro u âŸ¨x, hxs, hxuâŸ© âŸ¨y, hyt, hyuâŸ©,
  have : Îµ < diam u, from Îµr.trans_le ((hr x hxs y hyt).trans $ edist_le_diam_of_mem hxu hyu),
  exact infi_eq_top.2 (Î» h, (this.not_le h).elim)
end

lemma mk_metric_mono_smul {mâ‚ mâ‚‚ : â„â‰¥0âˆ â†’ â„â‰¥0âˆ} (hmâ‚ : mâ‚ 0 = 0) (hmâ‚‚ : mâ‚‚ 0 = 0)
  {c : â„â‰¥0âˆ} (hc : c â‰  âŠ¤) (h0 : c â‰  0) (hle : mâ‚ â‰¤á¶ [ğ“[Ioi 0] 0] c â€¢ mâ‚‚) :
  (mk_metric mâ‚ hmâ‚ : outer_measure X) â‰¤ c â€¢ mk_metric mâ‚‚ hmâ‚‚ :=
begin
  rcases (mem_nhds_within_Ioi_iff_exists_Ioo_subset' ennreal.zero_lt_one).1 hle with âŸ¨r, hr0, hrâŸ©,
  replace hr : âˆ€ r', r' < r â†’ mâ‚ r' â‰¤ c â€¢ mâ‚‚ r',
    from Î» r' hr', (zero_le r').eq_or_lt.elim (Î» h, h â–¸ by simp [hmâ‚, hmâ‚‚]) (Î» h, hr âŸ¨h, hr'âŸ©),
  refine Î» s, le_of_tendsto_of_tendsto (mk_metric'.tendsto_pre _ _ s)
    (ennreal.tendsto.const_mul (mk_metric'.tendsto_pre _ _ s) (or.inr hc))
    (mem_sets_of_superset (Ioo_mem_nhds_within_Ioi âŸ¨le_rfl, hr0âŸ©) (Î» r' hr', _)),
  simp only [mem_set_of_eq, mk_metric'.pre, induced_outer_measure],
  rw [â† smul_apply, smul_of_function hc],
  refine le_of_function.2 (Î» t, (of_function_le _).trans _) _,
  simp only [smul_eq_mul, pi.smul_apply, extend, infi_eq_if],
  split_ifs with ht ht,
  { exact hr _ (ht.trans_lt hr'.2) },
  { simp [h0] }
end

lemma mk_metric_mono {mâ‚ mâ‚‚ : â„â‰¥0âˆ â†’ â„â‰¥0âˆ} (hmâ‚ : mâ‚ 0 = 0) (hmâ‚‚ : mâ‚‚ 0 = 0)
  (hle : mâ‚ â‰¤á¶ [ğ“[Ioi 0] 0] mâ‚‚) :
  (mk_metric mâ‚ hmâ‚ : outer_measure X) â‰¤ mk_metric mâ‚‚ hmâ‚‚ :=
by { convert mk_metric_mono_smul hmâ‚ hmâ‚‚ ennreal.one_ne_top ennreal.zero_lt_one.ne' _; simp * }

lemma isometry_comap_mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (hm : m 0 = 0)
  {f : X â†’ Y} (hf : isometry f) (H : monotone m âˆ¨ surjective f) :
  comap f (mk_metric m hm) = mk_metric m hm :=
begin
  simp only [mk_metric, mk_metric', mk_metric'.pre, induced_outer_measure, comap_supr],
  refine supr_congr id surjective_id (Î» Îµ, supr_congr id surjective_id $ Î» hÎµ, _),
  rw comap_of_function _ (H.imp (Î» h_mono, _) id),
  { congr' with s : 1,
    apply extend_congr; try { intros }; simp only [hf.ediam_image, id] },
  { exact Î» s t hst, infi_le_infi2 (Î» ht, âŸ¨(diam_mono hst).trans ht, h_mono (diam_mono hst)âŸ©) }
end

lemma isometry_map_mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (hm : m 0 = 0)
  {f : X â†’ Y} (hf : isometry f) (H : monotone m âˆ¨ surjective f) :
  map f (mk_metric m hm) = restrict (range f) (mk_metric m hm) :=
by rw [â† isometry_comap_mk_metric _ _ hf H, map_comap]

lemma isometric_comap_mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (hm : m 0 = 0) (f : X â‰ƒáµ¢ Y) :
  comap f (mk_metric m hm) = mk_metric m hm :=
isometry_comap_mk_metric _ _ f.isometry (or.inr f.surjective)

lemma isometric_map_mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (hm : m 0 = 0) (f : X â‰ƒáµ¢ Y) :
  map f (mk_metric m hm) = mk_metric m hm :=
by rw [â† isometric_comap_mk_metric _ _ f, map_comap_of_surjective f.surjective]

lemma trim_mk_metric [measurable_space X] [borel_space X] (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (hm : m 0 = 0) :
  (mk_metric m hm : outer_measure X).trim = mk_metric m hm :=
by simp only [mk_metric, mk_metric'.eq_supr_nat, trim_supr, mk_metric'.trim_pre]

end outer_measure

namespace measure

variables [measurable_space X] [borel_space X]

def mk_metric' (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) : measure X :=
(outer_measure.mk_metric' m hm).to_measure (outer_measure.mk_metric'_is_metric _ _).le_caratheodory

def mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (hm : m 0 = 0) : measure X :=
mk_metric' (Î» s, m (diam s)) (by rwa diam_empty)

@[simp] lemma mk_metric'_to_outer_measure (m : set X â†’ â„â‰¥0âˆ) (hm : m âˆ… = 0) :
  (mk_metric' m hm).to_outer_measure = (outer_measure.mk_metric' m hm).trim :=
rfl

@[simp] lemma mk_metric_to_outer_measure (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (hm : m 0 = 0) :
  (mk_metric m hm : measure X).to_outer_measure = outer_measure.mk_metric m hm :=
outer_measure.trim_mk_metric m hm

end measure

end measure_theory
