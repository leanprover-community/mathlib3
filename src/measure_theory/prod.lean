/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import measure_theory.giry_monad
import measure_theory.set_integral

/-!
# The product measure

In this file we define and prove properties about the binary product measure. If `Î±` and `Î²` have
Ïƒ-finite measures `Î¼` resp. `Î½` then `Î± Ã— Î²` can be equipped with a Ïƒ-finite measure `Î¼.prod Î½` that
satisfies `(Î¼.prod Î½) s = âˆ«â» x, Î½ {y | (x, y) âˆˆ s} âˆ‚Î¼`.
We also have `(Î¼.prod Î½) (s.prod t) = Î¼ s * Î½ t`, i.e. the measure of a rectangle is the product of
the measures of the sides.

We also prove Tonelli's theorem and Fubini's theorem.

## Main definition

* `measure_theory.measure.prod`: The product of two measures.

## Main results

* `measure_theory.measure.prod_apply` states `Î¼.prod Î½ s = âˆ«â» x, Î½ {y | (x, y) âˆˆ s} âˆ‚Î¼`
  for measurable `s`. `measure_theory.measure.prod_apply_symm` is the reversed version.
* `measure_theory.measure.prod_prod` states `Î¼.prod Î½ (s.prod t) = Î¼ s * Î½ t` for measurable sets
  `s` and `t`.
* `measure_theory.lintegral_prod`: Tonelli's theorem. It states that for a measurable function
  `Î± Ã— Î² â†’ ennreal` we have `âˆ«â» z, f z âˆ‚(Î¼.prod Î½) = âˆ«â» x, âˆ«â» y, f (x, y) âˆ‚Î½ âˆ‚Î¼`. The version
  for functions `Î± â†’ Î² â†’ ennreal` is reversed, and called `lintegral_lintegral`. Both versions have
  a variant with `_symm` appended, where the order of integration is reversed.
  The lemma `measurable.lintegral_prod_right'` states that the inner integral of the right-hand side
  is measurable.
* `measure_theory.integrable_prod_iff` states that a binary function is integrable iff both
  * `y â†¦ f (x, y)` is integrable for almost every `x`, and
  * the function `x â†¦ âˆ« âˆ¥f (x, y)âˆ¥ dy` is integrable.
* `measure_theory.integral_prod`: Fubini's theorem. It states that for a integrable function
  `Î± Ã— Î² â†’ E` (where `E` is a second countable Banach space) we have
  `âˆ« z, f z âˆ‚(Î¼.prod Î½) = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼`. This theorem has the same variants as
  Tonelli's theorem. The lemma `measure_theory.integrable.integral_prod_right` states that the
  inner integral of the right-hand side is integrable.

## Implementation Notes

Many results are proven twice, once for functions in curried form (`Î± â†’ Î² â†’ Î³`) and one for
functions in uncurried form (`Î± Ã— Î² â†’ Î³`). The former often has an assumption
`measurable (uncurry f)`, which could be inconvenient to discharge, but for the latter it is more
common that the function has to be given explicitly, since Lean cannot synthesize the function by
itself. We name the lemmas about the uncurried form with a prime.
Tonelli's theorem and Fubini's theorem have a different naming scheme, since the version for the
uncurried version is reversed.

## Tags

product measure, Fubini's theorem, Tonelli's theorem, Fubini-Tonelli theorem
-/

noncomputable theory
open_locale classical topological_space
open set function real ennreal
open measure_theory measurable_space measure_theory.measure
open topological_space (hiding generate_from)
open filter (hiding prod_eq map)

variables {Î± Î² E : Type*} [measurable_space Î±] [measurable_space Î²]
variables {Î¼ : measure Î±} {Î½ : measure Î²}
variables [normed_group E] [measurable_space E]

/-! ### Measurability

Before we define the product measure, we can talk about the measurability of operations on binary
functions. We show that if `f` is a binary measurable function, then the function that integrates
along one of the variables (using either the Lebesgue or Bochner integral) is measurable.
-/

/-- The product Ïƒ-algebra is generated from boxes, i.e. `s.prod t` for sets `s : set Î±` and
  `t : set Î²`. -/
lemma generate_from_prod :
  generate_from (image2 set.prod { s : set Î± | is_measurable s } { t : set Î² | is_measurable t }) =
  prod.measurable_space :=
begin
  apply le_antisymm,
  { apply generate_from_le, rintro _ âŸ¨s, t, hs, ht, rflâŸ©, rw [prod_eq],
    exact (measurable_fst hs).inter (measurable_snd ht) },
  { refine sup_le _ _; rintro _ âŸ¨s, hs, rflâŸ©; apply is_measurable_generate_from,
    exact âŸ¨s, univ, hs, is_measurable.univ, prod_univâŸ©,
    exact âŸ¨univ, s, is_measurable.univ, hs, univ_prodâŸ© }
end

/-- Boxes form a Ï€-system. -/
lemma is_pi_system_prod :
  is_pi_system (image2 set.prod { s : set Î± | is_measurable s } { t : set Î² | is_measurable t }) :=
by { rintro _ _ âŸ¨sâ‚, tâ‚, hsâ‚, htâ‚, rflâŸ© âŸ¨sâ‚‚, tâ‚‚, hsâ‚‚, htâ‚‚, rflâŸ© _, rw [prod_inter_prod],
     exact mem_image2_of_mem (hsâ‚.inter hsâ‚‚) (htâ‚.inter htâ‚‚) }

/-- If `Î½` is a finite measure, and `s âŠ† Î± Ã— Î²` is measurable, then `x â†¦ Î½ { y | (x, y) âˆˆ s }` is
  a measurable function. `measurable_measure_prod_mk_left` is strictly more general. -/
lemma measurable_measure_prod_mk_left_finite [finite_measure Î½] {s : set (Î± Ã— Î²)}
  (hs : is_measurable s) : measurable (Î» x, Î½ (prod.mk x â»Â¹' s)) :=
begin
  refine induction_on_inter generate_from_prod.symm is_pi_system_prod _ _ _ _ hs,
  { simp [measurable_zero, const_def] },
  { rintro _ âŸ¨s, t, hs, ht, rflâŸ©, simp only [mk_preimage_prod_right_eq_if, measure_if],
    exact measurable_const.indicator hs },
  { intros t ht h2t,
    simp_rw [preimage_compl, measure_compl (measurable_prod_mk_left ht) (measure_lt_top Î½ _)],
    exact measurable_const.ennreal_sub h2t },
  { intros f h1f h2f h3f, simp_rw [preimage_Union],
    have : âˆ€ b, Î½ (â‹ƒ i, prod.mk b â»Â¹' f i) = âˆ‘' i, Î½ (prod.mk b â»Â¹' f i) :=
      Î» b, measure_Union (Î» i j hij, disjoint.preimage _ (h1f i j hij))
        (Î» i, measurable_prod_mk_left (h2f i)),
    simp_rw [this], apply measurable.ennreal_tsum h3f },
end

/-- If `Î½` is a Ïƒ-finite measure, and `s âŠ† Î± Ã— Î²` is measurable, then `x â†¦ Î½ { y | (x, y) âˆˆ s }` is
  a measurable function. -/
lemma measurable_measure_prod_mk_left [sigma_finite Î½] {s : set (Î± Ã— Î²)}
  (hs : is_measurable s) : measurable (Î» x, Î½ (prod.mk x â»Â¹' s)) :=
begin
  have : âˆ€ x, is_measurable (prod.mk x â»Â¹' s) := Î» x, measurable_prod_mk_left hs,
  simp only [â† @supr_restrict_spanning_sets _ _ Î½, this],
  apply measurable_supr, intro i,
  haveI : fact _ := measure_spanning_sets_lt_top Î½ i,
  exact measurable_measure_prod_mk_left_finite hs
end

/-- If `Î¼` is a Ïƒ-finite measure, and `s âŠ† Î± Ã— Î²` is measurable, then `y â†¦ Î¼ { x | (x, y) âˆˆ s }` is
  a measurable function. -/
lemma measurable_measure_prod_mk_right {Î¼ : measure Î±} [sigma_finite Î¼] {s : set (Î± Ã— Î²)}
  (hs : is_measurable s) : measurable (Î» y, Î¼ ((Î» x, (x, y)) â»Â¹' s)) :=
measurable_measure_prod_mk_left (is_measurable_swap_iff.mpr hs)

lemma measurable.map_prod_mk_left [sigma_finite Î½] : measurable (Î» x : Î±, map (prod.mk x) Î½) :=
begin
  apply measurable_of_measurable_coe, intros s hs,
  simp_rw [map_apply measurable_prod_mk_left hs],
  exact measurable_measure_prod_mk_left hs
end

lemma measurable.map_prod_mk_right {Î¼ : measure Î±} [sigma_finite Î¼] :
  measurable (Î» y : Î², map (Î» x : Î±, (x, y)) Î¼) :=
begin
  apply measurable_of_measurable_coe, intros s hs,
  simp_rw [map_apply measurable_prod_mk_right hs],
  exact measurable_measure_prod_mk_right hs
end

/-- The Lebesgue intergral is measurable. This shows that the integrand of (the right-hand-side of)
  Tonelli's theorem is measurable. -/
lemma measurable.lintegral_prod_right' [sigma_finite Î½] :
  âˆ€ {f : Î± Ã— Î² â†’ ennreal} (hf : measurable f), measurable (Î» x, âˆ«â» y, f (x, y) âˆ‚Î½) :=
begin
  have m := @measurable_prod_mk_left,
  refine measurable.ennreal_induction _ _ _,
  { intros c s hs, simp only [â† indicator_comp_right],
    suffices : measurable (Î» x, c * Î½ (prod.mk x â»Â¹' s)),
    { simpa [lintegral_indicator _ (m hs)] },
    exact measurable_const.ennreal_mul (measurable_measure_prod_mk_left hs) },
  { rintro f g - hf hg h2f h2g, simp_rw [pi.add_apply, lintegral_add (hf.comp m) (hg.comp m)],
    exact h2f.add h2g },
  { intros f hf h2f h3f,
    have := measurable_supr h3f,
    have : âˆ€ x, monotone (Î» n y, f n (x, y)) := Î» x i j hij y, h2f hij (x, y),
    simpa [lintegral_supr (Î» n, (hf n).comp m), this] }
end

/-- The Lebesgue intergral is measurable. This shows that the integrand of (the right-hand-side of)
  Tonelli's theorem is measurable.
  This version has the argument `f` in curried form. -/
lemma measurable.lintegral_prod_right [sigma_finite Î½] {f : Î± â†’ Î² â†’ ennreal}
  (hf : measurable (uncurry f)) : measurable (Î» x, âˆ«â» y, f x y âˆ‚Î½) :=
hf.lintegral_prod_right'

/-- The Lebesgue intergral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Tonelli's theorem is measurable. -/
lemma measurable.lintegral_prod_left' [sigma_finite Î¼] {f : Î± Ã— Î² â†’ ennreal}
  (hf : measurable f) : measurable (Î» y, âˆ«â» x, f (x, y) âˆ‚Î¼) :=
(measurable_swap_iff.mpr hf).lintegral_prod_right'

/-- The Lebesgue intergral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Tonelli's theorem is measurable.
  This version has the argument `f` in curried form. -/
lemma measurable.lintegral_prod_left [sigma_finite Î¼] {f : Î± â†’ Î² â†’ ennreal}
  (hf : measurable (uncurry f)) : measurable (Î» y, âˆ«â» x, f x y âˆ‚Î¼) :=
hf.lintegral_prod_left'

lemma is_measurable_integrable [sigma_finite Î½] [opens_measurable_space E] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
  (hf : measurable (uncurry f)) : is_measurable { x | integrable (f x) Î½ } :=
begin
  simp_rw [integrable, hf.of_uncurry_left, true_and],
  exact is_measurable_lt (measurable.lintegral_prod_right hf.ennnorm) measurable_const
end

section
variables [second_countable_topology E] [normed_space â„ E]
  [complete_space E] [borel_space E]

/-- The Bochner intergral is measurable. This shows that the integrand of (the right-hand-side of)
  Fubini's theorem is measurable.
  This version has `f` in curried form. -/
lemma measurable.integral_prod_right [sigma_finite Î½] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
  (hf : measurable (uncurry f)) : measurable (Î» x, âˆ« y, f x y âˆ‚Î½) :=
begin
  let s : â„• â†’ simple_func (Î± Ã— Î²) E := simple_func.approx_on _ hf univ _ (mem_univ 0),
  let s' : â„• â†’ Î± â†’ simple_func Î² E := Î» n x, (s n).comp (prod.mk x) measurable_prod_mk_left,
  let f' : â„• â†’ Î± â†’ E := Î» n, {x | integrable (f x) Î½}.indicator
    (Î» x, (s' n x).integral Î½),
  have hf' : âˆ€ n, measurable (f' n),
  { intro n, refine measurable.indicator _ (is_measurable_integrable hf),
    have : âˆ€ x, (s' n x).range.filter (Î» x, x â‰  0) âŠ†
      (s n).range,
    { intros x, refine finset.subset.trans (finset.filter_subset _) _, intro y,
      simp_rw [simple_func.mem_range], rintro âŸ¨z, rflâŸ©, exact âŸ¨(x, z), rflâŸ© },
    simp only [simple_func.integral_eq_sum_of_subset (this _)],
    refine finset.measurable_sum _ _, intro x,
    refine (measurable.to_real _).smul measurable_const,
    simp only [simple_func.coe_comp, preimage_comp] {single_pass := tt},
    apply measurable_measure_prod_mk_left,
    exact (s n).is_measurable_fiber x },
  have h2f' : tendsto f' at_top (ğ“ (Î» (x : Î±), âˆ« (y : Î²), f x y âˆ‚Î½)),
  { rw [tendsto_pi], intro x,
    by_cases hfx : integrable (f x) Î½,
    { have : âˆ€ n, integrable (s' n x) Î½,
      { intro n, apply (hfx.norm.add hfx.norm).mono' (s' n x).measurable,
        apply eventually_of_forall, intro y,
        simp_rw [s', simple_func.coe_comp], exact simple_func.norm_approx_on_zero_le _ _ (x, y) n },
      simp only [f', hfx, simple_func.integral_eq_integral _ (this _), indicator_of_mem,
        mem_set_of_eq],
      refine tendsto_integral_of_dominated_convergence (Î» y, âˆ¥f x yâˆ¥ + âˆ¥f x yâˆ¥)
        (Î» n, (s' n x).measurable) hf.of_uncurry_left (hfx.norm.add hfx.norm) _ _,
      { exact Î» n, eventually_of_forall (Î» y, simple_func.norm_approx_on_zero_le _ _ (x, y) n) },
      { exact eventually_of_forall (Î» y, simple_func.tendsto_approx_on _ _ (by simp)) } },
    { simpa [f', hfx, integral_undef] using @tendsto_const_nhds _ _ _ (0 : E) _, }
     },
  exact measurable_of_tendsto_metric hf' h2f'
end

/-- The Bochner intergral is measurable. This shows that the integrand of (the right-hand-side of)
  Fubini's theorem is measurable. -/
lemma measurable.integral_prod_right' [sigma_finite Î½] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : measurable f) : measurable (Î» x, âˆ« y, f (x, y) âˆ‚Î½) :=
by { rw [â† uncurry_curry f] at hf, exact hf.integral_prod_right }

/-- The Bochner intergral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Fubini's theorem is measurable.
  This version has `f` in curried form. -/
lemma measurable.integral_prod_left [sigma_finite Î¼] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
  (hf : measurable (uncurry f)) : measurable (Î» y, âˆ« x, f x y âˆ‚Î¼) :=
(hf.comp measurable_swap).integral_prod_right'

/-- The Bochner intergral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Fubini's theorem is measurable. -/
lemma measurable.integral_prod_left' [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : measurable f) : measurable (Î» y, âˆ« x, f (x, y) âˆ‚Î¼) :=
(hf.comp measurable_swap).integral_prod_right'

end

/-! ### The product measure -/

namespace measure_theory

namespace measure

/-- The binary product of measures. They are defined for arbitrary measures, but we basically
  prove all properties under the assumption that at least one of them is Ïƒ-finite. -/
protected def prod (Î¼ : measure Î±) (Î½ : measure Î²) : measure (Î± Ã— Î²) :=
bind Î¼ $ Î» x : Î±, map (prod.mk x) Î½

instance prod.measure_space {Î± Î²} [measure_space Î±] [measure_space Î²] : measure_space (Î± Ã— Î²) :=
{ volume := volume.prod volume }

variables {Î¼ Î½} [sigma_finite Î½]

lemma prod_apply {s : set (Î± Ã— Î²)} (hs : is_measurable s) :
  Î¼.prod Î½ s = âˆ«â» x, Î½ (prod.mk x â»Â¹' s) âˆ‚Î¼ :=
by simp_rw [measure.prod, bind_apply hs measurable.map_prod_mk_left,
  map_apply measurable_prod_mk_left hs]

@[simp] lemma prod_prod {s : set Î±} {t : set Î²}
  (hs : is_measurable s) (ht : is_measurable t) : Î¼.prod Î½ (s.prod t) = Î¼ s * Î½ t :=
by simp_rw [prod_apply (hs.prod ht), mk_preimage_prod_right_eq_if, measure_if,
  lintegral_indicator _ hs, lintegral_const, restrict_apply is_measurable.univ,
  univ_inter, mul_comm]

lemma ae_measure_lt_top {s : set (Î± Ã— Î²)} (hs : is_measurable s)
  (h2s : (Î¼.prod Î½) s < âŠ¤) : âˆ€áµ x âˆ‚Î¼, Î½ (prod.mk x â»Â¹' s) < âŠ¤ :=
by { simp_rw [prod_apply hs] at h2s, refine ae_lt_top (measurable_measure_prod_mk_left hs) h2s }

lemma integrable_measure_prod_mk_left {s : set (Î± Ã— Î²)}
  (hs : is_measurable s) (h2s : (Î¼.prod Î½) s < âŠ¤) :
  integrable (Î» x, (Î½ (prod.mk x â»Â¹' s)).to_real) Î¼ :=
begin
  refine âŸ¨(measurable_measure_prod_mk_left hs).to_real, _âŸ©,
  simp_rw [has_finite_integral, ennnorm_eq_of_real to_real_nonneg],
  convert h2s using 1, simp_rw [prod_apply hs], apply lintegral_congr_ae,
  refine (ae_measure_lt_top hs h2s).mp _, apply eventually_of_forall, intros x hx,
  rw [lt_top_iff_ne_top] at hx, simp [of_real_to_real, hx],
end

/-- Note: the assumption `hs` cannot be dropped. For a counterexample, see
  Walter Rudin *Real and Complex Analysis*, example (c) in section 8.9. -/
lemma measure_prod_null {s : set (Î± Ã— Î²)}
  (hs : is_measurable s) : Î¼.prod Î½ s = 0 â†” (Î» x, Î½ (prod.mk x â»Â¹' s)) =áµ[Î¼] 0 :=
by simp_rw [prod_apply hs, lintegral_eq_zero_iff (measurable_measure_prod_mk_left hs)]

/-- Note: the converse is not true without assuming that `s` is measurable. For a counterexample,
  see Walter Rudin *Real and Complex Analysis*, example (c) in section 8.9. -/
lemma measure_ae_null_of_prod_null {s : set (Î± Ã— Î²)}
  (h : Î¼.prod Î½ s = 0) : (Î» x, Î½ (prod.mk x â»Â¹' s)) =áµ[Î¼] 0 :=
begin
  obtain âŸ¨t, hst, mt, htâŸ© := exists_is_measurable_superset_of_measure_eq_zero h,
  simp_rw [measure_prod_null mt] at ht,
  rw [eventually_le_antisymm_iff],
  exact âŸ¨eventually_le.trans_eq
    (eventually_of_forall $ Î» x, (measure_mono (preimage_mono hst) : _)) ht,
    eventually_of_forall $ Î» x, zero_le _âŸ©
end

/-- Note: the converse is not true. For a counterexample, see
  Walter Rudin *Real and Complex Analysis*, example (c) in section 8.9. -/
lemma ae_ae_of_ae_prod {p : Î± Ã— Î² â†’ Prop} (h : âˆ€áµ z âˆ‚Î¼.prod Î½, p z) :
  âˆ€áµ x âˆ‚ Î¼, âˆ€áµ y âˆ‚ Î½, p (x, y) :=
measure_ae_null_of_prod_null h

variables [sigma_finite Î¼]

instance prod.sigma_finite : sigma_finite (Î¼.prod Î½) :=
âŸ¨âŸ¨Î» n, (spanning_sets Î¼ n).prod (spanning_sets Î½ n),
  Î» n, (is_measurable_spanning_sets Î¼ n).prod (is_measurable_spanning_sets Î½ n),
  Î» n, by { simp_rw [prod_prod (is_measurable_spanning_sets Î¼ n) (is_measurable_spanning_sets Î½ n)],
    exact mul_lt_top (measure_spanning_sets_lt_top Î¼ n) (measure_spanning_sets_lt_top Î½ n) },
  by { simp_rw [Union_prod_of_monotone (monotone_spanning_sets Î¼) (monotone_spanning_sets Î½),
    Union_spanning_sets, univ_prod_univ] }âŸ©âŸ©

/- Note: This proof would be shorter if `sigma_finite` was not `Prop`-valued, since we use that
  the sets given in the instance of `sigma_finite` is a Ï€-system. -/
/-- Measures on a product space are equal if they are equal on rectangles. -/
lemma prod_unique {Î¼Î½â‚ Î¼Î½â‚‚ : measure (Î± Ã— Î²)}
  (hâ‚ : âˆ€ s t, is_measurable s â†’ is_measurable t â†’ Î¼Î½â‚ (s.prod t) = Î¼ s * Î½ t)
  (hâ‚‚ : âˆ€ s t, is_measurable s â†’ is_measurable t â†’ Î¼Î½â‚‚ (s.prod t) = Î¼ s * Î½ t) : Î¼Î½â‚ = Î¼Î½â‚‚ :=
begin
  refine ext_of_generate_from_of_Union _
    (Î» i, (spanning_sets Î¼ i).prod (spanning_sets Î½ i))
    generate_from_prod.symm is_pi_system_prod _ _ _ _,
  { rw [Union_prod_of_monotone (monotone_spanning_sets Î¼) (monotone_spanning_sets Î½)],
    simp_rw [Union_spanning_sets, univ_prod_univ] },
  { intro i, apply mem_image2_of_mem; apply is_measurable_spanning_sets },
  { intro i, rw [hâ‚], apply mul_lt_top; apply measure_spanning_sets_lt_top,
    all_goals { apply is_measurable_spanning_sets } },
  { rintro _ âŸ¨s, t, hs, ht, rflâŸ©, simp * at * }
end

lemma prod_eq {Î¼Î½ : measure (Î± Ã— Î²)}
  (h : âˆ€ s t, is_measurable s â†’ is_measurable t â†’ Î¼Î½ (s.prod t) = Î¼ s * Î½ t) : Î¼.prod Î½ = Î¼Î½ :=
prod_unique (Î» s t hs ht, prod_prod hs ht) h

lemma prod_swap : map prod.swap (Î¼.prod Î½) = Î½.prod Î¼ :=
begin
  refine (prod_eq _).symm,
  intros s t hs ht,
  simp_rw [map_apply measurable_swap (hs.prod ht), preimage_swap_prod, prod_prod ht hs, mul_comm]
end

lemma prod_apply_symm {s : set (Î± Ã— Î²)} (hs : is_measurable s) :
  Î¼.prod Î½ s = âˆ«â» y, Î¼ ((Î» x, (x, y)) â»Â¹' s) âˆ‚Î½ :=
by { rw [â† prod_swap, map_apply measurable_swap hs],
     simp only [prod_apply (measurable_swap hs)], refl }

/-! ### The product of specific measures -/

lemma prod_restrict {s : set Î±} {t : set Î²} (hs : is_measurable s) (ht : is_measurable t) :
  (Î¼.restrict s).prod (Î½.restrict t) = (Î¼.prod Î½).restrict (s.prod t) :=
begin
  refine prod_eq (Î» s' t' hs' ht', _),
  simp_rw [restrict_apply (hs'.prod ht'), prod_inter_prod, prod_prod (hs'.inter hs) (ht'.inter ht),
    restrict_apply hs', restrict_apply ht']
end

lemma prod_dirac (y : Î²) : Î¼.prod (dirac y) = map (Î» x, (x, y)) Î¼ :=
begin
  refine prod_eq (Î» s t hs ht, _),
  simp_rw [map_apply measurable_prod_mk_right (hs.prod ht), mk_preimage_prod_left_eq_if, measure_if,
    dirac_apply _ ht, â† indicator_mul_right _ (Î» x, Î¼ s), pi.one_apply, mul_one]
end

lemma dirac_prod (x : Î±) : (dirac x).prod Î½ = map (prod.mk x) Î½ :=
begin
  refine prod_eq (Î» s t hs ht, _),
  simp_rw [map_apply measurable_prod_mk_left (hs.prod ht), mk_preimage_prod_right_eq_if, measure_if,
    dirac_apply _ hs, â† indicator_mul_left _ _ (Î» x, Î½ t), pi.one_apply, one_mul]
end

lemma dirac_prod_dirac {x : Î±} {y : Î²} : (dirac x).prod (dirac y) = dirac (x, y) :=
by rw [prod_dirac, map_dirac measurable_prod_mk_right]

lemma prod_sum {Î¹ : Type*} [fintype Î¹] (Î½ : Î¹ â†’ measure Î²) [âˆ€ i, sigma_finite (Î½ i)] :
  Î¼.prod (sum Î½) = sum (Î» i, Î¼.prod (Î½ i)) :=
begin
  refine prod_eq (Î» s t hs ht, _),
  simp_rw [sum_apply _ (hs.prod ht), sum_apply _ ht, prod_prod hs ht, tsum_fintype, finset.mul_sum]
end

lemma sum_prod {Î¹ : Type*} [fintype Î¹] (Î¼ : Î¹ â†’ measure Î±) [âˆ€ i, sigma_finite (Î¼ i)] :
  (sum Î¼).prod Î½ = sum (Î» i, (Î¼ i).prod Î½) :=
begin
  refine prod_eq (Î» s t hs ht, _),
  simp_rw [sum_apply _ (hs.prod ht), sum_apply _ hs, prod_prod hs ht, tsum_fintype, finset.sum_mul]
end

lemma prod_add (Î½' : measure Î²) [sigma_finite Î½'] : Î¼.prod (Î½ + Î½') = Î¼.prod Î½ + Î¼.prod Î½' :=
by { refine prod_eq (Î» s t hs ht, _), simp_rw [add_apply, prod_prod hs ht, left_distrib] }

lemma add_prod (Î¼' : measure Î±) [sigma_finite Î¼'] : (Î¼ + Î¼').prod Î½ = Î¼.prod Î½ + Î¼'.prod Î½ :=
by { refine prod_eq (Î» s t hs ht, _), simp_rw [add_apply, prod_prod hs ht, right_distrib] }

end measure

open measure_theory.measure

/-! ### The Lebesgue integral on a product -/

variables [sigma_finite Î½]

lemma lintegral_prod_swap [sigma_finite Î¼] (f : Î± Ã— Î² â†’ ennreal)
  (hf : measurable f) : âˆ«â» z, f z.swap âˆ‚(Î½.prod Î¼) = âˆ«â» z, f z âˆ‚(Î¼.prod Î½) :=
by rw [â† lintegral_map hf measurable_swap, prod_swap]

/-- Tonelli's Theorem: For `ennreal`-valued measurable functions on `Î± Ã— Î²`,
  the integral of `f` is equal to the iterated integral. -/
lemma lintegral_prod :
  âˆ€ (f : Î± Ã— Î² â†’ ennreal) (hf : measurable f), âˆ«â» z, f z âˆ‚(Î¼.prod Î½) = âˆ«â» x, âˆ«â» y, f (x, y) âˆ‚Î½ âˆ‚Î¼ :=
begin
  have m := @measurable_prod_mk_left,
  refine measurable.ennreal_induction _ _ _,
  { intros c s hs, simp only [â† indicator_comp_right],
    simp [lintegral_indicator, m hs, hs, lintegral_const_mul, measurable_measure_prod_mk_left hs,
      prod_apply] },
  { rintro f g - hf hg h2f h2g,
    simp [lintegral_add, measurable.lintegral_prod_right', hf.comp m, hg.comp m,
      hf, hg, h2f, h2g] },
  { intros f hf h2f h3f,
    have kf : âˆ€ x n, measurable (Î» y, f n (x, y)) := Î» x n, (hf n).comp m,
    have k2f : âˆ€ x, monotone (Î» n y, f n (x, y)) := Î» x i j hij y, h2f hij (x, y),
    have lf : âˆ€ n, measurable (Î» x, âˆ«â» y, f n (x, y) âˆ‚Î½) := Î» n, (hf n).lintegral_prod_right',
    have l2f : monotone (Î» n x, âˆ«â» y, f n (x, y) âˆ‚Î½) := Î» i j hij x, lintegral_mono (k2f x hij),
    simp only [lintegral_supr hf h2f, lintegral_supr (kf _), k2f, lintegral_supr lf l2f, h3f] },
end

/-- The symmetric verion of Tonelli's Theorem: For `ennreal`-valued measurable functions on `Î± Ã— Î²`,
  the integral of `f` is equal to the iterated integral, in reverse order. -/
lemma lintegral_prod_symm [sigma_finite Î¼] (f : Î± Ã— Î² â†’ ennreal)
  (hf : measurable f) : âˆ«â» z, f z âˆ‚(Î¼.prod Î½) = âˆ«â» y, âˆ«â» x, f (x, y) âˆ‚Î¼ âˆ‚Î½ :=
by { simp_rw [â† lintegral_prod_swap f hf], exact lintegral_prod _ (hf.comp measurable_swap) }

/-- The reversed version of Tonelli's Theorem. In this version `f` is in curried form, which makes
  it easier for the elaborator to figure out `f` automatically. -/
lemma lintegral_lintegral â¦ƒf : Î± â†’ Î² â†’ ennrealâ¦„
  (hf : measurable (uncurry f)) :
  âˆ«â» x, âˆ«â» y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ«â» z, f z.1 z.2 âˆ‚(Î¼.prod Î½) :=
(lintegral_prod _ hf).symm

/-- The reversed version of Tonelli's Theorem (symmetric version). In this version `f` is in curried
  form, which makes it easier for the elaborator to figure out `f` automatically. -/
lemma lintegral_lintegral_symm [sigma_finite Î¼] â¦ƒf : Î± â†’ Î² â†’ ennrealâ¦„
  (hf : measurable (uncurry f)) :
  âˆ«â» x, âˆ«â» y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ«â» z, f z.2 z.1 âˆ‚(Î½.prod Î¼) :=
(lintegral_prod_symm _ (hf.comp measurable_swap)).symm

/-- Change the order of Lebesgue integration. -/
lemma lintegral_lintegral_swap [sigma_finite Î¼] â¦ƒf : Î± â†’ Î² â†’ ennrealâ¦„
  (hf : measurable (uncurry f)) :
  âˆ«â» x, âˆ«â» y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ«â» y, âˆ«â» x, f x y âˆ‚Î¼ âˆ‚Î½ :=
(lintegral_lintegral hf).trans (lintegral_prod_symm _ hf)

/-! ### Integrability on a product -/
section

variables [opens_measurable_space E]

lemma integrable.swap [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (f âˆ˜ prod.swap) (Î½.prod Î¼) :=
âŸ¨hf.measurable.comp measurable_swap,
  (lintegral_prod_swap _ hf.measurable.ennnorm : _).le.trans_lt hf.has_finite_integralâŸ©

lemma integrable_swap_iff [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ :
  integrable (f âˆ˜ prod.swap) (Î½.prod Î¼) â†” integrable f (Î¼.prod Î½) :=
âŸ¨Î» hf, by { convert hf.swap, ext âŸ¨x, yâŸ©, refl }, Î» hf, hf.swapâŸ©

lemma has_finite_integral_prod_iff â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : measurable f) :
  has_finite_integral f (Î¼.prod Î½) â†” (âˆ€áµ x âˆ‚ Î¼, has_finite_integral (Î» y, f (x, y)) Î½) âˆ§
    has_finite_integral (Î» x, âˆ« y, âˆ¥f (x, y)âˆ¥ âˆ‚Î½) Î¼ :=
begin
  simp only [has_finite_integral, lintegral_prod _ h1f.ennnorm],
  have : âˆ€ x, âˆ€áµ y âˆ‚Î½, 0 â‰¤ âˆ¥f (x, y)âˆ¥ := Î» x, eventually_of_forall (Î» y, norm_nonneg _),
  simp_rw [integral_eq_lintegral_of_nonneg_ae (this _) (h1f.norm.comp measurable_prod_mk_left),
    ennnorm_eq_of_real to_real_nonneg, of_real_norm_eq_coe_nnnorm],
  -- this fact is probably too specialized to be its own lemma
  have : âˆ€ {p q r : Prop} (h1 : r â†’ p), (r â†” p âˆ§ q) â†” (p â†’ (r â†” q)) :=
  Î» p q r h1, by rw [â† and.congr_right_iff, and_iff_right_of_imp h1],
  rw [this],
  { intro h2f, rw lintegral_congr_ae,
    refine h2f.mp _, apply eventually_of_forall, intros x hx, dsimp only,
    rw [of_real_to_real], rw [â† lt_top_iff_ne_top], exact hx },
  { intro h2f, refine ae_lt_top _ h2f, exact h1f.ennnorm.lintegral_prod_right' },
end

/-- A binary function is integrable if the function `y â†¦ f (x, y)` is integrable for almost every
  `x` and the function `x â†¦ âˆ« âˆ¥f (x, y)âˆ¥ dy` is integrable. -/
lemma integrable_prod_iff â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : measurable f) :
  integrable f (Î¼.prod Î½) â†”
    (âˆ€áµ x âˆ‚ Î¼, integrable (Î» y, f (x, y)) Î½) âˆ§ integrable (Î» x, âˆ« y, âˆ¥f (x, y)âˆ¥ âˆ‚Î½) Î¼ :=
by simp only [integrable, h1f, h1f.comp measurable_prod_mk_left, h1f.norm.integral_prod_right',
  true_and, has_finite_integral_prod_iff]

/-- A binary function is integrable if the function `x â†¦ f (x, y)` is integrable for almost every
  `y` and the function `y â†¦ âˆ« âˆ¥f (x, y)âˆ¥ dx` is integrable. -/
lemma integrable_prod_iff' [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : measurable f) :
  integrable f (Î¼.prod Î½) â†”
    (âˆ€áµ y âˆ‚ Î½, integrable (Î» x, f (x, y)) Î¼) âˆ§ integrable (Î» y, âˆ« x, âˆ¥f (x, y)âˆ¥ âˆ‚Î¼) Î½ :=
by { convert integrable_prod_iff (h1f.comp measurable_swap) using 1, rw [integrable_swap_iff],
  apply_instance }

lemma integrable.prod_left_ae [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : âˆ€áµ y âˆ‚ Î½, integrable (Î» x, f (x, y)) Î¼ :=
((integrable_prod_iff' hf.measurable).mp hf).1

lemma integrable.prod_right_ae [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : âˆ€áµ x âˆ‚ Î¼, integrable (Î» y, f (x, y)) Î½ :=
hf.swap.prod_left_ae

lemma integrable.integral_norm_prod_left â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (Î» x, âˆ« y, âˆ¥f (x, y)âˆ¥ âˆ‚Î½) Î¼ :=
((integrable_prod_iff hf.measurable).mp hf).2

lemma integrable.integral_norm_prod_right [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (Î» y, âˆ« x, âˆ¥f (x, y)âˆ¥ âˆ‚Î¼) Î½ :=
hf.swap.integral_norm_prod_left

end

variables [second_countable_topology E] [normed_space â„ E]
  [complete_space E] [borel_space E]

lemma integrable.integral_prod_left â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (Î» x, âˆ« y, f (x, y) âˆ‚Î½) Î¼ :=
integrable.mono hf.integral_norm_prod_left hf.measurable.integral_prod_right' $
  eventually_of_forall $ Î» x, (norm_integral_le_integral_norm _).trans_eq $
  (norm_of_nonneg $ integral_nonneg_of_ae $ eventually_of_forall $ Î» y, (norm_nonneg _ : _)).symm

lemma integrable.integral_prod_right [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (Î» y, âˆ« x, f (x, y) âˆ‚Î¼) Î½ :=
hf.swap.integral_prod_left

/-! ### The Bochner integral on a product -/

variables [sigma_finite Î¼]

lemma integral_prod_swap (f : Î± Ã— Î² â†’ E)
  (hf : measurable f) : âˆ« z, f z.swap âˆ‚(Î½.prod Î¼) = âˆ« z, f z âˆ‚(Î¼.prod Î½) :=
by rw [â† integral_map measurable_swap hf, prod_swap]

variables {E' : Type*} [measurable_space E'] [normed_group E'] [borel_space E'] [complete_space E']
  [normed_space â„ E'] [second_countable_topology E']

/-! Some rules about the sum/difference of double integrals. They follow from `integral_add`, but
  we separate them out as separate lemmas, because they involve quite some steps. -/

/-- Integrals commute with addition inside another integral. `F` can be any measurable function. -/
lemma integral_fn_integral_add â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  {F : E â†’ E'} (hF : measurable F)
  (hf : integrable f (Î¼.prod Î½))
  (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, F (âˆ« y, f (x, y) + g (x, y) âˆ‚Î½) âˆ‚Î¼ = âˆ« x, F (âˆ« y, f (x, y) âˆ‚Î½ + âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ :=
begin
  refine integral_congr_ae
    (hF.comp (hf.add hg).measurable.integral_prod_right')
    (hF.comp (hf.measurable.integral_prod_right'.add hg.measurable.integral_prod_right')) _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae], dsimp only [mem_set_of_eq],
  intros x h2f h2g, simp [integral_add h2f h2g],
end

/-- Integrals commute with subtraction inside another integral.
  `F` can be any measurable function. -/
lemma integral_fn_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  {F : E â†’ E'} (hF : measurable F)
  (hf : integrable f (Î¼.prod Î½))
  (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î½) âˆ‚Î¼ = âˆ« x, F (âˆ« y, f (x, y) âˆ‚Î½ - âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ :=
begin
  refine integral_congr_ae
    (hF.comp (hf.sub hg).measurable.integral_prod_right')
    (hF.comp (hf.measurable.integral_prod_right'.sub hg.measurable.integral_prod_right')) _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae], dsimp only [mem_set_of_eq],
  intros x h2f h2g, simp [integral_sub h2f h2g]
end

/-- Integrals commute with subtraction inside a lower Lebesgue integral.
  `F` can be any function. -/
lemma lintegral_fn_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (F : E â†’ ennreal) (hf : integrable f (Î¼.prod Î½)) (hg : integrable g (Î¼.prod Î½)) :
  âˆ«â» x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î½) âˆ‚Î¼ = âˆ«â» x, F (âˆ« y, f (x, y) âˆ‚Î½ - âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ :=
begin
  refine lintegral_congr_ae _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae], dsimp only [mem_set_of_eq],
  intros x h2f h2g, simp [integral_sub h2f h2g]
end

/-- Double integrals commute with addition. -/
lemma integral_integral_add â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½))
  (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f (x, y) + g (x, y) âˆ‚Î½ âˆ‚Î¼ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ + âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
(integral_fn_integral_add measurable_id hf hg).trans $
  integral_add hf.integral_prod_left hg.integral_prod_left

/-- Double integrals commute with addition. This is the version with `(f + g) (x, y)`
  (instead of `f (x, y) + g (x, y)`) in the LHS. -/
lemma integral_integral_add' â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½))
  (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, (f + g) (x, y) âˆ‚Î½ âˆ‚Î¼ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ + âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
integral_integral_add hf hg

/-- Double integrals commute with subtraction. -/
lemma integral_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½))
  (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f (x, y) - g (x, y) âˆ‚Î½ âˆ‚Î¼ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ - âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
(integral_fn_integral_sub measurable_id hf hg).trans $
  integral_sub hf.integral_prod_left hg.integral_prod_left

/-- Double integrals commute with subtraction. This is the version with `(f - g) (x, y)`
  (instead of `f (x, y) - g (x, y)`) in the LHS. -/
lemma integral_integral_sub' â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½))
  (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, (f - g) (x, y) âˆ‚Î½ âˆ‚Î¼ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ - âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
integral_integral_sub hf hg

/-- The map that sends an LÂ¹-function `f : Î± Ã— Î² â†’ E` to `âˆ«âˆ«f` is continuous. -/
lemma continuous_integral_integral :
  continuous (Î» (f : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E), âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼) :=
begin
  rw [continuous_iff_continuous_at], intro g,
  refine tendsto_integral_of_l1 _ g.integrable.integral_prod_left
    (eventually_of_forall $ Î» h, h.integrable.integral_prod_left) _,
  simp_rw [edist_eq_coe_nnnorm_sub,
    â† lintegral_fn_integral_sub (Î» x, (nnnorm x : ennreal)) (l1.integrable _) g.integrable],
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds _ (Î» i, zero_le _) _,
  { exact Î» i, âˆ«â» x, âˆ«â» y, nnnorm (i (x, y) - g (x, y)) âˆ‚Î½ âˆ‚Î¼ },
  swap, { exact Î» i, lintegral_mono (Î» x, ennnorm_integral_le_lintegral_ennnorm _) },
  show tendsto (Î» (i : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E),
    âˆ«â» x, âˆ«â» (y : Î²), nnnorm (i (x, y) - g (x, y)) âˆ‚Î½ âˆ‚Î¼) (ğ“ g) (ğ“ 0),
  have : âˆ€ (i : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E), measurable (Î» z, (nnnorm (i z - g z) : ennreal)) :=
  Î» i, (i.measurable.sub g.measurable).ennnorm,
  simp_rw [â† lintegral_prod _ (this _), â† l1.of_real_norm_sub_eq_lintegral, â† of_real_zero],
  refine (continuous_of_real.tendsto 0).comp _,
  rw [â† tendsto_iff_norm_tendsto_zero], exact tendsto_id
end

/-- Fubini's Theorem: For integrable functions on `Î± Ã— Î²`,
  the Bochner integral of `f` is equal to the iterated Bochner integral.
  `integrable_prod_iff` can be useful to show that the function in question in integrable.
  `measure_theory.integrable.integral_prod_right` is useful to show that the inner integral
  of the right-hand side is integrable. -/
lemma integral_prod : âˆ€ (f : Î± Ã— Î² â†’ E) (hf : integrable f (Î¼.prod Î½)),
  âˆ« z, f z âˆ‚(Î¼.prod Î½) = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ :=
begin
  apply integrable.induction,
  { intros c s hs h2s, simp_rw [integral_indicator measurable_const hs, â† indicator_comp_right,
      function.comp, integral_indicator measurable_const (measurable_prod_mk_left hs),
      set_integral_const, integral_smul_const,
      integral_to_real (measurable_measure_prod_mk_left hs) (ae_measure_lt_top hs h2s),
      prod_apply hs] },
  { intros f g hfg i_f i_g hf hg,
    simp_rw [integral_add' i_f i_g, integral_integral_add' i_f i_g, hf, hg] },
  { exact is_closed_eq continuous_integral continuous_integral_integral },
  { intros f g hfg i_f m_g hf, convert hf using 1,
    { exact integral_congr_ae m_g i_f.measurable hfg.symm },
    { refine integral_congr_ae m_g.integral_prod_right' i_f.measurable.integral_prod_right' _,
      rw [eventually_eq] at hfg, refine (ae_ae_of_ae_prod hfg).mp _,
      apply eventually_of_forall, intros x hfgx,
      refine integral_congr_ae (m_g.comp measurable_prod_mk_left)
        (i_f.measurable.comp measurable_prod_mk_left) (ae_eq_symm hfgx) } }
end

/-- Symmetric version of Fubini's Theorem: For integrable functions on `Î± Ã— Î²`,
  the Bochner integral of `f` is equal to the iterated Bochner integral.
  This version has the integrals on the right-hand side in the other order. -/
lemma integral_prod_symm (f : Î± Ã— Î² â†’ E) (hf : integrable f (Î¼.prod Î½)) :
  âˆ« z, f z âˆ‚(Î¼.prod Î½) = âˆ« y, âˆ« x, f (x, y) âˆ‚Î¼ âˆ‚Î½ :=
by { simp_rw [â† integral_prod_swap f hf.measurable], exact integral_prod _ hf.swap }

/-- Reversed version of Fubini's Theorem. -/
lemma integral_integral {f : Î± â†’ Î² â†’ E} (hf : integrable (uncurry f) (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« z, f z.1 z.2 âˆ‚(Î¼.prod Î½) :=
(integral_prod _ hf).symm

/-- Reversed version of Fubini's Theorem (symmetric version). -/
lemma integral_integral_symm {f : Î± â†’ Î² â†’ E} (hf : integrable (uncurry f) (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« z, f z.2 z.1 âˆ‚(Î½.prod Î¼) :=
(integral_prod_symm _ hf.swap).symm

/-- Change the order of Bochner integration. -/
lemma integral_integral_swap â¦ƒf : Î± â†’ Î² â†’ Eâ¦„ (hf : integrable (uncurry f) (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« y, âˆ« x, f x y âˆ‚Î¼ âˆ‚Î½ :=
(integral_integral hf).trans (integral_prod_symm _ hf)

end measure_theory
