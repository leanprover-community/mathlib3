/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, Heather Macbeth
-/
import measure_theory.l1_space

/-!
# Density of simple functions

Show that each Borel measurable function can be approximated,
both pointwise and in `Láµ–` norm, by a sequence of simple functions.

## Main definitions

* `measure_theory.simple_func.nearest_pt (e : â„• â†’ Î±) (N : â„•) : Î± â†’â‚› â„•`: the `simple_func` sending
  each `x : Î±` to the point `e k` which is the nearest to `x` among `e 0`, ..., `e N`.
* `measure_theory.simple_func.approx_on (f : Î² â†’ Î±) (hf : measurable f) (s : set Î±) (yâ‚€ : Î±)
  (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (n : â„•) : Î² â†’â‚› Î±` : a simple function that takes values in `s`
  and approximates `f`.

## Main results

* `tendsto_approx_on` (pointwise convergence): If `f x âˆˆ s`, then the sequence of simple
  approximations `measure_theory.simple_func.approx_on f hf s yâ‚€ hâ‚€ n`, evaluated at `x`,
  tends to `f x` as `n` tends to `âˆž`.
* `tendsto_approx_on_univ_Lp_nnnorm` (Láµ– convergence): If `E` is a `normed_group` and `f` is
  measurable and satisfies `mem_â„’p f p Î¼`, then each of the simple approximations
  `simple_func.approx_on f hf s 0 hâ‚€ n` is also in Láµ–, and they tend in Láµ– to `f`.
* `tendsto_approx_on_univ_L1` (LÂ¹ convergence): If `E` is a `normed_group` and `f` is measurable
  and integrable, then the simple functions `simple_func.approx_on f hf s 0 hâ‚€ n` may be considered
  as elements of `Lp E 1 Î¼1`, and they tend in LÂ¹ to `f`.

## Notations

* `Î± â†’â‚› Î²` (local notation): the type of simple functions `Î± â†’ Î²`.
-/

open set filter topological_space
open_locale classical topological_space nnreal ennreal
variables {Î± Î² Î¹ E : Type*}

namespace measure_theory
open ennreal emetric

local infixr ` â†’â‚› `:25 := simple_func

namespace simple_func

/-! ### Pointwise approximation by simple functions -/

section pointwise
variables [measurable_space Î±] [emetric_space Î±] [opens_measurable_space Î±]

/-- `nearest_pt_ind e N x` is the index `k` such that `e k` is the nearest point to `x` among the
points `e 0`, ..., `e N`. If more than one point are at the same distance from `x`, then
`nearest_pt_ind e N x` returns the least of their indexes. -/
noncomputable def nearest_pt_ind (e : â„• â†’ Î±) : â„• â†’ Î± â†’â‚› â„•
| 0 := const Î± 0
| (N + 1) := piecewise (â‹‚ k â‰¤ N, {x | edist (e (N + 1)) x < edist (e k) x})
    (measurable_set.Inter $ Î» k, measurable_set.Inter_Prop $ Î» hk,
      measurable_set_lt measurable_edist_right measurable_edist_right)
    (const Î± $ N + 1) (nearest_pt_ind N)

/-- `nearest_pt e N x` is the nearest point to `x` among the points `e 0`, ..., `e N`. If more than
one point are at the same distance from `x`, then `nearest_pt e N x` returns the point with the
least possible index. -/
noncomputable def nearest_pt (e : â„• â†’ Î±) (N : â„•) : Î± â†’â‚› Î± :=
(nearest_pt_ind e N).map e

@[simp] lemma nearest_pt_ind_zero (e : â„• â†’ Î±) : nearest_pt_ind e 0 = const Î± 0 := rfl

@[simp] lemma nearest_pt_zero (e : â„• â†’ Î±) : nearest_pt e 0 = const Î± (e 0) := rfl

lemma nearest_pt_ind_succ (e : â„• â†’ Î±) (N : â„•) (x : Î±) :
  nearest_pt_ind e (N + 1) x =
    if âˆ€ k â‰¤ N, edist (e (N + 1)) x < edist (e k) x
    then N + 1 else nearest_pt_ind e N x :=
by { simp only [nearest_pt_ind, coe_piecewise, set.piecewise], congr, simp }

lemma nearest_pt_ind_le (e : â„• â†’ Î±) (N : â„•) (x : Î±) : nearest_pt_ind e N x â‰¤ N :=
begin
  induction N with N ihN, { simp },
  simp only [nearest_pt_ind_succ],
  split_ifs,
  exacts [le_rfl, ihN.trans N.le_succ]
end

lemma edist_nearest_pt_le (e : â„• â†’ Î±) (x : Î±) {k N : â„•} (hk : k â‰¤ N) :
  edist (nearest_pt e N x) x â‰¤ edist (e k) x :=
begin
  induction N with N ihN generalizing k,
  { simp [nonpos_iff_eq_zero.1 hk, le_refl] },
  { simp only [nearest_pt, nearest_pt_ind_succ, map_apply],
    split_ifs,
    { rcases hk.eq_or_lt with rfl|hk,
      exacts [le_rfl, (h k (nat.lt_succ_iff.1 hk)).le] },
    { push_neg at h,
      rcases h with âŸ¨l, hlN, hxlâŸ©,
      rcases hk.eq_or_lt with rfl|hk,
      exacts [(ihN hlN).trans hxl, ihN (nat.lt_succ_iff.1 hk)] } }
end

lemma tendsto_nearest_pt {e : â„• â†’ Î±} {x : Î±} (hx : x âˆˆ closure (range e)) :
  tendsto (Î» N, nearest_pt e N x) at_top (ð“ x) :=
begin
  refine (at_top_basis.tendsto_iff nhds_basis_eball).2 (Î» Îµ hÎµ, _),
  rcases emetric.mem_closure_iff.1 hx Îµ hÎµ with âŸ¨_, âŸ¨N, rflâŸ©, hNâŸ©,
  rw [edist_comm] at hN,
  exact âŸ¨N, trivial, Î» n hn, (edist_nearest_pt_le e x hn).trans_lt hNâŸ©
end

variables [measurable_space Î²] {f : Î² â†’ Î±}

/-- Approximate a measurable function by a sequence of simple functions `F n` such that
`F n x âˆˆ s`. -/
noncomputable def approx_on (f : Î² â†’ Î±) (hf : measurable f) (s : set Î±) (yâ‚€ : Î±) (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (n : â„•) :
  Î² â†’â‚› Î± :=
by haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©;
  exact comp (nearest_pt (Î» k, nat.cases_on k yâ‚€ (coe âˆ˜ dense_seq s) : â„• â†’ Î±) n) f hf

@[simp] lemma approx_on_zero {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) :
  approx_on f hf s yâ‚€ hâ‚€ 0 x = yâ‚€ :=
rfl

lemma approx_on_mem {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (n : â„•) (x : Î²) :
  approx_on f hf s yâ‚€ hâ‚€ n x âˆˆ s :=
begin
  haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©,
  suffices : âˆ€ n, (nat.cases_on n yâ‚€ (coe âˆ˜ dense_seq s) : Î±) âˆˆ s, { apply this },
  rintro (_|n),
  exacts [hâ‚€, subtype.mem _]
end

@[simp] lemma approx_on_comp {Î³ : Type*} [measurable_space Î³] {f : Î² â†’ Î±} (hf : measurable f)
  {g : Î³ â†’ Î²} (hg : measurable g) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (n : â„•) :
  approx_on (f âˆ˜ g) (hf.comp hg) s yâ‚€ hâ‚€ n = (approx_on f hf s yâ‚€ hâ‚€ n).comp g hg :=
rfl

lemma tendsto_approx_on {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] {x : Î²} (hx : f x âˆˆ closure s) :
  tendsto (Î» n, approx_on f hf s yâ‚€ hâ‚€ n x) at_top (ð“ $ f x) :=
begin
  haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©,
  rw [â† @subtype.range_coe _ s, â† image_univ, â† (dense_range_dense_seq s).closure_eq] at hx,
  simp only [approx_on, coe_comp],
  refine tendsto_nearest_pt (closure_minimal _ is_closed_closure hx),
  simp only [nat.range_cases_on, closure_union, range_comp coe],
  exact subset.trans (image_closure_subset_closure_image continuous_subtype_coe)
    (subset_union_right _ _)
end

lemma edist_approx_on_le {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) (n : â„•) :
  edist (approx_on f hf s yâ‚€ hâ‚€ n x) (f x) â‰¤ edist yâ‚€ (f x) :=
begin
  dsimp only [approx_on, coe_comp, (âˆ˜)],
  exact edist_nearest_pt_le _ _ (zero_le _)
end

lemma edist_approx_on_y0_le {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) (n : â„•) :
  edist yâ‚€ (approx_on f hf s yâ‚€ hâ‚€ n x) â‰¤ edist yâ‚€ (f x) + edist yâ‚€ (f x) :=
calc edist yâ‚€ (approx_on f hf s yâ‚€ hâ‚€ n x) â‰¤
  edist yâ‚€ (f x) + edist (approx_on f hf s yâ‚€ hâ‚€ n x) (f x) : edist_triangle_right _ _ _
... â‰¤ edist yâ‚€ (f x) + edist yâ‚€ (f x) : add_le_add_left (edist_approx_on_le hf hâ‚€ x n) _

end pointwise

/-! ### Lp approximation by simple functions -/

section Lp
variables [measurable_space Î²]
variables [measurable_space E] [normed_group E] {q : â„} {p : â„â‰¥0âˆž}

lemma nnnorm_approx_on_le [opens_measurable_space E] {f : Î² â†’ E} (hf : measurable f)
  {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (x : Î²) (n : â„•) :
  âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š â‰¤ âˆ¥f x - yâ‚€âˆ¥â‚Š :=
begin
  have := edist_approx_on_le hf hâ‚€ x n,
  rw edist_comm yâ‚€ at this,
  simp only [edist_nndist, nndist_eq_nnnorm] at this,
  exact_mod_cast this
end

lemma norm_approx_on_yâ‚€_le [opens_measurable_space E] {f : Î² â†’ E} (hf : measurable f)
  {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (x : Î²) (n : â„•) :
  âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - yâ‚€âˆ¥ â‰¤ âˆ¥f x - yâ‚€âˆ¥ + âˆ¥f x - yâ‚€âˆ¥ :=
begin
  have := edist_approx_on_y0_le hf hâ‚€ x n,
  repeat { rw [edist_comm yâ‚€, edist_eq_coe_nnnorm_sub] at this },
  exact_mod_cast this,
end

lemma norm_approx_on_zero_le [opens_measurable_space E] {f : Î² â†’ E} (hf : measurable f)
  {s : set E} (hâ‚€ : (0 : E) âˆˆ s) [separable_space s] (x : Î²) (n : â„•) :
  âˆ¥approx_on f hf s 0 hâ‚€ n xâˆ¥ â‰¤ âˆ¥f xâˆ¥ + âˆ¥f xâˆ¥ :=
begin
  have := edist_approx_on_y0_le hf hâ‚€ x n,
  simp [edist_comm (0 : E), edist_eq_coe_nnnorm] at this,
  exact_mod_cast this,
end

lemma tendsto_approx_on_Lp_nnnorm [opens_measurable_space E]
  {f : Î² â†’ E} (hf : measurable f) {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s]
  (hq : 0 < q) {Î¼ : measure Î²} (hÎ¼ : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closure s)
  (hi : snorm' (Î» x, f x - yâ‚€) q Î¼ < âˆž) :
  tendsto (Î» n, snorm' (approx_on f hf s yâ‚€ hâ‚€ n - f) q Î¼) at_top (ð“ 0) :=
begin
  suffices : tendsto (Î» n, âˆ«â» x, âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š ^ q âˆ‚Î¼) at_top (ð“ 0),
  { simp only [snorm'],
    have hq' : 0 < qâ»Â¹ := _root_.inv_pos.mpr hq,
    convert (ennreal.continuous_at_rpow_const hq').tendsto.comp this;
    simp [hq'] },
  -- We simply check the conditions of the Dominated Convergence Theorem:
  -- (1) The function "`q`-th power of distance between `f` and the approximation" is measurable
  have hF_meas : âˆ€ n, measurable (Î» x, (âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š : â„â‰¥0âˆž) ^ q),
  { simpa only [â† edist_eq_coe_nnnorm_sub] using
      Î» n, (approx_on f hf s yâ‚€ hâ‚€ n).measurable_bind (Î» y x, (edist y (f x)) ^ q)
      (Î» y, (measurable_edist_right.comp hf).pow_const q) },
  -- (2) The functions "`q`-th power of distance between `f` and the approximation" are uniformly
  -- bounded, at any given point, by `Î» x, âˆ¥f x - yâ‚€âˆ¥ ^ q`
  have h_bound : âˆ€ n,
    (Î» x, (âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š : â„â‰¥0âˆž) ^ q) â‰¤áµ[Î¼] (Î» x, âˆ¥f x - yâ‚€âˆ¥â‚Š ^ q),
  { exact Î» n, eventually_of_forall
      (Î» x, rpow_le_rpow (coe_mono (nnnorm_approx_on_le hf hâ‚€ x n)) hq.le) },
  -- (3) The bounding function `Î» x, âˆ¥f x - yâ‚€âˆ¥ ^ q` has finite integral
  have h_fin :  âˆ«â» (a : Î²), âˆ¥f a - yâ‚€âˆ¥â‚Š ^ q âˆ‚Î¼ < âŠ¤,
  { exact lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top hq hi },
  -- (4) The functions "`q`-th power of distance between `f` and the approximation" tend pointwise
  -- to zero
  have h_lim : âˆ€áµ (a : Î²) âˆ‚Î¼,
    tendsto (Î» n, (âˆ¥approx_on f hf s yâ‚€ hâ‚€ n a - f aâˆ¥â‚Š : â„â‰¥0âˆž) ^ q) at_top (ð“ 0),
  { filter_upwards [hÎ¼],
    intros a ha,
    have : tendsto (Î» n, (approx_on f hf s yâ‚€ hâ‚€ n) a - f a) at_top (ð“ (f a - f a)),
    { exact (tendsto_approx_on hf hâ‚€ ha).sub tendsto_const_nhds },
    convert ennreal.tendsto_coe.mpr (this.nnnorm.nnrpow tendsto_const_nhds (or.inr hq)),
    { ext1 x,
      rw [ennreal.coe_rpow_of_nonneg _ hq.le] },
    simp [nnreal.zero_rpow hq.ne'] },
  -- Then we apply the Dominated Convergence Theorem
  simpa using tendsto_lintegral_of_dominated_convergence _ hF_meas h_bound h_fin h_lim,
end

lemma mem_â„’p_approx_on [borel_space E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f)
  (hf : mem_â„’p f p Î¼) {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s]
  (hiâ‚€ : mem_â„’p (Î» x, yâ‚€) p Î¼) (n : â„•) :
  mem_â„’p (approx_on f fmeas s yâ‚€ hâ‚€ n) p Î¼ :=
begin
  refine âŸ¨(approx_on f fmeas s yâ‚€ hâ‚€ n).ae_measurable, _âŸ©,
  suffices : snorm (Î» x, approx_on f fmeas s yâ‚€ hâ‚€ n x - yâ‚€) p Î¼ < âŠ¤,
  { have : mem_â„’p (Î» x, approx_on f fmeas s yâ‚€ hâ‚€ n x - yâ‚€) p Î¼ :=
      âŸ¨(approx_on f fmeas s yâ‚€ hâ‚€ n - const Î² yâ‚€).ae_measurable, thisâŸ©,
    convert snorm_add_lt_top this hiâ‚€,
    ext x,
    simp },
  -- We don't necessarily have `mem_â„’p (Î» x, f x - yâ‚€) p Î¼`, because the `ae_measurable` part
  -- requires `ae_measurable.add`, which requires second-countability
  have hf' : mem_â„’p (Î» x, âˆ¥f x - yâ‚€âˆ¥) p Î¼,
  { have h_meas : measurable (Î» x, âˆ¥f x - yâ‚€âˆ¥),
    { simp only [â† dist_eq_norm],
      exact (continuous_id.dist continuous_const).measurable.comp fmeas },
    refine âŸ¨h_meas.ae_measurable, _âŸ©,
    rw snorm_norm,
    convert snorm_add_lt_top hf hiâ‚€.neg,
    ext x,
    simp [sub_eq_add_neg] },
  have : âˆ€áµ x âˆ‚Î¼, âˆ¥approx_on f fmeas s yâ‚€ hâ‚€ n x - yâ‚€âˆ¥ â‰¤ âˆ¥(âˆ¥f x - yâ‚€âˆ¥ + âˆ¥f x - yâ‚€âˆ¥)âˆ¥,
  { refine eventually_of_forall _,
    intros x,
    convert norm_approx_on_yâ‚€_le fmeas hâ‚€ x n,
    rw [real.norm_eq_abs, abs_of_nonneg],
    exact add_nonneg (norm_nonneg _) (norm_nonneg _) },
  calc snorm (Î» x, approx_on f fmeas s yâ‚€ hâ‚€ n x - yâ‚€) p Î¼
      â‰¤ snorm (Î» x, âˆ¥f x - yâ‚€âˆ¥ + âˆ¥f x - yâ‚€âˆ¥) p Î¼ : snorm_mono_ae this
  ... < âŠ¤ : snorm_add_lt_top hf' hf',
end

lemma tendsto_approx_on_univ_Lp_nnnorm [opens_measurable_space E] [second_countable_topology E]
  {f : Î² â†’ E} (hq : 0 < q) {Î¼ : measure Î²} (fmeas : measurable f) (hf : snorm' f q Î¼ < âˆž) :
  tendsto (Î» n, snorm' (approx_on f fmeas univ 0 trivial n - f) q Î¼) at_top (ð“ 0) :=
tendsto_approx_on_Lp_nnnorm fmeas trivial hq (by simp) (by simpa using hf)

lemma mem_â„’p_approx_on_univ [borel_space E] [second_countable_topology E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : mem_â„’p f p Î¼) (n : â„•) :
  mem_â„’p (approx_on f fmeas univ 0 trivial n) p Î¼ :=
mem_â„’p_approx_on fmeas hf (mem_univ _) zero_mem_â„’p n

end Lp

/-! ### L1 approximation by simple functions -/

section integrable
variables [measurable_space Î²]
variables [measurable_space E] [normed_group E]

lemma tendsto_approx_on_L1_nnnorm [opens_measurable_space E]
  {f : Î² â†’ E} (hf : measurable f) {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s]
  {Î¼ : measure Î²} (hÎ¼ : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closure s) (hi : has_finite_integral (Î» x, f x - yâ‚€) Î¼) :
  tendsto (Î» n, âˆ«â» x, âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š âˆ‚Î¼) at_top (ð“ 0) :=
by simpa [snorm'] using
  tendsto_approx_on_Lp_nnnorm hf hâ‚€ zero_lt_one hÎ¼
    (by simpa [snorm', has_finite_integral] using hi)

lemma integrable_approx_on [borel_space E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : integrable f Î¼)
  {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (hiâ‚€ : integrable (Î» x, yâ‚€) Î¼) (n : â„•) :
  integrable (approx_on f fmeas s yâ‚€ hâ‚€ n) Î¼ :=
begin
  rw â† mem_â„’p_one_iff_integrable at hf hiâ‚€ âŠ¢,
  exact mem_â„’p_approx_on fmeas hf hâ‚€ hiâ‚€ n,
end

lemma tendsto_approx_on_univ_L1_nnnorm [opens_measurable_space E] [second_countable_topology E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : integrable f Î¼) :
  tendsto (Î» n, âˆ«â» x, âˆ¥approx_on f fmeas univ 0 trivial n x - f xâˆ¥â‚Š âˆ‚Î¼) at_top (ð“ 0) :=
tendsto_approx_on_L1_nnnorm fmeas trivial (by simp) (by simpa using hf.2)

lemma integrable_approx_on_univ [borel_space E] [second_countable_topology E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : integrable f Î¼) (n : â„•) :
  integrable (approx_on f fmeas univ 0 trivial n) Î¼ :=
integrable_approx_on fmeas hf _ (integrable_zero _ _ _) n

lemma tendsto_approx_on_univ_L1 [borel_space E] [second_countable_topology E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : integrable f Î¼) :
  tendsto (Î» n, (integrable_approx_on_univ fmeas hf n).to_L1 (approx_on f fmeas univ 0 trivial n))
    at_top (ð“ $ hf.to_L1 f) :=
begin
  rw integrable.tendsto_to_L1_iff_tendsto_lintegral_zero,
  convert tendsto_approx_on_univ_L1_nnnorm fmeas hf
end

end integrable

end simple_func

end measure_theory
