/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import measure_theory.l1_space

/-!
# Density of simple functions

Show that each Borel measurable function can be approximated,
both pointwise and in `LÂ¹` norm, by a sequence of simple functions.
-/

open set filter topological_space
open_locale classical topological_space
variables {Î± Î² Î¹ E : Type*}

namespace measure_theory
open ennreal emetric

local infixr ` â†’â‚› `:25 := simple_func

namespace simple_func

variables [measurable_space Î±] [emetric_space Î±] [opens_measurable_space Î±]

/-- `nearest_pt e N x` is the index `k` such that `e k` is the nearest point to `x` among the points
`e 0`, ..., `e N`. If more than one point are at the same distance from `x`, then `nearest_pt e N x`
returns the least of their indexes. -/
noncomputable def nearest_pt_ind (e : â„• â†’ Î±) : â„• â†’ Î± â†’â‚› â„•
| 0 := const Î± 0
| (N + 1) := piecewise (â‹‚ k â‰¤ N, {x | edist (e (N + 1)) x < edist (e k) x})
    (is_measurable.Inter $ Î» k, is_measurable.Inter_Prop $ Î» hk,
      is_measurable_lt measurable_edist_right measurable_edist_right)
    (const Î± $ N + 1) (nearest_pt_ind N)

noncomputable def nearest_pt (e : â„• â†’ Î±) (N : â„•) : Î± â†’â‚› Î± :=
(nearest_pt_ind e N).map e

@[simp] lemma nearest_pt_ind_zero (e : â„• â†’ Î±) : nearest_pt_ind e 0 = const Î± 0 := rfl

@[simp] lemma nearest_pt_zero (e : â„• â†’ Î±) : nearest_pt e 0 = const Î± (e 0) := rfl

lemma nearest_pt_ind_succ (e : â„• â†’ Î±) (N : â„•) (x : Î±) :
  nearest_pt_ind e (N + 1) x =
    if âˆ€ k â‰¤ N, edist (e (N + 1)) x < edist (e k) x
    then N + 1 else nearest_pt_ind e N x :=
by { simp only [nearest_pt_ind, coe_piecewise, set.piecewise], congr, simp }

lemma nearest_pt_ind_le (e : â„• â†’ Î±) (N : â„•) (x : Î±) : nearest_pt_ind e N x â‰¤ N :=
begin
  induction N with N ihN, { simp },
  simp only [nearest_pt_ind_succ],
  split_ifs,
  exacts [le_rfl, ihN.trans N.le_succ]
end

lemma edist_nearest_pt_le (e : â„• â†’ Î±) (x : Î±) {k N : â„•} (hk : k â‰¤ N) :
  edist (nearest_pt e N x) x â‰¤ edist (e k) x :=
begin
  induction N with N ihN generalizing k,
  { simp [le_zero_iff_eq.1 hk, le_refl] },
  { simp only [nearest_pt, nearest_pt_ind_succ, map_apply],
    split_ifs,
    { rcases hk.eq_or_lt with rfl|hk,
      exacts [le_rfl, (h k (nat.lt_succ_iff.1 hk)).le] },
    { push_neg at h,
      rcases h with âŸ¨l, hlN, hxlâŸ©,
      rcases hk.eq_or_lt with rfl|hk,
      exacts [(ihN hlN).trans hxl, ihN (nat.lt_succ_iff.1 hk)] } }
end

lemma tendsto_nearest_pt {e : â„• â†’ Î±} {x : Î±} (hx : x âˆˆ closure (range e)) :
  tendsto (Î» N, nearest_pt e N x) at_top (ð“ x) :=
begin
  refine (at_top_basis.tendsto_iff nhds_basis_eball).2 (Î» Îµ hÎµ, _),
  rcases emetric.mem_closure_iff.1 hx Îµ hÎµ with âŸ¨_, âŸ¨N, rflâŸ©, hNâŸ©,
  rw [edist_comm] at hN,
  exact âŸ¨N, trivial, Î» n hn, (edist_nearest_pt_le e x hn).trans_lt hNâŸ©
end

variables [measurable_space Î²] {f : Î² â†’ Î±}

/-- Approximate a measurable function by a sequence of simple functions `F n` such that
`F n x âˆˆ s`. -/
noncomputable def approx_on (f : Î² â†’ Î±) (hf : measurable f) (s : set Î±) (yâ‚€ : Î±) (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (n : â„•) :
  Î² â†’â‚› Î± :=
by haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©;
  exact comp (nearest_pt (Î» k, nat.cases_on k yâ‚€ (coe âˆ˜ dense_seq s) : â„• â†’ Î±) n) f hf

@[simp] lemma approx_on_zero {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) :
  approx_on f hf s yâ‚€ hâ‚€ 0 x = yâ‚€ :=
rfl

lemma approx_on_mem {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (n : â„•) (x : Î²) :
  approx_on f hf s yâ‚€ hâ‚€ n x âˆˆ s :=
begin
  haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©,
  suffices : âˆ€ n, (nat.cases_on n yâ‚€ (coe âˆ˜ dense_seq s) : Î±) âˆˆ s, { apply this },
  rintro (_|n),
  exacts [hâ‚€, subtype.mem _]
end

lemma tendsto_approx_on {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] {x : Î²} (hx : f x âˆˆ closure s) :
  tendsto (Î» n, approx_on f hf s yâ‚€ hâ‚€ n x) at_top (ð“ $ f x) :=
begin
  haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©,
  rw [â† @subtype.range_coe _ s, â† image_univ, â† dense_seq_dense s] at hx,
  simp only [approx_on, coe_comp],
  refine tendsto_nearest_pt (closure_minimal _ is_closed_closure hx),
  simp only [nat.range_cases_on, closure_union, @range_comp _ _ _ _ coe],
  exact subset.trans (image_closure_subset_closure_image continuous_subtype_coe)
    (subset_union_right _ _)
end

lemma edist_approx_on_le {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) (n : â„•) :
  edist (approx_on f hf s yâ‚€ hâ‚€ n x) (f x) â‰¤ edist yâ‚€ (f x) :=
begin
  dsimp only [approx_on, coe_comp, (âˆ˜)],
  exact edist_nearest_pt_le _ _ (zero_le _)
end

lemma edist_approx_on_y0_le {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) (n : â„•) :
  edist yâ‚€ (approx_on f hf s yâ‚€ hâ‚€ n x) â‰¤ edist yâ‚€ (f x) + edist yâ‚€ (f x) :=
calc edist yâ‚€ (approx_on f hf s yâ‚€ hâ‚€ n x) â‰¤
  edist yâ‚€ (f x) + edist (approx_on f hf s yâ‚€ hâ‚€ n x) (f x) : edist_triangle_right _ _ _
... â‰¤ edist yâ‚€ (f x) + edist yâ‚€ (f x) : add_le_add_left (edist_approx_on_le hf hâ‚€ x n) _

-- Actually, we can avoid `second_countable_topology E` if needed
lemma tendsto_approx_on_l1 [measurable_space E] [normed_group E] [opens_measurable_space E]
  [second_countable_topology E] {f : Î² â†’ E} (hf : measurable f) {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s)
  {Î¼ : measure Î²} (hÎ¼ : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closure s) (hi : integrable (Î» x, f x - yâ‚€) Î¼) :
  tendsto (Î» n, âˆ«â» x, edist (approx_on f hf s yâ‚€ hâ‚€ n x) (f x) âˆ‚Î¼) at_top (ð“ 0) :=
begin
  simp only [integrable, â† nndist_eq_nnnorm, â† edist_nndist, â† edist_comm yâ‚€] at hi,
  convert tendsto_lintegral_of_dominated_convergence _
    (Î» n, (approx_on f hf s yâ‚€ hâ‚€ n).measurable.edist hf)
    (Î» n, eventually_of_forall $ Î» x, edist_approx_on_le hf hâ‚€ x n) hi
    (hÎ¼.mono $ Î» x hx, _),
  show tendsto (Î» n, edist _ (f x)) at_top (ð“ $ edist (f x) (f x)),
    from (tendsto_approx_on hf hâ‚€ hx).edist tendsto_const_nhds,
  simp
end

lemma integrable_approx_on [measurable_space E] [normed_group E] [borel_space E]
  {f : Î² â†’ E} (hf : measurable f) {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s]
  {Î¼ : measure Î²} (hi : integrable (Î» x, f x - yâ‚€) Î¼) (hiâ‚€ : integrable (Î» x, yâ‚€) Î¼) (n : â„•) :
  integrable (approx_on f hf s yâ‚€ hâ‚€ n) Î¼ :=
begin
  suffices : integrable (Î» x, approx_on f hf s yâ‚€ hâ‚€ n x - yâ‚€) Î¼,
  { simpa using this.add (approx_on f hf s yâ‚€ hâ‚€ n - const _ yâ‚€).measurable measurable_const hiâ‚€ },
  simp only [integrable, â† nndist_eq_nnnorm, â† edist_nndist, edist_comm _ yâ‚€] at hi âŠ¢,
  have : measurable (Î» x, edist yâ‚€ (f x)) :=
    (continuous_const.edist continuous_id).measurable.comp hf,
  calc âˆ«â» x, edist yâ‚€ (approx_on f hf s yâ‚€ hâ‚€ n x) âˆ‚Î¼ â‰¤ âˆ«â» x, edist yâ‚€ (f x) + edist yâ‚€ (f x) âˆ‚Î¼ :
    measure_theory.lintegral_mono (Î» x, edist_approx_on_y0_le hf hâ‚€ x n)
  ... = âˆ«â» x, edist yâ‚€ (f x) âˆ‚Î¼ + âˆ«â» x, edist yâ‚€ (f x) âˆ‚Î¼ :
    measure_theory.lintegral_add this this
  ... < âŠ¤ :
    add_lt_top.2 âŸ¨hi, hiâŸ©
end

end simple_func

open simple_func

variables [measurable_space Î±] [emetric_space Î²] [measurable_space Î²] [opens_measurable_space Î²]
  [normed_group E] [measurable_space E] [second_countable_topology E]

lemma simple_func_sequence_tendsto [opens_measurable_space E] {f : Î± â†’ E} (hf : measurable f) :
  âˆƒ (F : â„• â†’ (Î± â†’â‚› E)), âˆ€ x : Î±, tendsto (Î» n, F n x) at_top (ð“ (f x)) :=
âŸ¨approx_on f hf univ 0 trivial, Î» x, tendsto_approx_on hf _ (by simp)âŸ©

lemma simple_func_sequence_tendsto' [borel_space E] {Î¼ : measure Î±} {f : Î± â†’ E}
  (hfm : measurable f) (hfi : integrable f Î¼) :
    âˆƒ (F : â„• â†’ (Î± â†’â‚› E)), (âˆ€n, integrable (F n) Î¼) âˆ§
   tendsto (Î» n, âˆ«â» x,  nndist (F n x) (f x) âˆ‚Î¼) at_top (ð“ 0) :=
âŸ¨approx_on f hfm univ 0 trivial, integrable_approx_on _ _ (by simpa) (integrable_zero _ _ _),
  by { simp only [â† edist_nndist], exact tendsto_approx_on_l1 _ _ (by simp) (by simpa) }âŸ©

end measure_theory
