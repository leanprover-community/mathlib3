/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import measure_theory.l1_space

/-!
# Density of simple functions

Show that each Borel measurable function can be approximated,
both pointwise and in `L¬π` norm, by a sequence of simple functions.

## Main definitions

* `measure_theory.simple_func.nearest_pt (e : ‚Ñï ‚Üí Œ±) (N : ‚Ñï) : Œ± ‚Üí‚Çõ ‚Ñï`: the `simple_func` sending
  each `x : Œ±` to the point `e k` which is the nearest to `x` among `e 0`, ..., `e N`.
* `measure_theory.simple_func.approx_on (f : Œ≤ ‚Üí Œ±) (hf : measurable f) (s : set Œ±) (y‚ÇÄ : Œ±)
  (h‚ÇÄ : y‚ÇÄ ‚àà s) [separable_space s] (n : ‚Ñï) : Œ≤ ‚Üí‚Çõ Œ±` : a simple function that takes values in `s`
  and approximates `f`. If `f x ‚àà s`, then `measure_theory.simple_func.approx_on f hf s y‚ÇÄ h‚ÇÄ n x`
  tends to `f x` as `n` tends to `‚àû`. If `Œ±` is a `normed_group`, `f x - y‚ÇÄ`
  is `measure_theory.integrable`, and `f x ‚àà s` for a.e. `x`, then
  `simple_func.approx_on f hf s y‚ÇÄ h‚ÇÄ n` tends to `f` in `L‚ÇÅ`. The main use case is `s = univ`,
  `y‚ÇÄ = 0`.

## Notations

* `Œ± ‚Üí‚Çõ Œ≤` (local notation): the type of simple functions `Œ± ‚Üí Œ≤`.
-/

open set filter topological_space
open_locale classical topological_space ennreal
variables {Œ± Œ≤ Œπ E : Type*}

namespace measure_theory
open ennreal emetric

local infixr ` ‚Üí‚Çõ `:25 := simple_func

namespace simple_func

variables [measurable_space Œ±] [emetric_space Œ±] [opens_measurable_space Œ±]

/-- `nearest_pt_ind e N x` is the index `k` such that `e k` is the nearest point to `x` among the
points `e 0`, ..., `e N`. If more than one point are at the same distance from `x`, then
`nearest_pt_ind e N x` returns the least of their indexes. -/
noncomputable def nearest_pt_ind (e : ‚Ñï ‚Üí Œ±) : ‚Ñï ‚Üí Œ± ‚Üí‚Çõ ‚Ñï
| 0 := const Œ± 0
| (N + 1) := piecewise (‚ãÇ k ‚â§ N, {x | edist (e (N + 1)) x < edist (e k) x})
    (measurable_set.Inter $ Œª k, measurable_set.Inter_Prop $ Œª hk,
      measurable_set_lt measurable_edist_right measurable_edist_right)
    (const Œ± $ N + 1) (nearest_pt_ind N)

/-- `nearest_pt e N x` is the nearest point to `x` among the points `e 0`, ..., `e N`. If more than
one point are at the same distance from `x`, then `nearest_pt e N x` returns the point with the
least possible index. -/
noncomputable def nearest_pt (e : ‚Ñï ‚Üí Œ±) (N : ‚Ñï) : Œ± ‚Üí‚Çõ Œ± :=
(nearest_pt_ind e N).map e

@[simp] lemma nearest_pt_ind_zero (e : ‚Ñï ‚Üí Œ±) : nearest_pt_ind e 0 = const Œ± 0 := rfl

@[simp] lemma nearest_pt_zero (e : ‚Ñï ‚Üí Œ±) : nearest_pt e 0 = const Œ± (e 0) := rfl

lemma nearest_pt_ind_succ (e : ‚Ñï ‚Üí Œ±) (N : ‚Ñï) (x : Œ±) :
  nearest_pt_ind e (N + 1) x =
    if ‚àÄ k ‚â§ N, edist (e (N + 1)) x < edist (e k) x
    then N + 1 else nearest_pt_ind e N x :=
by { simp only [nearest_pt_ind, coe_piecewise, set.piecewise], congr, simp }

lemma nearest_pt_ind_le (e : ‚Ñï ‚Üí Œ±) (N : ‚Ñï) (x : Œ±) : nearest_pt_ind e N x ‚â§ N :=
begin
  induction N with N ihN, { simp },
  simp only [nearest_pt_ind_succ],
  split_ifs,
  exacts [le_rfl, ihN.trans N.le_succ]
end

lemma edist_nearest_pt_le (e : ‚Ñï ‚Üí Œ±) (x : Œ±) {k N : ‚Ñï} (hk : k ‚â§ N) :
  edist (nearest_pt e N x) x ‚â§ edist (e k) x :=
begin
  induction N with N ihN generalizing k,
  { simp [nonpos_iff_eq_zero.1 hk, le_refl] },
  { simp only [nearest_pt, nearest_pt_ind_succ, map_apply],
    split_ifs,
    { rcases hk.eq_or_lt with rfl|hk,
      exacts [le_rfl, (h k (nat.lt_succ_iff.1 hk)).le] },
    { push_neg at h,
      rcases h with ‚ü®l, hlN, hxl‚ü©,
      rcases hk.eq_or_lt with rfl|hk,
      exacts [(ihN hlN).trans hxl, ihN (nat.lt_succ_iff.1 hk)] } }
end

lemma tendsto_nearest_pt {e : ‚Ñï ‚Üí Œ±} {x : Œ±} (hx : x ‚àà closure (range e)) :
  tendsto (Œª N, nearest_pt e N x) at_top (ùìù x) :=
begin
  refine (at_top_basis.tendsto_iff nhds_basis_eball).2 (Œª Œµ hŒµ, _),
  rcases emetric.mem_closure_iff.1 hx Œµ hŒµ with ‚ü®_, ‚ü®N, rfl‚ü©, hN‚ü©,
  rw [edist_comm] at hN,
  exact ‚ü®N, trivial, Œª n hn, (edist_nearest_pt_le e x hn).trans_lt hN‚ü©
end

variables [measurable_space Œ≤] {f : Œ≤ ‚Üí Œ±}

/-- Approximate a measurable function by a sequence of simple functions `F n` such that
`F n x ‚àà s`. -/
noncomputable def approx_on (f : Œ≤ ‚Üí Œ±) (hf : measurable f) (s : set Œ±) (y‚ÇÄ : Œ±) (h‚ÇÄ : y‚ÇÄ ‚àà s)
  [separable_space s] (n : ‚Ñï) :
  Œ≤ ‚Üí‚Çõ Œ± :=
by haveI : nonempty s := ‚ü®‚ü®y‚ÇÄ, h‚ÇÄ‚ü©‚ü©;
  exact comp (nearest_pt (Œª k, nat.cases_on k y‚ÇÄ (coe ‚àò dense_seq s) : ‚Ñï ‚Üí Œ±) n) f hf

@[simp] lemma approx_on_zero {f : Œ≤ ‚Üí Œ±} (hf : measurable f) {s : set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
  [separable_space s] (x : Œ≤) :
  approx_on f hf s y‚ÇÄ h‚ÇÄ 0 x = y‚ÇÄ :=
rfl

lemma approx_on_mem {f : Œ≤ ‚Üí Œ±} (hf : measurable f) {s : set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
  [separable_space s] (n : ‚Ñï) (x : Œ≤) :
  approx_on f hf s y‚ÇÄ h‚ÇÄ n x ‚àà s :=
begin
  haveI : nonempty s := ‚ü®‚ü®y‚ÇÄ, h‚ÇÄ‚ü©‚ü©,
  suffices : ‚àÄ n, (nat.cases_on n y‚ÇÄ (coe ‚àò dense_seq s) : Œ±) ‚àà s, { apply this },
  rintro (_|n),
  exacts [h‚ÇÄ, subtype.mem _]
end

@[simp] lemma approx_on_comp {Œ≥ : Type*} [measurable_space Œ≥] {f : Œ≤ ‚Üí Œ±} (hf : measurable f)
  {g : Œ≥ ‚Üí Œ≤} (hg : measurable g) {s : set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s) [separable_space s] (n : ‚Ñï) :
  approx_on (f ‚àò g) (hf.comp hg) s y‚ÇÄ h‚ÇÄ n = (approx_on f hf s y‚ÇÄ h‚ÇÄ n).comp g hg :=
rfl

lemma tendsto_approx_on {f : Œ≤ ‚Üí Œ±} (hf : measurable f) {s : set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
  [separable_space s] {x : Œ≤} (hx : f x ‚àà closure s) :
  tendsto (Œª n, approx_on f hf s y‚ÇÄ h‚ÇÄ n x) at_top (ùìù $ f x) :=
begin
  haveI : nonempty s := ‚ü®‚ü®y‚ÇÄ, h‚ÇÄ‚ü©‚ü©,
  rw [‚Üê @subtype.range_coe _ s, ‚Üê image_univ, ‚Üê (dense_range_dense_seq s).closure_eq] at hx,
  simp only [approx_on, coe_comp],
  refine tendsto_nearest_pt (closure_minimal _ is_closed_closure hx),
  simp only [nat.range_cases_on, closure_union, range_comp coe],
  exact subset.trans (image_closure_subset_closure_image continuous_subtype_coe)
    (subset_union_right _ _)
end

lemma edist_approx_on_le {f : Œ≤ ‚Üí Œ±} (hf : measurable f) {s : set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
  [separable_space s] (x : Œ≤) (n : ‚Ñï) :
  edist (approx_on f hf s y‚ÇÄ h‚ÇÄ n x) (f x) ‚â§ edist y‚ÇÄ (f x) :=
begin
  dsimp only [approx_on, coe_comp, (‚àò)],
  exact edist_nearest_pt_le _ _ (zero_le _)
end

lemma edist_approx_on_y0_le {f : Œ≤ ‚Üí Œ±} (hf : measurable f) {s : set Œ±} {y‚ÇÄ : Œ±} (h‚ÇÄ : y‚ÇÄ ‚àà s)
  [separable_space s] (x : Œ≤) (n : ‚Ñï) :
  edist y‚ÇÄ (approx_on f hf s y‚ÇÄ h‚ÇÄ n x) ‚â§ edist y‚ÇÄ (f x) + edist y‚ÇÄ (f x) :=
calc edist y‚ÇÄ (approx_on f hf s y‚ÇÄ h‚ÇÄ n x) ‚â§
  edist y‚ÇÄ (f x) + edist (approx_on f hf s y‚ÇÄ h‚ÇÄ n x) (f x) : edist_triangle_right _ _ _
... ‚â§ edist y‚ÇÄ (f x) + edist y‚ÇÄ (f x) : add_le_add_left (edist_approx_on_le hf h‚ÇÄ x n) _

variables [measurable_space E] [normed_group E]

lemma norm_approx_on_zero_le [opens_measurable_space E] {f : Œ≤ ‚Üí E} (hf : measurable f)
  {s : set E} (h‚ÇÄ : (0 : E) ‚àà s) [separable_space s] (x : Œ≤) (n : ‚Ñï) :
  ‚à•approx_on f hf s 0 h‚ÇÄ n x‚à• ‚â§ ‚à•f x‚à• + ‚à•f x‚à• :=
begin
  have := edist_approx_on_y0_le hf h‚ÇÄ x n,
  simp [edist_comm (0 : E), edist_eq_coe_nnnorm] at this,
  exact_mod_cast this,
end

lemma tendsto_approx_on_L1_edist  [opens_measurable_space E]
 {f : Œ≤ ‚Üí E} (hf : measurable f) {s : set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [separable_space s]
  {Œº : measure Œ≤} (hŒº : ‚àÄ·µê x ‚àÇŒº, f x ‚àà closure s) (hi : has_finite_integral (Œª x, f x - y‚ÇÄ) Œº) :
  tendsto (Œª n, ‚à´‚Åª x, edist (approx_on f hf s y‚ÇÄ h‚ÇÄ n x) (f x) ‚àÇŒº) at_top (ùìù 0) :=
begin
  simp only [has_finite_integral, ‚Üê nndist_eq_nnnorm, ‚Üê edist_nndist, ‚Üê edist_comm y‚ÇÄ] at hi,
  have : ‚àÄ n, measurable (Œª x, edist (approx_on f hf s y‚ÇÄ h‚ÇÄ n x) (f x)) :=
    Œª n, (approx_on f hf s y‚ÇÄ h‚ÇÄ n).measurable_bind (Œª y x, edist y (f x))
      (Œª y, measurable_edist_right.comp hf),
  convert tendsto_lintegral_of_dominated_convergence _ this
    (Œª n, eventually_of_forall $ Œª x, edist_approx_on_le hf h‚ÇÄ x n) hi (hŒº.mono $ Œª x hx, _),
  show tendsto (Œª n, edist _ (f x)) at_top (ùìù $ edist (f x) (f x)),
    from (tendsto_approx_on hf h‚ÇÄ hx).edist tendsto_const_nhds,
  simp
end

lemma integrable_approx_on [borel_space E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (fmeas : measurable f) (hf : integrable f Œº)
  {s : set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s)
  [separable_space s] (hi‚ÇÄ : integrable (Œª x, y‚ÇÄ) Œº) (n : ‚Ñï) :
  integrable (approx_on f fmeas s y‚ÇÄ h‚ÇÄ n) Œº :=
begin
  refine ‚ü®(approx_on f fmeas s y‚ÇÄ h‚ÇÄ n).ae_measurable, _‚ü©,
  suffices : integrable (Œª x, approx_on f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ) Œº,
  { convert this.add' hi‚ÇÄ, ext1 x, simp },
  refine ‚ü®(approx_on f fmeas s y‚ÇÄ h‚ÇÄ n - const Œ≤ y‚ÇÄ).ae_measurable, _‚ü©,
  have hi := hf.sub' hi‚ÇÄ,
  simp only [has_finite_integral, ‚Üê nndist_eq_nnnorm, ‚Üê edist_nndist, edist_comm _ y‚ÇÄ,
    pi.sub_apply] at hi ‚ä¢,
  have : measurable (Œª x, edist y‚ÇÄ (f x)) := measurable_edist_right.comp fmeas,
  calc
  ‚à´‚Åª x, edist y‚ÇÄ (approx_on f fmeas s y‚ÇÄ h‚ÇÄ n x) ‚àÇŒº ‚â§ ‚à´‚Åª x, edist y‚ÇÄ (f x) + edist y‚ÇÄ (f x) ‚àÇŒº :
    measure_theory.lintegral_mono (Œª x, edist_approx_on_y0_le fmeas h‚ÇÄ x n)
  ... = ‚à´‚Åª x, edist y‚ÇÄ (f x) ‚àÇŒº + ‚à´‚Åª x, edist y‚ÇÄ (f x) ‚àÇŒº :
    measure_theory.lintegral_add this this
  ... < ‚àû :
    add_lt_top.2 ‚ü®hi, hi‚ü©
end

lemma tendsto_approx_on_univ_L1_edist [opens_measurable_space E] [second_countable_topology E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (fmeas : measurable f) (hf : integrable f Œº) :
  tendsto (Œª n, ‚à´‚Åª x, edist (approx_on f fmeas univ 0 trivial n x) (f x) ‚àÇŒº) at_top (ùìù 0) :=
tendsto_approx_on_L1_edist fmeas trivial (by simp) (by simpa using hf.2)

lemma integrable_approx_on_univ [borel_space E] [second_countable_topology E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (fmeas : measurable f) (hf : integrable f Œº) (n : ‚Ñï) :
  integrable (approx_on f fmeas univ 0 trivial n) Œº :=
integrable_approx_on fmeas hf _ (integrable_zero _ _ _) n

lemma tendsto_approx_on_univ_L1 [borel_space E] [second_countable_topology E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (fmeas : measurable f) (hf : integrable f Œº) :
  tendsto (Œª n, integrable.to_L1 (approx_on f fmeas univ 0 trivial n)
    (integrable_approx_on_univ fmeas hf n)) at_top (ùìù $ hf.to_L1 f) :=
tendsto_iff_edist_tendsto_0.2 $  by simpa using tendsto_approx_on_univ_L1_edist fmeas hf

end simple_func

end measure_theory
