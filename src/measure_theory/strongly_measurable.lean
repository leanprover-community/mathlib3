/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/

import measure_theory.simple_func_dense

/-!
# Strongly measurable functions

A function `f` is said to be strongly measurable with respect to a measure `Î¼` if `f` is the
sequential limit of simple functions whose support has finite measure.

Functions in `Lp` for `0 < p < âˆž` are strongly measurable.
If the measure is sigma-finite, measurable and strongly measurable are equivalent.

The main property of strongly measurable functions is `strongly_measurable.exists_set_sigma_finite`:
there exists a measurable set `t` such that `f` is supported on `t` and `Î¼.restrict t` is
sigma-finite. As a consequence, we can prove some results for those functions as if the measure was
sigma-finite.

## Main definitions

* `strongly_measurable f Î¼`: `f : Î± â†’ Î³` is the limit of a sequence `fs : â„• â†’ simple_func Î± Î³`
  such that for all `n âˆˆ â„•`, the measure of the support of `fs n` is finite.
* `strongly_measurable.sigma_finite_set`: a measurable set `t` such that `âˆ€ x âˆˆ tá¶œ, f x = 0` and
  `Î¼.restrict t` is sigma-finite.

## Main statements

* `strongly_measurable.exists_set_sigma_finite`: if a function `f` is strongly measurable with
  respect to a measure `Î¼`, then there exists a measurable set `t` such that `âˆ€ x âˆˆ tá¶œ, f x = 0`
  and `sigma_finite (Î¼.restrict t)`.
* `mem_â„’p.ae_strongly_measurable`: if `mem_â„’p f p Î¼` with `0 < p < âˆž`, then
  `âˆƒ g, strongly_measurable g Î¼ âˆ§ f =áµ[Î¼] g`.
* `Lp.strongly_measurable`: for `0 < p < âˆž`, `Lp` functions are strongly measurable.
* `stongly_measurable.measurable`: a stongly measurable function is measurable.
* `measurable.strongly_measurable`: if a measure is sigma-finite, then all measurable functions are
  strongly measurable.

## References

* HytÃ¶nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.

-/

open measure_theory filter topological_space
open_locale ennreal topological_space measure_theory

namespace measure_theory

/-- A function is `strongly_measurable` with respect to a measure if it is the limit of simple
  functions with support with finite measure. -/
def strongly_measurable {Î± Î³} [topological_space Î³] [has_zero Î³] {m0 : measurable_space Î±}
  [decidable_pred (Î» (y : Î³), y â‰  0)] (f : Î± â†’ Î³) (Î¼ : measure Î±) : Prop :=
âˆƒ fs : â„• â†’ simple_func Î± Î³,
  (âˆ€ n, Î¼ (â‹ƒ y âˆˆ finset.filter (Î» (y : Î³), y â‰  0) (fs n).range, (fs n) â»Â¹' {y}) < âˆž)
  âˆ§ (âˆ€ x, tendsto (Î» n, fs n x) at_top (ð“ (f x)))

namespace strongly_measurable

variables {Î± H : Type*} {m0 : measurable_space Î±} [normed_group H]
  {Î¼ : measure Î±} [decidable_pred (Î» (y : H), y â‰  0)] {f : Î± â†’ H}

lemma exists_set_sigma_finite (hf : strongly_measurable f Î¼) :
  âˆƒ t, measurable_set t âˆ§ (âˆ€ x âˆˆ tá¶œ, f x = 0) âˆ§ sigma_finite (Î¼.restrict t) :=
begin
  rcases hf with âŸ¨fs, hT_lt_top, h_approxâŸ©,
  let T := Î» n, â‹ƒ y âˆˆ finset.filter (Î» (y : H), y â‰  0) (fs n).range, (fs n) â»Â¹' {y},
  have hT_meas : âˆ€ n, measurable_set (T n),
    from Î» n, finset.measurable_set_bUnion _ (Î» y hy, simple_func.measurable_set_fiber _ _),
  let t := â‹ƒ n, T n,
  refine âŸ¨t, measurable_set.Union hT_meas, _, _âŸ©,
  { have h_fs_zero : âˆ€ n, âˆ€ x âˆˆ tá¶œ, fs n x = 0,
    { refine Î» n x hxt, _,
      simp only [true_and, exists_prop, set.mem_preimage, set.mem_Union, set.mem_range,
        set.mem_singleton_iff, not_exists_not, exists_eq_right', finset.mem_filter,
        set.mem_compl_eq, simple_func.mem_range, exists_apply_eq_apply] at hxt,
      exact hxt n, },
    refine Î» x hxt, tendsto_nhds_unique (h_approx x) _,
    rw funext (Î» n, h_fs_zero n x hxt),
    exact tendsto_const_nhds, },
  { refine measure.finite_spanning_sets_in.sigma_finite _ _,
    { exact set.range (Î» n, tá¶œ âˆª T n), },
    { refine âŸ¨Î» n, tá¶œ âˆª T n, Î» n, set.mem_range_self _, Î» n, _, _âŸ©,
      { rw [measure.restrict_apply' (measurable_set.Union hT_meas), set.union_inter_distrib_right,
          set.compl_inter_self t, set.empty_union],
        exact (measure_mono (set.inter_subset_left _ _)).trans_lt (hT_lt_top n), },
      rw â† set.union_Union tá¶œ T,
      exact set.compl_union_self _, },
    { intros s hs,
      rw set.mem_range at hs,
      cases hs with n hsn,
      rw â† hsn,
      exact (measurable_set.compl (measurable_set.Union hT_meas)).union (hT_meas n), }, },
end

/-- A measurable set `t` such that `f =áµ[Î¼.restrict tá¶œ] 0` and `sigma_finite (Î¼.restrict t)`. -/
def sigma_finite_set (hf : strongly_measurable f Î¼) : set Î± := hf.exists_set_sigma_finite.some

protected lemma measurable_set (hf : strongly_measurable f Î¼) :
  measurable_set hf.sigma_finite_set :=
hf.exists_set_sigma_finite.some_spec.1

lemma eq_zero_compl (hf : strongly_measurable f Î¼) : âˆ€ x âˆˆ hf.sigma_finite_setá¶œ, f x = 0 :=
hf.exists_set_sigma_finite.some_spec.2.1

lemma ae_eq_zero_compl (hf : strongly_measurable f Î¼) : f =áµ[Î¼.restrict hf.sigma_finite_setá¶œ] 0 :=
begin
  rw [eventually_eq, ae_restrict_iff' hf.measurable_set.compl],
  refine eventually_of_forall (Î» x hx, _),
  exact hf.eq_zero_compl x hx,
end

lemma sigma_finite_restrict (hf : strongly_measurable f Î¼) :
  sigma_finite (Î¼.restrict hf.sigma_finite_set) :=
hf.exists_set_sigma_finite.some_spec.2.2

/-- A strongly measurable function is measurable. -/
protected lemma measurable [measurable_space H] [borel_space H] (hf : strongly_measurable f Î¼) :
  measurable f :=
measurable_of_tendsto_metric (Î» n, (hf.some n).measurable) (tendsto_pi.mpr hf.some_spec.2)

end strongly_measurable

section strongly_measurable_of

variables {Î± G : Type*} [measurable_space G] [emetric_space G] [has_zero G]
  [second_countable_topology G] [opens_measurable_space G] [decidable_pred (Î» (y : G), y â‰  0)]
  {f : Î± â†’ G} {m0 : measurable_space Î±} {Î¼ : measure Î±}

lemma strongly_measurable_of_measurable_of_exists_set_sigma_finite (hf_meas : measurable f)
  (hf : âˆƒ t, measurable_set t âˆ§ (âˆ€ x âˆˆ tá¶œ, f x = 0) âˆ§ sigma_finite (Î¼.restrict t)) :
  strongly_measurable f Î¼ :=
begin
  obtain âŸ¨t, ht, hft_zero, htÎ¼âŸ© := hf,
  haveI : sigma_finite (Î¼.restrict t) := htÎ¼,
  let S := spanning_sets (Î¼.restrict t),
  have hS_meas : âˆ€ n, measurable_set (S n), from measurable_spanning_sets (Î¼.restrict t),
  let f_approx := simple_func.approx_on f hf_meas set.univ 0 (set.mem_univ _),
  let fs := Î» n, simple_func.restrict (f_approx n) (S n âˆ© t),
  have h_fs_t_compl : âˆ€ n, âˆ€ x âˆ‰ t, fs n x = 0,
  { intros n x hxt,
    rw simple_func.restrict_apply _ ((hS_meas n).inter ht),
    refine set.indicator_of_not_mem _ _,
    simp [hxt], },
  refine âŸ¨fs, _, Î» x, _âŸ©,
  { refine Î» n, (measure_bUnion_finset_le _ _).trans_lt _,
    refine ennreal.sum_lt_top_iff.mpr (Î» y hy, _),
    rw simple_func.restrict_preimage_singleton _ ((hS_meas n).inter ht) (finset.mem_filter.mp hy).2,
    refine (measure_mono (set.inter_subset_left _ _)).trans_lt _,
    have h_lt_top := measure_spanning_sets_lt_top (Î¼.restrict t) n,
    rwa measure.restrict_apply' ht at h_lt_top, },
  { by_cases hxt : x âˆˆ t,
    swap, { rw [funext (Î» n, h_fs_t_compl n x hxt), hft_zero x hxt], exact tendsto_const_nhds, },
    have h : tendsto (Î» n, (simple_func.approx_on f hf_meas set.univ 0 _ n) x) at_top (ð“ (f x)),
      from simple_func.tendsto_approx_on hf_meas (set.mem_univ 0) (by simp),
    obtain âŸ¨nâ‚, hnâ‚âŸ© : âˆƒ n, âˆ€ m, n â‰¤ m â†’ fs m x = f_approx m x,
    { obtain âŸ¨n, hnâŸ© : âˆƒ n, âˆ€ m, n â‰¤ m â†’ x âˆˆ S m âˆ© t,
      { suffices : âˆƒ n, âˆ€ m, n â‰¤ m â†’ x âˆˆ S m,
        { obtain âŸ¨n, hnâŸ© := this,
          exact âŸ¨n, Î» m hnm, set.mem_inter (hn m hnm) hxtâŸ©, },
        suffices : âˆƒ n, x âˆˆ S n,
        { rcases this with âŸ¨n, hnâŸ©,
          exact âŸ¨n, Î» m hnm, monotone_spanning_sets (Î¼.restrict t) hnm hnâŸ©, },
        rw [â† set.mem_Union, Union_spanning_sets (Î¼.restrict t)],
        trivial, },
      refine âŸ¨n, Î» m hnm, _âŸ©,
      simp_rw [fs, simple_func.restrict_apply _ ((hS_meas m).inter ht),
        set.indicator_of_mem (hn m hnm)], },
    rw tendsto_at_top' at h âŠ¢,
    intros s hs,
    obtain âŸ¨nâ‚‚, hnâ‚‚âŸ© := h s hs,
    refine âŸ¨max nâ‚ nâ‚‚, Î» m hm, _âŸ©,
    rw hnâ‚ m ((le_max_left _ _).trans hm.le),
    exact hnâ‚‚ m ((le_max_right _ _).trans hm.le), },
end

/-- If the measure is sigma-finite, all measurable functions are strongly measurable. -/
lemma measurable.strongly_measurable {m0 : measurable_space Î±} (hf : measurable f) (Î¼ : measure Î±)
  [sigma_finite Î¼]  :
  strongly_measurable f Î¼ :=
strongly_measurable_of_measurable_of_exists_set_sigma_finite hf
  âŸ¨set.univ, measurable_set.univ, by simp, by rwa measure.restrict_univâŸ©

end strongly_measurable_of

variables {Î± G : Type*} {p : â„â‰¥0âˆž} {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  [normed_group G] [measurable_space G] [borel_space G] [second_countable_topology G]
  [decidable_pred (Î» (y : G), y â‰  0)]
  {f : Î± â†’ G}

lemma strongly_measurable_iff_measurable_and_exists_set_sigma_finite
  {m0 : measurable_space Î±} (Î¼ : measure Î±) :
  strongly_measurable f Î¼ â†” measurable f
    âˆ§ (âˆƒ t, measurable_set t âˆ§ (âˆ€ x âˆˆ tá¶œ, f x = 0) âˆ§ sigma_finite (Î¼.restrict t)) :=
âŸ¨Î» h, âŸ¨h.measurable, h.exists_set_sigma_finiteâŸ©,
  Î» h, strongly_measurable_of_measurable_of_exists_set_sigma_finite h.1 h.2âŸ©

/-- If the measure is sigma-finite, strongly measurable and measurable are equivalent. -/
lemma strongly_measurable_iff_measurable {m0 : measurable_space Î±} (Î¼ : measure Î±)
  [sigma_finite Î¼] :
  strongly_measurable f Î¼ â†” measurable f :=
âŸ¨Î» h, h.measurable, Î» h, measurable.strongly_measurable h Î¼âŸ©

lemma mem_â„’p.strongly_measurable_of_measurable (hf : mem_â„’p f p Î¼) (hf_meas : measurable f)
  (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆž) :
  strongly_measurable f Î¼ :=
begin
  let fs := simple_func.approx_on f hf_meas set.univ 0 (set.mem_univ _),
  refine âŸ¨fs, _, _âŸ©,
  { have h_fs_Lp : âˆ€ n, mem_â„’p (fs n) p Î¼, from simple_func.mem_â„’p_approx_on_univ hf_meas hf,
    refine Î» n, (measure_bUnion_finset_le _ _).trans_lt (ennreal.sum_lt_top_iff.mpr (Î» y hy, _)),
    exact simple_func.measure_preimage_lt_top_of_mem_â„’p (pos_iff_ne_zero.mpr hp_ne_zero) hp_ne_top
      (fs n) (h_fs_Lp n) _ (finset.mem_filter.mp hy).2, },
  { exact Î» x, simple_func.tendsto_approx_on hf_meas (set.mem_univ 0) (by simp), },
end

lemma mem_â„’p.ae_strongly_measurable (hf : mem_â„’p f p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆž) :
  âˆƒ g, strongly_measurable g Î¼ âˆ§ f =áµ[Î¼] g :=
âŸ¨hf.ae_measurable.mk f,
  ((mem_â„’p_congr_ae hf.ae_measurable.ae_eq_mk).mp hf).strongly_measurable_of_measurable
    hf.ae_measurable.measurable_mk hp_ne_zero hp_ne_top,
  hf.ae_measurable.ae_eq_mkâŸ©

lemma integrable.ae_strongly_measurable (hf : integrable f Î¼) :
  âˆƒ g, strongly_measurable g Î¼ âˆ§ f =áµ[Î¼] g :=
(mem_â„’p_one_iff_integrable.mpr hf).ae_strongly_measurable one_ne_zero ennreal.coe_ne_top

lemma Lp.strongly_measurable (f : Lp G p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆž) :
  strongly_measurable f Î¼ :=
(Lp.mem_â„’p f).strongly_measurable_of_measurable (Lp.measurable f) hp_ne_zero hp_ne_top

lemma exists_set_sigma_finite_of_ae_strongly_measurable
  (hf : âˆƒ g, strongly_measurable g Î¼ âˆ§ f =áµ[Î¼] g) :
  âˆƒ t, measurable_set t âˆ§ f =áµ[Î¼.restrict tá¶œ] 0 âˆ§ sigma_finite (Î¼.restrict t) :=
begin
  rcases hf with âŸ¨g, hg, hfgâŸ©,
  refine âŸ¨hg.sigma_finite_set, hg.measurable_set, _, hg.sigma_finite_restrictâŸ©,
  refine eventually_eq.trans (ae_restrict_of_ae hfg) _,
  rw [eventually_eq, ae_restrict_iff],
  { exact eventually_of_forall hg.eq_zero_compl, },
  { exact measurable_set_eq_fun hg.measurable measurable_zero, },
end

end measure_theory
