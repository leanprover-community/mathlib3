/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/

import measure_theory.simple_func_dense

/-!
# Strongly measurable functions

A function `f` is said to be strongly measurable with respect to a measure `Œº` if `f` is the
sequential limit of simple functions whose support has finite measure.

If the measure is sigma-finite, measurable and strongly measurable are equivalent.

The main property of strongly measurable functions is `strongly_measurable.exists_set_sigma_finite`:
there exists a measurable set such that  `f =·µê[Œº.restrict t·∂ú] 0` and `Œº.restrict t` is sigma-finite.
As a consequence, we can prove some results for those functions as if the measure was sigma-finite.

## Main definitions

* `strongly_measurable f Œº` : `f : Œ± ‚Üí Œ≥` is the limit of a sequence `fs : ‚Ñï ‚Üí simple_func Œ± Œ≥`
  such that for all `n ‚àà ‚Ñï`, the measure of the support of `fs n` is finite.

## Main statements

* `strongly_measurable.exists_set_sigma_finite`: if a function `f` is strongly measurable with
  respect to a measure `Œº`, then there exists a measurable set `t` such that `f =·µê[Œº.restrict t·∂ú] 0`
  and `sigma_finite (Œº.restrict t)`.
* `mem_‚Ñíp.ae_strongly_measurable`: if `mem_‚Ñíp f p Œº` with `0 < p < ‚àû`, then
  `‚àÉ g, strongly_measurable g Œº ‚àß f =·µê[Œº] g`.
* `Lp.strongly_measurable`: for `0 < p < ‚àû`, `Lp` functions are strongly measurable.
* `stongly_measurable.measurable`: a stongly measurable function is measurable.
* `measurable.strongly_measurable`: if a measure is sigma-finite, then all measurable functions are
  strongly measurable.

## References

* Hyt√∂nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.

-/

open measure_theory filter topological_space
open_locale ennreal topological_space measure_theory

namespace measure_theory

/-- A function is `strongly_measurable` with respect to a measure if it is the limit of simple
  functions with support with finite measure. -/
def strongly_measurable {Œ± Œ≥} [topological_space Œ≥] [has_zero Œ≥] {m0 : measurable_space Œ±}
  [decidable_pred (Œª (y : Œ≥), y ‚â† 0)] (f : Œ± ‚Üí Œ≥) (Œº : measure Œ±) : Prop :=
‚àÉ fs : ‚Ñï ‚Üí simple_func Œ± Œ≥,
  (‚àÄ n, Œº (‚ãÉ y ‚àà finset.filter (Œª (y : Œ≥), y ‚â† 0) (fs n).range, (fs n) ‚Åª¬π' {y}) < ‚àû)
  ‚àß (‚àÄ x, tendsto (Œª n, fs n x) at_top (ùìù (f x)))

namespace strongly_measurable

variables {Œ± H : Type*} {m0 : measurable_space Œ±} [normed_group H] [measurable_space H]
  {Œº : measure Œ±}

lemma measurable [decidable_pred (Œª (y : H), y ‚â† 0)] [borel_space H] {f : Œ± ‚Üí H}
  (hf : strongly_measurable f Œº) : measurable f :=
measurable_of_tendsto_metric (Œª n, (hf.some n).measurable) (tendsto_pi.mpr hf.some_spec.2)

variables [decidable_pred (Œª (y : H), y ‚â† 0)] [has_measurable_sub‚ÇÇ H] [measurable_singleton_class H]
  {f : Œ± ‚Üí H}

lemma exists_set_sigma_finite (hf : strongly_measurable f Œº) :
  ‚àÉ t, measurable_set t ‚àß f =·µê[Œº.restrict t·∂ú] 0 ‚àß sigma_finite (Œº.restrict t) :=
begin
  rcases hf with ‚ü®fs, hT_lt_top, h_approx‚ü©,
  let T := Œª n, ‚ãÉ y ‚àà finset.filter (Œª (y : H), y ‚â† 0) (fs n).range, (fs n) ‚Åª¬π' {y},
  have hT_meas : ‚àÄ n, measurable_set (T n),
    from Œª n, finset.measurable_set_bUnion _ (Œª y hy, simple_func.measurable_set_fiber _ _),
  let t := ‚ãÉ n, T n,
  refine ‚ü®t, measurable_set.Union hT_meas, _, _‚ü©,
  { have h_fs_zero : ‚àÄ n, fs n =·µê[Œº.restrict t·∂ú] 0,
    { refine Œª n, (ae_restrict_iff (measurable_set_eq_fun (fs n).measurable measurable_zero)).mpr _,
      refine eventually_of_forall (Œª x hxt, _),
      simp only [true_and, exists_prop, set.mem_preimage, set.mem_Union, set.mem_range,
        set.mem_singleton_iff, not_exists_not, exists_eq_right', finset.mem_filter,
        set.mem_compl_eq, simple_func.mem_range, exists_apply_eq_apply] at hxt,
      exact hxt n, },
    simp_rw [eventually_eq, ‚Üê ae_all_iff] at h_fs_zero,
    refine h_fs_zero.mono (Œª x hx, _),
    refine tendsto_nhds_unique (h_approx x) _,
    rw funext (Œª n, hx n),
    exact tendsto_const_nhds, },
  { refine measure.finite_spanning_sets_in.sigma_finite _ _,
    { exact set.range (Œª n, t·∂ú ‚à™ T n), },
    { refine ‚ü®Œª n, t·∂ú ‚à™ T n, Œª n, set.mem_range_self _, Œª n, _, _‚ü©,
      { rw [measure.restrict_apply' (measurable_set.Union hT_meas), set.union_inter_distrib_right,
          set.compl_inter_self t, set.empty_union],
        exact (measure_mono (set.inter_subset_left _ _)).trans_lt (hT_lt_top n), },
      rw ‚Üê set.union_Union t·∂ú T,
      exact set.compl_union_self _, },
    { intros s hs,
      rw set.mem_range at hs,
      cases hs with n hsn,
      rw ‚Üê hsn,
      exact (measurable_set.compl (measurable_set.Union hT_meas)).union (hT_meas n), }, },
end

/-- A measurable set `t` such that `f =·µê[Œº.restrict t·∂ú] 0` and `sigma_finite (Œº.restrict t)`. -/
def sigma_finite_set (hf : strongly_measurable f Œº) : set Œ± := hf.exists_set_sigma_finite.some

protected lemma measurable_set (hf : strongly_measurable f Œº) :
  measurable_set hf.sigma_finite_set :=
hf.exists_set_sigma_finite.some_spec.1

lemma ae_eq_zero_compl (hf : strongly_measurable f Œº) :
  f =·µê[Œº.restrict hf.sigma_finite_set·∂ú] 0 :=
hf.exists_set_sigma_finite.some_spec.2.1

lemma sigma_finite_restrict (hf : strongly_measurable f Œº) :
  sigma_finite (Œº.restrict hf.sigma_finite_set) :=
hf.exists_set_sigma_finite.some_spec.2.2

end strongly_measurable

/-- If the measure is sigma-finite, all measurable functions are strongly measurable. -/
lemma measurable.strongly_measurable {Œ± G : Type*} [measurable_space G] [emetric_space G]
  [has_zero G] [second_countable_topology G] [opens_measurable_space G]
  [decidable_pred (Œª (y : G), y ‚â† 0)]
  {m0 : measurable_space Œ±} {f : Œ± ‚Üí G} (hf : measurable f) (Œº : measure Œ±) [sigma_finite Œº]  :
  strongly_measurable f Œº :=
begin
  let S := spanning_sets Œº,
  have hS_meas : ‚àÄ n, measurable_set (S n), from measurable_spanning_sets Œº,
  let f_approx := simple_func.approx_on f hf set.univ 0 (set.mem_univ _),
  let fs := Œª n, simple_func.restrict (f_approx n) (S n),
  refine ‚ü®fs, _, Œª x, _‚ü©,
  { refine Œª n, (measure_bUnion_finset_le _ _).trans_lt _,
    refine ennreal.sum_lt_top_iff.mpr (Œª y hy, _),
    rw simple_func.restrict_preimage_singleton _ (hS_meas n) (finset.mem_filter.mp hy).2,
    refine (measure_mono (set.inter_subset_left _ _)).trans_lt _,
    exact measure_spanning_sets_lt_top Œº n, },
  { have h : tendsto (Œª n, (simple_func.approx_on f hf set.univ 0 _ n) x) at_top (ùìù (f x)),
      from simple_func.tendsto_approx_on hf (set.mem_univ 0) (by simp),
    obtain ‚ü®n‚ÇÅ, hn‚ÇÅ‚ü© : ‚àÉ n, ‚àÄ m, n ‚â§ m ‚Üí fs m x = f_approx m x,
    { obtain ‚ü®n, hn‚ü© : ‚àÉ n, ‚àÄ m, n ‚â§ m ‚Üí x ‚àà S m,
      { suffices : ‚àÉ n, x ‚àà S n,
        { rcases this with ‚ü®n, hn‚ü©,
          exact ‚ü®n, Œª m hnm, monotone_spanning_sets Œº hnm hn‚ü©, },
        rw [‚Üê set.mem_Union, Union_spanning_sets Œº],
        trivial, },
      refine ‚ü®n, Œª m hnm, _‚ü©,
      simp_rw [fs, simple_func.restrict_apply _ (hS_meas m), set.indicator_of_mem (hn m hnm)], },
    rw tendsto_at_top' at h ‚ä¢,
    intros s hs,
    obtain ‚ü®n‚ÇÇ, hn‚ÇÇ‚ü© := h s hs,
    refine ‚ü®max n‚ÇÅ n‚ÇÇ, Œª m hm, _‚ü©,
    rw hn‚ÇÅ m ((le_max_left _ _).trans hm.le),
    exact hn‚ÇÇ m ((le_max_right _ _).trans hm.le), },
end

variables {Œ± G : Type*} {p : ‚Ñù‚â•0‚àû} {m m0 : measurable_space Œ±} {Œº : measure Œ±}
  [normed_group G] [measurable_space G] [borel_space G] [second_countable_topology G]

/-- If the measure is sigma-finite, strongly measurable and measurable are equivalent. -/
lemma strongly_measurable_iff_measurable [decidable_pred (Œª (y : G), y ‚â† 0)]
  {m0 : measurable_space Œ±} (Œº : measure Œ±) [sigma_finite Œº] {f : Œ± ‚Üí G} :
  strongly_measurable f Œº ‚Üî measurable f :=
‚ü®Œª h, h.measurable, Œª h, measurable.strongly_measurable h Œº‚ü©

lemma mem_‚Ñíp.strongly_measurable_of_measurable [decidable_pred (Œª (y : G), y ‚â† 0)] {f : Œ± ‚Üí G}
  (hf : mem_‚Ñíp f p Œº) (hf_meas : measurable f) (hp_pos : 0 < p) (hp_ne_top : p ‚â† ‚àû) :
  strongly_measurable f Œº :=
begin
  let fs := simple_func.approx_on f hf_meas set.univ 0 (set.mem_univ _),
  refine ‚ü®fs, _, _‚ü©,
  { have h_fs_Lp : ‚àÄ n, mem_‚Ñíp (fs n) p Œº,
      from simple_func.mem_‚Ñíp_approx_on_univ hf_meas hf,
    refine Œª n, (measure_bUnion_finset_le _ _).trans_lt _,
    refine ennreal.sum_lt_top_iff.mpr (Œª y hy, _),
    refine simple_func.measure_preimage_lt_top_of_mem_‚Ñíp hp_pos hp_ne_top (fs n) (h_fs_Lp n) _ _,
    exact (finset.mem_filter.mp hy).2, },
  { exact Œª x, simple_func.tendsto_approx_on hf_meas (set.mem_univ 0)
      (by { rw [closure_univ], exact set.mem_univ (f x), }), },
end

lemma mem_‚Ñíp.ae_strongly_measurable [decidable_pred (Œª (y : G), y ‚â† 0)] {f : Œ± ‚Üí G}
  (hf : mem_‚Ñíp f p Œº) (hp_pos : 0 < p) (hp_ne_top : p ‚â† ‚àû) :
  ‚àÉ g, strongly_measurable g Œº ‚àß f =·µê[Œº] g :=
‚ü®hf.ae_measurable.mk f,
  ((mem_‚Ñíp_congr_ae hf.ae_measurable.ae_eq_mk).mp hf).strongly_measurable_of_measurable
    hf.ae_measurable.measurable_mk hp_pos hp_ne_top,
  hf.ae_measurable.ae_eq_mk‚ü©

lemma integrable.ae_strongly_measurable [decidable_pred (Œª (y : G), y ‚â† 0)] {f : Œ± ‚Üí G}
  (hf : integrable f Œº) :
  ‚àÉ g, strongly_measurable g Œº ‚àß f =·µê[Œº] g :=
(mem_‚Ñíp_one_iff_integrable.mpr hf).ae_strongly_measurable ennreal.zero_lt_one ennreal.coe_ne_top

lemma Lp.strongly_measurable [decidable_pred (Œª (y : G), y ‚â† 0)] (f : Lp G p Œº)
  (hp_pos : 0 < p) (hp_ne_top : p ‚â† ‚àû) :
  strongly_measurable f Œº :=
(Lp.mem_‚Ñíp f).strongly_measurable_of_measurable (Lp.measurable _) hp_pos hp_ne_top

lemma Lp.exists_set_sigma_finite (f : Lp G p Œº) (hp_pos : 0 < p) (hp_ne_top : p ‚â† ‚àû) :
  ‚àÉ t, measurable_set t ‚àß f =·µê[Œº.restrict t·∂ú] 0 ‚àß sigma_finite (Œº.restrict t) :=
begin
  haveI : decidable_pred (Œª (y : G), y ‚â† 0) := classical.dec_pred _,
  exact (Lp.strongly_measurable f hp_pos hp_ne_top).exists_set_sigma_finite,
end

lemma exists_set_sigma_finite_of_ae_strongly_measurable [decidable_pred (Œª (y : G), y ‚â† 0)]
  {f : Œ± ‚Üí G} (hf : ‚àÉ g, strongly_measurable g Œº ‚àß f =·µê[Œº] g) :
  ‚àÉ t, measurable_set t ‚àß f =·µê[Œº.restrict t·∂ú] 0 ‚àß sigma_finite (Œº.restrict t) :=
begin
  rcases hf with ‚ü®g, hg, hfg‚ü©,
  exact ‚ü®hg.sigma_finite_set, hg.measurable_set,
    eventually_eq.trans (ae_restrict_of_ae hfg) hg.ae_eq_zero_compl, hg.sigma_finite_restrict‚ü©,
end

lemma exists_set_sigma_finite_of_ae_strongly_measurable' [decidable_pred (Œª (y : G), y ‚â† 0)]
  (hm : m ‚â§ m0) {f : Œ± ‚Üí G} (hf : ‚àÉ g, strongly_measurable g (Œº.trim hm) ‚àß f =·µê[Œº] g) :
  ‚àÉ t, measurable_set[m] t ‚àß f =·µê[Œº.restrict t·∂ú] 0 ‚àß @sigma_finite _ m ((Œº.restrict t).trim hm) :=
begin
  rcases hf with ‚ü®g, hg, hfg‚ü©,
  refine ‚ü®hg.sigma_finite_set, hg.measurable_set, _, _‚ü©,
  { have hfg_eq : f =·µê[Œº.restrict hg.sigma_finite_set·∂ú] g, from ae_restrict_of_ae hfg,
    have hg_zero := hg.ae_eq_zero_compl,
    refine hfg_eq.trans (measure_eq_zero_of_trim_eq_zero hm _),
    rwa restrict_trim hm Œº (@measurable_set.compl _ _ m hg.measurable_set) at hg_zero, },
  { exact (restrict_trim hm Œº hg.measurable_set).subst hg.sigma_finite_restrict, },
end

end measure_theory
