/-
Copyright (c) 2022 Eric Rodriguez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Rodriguez
-/

import number_theory.cyclotomic.primitive_roots
import field_theory.polynomial_galois_group

/-!
# Galois group of cyclotomic extensions

In this file, we show the relationship between the Galois group of `K(Œ∂‚Çô)` and `(zmod n)À£`;
it is always a subgroup, and if the `n`th cyclotomic polynomial is irreducible, they are isomorphic.

## Main results

* `is_primitive_root.aut_to_pow_injective`: `is_primitive_root.aut_to_pow` is injective
  in the case that it's considered over a cyclotomic field extension.
* `is_cyclotomic_extension.aut_equiv_pow`: If the `n`th cyclotomic polynomial is irreducible in `K`,
  then `is_primitive_root.aut_to_pow` is a `mul_equiv` (for example, in `‚Ñö` and certain `ùîΩ‚Çö`).
* `gal_X_pow_equiv_units_zmod`, `gal_cyclotomic_equiv_units_zmod`: Repackage
  `is_cyclotomic_extension.aut_equiv_pow` in terms of `polynomial.gal`.
* `is_cyclotomic_extension.aut.comm_group`: Cyclotomic extensions are abelian.

## References

* https://kconrad.math.uconn.edu/blurbs/galoistheory/cyclotomic.pdf

## TODO

* We currently can get away with the fact that the power of a primitive root is a primitive root,
  but the correct long-term solution for computing other explicit Galois groups is creating
  `power_basis.map_conjugate`; but figuring out the exact correct assumptions + proof for this is
  mathematically nontrivial. (Current thoughts: the correct condition is that the annihilating
  ideal of both elements is equal. This may not hold in an ID, and definitely holds in an ICD.)

-/

variables {n : ‚Ñï+} (K : Type*) [field K] {L : Type*} {Œº : L}

open polynomial is_cyclotomic_extension

open_locale cyclotomic

namespace is_primitive_root

variables [comm_ring L] [is_domain L] (hŒº : is_primitive_root Œº n) [algebra K L]
          [is_cyclotomic_extension {n} K L]

/-- `is_primitive_root.aut_to_pow` is injective in the case that it's considered over a cyclotomic
field extension. -/
lemma aut_to_pow_injective : function.injective $ hŒº.aut_to_pow K :=
begin
  intros f g hfg,
  apply_fun units.val at hfg,
  simp only [is_primitive_root.coe_aut_to_pow_apply, units.val_eq_coe] at hfg,
  generalize_proofs hf' hg' at hfg,
  have hf := hf'.some_spec,
  have hg := hg'.some_spec,
  generalize_proofs hŒ∂ at hf hg,
  suffices : f hŒº.to_roots_of_unity = g hŒº.to_roots_of_unity,
  { apply alg_equiv.coe_alg_hom_injective,
    apply (hŒº.power_basis K).alg_hom_ext,
    exact this },
  rw zmod.eq_iff_modeq_nat at hfg,
  refine (hf.trans _).trans hg.symm,
  rw [‚Üêroots_of_unity.coe_pow _ hf'.some, ‚Üêroots_of_unity.coe_pow _ hg'.some],
  congr' 1,
  rw [pow_eq_pow_iff_modeq],
  convert hfg,
  rw [hŒº.eq_order_of],
  rw [‚ÜêhŒº.coe_to_roots_of_unity_coe] {occs := occurrences.pos [2]},
  rw [order_of_units, order_of_subgroup]
end

end is_primitive_root

namespace is_cyclotomic_extension

variables [comm_ring L] [is_domain L] (hŒº : is_primitive_root Œº n) [algebra K L]
          [is_cyclotomic_extension {n} K L]

/-- Cyclotomic extensions are abelian. -/
noncomputable def aut.comm_group : comm_group (L ‚âÉ‚Çê[K] L) :=
((zeta_spec n K L).aut_to_pow_injective K).comm_group _
  (map_one _) (map_mul _) (map_inv _) (map_div _) (map_pow _) (map_zpow _)

variables (h : irreducible (cyclotomic n K)) {K} (L)

include h

/-- The `mul_equiv` that takes an automorphism `f` to the element `k : (zmod n)À£` such that
  `f Œº = Œº ^ k` for any root of unity `Œº`. A  strengthening of `is_primitive_root.aut_to_pow`. -/
@[simps] noncomputable def aut_equiv_pow : (L ‚âÉ‚Çê[K] L) ‚âÉ* (zmod n)À£ :=
let hŒ∂ := zeta_spec n K L,
    hŒº := Œª t, hŒ∂.pow_of_coprime _ (zmod.val_coe_unit_coprime t) in
{ inv_fun := Œª t, (hŒ∂.power_basis K).equiv_of_minpoly ((hŒº t).power_basis K)
  begin
    haveI := is_cyclotomic_extension.ne_zero' n K L,
    simp only [is_primitive_root.power_basis_gen],
    have hr := is_primitive_root.minpoly_eq_cyclotomic_of_irreducible
               ((zeta_spec n K L).pow_of_coprime _ (zmod.val_coe_unit_coprime t)) h,
    exact ((zeta_spec n K L).minpoly_eq_cyclotomic_of_irreducible h).symm.trans hr
  end,
  left_inv := Œª f, begin
    simp only [monoid_hom.to_fun_eq_coe],
    apply alg_equiv.coe_alg_hom_injective,
    apply (hŒ∂.power_basis K).alg_hom_ext,
    simp only [alg_equiv.coe_alg_hom, alg_equiv.map_pow],
    rw power_basis.equiv_of_minpoly_gen,
    simp only [is_primitive_root.power_basis_gen, is_primitive_root.aut_to_pow_spec],
  end,
  right_inv := Œª x, begin
    simp only [monoid_hom.to_fun_eq_coe],
    generalize_proofs _ h,
    have key := hŒ∂.aut_to_pow_spec K ((hŒ∂.power_basis K).equiv_of_minpoly ((hŒº x).power_basis K) h),
    have := (hŒ∂.power_basis K).equiv_of_minpoly_gen ((hŒº x).power_basis K) h,
    rw hŒ∂.power_basis_gen K at this,
    rw [this, is_primitive_root.power_basis_gen] at key,
    rw ‚Üê hŒ∂.coe_to_roots_of_unity_coe at key {occs := occurrences.pos [1, 5]},
    simp only [‚Üêcoe_coe, ‚Üêroots_of_unity.coe_pow] at key,
    replace key := roots_of_unity.coe_injective key,
    rw [pow_eq_pow_iff_modeq, ‚Üêorder_of_subgroup, ‚Üêorder_of_units, hŒ∂.coe_to_roots_of_unity_coe,
        ‚Üê(zeta_spec n K L).eq_order_of, ‚Üêzmod.eq_iff_modeq_nat] at key,
    simp only [zmod.nat_cast_val, zmod.cast_id', id.def] at key,
    exact units.ext key
  end,
  .. (zeta_spec n K L).aut_to_pow K }

include hŒº

variables {L}

/-- Maps `Œº` to the `alg_equiv` that sends `is_cyclotomic_extension.zeta` to `Œº`. -/
noncomputable def from_zeta_aut : L ‚âÉ‚Çê[K] L :=
let hŒ∂ := (zeta_spec n K L).eq_pow_of_pow_eq_one hŒº.pow_eq_one n.pos in
(aut_equiv_pow L h).symm $ zmod.unit_of_coprime hŒ∂.some $
((zeta_spec n K L).pow_iff_coprime n.pos hŒ∂.some).mp $ hŒ∂.some_spec.some_spec.symm ‚ñ∏ hŒº

lemma from_zeta_aut_spec : from_zeta_aut hŒº h (zeta n K L) = Œº :=
begin
  simp_rw [from_zeta_aut, aut_equiv_pow_symm_apply],
  generalize_proofs hŒ∂ h _ hŒº _,
  rw [‚ÜêhŒ∂.power_basis_gen K] {occs := occurrences.pos [4]},
  rw [power_basis.equiv_of_minpoly_gen, hŒº.power_basis_gen K],
  convert h.some_spec.some_spec,
  exact zmod.val_cast_of_lt h.some_spec.some
end

end is_cyclotomic_extension

section gal

variables [field L] (hŒº : is_primitive_root Œº n) [algebra K L]
          [is_cyclotomic_extension {n} K L] (h : irreducible (cyclotomic n K)) {K}

/-- `is_cyclotomic_extension.aut_equiv_pow` repackaged in terms of `gal`.
Asserts that the Galois group of `cyclotomic n K` is equivalent to `(zmod n)À£`
if `cyclotomic n K` is irreducible in the base field. -/
noncomputable def gal_cyclotomic_equiv_units_zmod :
  (cyclotomic n K).gal ‚âÉ* (zmod n)À£ :=
(alg_equiv.aut_congr (is_splitting_field.alg_equiv _ _)).symm.trans
(is_cyclotomic_extension.aut_equiv_pow L h)

/-- `is_cyclotomic_extension.aut_equiv_pow` repackaged in terms of `gal`.
Asserts that the Galois group of `X ^ n - 1` is equivalent to `(zmod n)À£`
if `cyclotomic n K` is irreducible in the base field. -/
noncomputable def gal_X_pow_equiv_units_zmod :
  (X ^ (n : ‚Ñï) - 1).gal ‚âÉ* (zmod n)À£ :=
(alg_equiv.aut_congr (is_splitting_field.alg_equiv _ _)).symm.trans
(is_cyclotomic_extension.aut_equiv_pow L h)

end gal
