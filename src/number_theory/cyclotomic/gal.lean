/-
Copyright (c) 2022 Eric Rodriguez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Rodriguez
-/

import number_theory.cyclotomic.zeta
import field_theory.polynomial_galois_group

/-!
# Galois group of cyclotomic extensions

In this file, we show the relationship between the Galois group of`K(Œ∂‚Çô)` and `(zmod n)À£`;
it is always a subgroup, and if the `n`th cyclotomic polynomial is irreducible, they are isomorphic.

## Main results

* `is_cyclotomic_extension.aut_to_pow_injective`: `is_primitive_root.aut_to_pow` is injective
  in the case that it's considered over a cyclotomic field extension, where `n` does not divide
  the characteristic of K. As a corollary, $Gal(K(Œ∂‚Çô)/K)$ is abelian.
* `is_cyclotomic_extension.aut_equiv_pow`: If, additionally, the `n`th cyclotomic polynomial is
  irreducible in K, then `aut_to_pow` is a `mul_equiv` (for example, in ‚Ñö and certain ùîΩ‚Çö).
* `gal_X_pow_equiv_units_zmod`, `gal_cyclotomic_equiv_units_zmod`: Repackage `aut_equiv_pow` in
  terms of `polynomial.gal`.

## References

* https://kconrad.math.uconn.edu/blurbs/galoistheory/cyclotomic.pdf

## TODO

* `zeta_pow_power_basis` is not sufficiently general; the correct solution is some
  `power_basis.map_conjugate`, but figuring out the exact correct assumptions + proof for this is
  mathematically nontrivial. (Current thoughts: the ideal of polynomials with Œ± as a root is equal
  to the ideal of polynomials with Œ≤ as a root, which, in an integrally closed domain, reduces
  to the usual condition that they are the roots of each others' minimal polynomials.)

-/

local attribute [instance] pnat.fact_pos

variables (K : Type*) [field K] (L : Type*) [field L] {Œº : L} (n : ‚Ñï+) (hŒº : is_primitive_root Œº n)
          [algebra K L] [is_cyclotomic_extension {n} K L]

local notation `Œ∂` := is_cyclotomic_extension.zeta n K L

open polynomial ne_zero

namespace is_cyclotomic_extension

/-- `is_primitive_root.aut_to_pow` is injective in the case that it's considered over a cyclotomic
field extension, where `n` does not divide the characteristic of K. -/
lemma aut_to_pow_injective [ne_zero ((n : ‚Ñï) : K)] : function.injective $
    (@zeta_primitive_root n K L _ _ _ _ _ $ of_no_zero_smul_divisors K L n).aut_to_pow K :=
begin
  intros f g hfg,
  apply_fun units.val at hfg,
  simp only [is_primitive_root.coe_aut_to_pow_apply, units.val_eq_coe] at hfg,
  generalize_proofs hf' hg' at hfg,
  have hf := hf'.some_spec,
  have hg := hg'.some_spec,
  generalize_proofs hŒ∂ at hf hg,
  suffices : f hŒ∂.to_roots_of_unity = g hŒ∂.to_roots_of_unity,
  { apply alg_equiv.coe_alg_hom_injective,
    apply (zeta.power_basis n K L).alg_hom_ext,
    exact this },
  rw zmod.eq_iff_modeq_nat at hfg,
  refine (hf.trans _).trans hg.symm,
  rw [‚Üêroots_of_unity.coe_pow _ hf'.some, ‚Üêroots_of_unity.coe_pow _ hg'.some],
  congr' 1,
  rw [pow_eq_pow_iff_modeq],
  convert hfg,
  rw [hŒ∂.eq_order_of],
  rw [‚ÜêhŒ∂.coe_to_roots_of_unity_coe] {occs := occurrences.pos [2]},
  rw [order_of_units, order_of_subgroup]
end

-- As a corollary, cyclotomic extensions are abelian extensions! (Note this cannot be an instance)
noncomputable example [ne_zero ((n : ‚Ñï) : K)] : comm_group (L ‚âÉ‚Çê[K] L) :=
function.injective.comm_group _ (aut_to_pow_injective K L n) (map_one _)
  (map_mul _) (map_inv _) (map_div _)

/-- The power basis given by `Œ∂ ^ t`. -/
@[simps] noncomputable def zeta_pow_power_basis [ne_zero ((n : ‚Ñï) : K)] (t : (zmod n)À£) :
  power_basis K L :=
begin
  haveI := of_no_zero_smul_divisors K L n,
  refine power_basis.map (algebra.adjoin.power_basis $ integral {n} K L $ Œ∂ ^ (t : zmod n).val) _,
  refine (subalgebra.equiv_of_eq _ _ (adjoin_primitive_root_eq_top n _ _)).trans algebra.top_equiv,
  exact (zeta_primitive_root n K L).pow_of_coprime _ (zmod.val_coe_unit_coprime t),
end

variables (h : irreducible (cyclotomic n K)) {K}

include h

/-- The `mul_equiv` that takes an automorphism to the power of Œº that Œº gets mapped to under it.
    A stronger version of `is_primitive_root.aut_to_pow`. -/
@[simps] noncomputable def aut_equiv_pow [ne_zero ((n : ‚Ñï) : K)] : (L ‚âÉ‚Çê[K] L) ‚âÉ* (zmod n)À£ :=
let hn := of_no_zero_smul_divisors K L n in by exactI
{ inv_fun := Œª t, (zeta.power_basis n K L).equiv_of_minpoly (zeta_pow_power_basis K L n t)
  begin
    simp only [zeta.power_basis_gen, zeta_pow_power_basis_gen],
    have hr := is_primitive_root.minpoly_eq_cyclotomic_of_irreducible
               ((zeta_primitive_root n K L).pow_of_coprime _ (zmod.val_coe_unit_coprime t)) h,
    exact ((zeta_primitive_root n K L).minpoly_eq_cyclotomic_of_irreducible h).symm.trans hr
  end,
  left_inv := Œª f, begin
    simp only [monoid_hom.to_fun_eq_coe],
    apply alg_equiv.coe_alg_hom_injective,
    apply (zeta.power_basis n K L).alg_hom_ext,
    simp only [alg_equiv.coe_alg_hom, alg_equiv.map_pow],
    rw power_basis.equiv_of_minpoly_gen,
    simp only [zeta_pow_power_basis_gen, zeta.power_basis_gen, is_primitive_root.aut_to_pow_spec],
  end,
  right_inv := Œª x, begin
    simp only [monoid_hom.to_fun_eq_coe],
    generalize_proofs _ hŒ∂ _ h,
    have key := hŒ∂.aut_to_pow_spec K ((zeta.power_basis n K L).equiv_of_minpoly
                                      (zeta_pow_power_basis K L n x) h),
    have := (zeta.power_basis n K L).equiv_of_minpoly_gen,
    rw zeta.power_basis_gen at this {occs := occurrences.pos [2]},
    rw [this, zeta_pow_power_basis_gen] at key,
    rw ‚Üê hŒ∂.coe_to_roots_of_unity_coe at key {occs := occurrences.pos [1, 3]},
    simp only [‚Üêcoe_coe, ‚Üêroots_of_unity.coe_pow] at key,
    replace key := roots_of_unity.coe_injective key,
    rw [pow_eq_pow_iff_modeq, ‚Üêorder_of_subgroup, ‚Üêorder_of_units, hŒ∂.coe_to_roots_of_unity_coe,
        ‚Üê(zeta_primitive_root n K L).eq_order_of, ‚Üêzmod.eq_iff_modeq_nat] at key,
    simp only [zmod.nat_cast_val, zmod.cast_id', id.def] at key,
    exact units.ext key
  end,
  .. (zeta_primitive_root n K L).aut_to_pow K }

end is_cyclotomic_extension

open is_cyclotomic_extension

namespace is_primitive_root

variables (h : irreducible (cyclotomic n K)) {K L}

include h hŒº

/-- Maps `Œº` to the `alg_equiv` that sends `is_cyclotomic_extension.zeta` to `Œº`. -/
noncomputable def from_zeta_aut [ne_zero ((n : ‚Ñï) : K)] : L ‚âÉ‚Çê[K] L :=
have _ := of_no_zero_smul_divisors K L n, by exactI
let hŒ∂ := (zeta_primitive_root n K L).eq_pow_of_pow_eq_one hŒº.pow_eq_one n.pos in
(aut_equiv_pow L n h).symm $ zmod.unit_of_coprime hŒ∂.some $
((zeta_primitive_root n K L).pow_iff_coprime n.pos hŒ∂.some).mp $ hŒ∂.some_spec.some_spec.symm ‚ñ∏ hŒº

lemma from_zeta_aut_spec [ne_zero ((n : ‚Ñï) : K)] : from_zeta_aut n hŒº h Œ∂ = Œº :=
begin
  simp only [from_zeta_aut, exists_prop, aut_equiv_pow_symm_apply, ‚Üêzeta.power_basis_gen,
             power_basis.equiv_of_minpoly_gen, zeta_pow_power_basis_gen, zmod.coe_unit_of_coprime],
  generalize_proofs hŒ∂,
  convert hŒ∂.some_spec.2,
  exact zmod.val_cast_of_lt hŒ∂.some_spec.1
end

end is_primitive_root

section gal

local attribute [instance] splitting_field_X_pow_sub_one splitting_field_cyclotomic

/-- `is_cyclotomic_extension.aut_equiv_pow` repackaged in terms of `gal`. Asserts that the
Galois group of `cyclotomic n K` is equivalent to `(zmod n)À£` if `n` does not divide the
characteristic of `K`, and `cyclotomic n K` is irreducible in the base field. -/
noncomputable def gal_cyclotomic_equiv_units_zmod [ne_zero ((n : ‚Ñï) : K)]
  (h : irreducible (cyclotomic n K)) : (cyclotomic n K).gal ‚âÉ* (zmod n)À£ :=
(alg_equiv.aut_congr (is_splitting_field.alg_equiv _ _)).symm.trans
(is_cyclotomic_extension.aut_equiv_pow L n h)

/-- `is_cyclotomic_extension.aut_equiv_pow` repackaged in terms of `gal`. Asserts that the
Galois group of `X ^ n - 1` is equivalent to `(zmod n)À£` if `n` does not divide the characteristic
of `K`, and `cyclotomic n K` is irreducible in the base field. -/
noncomputable def gal_X_pow_equiv_units_zmod [ne_zero ((n : ‚Ñï) : K)]
  (h : irreducible (cyclotomic n K)) : (X ^ (n : ‚Ñï) - 1).gal ‚âÉ* (zmod n)À£ :=
(alg_equiv.aut_congr (is_splitting_field.alg_equiv _ _)).symm.trans
(is_cyclotomic_extension.aut_equiv_pow L n h)

end gal
