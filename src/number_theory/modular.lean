/-
Copyright (c) 2021 Alex Kontorovich and Heather Macbeth and Marc Masdeu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth, Marc Masdeu
-/

import analysis.complex.upper_half_plane
import linear_algebra.general_linear_group

/-!
# The action of the modular group SL(2, ‚Ñ§) on the upper half-plane

We define the action of `SL(2,‚Ñ§)` on `‚Ñç` (via restriction of the `SL(2,‚Ñù)` action in
`analysis.complex.upper_half_plane`). We then define the standard fundamental domain
(`modular_group.fundamental_domain`, `ùíü`) for this action and show
(`modular_group.exists_smul_mem_fundamental_domain`) that any point in `‚Ñç` can be
moved inside `ùíü`.

Standard proofs make use of the identity

`g ‚Ä¢ z = a / c - 1 / (c (cz + d))`

for `g = [[a, b], [c, d]]` in `SL(2)`, but this requires separate handling of whether `c = 0`.
Instead, our proof makes use of the following perhaps novel identity (see
`modular_group.smul_eq_lc_row0_add`):

`g ‚Ä¢ z = (a c + b d) / (c^2 + d^2) + (d z - c) / ((c^2 + d^2) (c z + d))`

where there is no issue of division by zero.

Another feature is that we delay until the very end the consideration of special matrices
`T=[[1,1],[0,1]]` (see `modular_group.T`) and `S=[[0,-1],[1,0]]` (see `modular_group.S`), by
instead using abstract theory on the properness of certain maps (phrased in terms of the filters
`filter.cocompact`, `filter.cofinite`, etc) to deduce existence theorems, first to prove the
existence of `g` maximizing `(g‚Ä¢z).im` (see `modular_group.exists_max_im`), and then among
those, to minimize `|(g‚Ä¢z).re|` (see `modular_group.exists_row_one_eq_and_min_re`).
-/

open complex matrix matrix.special_linear_group upper_half_plane
noncomputable theory

local notation `SL(` n `, ` R `)`:= special_linear_group (fin n) R
local prefix `‚Üë‚Çò`:1024 := @coe _ (matrix (fin 2) (fin 2) ‚Ñ§) _


open_locale upper_half_plane complex_conjugate

local attribute [instance] fintype.card_fin_even

namespace modular_group

section upper_half_plane_action

/-- For a subring `R` of `‚Ñù`, the action of `SL(2, R)` on the upper half-plane, as a restriction of
the `SL(2, ‚Ñù)`-action defined by `upper_half_plane.mul_action`. -/
instance {R : Type*} [comm_ring R] [algebra R ‚Ñù] : mul_action SL(2, R) ‚Ñç :=
mul_action.comp_hom ‚Ñç (map (algebra_map R ‚Ñù))

lemma coe_smul (g : SL(2, ‚Ñ§)) (z : ‚Ñç) : ‚Üë(g ‚Ä¢ z) = num g z / denom g z := rfl
lemma re_smul (g : SL(2, ‚Ñ§)) (z : ‚Ñç) : (g ‚Ä¢ z).re = (num g z / denom g z).re := rfl
@[simp] lemma smul_coe (g : SL(2, ‚Ñ§)) (z : ‚Ñç) : (g : SL(2,‚Ñù)) ‚Ä¢ z = g ‚Ä¢ z := rfl

@[simp] lemma neg_smul (g : SL(2, ‚Ñ§)) (z : ‚Ñç) : -g ‚Ä¢ z = g ‚Ä¢ z :=
show ‚Üë(-g) ‚Ä¢ _ = _, by simp [neg_smul g z]

lemma im_smul (g : SL(2, ‚Ñ§)) (z : ‚Ñç) : (g ‚Ä¢ z).im = (num g z / denom g z).im := rfl

lemma im_smul_eq_div_norm_sq (g : SL(2, ‚Ñ§)) (z : ‚Ñç) :
  (g ‚Ä¢ z).im = z.im / (complex.norm_sq (denom g z)) :=
im_smul_eq_div_norm_sq g z

@[simp] lemma denom_apply (g : SL(2, ‚Ñ§)) (z : ‚Ñç) : denom g z = ‚Üë‚Çòg 1 0 * z + ‚Üë‚Çòg 1 1 := by simp

end upper_half_plane_action

section bottom_row

/-- The two numbers `c`, `d` in the "bottom_row" of `g=[[*,*],[c,d]]` in `SL(2, ‚Ñ§)` are coprime. -/
lemma bottom_row_coprime {R : Type*} [comm_ring R] (g : SL(2, R)) :
  is_coprime ((‚Üëg : matrix (fin 2) (fin 2) R) 1 0) ((‚Üëg : matrix (fin 2) (fin 2) R) 1 1) :=
begin
  use [- (‚Üëg : matrix (fin 2) (fin 2) R) 0 1, (‚Üëg : matrix (fin 2) (fin 2) R) 0 0],
  rw [add_comm, ‚Üêneg_mul_eq_neg_mul, ‚Üêsub_eq_add_neg, ‚Üêdet_fin_two],
  exact g.det_coe,
end

/-- Every pair `![c, d]` of coprime integers is the "bottom_row" of some element `g=[[*,*],[c,d]]`
of `SL(2,‚Ñ§)`. -/
lemma bottom_row_surj {R : Type*} [comm_ring R] :
  set.surj_on (Œª g : SL(2, R), @coe _ (matrix (fin 2) (fin 2) R) _ g 1) set.univ
    {cd | is_coprime (cd 0) (cd 1)} :=
begin
  rintros cd ‚ü®b‚ÇÄ, a, gcd_eqn‚ü©,
  let A := ![![a, -b‚ÇÄ], cd],
  have det_A_1 : det A = 1,
  { convert gcd_eqn,
    simp [A, det_fin_two, (by ring : a * (cd 1) + b‚ÇÄ * (cd 0) = b‚ÇÄ * (cd 0) + a * (cd 1))] },
  refine ‚ü®‚ü®A, det_A_1‚ü©, set.mem_univ _, _‚ü©,
  ext; simp [A]
end

end bottom_row

section tendsto_lemmas

open filter continuous_linear_map
local attribute [instance] matrix.normed_group matrix.normed_space
local attribute [simp] coe_smul

/-- The function `(c,d) ‚Üí |cz+d|^2` is proper, that is, preimages of bounded-above sets are finite.
-/
lemma tendsto_norm_sq_coprime_pair (z : ‚Ñç) :
  filter.tendsto (Œª p : fin 2 ‚Üí ‚Ñ§, ((p 0 : ‚ÑÇ) * z + p 1).norm_sq)
  cofinite at_top :=
begin
  let œÄ‚ÇÄ : (fin 2 ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù := linear_map.proj 0,
  let œÄ‚ÇÅ : (fin 2 ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù := linear_map.proj 1,
  let f : (fin 2 ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚ÑÇ := œÄ‚ÇÄ.smul_right (z:‚ÑÇ) + œÄ‚ÇÅ.smul_right 1,
  have f_def : ‚áëf = Œª (p : fin 2 ‚Üí ‚Ñù), (p 0 : ‚ÑÇ) * ‚Üëz + p 1,
  { ext1,
    dsimp only [linear_map.coe_proj, real_smul,
      linear_map.coe_smul_right, linear_map.add_apply],
    rw mul_one, },
  have : (Œª (p : fin 2 ‚Üí ‚Ñ§), norm_sq ((p 0 : ‚ÑÇ) * ‚Üëz + ‚Üë(p 1)))
    = norm_sq ‚àò f ‚àò (Œª p : fin 2 ‚Üí ‚Ñ§, (coe : ‚Ñ§ ‚Üí ‚Ñù) ‚àò p),
  { ext1,
    rw f_def,
    dsimp only [function.comp],
    rw [of_real_int_cast, of_real_int_cast], },
  rw this,
  have hf : f.ker = ‚ä•,
  { let g : ‚ÑÇ ‚Üí‚Çó[‚Ñù] (fin 2 ‚Üí ‚Ñù) :=
      linear_map.pi ![im_lm, im_lm.comp ((z:‚ÑÇ) ‚Ä¢ (conj_ae  : ‚ÑÇ ‚Üí‚Çó[‚Ñù] ‚ÑÇ))],
    suffices : ((z:‚ÑÇ).im‚Åª¬π ‚Ä¢ g).comp f = linear_map.id,
    { exact linear_map.ker_eq_bot_of_inverse this },
    apply linear_map.ext,
    intros c,
    have hz : (z:‚ÑÇ).im ‚â† 0 := z.2.ne',
    rw [linear_map.comp_apply, linear_map.smul_apply, linear_map.id_apply],
    ext i,
    dsimp only [g, pi.smul_apply, linear_map.pi_apply, smul_eq_mul],
    fin_cases i,
    { show ((z : ‚ÑÇ).im)‚Åª¬π * (f c).im = c 0,
      rw [f_def, add_im, of_real_mul_im, of_real_im, add_zero, mul_left_comm,
        inv_mul_cancel hz, mul_one], },
    { show ((z : ‚ÑÇ).im)‚Åª¬π * ((z : ‚ÑÇ) * conj (f c)).im = c 1,
      rw [f_def, ring_equiv.map_add, ring_equiv.map_mul, mul_add, mul_left_comm, mul_conj,
        conj_of_real, conj_of_real, ‚Üê of_real_mul, add_im, of_real_im, zero_add,
        inv_mul_eq_iff_eq_mul‚ÇÄ hz],
      simp only [of_real_im, of_real_re, mul_im, zero_add, mul_zero] } },
  have h‚ÇÅ := (linear_equiv.closed_embedding_of_injective hf).tendsto_cocompact,
  have h‚ÇÇ : tendsto (Œª p : fin 2 ‚Üí ‚Ñ§, (coe : ‚Ñ§ ‚Üí ‚Ñù) ‚àò p) cofinite (cocompact _),
  { convert tendsto.pi_map_Coprod (Œª i, int.tendsto_coe_cofinite),
    { rw Coprod_cofinite },
    { rw Coprod_cocompact } },
  exact tendsto_norm_sq_cocompact_at_top.comp (h‚ÇÅ.comp h‚ÇÇ)
end


/-- Given `coprime_pair` `p=(c,d)`, the matrix `[[a,b],[*,*]]` is sent to `a*c+b*d`.
  This is the linear map version of this operation.
-/
def lc_row0 (p : fin 2 ‚Üí ‚Ñ§) : (matrix (fin 2) (fin 2) ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù :=
((p 0:‚Ñù) ‚Ä¢ linear_map.proj 0 + (p 1:‚Ñù) ‚Ä¢ linear_map.proj 1 : (fin 2 ‚Üí ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù).comp
  (linear_map.proj 0)

@[simp] lemma lc_row0_apply (p : fin 2 ‚Üí ‚Ñ§) (g : matrix (fin 2) (fin 2) ‚Ñù) :
  lc_row0 p g = p 0 * g 0 0 + p 1 * g 0 1 :=
rfl

lemma lc_row0_apply' (a b : ‚Ñù) (c d : ‚Ñ§) (v : fin 2 ‚Üí ‚Ñù) :
  lc_row0 ![c, d] ![![a, b], v] = c * a + d * b :=
by simp

/-- Linear map sending the matrix [a, b; c, d] to the matrix [ac‚ÇÄ + bd‚ÇÄ, - ad‚ÇÄ + bc‚ÇÄ; c, d], for
some fixed `(c‚ÇÄ, d‚ÇÄ)`. -/
@[simps] def lc_row0_extend {cd : fin 2 ‚Üí ‚Ñ§} (hcd : is_coprime (cd 0) (cd 1)) :
  (matrix (fin 2) (fin 2) ‚Ñù) ‚âÉ‚Çó[‚Ñù] matrix (fin 2) (fin 2) ‚Ñù :=
linear_equiv.Pi_congr_right
![begin
    refine linear_map.general_linear_group.general_linear_equiv ‚Ñù (fin 2 ‚Üí ‚Ñù)
      (general_linear_group.to_linear (plane_conformal_matrix (cd 0 : ‚Ñù) (cd 1 : ‚Ñù) _)),
    norm_cast,
    exact hcd.sq_add_sq_ne_zero
  end,
  (linear_equiv.refl _ _)]

/-- The map `lc_row0` is proper, that is, preimages of cocompact sets are finite in
`[[* , *], [c, d]]`.-/
theorem tendsto_lc_row0 {cd : fin 2 ‚Üí ‚Ñ§} (hcd : is_coprime (cd 0) (cd 1)) :
  tendsto (Œª g : {g : SL(2, ‚Ñ§) // g 1 = cd}, lc_row0 cd ‚Üë(‚Üëg : SL(2, ‚Ñù))) cofinite (cocompact ‚Ñù) :=
begin
  let mB : ‚Ñù ‚Üí (matrix (fin 2) (fin 2)  ‚Ñù) := Œª t, ![![t, (-(1:‚Ñ§):‚Ñù)], coe ‚àò cd],
  have hmB : continuous mB,
  { refine continuous_pi (Œª i, _),
    fin_cases i,
    { refine continuous_pi (Œª j, _),
      fin_cases j,
      { exact continuous_id },
      { exact @continuous_const _ _ _ _ (-(1:‚Ñ§):‚Ñù) } },
    exact @continuous_const _ _ _ _ (coe ‚àò cd) },
  convert filter.tendsto.of_tendsto_comp _ (comap_cocompact hmB),
  let f‚ÇÅ : SL(2, ‚Ñ§) ‚Üí matrix (fin 2) (fin 2) ‚Ñù :=
    Œª g, matrix.map (‚Üëg : matrix _ _ ‚Ñ§) (coe : ‚Ñ§ ‚Üí ‚Ñù),
  have cocompact_‚Ñù_to_cofinite_‚Ñ§_matrix :
    tendsto (Œª m : matrix (fin 2) (fin 2) ‚Ñ§, matrix.map m (coe : ‚Ñ§ ‚Üí ‚Ñù)) cofinite (cocompact _),
  { convert tendsto.pi_map_Coprod (Œª i, tendsto.pi_map_Coprod (Œª j, int.tendsto_coe_cofinite)),
    { simp [Coprod_cofinite] },
    { simp only [Coprod_cocompact],
      refl } },
  have hf‚ÇÅ : tendsto f‚ÇÅ cofinite (cocompact _) :=
    cocompact_‚Ñù_to_cofinite_‚Ñ§_matrix.comp subtype.coe_injective.tendsto_cofinite,
  have hf‚ÇÇ : closed_embedding (lc_row0_extend hcd) :=
    (lc_row0_extend hcd).to_continuous_linear_equiv.to_homeomorph.closed_embedding,
  convert hf‚ÇÇ.tendsto_cocompact.comp (hf‚ÇÅ.comp subtype.coe_injective.tendsto_cofinite) using 1,
  funext g,
  obtain ‚ü®g, hg‚ü© := g,
  funext j,
  fin_cases j,
  { ext i,
    fin_cases i,
    { simp [mB, f‚ÇÅ, matrix.mul_vec, matrix.dot_product, fin.sum_univ_succ] },
    { convert congr_arg (Œª n : ‚Ñ§, (-n:‚Ñù)) g.det_coe.symm using 1,
      simp [f‚ÇÅ, ‚Üê hg, matrix.mul_vec, matrix.dot_product, fin.sum_univ_succ, matrix.det_fin_two,
        -special_linear_group.det_coe],
      ring } },
  { exact congr_arg (Œª p, (coe : ‚Ñ§ ‚Üí ‚Ñù) ‚àò p) hg.symm }
end

/-- This replaces `(g‚Ä¢z).re = a/c + *` in the standard theory with the following novel identity:

  `g ‚Ä¢ z = (a c + b d) / (c^2 + d^2) + (d z - c) / ((c^2 + d^2) (c z + d))`

  which does not need to be decomposed depending on whether `c = 0`. -/
lemma smul_eq_lc_row0_add {p : fin 2 ‚Üí ‚Ñ§} (hp : is_coprime (p 0) (p 1)) (z : ‚Ñç) {g : SL(2,‚Ñ§)}
  (hg : ‚Üë‚Çòg 1 = p) :
  ‚Üë(g ‚Ä¢ z) = ((lc_row0 p ‚Üë(g : SL(2, ‚Ñù))) : ‚ÑÇ) / (p 0 ^ 2 + p 1 ^ 2)
    + ((p 1 : ‚ÑÇ) * z - p 0) / ((p 0 ^ 2 + p 1 ^ 2) * (p 0 * z + p 1)) :=
begin
  have nonZ1 : (p 0 : ‚ÑÇ) ^ 2 + (p 1) ^ 2 ‚â† 0 := by exact_mod_cast hp.sq_add_sq_ne_zero,
  have : (coe : ‚Ñ§ ‚Üí ‚Ñù) ‚àò p ‚â† 0 := Œª h, hp.ne_zero ((@int.cast_injective ‚Ñù _ _ _).comp_left h),
  have nonZ2 : (p 0 : ‚ÑÇ) * z + p 1 ‚â† 0 := by simpa using linear_ne_zero _ z this,
  field_simp [nonZ1, nonZ2, denom_ne_zero, -upper_half_plane.denom, -denom_apply],
  rw (by simp : (p 1 : ‚ÑÇ) * z - p 0 = ((p 1) * z - p 0) * ‚Üë(det (‚Üëg : matrix (fin 2) (fin 2) ‚Ñ§))),
  rw [‚Üêhg, det_fin_two],
  simp only [int.coe_cast_ring_hom, coe_matrix_coe, coe_fn_eq_coe,
    int.cast_mul, of_real_int_cast, map_apply, denom, int.cast_sub],
  ring,
end

lemma tendsto_abs_re_smul (z:‚Ñç) {p : fin 2 ‚Üí ‚Ñ§} (hp : is_coprime (p 0) (p 1)) :
  tendsto (Œª g : {g : SL(2, ‚Ñ§) // g 1 = p}, |((g : SL(2, ‚Ñ§)) ‚Ä¢ z).re|)
    cofinite at_top :=
begin
  suffices : tendsto (Œª g : (Œª g : SL(2, ‚Ñ§), g 1) ‚Åª¬π' {p}, (((g : SL(2, ‚Ñ§)) ‚Ä¢ z).re))
    cofinite (cocompact ‚Ñù),
  { exact tendsto_norm_cocompact_at_top.comp this },
  have : ((p 0 : ‚Ñù) ^ 2 + p 1 ^ 2)‚Åª¬π ‚â† 0,
  { apply inv_ne_zero,
    exact_mod_cast hp.sq_add_sq_ne_zero },
  let f := homeomorph.mul_right‚ÇÄ _ this,
  let ff := homeomorph.add_right (((p 1:‚ÑÇ)* z - p 0) / ((p 0 ^ 2 + p 1 ^ 2) * (p 0 * z + p 1))).re,
  convert ((f.trans ff).closed_embedding.tendsto_cocompact).comp (tendsto_lc_row0 hp),
  ext g,
  change ((g : SL(2, ‚Ñ§)) ‚Ä¢ z).re = (lc_row0 p ‚Üë(‚Üëg : SL(2, ‚Ñù))) / (p 0 ^ 2 + p 1 ^ 2)
  + (((p 1:‚ÑÇ )* z - p 0) / ((p 0 ^ 2 + p 1 ^ 2) * (p 0 * z + p 1))).re,
  exact_mod_cast (congr_arg complex.re (smul_eq_lc_row0_add hp z g.2))
end

end tendsto_lemmas

section fundamental_domain

local attribute [simp] coe_smul re_smul

/-- For `z : ‚Ñç`, there is a `g : SL(2,‚Ñ§)` maximizing `(g‚Ä¢z).im` -/
lemma exists_max_im (z : ‚Ñç) :
  ‚àÉ g : SL(2, ‚Ñ§), ‚àÄ g' : SL(2, ‚Ñ§), (g' ‚Ä¢ z).im ‚â§ (g ‚Ä¢ z).im :=
begin
  classical,
  let s : set (fin 2 ‚Üí ‚Ñ§) := {cd | is_coprime (cd 0) (cd 1)},
  have hs : s.nonempty := ‚ü®![1, 1], is_coprime_one_left‚ü©,
  obtain ‚ü®p, hp_coprime, hp‚ü© :=
    filter.tendsto.exists_within_forall_le hs (tendsto_norm_sq_coprime_pair z),
  obtain ‚ü®g, -, hg‚ü© := bottom_row_surj hp_coprime,
  refine ‚ü®g, Œª g', _‚ü©,
  rw [im_smul_eq_div_norm_sq, im_smul_eq_div_norm_sq, div_le_div_left],
  { simpa [‚Üê hg] using hp (g' 1) (bottom_row_coprime g') },
  { exact z.im_pos },
  { exact norm_sq_denom_pos g' z },
  { exact norm_sq_denom_pos g z },
end

/-- Given `z : ‚Ñç` and a bottom row `(c,d)`, among the `g : SL(2,‚Ñ§)` with this bottom row, minimize
  `|(g‚Ä¢z).re|`.  -/
lemma exists_row_one_eq_and_min_re (z:‚Ñç) {cd : fin 2 ‚Üí ‚Ñ§} (hcd : is_coprime (cd 0) (cd 1)) :
  ‚àÉ g : SL(2,‚Ñ§), ‚Üë‚Çòg 1 = cd ‚àß (‚àÄ g' : SL(2,‚Ñ§), ‚Üë‚Çòg 1 = ‚Üë‚Çòg' 1 ‚Üí
  |(g ‚Ä¢ z).re| ‚â§ |(g' ‚Ä¢ z).re|) :=
begin
  haveI : nonempty {g : SL(2, ‚Ñ§) // g 1 = cd} := let ‚ü®x, hx‚ü© := bottom_row_surj hcd in ‚ü®‚ü®x, hx.2‚ü©‚ü©,
  obtain ‚ü®g, hg‚ü© := filter.tendsto.exists_forall_le (tendsto_abs_re_smul z hcd),
  refine ‚ü®g, g.2, _‚ü©,
  { intros g1 hg1,
    have : g1 ‚àà ((Œª g : SL(2, ‚Ñ§), g 1) ‚Åª¬π' {cd}),
    { rw [set.mem_preimage, set.mem_singleton_iff],
      exact eq.trans hg1.symm (set.mem_singleton_iff.mp (set.mem_preimage.mp g.2)) },
    exact hg ‚ü®g1, this‚ü© },
end

/-- The matrix `T = [[1,1],[0,1]]` as an element of `SL(2,‚Ñ§)` -/
def T : SL(2,‚Ñ§) := ‚ü®![![1, 1], ![0, 1]], by norm_num [matrix.det_fin_two]‚ü©

/-- The matrix `T' (= T‚Åª¬π) = [[1,-1],[0,1]]` as an element of `SL(2,‚Ñ§)` -/
def T' : SL(2,‚Ñ§) := ‚ü®![![1, -1], ![0, 1]], by norm_num [matrix.det_fin_two]‚ü©

/-- The matrix `S = [[0,-1],[1,0]]` as an element of `SL(2,‚Ñ§)` -/
def S : SL(2,‚Ñ§) := ‚ü®![![0, -1], ![1, 0]], by norm_num [matrix.det_fin_two]‚ü©

/-- The standard (closed) fundamental domain of the action of `SL(2,‚Ñ§)` on `‚Ñç` -/
def fundamental_domain : set ‚Ñç :=
{z | 1 ‚â§ (complex.norm_sq z) ‚àß |z.re| ‚â§ (1 : ‚Ñù) / 2}

localized "notation `ùíü` := modular_group.fundamental_domain" in modular

/-- If `|z|<1`, then applying `S` strictly decreases `im` -/
lemma im_lt_im_S_smul {z : ‚Ñç} (h: norm_sq z < 1) : z.im < (S ‚Ä¢ z).im :=
begin
  have : z.im < z.im / norm_sq (z:‚ÑÇ),
  { have imz : 0 < z.im := im_pos z,
    apply (lt_div_iff z.norm_sq_pos).mpr,
    nlinarith },
  convert this,
  simp only [im_smul_eq_div_norm_sq],
  field_simp [norm_sq_denom_ne_zero, norm_sq_ne_zero, S]
end

/-- Any `z : ‚Ñç` can be moved to `ùíü` by an element of `SL(2,‚Ñ§)`  -/
lemma exists_smul_mem_fundamental_domain (z : ‚Ñç) : ‚àÉ g : SL(2,‚Ñ§), g ‚Ä¢ z ‚àà ùíü :=
begin
  -- obtain a g‚ÇÄ which maximizes im (g ‚Ä¢ z),
  obtain ‚ü®g‚ÇÄ, hg‚ÇÄ‚ü© := exists_max_im z,
  -- then among those, minimize re
  obtain ‚ü®g, hg, hg'‚ü© := exists_row_one_eq_and_min_re z (bottom_row_coprime g‚ÇÄ),
  refine ‚ü®g, _‚ü©,
  -- `g` has same max im property as `g‚ÇÄ`
  have hg‚ÇÄ' : ‚àÄ (g' : SL(2,‚Ñ§)), (g' ‚Ä¢ z).im ‚â§ (g ‚Ä¢ z).im,
  { have hg'' : (g ‚Ä¢ z).im = (g‚ÇÄ ‚Ä¢ z).im,
    { rw [im_smul_eq_div_norm_sq, im_smul_eq_div_norm_sq, denom_apply, denom_apply, hg] },
    simpa only [hg''] using hg‚ÇÄ },
  split,
  { -- Claim: `1 ‚â§ ‚áënorm_sq ‚Üë(g ‚Ä¢ z)`. If not, then `S‚Ä¢g‚Ä¢z` has larger imaginary part
    contrapose! hg‚ÇÄ',
    refine ‚ü®S * g, _‚ü©,
    rw mul_action.mul_smul,
    exact im_lt_im_S_smul hg‚ÇÄ' },
  { show |(g ‚Ä¢ z).re| ‚â§ 1 / 2, -- if not, then either `T` or `T'` decrease |Re|.
    rw abs_le,
    split,
    { contrapose! hg',
      refine ‚ü®T * g, by simp [T, matrix.mul, matrix.dot_product, fin.sum_univ_succ], _‚ü©,
      rw mul_action.mul_smul,
      have : |(g ‚Ä¢ z).re + 1| < |(g ‚Ä¢ z).re| :=
        by cases abs_cases ((g ‚Ä¢ z).re + 1); cases abs_cases (g ‚Ä¢ z).re; linarith,
      convert this,
      simp [T] },
    { contrapose! hg',
      refine ‚ü®T' * g, by simp [T', matrix.mul, matrix.dot_product, fin.sum_univ_succ], _‚ü©,
      rw mul_action.mul_smul,
      have : |(g ‚Ä¢ z).re - 1| < |(g ‚Ä¢ z).re| :=
        by cases abs_cases ((g ‚Ä¢ z).re - 1); cases abs_cases (g ‚Ä¢ z).re; linarith,
      convert this,
      simp [T', sub_eq_add_neg] } }
end

end fundamental_domain

end modular_group
