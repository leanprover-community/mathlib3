/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, David Loeffler
-/
import algebra.module.submodule.basic
import analysis.complex.upper_half_plane.basic
import linear_algebra.general_linear_group
import linear_algebra.special_linear_group
import algebra.direct_sum.ring
import number_theory.modular
import geometry.manifold.mfderiv
import analysis.complex.upper_half_plane.functions_bounded_at_inf

/-!
# Modular forms

This file defines modular forms and proves some basic properties about them.

We begin by defining the `slash` operator on the space of functions `‚Ñç ‚Üí ‚ÑÇ` and from this
define the notion of weakly modular form.

Finally we construct the vector space of modular forms and prove that the product of two
modular forms is a modular form (of higher weight).
-/

universes u v

open complex upper_half_plane

open_locale topological_space manifold upper_half_plane

noncomputable theory

local notation `‚Ñç'` := (‚ü®upper_half_plane.upper_half_space ,
 upper_half_plane.upper_half_plane_is_open‚ü©: topological_space.opens ‚ÑÇ)

local prefix `‚Üë‚Çò`:1024 := @coe _ (matrix (fin 2) (fin 2) _) _

local notation `GL(` n `, ` R `)`‚Å∫ := matrix.GL_pos (fin n) R

local notation `SL(` n `, ` R `)` := matrix.special_linear_group (fin n) R

variable (M : GL(2, ‚Ñù)‚Å∫)

/--The weight `k` action of `GL(2, ‚Ñù)‚Å∫` on functions `f : ‚Ñç ‚Üí ‚ÑÇ`. -/
def slash : ‚Ñ§ ‚Üí GL(2, ‚Ñù)‚Å∫ ‚Üí (‚Ñç ‚Üí ‚ÑÇ) ‚Üí (‚Ñç ‚Üí ‚ÑÇ) := Œª k Œ≥ f,
  (Œª (x : ‚Ñç), f (Œ≥ ‚Ä¢ x) * (((‚Üë‚Çò Œ≥).det) : ‚Ñù)^(k-1) * (upper_half_plane.denom Œ≥ x)^(-k))

namespace modular_forms

variables {Œì : subgroup SL(2,‚Ñ§)} {k: ‚Ñ§} (f : (‚Ñç ‚Üí ‚ÑÇ))

localized "notation f ` ‚à£[`:100 k `]`:0 Œ≥ :100 := slash k Œ≥ f" in modular_form

lemma slash_right_action (k : ‚Ñ§) (A B : GL(2, ‚Ñù)‚Å∫) (f : ‚Ñç ‚Üí ‚ÑÇ) :
  (f ‚à£[k] A) ‚à£[k] B = f ‚à£[k] (A * B) :=
begin
  ext1,
  simp_rw [slash,(upper_half_plane.denom_cocycle A B x)],
  have e3 : (A * B) ‚Ä¢ x = A ‚Ä¢ B ‚Ä¢ x , by { convert (upper_half_plane.mul_smul' A B x), } ,
  rw e3,
  simp only [upper_half_plane.num, upper_half_plane.denom, of_real_mul, subgroup.coe_mul, coe_coe,
    upper_half_plane.coe_smul, units.coe_mul, matrix.mul_eq_mul, matrix.det_mul,
    upper_half_plane.smul_aux, upper_half_plane.smul_aux', subtype.coe_mk] at *,
  field_simp,
  ring_nf,
  have : (((‚Üë(‚ÜëA : GL (fin 2) ‚Ñù) : (matrix (fin 2) (fin 2) ‚Ñù)).det : ‚ÑÇ) *
    ((‚Üë(‚ÜëB : GL (fin 2) ‚Ñù) : (matrix (fin 2) (fin 2) ‚Ñù)).det : ‚ÑÇ))^(k-1) =
    ((‚Üë(‚ÜëB : GL (fin 2) ‚Ñù) : (matrix (fin 2) (fin 2) ‚Ñù)).det : ‚ÑÇ)^(k-1) *
    ((‚Üë(‚ÜëA : GL (fin 2) ‚Ñù) : (matrix (fin 2) (fin 2) ‚Ñù)).det : ‚ÑÇ)^(k-1) ,
    by {simp_rw [‚Üêmul_zpow, mul_comm]},
  simp_rw [this, ‚Üê mul_assoc, ‚Üêmul_zpow],
  end

lemma slash_add (k : ‚Ñ§) (A : GL(2, ‚Ñù)‚Å∫) (f g : ‚Ñç ‚Üí ‚ÑÇ) :
  (f + g) ‚à£[k] A = (f ‚à£[k] A) + (g ‚à£[k] A) :=
begin
  simp only [slash, pi.add_apply, matrix.general_linear_group.coe_det_apply, subtype.val_eq_coe,
    coe_coe],
  ext1,
  simp only [pi.add_apply],
  ring,
end

lemma slash_mul_one (k : ‚Ñ§) (f : ‚Ñç ‚Üí ‚ÑÇ) : (f ‚à£[k] 1) = f :=
begin
 simp_rw slash,
 ext1,
 simp,
end

--Need to make the API better because of this
lemma slash_mul_one_SL2 (k : ‚Ñ§) (f : ‚Ñç ‚Üí ‚ÑÇ) : (f ‚à£[k] (1 : SL(2, ‚Ñ§))) = f :=
begin
  have : ((1 : SL(2, ‚Ñ§)) : GL(2, ‚Ñù)‚Å∫) = (1 : GL(2, ‚Ñù)‚Å∫),
    by { ext, simp, },
  rw this,
  apply slash_mul_one,
end

lemma smul_slash (k : ‚Ñ§) (A : GL(2, ‚Ñù)‚Å∫) (f : ‚Ñç ‚Üí ‚ÑÇ) (c : ‚ÑÇ) : (c ‚Ä¢ f) ‚à£[k] A = c ‚Ä¢ (f ‚à£[k] A) :=
begin
  ext1,
  simp_rw slash,
  simp only [slash, algebra.id.smul_eq_mul, matrix.general_linear_group.coe_det_apply,
    pi.smul_apply, subtype.val_eq_coe, coe_coe],
  ring,
end

lemma slash_mul (k1 k2 : ‚Ñ§) (A : GL(2, ‚Ñù)‚Å∫) (f g : ‚Ñç ‚Üí ‚ÑÇ) :
  (f * g) ‚à£[k1+k2] A = (((‚Üë‚Çò A).det) : ‚Ñù) ‚Ä¢ (f ‚à£[k1] A) * (g ‚à£[k2] A) :=
begin
  ext1,
  simp only [slash, matrix.general_linear_group.coe_det_apply, subtype.val_eq_coe, ‚Üêmul_assoc],
  have h1 : ((((‚Üë‚Çò A).det) : ‚Ñù)^(k1 + k2 - 1) : ‚ÑÇ) =
  (((‚Üë‚Çò A).det) : ‚Ñù) * (((‚Üë‚Çò A).det) : ‚Ñù)^(k1 - 1) * (((‚Üë‚Çò A).det) : ‚Ñù)^(k2 - 1),
  by {simp only [mul_assoc, matrix.general_linear_group.coe_det_apply, subtype.val_eq_coe, coe_coe],
  rw [‚Üêzpow_add‚ÇÄ, ‚Üêzpow_one_add‚ÇÄ],
  ring_exp,
  all_goals { norm_cast, apply (matrix.GL_pos.det_ne_zero A), }, },
  have h22 : (upper_half_plane.denom A x)^(-(k1+k2)) = (upper_half_plane.denom A x)^(-k1) *
    (upper_half_plane.denom A x)^(-k2),
  by { rw [int.neg_add, zpow_add‚ÇÄ], exact upper_half_plane.denom_ne_zero A x, },
  rw [h1,h22],
  simp only [upper_half_plane.denom, pi.mul_apply, coe_coe, zpow_neg, algebra.smul_mul_assoc,
    pi.smul_apply, real_smul],
  ring,
end

lemma slash_mul_SL2 (k1 k2 : ‚Ñ§) (A : SL(2,‚Ñ§)) (f g : ‚Ñç ‚Üí ‚ÑÇ) :
  (f * g) ‚à£[k1 + k2] A = (f ‚à£[k1] A) * (g ‚à£[k2] A) :=
begin
  have : (((‚Üë‚Çò(A : GL(2,‚Ñù)‚Å∫)).det) : ‚Ñù) = 1,
  { simp only [coe_coe,matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix,
    matrix.special_linear_group.det_coe], },
  simp_rw [slash_mul, this, one_smul],
end

/--The space of functions that are modular-/
def weakly_modular_submodule (k : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) : submodule ‚ÑÇ (‚Ñç ‚Üí ‚ÑÇ) :=
  {carrier := { f : (‚Ñç ‚Üí ‚ÑÇ) | ‚àÄ (Œ≥ : Œì), (f ‚à£[k] (Œ≥ : GL(2, ‚Ñù)‚Å∫)) = f },
  zero_mem' := by { simp only [slash, set.mem_set_of_eq, pi.zero_apply, zero_mul, forall_const],
    refl, },
  add_mem' := by { intros f g hf hg Œ≥,
    rw [slash_add k Œ≥ f g, hf Œ≥, hg Œ≥], },
  smul_mem' := by { intros c f hf Œ≥,
    have : (c ‚Ä¢ f) ‚à£[k] Œ≥ = c ‚Ä¢ (f ‚à£[k] Œ≥), by {apply smul_slash},
    rw (hf Œ≥) at this,
    apply this,} }

lemma wmodular_mem (k : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) (f : ‚Ñç ‚Üí ‚ÑÇ) :
  f ‚àà (weakly_modular_submodule k Œì) ‚Üî ‚àÄ (Œ≥ : Œì), (f ‚à£[k] (Œ≥ : GL(2, ‚Ñù)‚Å∫)) = f := iff.rfl

lemma slash_mul_subgroup (k1 k2 : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) (A : Œì) (f g : ‚Ñç ‚Üí ‚ÑÇ) :
  (f * g) ‚à£[k1+k2] A = (f ‚à£[k1] A) * (g ‚à£[k2] A) :=
begin
  have : (((‚Üë‚Çò(A : GL(2,‚Ñù)‚Å∫)).det) : ‚Ñù) = 1,
  by { simp only [coe_coe,matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix,
    matrix.special_linear_group.det_coe], },
  simp_rw [slash_mul, this, one_smul],
end

/--A function `f : ‚Ñç ‚Üí ‚ÑÇ` is modular, of weight `k ‚àà ‚Ñ§` and level `Œì`, if for every matrix in
 `Œ≥ ‚àà Œì` we have `f(Œ≥ ‚Ä¢ z)= (c*z+d)^k f(z)` where `Œ≥= ![![a, b], ![c, d]]`,
 and it acts on `‚Ñç` via Moebius trainsformations. -/
lemma wmodular_mem' (k : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) (f : ‚Ñç ‚Üí ‚ÑÇ) :
  f ‚àà (weakly_modular_submodule k Œì) ‚Üî ‚àÄ Œ≥ : Œì, ‚àÄ z : ‚Ñç,
  f (Œ≥ ‚Ä¢ z) = ((‚Üë‚ÇòŒ≥ 1 0 : ‚Ñù) * z +(‚Üë‚ÇòŒ≥ 1 1 : ‚Ñù))^k * f z :=
begin
  simp only [wmodular_mem],
  split,
  { intros h1 Œ≥ z,
  have h3 : (f ‚à£[k] Œ≥) z = f z , by {simp_rw (h1 Œ≥)},
  rw [‚Üêh3, slash, mul_comm],
  have h55 := zpow_neg_mul_zpow_self k (upper_half_plane.denom_ne_zero (Œ≥ : GL(2, ‚Ñù)‚Å∫) z),
  simp only [upper_half_plane.denom, upper_half_plane.subgroup_to_sl_moeb, upper_half_plane.sl_moeb,
    coe_coe, matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix,
    matrix.special_linear_group.coe_matrix_coe, int.coe_cast_ring_hom, matrix.map_apply,
    of_real_int_cast] at *,
  rw [mul_assoc, h55, ‚Üêint.coe_cast_ring_hom, ‚Üêmatrix.special_linear_group.coe_matrix_coe,
    matrix.special_linear_group.det_coe ((Œ≥ : SL(2, ‚Ñ§)) : SL(2, ‚Ñù))],
  simp only [of_real_one, one_zpow, mul_one], },
  { intros hf Œ≥,
  simp_rw slash,
  ext1,
  rw [‚Üêupper_half_plane.subgroup_moeb, (hf Œ≥ x), mul_comm],
  have h55 := zpow_neg_mul_zpow_self k (upper_half_plane.denom_ne_zero (Œ≥ : GL(2, ‚Ñù)‚Å∫) x),
  simp_rw upper_half_plane.denom at *,
  simp only [matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix, coe_coe,
    matrix.special_linear_group.coe_matrix_coe, int.coe_cast_ring_hom, matrix.map_apply,
    of_real_int_cast] at h55,
  simp only [coe_coe, matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix,
    matrix.map_apply, of_real_int_cast],
  rw (matrix.special_linear_group.det_coe ((Œ≥ : SL(2, ‚Ñ§)) : SL(2, ‚Ñù))),
  simp only [matrix.special_linear_group.coe_matrix_coe, int.coe_cast_ring_hom, matrix.map_apply,
    of_real_int_cast, of_real_one, one_zpow, mul_one],
  simp_rw [‚Üê mul_assoc, h55],
  simp },
end

lemma mul_modular (k_1 k_2 : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) (f g : ‚Ñç ‚Üí ‚ÑÇ)
  (hf : f ‚àà weakly_modular_submodule k_1 Œì) (hg : g ‚àà weakly_modular_submodule k_2 Œì) :
  f * g ‚àà weakly_modular_submodule (k_1 + k_2) Œì :=
begin
  simp only [wmodular_mem', pi.mul_apply, coe_coe] at *,
  intros Œ≥ z,
  rw [(hf Œ≥ z), (hg Œ≥ z)],
  have pown := zpow_add‚ÇÄ (upper_half_plane.denom_ne_zero (Œ≥ : GL(2, ‚Ñù)‚Å∫) z) k_1 k_2,
  simp only [upper_half_plane.denom, coe_fn_coe_base, ne.def,
    matrix.general_linear_group.coe_fn_eq_coe, coe_coe] at pown,
  rw pown,
  ring,
end

/-- A function `f : ‚Ñç ‚Üí ‚ÑÇ` is a modular form weight `k ‚àà ‚Ñ§` and of level `Œì` if it is holomorphic,
 weakly modular and bounded at infinity -/
structure is_modular_form_of_weight_and_level (k : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) (f : ‚Ñç ‚Üí ‚ÑÇ) : Prop :=
  (hol : mdifferentiable ùìò(‚ÑÇ) ùìò(‚ÑÇ) (‚Üëf : ‚Ñç' ‚Üí ‚ÑÇ))
  (transf : f ‚àà weakly_modular_submodule k Œì)
  (infinity : ‚àÄ (A : SL(2,‚Ñ§)), is_bound_at_inf (f ‚à£[k] A))

/-- A function `f : ‚Ñç ‚Üí ‚ÑÇ` is a cusp form of weight `k ‚àà ‚Ñ§` and of level `Œì` if it is holomorphic,
 weakly modular, and zero at infinity -/
structure is_cusp_form_of_weight_and_level (k : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) (f : ‚Ñç ‚Üí ‚ÑÇ) : Prop :=
  (hol : mdifferentiable ùìò(‚ÑÇ) ùìò(‚ÑÇ) (‚Üëf : ‚Ñç' ‚Üí ‚ÑÇ))
  (transf : f ‚àà weakly_modular_submodule k Œì)
  (infinity : ‚àÄ (A : SL(2,‚Ñ§)), is_zero_at_inf (f ‚à£[k] A))

/-- The zero modular form is a cusp form-/
lemma zero_cusp_form : is_cusp_form_of_weight_and_level k Œì 0 :=
{ hol := by {apply mdifferentiable_zero,},
  transf := (weakly_modular_submodule k Œì).zero_mem',
  infinity := by { intro A,
    convert zero_form_is_zero_at_inf,
    rw slash,
    simp only [pi.zero_apply, zero_mul],
    refl, } }

lemma is_modular_form_of_weight_and_level_of_is_cusp_form_of_weight_and_level {f : ‚Ñç ‚Üí ‚ÑÇ}
  (h : is_cusp_form_of_weight_and_level k Œì f) : is_modular_form_of_weight_and_level k Œì f :=
{ hol := h.1,
  transf := h.2,
  infinity := Œª (A : SL(2,‚Ñ§)), is_zero_at_inf_is_bound _ (h.3 A), }

 /-- The zero modular form is a modular form-/
lemma zero_mod_form : is_modular_form_of_weight_and_level k Œì 0 :=
begin
  apply_rules [is_modular_form_of_weight_and_level_of_is_cusp_form_of_weight_and_level,
    zero_cusp_form],
end

/-- This is the space of modular forms of weight `k` and level `Œì`-/
def space_of_mod_forms_of_weight_and_level (k : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) : submodule ‚ÑÇ (‚Ñç ‚Üí ‚ÑÇ) :=
{ carrier := { f : ‚Ñç ‚Üí ‚ÑÇ | is_modular_form_of_weight_and_level k Œì f},
  zero_mem':= by { simp only [set.mem_set_of_eq], apply zero_mod_form, },
  add_mem' := by { intros a b ha hb,
    split,
    exact mdifferentiable_add ha.hol hb.hol,
    exact (weakly_modular_submodule k Œì).add_mem' ha.transf hb.transf,
    intro A, rw slash_add,
    exact (bounded_at_infty_submodule.add_mem' (ha.infinity A) (hb.infinity A)) },
  smul_mem' := by { intros c f hf,
    split,
    exact mdifferentiable_smul _ hf.hol,
    exact (weakly_modular_submodule k Œì).smul_mem' _ hf.transf,
    intro A,
    rw smul_slash,
    apply (bounded_at_infty_submodule.smul_mem' c (hf.infinity A)), }, }

localized "notation `M`:= space_of_mod_forms_of_weight_and_level " in modular_forms

/-- This is the space of cuspforms of weigth `k` and level `Œì` -/
def space_of_cusp_forms_of_weight_and_level (k : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) : submodule ‚ÑÇ (‚Ñç ‚Üí ‚ÑÇ) :=
{ carrier := is_cusp_form_of_weight_and_level k Œì,
  zero_mem' := by apply zero_cusp_form,
  add_mem' := by { intros a b ha hb,
    split,
    exact mdifferentiable_add ha.hol hb.hol,
    exact (weakly_modular_submodule k Œì).add_mem' ha.transf hb.transf,
    intro A, rw slash_add,
    apply (zero_at_infty_submodule.add_mem' (ha.infinity A) (hb.infinity A)) },
  smul_mem' := by { intros c f hf, split,
    exact mdifferentiable_smul _ hf.hol,
    exact (weakly_modular_submodule k Œì).smul_mem' _ hf.transf,
    intro A,
    rw smul_slash,
    apply zero_at_infty_submodule.smul_mem' c (hf.infinity A), }, }

localized "notation `S`:= space_of_cusp_forms_of_weight_and_level" in modular_forms

/--The product of two modular forms is a modular form whose weight is the sum of the weights-/
lemma mul_modform (k_1 k_2 : ‚Ñ§) (Œì : subgroup SL(2,‚Ñ§)) (f g : ‚Ñç ‚Üí ‚ÑÇ)
  (hf : f ‚àà M k_1 Œì) (hg : g ‚àà M k_2 Œì) : f * g ‚àà (M (k_1 + k_2) Œì) :=
begin
  refine ‚ü®mdifferentiable_mul hf.1 hg.1, mul_modular _ _ _ _ _ hf.2 hg.2, _‚ü©,
  intro A,
  rw slash_mul_SL2 k_1 k_2 A f g,
  exact prod_of_bound_is_bound (hf.infinity A) (hg.infinity A),
end

/-! Constant functions are modular forms of weight 0 -/
section const_mod_form

/--A modular form of weight zero-/
def const_one_form : ‚Ñç ‚Üí ‚ÑÇ := 1

/-- The constant function is bounded at infinity -/
lemma const_one_form_is_bound : is_bound_at_inf const_one_form :=
  @asymptotics.is_O_const_const _ _ ‚ÑÇ _ _ 1 _ one_ne_zero _

/-- The constant function 1 is invariant under any subgroup of SL2Z -/
lemma const_one_form_is_invar (A : SL(2,‚Ñ§)) : const_one_form ‚à£[0] A = const_one_form :=
begin
  rw [slash, const_one_form],
  dsimp only,
  have : (((‚Üë‚Çò(A : GL(2,‚Ñù)‚Å∫)).det) : ‚Ñù) = 1,
  { simp only [coe_coe,
      matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix,
      matrix.special_linear_group.det_coe], },
  rw [zero_sub, this],
  simp only [pi.one_apply, of_real_one, one_zpow, mul_one, neg_zero', zpow_zero],
  refl,
end

/-- The constant function 1 is modular of weight 0 -/
lemma const_mod_form : const_one_form ‚àà M 0 Œì :=
{ hol := by { simp_rw const_one_form, apply mdifferentiable_one, },
  transf := by { intro Œ≥, apply const_one_form_is_invar, },
  infinity := by { intro A, rw const_one_form_is_invar A, exact const_one_form_is_bound,} }

end const_mod_form

end modular_forms
