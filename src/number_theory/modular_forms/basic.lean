/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import geometry.manifold.mfderiv
import analysis.complex.upper_half_plane.functions_bounded_at_infty
import analysis.complex.upper_half_plane.topology
import number_theory.modular_forms.slash_invariant_forms
import ring_theory.graded_algebra.basic
/-!
# Modular forms

This file defines modular forms and proves some basic properties about them.

We begin by defining modular forms and cusp forms as extension of `slash_invariant_forms` then we
define the space of modular forms, cusp forms and prove that the product of two modular forms is a
modular form.
-/

open complex upper_half_plane

open_locale topological_space manifold upper_half_plane

noncomputable theory

instance upper_half_plane.charted_space : charted_space ‚ÑÇ ‚Ñç :=
upper_half_plane.open_embedding_coe.singleton_charted_space

instance upper_half_plane.smooth_manifold_with_corners : smooth_manifold_with_corners ùìò(‚ÑÇ) ‚Ñç :=
upper_half_plane.open_embedding_coe.singleton_smooth_manifold_with_corners ùìò(‚ÑÇ)

local prefix `‚Üë‚Çò`:1024 := @coe _ (matrix (fin 2) (fin 2) _) _

local notation `GL(` n `, ` R `)`‚Å∫ := matrix.GL_pos (fin n) R

local notation `SL(` n `, ` R `)` := matrix.special_linear_group (fin n) R

section modular_form

open modular_form

variables (F : Type*) (Œì : subgroup SL(2, ‚Ñ§)) (k : ‚Ñ§)

local notation f `‚à£[`:73 k:0, A `]` :72 := slash_action.map ‚ÑÇ k A f

set_option old_structure_cmd true

/--These are `slash_invariant_form`'s that are holomophic and bounded at infinity. -/
structure modular_form extends slash_invariant_form Œì k :=
(hol' : mdifferentiable ùìò(‚ÑÇ) ùìò(‚ÑÇ) (to_fun : ‚Ñç ‚Üí ‚ÑÇ))
(bdd_at_infty' : ‚àÄ (A : SL(2, ‚Ñ§)), is_bounded_at_im_infty (to_fun ‚à£[k, A]))

/-- The `slash_invariant_form` associated to a `modular_form`. -/
add_decl_doc modular_form.to_slash_invariant_form

/--These are `slash_invariant_form`s that are holomophic and zero at infinity. -/
structure cusp_form extends slash_invariant_form Œì k :=
(hol' : mdifferentiable ùìò(‚ÑÇ) ùìò(‚ÑÇ) (to_fun : ‚Ñç ‚Üí ‚ÑÇ))
(zero_at_infty' : ‚àÄ (A : SL(2, ‚Ñ§)), is_zero_at_im_infty (to_fun ‚à£[k, A]))

/-- The `slash_invariant_form` associated to a `cusp_form`. -/
add_decl_doc cusp_form.to_slash_invariant_form

/--`modular_form_class F Œì k` says that `F` is a type of bundled functions that extend
`slash_invariant_forms_class` by requiring that the functions be holomorphic and bounded
at infinity. -/
class modular_form_class extends slash_invariant_form_class F Œì k :=
(hol : ‚àÄ f : F, mdifferentiable ùìò(‚ÑÇ) ùìò(‚ÑÇ) (f : ‚Ñç ‚Üí ‚ÑÇ))
(bdd_at_infty : ‚àÄ (f : F) (A : SL(2, ‚Ñ§)), is_bounded_at_im_infty (f ‚à£[k, A]))

/--`cusp_form_class F Œì k` says that `F` is a type of bundled functions that extend
`slash_invariant_forms_class` by requiring that the functions be holomorphic and zero
at infinity. -/
class cusp_form_class extends slash_invariant_form_class F Œì k :=
(hol : ‚àÄ f : F, mdifferentiable ùìò(‚ÑÇ) ùìò(‚ÑÇ) (f : ‚Ñç ‚Üí ‚ÑÇ))
(zero_at_infty : ‚àÄ (f : F) (A : SL(2, ‚Ñ§)), is_zero_at_im_infty (f ‚à£[k, A]))

@[priority 100]
instance modular_form_class.modular_form : modular_form_class (modular_form Œì k) Œì k :=
{ coe := modular_form.to_fun,
  coe_injective' := Œª f g h, by cases f; cases g; congr',
  slash_action_eq := modular_form.slash_action_eq',
  hol := modular_form.hol',
  bdd_at_infty := modular_form.bdd_at_infty' }

@[priority 100]
instance cusp_form_class.cusp_form : cusp_form_class (cusp_form Œì k) Œì k :=
{ coe := cusp_form.to_fun,
  coe_injective' := Œª f g h, by cases f; cases g; congr',
  slash_action_eq := cusp_form.slash_action_eq',
  hol := cusp_form.hol',
  zero_at_infty := cusp_form.zero_at_infty' }

variables {F Œì k}

@[simp] lemma modular_form_to_fun_eq_coe {f : modular_form Œì k} : f.to_fun = (f : ‚Ñç ‚Üí ‚ÑÇ) := rfl
@[simp] lemma cusp_form_to_fun_eq_coe {f : cusp_form Œì k} : f.to_fun = (f : ‚Ñç ‚Üí ‚ÑÇ) := rfl

@[ext] theorem modular_form.ext {f g : modular_form Œì k} (h : ‚àÄ x, f x = g x) : f = g :=
fun_like.ext f g h

@[ext] theorem cusp_form.ext {f g : cusp_form Œì k} (h : ‚àÄ x, f x = g x) : f = g :=
fun_like.ext f g h

/-- Copy of a `modular_form` with a new `to_fun` equal to the old one. Useful to fix
definitional equalities. -/
protected def modular_form.copy (f : modular_form Œì k) (f' : ‚Ñç ‚Üí ‚ÑÇ) (h : f' = ‚áëf) :
  modular_form Œì k :=
{ to_fun := f',
  slash_action_eq' := h.symm ‚ñ∏ f.slash_action_eq',
  hol' := h.symm ‚ñ∏ f.hol',
  bdd_at_infty' := Œª A, h.symm ‚ñ∏ f.bdd_at_infty' A }

/-- Copy of a `cusp_form` with a new `to_fun` equal to the old one. Useful to fix
definitional equalities. -/
protected def cusp_form.copy (f : cusp_form Œì k) (f' : ‚Ñç ‚Üí ‚ÑÇ) (h : f' = ‚áëf) :
  cusp_form Œì k :=
{ to_fun := f',
  slash_action_eq' := h.symm ‚ñ∏ f.slash_action_eq',
  hol' := h.symm ‚ñ∏ f.hol',
  zero_at_infty' := Œª A, h.symm ‚ñ∏ f.zero_at_infty' A }

end modular_form

namespace modular_form

open slash_invariant_form

variables {F : Type*} {Œì : subgroup SL(2, ‚Ñ§)} {k : ‚Ñ§}

instance has_add : has_add (modular_form Œì k) :=
‚ü® Œª f g,
  { hol' := f.hol'.add g.hol',
    bdd_at_infty' := Œª A, by simpa using (f.bdd_at_infty' A).add (g.bdd_at_infty' A),
    .. (f : slash_invariant_form Œì k) + g }‚ü©

@[simp] lemma coe_add (f g : modular_form Œì k) : ‚áë(f + g) = f + g := rfl

@[simp] lemma add_apply (f g : modular_form Œì k) (z : ‚Ñç) : (f + g) z = f z + g z := rfl

instance has_zero : has_zero (modular_form Œì k) :=
‚ü® { hol' := (Œª _, mdifferentiable_at_const ùìò(‚ÑÇ, ‚ÑÇ) ùìò(‚ÑÇ, ‚ÑÇ)),
    bdd_at_infty' := Œª A, by simpa using zero_form_is_bounded_at_im_infty,
    .. (0 : slash_invariant_form Œì k) } ‚ü©

@[simp] lemma coe_zero : ‚áë(0 : modular_form Œì k) = (0 : ‚Ñç ‚Üí ‚ÑÇ) := rfl

@[simp] lemma zero_apply (z : ‚Ñç) : (0 : modular_form Œì k) z = 0 := rfl

section
variables {Œ± : Type*} [has_smul Œ± ‚ÑÇ] [is_scalar_tower Œ± ‚ÑÇ ‚ÑÇ]

instance has_smul : has_smul Œ± (modular_form Œì k) :=
‚ü® Œª c f,
  { to_fun := c ‚Ä¢ f,
    hol' := by simpa using f.hol'.const_smul (c ‚Ä¢ (1 : ‚ÑÇ)),
    bdd_at_infty' := Œª A, by simpa using (f.bdd_at_infty' A).const_smul_left (c ‚Ä¢ (1 : ‚ÑÇ)),
     .. c ‚Ä¢ (f : slash_invariant_form Œì k)}‚ü©

@[simp] lemma coe_smul (f : (modular_form Œì k)) (n : Œ±) : ‚áë(n ‚Ä¢ f) = n ‚Ä¢ f := rfl
@[simp] lemma smul_apply (f : (modular_form Œì k)) (n : Œ±) (z : ‚Ñç) :
   (n ‚Ä¢ f) z = n ‚Ä¢ (f z) := rfl
end

instance has_neg : has_neg (modular_form Œì k) :=
‚ü®Œª f,
  { to_fun := -f,
    hol' := f.hol'.neg,
    bdd_at_infty':= Œª A, by simpa using (f.bdd_at_infty' A).neg,
    .. -(f : slash_invariant_form Œì k) }‚ü©

@[simp] lemma coe_neg (f : modular_form Œì k) : ‚áë(-f) = -f := rfl

@[simp] lemma neg_apply (f : modular_form Œì k) (z : ‚Ñç) : (-f) z = - (f z) := rfl

instance has_sub : has_sub (modular_form Œì k) :=
‚ü® Œª f g, f + -g ‚ü©

@[simp] lemma coe_sub (f g : (modular_form Œì k)) : ‚áë(f - g) = f - g := rfl

@[simp] lemma sub_apply (f g : modular_form Œì k) (z : ‚Ñç) : (f - g) z = f z - g z := rfl

instance : add_comm_group (modular_form Œì k) :=
fun_like.coe_injective.add_comm_group _ rfl coe_add coe_neg coe_sub coe_smul coe_smul

/--Additive coercion from `modular_form` to `‚Ñç ‚Üí ‚ÑÇ`. -/
@[simps] def coe_hom : (modular_form Œì k) ‚Üí+ (‚Ñç ‚Üí ‚ÑÇ) :=
{ to_fun := Œª f, f,
  map_zero' := coe_zero,
  map_add' := Œª _ _, rfl }

instance : module ‚ÑÇ (modular_form Œì k) :=
function.injective.module ‚ÑÇ coe_hom fun_like.coe_injective (Œª _ _, rfl)

instance : inhabited (modular_form Œì k) := ‚ü®0‚ü©

/--The modular form of weight `k_1 + k_2` given by the product of two modular forms of weights
`k_1` and `k_2`. -/
def mul {k_1 k_2 : ‚Ñ§} {Œì : subgroup SL(2, ‚Ñ§)} (f : (modular_form Œì k_1))
  (g : (modular_form Œì k_2)) : (modular_form Œì (k_1 + k_2)) :=
{ to_fun := f * g,
  slash_action_eq' := Œª A, by simp_rw [mul_slash_subgroup, modular_form_class.slash_action_eq],
  hol' := f.hol'.mul g.hol',
  bdd_at_infty' := Œª A, by simpa using (f.bdd_at_infty' A).mul (g.bdd_at_infty' A) }

@[simp] lemma mul_coe {k_1 k_2 : ‚Ñ§} {Œì : subgroup SL(2, ‚Ñ§)} (f : (modular_form Œì k_1))
  (g : (modular_form Œì k_2)) : ((f.mul g) : ‚Ñç ‚Üí ‚ÑÇ) = f * g := rfl

instance : has_one (modular_form Œì 0) :=
‚ü®{  hol' := Œª x, mdifferentiable_at_const ùìò(‚ÑÇ, ‚ÑÇ) ùìò(‚ÑÇ, ‚ÑÇ),
    bdd_at_infty' := Œª A, by simpa using at_im_infty.const_bounded_at_filter (1:‚ÑÇ),
      .. (1 : slash_invariant_form Œì 0) }‚ü©

@[simp] lemma one_coe_eq_one : ((1 : modular_form Œì 0) : ‚Ñç ‚Üí ‚ÑÇ) = 1 := rfl

/-cast for modular forms, which is useful for removing `heq`'s. -/
def mcast {a b : ‚Ñ§} {Œì : subgroup SL(2, ‚Ñ§)} (h : a = b) (f : modular_form Œì a) :
  (modular_form Œì b) :=
{ to_fun := (f : ‚Ñç ‚Üí ‚ÑÇ),
  slash_action_eq' := by {intro A, have := f.slash_action_eq' A, convert this, exact h.symm,},
  hol' := f.hol',
  bdd_at_infty' := by {intro A, convert f.bdd_at_infty' A, exact h.symm }}

lemma type_eq {a b : ‚Ñ§} (Œì : subgroup SL(2, ‚Ñ§)) (h : a = b) :
  (modular_form Œì a) = (modular_form Œì b) :=
begin
  induction h,
  refl,
end

lemma cast_eq_mcast {a b : ‚Ñ§} {Œì : subgroup SL(2, ‚Ñ§)} (h : a = b) (f : modular_form Œì a) :
  cast (type_eq Œì h) f = mcast h f :=
begin
  induction h,
  ext1,
  refl,
end

lemma heq_one_mul (k : ‚Ñ§) {Œì : subgroup SL(2, ‚Ñ§)} (f : modular_form Œì k) :
  (1 : modular_form Œì 0).mul f == f :=
begin
   apply heq_of_cast_eq (type_eq Œì (zero_add k).symm).symm,
    funext,
    rw [cast_eq_mcast, mcast, mul],
    simp only [one_coe_eq_one, one_mul],
    ext1,
    refl,
    simp only [zero_add]
end

lemma heq_mul_one (k : ‚Ñ§) {Œì : subgroup SL(2, ‚Ñ§)} (f : modular_form Œì k) :
  f.mul (1 : modular_form Œì 0) == f :=
begin
      apply heq_of_cast_eq (type_eq Œì (add_zero k).symm).symm,
      funext,
      rw [cast_eq_mcast, mcast, mul],
      simp only [one_coe_eq_one, mul_one],
      ext1,
      refl,
      simp only [add_zero]
end

lemma heq_mul_assoc {a b c : ‚Ñ§} (f : modular_form Œì a) (g : modular_form Œì b)
  (h : modular_form Œì c) : (f.mul g).mul h ==  f.mul (g.mul h) :=
begin
  apply heq_of_cast_eq (type_eq Œì (add_assoc a b c)),
  rw [cast_eq_mcast, mcast],
  ext1,
  simp only [mul_coe, pi.mul_apply, ‚Üêmul_assoc],
  refl,
end

lemma heq_mul_comm (a b : ‚Ñ§) (f : modular_form Œì a) (g : modular_form Œì b) : f.mul g == g.mul f :=
begin
  apply heq_of_cast_eq (type_eq Œì (add_comm a b)),
  rw [cast_eq_mcast, mcast],
  ext1,
  simp only [mul_coe, pi.mul_apply, mul_comm],
  refl,
end

instance graded_mod_ring (Œì : subgroup SL(2, ‚Ñ§)) : direct_sum.gcomm_ring (Œª k, modular_form Œì k) :=
{ mul := Œª k_1, Œª k_2, Œª f g, f.mul g,
  one := 1,
  one_mul := by {intro f,
    rw [graded_monoid.ghas_one.to_has_one, graded_monoid.ghas_mul.to_has_mul],
    apply sigma.ext,
    { simp only [zero_add] },
    { simp only [submodule.coe_mk, one_mul, heq_one_mul] }},
  mul_one := by {intro f,
    rw [graded_monoid.ghas_one.to_has_one, graded_monoid.ghas_mul.to_has_mul],
    apply sigma.ext,
    { simp only [add_zero] },
    { simp only [submodule.coe_mk, mul_one, heq_mul_one]}},
  mul_assoc := by {intros f g h,
    rw graded_monoid.ghas_mul.to_has_mul,
    apply sigma.ext,
    { apply add_assoc },
    { simp only [submodule.coe_mk, heq_mul_assoc] }},
  mul_zero := by {intros i j f, ext1, simp,},
  zero_mul := by {intros i j f, ext1, simp,},
  mul_add := by {intros i j f g h,
    ext1,
    simp only [pi.mul_apply, mul_add, mul_coe, add_apply],},
  add_mul := by {intros i j f g h,
    ext1,
    simp only [add_mul, mul_coe, pi.mul_apply, add_apply],},
  mul_comm := by {intros f g,
    rw graded_monoid.ghas_mul.to_has_mul,
    apply sigma.ext,
    { apply add_comm },
    { apply heq_mul_comm }},
  gnpow_zero' := by {intro f,
    apply sigma.ext,
    repeat {refl}},
  gnpow_succ' := by {intros n f,
    rw graded_monoid.ghas_mul.to_has_mul,
    apply sigma.ext,
    repeat {refl}},
  nat_cast := Œª n, n ‚Ä¢ (1 : (modular_form Œì 0)),
  nat_cast_zero := by {simp},
  nat_cast_succ := by {intro n, simp only [add_smul, one_nsmul, add_right_inj], refl,},
  int_cast := Œª n, n ‚Ä¢ (1 : (modular_form Œì 0)),
  int_cast_of_nat := by {simp},
  int_cast_neg_succ_of_nat := by {intro , apply _root_.neg_smul }}

end modular_form

namespace cusp_form
open modular_form

variables {F : Type*} {Œì : subgroup SL(2, ‚Ñ§)} {k : ‚Ñ§}

instance has_add : has_add (cusp_form Œì k) :=
‚ü® Œª f g,
  { to_fun := f + g,
    hol' := f.hol'.add g.hol',
    zero_at_infty' := Œª A, by simpa using (f.zero_at_infty' A).add (g.zero_at_infty' A),
    .. (f : slash_invariant_form Œì k) + g }‚ü©

@[simp] lemma coe_add (f g : cusp_form Œì k) : ‚áë(f + g) = f + g := rfl

@[simp] lemma add_apply (f g : cusp_form Œì k) (z : ‚Ñç) : (f + g) z = f z + g z := rfl

instance has_zero : has_zero (cusp_form Œì k) :=
‚ü® { to_fun := 0,
    hol' := (Œª _, mdifferentiable_at_const ùìò(‚ÑÇ, ‚ÑÇ) ùìò(‚ÑÇ, ‚ÑÇ)),
    zero_at_infty' := by simpa using filter.zero_zero_at_filter _,
    .. (0 : slash_invariant_form Œì k) }‚ü©

@[simp] lemma coe_zero : ‚áë(0 : cusp_form Œì k) = (0 : ‚Ñç ‚Üí ‚ÑÇ) := rfl

@[simp] lemma zero_apply (z : ‚Ñç) : (0 : cusp_form Œì k) z = 0 := rfl

section
variables {Œ± : Type*} [has_smul Œ± ‚ÑÇ] [is_scalar_tower Œ± ‚ÑÇ ‚ÑÇ]

instance has_smul : has_smul Œ± (cusp_form Œì k) :=
‚ü® Œª c f,
  { to_fun := c ‚Ä¢ f,
    hol' := by simpa using f.hol'.const_smul (c ‚Ä¢ (1 : ‚ÑÇ)),
    zero_at_infty' := Œª A, by simpa using (f.zero_at_infty' A).smul (c ‚Ä¢ (1 : ‚ÑÇ)),
    .. c ‚Ä¢ (f : slash_invariant_form Œì k) }‚ü©

@[simp] lemma coe_smul (f : (cusp_form Œì k)) (n : Œ±) : ‚áë(n ‚Ä¢ f) = n ‚Ä¢ f := rfl
@[simp] lemma smul_apply (f : (cusp_form Œì k)) (n : Œ±) {z : ‚Ñç} :
   (n ‚Ä¢ f) z = n ‚Ä¢ (f z) := rfl

end

instance has_neg : has_neg (cusp_form Œì k) :=
‚ü®Œª f,
  { to_fun := -f,
    hol' := f.hol'.neg,
    zero_at_infty':= Œª A, by simpa using (f.zero_at_infty' A).neg,
    .. -(f : slash_invariant_form Œì k)} ‚ü©

@[simp] lemma coe_neg (f : cusp_form Œì k) : ‚áë(-f) = -f := rfl
@[simp] lemma neg_apply (f : cusp_form Œì k) (z : ‚Ñç) : (-f) z = -(f z) := rfl

instance has_sub : has_sub (cusp_form Œì k) :=
‚ü® Œª f g, f + -g ‚ü©

@[simp] lemma coe_sub (f g : cusp_form Œì k) : ‚áë(f - g) = f - g := rfl
@[simp] lemma sub_apply (f g : cusp_form Œì k) (z : ‚Ñç) : (f - g) z = f z - g z := rfl

instance : add_comm_group (cusp_form Œì k) :=
fun_like.coe_injective.add_comm_group _ rfl coe_add coe_neg coe_sub coe_smul coe_smul

/--Additive coercion from `cusp_form` to `‚Ñç ‚Üí ‚ÑÇ`. -/
@[simps] def coe_hom : (cusp_form Œì k) ‚Üí+ (‚Ñç ‚Üí ‚ÑÇ) :=
{ to_fun := Œª f, f,
  map_zero' := cusp_form.coe_zero,
  map_add' := Œª _ _, rfl }

instance : module ‚ÑÇ (cusp_form Œì k) :=
function.injective.module ‚ÑÇ coe_hom fun_like.coe_injective (Œª _ _, rfl)

instance : inhabited (cusp_form Œì k) := ‚ü®0‚ü©

@[priority 99]
instance [cusp_form_class F Œì k] : modular_form_class F Œì k :=
{ coe := fun_like.coe,
  coe_injective' := fun_like.coe_injective',
  slash_action_eq := cusp_form_class.slash_action_eq,
  hol := cusp_form_class.hol,
  bdd_at_infty := Œª _ _, (cusp_form_class.zero_at_infty _ _).bounded_at_filter}

/--The modular form of weight `k_1 + k_2` given by the product of two modular forms of weights
`k_1` and `k_2`. -/
def mul {k_1 k_2 : ‚Ñ§} {Œì : subgroup SL(2, ‚Ñ§)} (f : (cusp_form Œì k_1))
  (g : (cusp_form Œì k_2)) : (cusp_form Œì (k_1 + k_2)) :=
{ to_fun := f * g,
  slash_action_eq' := Œª A, by simp_rw [mul_slash_subgroup, modular_form_class.slash_action_eq],
  hol' := f.hol'.mul g.hol',
  zero_at_infty' := Œª A, by simpa using (f.zero_at_infty' A).mul (g.zero_at_infty' A) }

@[simp] lemma mul_coe {k_1 k_2 : ‚Ñ§} {Œì : subgroup SL(2, ‚Ñ§)} (f : (cusp_form Œì k_1))
  (g : (cusp_form Œì k_2)) : ((f.mul g) : ‚Ñç ‚Üí ‚ÑÇ) = f * g := rfl

/-cast for modular forms, which is useful for removing `heq`'s. -/
def mcast {a b : ‚Ñ§} {Œì : subgroup SL(2, ‚Ñ§)} (h : a = b) (f : cusp_form Œì a) :
  (cusp_form Œì b) :=
{ to_fun := (f : ‚Ñç ‚Üí ‚ÑÇ),
  slash_action_eq' := by {intro A, have := f.slash_action_eq' A, convert this, exact h.symm,},
  hol' := f.hol',
  zero_at_infty' := by {intro A, convert f.zero_at_infty' A, exact h.symm }}

lemma type_eq {a b : ‚Ñ§} (Œì : subgroup SL(2, ‚Ñ§)) (h : a = b) :
  (cusp_form Œì a) = (cusp_form Œì b) :=
begin
  induction h,
  refl,
end

lemma cast_eq_mcast {a b : ‚Ñ§} {Œì : subgroup SL(2, ‚Ñ§)} (h : a = b) (f : cusp_form Œì a) :
  cast (type_eq Œì h) f = mcast h f :=
begin
  induction h,
  ext1,
  refl,
end

instance gnon_unital_non_assoc_semiring (Œì : subgroup SL(2, ‚Ñ§)) :
  direct_sum.gnon_unital_non_assoc_semiring (Œª k, cusp_form Œì k) :=
{ mul := Œª k_1, Œª k_2, Œª f g, f.mul g,
  mul_zero := by {intros i j f, ext1, simp,},
  zero_mul := by {intros i j f, ext1, simp,},
  mul_add := by {intros i j f g h,
    ext1,
    simp only [pi.mul_apply, mul_add, mul_coe, add_apply],},
  add_mul := by {intros i j f g h,
    ext1,
    simp only [add_mul, mul_coe, pi.mul_apply, add_apply]}}

--what class should it be `gnon_unital_comm_semiring`?

lemma heq_mul_assoc {a b c : ‚Ñ§} (f : cusp_form Œì a) (g : cusp_form Œì b)
  (h : cusp_form Œì c) : (f.mul g).mul h ==  f.mul (g.mul h) :=
begin
  apply heq_of_cast_eq (type_eq Œì (add_assoc a b c)),
  rw [cast_eq_mcast, mcast],
  ext1,
  simp only [mul_coe, pi.mul_apply, ‚Üêmul_assoc],
  refl,
end

lemma heq_mul_comm (a b : ‚Ñ§) (f : cusp_form Œì a) (g : cusp_form Œì b) : f.mul g == g.mul f :=
begin
  apply heq_of_cast_eq (type_eq Œì (add_comm a b)),
  rw [cast_eq_mcast, mcast],
  ext1,
  simp only [mul_coe, pi.mul_apply, mul_comm],
  refl,
end

end cusp_form
