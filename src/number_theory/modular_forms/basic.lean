/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import geometry.manifold.mfderiv
import analysis.complex.upper_half_plane.functions_bounded_at_infty
import analysis.complex.upper_half_plane.topology
import number_theory.modular_forms.slash_invariant_forms
/-!
# Modular forms

This file defines modular forms and proves some basic properties about them.

We begin by defining modular forms and cusp forms as extension of `slash_invariant_forms` then we
define the space of modular forms, cusp forms and prove that the product of two modular forms is a
modular form.
-/

open complex upper_half_plane

open_locale topology manifold upper_half_plane

noncomputable theory

instance upper_half_plane.charted_space : charted_space â„‚ â„ :=
upper_half_plane.open_embedding_coe.singleton_charted_space

instance upper_half_plane.smooth_manifold_with_corners : smooth_manifold_with_corners ğ“˜(â„‚) â„ :=
upper_half_plane.open_embedding_coe.singleton_smooth_manifold_with_corners ğ“˜(â„‚)

local prefix `â†‘â‚˜`:1024 := @coe _ (matrix (fin 2) (fin 2) _) _

local notation `GL(` n `, ` R `)`âº := matrix.GL_pos (fin n) R

local notation `SL(` n `, ` R `)` := matrix.special_linear_group (fin n) R

section modular_form

open modular_form

variables (F : Type*) (Î“ : subgroup SL(2, â„¤)) (k : â„¤)

local notation f `âˆ£[`:73 k:0, A `]` :72 := slash_action.map â„‚ k A f

set_option old_structure_cmd true

/--These are `slash_invariant_form`'s that are holomophic and bounded at infinity. -/
structure modular_form extends slash_invariant_form Î“ k :=
(holo' : mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (to_fun : â„ â†’ â„‚))
(bdd_at_infty' : âˆ€ (A : SL(2, â„¤)), is_bounded_at_im_infty (to_fun âˆ£[k, A]))

/-- The `slash_invariant_form` associated to a `modular_form`. -/
add_decl_doc modular_form.to_slash_invariant_form

/--These are `slash_invariant_form`s that are holomophic and zero at infinity. -/
structure cusp_form extends slash_invariant_form Î“ k :=
(holo' : mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (to_fun : â„ â†’ â„‚))
(zero_at_infty' : âˆ€ (A : SL(2, â„¤)), is_zero_at_im_infty (to_fun âˆ£[k, A]))

/-- The `slash_invariant_form` associated to a `cusp_form`. -/
add_decl_doc cusp_form.to_slash_invariant_form

/--`modular_form_class F Î“ k` says that `F` is a type of bundled functions that extend
`slash_invariant_forms_class` by requiring that the functions be holomorphic and bounded
at infinity. -/
class modular_form_class extends slash_invariant_form_class F Î“ k :=
(holo: âˆ€ f : F, mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (f : â„ â†’ â„‚))
(bdd_at_infty : âˆ€ (f : F) (A : SL(2, â„¤)), is_bounded_at_im_infty (f âˆ£[k, A]))

/--`cusp_form_class F Î“ k` says that `F` is a type of bundled functions that extend
`slash_invariant_forms_class` by requiring that the functions be holomorphic and zero
at infinity. -/
class cusp_form_class extends slash_invariant_form_class F Î“ k :=
(holo: âˆ€ f : F, mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (f : â„ â†’ â„‚))
(zero_at_infty : âˆ€ (f : F) (A : SL(2, â„¤)), is_zero_at_im_infty (f âˆ£[k, A]))

@[priority 100]
instance modular_form_class.modular_form : modular_form_class (modular_form Î“ k) Î“ k :=
{ coe := modular_form.to_fun,
  coe_injective' := Î» f g h, by cases f; cases g; congr',
  slash_action_eq := modular_form.slash_action_eq',
  holo:= modular_form.holo',
  bdd_at_infty := modular_form.bdd_at_infty' }

@[priority 100]
instance cusp_form_class.cusp_form : cusp_form_class (cusp_form Î“ k) Î“ k :=
{ coe := cusp_form.to_fun,
  coe_injective' := Î» f g h, by cases f; cases g; congr',
  slash_action_eq := cusp_form.slash_action_eq',
  holo:= cusp_form.holo',
  zero_at_infty := cusp_form.zero_at_infty' }

variables {F Î“ k}

@[simp] lemma modular_form_to_fun_eq_coe {f : modular_form Î“ k} : f.to_fun = (f : â„ â†’ â„‚) := rfl
@[simp] lemma cusp_form_to_fun_eq_coe {f : cusp_form Î“ k} : f.to_fun = (f : â„ â†’ â„‚) := rfl

@[ext] theorem modular_form.ext {f g : modular_form Î“ k} (h : âˆ€ x, f x = g x) : f = g :=
fun_like.ext f g h

@[ext] theorem cusp_form.ext {f g : cusp_form Î“ k} (h : âˆ€ x, f x = g x) : f = g :=
fun_like.ext f g h

/-- Copy of a `modular_form` with a new `to_fun` equal to the old one. Useful to fix
definitional equalities. -/
protected def modular_form.copy (f : modular_form Î“ k) (f' : â„ â†’ â„‚) (h : f' = â‡‘f) :
  modular_form Î“ k :=
{ to_fun := f',
  slash_action_eq' := h.symm â–¸ f.slash_action_eq',
  holo' := h.symm â–¸ f.holo',
  bdd_at_infty' := Î» A, h.symm â–¸ f.bdd_at_infty' A }

/-- Copy of a `cusp_form` with a new `to_fun` equal to the old one. Useful to fix
definitional equalities. -/
protected def cusp_form.copy (f : cusp_form Î“ k) (f' : â„ â†’ â„‚) (h : f' = â‡‘f) :
  cusp_form Î“ k :=
{ to_fun := f',
  slash_action_eq' := h.symm â–¸ f.slash_action_eq',
  holo' := h.symm â–¸ f.holo',
  zero_at_infty' := Î» A, h.symm â–¸ f.zero_at_infty' A }

end modular_form

namespace modular_form

open slash_invariant_form

variables {F : Type*} {Î“ : subgroup SL(2, â„¤)} {k : â„¤}

instance has_add : has_add (modular_form Î“ k) :=
âŸ¨ Î» f g,
  { holo' := f.holo'.add g.holo',
    bdd_at_infty' := Î» A, by simpa using (f.bdd_at_infty' A).add (g.bdd_at_infty' A),
    .. (f : slash_invariant_form Î“ k) + g }âŸ©

@[simp] lemma coe_add (f g : modular_form Î“ k) : â‡‘(f + g) = f + g := rfl

@[simp] lemma add_apply (f g : modular_form Î“ k) (z : â„) : (f + g) z = f z + g z := rfl

instance has_zero : has_zero (modular_form Î“ k) :=
âŸ¨ { holo' := (Î» _, mdifferentiable_at_const ğ“˜(â„‚, â„‚) ğ“˜(â„‚, â„‚)),
    bdd_at_infty' := Î» A, by simpa using zero_form_is_bounded_at_im_infty,
    .. (0 : slash_invariant_form Î“ k) } âŸ©

@[simp] lemma coe_zero : â‡‘(0 : modular_form Î“ k) = (0 : â„ â†’ â„‚) := rfl

@[simp] lemma zero_apply (z : â„) : (0 : modular_form Î“ k) z = 0 := rfl

section
variables {Î± : Type*} [has_smul Î± â„‚] [is_scalar_tower Î± â„‚ â„‚]

instance has_smul : has_smul Î± (modular_form Î“ k) :=
âŸ¨ Î» c f,
  { to_fun := c â€¢ f,
    holo' := by simpa using f.holo'.const_smul (c â€¢ (1 : â„‚)),
    bdd_at_infty' := Î» A, by simpa using (f.bdd_at_infty' A).const_smul_left (c â€¢ (1 : â„‚)),
     .. c â€¢ (f : slash_invariant_form Î“ k)}âŸ©

@[simp] lemma coe_smul (f : (modular_form Î“ k)) (n : Î±) : â‡‘(n â€¢ f) = n â€¢ f := rfl
@[simp] lemma smul_apply (f : (modular_form Î“ k)) (n : Î±) (z : â„) :
   (n â€¢ f) z = n â€¢ (f z) := rfl
end

instance has_neg : has_neg (modular_form Î“ k) :=
âŸ¨Î» f,
  { to_fun := -f,
    holo' := f.holo'.neg,
    bdd_at_infty':= Î» A, by simpa using (f.bdd_at_infty' A).neg,
    .. -(f : slash_invariant_form Î“ k) }âŸ©

@[simp] lemma coe_neg (f : modular_form Î“ k) : â‡‘(-f) = -f := rfl

@[simp] lemma neg_apply (f : modular_form Î“ k) (z : â„) : (-f) z = - (f z) := rfl

instance has_sub : has_sub (modular_form Î“ k) :=
âŸ¨ Î» f g, f + -g âŸ©

@[simp] lemma coe_sub (f g : (modular_form Î“ k)) : â‡‘(f - g) = f - g := rfl

@[simp] lemma sub_apply (f g : modular_form Î“ k) (z : â„) : (f - g) z = f z - g z := rfl

instance : add_comm_group (modular_form Î“ k) :=
fun_like.coe_injective.add_comm_group _ rfl coe_add coe_neg coe_sub coe_smul coe_smul

/--Additive coercion from `modular_form` to `â„ â†’ â„‚`. -/
@[simps] def coe_hom : (modular_form Î“ k) â†’+ (â„ â†’ â„‚) :=
{ to_fun := Î» f, f,
  map_zero' := coe_zero,
  map_add' := Î» _ _, rfl }

instance : module â„‚ (modular_form Î“ k) :=
function.injective.module â„‚ coe_hom fun_like.coe_injective (Î» _ _, rfl)

instance : inhabited (modular_form Î“ k) := âŸ¨0âŸ©

/--The modular form of weight `k_1 + k_2` given by the product of two modular forms of weights
`k_1` and `k_2`. -/
def mul {k_1 k_2 : â„¤} {Î“ : subgroup SL(2, â„¤)} (f : (modular_form Î“ k_1))
  (g : (modular_form Î“ k_2)) : (modular_form Î“ (k_1 + k_2)) :=
{ to_fun := f * g,
  slash_action_eq' := Î» A, by simp_rw [mul_slash_subgroup, modular_form_class.slash_action_eq],
  holo' := f.holo'.mul g.holo',
  bdd_at_infty' := Î» A, by simpa using (f.bdd_at_infty' A).mul (g.bdd_at_infty' A) }

@[simp] lemma mul_coe {k_1 k_2 : â„¤} {Î“ : subgroup SL(2, â„¤)} (f : (modular_form Î“ k_1))
  (g : (modular_form Î“ k_2)) : ((f.mul g) : â„ â†’ â„‚) = f * g := rfl

instance : has_one (modular_form Î“ 0) :=
âŸ¨{  holo' := Î» x, mdifferentiable_at_const ğ“˜(â„‚, â„‚) ğ“˜(â„‚, â„‚),
    bdd_at_infty' := Î» A, by simpa using at_im_infty.const_bounded_at_filter (1:â„‚),
      .. (1 : slash_invariant_form Î“ 0) }âŸ©

@[simp] lemma one_coe_eq_one : ((1 : modular_form Î“ 0) : â„ â†’ â„‚) = 1 := rfl

end modular_form

namespace cusp_form
open modular_form

variables {F : Type*} {Î“ : subgroup SL(2, â„¤)} {k : â„¤}

instance has_add : has_add (cusp_form Î“ k) :=
âŸ¨ Î» f g,
  { to_fun := f + g,
    holo' := f.holo'.add g.holo',
    zero_at_infty' := Î» A, by simpa using (f.zero_at_infty' A).add (g.zero_at_infty' A),
    .. (f : slash_invariant_form Î“ k) + g }âŸ©

@[simp] lemma coe_add (f g : cusp_form Î“ k) : â‡‘(f + g) = f + g := rfl

@[simp] lemma add_apply (f g : cusp_form Î“ k) (z : â„) : (f + g) z = f z + g z := rfl

instance has_zero : has_zero (cusp_form Î“ k) :=
âŸ¨ { to_fun := 0,
    holo' := (Î» _, mdifferentiable_at_const ğ“˜(â„‚, â„‚) ğ“˜(â„‚, â„‚)),
    zero_at_infty' := by simpa using filter.zero_zero_at_filter _,
    .. (0 : slash_invariant_form Î“ k) }âŸ©

@[simp] lemma coe_zero : â‡‘(0 : cusp_form Î“ k) = (0 : â„ â†’ â„‚) := rfl

@[simp] lemma zero_apply (z : â„) : (0 : cusp_form Î“ k) z = 0 := rfl

section
variables {Î± : Type*} [has_smul Î± â„‚] [is_scalar_tower Î± â„‚ â„‚]

instance has_smul : has_smul Î± (cusp_form Î“ k) :=
âŸ¨ Î» c f,
  { to_fun := c â€¢ f,
    holo' := by simpa using f.holo'.const_smul (c â€¢ (1 : â„‚)),
    zero_at_infty' := Î» A, by simpa using (f.zero_at_infty' A).smul (c â€¢ (1 : â„‚)),
    .. c â€¢ (f : slash_invariant_form Î“ k) }âŸ©

@[simp] lemma coe_smul (f : (cusp_form Î“ k)) (n : Î±) : â‡‘(n â€¢ f) = n â€¢ f := rfl
@[simp] lemma smul_apply (f : (cusp_form Î“ k)) (n : Î±) {z : â„} :
   (n â€¢ f) z = n â€¢ (f z) := rfl

end

instance has_neg : has_neg (cusp_form Î“ k) :=
âŸ¨Î» f,
  { to_fun := -f,
    holo' := f.holo'.neg,
    zero_at_infty':= Î» A, by simpa using (f.zero_at_infty' A).neg,
    .. -(f : slash_invariant_form Î“ k)} âŸ©

@[simp] lemma coe_neg (f : cusp_form Î“ k) : â‡‘(-f) = -f := rfl
@[simp] lemma neg_apply (f : cusp_form Î“ k) (z : â„) : (-f) z = -(f z) := rfl

instance has_sub : has_sub (cusp_form Î“ k) :=
âŸ¨ Î» f g, f + -g âŸ©

@[simp] lemma coe_sub (f g : cusp_form Î“ k) : â‡‘(f - g) = f - g := rfl
@[simp] lemma sub_apply (f g : cusp_form Î“ k) (z : â„) : (f - g) z = f z - g z := rfl

instance : add_comm_group (cusp_form Î“ k) :=
fun_like.coe_injective.add_comm_group _ rfl coe_add coe_neg coe_sub coe_smul coe_smul

/--Additive coercion from `cusp_form` to `â„ â†’ â„‚`. -/
@[simps] def coe_hom : (cusp_form Î“ k) â†’+ (â„ â†’ â„‚) :=
{ to_fun := Î» f, f,
  map_zero' := cusp_form.coe_zero,
  map_add' := Î» _ _, rfl }

instance : module â„‚ (cusp_form Î“ k) :=
function.injective.module â„‚ coe_hom fun_like.coe_injective (Î» _ _, rfl)

instance : inhabited (cusp_form Î“ k) := âŸ¨0âŸ©

@[priority 99]
instance [cusp_form_class F Î“ k] : modular_form_class F Î“ k :=
{ coe := fun_like.coe,
  coe_injective' := fun_like.coe_injective',
  slash_action_eq := cusp_form_class.slash_action_eq,
  holo:= cusp_form_class.holo,
  bdd_at_infty := Î» _ _, (cusp_form_class.zero_at_infty _ _).bounded_at_filter}

end cusp_form
