/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import number_theory.modular_forms.basic
import analysis.special_functions.gaussian
import analysis.calculus.series
import analysis.complex.locally_uniform_limit

/-! # Jacobi's theta function

This file defines the Jacobi theta function

$$\theta(\tau) = \sum_{n \in \mathbb{Z}} \exp (i \pi n ^ 2 \tau),$$

and proves the modular transformation properties `Î¸ (Ï„ + 2) = Î¸ Ï„` and
`Î¸ (-1 / Ï„) = (-I * Ï„) ^ (1 / 2) * Î¸ Ï„`, using Poisson's summation formula for the latter. We also
show that `Î¸` is differentiable on `â„`, and `Î¸(Ï„) - 1` has exponential decay as `im Ï„ â†’ âˆ`.
-/

open complex real asymptotics

open_locale real big_operators upper_half_plane manifold

/-- Jacobi's theta function `âˆ‘' (n : â„¤), exp (Ï€ * I * n ^ 2 * Ï„)`. -/
noncomputable def jacobi_theta (Ï„ : â„) : â„‚ := âˆ‘' (n : â„¤), cexp (Ï€ * I * n ^ 2 * Ï„)

lemma jacobi_theta_term_bound {z : â„‚} (hz : 0 < z.im) (n : â„¤) :
  â€–cexp (Ï€ * I * n ^ 2 * z)â€– â‰¤ exp (-Ï€ * z.im) ^ n.nat_abs :=
begin
  let y := rexp (-Ï€ * z.im),
  have h : y < 1, from exp_lt_one_iff.mpr (mul_neg_of_neg_of_pos (neg_lt_zero.mpr pi_pos) hz),
  refine (le_of_eq _).trans (_ : y ^ (n ^ 2) â‰¤ _),
  { rw [complex.norm_eq_abs, complex.abs_exp],
    have : (â†‘Ï€ * I * n ^ 2 * z).re = (-Ï€ * z.im) * n ^ 2,
    { rw [(by { push_cast, ring } : â†‘Ï€ * I * n ^ 2 * z = â†‘(Ï€ * n ^ 2) * (z * I)),
        of_real_mul_re, mul_I_re],
      ring },
    obtain âŸ¨m, hmâŸ© := int.eq_coe_of_zero_le (sq_nonneg n),
    rw [this, exp_mul, â†int.cast_pow, rpow_int_cast, hm, zpow_coe_nat] },
  { have : n ^ 2 = â†‘(n.nat_abs ^ 2), by rw [nat.cast_pow, int.nat_abs_sq],
    rw [this, zpow_coe_nat],
    exact pow_le_pow_of_le_one (exp_pos _).le h.le ((sq n.nat_abs).symm â–¸ n.nat_abs.le_mul_self) },
end

lemma jacobi_theta_unif_summable {R : â„} (hR : 0 < R) :
  âˆƒ (bd : â„¤ â†’ â„), (summable bd) âˆ§
  (âˆ€ {Ï„ : â„‚} (hÏ„ : R â‰¤ Ï„.im) (n : â„¤), â€–cexp (Ï€ * I * n ^ 2 * Ï„)â€– â‰¤ bd n) :=
begin
  let y := rexp (-Ï€ * R),
  have h : y < 1, from exp_lt_one_iff.mpr (mul_neg_of_neg_of_pos (neg_lt_zero.mpr pi_pos) hR),
  refine âŸ¨Î» n, y ^ n.nat_abs, summable_int_of_summable_nat _ _, Î» Ï„ hÏ„ n, _âŸ©, swap 3,
  { refine (jacobi_theta_term_bound (hR.trans_le hÏ„) n).trans _,
    refine pow_le_pow_of_le_left (exp_pos _).le (real.exp_le_exp.mpr _) _,
    rwa [mul_le_mul_left_of_neg (neg_lt_zero.mpr pi_pos)] },
  all_goals { simpa only [int.nat_abs_neg, int.nat_abs_of_nat]
    using summable_geometric_of_lt_1 (real.exp_pos _).le h },
end

lemma jacobi_theta_summable {z : â„‚} (hz : 0 < z.im) :
  summable (Î» n : â„¤, cexp (Ï€ * I * n ^ 2 * z)) :=
let âŸ¨bd, h, h'âŸ© := jacobi_theta_unif_summable hz in
  summable_norm_iff.mp (summable_of_nonneg_of_le (Î» n, norm_nonneg _) (h' $ le_refl _) h)

lemma jacobi_theta_two_vadd (Ï„ : â„) : jacobi_theta ((2 : â„) +áµ¥ Ï„) = jacobi_theta Ï„ :=
begin
  refine tsum_congr (Î» n, _),
  rw [upper_half_plane.coe_vadd, of_real_bit0, of_real_one],
  suffices : cexp (â†‘Ï€ * I * â†‘n ^ 2 * 2) = 1, by rw [mul_add, complex.exp_add, this, one_mul],
  rw [(by { push_cast, ring } : â†‘Ï€ * I * â†‘n ^ 2 * 2 = â†‘(n ^ 2) * (2 * Ï€ * I)),
    complex.exp_int_mul, complex.exp_two_pi_mul_I, one_zpow],
end

lemma jacobi_theta_T_sq_smul (Ï„ : â„) : jacobi_theta (modular_group.T ^ 2 â€¢ Ï„) = jacobi_theta Ï„ :=
begin
  suffices : (2 : â„) +áµ¥ Ï„ = modular_group.T ^ (2 : â„¤) â€¢ Ï„, from this â–¸ (jacobi_theta_two_vadd Ï„),
  simp only [â†subtype.coe_inj, upper_half_plane.modular_T_zpow_smul, int.cast_two],
end

lemma jacobi_theta_S_smul (Ï„ : â„) :
  jacobi_theta (modular_group.S â€¢ Ï„) = (-I * Ï„) ^ (1 / 2 : â„‚) * jacobi_theta Ï„ :=
begin
  unfold jacobi_theta,
  rw [upper_half_plane.modular_S_smul, upper_half_plane.coe_mk],
  have ha : 0 < (-I * Ï„).re,
  { rw [neg_mul, neg_re, mul_re, I_re, I_im, zero_mul, one_mul, zero_sub, neg_neg],
    exact Ï„.im_pos },
  have ha' : (-I * Ï„) ^ (1 / 2 : â„‚) â‰  0,
  { rw [ne.def, cpow_eq_zero_iff],
    contrapose! ha,
    rw [ha.1, zero_re] },
  have hÏ„ : (Ï„ : â„‚) â‰  0, from Ï„.ne_zero,
  have := complex.tsum_exp_neg_mul_int_sq ha,
  rw [mul_comm ((1:â„‚) / _) _, mul_one_div, eq_div_iff ha', mul_comm _ (_ ^ _), eq_comm] at this,
  convert this using 3,
  { ext1 n,
    congr' 1,
    field_simp [hÏ„, I_ne_zero],
    ring_nf,
    rw [I_sq, mul_neg, mul_one, neg_mul, neg_neg] },
  { ext1 n,
    congr' 1,
    ring_nf }
end

lemma jacobi_theta_has_sum_nat (Ï„ : â„) :
  has_sum (Î» (n : â„•), cexp (Ï€ * I * (n + 1) ^ 2 * Ï„)) ((jacobi_theta Ï„ - 1) / 2) :=
begin
  have := (jacobi_theta_summable Ï„.im_pos).has_sum.sum_nat_of_sum_int,
  rw â†@has_sum_nat_add_iff' â„‚ _ _ _ _ 1 at this,
  simp_rw [finset.sum_range_one, int.cast_neg, int.cast_coe_nat, nat.cast_zero, neg_zero,
    int.cast_zero, sq (0:â„‚), mul_zero, zero_mul, neg_sq, â†mul_two, complex.exp_zero,
    add_sub_assoc, (by norm_num : (1 : â„‚) - 1 * 2 = -1), â†sub_eq_add_neg,
    nat.cast_add, nat.cast_one] at this,
  convert this.div_const 2,
  simp_rw mul_div_cancel _ two_ne_zero,
end

lemma jacobi_theta_eq_tsum_nat (Ï„ : â„) :
  jacobi_theta Ï„ = 1 + 2 * âˆ‘' (n : â„•), cexp (Ï€ * I * (n + 1) ^ 2 * Ï„) :=
by rw [(jacobi_theta_has_sum_nat Ï„).tsum_eq, mul_div_cancel' _ (two_ne_zero' â„‚), â†add_sub_assoc,
  add_sub_cancel']

/-- An explicit upper bound for `â€–jacobi_theta Ï„ - 1â€–`. -/
lemma jacobi_theta_sub_one_norm_le (Ï„ : â„) :
  â€–jacobi_theta Ï„ - 1â€– â‰¤ 2 / (1 - exp (-Ï€ * Ï„.im)) * exp (-Ï€ * Ï„.im) :=
begin
  suffices : â€–âˆ‘' (n : â„•), cexp (Ï€ * I * (n + 1) ^ 2 * Ï„)â€– â‰¤ exp (-Ï€ * Ï„.im) / (1 - exp (-Ï€ * Ï„.im)),
  { calc â€–jacobi_theta Ï„ - 1â€– = 2 * â€–âˆ‘' (n : â„•), cexp (Ï€ * I * (n + 1) ^ 2 * Ï„)â€– :
      by rw [sub_eq_iff_eq_add'.mpr (jacobi_theta_eq_tsum_nat Ï„), norm_mul, complex.norm_eq_abs,
        complex.abs_two]
    ... â‰¤ 2 * (rexp (-Ï€ * Ï„.im) / (1 - rexp (-Ï€ * Ï„.im))) :
      by rwa [mul_le_mul_left (zero_lt_two' â„)]
    ... = 2 / (1 - rexp (-Ï€ * Ï„.im)) * rexp (-Ï€ * Ï„.im) : by rw [div_mul_comm, mul_comm] },
  have : âˆ€ (n : â„•), â€–cexp (Ï€ * I * (n + 1) ^ 2 * Ï„)â€– â‰¤ exp (-Ï€ * Ï„.im) ^ (n + 1),
  { intro n,
    simpa only [int.cast_add, int.cast_one] using jacobi_theta_term_bound Ï„.im_pos (n + 1) },
  have s : has_sum (Î» n : â„•, rexp (-Ï€ * Ï„.im) ^ (n + 1)) (exp (-Ï€ * Ï„.im) / (1 - exp (-Ï€ * Ï„.im))),
  { simp_rw [pow_succ, div_eq_mul_inv, has_sum_mul_left_iff (real.exp_ne_zero _)],
    exact has_sum_geometric_of_lt_1 (exp_pos (-Ï€ * Ï„.im)).le
      (exp_lt_one_iff.mpr $ (mul_neg_of_neg_of_pos (neg_lt_zero.mpr pi_pos) Ï„.im_pos)) },
  have aux : summable (Î» (n : â„•), â€–cexp (â†‘Ï€ * I * (â†‘n + 1) ^ 2 * â†‘Ï„)â€–),
    from summable_of_nonneg_of_le (Î» n, norm_nonneg _) this s.summable,
  exact (norm_tsum_le_tsum_norm aux).trans
    ((tsum_mono aux s.summable this).trans (le_of_eq s.tsum_eq)),
end

/-- The norm of `jacobi_theta Ï„ - 1` decays exponentially as `im Ï„ â†’ âˆ`. -/
lemma jacobi_theta_sub_one_is_O_im_infty :
  is_O upper_half_plane.at_im_infty (Î» Ï„, jacobi_theta Ï„ - 1) (Î» Ï„, rexp (-Ï€ * Ï„.im)) :=
begin
  simp_rw [is_O, is_O_with, filter.eventually, upper_half_plane.at_im_infty_mem],
  refine âŸ¨2 / (1 - rexp (-Ï€)), 1, (Î» Ï„ hÏ„, (jacobi_theta_sub_one_norm_le Ï„).trans _)âŸ©,
  rw [real.norm_eq_abs, real.abs_exp],
  refine mul_le_mul_of_nonneg_right _ (exp_pos _).le,
  rw [div_le_div_left (zero_lt_two' â„), sub_le_sub_iff_left, exp_le_exp, neg_mul, neg_le_neg_iff],
  { exact le_mul_of_one_le_right pi_pos.le hÏ„ },
  { rw [sub_pos, exp_lt_one_iff, neg_mul, neg_lt_zero], exact mul_pos pi_pos Ï„.im_pos },
  { rw [sub_pos, exp_lt_one_iff, neg_lt_zero], exact pi_pos }
end

-- Formulation of this result is somewhat roundabout, since functions on subtypes don't play well
-- with `differentiable_at`.
lemma jacobi_theta_differentiable_at (Ï„ : â„) :
  differentiable_at â„‚ (Î» z, âˆ‘' (n : â„¤), cexp (Ï€ * I * n ^ 2 * z)) â†‘Ï„ :=
begin
  suffices : âˆ€ (y : â„) (hy : 0 < y),
    differentiable_on â„‚ (Î» z, âˆ‘' (n : â„¤), cexp (Ï€ * I * n ^ 2 * z)) {w : â„‚ | y < im w},
  from let âŸ¨y, hy, hy'âŸ© := exists_between Ï„.im_pos in (this y hy).differentiable_at
    ((complex.continuous_im.is_open_preimage _ is_open_Ioi).mem_nhds (Ï„.coe_im â–¸ hy')),
  intros y hy,
  have h1 : âˆ€ (n : â„¤) (w : â„‚) (hw : y < im w), differentiable_within_at â„‚
    (Î» (v : â„‚), cexp (â†‘Ï€ * I * â†‘n ^ 2 * v)) {z : â„‚ | y < im z} w,
  from Î» n w hw, (differentiable_at_id.const_mul _).cexp.differentiable_within_at,
  have h2 : is_open {w : â„‚ | y < im w}, from continuous_im.is_open_preimage _ is_open_Ioi,
  obtain âŸ¨bd, bd_s, le_bdâŸ© := jacobi_theta_unif_summable hy,
  exact differentiable_on_tsum_of_summable_norm bd_s h1 h2 (Î» i w hw, le_bd (le_of_lt hw) i),
end

lemma jacobi_theta_mdifferentiable : mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) jacobi_theta :=
Î» Ï„, (jacobi_theta_differentiable_at Ï„).mdifferentiable_at.comp Ï„ Ï„.mdifferentiable_coe

lemma jacobi_theta_continuous : continuous jacobi_theta := jacobi_theta_mdifferentiable.continuous
