/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import algebra.module.submodule
import analysis.complex.upper_half_plane
import linear_algebra.general_linear_group
import linear_algebra.special_linear_group
import algebra.direct_sum.ring
import number_theory.modular
import geometry.manifold.mfderiv

/-!
# Modular forms

This file defines modular forms and proves some basic properties about them.

We begin by defining the `slash_k` operator on the space of functions `â„ â†’ â„‚` and from this
define the notion of weakly modular form.#check

We then define `bounded_at_infinity` and `zero_at_infinity`. Finally we construct the vector
space of modular forms and prove that the product of two modular forms is a modular form
(of higher weight).
-/

universes u v

open complex

open_locale topological_space manifold upper_half_plane

noncomputable theory

local notation `â„'`:=(âŸ¨upper_half_plane.upper_half_space ,
 upper_half_plane.upper_half_plane_is_openâŸ©: topological_space.opens â„‚)

local prefix `â†‘â‚˜`:1024 := @coe _ (matrix (fin 2) (fin 2) _) _

local notation `GL(` n `, ` R `)`âº:= matrix.GL_pos (fin n) R

local notation `SL(` n `, ` R `)`:= matrix.special_linear_group (fin n) R

variable (M : GL(2, â„)âº)

def slash_k : â„¤ â†’ GL(2, â„)âº â†’ (â„ â†’ â„‚) â†’ (â„ â†’ â„‚) := Î» k Î³ f,
  (Î» (x : â„), f (Î³ â€¢ x) * (((â†‘â‚˜ Î³).det ) : â„)^(k-1) * (upper_half_plane.denom Î³ x)^(-k))

namespace modular_forms

variables (Î“ : subgroup SL(2,â„¤)) (C : GL(2, â„)âº) (k: â„¤) (f : (â„ â†’ â„‚))

localized "notation  f  ` âˆ£[`:100 k `]`:0 Î³ :100 := slash_k k Î³ f" in modular_form

lemma slash_k_right_action (k : â„¤) (A B : GL(2, â„)âº) (f : â„ â†’ â„‚ ) :
  (f âˆ£[k] A) âˆ£[k] B = f âˆ£[k] (A * B) :=
begin
  ext1,
  simp_rw [slash_k,(upper_half_plane.denom_cocycle A B x)],
  have e3 : (A * B) â€¢ x = A â€¢ B â€¢ x , by {convert (upper_half_plane.mul_smul' A B x),} ,
  rw e3,
  simp only [upper_half_plane.num, upper_half_plane.denom, of_real_mul, subgroup.coe_mul, coe_coe,
  upper_half_plane.coe_smul, units.coe_mul, matrix.mul_eq_mul, matrix.det_mul,
  upper_half_plane.smul_aux, upper_half_plane.smul_aux', subtype.coe_mk] at *,
  field_simp,
  ring_nf,
  have : (((â†‘(â†‘A : GL (fin 2) â„) : (matrix (fin 2) (fin 2) â„)).det : â„‚) *
  ((â†‘(â†‘B  : GL (fin 2) â„) : (matrix (fin 2) (fin 2) â„)).det : â„‚))^(k-1) =
  ((â†‘(â†‘B : GL (fin 2) â„) : (matrix (fin 2) (fin 2) â„)).det : â„‚)^(k-1) *
  ((â†‘(â†‘A : GL (fin 2) â„) : (matrix (fin 2) (fin 2) â„)).det : â„‚)^(k-1) ,
  by {simp_rw [â†mul_zpowâ‚€, mul_comm]},
  simp_rw [this,â† mul_assoc,â†mul_zpowâ‚€],
  end

lemma slash_k_add (k : â„¤) (A : GL(2, â„)âº) (f g : â„ â†’ â„‚) :
  (f + g) âˆ£[k] A = (f âˆ£[k] A) + (g âˆ£[k] A) :=
begin
  simp only [slash_k, pi.add_apply, matrix.general_linear_group.coe_det_apply, subtype.val_eq_coe,
  coe_coe],
  ext1,
  simp only [pi.add_apply],
  ring,
end

lemma slash_k_mul_one (k : â„¤) (f : â„ â†’ â„‚) : (f âˆ£[k] 1) = f :=
begin
 simp_rw slash_k,
 ext1,
 simp,
end

lemma smul_slash_k (k : â„¤) (A : GL(2, â„)âº) (f : â„ â†’ â„‚ ) (c  : â„‚) : (c â€¢ f) âˆ£[k] A = c â€¢ (f âˆ£[k] A):=
begin
  ext1,
  simp_rw slash_k,
  simp only [algebra.id.smul_eq_mul, matrix.general_linear_group.coe_det_apply, pi.smul_apply,
  subtype.val_eq_coe, coe_coe],
  ring,
end

lemma slash_k_mul (k1 k2 : â„¤) (A : GL(2, â„)âº) (f g : â„ â†’ â„‚) :
  (f * g) âˆ£[k1+k2] A = (((â†‘â‚˜ A).det) : â„) â€¢ (f âˆ£[k1] A) * (g âˆ£[k2] A) :=
begin
  ext1,
  simp only [slash_k, matrix.general_linear_group.coe_det_apply, subtype.val_eq_coe, â†mul_assoc],
  have h1 : ((((â†‘â‚˜ A).det) : â„)^(k1 + k2 - 1) : â„‚) =
  (((â†‘â‚˜ A).det) : â„) * (((â†‘â‚˜ A).det) : â„)^(k1 - 1) * (((â†‘â‚˜ A).det) : â„)^(k2 - 1),
  by {simp only [mul_assoc, matrix.general_linear_group.coe_det_apply, subtype.val_eq_coe, coe_coe],
  rw [â†zpow_addâ‚€, â†zpow_one_addâ‚€],
  ring_exp,
  all_goals{norm_cast, apply (matrix.GL_pos.det_ne_zero A),},},
  have h22 : (upper_half_plane.denom A x)^(-(k1+k2))=(upper_half_plane.denom A x)^(-k1) *
  (upper_half_plane.denom A  x)^(-k2),
  by {rw [int.neg_add, zpow_addâ‚€], exact upper_half_plane.denom_ne_zero A x,},
  rw [h1,h22],
  simp only [upper_half_plane.denom, pi.mul_apply, coe_coe, zpow_negâ‚€, algebra.smul_mul_assoc,
  pi.smul_apply, real_smul],
  ring,
end

/--The  space of functions that are modular-/
def weakly_modular_submodule (k : â„¤)  (Î“ : subgroup SL(2,â„¤)): submodule â„‚ (â„  â†’ â„‚) :=
  {carrier := {f : (â„ â†’ â„‚) | âˆ€ (Î³ : Î“),  (f âˆ£[k] (Î³ : GL(2, â„)âº)) = f },
  zero_mem' := by {simp only [slash_k, set.mem_set_of_eq, pi.zero_apply, zero_mul, forall_const],
  refl,},
  add_mem' := by {intros f g hf hg Î³,
  rw [slash_k_add k Î³ f g, hf Î³, hg Î³], },
  smul_mem' := by {intros c f hf Î³,
  have : (c â€¢ f) âˆ£[k] Î³ = c â€¢ (f  âˆ£[k] Î³ ), by {apply smul_slash_k},
  rw (hf Î³) at this,
  apply this,}}

lemma wmodular_mem (k : â„¤) (Î“ : subgroup SL(2,â„¤)) (f : â„ â†’ â„‚) :
  f âˆˆ (weakly_modular_submodule k Î“) â†”  âˆ€ (Î³ : Î“), (f âˆ£[k] (Î³ : GL(2, â„)âº)) = f := iff.rfl

lemma slash_k_mul_subgroup (k1 k2 : â„¤) (Î“ : subgroup SL(2,â„¤)) (A : Î“) (f g : â„ â†’ â„‚) :
  (f * g) âˆ£[k1+k2] A = (f âˆ£[k1] A) * (g âˆ£[k2] A) :=
begin
  have : (((â†‘â‚˜(A : GL(2,â„)âº)).det): â„) = 1,
  by {simp only [coe_coe,matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix,
  matrix.special_linear_group.det_coe],},
  simp_rw [slash_k_mul, this , one_smul],
end

/--A function `f : â„ â†’ â„‚` is modular, of level `Î“` and weight `k âˆˆ â„¤`, if for every matrix in
 `Î³ âˆˆ  Î“` we have `f(Î³  â€¢ z)= (c*z+d)^k f(z)` where `Î³= ![![a, b], ![c, d]]`,
 and it acts on `â„` via Moebius trainsformations. -/
@[simp]
lemma wmodular_mem' (k : â„¤) (Î“ : subgroup SL(2,â„¤)) (f : â„ â†’ â„‚) :
  f âˆˆ (weakly_modular_submodule k Î“) â†”  âˆ€ Î³ : Î“, âˆ€ z : â„,
  f (Î³ â€¢ z) = ((â†‘â‚˜Î³ 1 0 : â„) * z +(â†‘â‚˜Î³ 1 1 : â„))^k * f z :=
begin
  simp only [wmodular_mem],
  split,
  {intros h1 Î³ z,
  have h3 : (f âˆ£[k] Î³) z = f z , by {simp_rw (h1 Î³)},
  rw [â†h3, slash_k, mul_comm],
  have h55 := zpow_neg_mul_zpow_self  k (upper_half_plane.denom_ne_zero (Î³ : GL(2, â„)âº) z),
  simp only [upper_half_plane.denom, upper_half_plane.subgroup_to_sl_moeb, upper_half_plane.sl_moeb,
  coe_coe, matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix,
  matrix.special_linear_group.coe_matrix_coe, int.coe_cast_ring_hom, matrix.map_apply,
  of_real_int_cast] at *,
  rw [mul_assoc, h55, â†int.coe_cast_ring_hom, â†matrix.special_linear_group.coe_matrix_coe,
  matrix.special_linear_group.det_coe ((Î³ : SL(2, â„¤) ) : SL(2, â„))],
  simp only [of_real_one, one_zpowâ‚€, mul_one],},
  {intros hf Î³,
  simp_rw slash_k,
  ext1,
  rw [â†upper_half_plane.subgroup_moeb, (hf Î³ x), mul_comm],
  have h55 := zpow_neg_mul_zpow_self k (upper_half_plane.denom_ne_zero (Î³ : GL(2, â„)âº) x),
  simp_rw upper_half_plane.denom at *,
  simp only [matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix, coe_coe,
  matrix.special_linear_group.coe_matrix_coe, int.coe_cast_ring_hom, matrix.map_apply,
  of_real_int_cast] at h55,
  simp only [coe_coe, matrix.special_linear_group.coe_GL_pos_coe_GL_coe_matrix,
  matrix.map_apply, of_real_int_cast],
  rw (matrix.special_linear_group.det_coe ((Î³ : SL(2, â„¤) ) : SL(2, â„))),
  simp only [matrix.special_linear_group.coe_matrix_coe, int.coe_cast_ring_hom, matrix.map_apply,
  of_real_int_cast, of_real_one, one_zpowâ‚€, mul_one],
  simp_rw [â† mul_assoc,h55],
  simp},
end

lemma mul_modular  (k_1 k_2 : â„¤) (Î“ : subgroup SL(2,â„¤)) (f g : â„ â†’ â„‚)
  (hf : f âˆˆ  weakly_modular_submodule k_1 Î“)  (hg : g âˆˆ weakly_modular_submodule k_2 Î“) :
  f * g  âˆˆ  weakly_modular_submodule (k_1 + k_2) Î“ :=
begin
  simp only [wmodular_mem', pi.mul_apply, coe_coe] at *,
  intros Î³ z,
  rw [(hf Î³ z),(hg Î³ z)],
  have pown := zpow_addâ‚€ (upper_half_plane.denom_ne_zero (Î³ : GL(2, â„)âº) z) k_1 k_2,
  simp only [upper_half_plane.denom, coe_fn_coe_base, ne.def,
  matrix.general_linear_group.coe_fn_eq_coe, coe_coe] at pown,
  rw pown,
  ring,
end

/--A function ` f : â„ â†’ â„‚` is bounded at infinity if there exist real numbers `M,A` such that
for all `z âˆˆ â„` with `im z â‰¥ A` we have `abs(f (z)) â‰¤ M`,
 i.e. the function is bounded as you approach `iâˆ`.  -/
def is_bound_at_infinity := { f : â„ â†’ â„‚ | âˆƒ (M A : â„), âˆ€ z : â„, A â‰¤ im z â†’ abs (f z) â‰¤ M }

/--A function ` f : â„ â†’ â„‚` is zero at infinity if for any `Îµ > 0` there exist a real
number `A` such that for all `z âˆˆ â„` with `im z â‰¥ A` we have `abs(f (z)) â‰¤ Îµ`,
 i.e. the function tends to zero as you approach `iâˆ`.  -/
def is_zero_at_infinity :=
  { f : â„ â†’ â„‚ | âˆ€ Îµ : â„, 0 < Îµ  â†’ âˆƒ A : â„, âˆ€ z : â„, A â‰¤ im z  â†’ abs (f z) â‰¤ Îµ }

@[simp]lemma bound_mem (f : â„ â†’ â„‚):
  (f âˆˆ  is_bound_at_infinity ) â†” âˆƒ (M A : â„), âˆ€ z : â„, A â‰¤ im z  â†’ abs (f z) â‰¤ M := iff.rfl

@[simp]lemma zero_at_inf_mem (f : â„ â†’ â„‚) :
  (f âˆˆ  is_zero_at_infinity  ) â†” âˆ€ Îµ : â„, 0 < Îµ  â†’ âˆƒ A : â„,
  âˆ€ z : â„, A â‰¤ im z  â†’ abs (f z) â‰¤ Îµ := iff.rfl

lemma zero_form_is_zero_at_inf : (0 : (â„ â†’ â„‚)) âˆˆ is_zero_at_infinity:=
begin
  refine Î» Îµ he, âŸ¨0, by {simp only [complex.abs_zero, pi.zero_apply, he.le, implies_true_iff]}âŸ©,
end

lemma is_zero_at_inf_is_bound (f : â„ â†’ â„‚) (hf : f âˆˆ is_zero_at_infinity) :
  (f âˆˆ is_bound_at_infinity):=
begin
  refine âŸ¨1, by {apply hf, linarith}âŸ©,
end

lemma zero_form_is_bound : (0 : (â„ â†’ â„‚)) âˆˆ  is_bound_at_infinity:=
begin
 apply is_zero_at_inf_is_bound _ (zero_form_is_zero_at_inf),
end

/--This is the submodule of functions that are bounded at infinity-/
def bounded_at_infty_submodule: submodule (â„‚) (â„  â†’ â„‚):=
  {carrier :={ f : â„ â†’ â„‚ | âˆƒ (M A : â„), âˆ€ z : â„, A â‰¤ im z â†’ abs (f z) â‰¤ M },
  zero_mem' :=by {simp only [pi.zero_apply, complex.abs_zero, subtype.forall,
  upper_half_plane.coe_im, set.mem_set_of_eq],
  refine âŸ¨1 ,0 , (Î» _ _, zero_le_one)âŸ©,},
  add_mem' := by  {intros f g hf hg,
  obtain âŸ¨Mf, Af, hAMfâŸ© := hf,
  obtain âŸ¨Mg, Ag, hAMgâŸ© := hg,
  refine âŸ¨(Mf + Mg),(max Af Ag) , (Î» z hz , le_trans (complex.abs_add _ _) (add_le_add ((hAMf z)
  (le_trans (le_max_left _ _) hz)) ((hAMg z) (le_trans (le_max_right _ _) hz))))âŸ©},
  smul_mem' := by {intros c f hyp,
  obtain âŸ¨M, A,hAMâŸ© := hyp,
  refine âŸ¨(complex.abs c â€¢ M), A, Î» z hz, by { convert (mul_le_mul_of_nonneg_left (hAM z hz)
  (complex.abs_nonneg c)), apply complex.abs_mul}âŸ©,},}


 /--The submodule of functions that are zero at infinity-/
def zero_at_infty_submodule : submodule (â„‚) (â„  â†’ â„‚) :=
  {carrier := { f : â„ â†’ â„‚ | âˆ€ Îµ : â„, 0 < Îµ  â†’ âˆƒ A : â„, âˆ€ z : â„, A â‰¤ im z  â†’ abs (f z) â‰¤ Îµ },
  zero_mem' := by {intros Îµ he,
  refine âŸ¨(-1: â„ ), Î» (_ _ ), by{ rw [pi.zero_apply, complex.abs_zero], apply he.le,}âŸ©},
  add_mem' := by  {intros f g hf hg Îµ hÎµ,
  cases hf (Îµ/2) (half_pos hÎµ) with Af hAf,
  cases hg (Îµ/2) (half_pos hÎµ) with Ag hAg,
  existsi (max Af Ag),
  rw show Îµ = Îµ / 2 + Îµ / 2, by simp only [add_halves'],
  refine Î» z, (Î» hz, le_trans (complex.abs_add _ _)  (add_le_add ((hAf z)
  (le_trans (le_max_left _ _) hz)) ((hAg z) (le_trans (le_max_right _ _) hz))))},
  smul_mem' := by {intros c f hyp Îµ hÎµ,
  by_cases hc : (c = 0),
  {existsi (0 : â„ ), intros, simp only [hc, pi.zero_apply, complex.abs_zero, zero_smul],
  exact le_of_lt hÎµ},
  have hcc : 0 <  (Îµ / complex.abs c),
  by { rw div_eq_mul_inv, apply mul_pos hÎµ (inv_pos.2 (complex.abs_pos.2 hc)),},
  {cases hyp (Îµ / complex.abs c) (hcc) with A hA,
  refine âŸ¨A, Î»  z hz, by {simp only [complex.abs_mul,algebra.id.smul_eq_mul, pi.smul_apply],
  rw show Îµ = complex.abs c * (Îµ / complex.abs c),
  by {rw [mul_comm], refine (div_mul_cancel _ (complex.abs_ne_zero.2 hc)).symm},
  apply mul_le_mul_of_nonneg_left (hA z hz) (complex.abs_nonneg c),}âŸ© },},}

/-- The product of two bounded-at-infinity functions is bounded-at-infinty --/
lemma prod_of_bound_is_bound (f g : â„ â†’ â„‚) (hf : f âˆˆ is_bound_at_infinity)
  (hg : g âˆˆ is_bound_at_infinity): ((f * g) âˆˆ is_bound_at_infinity) :=
begin
  simp only [is_bound_at_infinity, upper_half_plane.coe_im, set.mem_set_of_eq, pi.mul_apply,
  complex.abs_mul] at *,
  obtain âŸ¨Mf, Af, hMAfâŸ© := hf,
  obtain âŸ¨Mg, Ag, hMAgâŸ© := hg,
  refine âŸ¨Mf * Mg, max Af Ag, Î» z hAfg, mul_le_mul ( hMAf z (max_le_iff.1 hAfg).1)
  (hMAg z (max_le_iff.1 hAfg).2) (complex.abs_nonneg _) (le_trans (complex.abs_nonneg (f(z)))
  (hMAf z (max_le_iff.1 hAfg).1 ))âŸ©,
end

/--The extension of a function from `â„` to `â„'`-/
def hol_extn (f : â„ â†’ â„‚) : â„' â†’ â„‚ := Î» (z : â„'), (f (z : â„))

instance : has_coe (â„ â†’ â„‚) (â„' â†’ â„‚) :=
âŸ¨Î» f, hol_extn f âŸ©

/-- A function `f : â„ â†’ â„‚` is a modular form of level `Î“` and weight `k âˆˆ â„¤` if it is holomorphic,
 Petersson and bounded at infinity -/

structure is_modular_form_of_lvl_and_weight (Î“ : subgroup SL(2,â„¤)) (k : â„¤) (f : â„ â†’ â„‚) : Prop :=
  (hol      : mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (â†‘f : â„' â†’ â„‚))
  (transf   : f âˆˆ weakly_modular_submodule k Î“ )
  (infinity : âˆ€ (A : (âŠ¤ : subgroup SL(2,â„¤))), (f âˆ£[k] A) âˆˆ is_bound_at_infinity )

lemma mk (Î“ : subgroup SL(2,â„¤)) (k : â„¤) (f : â„ â†’ â„‚)
  (h : mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (â†‘f : â„' â†’ â„‚) )
  (h2: f âˆˆ weakly_modular_submodule k Î“ )
  (h3 : âˆ€ (A : (âŠ¤ : subgroup SL(2,â„¤))), (f âˆ£[k] A) âˆˆ is_bound_at_infinity ) :
  is_modular_form_of_lvl_and_weight Î“ k f :=
  {hol := h,
  transf := h2,
  infinity := h3,}

/-- A function `f : â„ â†’ â„‚` is a cusp form of level one and weight `k âˆˆ â„¤` if it is holomorphic,
 Petersson and zero at infinity -/
structure is_cusp_form_of_lvl_and_weight (Î“ : subgroup SL(2,â„¤)) (k : â„¤) (f : â„ â†’ â„‚) : Prop :=
  (hol      : mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (â†‘f : â„' â†’ â„‚))
  (transf   : f âˆˆ weakly_modular_submodule k Î“)
  (infinity : âˆ€ (A : (âŠ¤ : subgroup SL(2,â„¤))), (f âˆ£[k] A) âˆˆ is_zero_at_infinity )

lemma is_cuspform_mk (Î“ : subgroup SL(2,â„¤)) (k : â„¤) (f : â„ â†’ â„‚)
  (h : mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (â†‘f : â„' â†’ â„‚) )
  (h2 : f âˆˆ weakly_modular_submodule k Î“)
  (h3 :  âˆ€ (A : (âŠ¤ : subgroup SL(2,â„¤))), (f âˆ£[k] A) âˆˆ is_zero_at_infinity ) :
  is_cusp_form_of_lvl_and_weight Î“ k f :=
  {hol := h,
  transf := h2,
  infinity := h3}

/-- The zero modular form is a cusp form-/
lemma zero_cusp_form :  (is_cusp_form_of_lvl_and_weight Î“ k) (0 : (â„ â†’ â„‚)) :=
  { hol := by {apply mdifferentiable_zero,},
  transf := (weakly_modular_submodule k Î“).zero_mem',
  infinity := by {intros A Îµ he,
  refine âŸ¨(-1: â„ ), Î» x  h1,
  by {simp only [slash_k, complex.abs_zero, zero_mul, pi.zero_apply, complex.abs_mul],
  linarith}âŸ©}}

lemma is_modular_form_of_lvl_and_weight_of_is_cusp_form_of_lvl_and_weight (f : â„ â†’ â„‚)
  (h : is_cusp_form_of_lvl_and_weight Î“ k f) : is_modular_form_of_lvl_and_weight Î“ k f :=
  {hol := h.1,
  transf := h.2,
  infinity := by {intro A, have h3:=  h.3 A, apply  is_zero_at_inf_is_bound _ h3,}}

 /-- The zero modular form is a modular form-/
lemma zero_mod_form :  (is_modular_form_of_lvl_and_weight Î“ (k : â„¤) ) (0 : (â„ â†’ â„‚)):=
begin
  apply_rules [is_modular_form_of_lvl_and_weight_of_is_cusp_form_of_lvl_and_weight, zero_cusp_form],
end

/-- This is the space of modular forms of level `Î“` and weight `k`-/
def space_of_mod_forms_of_weight_and_level (Î“ : subgroup SL(2,â„¤)) (k : â„¤) : submodule â„‚ (â„ â†’ â„‚) :=
  {carrier:={ f : â„ â†’ â„‚ | is_modular_form_of_lvl_and_weight Î“ k f},
  zero_mem':=by {simp only [set.mem_set_of_eq], apply zero_mod_form, },
  add_mem' :=by {simp only [set.mem_set_of_eq], intros a b ha hb,
  refine âŸ¨mdifferentiable_add _ _ ha.hol hb.hol,
  (weakly_modular_submodule  k Î“).add_mem' ha.transf hb.transf, by{intro A,
  rw slash_k_add, apply (bounded_at_infty_submodule.add_mem' (ha.infinity A) (hb.infinity A))}âŸ©, },
  smul_mem' := by {intros c f hf,
  refine âŸ¨mdifferentiable_smul _ _ hf.hol, (weakly_modular_submodule  k Î“).smul_mem' _ hf.transf,
  by {intro A, rw smul_slash_k, apply (bounded_at_infty_submodule.smul_mem' c (hf.infinity A))}âŸ©,},}

localized "notation `M(`k`, `Î“`)`:= space_of_mod_forms_of_weight_and_level Î“ k" in modular_forms

/-- This is the space of cuspforms of level `Î“` and weigth `k`-/
def space_of_cusp_forms_of_weight_and_level (Î“ : subgroup SL(2,â„¤)) (k : â„¤): submodule â„‚ (â„ â†’ â„‚) :=
  {carrier := { f : â„ â†’ â„‚ | is_cusp_form_of_lvl_and_weight Î“ k f},
  zero_mem' := by {simp only [set.mem_set_of_eq], apply zero_cusp_form, },
  add_mem' := by {simp only [set.mem_set_of_eq], intros a b ha hb,
  refine âŸ¨mdifferentiable_add _ _ ha.hol hb.hol,
  (weakly_modular_submodule  k Î“).add_mem' ha.transf hb.transf, by{intro A,
  rw slash_k_add, apply (zero_at_infty_submodule.add_mem' (ha.infinity A) (hb.infinity A))}âŸ©, },
  smul_mem' := by {intros c f hf,
  refine âŸ¨mdifferentiable_smul _ _ hf.hol, (weakly_modular_submodule  k Î“).smul_mem' _ hf.transf,
  by {intro A, rw smul_slash_k, apply (zero_at_infty_submodule.smul_mem' c (hf.infinity A))}âŸ©,},}

localized "notation `S(`k`, `Î“`)`:= space_of_cusp_forms_of_weight_and_level Î“ k" in modular_forms

lemma mul_modform (k_1 k_2 : â„¤) (Î“ : subgroup SL(2,â„¤)) (f g : â„ â†’ â„‚)
  (hf : f âˆˆ M(k_1, Î“)) (hg : g âˆˆ M(k_2, Î“)) : f * g  âˆˆ  M(k_1+k_2, Î“) :=
begin
  refine âŸ¨mdifferentiable_mul _ _ hf.1 hg.1, mul_modular  _ _ _ _ _ hf.2 hg.2 ,
  by {intro A, rw slash_k_mul_subgroup k_1 k_2 âŠ¤ A f g,
  apply prod_of_bound_is_bound _ _ (hf.3 A) (hg.3 A) }âŸ©,
end

end modular_forms
