/-
Copyright (c) 2021 Ashvni Narayanan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ashvni Narayanan, Anne Baanen
-/

import ring_theory.dedekind_domain.integral_closure
import algebra.char_p.algebra
import analysis.complex.polynomial

/-!
# Number fields
This file defines a number field, the ring of integers corresponding to it and includes some
basic facts about the embeddings into `â„‚` of a number field.

## Main definitions
 - `number_field` defines a number field as a field which has characteristic zero and is finite
    dimensional over â„š.
 - `ring_of_integers` defines the ring of integers (or number ring) corresponding to a number field
    as the integral closure of â„¤ in the number field.

## Main Results
 - `lift`: for `L/K` an extension of number fields, any embedding of a number field `K`
    can be lifted to an embedding of `L`.
 - `eq_roots`: let `x âˆˆ K` with `K` number field, then the images of `x` by the embeddings
    of `K` are exactly the roots in `â„‚` of the minimal polynomial of `x` over `â„š`.

## Implementation notes
The definitions that involve a field of fractions choose a canonical field of fractions,
but are independent of that choice.

## References
* [D. Marcus, *Number Fields*][marcus1977number]
* [J.W.S. Cassels, A. FrÃ¶lich, *Algebraic Number Theory*][cassels1967algebraic]
* [P. Samuel, *Algebraic Theory of Numbers*][samuel1970algebraic]

## Tags
number field, ring of integers
-/

/-- A number field is a field which has characteristic zero and is finite
dimensional over â„š. -/
class number_field (K : Type*) [field K] : Prop :=
[to_char_zero : char_zero K]
[to_finite_dimensional : finite_dimensional â„š K]

open function
open_locale classical big_operators

/-- `â„¤` with its usual ring structure is not a field. -/
lemma int.not_is_field : Â¬ is_field â„¤ :=
Î» h, int.not_even_one $ (h.mul_inv_cancel two_ne_zero).imp $ Î» a, (by rw â† two_mul; exact eq.symm)

namespace number_field

variables (K L : Type*) [field K] [field L] [nf : number_field K]

include nf

-- See note [lower instance priority]
attribute [priority 100, instance] number_field.to_char_zero number_field.to_finite_dimensional

protected lemma is_algebraic : algebra.is_algebraic â„š K := algebra.is_algebraic_of_finite _ _

omit nf

/-- The ring of integers (or number ring) corresponding to a number field
is the integral closure of â„¤ in the number field. -/
def ring_of_integers := integral_closure â„¤ K

localized "notation `ğ“` := number_field.ring_of_integers" in number_field

lemma mem_ring_of_integers (x : K) : x âˆˆ ğ“ K â†” is_integral â„¤ x := iff.rfl

/-- Given an algebra between two fields, create an algebra between their two rings of integers.

For now, this is not an instance by default as it creates an equal-but-not-defeq diamond with
`algebra.id` when `K = L`. This is caused by `x = âŸ¨x, x.propâŸ©` not being defeq on subtypes. This
will likely change in Lean 4. -/
def ring_of_integers_algebra [algebra K L] : algebra (ğ“ K) (ğ“ L) := ring_hom.to_algebra
{ to_fun := Î» k, âŸ¨algebra_map K L k, is_integral.algebra_map k.2âŸ©,
  map_zero' := subtype.ext $ by simp only [subtype.coe_mk, subalgebra.coe_zero, map_zero],
  map_one'  := subtype.ext $ by simp only [subtype.coe_mk, subalgebra.coe_one, map_one],
  map_add' := Î» x y, subtype.ext $ by simp only [map_add, subalgebra.coe_add, subtype.coe_mk],
  map_mul' := Î» x y, subtype.ext $ by simp only [subalgebra.coe_mul, map_mul, subtype.coe_mk] }

namespace ring_of_integers

variables {K}

instance [number_field K] : is_fraction_ring (ğ“ K) K :=
integral_closure.is_fraction_ring_of_finite_extension â„š _

instance : is_integral_closure (ğ“ K) â„¤ K :=
integral_closure.is_integral_closure _ _

instance [number_field K] : is_integrally_closed (ğ“ K) :=
integral_closure.is_integrally_closed_of_finite_extension â„š

lemma is_integral_coe (x : ğ“ K) : is_integral â„¤ (x : K) :=
x.2

/-- The ring of integers of `K` are equivalent to any integral closure of `â„¤` in `K` -/
protected noncomputable def equiv (R : Type*) [comm_ring R] [algebra R K]
  [is_integral_closure R â„¤ K] : ğ“ K â‰ƒ+* R :=
(is_integral_closure.equiv â„¤ R K _).symm.to_ring_equiv

variables (K)

instance [number_field K] : char_zero (ğ“ K) := char_zero.of_module _ K

/-- The ring of integers of a number field is not a field. -/
lemma not_is_field [number_field K] : Â¬ is_field (ğ“ K) :=
begin
  have h_inj : function.injective â‡‘(algebra_map â„¤ (ğ“ K)),
  { exact ring_hom.injective_int (algebra_map â„¤ (ğ“ K)) },
  intro hf,
  exact int.not_is_field
    (((is_integral_closure.is_integral_algebra â„¤ K).is_field_iff_is_field h_inj).mpr hf)
end

instance [number_field K] : is_dedekind_domain (ğ“ K) :=
is_integral_closure.is_dedekind_domain â„¤ â„š K _

end ring_of_integers

end number_field

namespace rat

open number_field

local attribute [instance] subsingleton_rat_module

instance rat.number_field : number_field â„š :=
{ to_char_zero := infer_instance,
  to_finite_dimensional :=
    -- The vector space structure of `â„š` over itself can arise in multiple ways:
    -- all fields are vector spaces over themselves (used in `rat.finite_dimensional`)
    -- all char 0 fields have a canonical embedding of `â„š` (used in `number_field`).
    -- Show that these coincide:
    by convert (infer_instance : finite_dimensional â„š â„š), }

/-- The ring of integers of `â„š` as a number field is just `â„¤`. -/
noncomputable def ring_of_integers_equiv : ring_of_integers â„š â‰ƒ+* â„¤ :=
ring_of_integers.equiv â„¤

end rat

namespace adjoin_root

section

open_locale polynomial

local attribute [-instance] algebra_rat

/-- The quotient of `â„š[X]` by the ideal generated by an irreducible polynomial of `â„š[X]`
is a number field. -/
instance {f : â„š[X]} [hf : irreducible f] : number_field (adjoin_root f) :=
{ to_char_zero := char_zero_of_injective_algebra_map (algebra_map â„š _).injective,
  to_finite_dimensional := begin
   let := (adjoin_root.power_basis (irreducible.ne_zero hf : f â‰  0)),
   convert power_basis.finite_dimensional this,
   haveI : subsingleton (algebra â„š (adjoin_root f)) := algebra_rat_subsingleton,
   exact subsingleton.elim _ _,
  end }

end

end adjoin_root

namespace number_field.embeddings

open set finite_dimensional complex
open_locale complex_conjugate

variables {K L : Type*} [field K] [field L]
section number_field
variables [number_field K] [number_field L]  (x : K)

/-- The equivalence between algebra maps from a number field to `â„‚` and plain
ring morphisms between them. -/
noncomputable def equiv_alg : (K â†’â‚[â„š] â„‚) â‰ƒ (K â†’+* â„‚) :=
{ to_fun := coe,
  inv_fun := Î» f : K â†’+* â„‚, alg_hom.mk' f (Î» (c : â„š) x, map_rat_smul f _ _),
  left_inv  := Î» x, alg_hom.ext  $ by simp only [forall_const, alg_hom.coe_to_ring_hom,
                                                 eq_self_iff_true, alg_hom.coe_mk'],
  right_inv := Î» x, ring_hom.ext $ by simp only [forall_const, alg_hom.coe_to_ring_hom,
                                                 eq_self_iff_true, alg_hom.coe_mk'] }

/-- There are finitely many embeddings of a number field. -/
noncomputable instance : fintype (K â†’+* â„‚) := fintype.of_equiv (K â†’â‚[â„š] â„‚) equiv_alg

/-- The number of embeddings of a number field is its finrank. -/
lemma card_embeddings : fintype.card (K â†’+* â„‚) = finrank â„š K :=
by rw [fintype.of_equiv_card equiv_alg, alg_hom.card]

open polynomial

/-- Any embeddings of a number field `K` can be extended to an embedding of an extension `L`. -/
lemma lift {L : Type*} [field L] [number_field L] [algebra K L] (Ï† : K â†’+* â„‚) :
  âˆƒ Ïˆ : L â†’+* â„‚, Ï† = Ïˆ.comp (algebra_map K L) :=
begin
  letI : algebra K â„‚, from ring_hom.to_algebra Ï†,
  letI : is_alg_closed â„‚, from complex.is_alg_closed,
  have hS : algebra.is_algebraic K L,
  { show âˆ€ (a : L), is_algebraic K a,
    intro a,
    obtain âŸ¨p, hpâŸ© := (number_field.is_algebraic L) a,
    haveI : algebra â„š K, { exact algebra_rat },
    use (map (algebra_map â„š K) p),
    split,
    simp only [ne.def, map_eq_zero, not_false_iff, hp.left],
    simp only [aeval_map, map_zero, hp.right], },
  let Ïˆâ‚€ : L â†’â‚[K] â„‚ := is_alg_closed.lift hS,
  let Ïˆ := Ïˆâ‚€.to_ring_hom,
  use Ïˆ,
  refine fun_like.ext Ï† (Ïˆ.comp (algebra_map K L)) _,
  intro x,
  show Ï† x = Ïˆâ‚€ ((algebra_map K L) x),
  rw alg_hom.commutes Ïˆâ‚€ x,
  exact rfl,
end

/-- For `x âˆˆ K`, with `K` a number field, the images of `x` by the embeddings of `K` are exactly
the roots of the minimal polynomial of `x` over `â„š` -/
lemma eq_roots : range (Î» Ï† : K â†’+* â„‚, Ï† x) = (minpoly â„š x).root_set â„‚ :=
begin
  have hx : is_integral â„š x, { exact is_separable.is_integral â„š x },
  ext a,
  split,
  { rintro âŸ¨Ï†, hÏ†âŸ©,
    rw [mem_root_set_iff, â†hÏ†],
    let Ïˆ := ring_hom.to_rat_alg_hom Ï†,
    show (aeval (Ïˆ x)) (minpoly â„š x) = 0,
    { rw aeval_alg_hom_apply Ïˆ x (minpoly â„š x),
      simp only [minpoly.aeval, map_zero] },
    exact minpoly.ne_zero hx, },
  { intro ha,
    let Qx := adjoin_root (minpoly â„š x),
    haveI : irreducible (minpoly â„š x), { exact minpoly.irreducible hx },
    haveI : number_field Qx := by apply_instance,
    have hK : (aeval x) (minpoly â„š x) = 0, { exact minpoly.aeval _ _, },
    have hC : (aeval a) (minpoly â„š x) = 0,
    { rw [aeval_def, â†eval_map, â†mem_root_set_iff'],
      exact ha,
      suffices : (minpoly â„š x) â‰  0,
      { contrapose! this,
        simp only [polynomial.ext_iff, coeff_map, coeff_zero] at this âŠ¢,
        suffices inj : function.injective (algebra_map â„š â„‚),
        { exact Î» n : â„•, inj (by rw [(this n), (algebra_map â„š â„‚).map_zero]),},
        exact (algebra_map â„š â„‚).injective, },
      exact minpoly.ne_zero hx, },
    let Ïˆ : Qx â†’+* â„‚ := adjoin_root.lift (algebra_map â„š â„‚) a hC,
    letI : algebra Qx K,
    { exact ring_hom.to_algebra (adjoin_root.lift (algebra_map â„š K) x hK), },
    obtain âŸ¨Ï†, hÏ†âŸ© := lift Ïˆ,
    use Ï†,
    rw (_ : x = (algebra_map Qx K) (adjoin_root.root (minpoly â„š x))),
    rw (_ : a = Ïˆ (adjoin_root.root (minpoly â„š x))),
    simp only [congr_fun, hÏ†, ring_hom.coe_comp],
    exact (adjoin_root.lift_root hC).symm,
    exact (adjoin_root.lift_root hK).symm,
    apply_instance, },
end

end number_field

variables (Ï† : K â†’* â„‚)

/-- An embedding is real if its fixed by complex conjugation. -/
def is_real (Ï† : K â†’+* â„‚) : Prop := conj âˆ˜ Ï† = Ï†

/-- An embedding is real if its not fixed by complex conjugation. -/
def is_complex (Ï† : K â†’+* â„‚) : Prop := conj âˆ˜ Ï† â‰  Ï†

/-- Two embeddings are conjuate if `conj` takes one to the other. -/
def are_conj (Ï† Î¸ : K â†’+* â„‚) : Prop := conj âˆ˜ Ï† = Î¸

/-- An element of a number field is real if its image under any embedding is fixed by conj. -/
def element_is_real (x : K) : Prop := âˆ€ Ï† : K â†’+* â„‚, conj (Ï† x) = Ï† x

local notation `r1` := fintype.card { Ï†  : K â†’+* â„‚ // is_real Ï† }

local notation `c2` := fintype.card { Ï†  : K â†’+* â„‚ // is_complex Ï† }

lemma not_real_eq_complex (Ï† : K â†’+* â„‚) : is_real Ï† â†” Â¬ is_complex Ï† :=
begin
  rw [is_real, is_complex],
  simp only [not_not],
end

lemma real_eq_rank_sub_complex [number_field K] :
  r1 = finrank â„š K  - c2 :=
begin
  rw â† card_embeddings,
  simp_rw not_real_eq_complex,
  exact fintype.card_subtype_compl _,
end

lemma elem_is_real_is_real (x : K) (h : element_is_real x) :
   âˆ€  Ï† : K â†’+* â„‚, âˆƒ (r : â„), Ï† x = (r : â„‚) :=
begin
  intro Ï†,
  simp_rw [element_is_real] at h,
  have h1 := h Ï†,
  rw eq_conj_iff_real at h1,
  exact h1,
end

end number_field.embeddings

namespace number_field

open number_field.embeddings

/-- A number field all of whose embeddings are real. -/
def is_totally_real {K : Type*} [field K] : Prop := âˆ€ Ï† : K â†’+* â„‚, is_real Ï†

/-- A number field all of whose embeddings are complex. -/
def is_totally_complex {K : Type*} [field K] : Prop := âˆ€ Ï† : K â†’+* â„‚, is_complex Ï†

end number_field
