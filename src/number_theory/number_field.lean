/-
Copyright (c) 2021 Ashvni Narayanan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ashvni Narayanan, Anne Baanen
-/

import ring_theory.dedekind_domain.integral_closure
import algebra.char_p.algebra

/-!
# Number fields
This file defines a number field, the ring of integers corresponding to it and includes some
basic facts about the embeddings into an algebraic closed field.

## Main definitions
 - `number_field` defines a number field as a field which has characteristic zero and is finite
    dimensional over â„š.
 - `ring_of_integers` defines the ring of integers (or number ring) corresponding to a number field
    as the integral closure of â„¤ in the number field.

## Main Result
 - `eq_roots`: let `x âˆˆ K` with `K` number field and let `A` be an algebraic closed field of
    char. 0, then the images of `x` by the embeddings of `K` in `A` are exactly the roots in
    `A` of the minimal polynomial of `x` over `â„š`.

## Implementation notes
The definitions that involve a field of fractions choose a canonical field of fractions,
but are independent of that choice.

## References
* [D. Marcus, *Number Fields*][marcus1977number]
* [J.W.S. Cassels, A. FrÃ¶lich, *Algebraic Number Theory*][cassels1967algebraic]
* [P. Samuel, *Algebraic Theory of Numbers*][samuel1970algebraic]

## Tags
number field, ring of integers
-/

/-- A number field is a field which has characteristic zero and is finite
dimensional over â„š. -/
class number_field (K : Type*) [field K] : Prop :=
[to_char_zero : char_zero K]
[to_finite_dimensional : finite_dimensional â„š K]

open function
open_locale classical big_operators

/-- `â„¤` with its usual ring structure is not a field. -/
lemma int.not_is_field : Â¬ is_field â„¤ :=
Î» h, int.not_even_one $ (h.mul_inv_cancel two_ne_zero).imp $ Î» a, (by rw â† two_mul; exact eq.symm)

namespace number_field

variables (K L : Type*) [field K] [field L] [nf : number_field K]

include nf

-- See note [lower instance priority]
attribute [priority 100, instance] number_field.to_char_zero number_field.to_finite_dimensional

protected lemma is_algebraic : algebra.is_algebraic â„š K := algebra.is_algebraic_of_finite _ _

omit nf

/-- The ring of integers (or number ring) corresponding to a number field
is the integral closure of â„¤ in the number field. -/
def ring_of_integers := integral_closure â„¤ K

localized "notation `ğ“` := number_field.ring_of_integers" in number_field

lemma mem_ring_of_integers (x : K) : x âˆˆ ğ“ K â†” is_integral â„¤ x := iff.rfl

/-- Given an algebra between two fields, create an algebra between their two rings of integers.

For now, this is not an instance by default as it creates an equal-but-not-defeq diamond with
`algebra.id` when `K = L`. This is caused by `x = âŸ¨x, x.propâŸ©` not being defeq on subtypes. This
will likely change in Lean 4. -/
def ring_of_integers_algebra [algebra K L] : algebra (ğ“ K) (ğ“ L) := ring_hom.to_algebra
{ to_fun := Î» k, âŸ¨algebra_map K L k, is_integral.algebra_map k.2âŸ©,
  map_zero' := subtype.ext $ by simp only [subtype.coe_mk, subalgebra.coe_zero, map_zero],
  map_one'  := subtype.ext $ by simp only [subtype.coe_mk, subalgebra.coe_one, map_one],
  map_add' := Î» x y, subtype.ext $ by simp only [map_add, subalgebra.coe_add, subtype.coe_mk],
  map_mul' := Î» x y, subtype.ext $ by simp only [subalgebra.coe_mul, map_mul, subtype.coe_mk] }

namespace ring_of_integers

variables {K}

instance [number_field K] : is_fraction_ring (ğ“ K) K :=
integral_closure.is_fraction_ring_of_finite_extension â„š _

instance : is_integral_closure (ğ“ K) â„¤ K :=
integral_closure.is_integral_closure _ _

instance [number_field K] : is_integrally_closed (ğ“ K) :=
integral_closure.is_integrally_closed_of_finite_extension â„š

lemma is_integral_coe (x : ğ“ K) : is_integral â„¤ (x : K) :=
x.2

/-- The ring of integers of `K` are equivalent to any integral closure of `â„¤` in `K` -/
protected noncomputable def equiv (R : Type*) [comm_ring R] [algebra R K]
  [is_integral_closure R â„¤ K] : ğ“ K â‰ƒ+* R :=
(is_integral_closure.equiv â„¤ R K _).symm.to_ring_equiv

variables (K)

instance [number_field K] : char_zero (ğ“ K) := char_zero.of_module _ K

/-- The ring of integers of a number field is not a field. -/
lemma not_is_field [number_field K] : Â¬ is_field (ğ“ K) :=
begin
  have h_inj : function.injective â‡‘(algebra_map â„¤ (ğ“ K)),
  { exact ring_hom.injective_int (algebra_map â„¤ (ğ“ K)) },
  intro hf,
  exact int.not_is_field
    (((is_integral_closure.is_integral_algebra â„¤ K).is_field_iff_is_field h_inj).mpr hf)
end

instance [number_field K] : is_dedekind_domain (ğ“ K) :=
is_integral_closure.is_dedekind_domain â„¤ â„š K _

end ring_of_integers

end number_field

namespace rat

open number_field

local attribute [instance] subsingleton_rat_module

instance number_field : number_field â„š :=
{ to_char_zero := infer_instance,
  to_finite_dimensional :=
    -- The vector space structure of `â„š` over itself can arise in multiple ways:
    -- all fields are vector spaces over themselves (used in `rat.finite_dimensional`)
    -- all char 0 fields have a canonical embedding of `â„š` (used in `number_field`).
    -- Show that these coincide:
    by convert (infer_instance : finite_dimensional â„š â„š), }

/-- The ring of integers of `â„š` as a number field is just `â„¤`. -/
noncomputable def ring_of_integers_equiv : ring_of_integers â„š â‰ƒ+* â„¤ :=
ring_of_integers.equiv â„¤

end rat

namespace adjoin_root

section

open_locale polynomial

local attribute [-instance] algebra_rat
local attribute [instance] algebra_rat_subsingleton

/-- The quotient of `â„š[X]` by the ideal generated by an irreducible polynomial of `â„š[X]`
is a number field. -/
instance {f : â„š[X]} [hf : fact (irreducible f)] : number_field (adjoin_root f) :=
{ to_char_zero := char_zero_of_injective_algebra_map (algebra_map â„š _).injective,
  to_finite_dimensional := by convert (adjoin_root.power_basis hf.out.ne_zero).finite_dimensional }

end

end adjoin_root

namespace number_field.embeddings

section number_field

open set finite_dimensional polynomial

variables {K L : Type*} [field K] [field L]
variables [number_field K] [number_field L]  (x : K)

variables {A : Type*} [field A] [char_zero A]

/-- There are finitely many embeddings of a number field. -/
noncomputable instance : fintype (K â†’+* A) := fintype.of_equiv (K â†’â‚[â„š] A)
ring_hom.equiv_rat_alg_hom.symm

variables [is_alg_closed A]

/-- The number of embeddings of a number field is its finrank. -/
lemma card : fintype.card (K â†’+* A) = finrank â„š K :=
by rw [fintype.of_equiv_card ring_hom.equiv_rat_alg_hom.symm, alg_hom.card]

/-- For `x âˆˆ K`, with `K` a number field, the images of `x` by the embeddings of `K` are exactly
the roots of the minimal polynomial of `x` over `â„š` -/
lemma eq_roots : range (Î» Ï† : K â†’+* A, Ï† x) = (minpoly â„š x).root_set A :=
begin
  have hx : is_integral â„š x := is_separable.is_integral â„š x,
  ext a, split,
  { rintro âŸ¨Ï†, hÏ†âŸ©,
    rw [mem_root_set_iff, â†hÏ†],
    { let Ïˆ := ring_hom.equiv_rat_alg_hom Ï†,
      show (aeval (Ïˆ x)) (minpoly â„š x) = 0,
      rw aeval_alg_hom_apply Ïˆ x (minpoly â„š x),
      simp only [minpoly.aeval, map_zero], },
    exact minpoly.ne_zero hx, },
  { intro ha,
    let Qx := adjoin_root (minpoly â„š x),
    haveI : fact (irreducible $ minpoly â„š x) := âŸ¨minpoly.irreducible hxâŸ©,
    have hK : (aeval x) (minpoly â„š x) = 0 := minpoly.aeval _ _,
    have hA : (aeval a) (minpoly â„š x) = 0,
    { rw [aeval_def, â†eval_map, â†mem_root_set_iff'],
      exact ha,
      refine polynomial.monic.ne_zero _,
      exact polynomial.monic.map (algebra_map â„š A) (minpoly.monic hx), },
    let Ïˆ : Qx â†’+* A := by convert adjoin_root.lift (algebra_map â„š A) a hA,
    letI : algebra Qx A := ring_hom.to_algebra Ïˆ,
    letI : algebra Qx K := ring_hom.to_algebra (by convert adjoin_root.lift (algebra_map â„š K) x hK),
    let Ï†â‚€ : K â†’â‚[Qx] A,
    { refine is_alg_closed.lift (algebra.is_algebraic_of_larger_base â„š Qx _),
      exact number_field.is_algebraic _, },
    let Ï† := Ï†â‚€.to_ring_hom,
    use Ï†,
    rw (_ : x = (algebra_map Qx K) (adjoin_root.root (minpoly â„š x))),
    { rw (_ : a = Ïˆ (adjoin_root.root (minpoly â„š x))),
      refine alg_hom.commutes _ _,
      exact (adjoin_root.lift_root hA).symm, },
    exact (adjoin_root.lift_root hK).symm, },
end

end number_field

end number_field.embeddings
