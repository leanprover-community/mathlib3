/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import number_theory.number_field.embeddings
import measure_theory.group.geometry_of_numbers
import ring_theory.discriminant
import algebra.module.zlattice

/-!
# Canonical embedding of a number field

The canonical embedding of a number field `K` of signature `(râ‚, râ‚‚)` is the ring homomorphism
`K â†’+* â„^râ‚ Ã— â„‚^râ‚‚` that sends `x âˆˆ K` to `(Ï†_â‚(x),...,Ï†_râ‚(x)) Ã— (Ïˆ_â‚(x),..., Ïˆ_râ‚‚(x))` where
`Ï†_â‚,...,Ï†_râ‚` are its real embeddings and `Ïˆ_â‚,..., Ïˆ_râ‚‚` are its complex embeddings (up to
complex conjugation).

## Main definitions and results

* `number_field.canonical_embedding.ring_of_integers.inter_ball_finite`: the intersection of the
image of the ring of integers by the canonical embedding and any ball centered at `0` of finite
radius is finite.
* `number_field.canonical_embedding.lattice_basis`:  a `â„`-basis of `â„^râ‚ Ã— â„‚^râ‚‚` that is also
a `â„¤`-basis of the image of `ğ“ K` by `canonical_embedding`.
* `number_field.canonical_embedding.exists_ne_zero_mem_ring_of_integers_le`: for
`f : (infinite_place K) â†’ nnreal` such that `prod_w f w` is large enough, returns `a : ğ“ K` with
`a â‰  0` and `âˆ€ w : infinite_place K, w a < f w`.

## Tags

number field, infinite places
-/

noncomputable theory

open function finite_dimensional finset fintype number_field number_field.infinite_place metric
module

open_locale classical number_field

variables (K : Type*) [field K]

namespace number_field.canonical_embedding

-- The ambient space `â„^râ‚ Ã— â„‚^râ‚‚` with `(râ‚, râ‚‚)` the signature of `K`.
localized "notation `E` :=
  ({w : infinite_place K // is_real w} â†’ â„) Ã— ({w : infinite_place K // is_complex w} â†’ â„‚)"
  in canonical_embedding

lemma space_rank [number_field K] :
  finrank â„ E = finrank â„š K :=
begin
  haveI : module.free â„ â„‚ := infer_instance,
  rw [finrank_prod, finrank_pi, finrank_pi_fintype, complex.finrank_real_complex,
    sum_const, card_univ, â† card_real_embeddings, algebra.id.smul_eq_mul, mul_comm,
    â† card_complex_embeddings, â† number_field.embeddings.card K â„‚, card_subtype_compl,
    nat.add_sub_of_le (card_subtype_le _)],
end

lemma non_trivial_space [number_field K] : nontrivial E :=
begin
  obtain âŸ¨wâŸ© := infinite_place.nonempty K,
  obtain hw | hw := w.is_real_or_is_complex,
  { haveI : nonempty {w : infinite_place K // is_real w} := âŸ¨âŸ¨w, hwâŸ©âŸ©,
    exact nontrivial_prod_left },
  { haveI : nonempty {w : infinite_place K // is_complex w} := âŸ¨âŸ¨w, hwâŸ©âŸ©,
    exact nontrivial_prod_right }
end

/-- The canonical embedding of a number field `K` of signature `(râ‚, râ‚‚)` into `â„^râ‚ Ã— â„‚^râ‚‚`. -/
def _root_.number_field.canonical_embedding : K â†’+* E :=
ring_hom.prod (pi.ring_hom (Î» w, w.prop.embedding)) (pi.ring_hom (Î» w, w.val.embedding))

lemma _root_.number_field.canonical_embedding_injective [number_field K] :
  injective (number_field.canonical_embedding K) :=
  @ring_hom.injective _ _ _ _ (non_trivial_space K) _

open number_field

variable {K}

@[simp]
lemma apply_at_real_infinite_place (w : {w : infinite_place K // is_real w}) (x : K) :
  (number_field.canonical_embedding K x).1 w = w.prop.embedding x :=
by simp only [canonical_embedding, ring_hom.prod_apply, pi.ring_hom_apply]

@[simp]
lemma apply_at_complex_infinite_place (w : { w : infinite_place K // is_complex w}) (x : K) :
  (number_field.canonical_embedding K x).2 w = embedding w.val x :=
by simp only [canonical_embedding, ring_hom.prod_apply, pi.ring_hom_apply]

lemma nnnorm_eq [number_field K] (x : K) :
  â€–canonical_embedding K xâ€–â‚Š = univ.sup (Î» w : infinite_place K, âŸ¨w x, map_nonneg w xâŸ©) :=
begin
  rw [prod.nnnorm_def', pi.nnnorm_def, pi.nnnorm_def],
  rw ( _ : univ = {w : infinite_place K | is_real w}.to_finset
    âˆª {w : infinite_place K | is_complex w}.to_finset),
  { rw [sup_union, sup_eq_max],
    refine congr_arg2 _ _ _,
    { convert (univ.sup_map (embedding.subtype (Î» w : infinite_place K, is_real w))
        (Î» w, (âŸ¨w x, map_nonneg w xâŸ© : nnreal))).symm using 2,
      ext w,
      simp only [apply_at_real_infinite_place, coe_nnnorm, real.norm_eq_abs,
        embedding.coe_subtype, subtype.coe_mk, is_real.abs_embedding_apply], },
    { convert (univ.sup_map (embedding.subtype (Î» w : infinite_place K,
        is_complex w)) (Î» w, (âŸ¨w x, map_nonneg w xâŸ© : nnreal))).symm using 2,
      ext w,
      simp only [apply_at_complex_infinite_place, subtype.val_eq_coe, coe_nnnorm,
        complex.norm_eq_abs, embedding.coe_subtype, subtype.coe_mk, abs_embedding], }},
  { ext w,
    simp only [w.is_real_or_is_complex, set.mem_set_of_eq, mem_union, set.mem_to_finset,
      mem_univ], },
end

lemma norm_le_iff [number_field K] (x : K) (r : â„) :
  â€–canonical_embedding K xâ€– â‰¤ r â†” âˆ€ w : infinite_place K, w x â‰¤ r :=
begin
  obtain hr | hr := lt_or_le r 0,
  { obtain âŸ¨wâŸ© := infinite_place.nonempty K,
    exact iff_of_false (hr.trans_le $ norm_nonneg _).not_le
      (Î» h, hr.not_le $ (map_nonneg w _).trans $ h _) },
  { lift r to nnreal using hr,
    simp_rw [â† coe_nnnorm, nnnorm_eq, nnreal.coe_le_coe, finset.sup_le_iff, mem_univ,
      forall_true_left, â†nnreal.coe_le_coe, subtype.coe_mk] }
end

variables (K)

/-- The image of `ğ“ K` as a subring of `â„^râ‚ Ã— â„‚^râ‚‚`. -/
def integer_lattice : subring E :=
(ring_hom.range (algebra_map (ğ“ K) K)).map (canonical_embedding K)

/-- The linear equiv between `ğ“ K` and the integer lattice. -/
def equiv_integer_lattice [number_field K] :
  ğ“ K â‰ƒâ‚—[â„¤] integer_lattice K :=
linear_equiv.of_bijective
  { to_fun := Î» x, âŸ¨canonical_embedding K (algebra_map (ğ“ K) K x), algebra_map (ğ“ K) K x,
      by simp only [subring.mem_carrier, ring_hom.mem_range, exists_apply_eq_apply], rflâŸ©,
    map_add' := Î» x y, by simpa only [map_add],
    map_smul' := Î» c x, by simpa only [zsmul_eq_mul, map_mul, map_int_cast] }
  begin
    refine âŸ¨Î» _ _ h, _,  Î» âŸ¨_, _, âŸ¨a, rflâŸ©, rflâŸ©, âŸ¨a, rflâŸ©âŸ©,
    rw [linear_map.coe_mk, subtype.mk_eq_mk] at h,
    exact is_fraction_ring.injective (ğ“ K) K (canonical_embedding_injective K h),
  end

lemma integer_lattice.inter_ball_finite [number_field K] (r : â„) :
  ((integer_lattice K : set E) âˆ© (closed_ball 0 r)).finite :=
begin
  obtain hr | hr := lt_or_le r 0,
  {  simp [closed_ball_eq_empty.2 hr] },
  have heq :
    âˆ€ x, canonical_embedding K x âˆˆ closed_ball (0 : E) r â†” âˆ€ Ï† : K â†’+* â„‚, â€–Ï† xâ€– â‰¤ r,
  { simp only [â† place_apply, â† infinite_place.coe_mk, mem_closed_ball_zero_iff, norm_le_iff],
    exact Î» x, le_iff_le x r, },
  convert (embeddings.finite_of_norm_le K â„‚ r).image (canonical_embedding K),
  ext, split,
  { rintro âŸ¨âŸ¨_, âŸ¨x, rflâŸ©, rflâŸ©, hx2âŸ©,
    exact âŸ¨x, âŸ¨set_like.coe_mem x, (heq x).mp hx2âŸ©, rflâŸ©, },
  { rintro âŸ¨x, âŸ¨hx1, hx2âŸ©, rflâŸ©,
    exact âŸ¨âŸ¨x, âŸ¨âŸ¨x, hx1âŸ©, rflâŸ©, rflâŸ©, (heq x).mpr hx2âŸ©, }
end

instance [number_field K] : countable (integer_lattice K) :=
begin
  have : (â‹ƒ n : â„•, ((integer_lattice K : set E) âˆ© (closed_ball 0 n))).countable,
  { exact set.countable_Union (Î» n, (integer_lattice.inter_ball_finite K n).countable) },
  refine (this.mono _).to_subtype,
  rintro _ âŸ¨x, hx, rflâŸ©,
  rw set.mem_Union,
  exact âŸ¨âŒˆâ€–canonical_embedding K xâ€–âŒ‰â‚Š, âŸ¨x, hx, rflâŸ©, mem_closed_ball_zero_iff.2 (nat.le_ceil _)âŸ©,
end

section basis

open_locale complex_conjugate

variable (K)

/-- The embedding of `K` into `K â†’+* (K â†’+* â„‚) â†’ â„‚` defined by sending `x : K` to the vector of its
image by all the complex embeddings of `K`. -/
def _root_.number_field.full_embedding : K â†’+* (K â†’+* â„‚) â†’ â„‚ :=
{ to_fun := Î» x Ï†, Ï† x,
  map_zero' := funext (Î» Ï†, map_zero Ï†),
  map_one' := funext (Î» Ï†, map_one Ï†),
  map_add' := Î» x y, funext (Î» Ï†, map_add Ï† x y),
  map_mul' := Î» x y, funext (Î» Ï†, map_mul Ï† x y), }

/-- The map from `(K â†’+* â„‚) â†’ â„‚` to `space K` that gives a commuting diagramm, see
`number_field.canonical_embedding.commutes`. -/
def comm_map : ((K â†’+* â„‚) â†’ â„‚) â†’â‚—[â„] E :=
{ to_fun := Î» e, âŸ¨Î» w, (e w.val.embedding).re, Î» w, (e w.val.embedding)âŸ©,
  map_smul' := Î» _ _, by simp_rw [ring_hom.id_apply, prod.smul_mk, pi.smul_def, smul_eq_mul,
    complex.real_smul, complex.of_real_mul_re],
  map_add' := Î» _ _, by simp only [subtype.val_eq_coe, pi.add_apply, complex.add_re, prod.mk_add_mk,
    pi.add_def, eq_self_iff_true], }

lemma _root_.number_field.full_embedding.conj_apply [number_field K] {x : (K â†’+* â„‚) â†’ â„‚}
  (Ï† : K â†’+* â„‚) (hx : x âˆˆ submodule.span â„
    (set.range (Î» i, number_field.full_embedding K (integral_basis K i)))) :
  x (complex_embedding.conjugate Ï†) = conj (x Ï†) :=
begin
  refine submodule.span_induction hx _ _ (Î» _ _ hx hy, _) (Î» _ _ hx, _),
  { rintros _ âŸ¨_, rflâŸ©, refl, },
  { simp only [pi.zero_apply, map_zero], },
  { rw [pi.add_apply, pi.add_apply, map_add, hx, hy], },
  { rw [pi.smul_apply, pi.smul_apply, complex.real_smul, complex.real_smul, map_mul, hx,
      is_R_or_C.conj_of_real], }
end

open number_field

lemma comm_map_eq_zero [number_field K] {x : (K â†’+* â„‚) â†’ â„‚}
  (hx : x âˆˆ submodule.span â„ (set.range (Î» i, full_embedding K (integral_basis K i))))
  (hc : comm_map K x = 0):
  x = 0 :=
begin
  ext1 Ï†,
  rw pi.zero_apply,
  by_cases hÏ† : complex_embedding.is_real Ï†,
  { rw (_ : x Ï† = (x Ï†).re),
    { convert congr_arg (coe : â„ â†’ â„‚)
        (congr_arg (Î» x : E, x.1 âŸ¨mk Ï†, âŸ¨Ï†, hÏ†, rflâŸ©âŸ©) hc),
      exact (complex_embeddings.is_real.embedding_mk hÏ†).symm, },
    { rw [eq_comm, â† complex.conj_eq_iff_re, â† full_embedding.conj_apply K _ hx,
        complex_embedding.is_real_iff.mp hÏ†], }},
  { have heqz := congr_arg (Î» x : E, x.2 âŸ¨mk Ï†, âŸ¨Ï†, hÏ†, rflâŸ©âŸ©) hc,
    by_cases h_same : Ï† = (infinite_place.mk Ï†).embedding,
    { convert heqz using 2, },
    { rw [ â† map_eq_zero_iff (star_ring_end â„‚) star_injective, â† full_embedding.conj_apply K _ hx],
      rw (_ : Ï† = complex_embedding.conjugate (infinite_place.mk Ï†).embedding),
      { convert heqz using 2,
        ext1 Ï†,
        simp only [complex_embedding.conjugate_coe_eq, star_ring_end_self_apply], },
      { rw eq_comm,
        refine (mk_eq_iff.mp _).resolve_left (ne_comm.mp h_same),
        exact mk_embedding _, }}},
end

lemma commutes (x : K) :
  comm_map K (full_embedding K x) = canonical_embedding K x :=
begin
  simp_rw [comm_map, full_embedding, canonical_embedding, subtype.val_eq_coe, ring_hom.coe_mk,
    linear_map.coe_mk, ring_hom.prod_apply, prod.mk.inj_iff],
  split,
  { ext w,
    simpa only [pi.ring_hom_apply, â† complex_embedding.is_real.coe_embedding_apply
      (is_real_iff.mp w.prop) x, complex.of_real_re], },
  { ext1 w,
    simp only [pi.ring_hom_apply], },
end

/-- A `â„`-basis of `(space K)` that is also a `â„¤`-basis of the `unit_lattice`. -/
def lattice_basis [number_field K] : basis (free.choose_basis_index â„¤ (ğ“ K)) â„ E :=
begin
  let e : (K â†’+* â„‚) â‰ƒ free.choose_basis_index â„¤ (ğ“ K) :=
    equiv_of_card_eq ((embeddings.card K â„‚).trans (finrank_eq_card_basis (integral_basis K))),
  -- Assume that `full_embedding K (integral_basis K)` is `â„‚`-linear independent then we deduce
  -- using `comm_map_eq_zero` that `canonical_embedding K (integral_basis K)` is
  -- `â„‚`-linear independent and thus `â„¤`-linear independent and yields the desired basis.
  suffices : linear_independent â„‚ (Î» i, full_embedding K (integral_basis K (e i))),
  { replace := @linear_independent.restrict_scalars _ â„ â„‚ _ _ _ _ _ _ _ _ _
      (smul_left_injective â„ one_ne_zero) this,
    replace : linear_independent â„ (Î» i, full_embedding K (integral_basis K i)),
    { refine (linear_independent_equiv' e.symm _).mpr this,
      ext1 Ï†,
      simp only [equiv.apply_symm_apply, comp_app], },
    replace : linear_independent â„ (Î» i, (comm_map K âˆ˜ full_embedding K) (integral_basis K i)),
    { exact linear_independent.map this
        (linear_map.disjoint_ker.mpr (Î» x hx hc, comm_map_eq_zero K hx hc)), },
    replace : linear_independent â„ (Î» i, canonical_embedding K (integral_basis K i)),
    { refine (linear_independent_equiv' (equiv.refl _) _).mp this,
      ext1 i,
      exact (commutes K (integral_basis K i)).symm, },
    refine basis_of_linear_independent_of_card_eq_finrank this _,
    rw [canonical_embedding.space_rank, â† finrank_eq_card_choose_basis_index,
      â† ring_of_integers.rank], },
  -- To prove that `full_embedding K (integral_basis K)` is `â„‚`-linear independent, we
  -- prove that the square of the determinant of its matrix on the standard basis of
  -- `((K â†’* â„‚) â†’ â„‚)` is the discrimininant of the `â„š`-algebra `K` and thus it is not zero.
  let B := pi.basis_fun â„‚ (K â†’+* â„‚),
  let M := B.to_matrix (Î» i, full_embedding K (integral_basis K (e i))),
  suffices : M.det â‰  0,
  { rw [â† is_unit_iff_ne_zero, â† basis.det_apply, â† is_basis_iff_det] at this,
    exact this.1, },
  let N := algebra.embeddings_matrix_reindex â„š â„‚ (Î» i, integral_basis K (e i))
    ring_hom.equiv_rat_alg_hom,
  rw (_ : M = N.transpose),
  { rw [matrix.det_transpose, â† @pow_ne_zero_iff â„‚ _ _ _ 2 (by norm_num)],
    convert (map_ne_zero_iff _ (algebra_map â„š â„‚).injective).mpr
      (algebra.discr_not_zero_of_basis â„š (integral_basis K)),
    rw â† algebra.discr_reindex â„š (integral_basis K) e.symm,
    exact (algebra.discr_eq_det_embeddings_matrix_reindex_pow_two â„š â„‚
      (Î» i, integral_basis K (e i)) ring_hom.equiv_rat_alg_hom).symm, },
  { ext1 Ï† j,
    simpa only [M, N, basis.to_matrix_apply _ _ Ï† j, pi.basis_fun_repr], },
end

lemma lattice_basis_apply [number_field K] (i : free.choose_basis_index â„¤ (ğ“ K)) :
  (lattice_basis K) i = (canonical_embedding K) (integral_basis K i) :=
by simp only [lattice_basis, coe_basis_of_linear_independent_of_card_eq_finrank]

lemma lattice_basis_span [number_field K] :
  (submodule.span â„¤ (set.range (lattice_basis K)) : set E) = integer_lattice K :=
begin
  rw (_ : set.range (lattice_basis K) =
    (canonical_embedding K).to_int_alg_hom.to_linear_map '' (set.range (integral_basis K))),
  { rw â† submodule.map_span,
    rw (_ : set.range (integral_basis K) =
      (algebra_map (ğ“ K) K).to_int_alg_hom.to_linear_map '' (set.range (ring_of_integers.basis K))),
    { rw [â† submodule.map_span, (ring_of_integers.basis K).span_eq, submodule.map_coe,
        submodule.map_coe],
      ext, split,
      { rintro âŸ¨_, âŸ¨a, _, rflâŸ©, rflâŸ©,
        exact âŸ¨a, set.mem_range_self a, rflâŸ©, },
      { rintro âŸ¨_, âŸ¨a, rflâŸ©, rflâŸ©,
        exact âŸ¨a, âŸ¨a, trivial, rflâŸ©, rflâŸ©, }},
    { rw â† set.range_comp,
      refine congr_arg _ _,
      ext, simpa only [integral_basis_apply, comp_app, alg_hom.to_linear_map_apply], }},
  { rw â† set.range_comp,
    refine congr_arg _ _,
    ext1, simpa only [lattice_basis_apply, integral_basis_apply, comp_app,
      alg_hom.to_linear_map_apply], },
end

end basis

section convex_body

open measure_theory

-- TODO: use big_operators

/-- The real part of the convex body defined by `f`, see `convex_body`. -/
def convex_body_real (f : infinite_place K â†’ nnreal) : set ({w : infinite_place K // is_real w} â†’ â„)
:= set.pi set.univ (Î» w, ball 0 (f w))

/-- The complex part of the convex body defined by `f`, see `convex_body`. -/
def convex_body_complex (f : infinite_place K â†’ nnreal) :
  set ({w : infinite_place K // is_complex w} â†’ â„‚) := set.pi set.univ (Î» w, ball 0 (f w))

/-- The convex body defined by `f`: the set of points `x : E` such that `x w < f w` for all
infinite places `w`. -/
def convex_body (f : infinite_place K â†’ nnreal): set E :=
(convex_body_real K f) Ã—Ë¢ (convex_body_complex K f)

lemma convex_body.symmetric (f : infinite_place K â†’ nnreal) (x : E)
  (hx : x âˆˆ (convex_body K f)) : -x âˆˆ (convex_body K f) :=
begin
  refine set.mem_prod.1 âŸ¨_, _âŸ©,
  { intros w _,
    simpa only [prod.fst_neg, pi.neg_apply, mem_ball_zero_iff, real.norm_eq_abs, abs_neg]
      using mem_ball_zero_iff.1 (hx.1 w (set.mem_univ _)), },
  { intros w _,
    simpa only [prod.snd_neg, pi.neg_apply, mem_ball_zero_iff, complex.norm_eq_abs,
      absolute_value.map_neg] using mem_ball_zero_iff.mp (hx.right w (set.mem_univ w)), }
end

lemma convex_body.convex (f : infinite_place K â†’ nnreal) :
  convex â„ (convex_body K f) :=
convex.prod (convex_pi (Î» i _, (convex_ball 0 (f i)))) (convex_pi (Î» i _, (convex_ball 0 (f i))))

lemma convex_body_mem (f : infinite_place K â†’ nnreal) (x : K) :
  canonical_embedding K x âˆˆ (convex_body K f) â†” âˆ€ w : infinite_place K, w x < f w :=
begin
  suffices : (âˆ€ w : infinite_place K, w x < f w) â†”
    (âˆ€ w : infinite_place K, w.is_real âˆ¨ w.is_complex â†’ w x < f w),
  { simp only [convex_body, this, ball_or_left_distrib, set.mem_prod, convex_body_real,
    convex_body_complex, set.mem_univ_pi, apply_at_real_infinite_place, mem_ball_zero_iff,
    real.norm_eq_abs, is_real.abs_embedding_apply, subtype.forall, subtype.coe_mk,
    apply_at_complex_infinite_place, complex.norm_eq_abs, abs_embedding], },
  simp only [â† infinite_place.not_is_real_iff_is_complex, em, forall_true_left],
end

variable [number_field K]

lemma convex_body_real.volume (f : infinite_place K â†’ nnreal) :
  volume (convex_body_real K f) = 2 ^ card {w : infinite_place K // is_real w} *
    univ.prod (Î» w : {w : infinite_place K // is_real w}, f w) :=
by simp_rw [convex_body_real, volume_pi, measure.pi_pi, real.volume_ball,
    ennreal.of_real_mul (by norm_num : 0 â‰¤ (2 : â„)), ennreal.of_real_bit0, ennreal.of_real_one,
    ennreal.of_real_coe_nnreal, prod_mul_distrib, prod_const, card_univ]

lemma convex_body_complex.volume (f : infinite_place K â†’ nnreal) :
  volume (convex_body_complex K f) =
    volume (ball (0 : â„‚) 1) ^ card {w : infinite_place K // is_complex w} *
    univ.prod (Î» w : {w : infinite_place K // is_complex w}, (f w) ^ 2) :=
begin
  haveI : measure.is_add_haar_measure complex.measure_space.volume := infer_instance,
  rw [convex_body_complex, volume_pi, measure.pi_pi],
  conv { to_lhs, congr, skip, funext,
    rw measure.add_haar_ball volume (0 : â„‚) (f i).prop,
    rw ennreal.of_real_pow (f i).prop, },
  simp_rw [prod_mul_distrib, prod_const, mul_comm, complex.finrank_real_complex, card_univ,
    ennreal.of_real_coe_nnreal],
end

/-- The fudge factor that appears in the formula for the volume of `convex_body`.-/
def constant_volume : ennreal := 2 ^ card {w : infinite_place K // is_real w} *
  volume (ball (0 : â„‚) 1) ^ card {w : infinite_place K // is_complex w}

lemma constant_volume_pos : 0 < (constant_volume K) :=
begin
  refine ennreal.mul_pos (ne_zero.ne _) _,
  exact ennreal.pow_ne_zero (ne_of_gt (measure_ball_pos _ _ (by norm_num))) _,
end

lemma constant_volume_lt_top : (constant_volume K) < âŠ¤ :=
begin
  refine ennreal.mul_lt_top _ _,
  { exact ne_of_lt (ennreal.pow_lt_top (lt_top_iff_ne_top.mpr ennreal.two_ne_top) _), },
  { exact ne_of_lt (ennreal.pow_lt_top measure_ball_lt_top _), },
end

lemma convex_body.volume (f : infinite_place K â†’ nnreal) :
  volume (convex_body K f) = (constant_volume K) *
    univ.prod (Î» w : infinite_place K, (ite (w.is_real) (f w) (f w ^ 2))) :=
begin
  haveI : measure.is_add_haar_measure complex.measure_space.volume := infer_instance,
  simp_rw [measure.volume_eq_prod, convex_body, measure.prod_prod, convex_body_real.volume,
    convex_body_complex.volume, constant_volume, prod_ite, â† prod_subtype_eq_prod_filter],
  rw (by { ext, simp only [mem_subtype, mem_univ], } :
    finset.subtype (Î» (w : infinite_place K), is_real w) univ = univ),
  rw ( by { ext, simp only [mem_subtype, mem_univ], } :
    finset.subtype (Î» (w : infinite_place K), Â¬ is_real w) univ = univ),
  rw â† equiv.prod_comp (equiv.subtype_equiv_right
    (Î» w, (@infinite_place.not_is_real_iff_is_complex K _ w).symm)) _,
  simp_rw [equiv.subtype_equiv_right_apply_coe],
  ring,
end

/-- The bound that appears in Minkowski theorem, see
`exists_ne_zero_mem_lattice_of_measure_mul_two_pow_lt_measure`.-/
def minkowski_bound : ennreal := volume (zspan.fundamental_domain (lattice_basis K)) *
  2 ^ (finrank â„ E)

lemma minkowski_bound_lt_top : minkowski_bound K < âŠ¤ :=
begin
  haveI : measure.is_add_haar_measure complex.measure_space.volume := infer_instance,
  refine ennreal.mul_lt_top _ _,
  { exact ne_of_lt (zspan.fundamental_domain_bounded (lattice_basis K)).measure_lt_top, },
  { exact ne_of_lt (ennreal.pow_lt_top (lt_top_iff_ne_top.mpr ennreal.two_ne_top) _), },
end

lemma exists_ne_zero_mem_ring_of_integers_lt {f : (infinite_place K) â†’ nnreal}
  (hf : minkowski_bound K < volume (convex_body K f)) :
  âˆƒ (a : ğ“ K), a â‰  0 âˆ§ âˆ€ w : infinite_place K, w a < f w :=
begin
  haveI : @measure.is_add_haar_measure E _ _ _ volume,
  begin
    haveI : measure.is_add_haar_measure complex.measure_space.volume := infer_instance,
    haveI : @measure.is_add_haar_measure ({w : infinite_place K // is_complex w} â†’ â„‚) _ _ _ volume
      := measure.is_add_haar_measure.mk,
    exact measure.prod.is_add_haar_measure volume volume,
  end,
  haveI : countable (submodule.span â„¤ (set.range (lattice_basis K))).to_add_subgroup,
  { change countable (submodule.span â„¤ (set.range (lattice_basis K)) : set E),
    rw lattice_basis_span,
    exact integer_lattice.countable K, },
  have h_funddomain := zspan.is_add_fundamental_domain (lattice_basis K) volume,
  obtain âŸ¨âŸ¨x, hxâŸ©, hnz, hmemâŸ© := exists_ne_zero_mem_lattice_of_measure_mul_two_pow_lt_measure
    h_funddomain hf (convex_body.symmetric K f) (convex_body.convex K f),
  rw [submodule.mem_to_add_subgroup, â† set_like.mem_coe, lattice_basis_span] at hx,
  obtain âŸ¨_, âŸ¨a, rflâŸ©, rflâŸ© := hx,
  refine âŸ¨a, _, by { rwa â† convex_body_mem, }âŸ©,
  contrapose! hnz,
  simp only [hnz, map_zero, submodule.mk_eq_zero],
end

end convex_body

end number_field.canonical_embedding
