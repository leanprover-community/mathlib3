/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import group_theory.torsion
import number_theory.number_field.embeddings
import number_theory.number_field.norm
import ring_theory.roots_of_unity

/-!
 # Units of a number field
This file defines and proves results about the group `(ğ“ K)Ë£` of units of the ring of integers `ğ“ K`
of a number field `K`.

 ## Tags
number field, units
 -/

open_locale number_field

noncomputable theory

open number_field units

section rat

lemma rat.ring_of_integers.is_unit_iff {x : ğ“ â„š} :
  is_unit x â†” ((x : â„š) = 1) âˆ¨ ((x : â„š) = -1) :=
by simp_rw [(is_unit_map_iff (rat.ring_of_integers_equiv : ğ“ â„š â†’+* â„¤) x).symm, int.is_unit_iff,
  ring_equiv.coe_to_ring_hom, ring_equiv.map_eq_one_iff, ring_equiv.map_eq_neg_one_iff,
  â† subtype.coe_injective.eq_iff, add_subgroup_class.coe_neg, algebra_map.coe_one]

end rat

variables (K : Type*) [field K]

section is_unit

local attribute [instance] number_field.ring_of_integers_algebra

open finite_dimensional

lemma is_unit_iff_norm [number_field K] (x : ğ“ K) :
  is_unit x â†” abs (ring_of_integers.norm â„š x : â„š) = 1 :=
begin
  letI : algebra K (algebraic_closure K) := algebraic_closure.algebra K,
  let L := normal_closure â„š K (algebraic_closure K),
  haveI : finite_dimensional K L := finite_dimensional.right â„š K L,
  haveI : is_alg_closure â„š (algebraic_closure K) :=
    is_alg_closure.of_algebraic â„š K (algebraic_closure K) (number_field.is_algebraic K),
  haveI : is_galois K L := is_galois.tower_top_of_is_galois â„š K L,
  suffices : is_unit (ring_of_integers.norm K (algebra_map (ğ“ K) (ğ“ L) x)) â†”
    |(ring_of_integers.norm â„š (algebra_map (ğ“ K) (ğ“ L) x) : â„š)| = 1,
  { convert this using 1,
    { rw (_ : ring_of_integers.norm K (algebra_map (ğ“ K) (ğ“ L) x) = x ^ (finrank K L)),
      { rw is_unit_pow_iff,
        exact pos_iff_ne_zero.mp finrank_pos, },
      { rw [â† subtype.coe_inj, ring_of_integers.coe_norm_algebra_map, algebra.norm_algebra_map,
        subsemiring_class.coe_pow], }},
    { rw [ring_of_integers.norm_apply_coe, ring_of_integers.norm_apply_coe,
        show (algebra_map (ğ“ K) (ğ“ L) x : L) = algebra_map K L (x : K), by refl,
        â†algebra.norm_norm â„š K (algebra_map K L x : L), algebra.norm_algebra_map, map_pow, abs_pow],
      nth_rewrite 1 â† one_pow (finrank K L),
      rw pow_left_inj (abs_nonneg _ : 0 â‰¤ |(algebra.norm â„š) â†‘x|) zero_le_one
      (@finrank_pos K L _ _ _ _ _), }},
  { rw [ring_of_integers.is_unit_norm K, â† abs_one, abs_eq_abs, â† rat.ring_of_integers.is_unit_iff],
    exact (ring_of_integers.is_unit_norm â„š).symm, },
end

end is_unit

namespace number_field.units

open number_field number_field.infinite_place

/-- The `monoid_hom` from the group of units `(ğ“ K)Ë£` to the field `K`. -/
def coe_to_field : (ğ“ K)Ë£ â†’* K := (coe_hom K).comp  (map (algebra_map (ğ“ K) K))

lemma coe_to_field.injective : function.injective (coe_to_field K) :=
Î» _ _ h, eq_iff.mp (no_zero_smul_divisors.algebra_map_injective (ğ“ K) K h)

/-- There is a natural coercion from `(ğ“ K)Ë£` to `(ğ“ K)` and then from `(ğ“ K)` to `K` but it is
useful to also have a direct one from `(ğ“ K)Ë£` to `K`. -/
instance ring_of_integers.units.has_coe : has_coe (ğ“ K)Ë£ K := âŸ¨coe_to_field KâŸ©

@[ext]
lemma ext {x y : (ğ“ K)Ë£} : x = y â†” (x : K) = (y : K) := (coe_to_field.injective K).eq_iff.symm

@[simp]
lemma coe_pow {x : (ğ“ K)Ë£} {n : â„•} : ((x ^ n : (ğ“ K)Ë£) : K) = (x : K) ^ n :=
map_pow (coe_to_field K) _ _

@[simp]
lemma coe_one : ((1 : (ğ“ K)Ë£) : K) = (1 : K) := rfl

section torsion

/-- The torsion subgroup of the group of units. -/
def torsion : subgroup (ğ“ K)Ë£ := comm_group.torsion (ğ“ K)Ë£

lemma mem_torsion (x : (ğ“ K)Ë£) [number_field K] :
  x âˆˆ torsion K â†” âˆ€ w : infinite_place K, w x = 1 :=
begin
  rw [eq_iff_eq (x : K) 1, torsion, comm_group.mem_torsion, is_of_fin_order_iff_pow_eq_one],
  refine âŸ¨_, Î» h, _âŸ©,
  { rintros âŸ¨n, h1, h2âŸ© Ï†,
    convert @norm_map_one_of_pow_eq_one _ _ _ _ Ï†.to_monoid_hom _ âŸ¨n, h1âŸ© _,
    rwa [ext, coe_pow, coe_one] at h2, },
  { obtain âŸ¨n, hn, hxâŸ© := embeddings.pow_eq_one_of_norm_eq_one K â„‚ x.1.2 h,
    refine âŸ¨n, hn, by { rw [ext, coe_pow, coe_one]; exact hx, }âŸ©},
end

lemma torsion_finite [number_field K] : finite (torsion K) :=
begin
  refine set.finite_coe_iff.mpr (set.finite.of_finite_image _
    ((coe_to_field.injective K).inj_on _)),
  refine (embeddings.finite_of_norm_le K â„‚ 1).subset (Î» a ha, _),
  rcases ha with âŸ¨âŸ¨u, _, _, _âŸ©, hu, rflâŸ©,
  refine âŸ¨u.2, (le_iff_le _ 1).mp _âŸ©,
  convert Î» w, le_of_eq (((mem_torsion K _).mp hu) w) using 1,
end

instance [number_field K] : fintype (torsion K) := @fintype.of_finite (torsion K) (torsion_finite K)

lemma torsion_cyclic [number_field K] : is_cyclic (torsion K) := subgroup_units_cyclic _

/-- The order of the torsion group of the units of `K`. -/
def torsion_order [number_field K] : â„•+ := âŸ¨fintype.card (torsion K), fintype.card_posâŸ©

lemma torsion_eq_roots_of_unity [number_field K]  :
  torsion K = roots_of_unity (torsion_order K) (ğ“ K) :=
begin
  ext1,
  rw [torsion, mem_roots_of_unity],
  refine âŸ¨Î» h, _, Î» h, _âŸ©,
  { exact subtype.ext_iff.mp (@pow_card_eq_one (torsion K) âŸ¨x, hâŸ© _ _), },
  { rw [comm_group.mem_torsion, is_of_fin_order_iff_pow_eq_one],
    exact âŸ¨torsion_order K, (torsion_order K).pos, hâŸ©,}
end

end torsion

end number_field.units
