/-
Copyright (c) 2018 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis
-/
import analysis.specific_limits.basic
import data.polynomial.identities
import number_theory.padics.padic_integers
import topology.algebra.polynomial
import topology.metric_space.cau_seq_filter

/-!
# Hensel's lemma on â„¤_p

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file proves Hensel's lemma on â„¤_p, roughly following Keith Conrad's writeup:
<http://www.math.uconn.edu/~kconrad/blurbs/gradnumthy/hensel.pdf>

Hensel's lemma gives a simple condition for the existence of a root of a polynomial.

The proof and motivation are described in the paper
[R. Y. Lewis, *A formal proof of Hensel's lemma over the p-adic integers*][lewis2019].

## References

* <http://www.math.uconn.edu/~kconrad/blurbs/gradnumthy/hensel.pdf>
* [R. Y. Lewis, *A formal proof of Hensel's lemma over the p-adic integers*][lewis2019]
* <https://en.wikipedia.org/wiki/Hensel%27s_lemma>

## Tags

p-adic, p adic, padic, p-adic integer
-/

noncomputable theory

open_locale classical topology

-- We begin with some general lemmas that are used below in the computation.

lemma padic_polynomial_dist {p : â„•} [fact p.prime] (F : polynomial â„¤_[p]) (x y : â„¤_[p]) :
  â€–F.eval x - F.eval yâ€– â‰¤ â€–x - yâ€– :=
let âŸ¨z, hzâŸ© := F.eval_sub_factor x y in calc
  â€–F.eval x - F.eval yâ€– = â€–zâ€– * â€–x - yâ€– : by simp [hz]
    ... â‰¤ 1 * â€–x - yâ€– : mul_le_mul_of_nonneg_right (padic_int.norm_le_one _) (norm_nonneg _)
    ... = â€–x - yâ€– : by simp

open filter metric

private lemma comp_tendsto_lim {p : â„•} [fact p.prime] {F : polynomial â„¤_[p]}
  (ncs : cau_seq â„¤_[p] norm) :
  tendsto (Î» i, F.eval (ncs i)) at_top (ğ“ (F.eval ncs.lim)) :=
F.continuous_at.tendsto.comp ncs.tendsto_limit

section
parameters {p : â„•} [fact p.prime] {ncs : cau_seq â„¤_[p] norm} {F : polynomial â„¤_[p]} {a : â„¤_[p]}
           (ncs_der_val : âˆ€ n, â€–F.derivative.eval (ncs n)â€– = â€–F.derivative.eval aâ€–)
include ncs_der_val

private lemma ncs_tendsto_const :
  tendsto (Î» i, â€–F.derivative.eval (ncs i)â€–) at_top (ğ“ â€–F.derivative.eval aâ€–) :=
by convert tendsto_const_nhds; ext; rw ncs_der_val

private lemma ncs_tendsto_lim :
  tendsto (Î» i, â€–F.derivative.eval (ncs i)â€–) at_top (ğ“ (â€–F.derivative.eval ncs.limâ€–)) :=
tendsto.comp (continuous_iff_continuous_at.1 continuous_norm _) (comp_tendsto_lim _)

private lemma norm_deriv_eq : â€–F.derivative.eval ncs.limâ€– = â€–F.derivative.eval aâ€– :=
tendsto_nhds_unique ncs_tendsto_lim ncs_tendsto_const

end

section
parameters {p : â„•} [fact p.prime] {ncs : cau_seq â„¤_[p] norm} {F : polynomial â„¤_[p]}
           (hnorm : tendsto (Î» i, â€–F.eval (ncs i)â€–) at_top (ğ“ 0))
include hnorm

private lemma tendsto_zero_of_norm_tendsto_zero : tendsto (Î» i, F.eval (ncs i)) at_top (ğ“ 0) :=
tendsto_iff_norm_tendsto_zero.2 (by simpa using hnorm)

lemma limit_zero_of_norm_tendsto_zero : F.eval ncs.lim = 0 :=
tendsto_nhds_unique (comp_tendsto_lim _) tendsto_zero_of_norm_tendsto_zero

end

section hensel
open nat

parameters {p : â„•} [fact p.prime] {F : polynomial â„¤_[p]} {a : â„¤_[p]}
           (hnorm : â€–F.eval aâ€– < â€–F.derivative.eval aâ€–^2) (hnsol : F.eval a â‰  0)
include hnorm

/-- `T` is an auxiliary value that is used to control the behavior of the polynomial `F`. -/
private def T : â„ := â€–(F.eval a / (F.derivative.eval a)^2 : â„š_[p])â€–

private lemma deriv_sq_norm_pos : 0 < â€–F.derivative.eval aâ€– ^ 2 :=
lt_of_le_of_lt (norm_nonneg _) hnorm

private lemma deriv_sq_norm_ne_zero : â€–F.derivative.eval aâ€–^2 â‰  0 := ne_of_gt deriv_sq_norm_pos

private lemma deriv_norm_ne_zero : â€–F.derivative.eval aâ€– â‰  0 :=
Î» h, deriv_sq_norm_ne_zero (by simp [*, sq])

private lemma deriv_norm_pos : 0 < â€–F.derivative.eval aâ€– :=
lt_of_le_of_ne (norm_nonneg _) (ne.symm deriv_norm_ne_zero)

private lemma deriv_ne_zero : F.derivative.eval a â‰  0 := mt norm_eq_zero.2 deriv_norm_ne_zero

private lemma T_def : T = â€–F.eval aâ€– / â€–F.derivative.eval aâ€–^2 :=
by simp [T, â† padic_int.norm_def]

private lemma T_lt_one : T < 1 :=
let h := (div_lt_one deriv_sq_norm_pos).2 hnorm in
by rw T_def; apply h

private lemma T_nonneg : 0 â‰¤ T := norm_nonneg _

private lemma T_pow_nonneg (n : â„•) : 0 â‰¤ T ^ n := pow_nonneg T_nonneg _

private lemma T_pow {n : â„•} (hn : n â‰  0) : T ^ n < 1 := pow_lt_one T_nonneg T_lt_one hn

private lemma T_pow' (n : â„•) : T ^ (2 ^ n) < 1 := T_pow (pow_ne_zero _ two_ne_zero)

/-- We will construct a sequence of elements of â„¤_p satisfying successive values of `ih`. -/
private def ih (n : â„•) (z : â„¤_[p]) : Prop :=
â€–F.derivative.eval zâ€– = â€–F.derivative.eval aâ€– âˆ§ â€–F.eval zâ€– â‰¤ â€–F.derivative.eval aâ€–^2 * T ^ (2^n)

private lemma ih_0 : ih 0 a :=
âŸ¨ rfl, by simp [T_def, mul_div_cancel' _ (ne_of_gt (deriv_sq_norm_pos hnorm))] âŸ©

private lemma calc_norm_le_one {n : â„•} {z : â„¤_[p]} (hz : ih n z) :
         â€–(â†‘(F.eval z) : â„š_[p]) / â†‘(F.derivative.eval z)â€– â‰¤ 1 :=
calc â€–(â†‘(F.eval z) : â„š_[p]) / â†‘(F.derivative.eval z)â€–
    = â€–(â†‘(F.eval z) : â„š_[p])â€– / â€–(â†‘(F.derivative.eval z) : â„š_[p])â€– : norm_div _ _
... = â€–F.eval zâ€– / â€–F.derivative.eval aâ€– : by simp [hz.1]
... â‰¤ â€–F.derivative.eval aâ€–^2 * T^(2^n) / â€–F.derivative.eval aâ€– :
  (div_le_div_right deriv_norm_pos).2 hz.2
... = â€–F.derivative.eval aâ€– * T^(2^n) : div_sq_cancel _ _
... â‰¤ 1 : mul_le_one (padic_int.norm_le_one _) (T_pow_nonneg _) (le_of_lt (T_pow' _))

private lemma calc_deriv_dist {z z' z1 : â„¤_[p]} (hz' : z' = z - z1)
  (hz1 : â€–z1â€– = â€–F.eval zâ€– / â€–F.derivative.eval aâ€–) {n} (hz : ih n z) :
  â€–F.derivative.eval z' - F.derivative.eval zâ€– < â€–F.derivative.eval aâ€– :=
calc
  â€–F.derivative.eval z' - F.derivative.eval zâ€–
    â‰¤ â€–z' - zâ€– : padic_polynomial_dist _ _ _
... = â€–z1â€– : by simp only [sub_eq_add_neg, add_assoc, hz', add_add_neg_cancel'_right, norm_neg]
... = â€–F.eval zâ€– / â€–F.derivative.eval aâ€– : hz1
... â‰¤ â€–F.derivative.eval aâ€–^2 * T^(2^n) / â€–F.derivative.eval aâ€– :
  (div_le_div_right deriv_norm_pos).2 hz.2
... = â€–F.derivative.eval aâ€– * T^(2^n) : div_sq_cancel _ _
... < â€–F.derivative.eval aâ€– :
  (mul_lt_iff_lt_one_right deriv_norm_pos).2 (T_pow' _)

private def calc_eval_z'  {z z' z1 : â„¤_[p]} (hz' : z' = z - z1) {n} (hz : ih n z)
  (h1 : â€–(â†‘(F.eval z) : â„š_[p]) / â†‘(F.derivative.eval z)â€– â‰¤ 1) (hzeq : z1 = âŸ¨_, h1âŸ©) :
  {q : â„¤_[p] // F.eval z' = q * z1^2} :=
begin
  have hdzne : F.derivative.eval z â‰  0 :=
    mt norm_eq_zero.2 (by rw hz.1; apply deriv_norm_ne_zero; assumption),
  have hdzne' : (â†‘(F.derivative.eval z) : â„š_[p]) â‰  0 := Î» h, hdzne (subtype.ext_iff_val.2 h),
  obtain âŸ¨q, hqâŸ© := F.binom_expansion z (-z1),
  have : â€–(â†‘(F.derivative.eval z) * (â†‘(F.eval z) / â†‘(F.derivative.eval z)) : â„š_[p])â€– â‰¤ 1,
  { rw padic_norm_e.mul, exact mul_le_one (padic_int.norm_le_one _) (norm_nonneg _) h1 },
  have : F.derivative.eval z * (-z1) = -F.eval z,
  { calc F.derivative.eval z * (-z1)
        = (F.derivative.eval z) * -âŸ¨â†‘(F.eval z) / â†‘(F.derivative.eval z), h1âŸ© : by rw [hzeq]
    ... = -((F.derivative.eval z) * âŸ¨â†‘(F.eval z) / â†‘(F.derivative.eval z), h1âŸ©) : mul_neg _ _
    ... = -(âŸ¨â†‘(F.derivative.eval z) * (â†‘(F.eval z) / â†‘(F.derivative.eval z)), thisâŸ©) :
      subtype.ext $ by simp only [padic_int.coe_neg, padic_int.coe_mul, subtype.coe_mk]
    ... = -(F.eval z) : by simp only [mul_div_cancel' _ hdzne', subtype.coe_eta] },
  exact âŸ¨q, by simpa only [sub_eq_add_neg, this, hz', add_right_neg, neg_sq, zero_add] using hqâŸ©,
end

private def calc_eval_z'_norm {z z' z1 : â„¤_[p]} {n} (hz : ih n z) {q}
  (heq : F.eval z' = q * z1^2) (h1 : â€–(â†‘(F.eval z) : â„š_[p]) / â†‘(F.derivative.eval z)â€– â‰¤ 1)
  (hzeq : z1 = âŸ¨_, h1âŸ©) : â€–F.eval z'â€– â‰¤ â€–F.derivative.eval aâ€–^2 * T^(2^(n+1)) :=
calc â€–F.eval z'â€–
    = â€–qâ€– * â€–z1â€–^2 : by simp [heq]
... â‰¤ 1 * â€–z1â€–^2 :
  mul_le_mul_of_nonneg_right (padic_int.norm_le_one _) (pow_nonneg (norm_nonneg _) _)
... = â€–F.eval zâ€–^2 / â€–F.derivative.eval aâ€–^2 :
  by simp [hzeq, hz.1, div_pow]
... â‰¤ (â€–F.derivative.eval aâ€–^2 * T^(2^n))^2 / â€–F.derivative.eval aâ€–^2 :
  (div_le_div_right deriv_sq_norm_pos).2 (pow_le_pow_of_le_left (norm_nonneg _) hz.2 _)
... = (â€–F.derivative.eval aâ€–^2)^2 * (T^(2^n))^2 / â€–F.derivative.eval aâ€–^2 : by simp only [mul_pow]
... = â€–F.derivative.eval aâ€–^2 * (T^(2^n))^2 : div_sq_cancel _ _
... = â€–F.derivative.eval aâ€–^2 * T^(2^(n + 1)) : by rw [â†pow_mul, pow_succ' 2]

set_option eqn_compiler.zeta true

/-- Given `z : â„¤_[p]` satisfying `ih n z`, construct `z' : â„¤_[p]` satisfying `ih (n+1) z'`. We need
the hypothesis `ih n z`, since otherwise `z'` is not necessarily an integer. -/
private def ih_n {n : â„•} {z : â„¤_[p]} (hz : ih n z) : {z' : â„¤_[p] // ih (n+1) z'} :=
have h1 : â€–(â†‘(F.eval z) : â„š_[p]) / â†‘(F.derivative.eval z)â€– â‰¤ 1, from calc_norm_le_one hz,
let z1 : â„¤_[p] := âŸ¨_, h1âŸ©,
    z' : â„¤_[p] := z - z1 in
âŸ¨ z',
  have hdist : â€–F.derivative.eval z' - F.derivative.eval zâ€– < â€–F.derivative.eval aâ€–,
    from calc_deriv_dist rfl (by simp [z1, hz.1]) hz,
  have hfeq : â€–F.derivative.eval z'â€– = â€–F.derivative.eval aâ€–,
    begin
      rw [sub_eq_add_neg, â† hz.1, â†norm_neg (F.derivative.eval z)] at hdist,
      have := padic_int.norm_eq_of_norm_add_lt_right hdist,
      rwa [norm_neg, hz.1] at this
    end,
  let âŸ¨q, heqâŸ© := calc_eval_z' rfl hz h1 rfl in
  have hnle : â€–F.eval z'â€– â‰¤ â€–F.derivative.eval aâ€–^2 * T^(2^(n+1)),
    from calc_eval_z'_norm hz heq h1 rfl,
  âŸ¨hfeq, hnleâŸ©âŸ©

set_option eqn_compiler.zeta false

-- why doesn't "noncomputable theory" stick here?
private noncomputable def newton_seq_aux : Î  n : â„•, {z : â„¤_[p] // ih n z}
| 0 := âŸ¨a, ih_0âŸ©
| (k+1) := ih_n (newton_seq_aux k).2

private def newton_seq (n : â„•) : â„¤_[p] := (newton_seq_aux n).1

private lemma newton_seq_deriv_norm (n : â„•) :
  â€–F.derivative.eval (newton_seq n)â€– = â€–F.derivative.eval aâ€– :=
(newton_seq_aux n).2.1

private lemma newton_seq_norm_le (n : â„•) :
  â€–F.eval (newton_seq n)â€– â‰¤ â€–F.derivative.eval aâ€–^2 * T ^ (2^n) :=
(newton_seq_aux n).2.2

private lemma newton_seq_norm_eq (n : â„•) :
  â€–newton_seq (n+1) - newton_seq nâ€– =
    â€–F.eval (newton_seq n)â€– / â€–F.derivative.eval (newton_seq n)â€– :=
by simp [newton_seq, newton_seq_aux, ih_n, sub_eq_add_neg, add_comm]

private lemma newton_seq_succ_dist (n : â„•) :
  â€–newton_seq (n+1) - newton_seq nâ€– â‰¤ â€–F.derivative.eval aâ€– * T^(2^n) :=
calc â€–newton_seq (n+1) - newton_seq nâ€–
    = â€–F.eval (newton_seq n)â€– / â€–F.derivative.eval (newton_seq n)â€– : newton_seq_norm_eq _
... = â€–F.eval (newton_seq n)â€– / â€–F.derivative.eval aâ€– : by rw newton_seq_deriv_norm
... â‰¤ â€–F.derivative.eval aâ€–^2 * T ^ (2^n) / â€–F.derivative.eval aâ€– :
  (div_le_div_right deriv_norm_pos).2 (newton_seq_norm_le _)
... = â€–F.derivative.eval aâ€– * T^(2^n) : div_sq_cancel _ _

include hnsol
private lemma T_pos : T > 0 :=
begin
  rw T_def,
  exact div_pos (norm_pos_iff.2 hnsol) (deriv_sq_norm_pos hnorm)
end

private lemma newton_seq_succ_dist_weak (n : â„•) :
  â€–newton_seq (n+2) - newton_seq (n+1)â€– < â€–F.eval aâ€– / â€–F.derivative.eval aâ€– :=
have 2 â‰¤ 2^(n+1),
  from have _, from pow_le_pow (by norm_num : 1 â‰¤ 2) (nat.le_add_left _ _ : 1 â‰¤ n + 1),
    by simpa using this,
calc â€–newton_seq (n+2) - newton_seq (n+1)â€–
    â‰¤ â€–F.derivative.eval aâ€– * T^(2^(n+1)) : newton_seq_succ_dist _
... â‰¤ â€–F.derivative.eval aâ€– * T^2 :
  mul_le_mul_of_nonneg_left (pow_le_pow_of_le_one (norm_nonneg _) (le_of_lt T_lt_one) this)
    (norm_nonneg _)
... < â€–F.derivative.eval aâ€– * T^1 :
  mul_lt_mul_of_pos_left (pow_lt_pow_of_lt_one T_pos T_lt_one (by norm_num)) deriv_norm_pos
... = â€–F.eval aâ€– / â€–F.derivative.eval aâ€– :
  begin
    rw [T, sq, pow_one, norm_div, â†mul_div_assoc, padic_norm_e.mul],
    apply mul_div_mul_left,
    apply deriv_norm_ne_zero; assumption
  end

private lemma newton_seq_dist_aux (n : â„•) :
  âˆ€ k : â„•, â€–newton_seq (n + k) - newton_seq nâ€– â‰¤ â€–F.derivative.eval aâ€– * T^(2^n)
| 0 := by simp [T_pow_nonneg hnorm, mul_nonneg]
| (k+1) :=
  have 2^n â‰¤ 2^(n+k),
    by {apply pow_le_pow, norm_num, apply nat.le_add_right},
  calc
  â€–newton_seq (n + (k + 1)) - newton_seq nâ€–
    = â€–newton_seq ((n + k) + 1) - newton_seq nâ€– : by rw add_assoc
... = â€–(newton_seq ((n + k) + 1) - newton_seq (n+k)) + (newton_seq (n+k) - newton_seq n)â€– :
  by rw â†sub_add_sub_cancel
... â‰¤ max (â€–newton_seq ((n + k) + 1) - newton_seq (n+k)â€–) (â€–newton_seq (n+k) - newton_seq nâ€–) :
  padic_int.nonarchimedean _ _
... â‰¤ max (â€–F.derivative.eval aâ€– * T^(2^((n + k)))) (â€–F.derivative.eval aâ€– * T^(2^n)) :
  max_le_max (newton_seq_succ_dist _) (newton_seq_dist_aux _)
... = â€–F.derivative.eval aâ€– * T^(2^n) :
  max_eq_right $ mul_le_mul_of_nonneg_left
    (pow_le_pow_of_le_one (norm_nonneg _) (le_of_lt T_lt_one) this) (norm_nonneg _)

private lemma newton_seq_dist {n k : â„•} (hnk : n â‰¤ k) :
  â€–newton_seq k - newton_seq nâ€– â‰¤ â€–F.derivative.eval aâ€– * T^(2^n) :=
have hex : âˆƒ m, k = n + m, from exists_eq_add_of_le hnk,
let âŸ¨_, hex'âŸ© := hex in
by rw hex'; apply newton_seq_dist_aux; assumption

private lemma newton_seq_dist_to_a :
  âˆ€ n : â„•, 0 < n â†’ â€–newton_seq n - aâ€– = â€–F.eval aâ€– / â€–F.derivative.eval aâ€–
| 1 h := by simp [sub_eq_add_neg, add_assoc, newton_seq, newton_seq_aux, ih_n]
| (k+2) h :=
  have hlt : â€–newton_seq (k+2) - newton_seq (k+1)â€– < â€–newton_seq (k+1) - aâ€–,
    by rw newton_seq_dist_to_a (k+1) (succ_pos _); apply newton_seq_succ_dist_weak; assumption,
  have hne' : â€–newton_seq (k + 2) - newton_seq (k+1)â€– â‰  â€–newton_seq (k+1) - aâ€–, from ne_of_lt hlt,
  calc  â€–newton_seq (k + 2) - aâ€–
    = â€–(newton_seq (k + 2) - newton_seq (k+1)) + (newton_seq (k+1) - a)â€– : by rw â†sub_add_sub_cancel
... = max (â€–newton_seq (k + 2) - newton_seq (k+1)â€–) (â€–newton_seq (k+1) - aâ€–) :
  padic_int.norm_add_eq_max_of_ne hne'
... = â€–newton_seq (k+1) - aâ€– : max_eq_right_of_lt hlt
... = â€–polynomial.eval a Fâ€– / â€–polynomial.eval a (polynomial.derivative F)â€– :
  newton_seq_dist_to_a (k+1) (succ_pos _)

private lemma bound' : tendsto (Î» n : â„•, â€–F.derivative.eval aâ€– * T^(2^n)) at_top (ğ“ 0) :=
begin
  rw â†mul_zero (â€–F.derivative.eval aâ€–),
  exact tendsto_const_nhds.mul
                    (tendsto.comp
                      (tendsto_pow_at_top_nhds_0_of_lt_1 (norm_nonneg _) (T_lt_one hnorm))
                      (nat.tendsto_pow_at_top_at_top_of_one_lt (by norm_num)))
end

private lemma bound : âˆ€ {Îµ}, Îµ > 0 â†’ âˆƒ N : â„•, âˆ€ {n}, n â‰¥ N â†’ â€–F.derivative.eval aâ€– * T^(2^n) < Îµ :=
begin
  have := bound' hnorm hnsol,
  simp [tendsto, nhds] at this,
  intros Îµ hÎµ,
  cases this (ball 0 Îµ) (mem_ball_self hÎµ) (is_open_ball) with N hN,
  existsi N, intros n hn,
  simpa [abs_of_nonneg (T_nonneg _)] using hN _ hn
end

private lemma bound'_sq : tendsto (Î» n : â„•, â€–F.derivative.eval aâ€–^2 * T^(2^n)) at_top (ğ“ 0) :=
begin
  rw [â†mul_zero (â€–F.derivative.eval aâ€–), sq],
  simp only [mul_assoc],
  apply tendsto.mul,
  { apply tendsto_const_nhds },
  { apply bound', assumption }
end

private theorem newton_seq_is_cauchy : is_cau_seq norm newton_seq :=
begin
  intros Îµ hÎµ,
  cases bound hnorm hnsol hÎµ with N hN,
  existsi N,
  intros j hj,
  apply lt_of_le_of_lt,
  { apply newton_seq_dist _ _ hj, assumption },
  { apply hN, exact le_rfl }
end

private def newton_cau_seq : cau_seq â„¤_[p] norm := âŸ¨_, newton_seq_is_cauchyâŸ©

private def soln : â„¤_[p] := newton_cau_seq.lim

private lemma soln_spec {Îµ : â„} (hÎµ : Îµ > 0) :
  âˆƒ (N : â„•), âˆ€ {i : â„•}, i â‰¥ N â†’ â€–soln - newton_cau_seq iâ€– < Îµ :=
setoid.symm (cau_seq.equiv_lim newton_cau_seq) _ hÎµ

private lemma soln_deriv_norm : â€–F.derivative.eval solnâ€– = â€–F.derivative.eval aâ€– :=
norm_deriv_eq newton_seq_deriv_norm

private lemma newton_seq_norm_tendsto_zero :
  tendsto (Î» i, â€–F.eval (newton_cau_seq i)â€–) at_top (ğ“ 0) :=
squeeze_zero (Î» _, norm_nonneg _) newton_seq_norm_le bound'_sq

private lemma newton_seq_dist_tendsto :
  tendsto (Î» n, â€–newton_cau_seq n - aâ€–) at_top (ğ“ (â€–F.eval aâ€– / â€–F.derivative.eval aâ€–)) :=
tendsto_const_nhds.congr' $ eventually_at_top.2 âŸ¨1, Î» _ hx, (newton_seq_dist_to_a _ hx).symmâŸ©

private lemma newton_seq_dist_tendsto' :
  tendsto (Î» n, â€–newton_cau_seq n - aâ€–) at_top (ğ“ â€–soln - aâ€–) :=
(continuous_norm.tendsto _).comp (newton_cau_seq.tendsto_limit.sub tendsto_const_nhds)

private lemma soln_dist_to_a : â€–soln - aâ€– = â€–F.eval aâ€– / â€–F.derivative.eval aâ€– :=
tendsto_nhds_unique newton_seq_dist_tendsto' newton_seq_dist_tendsto

private lemma soln_dist_to_a_lt_deriv : â€–soln - aâ€– < â€–F.derivative.eval aâ€– :=
begin
  rw [soln_dist_to_a, div_lt_iff],
  { rwa sq at hnorm },
  { apply deriv_norm_pos, assumption }
end

private lemma eval_soln : F.eval soln = 0 :=
limit_zero_of_norm_tendsto_zero newton_seq_norm_tendsto_zero

private lemma soln_unique (z : â„¤_[p]) (hev : F.eval z = 0)
  (hnlt : â€–z - aâ€– < â€–F.derivative.eval aâ€–) :
  z = soln :=
have soln_dist : â€–z - solnâ€– < â€–F.derivative.eval aâ€–, from calc
  â€–z - solnâ€– = â€–(z - a) + (a - soln)â€– : by rw sub_add_sub_cancel
        ... â‰¤ max (â€–z - aâ€–) (â€–a - solnâ€–) : padic_int.nonarchimedean _ _
        ... < â€–F.derivative.eval aâ€– : max_lt hnlt (norm_sub_rev soln a â–¸ soln_dist_to_a_lt_deriv),
let h := z - soln,
    âŸ¨q, hqâŸ© := F.binom_expansion soln h in
have (F.derivative.eval soln + q * h) * h = 0, from eq.symm (calc
  0 = F.eval (soln + h) : by simp [hev, h]
... = F.derivative.eval soln * h + q * h^2 : by rw [hq, eval_soln, zero_add]
... = (F.derivative.eval soln + q * h) * h : by rw [sq, right_distrib, mul_assoc]),
have h = 0, from by_contradiction $ Î» hne,
  have F.derivative.eval soln + q * h = 0,
    from (eq_zero_or_eq_zero_of_mul_eq_zero this).resolve_right hne,
  have F.derivative.eval soln = (-q) * h, by simpa using eq_neg_of_add_eq_zero_left this,
  lt_irrefl â€–F.derivative.eval solnâ€– (calc
  â€–F.derivative.eval solnâ€– = â€–(-q) * hâ€– : by rw this
... â‰¤ 1 * â€–hâ€– :
  by { rw padic_int.norm_mul,
       exact mul_le_mul_of_nonneg_right (padic_int.norm_le_one _) (norm_nonneg _) }
... = â€–z - solnâ€– : by simp [h]
... < â€–F.derivative.eval solnâ€– : by rw soln_deriv_norm; apply soln_dist),
eq_of_sub_eq_zero (by rw â†this; refl)

end hensel

variables {p : â„•} [fact p.prime] {F : polynomial â„¤_[p]} {a : â„¤_[p]}

private lemma a_soln_is_unique (ha : F.eval a = 0) (z' : â„¤_[p]) (hz' : F.eval z' = 0)
  (hnormz' : â€–z' - aâ€– < â€–F.derivative.eval aâ€–) : z' = a :=
let h := z' - a,
    âŸ¨q, hqâŸ© := F.binom_expansion a h in
have (F.derivative.eval a + q * h) * h = 0, from eq.symm (calc
  0 = F.eval (a + h) : show 0 = F.eval (a + (z' - a)), by rw add_comm; simp [hz']
... = F.derivative.eval a * h + q * h^2 : by rw [hq, ha, zero_add]
... = (F.derivative.eval a + q * h) * h : by rw [sq, right_distrib, mul_assoc]),
have h = 0, from by_contradiction $ Î» hne,
  have F.derivative.eval a + q * h = 0,
    from (eq_zero_or_eq_zero_of_mul_eq_zero this).resolve_right hne,
  have F.derivative.eval a = (-q) * h, by simpa using eq_neg_of_add_eq_zero_left this,
  lt_irrefl â€–F.derivative.eval aâ€– (calc
    â€–F.derivative.eval aâ€– = â€–qâ€–*â€–hâ€– : by simp [this]
    ... â‰¤ 1*â€–hâ€– : mul_le_mul_of_nonneg_right (padic_int.norm_le_one _) (norm_nonneg _)
    ... < â€–F.derivative.eval aâ€– : by simpa [h]),
eq_of_sub_eq_zero (by rw â†this; refl)

variable (hnorm : â€–F.eval aâ€– < â€–F.derivative.eval aâ€–^2)
include hnorm

private lemma a_is_soln (ha : F.eval a = 0) :
  F.eval a = 0 âˆ§ â€–a - aâ€– < â€–F.derivative.eval aâ€– âˆ§ â€–F.derivative.eval aâ€– = â€–F.derivative.eval aâ€– âˆ§
  âˆ€ z', F.eval z' = 0 â†’ â€–z' - aâ€– < â€–F.derivative.eval aâ€– â†’ z' = a :=
âŸ¨ha, by simp [deriv_ne_zero hnorm], rfl, a_soln_is_unique haâŸ©

lemma hensels_lemma : âˆƒ z : â„¤_[p], F.eval z = 0 âˆ§ â€–z - aâ€– < â€–F.derivative.eval aâ€– âˆ§
  â€–F.derivative.eval zâ€– = â€–F.derivative.eval aâ€– âˆ§
  âˆ€ z', F.eval z' = 0 â†’ â€–z' - aâ€– < â€–F.derivative.eval aâ€– â†’ z' = z :=
if ha : F.eval a = 0 then âŸ¨a, a_is_soln hnorm haâŸ© else
by refine âŸ¨soln _ _, eval_soln _ _, soln_dist_to_a_lt_deriv _ _, soln_deriv_norm _ _,
  soln_unique _ _âŸ©; assumption
