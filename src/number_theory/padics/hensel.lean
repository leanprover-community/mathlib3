/-
Copyright (c) 2018 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis
-/
import analysis.specific_limits.basic
import data.polynomial.identities
import number_theory.padics.padic_integers
import topology.algebra.polynomial
import topology.metric_space.cau_seq_filter

/-!
# Hensel's lemma on ‚Ñ§_p

This file proves Hensel's lemma on ‚Ñ§_p, roughly following Keith Conrad's writeup:
<http://www.math.uconn.edu/~kconrad/blurbs/gradnumthy/hensel.pdf>

Hensel's lemma gives a simple condition for the existence of a root of a polynomial.

The proof and motivation are described in the paper
[R. Y. Lewis, *A formal proof of Hensel's lemma over the p-adic integers*][lewis2019].

## References

* <http://www.math.uconn.edu/~kconrad/blurbs/gradnumthy/hensel.pdf>
* [R. Y. Lewis, *A formal proof of Hensel's lemma over the p-adic integers*][lewis2019]
* <https://en.wikipedia.org/wiki/Hensel%27s_lemma>

## Tags

p-adic, p adic, padic, p-adic integer
-/

noncomputable theory

open_locale classical topological_space

-- We begin with some general lemmas that are used below in the computation.

lemma padic_polynomial_dist {p : ‚Ñï} [fact p.prime] (F : polynomial ‚Ñ§_[p]) (x y : ‚Ñ§_[p]) :
  ‚à•F.eval x - F.eval y‚à• ‚â§ ‚à•x - y‚à• :=
let ‚ü®z, hz‚ü© := F.eval_sub_factor x y in calc
  ‚à•F.eval x - F.eval y‚à• = ‚à•z‚à• * ‚à•x - y‚à• : by simp [hz]
    ... ‚â§ 1 * ‚à•x - y‚à• : mul_le_mul_of_nonneg_right (padic_int.norm_le_one _) (norm_nonneg _)
    ... = ‚à•x - y‚à• : by simp

open filter metric

private lemma comp_tendsto_lim {p : ‚Ñï} [fact p.prime] {F : polynomial ‚Ñ§_[p]}
  (ncs : cau_seq ‚Ñ§_[p] norm) :
  tendsto (Œª i, F.eval (ncs i)) at_top (ùìù (F.eval ncs.lim)) :=
F.continuous_at.tendsto.comp ncs.tendsto_limit

section
parameters {p : ‚Ñï} [fact p.prime] {ncs : cau_seq ‚Ñ§_[p] norm} {F : polynomial ‚Ñ§_[p]} {a : ‚Ñ§_[p]}
           (ncs_der_val : ‚àÄ n, ‚à•F.derivative.eval (ncs n)‚à• = ‚à•F.derivative.eval a‚à•)
include ncs_der_val

private lemma ncs_tendsto_const :
  tendsto (Œª i, ‚à•F.derivative.eval (ncs i)‚à•) at_top (ùìù ‚à•F.derivative.eval a‚à•) :=
by convert tendsto_const_nhds; ext; rw ncs_der_val

private lemma ncs_tendsto_lim :
  tendsto (Œª i, ‚à•F.derivative.eval (ncs i)‚à•) at_top (ùìù (‚à•F.derivative.eval ncs.lim‚à•)) :=
tendsto.comp (continuous_iff_continuous_at.1 continuous_norm _) (comp_tendsto_lim _)

private lemma norm_deriv_eq : ‚à•F.derivative.eval ncs.lim‚à• = ‚à•F.derivative.eval a‚à• :=
tendsto_nhds_unique ncs_tendsto_lim ncs_tendsto_const

end

section
parameters {p : ‚Ñï} [fact p.prime] {ncs : cau_seq ‚Ñ§_[p] norm} {F : polynomial ‚Ñ§_[p]}
           (hnorm : tendsto (Œª i, ‚à•F.eval (ncs i)‚à•) at_top (ùìù 0))
include hnorm

private lemma tendsto_zero_of_norm_tendsto_zero : tendsto (Œª i, F.eval (ncs i)) at_top (ùìù 0) :=
tendsto_iff_norm_tendsto_zero.2 (by simpa using hnorm)

lemma limit_zero_of_norm_tendsto_zero : F.eval ncs.lim = 0 :=
tendsto_nhds_unique (comp_tendsto_lim _) tendsto_zero_of_norm_tendsto_zero

end

section hensel
open nat

parameters {p : ‚Ñï} [fact p.prime] {F : polynomial ‚Ñ§_[p]} {a : ‚Ñ§_[p]}
           (hnorm : ‚à•F.eval a‚à• < ‚à•F.derivative.eval a‚à•^2) (hnsol : F.eval a ‚â† 0)
include hnorm

/-- `T` is an auxiliary value that is used to control the behavior of the polynomial `F`. -/
private def T : ‚Ñù := ‚à•(F.eval a / (F.derivative.eval a)^2 : ‚Ñö_[p])‚à•

private lemma deriv_sq_norm_pos : 0 < ‚à•F.derivative.eval a‚à• ^ 2 :=
lt_of_le_of_lt (norm_nonneg _) hnorm

private lemma deriv_sq_norm_ne_zero : ‚à•F.derivative.eval a‚à•^2 ‚â† 0 := ne_of_gt deriv_sq_norm_pos

private lemma deriv_norm_ne_zero : ‚à•F.derivative.eval a‚à• ‚â† 0 :=
Œª h, deriv_sq_norm_ne_zero (by simp [*, sq])

private lemma deriv_norm_pos : 0 < ‚à•F.derivative.eval a‚à• :=
lt_of_le_of_ne (norm_nonneg _) (ne.symm deriv_norm_ne_zero)

private lemma deriv_ne_zero : F.derivative.eval a ‚â† 0 := mt norm_eq_zero.2 deriv_norm_ne_zero

private lemma T_def : T = ‚à•F.eval a‚à• / ‚à•F.derivative.eval a‚à•^2 :=
calc T = ‚à•F.eval a‚à• / ‚à•((F.derivative.eval a)^2 : ‚Ñö_[p])‚à• : norm_div _ _
   ... = ‚à•F.eval a‚à• / ‚à•(F.derivative.eval a)^2‚à• : by simp [norm, padic_int.norm_def]
   ... = ‚à•F.eval a‚à• / ‚à•(F.derivative.eval a)‚à•^2 : by simp

private lemma T_lt_one : T < 1 :=
let h := (div_lt_one deriv_sq_norm_pos).2 hnorm in
by rw T_def; apply h

private lemma T_pow {n : ‚Ñï} (hn : n > 0) : T ^ n < 1 :=
have T ^ n ‚â§ T ^ 1,
from pow_le_pow_of_le_one (norm_nonneg _) (le_of_lt T_lt_one) (succ_le_of_lt hn),
lt_of_le_of_lt (by simpa) T_lt_one

private lemma T_pow' (n : ‚Ñï) : T ^ (2 ^ n) < 1 := (T_pow (pow_pos (by norm_num) _))

private lemma T_pow_nonneg (n : ‚Ñï) : 0 ‚â§ T ^ n := pow_nonneg (norm_nonneg _) _

/-- We will construct a sequence of elements of ‚Ñ§_p satisfying successive values of `ih`. -/
private def ih (n : ‚Ñï) (z : ‚Ñ§_[p]) : Prop :=
‚à•F.derivative.eval z‚à• = ‚à•F.derivative.eval a‚à• ‚àß ‚à•F.eval z‚à• ‚â§ ‚à•F.derivative.eval a‚à•^2 * T ^ (2^n)

private lemma ih_0 : ih 0 a :=
‚ü® rfl, by simp [T_def, mul_div_cancel' _ (ne_of_gt (deriv_sq_norm_pos hnorm))] ‚ü©

private lemma calc_norm_le_one {n : ‚Ñï} {z : ‚Ñ§_[p]} (hz : ih n z) :
         ‚à•(‚Üë(F.eval z) : ‚Ñö_[p]) / ‚Üë(F.derivative.eval z)‚à• ‚â§ 1 :=
calc ‚à•(‚Üë(F.eval z) : ‚Ñö_[p]) / ‚Üë(F.derivative.eval z)‚à•
    = ‚à•(‚Üë(F.eval z) : ‚Ñö_[p])‚à• / ‚à•(‚Üë(F.derivative.eval z) : ‚Ñö_[p])‚à• : norm_div _ _
... = ‚à•F.eval z‚à• / ‚à•F.derivative.eval a‚à• : by simp [hz.1]
... ‚â§ ‚à•F.derivative.eval a‚à•^2 * T^(2^n) / ‚à•F.derivative.eval a‚à• :
  (div_le_div_right deriv_norm_pos).2 hz.2
... = ‚à•F.derivative.eval a‚à• * T^(2^n) : div_sq_cancel _ _
... ‚â§ 1 : mul_le_one (padic_int.norm_le_one _) (T_pow_nonneg _) (le_of_lt (T_pow' _))

private lemma calc_deriv_dist {z z' z1 : ‚Ñ§_[p]} (hz' : z' = z - z1)
  (hz1 : ‚à•z1‚à• = ‚à•F.eval z‚à• / ‚à•F.derivative.eval a‚à•) {n} (hz : ih n z) :
  ‚à•F.derivative.eval z' - F.derivative.eval z‚à• < ‚à•F.derivative.eval a‚à• :=
calc
  ‚à•F.derivative.eval z' - F.derivative.eval z‚à•
    ‚â§ ‚à•z' - z‚à• : padic_polynomial_dist _ _ _
... = ‚à•z1‚à• : by simp only [sub_eq_add_neg, add_assoc, hz', add_add_neg_cancel'_right, norm_neg]
... = ‚à•F.eval z‚à• / ‚à•F.derivative.eval a‚à• : hz1
... ‚â§ ‚à•F.derivative.eval a‚à•^2 * T^(2^n) / ‚à•F.derivative.eval a‚à• :
  (div_le_div_right deriv_norm_pos).2 hz.2
... = ‚à•F.derivative.eval a‚à• * T^(2^n) : div_sq_cancel _ _
... < ‚à•F.derivative.eval a‚à• :
  (mul_lt_iff_lt_one_right deriv_norm_pos).2 (T_pow (pow_pos (by norm_num) _))

private def calc_eval_z'  {z z' z1 : ‚Ñ§_[p]} (hz' : z' = z - z1) {n} (hz : ih n z)
  (h1 : ‚à•(‚Üë(F.eval z) : ‚Ñö_[p]) / ‚Üë(F.derivative.eval z)‚à• ‚â§ 1) (hzeq : z1 = ‚ü®_, h1‚ü©) :
  {q : ‚Ñ§_[p] // F.eval z' = q * z1^2} :=
have hdzne' : (‚Üë(F.derivative.eval z) : ‚Ñö_[p]) ‚â† 0, from
  have hdzne : F.derivative.eval z ‚â† 0,
    from mt norm_eq_zero.2 (by rw hz.1; apply deriv_norm_ne_zero; assumption),
  Œª h, hdzne $ subtype.ext_iff_val.2 h,
let ‚ü®q, hq‚ü© := F.binom_expansion z (-z1) in
have ‚à•(‚Üë(F.derivative.eval z) * (‚Üë(F.eval z) / ‚Üë(F.derivative.eval z)) : ‚Ñö_[p])‚à• ‚â§ 1,
  by { rw padic_norm_e.mul, exact mul_le_one (padic_int.norm_le_one _) (norm_nonneg _) h1 },
have F.derivative.eval z * (-z1) = -F.eval z, from calc
  F.derivative.eval z * (-z1)
    = (F.derivative.eval z) * -‚ü®‚Üë(F.eval z) / ‚Üë(F.derivative.eval z), h1‚ü© : by rw [hzeq]
... = -((F.derivative.eval z) * ‚ü®‚Üë(F.eval z) / ‚Üë(F.derivative.eval z), h1‚ü©) :
  by simp [subtype.ext_iff_val]
... = -(‚ü®‚Üë(F.derivative.eval z) * (‚Üë(F.eval z) / ‚Üë(F.derivative.eval z)), this‚ü©) :
  subtype.ext $ by simp
... = -(F.eval z) : by simp [mul_div_cancel' _ hdzne'],
have heq : F.eval z' = q * z1^2, by simpa [sub_eq_add_neg, this, hz'] using hq,
‚ü®q, heq‚ü©

private def calc_eval_z'_norm {z z' z1 : ‚Ñ§_[p]} {n} (hz : ih n z) {q}
  (heq : F.eval z' = q * z1^2) (h1 : ‚à•(‚Üë(F.eval z) : ‚Ñö_[p]) / ‚Üë(F.derivative.eval z)‚à• ‚â§ 1)
  (hzeq : z1 = ‚ü®_, h1‚ü©) : ‚à•F.eval z'‚à• ‚â§ ‚à•F.derivative.eval a‚à•^2 * T^(2^(n+1)) :=
calc ‚à•F.eval z'‚à•
    = ‚à•q‚à• * ‚à•z1‚à•^2 : by simp [heq]
... ‚â§ 1 * ‚à•z1‚à•^2 :
  mul_le_mul_of_nonneg_right (padic_int.norm_le_one _) (pow_nonneg (norm_nonneg _) _)
... = ‚à•F.eval z‚à•^2 / ‚à•F.derivative.eval a‚à•^2 :
  by simp [hzeq, hz.1, div_pow]
... ‚â§ (‚à•F.derivative.eval a‚à•^2 * T^(2^n))^2 / ‚à•F.derivative.eval a‚à•^2 :
  (div_le_div_right deriv_sq_norm_pos).2 (pow_le_pow_of_le_left (norm_nonneg _) hz.2 _)
... = (‚à•F.derivative.eval a‚à•^2)^2 * (T^(2^n))^2 / ‚à•F.derivative.eval a‚à•^2 : by simp only [mul_pow]
... = ‚à•F.derivative.eval a‚à•^2 * (T^(2^n))^2 : div_sq_cancel _ _
... = ‚à•F.derivative.eval a‚à•^2 * T^(2^(n + 1)) : by rw [‚Üêpow_mul, pow_succ' 2]

set_option eqn_compiler.zeta true

/-- Given `z : ‚Ñ§_[p]` satisfying `ih n z`, construct `z' : ‚Ñ§_[p]` satisfying `ih (n+1) z'`. We need
the hypothesis `ih n z`, since otherwise `z'` is not necessarily an integer. -/
private def ih_n {n : ‚Ñï} {z : ‚Ñ§_[p]} (hz : ih n z) : {z' : ‚Ñ§_[p] // ih (n+1) z'} :=
have h1 : ‚à•(‚Üë(F.eval z) : ‚Ñö_[p]) / ‚Üë(F.derivative.eval z)‚à• ‚â§ 1, from calc_norm_le_one hz,
let z1 : ‚Ñ§_[p] := ‚ü®_, h1‚ü©,
    z' : ‚Ñ§_[p] := z - z1 in
‚ü® z',
  have hdist : ‚à•F.derivative.eval z' - F.derivative.eval z‚à• < ‚à•F.derivative.eval a‚à•,
    from calc_deriv_dist rfl (by simp [z1, hz.1]) hz,
  have hfeq : ‚à•F.derivative.eval z'‚à• = ‚à•F.derivative.eval a‚à•,
    begin
      rw [sub_eq_add_neg, ‚Üê hz.1, ‚Üênorm_neg (F.derivative.eval z)] at hdist,
      have := padic_int.norm_eq_of_norm_add_lt_right hdist,
      rwa [norm_neg, hz.1] at this
    end,
  let ‚ü®q, heq‚ü© := calc_eval_z' rfl hz h1 rfl in
  have hnle : ‚à•F.eval z'‚à• ‚â§ ‚à•F.derivative.eval a‚à•^2 * T^(2^(n+1)),
    from calc_eval_z'_norm hz heq h1 rfl,
  ‚ü®hfeq, hnle‚ü©‚ü©

set_option eqn_compiler.zeta false

-- why doesn't "noncomputable theory" stick here?
private noncomputable def newton_seq_aux : Œ† n : ‚Ñï, {z : ‚Ñ§_[p] // ih n z}
| 0 := ‚ü®a, ih_0‚ü©
| (k+1) := ih_n (newton_seq_aux k).2

private def newton_seq (n : ‚Ñï) : ‚Ñ§_[p] := (newton_seq_aux n).1

private lemma newton_seq_deriv_norm (n : ‚Ñï) :
  ‚à•F.derivative.eval (newton_seq n)‚à• = ‚à•F.derivative.eval a‚à• :=
(newton_seq_aux n).2.1

private lemma newton_seq_norm_le (n : ‚Ñï) :
  ‚à•F.eval (newton_seq n)‚à• ‚â§ ‚à•F.derivative.eval a‚à•^2 * T ^ (2^n) :=
(newton_seq_aux n).2.2

private lemma newton_seq_norm_eq (n : ‚Ñï) :
  ‚à•newton_seq (n+1) - newton_seq n‚à• =
    ‚à•F.eval (newton_seq n)‚à• / ‚à•F.derivative.eval (newton_seq n)‚à• :=
by simp [newton_seq, newton_seq_aux, ih_n, sub_eq_add_neg, add_comm]

private lemma newton_seq_succ_dist (n : ‚Ñï) :
  ‚à•newton_seq (n+1) - newton_seq n‚à• ‚â§ ‚à•F.derivative.eval a‚à• * T^(2^n) :=
calc ‚à•newton_seq (n+1) - newton_seq n‚à•
    = ‚à•F.eval (newton_seq n)‚à• / ‚à•F.derivative.eval (newton_seq n)‚à• : newton_seq_norm_eq _
... = ‚à•F.eval (newton_seq n)‚à• / ‚à•F.derivative.eval a‚à• : by rw newton_seq_deriv_norm
... ‚â§ ‚à•F.derivative.eval a‚à•^2 * T ^ (2^n) / ‚à•F.derivative.eval a‚à• :
  (div_le_div_right deriv_norm_pos).2 (newton_seq_norm_le _)
... = ‚à•F.derivative.eval a‚à• * T^(2^n) : div_sq_cancel _ _

include hnsol
private lemma T_pos : T > 0 :=
begin
  rw T_def,
  exact div_pos (norm_pos_iff.2 hnsol) (deriv_sq_norm_pos hnorm)
end

private lemma newton_seq_succ_dist_weak (n : ‚Ñï) :
  ‚à•newton_seq (n+2) - newton_seq (n+1)‚à• < ‚à•F.eval a‚à• / ‚à•F.derivative.eval a‚à• :=
have 2 ‚â§ 2^(n+1),
  from have _, from pow_le_pow (by norm_num : 1 ‚â§ 2) (nat.le_add_left _ _ : 1 ‚â§ n + 1),
    by simpa using this,
calc ‚à•newton_seq (n+2) - newton_seq (n+1)‚à•
    ‚â§ ‚à•F.derivative.eval a‚à• * T^(2^(n+1)) : newton_seq_succ_dist _
... ‚â§ ‚à•F.derivative.eval a‚à• * T^2 :
  mul_le_mul_of_nonneg_left (pow_le_pow_of_le_one (norm_nonneg _) (le_of_lt T_lt_one) this)
    (norm_nonneg _)
... < ‚à•F.derivative.eval a‚à• * T^1 :
  mul_lt_mul_of_pos_left (pow_lt_pow_of_lt_one T_pos T_lt_one (by norm_num)) deriv_norm_pos
... = ‚à•F.eval a‚à• / ‚à•F.derivative.eval a‚à• :
  begin
    rw [T, sq, pow_one, norm_div, ‚Üêmul_div_assoc, padic_norm_e.mul],
    apply mul_div_mul_left,
    apply deriv_norm_ne_zero; assumption
  end

private lemma newton_seq_dist_aux (n : ‚Ñï) :
  ‚àÄ k : ‚Ñï, ‚à•newton_seq (n + k) - newton_seq n‚à• ‚â§ ‚à•F.derivative.eval a‚à• * T^(2^n)
| 0 := by simp [T_pow_nonneg hnorm, mul_nonneg]
| (k+1) :=
  have 2^n ‚â§ 2^(n+k),
    by {apply pow_le_pow, norm_num, apply nat.le_add_right},
  calc
  ‚à•newton_seq (n + (k + 1)) - newton_seq n‚à•
    = ‚à•newton_seq ((n + k) + 1) - newton_seq n‚à• : by rw add_assoc
... = ‚à•(newton_seq ((n + k) + 1) - newton_seq (n+k)) + (newton_seq (n+k) - newton_seq n)‚à• :
  by rw ‚Üêsub_add_sub_cancel
... ‚â§ max (‚à•newton_seq ((n + k) + 1) - newton_seq (n+k)‚à•) (‚à•newton_seq (n+k) - newton_seq n‚à•) :
  padic_int.nonarchimedean _ _
... ‚â§ max (‚à•F.derivative.eval a‚à• * T^(2^((n + k)))) (‚à•F.derivative.eval a‚à• * T^(2^n)) :
  max_le_max (newton_seq_succ_dist _) (newton_seq_dist_aux _)
... = ‚à•F.derivative.eval a‚à• * T^(2^n) :
  max_eq_right $ mul_le_mul_of_nonneg_left
    (pow_le_pow_of_le_one (norm_nonneg _) (le_of_lt T_lt_one) this) (norm_nonneg _)

private lemma newton_seq_dist {n k : ‚Ñï} (hnk : n ‚â§ k) :
  ‚à•newton_seq k - newton_seq n‚à• ‚â§ ‚à•F.derivative.eval a‚à• * T^(2^n) :=
have hex : ‚àÉ m, k = n + m, from exists_eq_add_of_le hnk,
let ‚ü®_, hex'‚ü© := hex in
by rw hex'; apply newton_seq_dist_aux; assumption

private lemma newton_seq_dist_to_a :
  ‚àÄ n : ‚Ñï, 0 < n ‚Üí ‚à•newton_seq n - a‚à• = ‚à•F.eval a‚à• / ‚à•F.derivative.eval a‚à•
| 1 h := by simp [sub_eq_add_neg, add_assoc, newton_seq, newton_seq_aux, ih_n]
| (k+2) h :=
  have hlt : ‚à•newton_seq (k+2) - newton_seq (k+1)‚à• < ‚à•newton_seq (k+1) - a‚à•,
    by rw newton_seq_dist_to_a (k+1) (succ_pos _); apply newton_seq_succ_dist_weak; assumption,
  have hne' : ‚à•newton_seq (k + 2) - newton_seq (k+1)‚à• ‚â† ‚à•newton_seq (k+1) - a‚à•, from ne_of_lt hlt,
  calc  ‚à•newton_seq (k + 2) - a‚à•
    = ‚à•(newton_seq (k + 2) - newton_seq (k+1)) + (newton_seq (k+1) - a)‚à• : by rw ‚Üêsub_add_sub_cancel
... = max (‚à•newton_seq (k + 2) - newton_seq (k+1)‚à•) (‚à•newton_seq (k+1) - a‚à•) :
  padic_int.norm_add_eq_max_of_ne hne'
... = ‚à•newton_seq (k+1) - a‚à• : max_eq_right_of_lt hlt
... = ‚à•polynomial.eval a F‚à• / ‚à•polynomial.eval a (polynomial.derivative F)‚à• :
  newton_seq_dist_to_a (k+1) (succ_pos _)

private lemma bound' : tendsto (Œª n : ‚Ñï, ‚à•F.derivative.eval a‚à• * T^(2^n)) at_top (ùìù 0) :=
begin
  rw ‚Üêmul_zero (‚à•F.derivative.eval a‚à•),
  exact tendsto_const_nhds.mul
                    (tendsto.comp
                      (tendsto_pow_at_top_nhds_0_of_lt_1 (norm_nonneg _) (T_lt_one hnorm))
                      (nat.tendsto_pow_at_top_at_top_of_one_lt (by norm_num)))
end

private lemma bound : ‚àÄ {Œµ}, Œµ > 0 ‚Üí ‚àÉ N : ‚Ñï, ‚àÄ {n}, n ‚â• N ‚Üí ‚à•F.derivative.eval a‚à• * T^(2^n) < Œµ :=
have mtn : ‚àÄ n : ‚Ñï, ‚à•polynomial.eval a (polynomial.derivative F)‚à• * T ^ (2 ^ n) ‚â• 0,
  from Œª n, mul_nonneg (norm_nonneg _) (T_pow_nonneg _),
begin
  have := bound' hnorm hnsol,
  simp [tendsto, nhds] at this,
  intros Œµ hŒµ,
  cases this (ball 0 Œµ) (mem_ball_self hŒµ) (is_open_ball) with N hN,
  existsi N, intros n hn,
  simpa [norm_mul, real.norm_eq_abs, abs_of_nonneg (mtn n)] using hN _ hn
end

private lemma bound'_sq : tendsto (Œª n : ‚Ñï, ‚à•F.derivative.eval a‚à•^2 * T^(2^n)) at_top (ùìù 0) :=
begin
  rw [‚Üêmul_zero (‚à•F.derivative.eval a‚à•), sq],
  simp only [mul_assoc],
  apply tendsto.mul,
  { apply tendsto_const_nhds },
  { apply bound', assumption }
end

private theorem newton_seq_is_cauchy : is_cau_seq norm newton_seq :=
begin
  intros Œµ hŒµ,
  cases bound hnorm hnsol hŒµ with N hN,
  existsi N,
  intros j hj,
  apply lt_of_le_of_lt,
  { apply newton_seq_dist _ _ hj, assumption },
  { apply hN, exact le_rfl }
end

private def newton_cau_seq : cau_seq ‚Ñ§_[p] norm := ‚ü®_, newton_seq_is_cauchy‚ü©

private def soln : ‚Ñ§_[p] := newton_cau_seq.lim

private lemma soln_spec {Œµ : ‚Ñù} (hŒµ : Œµ > 0) :
  ‚àÉ (N : ‚Ñï), ‚àÄ {i : ‚Ñï}, i ‚â• N ‚Üí ‚à•soln - newton_cau_seq i‚à• < Œµ :=
setoid.symm (cau_seq.equiv_lim newton_cau_seq) _ hŒµ

private lemma soln_deriv_norm : ‚à•F.derivative.eval soln‚à• = ‚à•F.derivative.eval a‚à• :=
norm_deriv_eq newton_seq_deriv_norm

private lemma newton_seq_norm_tendsto_zero :
  tendsto (Œª i, ‚à•F.eval (newton_cau_seq i)‚à•) at_top (ùìù 0) :=
squeeze_zero (Œª _, norm_nonneg _) newton_seq_norm_le bound'_sq

private lemma newton_seq_dist_tendsto :
  tendsto (Œª n, ‚à•newton_cau_seq n - a‚à•) at_top (ùìù (‚à•F.eval a‚à• / ‚à•F.derivative.eval a‚à•)) :=
tendsto_const_nhds.congr' $ eventually_at_top.2 ‚ü®1, Œª _ hx, (newton_seq_dist_to_a _ hx).symm‚ü©

private lemma newton_seq_dist_tendsto' :
  tendsto (Œª n, ‚à•newton_cau_seq n - a‚à•) at_top (ùìù ‚à•soln - a‚à•) :=
(continuous_norm.tendsto _).comp (newton_cau_seq.tendsto_limit.sub tendsto_const_nhds)

private lemma soln_dist_to_a : ‚à•soln - a‚à• = ‚à•F.eval a‚à• / ‚à•F.derivative.eval a‚à• :=
tendsto_nhds_unique newton_seq_dist_tendsto' newton_seq_dist_tendsto

private lemma soln_dist_to_a_lt_deriv : ‚à•soln - a‚à• < ‚à•F.derivative.eval a‚à• :=
begin
  rw [soln_dist_to_a, div_lt_iff],
  { rwa sq at hnorm },
  { apply deriv_norm_pos, assumption }
end

private lemma eval_soln : F.eval soln = 0 :=
limit_zero_of_norm_tendsto_zero newton_seq_norm_tendsto_zero

private lemma soln_unique (z : ‚Ñ§_[p]) (hev : F.eval z = 0)
  (hnlt : ‚à•z - a‚à• < ‚à•F.derivative.eval a‚à•) :
  z = soln :=
have soln_dist : ‚à•z - soln‚à• < ‚à•F.derivative.eval a‚à•, from calc
  ‚à•z - soln‚à• = ‚à•(z - a) + (a - soln)‚à• : by rw sub_add_sub_cancel
        ... ‚â§ max (‚à•z - a‚à•) (‚à•a - soln‚à•) : padic_int.nonarchimedean _ _
        ... < ‚à•F.derivative.eval a‚à• : max_lt hnlt (norm_sub_rev soln a ‚ñ∏ soln_dist_to_a_lt_deriv),
let h := z - soln,
    ‚ü®q, hq‚ü© := F.binom_expansion soln h in
have (F.derivative.eval soln + q * h) * h = 0, from eq.symm (calc
  0 = F.eval (soln + h) : by simp [hev, h]
... = F.derivative.eval soln * h + q * h^2 : by rw [hq, eval_soln, zero_add]
... = (F.derivative.eval soln + q * h) * h : by rw [sq, right_distrib, mul_assoc]),
have h = 0, from by_contradiction $ Œª hne,
  have F.derivative.eval soln + q * h = 0,
    from (eq_zero_or_eq_zero_of_mul_eq_zero this).resolve_right hne,
  have F.derivative.eval soln = (-q) * h, by simpa using eq_neg_of_add_eq_zero_left this,
  lt_irrefl ‚à•F.derivative.eval soln‚à• (calc
  ‚à•F.derivative.eval soln‚à• = ‚à•(-q) * h‚à• : by rw this
... ‚â§ 1 * ‚à•h‚à• :
  by { rw padic_int.norm_mul,
       exact mul_le_mul_of_nonneg_right (padic_int.norm_le_one _) (norm_nonneg _) }
... = ‚à•z - soln‚à• : by simp [h]
... < ‚à•F.derivative.eval soln‚à• : by rw soln_deriv_norm; apply soln_dist),
eq_of_sub_eq_zero (by rw ‚Üêthis; refl)

end hensel

variables {p : ‚Ñï} [fact p.prime] {F : polynomial ‚Ñ§_[p]} {a : ‚Ñ§_[p]}

private lemma a_soln_is_unique (ha : F.eval a = 0) (z' : ‚Ñ§_[p]) (hz' : F.eval z' = 0)
  (hnormz' : ‚à•z' - a‚à• < ‚à•F.derivative.eval a‚à•) : z' = a :=
let h := z' - a,
    ‚ü®q, hq‚ü© := F.binom_expansion a h in
have (F.derivative.eval a + q * h) * h = 0, from eq.symm (calc
  0 = F.eval (a + h) : show 0 = F.eval (a + (z' - a)), by rw add_comm; simp [hz']
... = F.derivative.eval a * h + q * h^2 : by rw [hq, ha, zero_add]
... = (F.derivative.eval a + q * h) * h : by rw [sq, right_distrib, mul_assoc]),
have h = 0, from by_contradiction $ Œª hne,
  have F.derivative.eval a + q * h = 0,
    from (eq_zero_or_eq_zero_of_mul_eq_zero this).resolve_right hne,
  have F.derivative.eval a = (-q) * h, by simpa using eq_neg_of_add_eq_zero_left this,
  lt_irrefl ‚à•F.derivative.eval a‚à• (calc
    ‚à•F.derivative.eval a‚à• = ‚à•q‚à•*‚à•h‚à• : by simp [this]
    ... ‚â§ 1*‚à•h‚à• : mul_le_mul_of_nonneg_right (padic_int.norm_le_one _) (norm_nonneg _)
    ... < ‚à•F.derivative.eval a‚à• : by simpa [h]),
eq_of_sub_eq_zero (by rw ‚Üêthis; refl)

variable (hnorm : ‚à•F.eval a‚à• < ‚à•F.derivative.eval a‚à•^2)
include hnorm

private lemma a_is_soln (ha : F.eval a = 0) :
  F.eval a = 0 ‚àß ‚à•a - a‚à• < ‚à•F.derivative.eval a‚à• ‚àß ‚à•F.derivative.eval a‚à• = ‚à•F.derivative.eval a‚à• ‚àß
  ‚àÄ z', F.eval z' = 0 ‚Üí ‚à•z' - a‚à• < ‚à•F.derivative.eval a‚à• ‚Üí z' = a :=
‚ü®ha, by simp [deriv_ne_zero hnorm], rfl, a_soln_is_unique ha‚ü©

lemma hensels_lemma : ‚àÉ z : ‚Ñ§_[p], F.eval z = 0 ‚àß ‚à•z - a‚à• < ‚à•F.derivative.eval a‚à• ‚àß
  ‚à•F.derivative.eval z‚à• = ‚à•F.derivative.eval a‚à• ‚àß
  ‚àÄ z', F.eval z' = 0 ‚Üí ‚à•z' - a‚à• < ‚à•F.derivative.eval a‚à• ‚Üí z' = z :=
if ha : F.eval a = 0 then ‚ü®a, a_is_soln hnorm ha‚ü© else
by refine ‚ü®soln _ _, eval_soln _ _, soln_dist_to_a_lt_deriv _ _, soln_deriv_norm _ _,
  soln_unique _ _‚ü©; assumption
