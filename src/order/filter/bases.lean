/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury Kudryashov, Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import order.filter.at_top_bot
import data.set.countable

/-!
# Filter bases

A filter basis `B : filter_basis Î±` on a type `Î±` is a nonempty collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element of
the collection. Compared to filters, filter bases do not require that any set containing
an element of `B` belongs to `B`.
A filter basis `B` can be used to construct `B.filter : filter Î±` such that a set belongs
to `B.filter` if and only if it contains an element of `B`.

Given an indexing type `Î¹`, a predicate `p : Î¹ â†’ Prop`, and a map `s : Î¹ â†’ set Î±`,
the proposition `h : filter.is_basis p s` makes sure the range of `s` bounded by `p`
(ie. `s '' set_of p`) defines a filter basis `h.filter_basis`.

If one already has a filter `l` on `Î±`, `filter.has_basis l p s` (where `p : Î¹ â†’ Prop`
and `s : Î¹ â†’ set Î±` as above) means that a set belongs to `l` if and
only if it contains some `s i` with `p i`. It implies `h : filter.is_basis p s`, and
`l = h.filter_basis.filter`. The point of this definition is that checking statements
involving elements of `l` often reduces to checking them on the basis elements.

This file also introduces more restricted classes of bases, involving monotonicity or
countability. In particular, for `l : filter Î±`, `l.is_countably_generated` means
there is a countable set of sets which generates `s`. This is reformulated in term of bases,
and consequences are derived.

## Main statements

* `has_basis.mem_iff`, `has_basis.mem_of_superset`, `has_basis.mem_of_mem` : restate `t âˆˆ f` in terms
  of a basis;
* `basis_sets` : all sets of a filter form a basis;
* `has_basis.inf`, `has_basis.inf_principal`, `has_basis.prod`, `has_basis.prod_self`,
  `has_basis.map`, `has_basis.comap` : combinators to construct filters of `l âŠ“ l'`,
  `l âŠ“ ğ“Ÿ t`, `l.prod l'`, `l.prod l`, `l.map f`, `l.comap f` respectively;
* `has_basis.le_iff`, `has_basis.ge_iff`, has_basis.le_basis_iff` : restate `l â‰¤ l'` in terms
  of bases.
* `has_basis.tendsto_right_iff`, `has_basis.tendsto_left_iff`, `has_basis.tendsto_iff` : restate
  `tendsto f l l'` in terms of bases.
* `is_countably_generated_iff_exists_antimono_basis` : proves a filter is
  countably generated if and only if it admis a basis parametrized by a
  decreasing sequence of sets indexed by `â„•`.
* `tendsto_iff_seq_tendsto ` : an abstract version of "sequentially continuous implies continuous".

## Implementation notes

As with `Union`/`bUnion`/`sUnion`, there are three different approaches to filter bases:

* `has_basis l s`, `s : set (set Î±)`;
* `has_basis l s`, `s : Î¹ â†’ set Î±`;
* `has_basis l p s`, `p : Î¹ â†’ Prop`, `s : Î¹ â†’ set Î±`.

We use the latter one because, e.g., `ğ“ x` in an `emetric_space` or in a `metric_space` has a basis
of this form. The other two can be emulated using `s = id` or `p = Î» _, true`.

With this approach sometimes one needs to `simp` the statement provided by the `has_basis`
machinery, e.g., `simp only [exists_prop, true_and]` or `simp only [forall_const]` can help
with the case `p = Î» _, true`.
-/

open set filter
open_locale filter

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î¹ : Type*} {Î¹' : Type*}

/-- A filter basis `B` on a type `Î±` is a nonempty collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element
of the collection. -/
structure filter_basis (Î± : Type*) :=
(sets                   : set (set Î±))
(nonempty               : sets.nonempty)
(inter_sets {x y}       : x âˆˆ sets â†’ y âˆˆ sets â†’ âˆƒ z âˆˆ sets, z âŠ† x âˆ© y)

/-- If `B` is a filter basis on `Î±`, and `U` a subset of `Î±` then we can write `U âˆˆ B` as on paper. -/
@[reducible]
instance {Î± : Type*}: has_mem (set Î±) (filter_basis Î±) := âŸ¨Î» U B, U âˆˆ B.setsâŸ©

-- For illustration purposes, the filter basis defining (at_top : filter â„•)
instance : inhabited (filter_basis â„•) :=
âŸ¨{ sets := range Ici,
  nonempty := âŸ¨Ici 0, mem_range_self 0âŸ©,
  inter_sets := begin
    rintros _ _ âŸ¨n, rflâŸ© âŸ¨m, rflâŸ©,
    refine âŸ¨Ici (max n m), mem_range_self _, _âŸ©,
    rintros p p_in,
    split ; rw mem_Ici at *,
    exact le_of_max_le_left p_in,
    exact le_of_max_le_right p_in,
  end }âŸ©

/-- `is_basis p s` means the image of `s` bounded by `p` is a filter basis. -/
protected structure filter.is_basis (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±) : Prop :=
(nonempty : âˆƒ i, p i)
(inter : âˆ€ {i j}, p i â†’ p j â†’ âˆƒ k, p k âˆ§ s k âŠ† s i âˆ© s j)

namespace filter
namespace is_basis

/-- Constructs a filter basis from an indexed family of sets satisfying `is_basis`. -/
protected def filter_basis {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} (h : is_basis p s) : filter_basis Î± :=
{ sets := s '' set_of p,
  nonempty := let âŸ¨i, hiâŸ© := h.nonempty in âŸ¨s i, mem_image_of_mem s hiâŸ©,
  inter_sets := by { rintros _ _ âŸ¨i, hi, rflâŸ© âŸ¨j, hj, rflâŸ©,
                     rcases h.inter hi hj with âŸ¨k, hk, hk'âŸ©,
                     exact âŸ¨_, mem_image_of_mem s hk, hk'âŸ© } }

variables {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} (h : is_basis p s)

lemma mem_filter_basis_iff {U : set Î±} : U âˆˆ h.filter_basis â†” âˆƒ i, p i âˆ§ s i = U :=
iff.rfl
end is_basis
end filter

namespace filter_basis

/-- The filter associated to a filter basis. -/
protected def filter (B : filter_basis Î±) : filter Î± :=
{ sets := {s | âˆƒ t âˆˆ B, t âŠ† s},
  univ_sets := let âŸ¨s, s_inâŸ© := B.nonempty in âŸ¨s, s_in, s.subset_univâŸ©,
  sets_of_superset := Î» x y âŸ¨s, s_in, hâŸ© hxy, âŸ¨s, s_in, set.subset.trans h hxyâŸ©,
  inter_sets := Î» x y âŸ¨s, s_in, hsâŸ© âŸ¨t, t_in, htâŸ©,
                let âŸ¨u, u_in, u_subâŸ© := B.inter_sets s_in t_in in
                âŸ¨u, u_in, set.subset.trans u_sub $ set.inter_subset_inter hs htâŸ© }

lemma mem_filter_iff (B : filter_basis Î±) {U : set Î±} : U âˆˆ B.filter â†” âˆƒ s âˆˆ B, s âŠ† U :=
iff.rfl

lemma mem_filter_of_mem (B : filter_basis Î±) {U : set Î±} : U âˆˆ B â†’ U âˆˆ B.filter:=
Î» U_in, âŸ¨U, U_in, subset.refl _âŸ©

lemma eq_infi_principal (B : filter_basis Î±) : B.filter = â¨… s : B.sets, ğ“Ÿ s :=
begin
  ext U,
  rw [mem_filter_iff, mem_infi],
  { simp },
  { rintros âŸ¨U, U_inâŸ© âŸ¨V, V_inâŸ©,
    rcases B.inter_sets U_in V_in with âŸ¨W, W_in, W_subâŸ©,
    use [W, W_in],
    finish },
  cases B.nonempty with U U_in,
  exact âŸ¨âŸ¨U, U_inâŸ©âŸ©,
end

protected lemma generate (B : filter_basis Î±) : generate B.sets = B.filter :=
begin
  apply le_antisymm,
  { intros U U_in,
    rcases B.mem_filter_iff.mp U_in with âŸ¨V, V_in, hâŸ©,
    exact generate_sets.superset (generate_sets.basic V_in) h },
  { rw sets_iff_generate,
    apply mem_filter_of_mem }
end
end filter_basis

namespace filter
namespace is_basis
variables {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±}

/-- Constructs a filter from an indexed family of sets satisfying `is_basis`. -/
protected def filter (h : is_basis p s) : filter Î± := h.filter_basis.filter

protected lemma mem_filter_iff (h : is_basis p s) {U : set Î±} :
  U âˆˆ h.filter â†” âˆƒ i, p i âˆ§ s i âŠ† U :=
begin
  erw [h.filter_basis.mem_filter_iff],
  simp only [mem_filter_basis_iff h, exists_prop],
  split,
  { rintros âŸ¨_, âŸ¨i, pi, rflâŸ©, hâŸ©,
    tauto },
  { tauto }
end

lemma filter_eq_generate (h : is_basis p s) : h.filter = generate {U | âˆƒ i, p i âˆ§ s i = U} :=
by erw h.filter_basis.generate ; refl
end is_basis

/-- We say that a filter `l` has a basis `s : Î¹ â†’ set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`. -/
protected structure has_basis (l : filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±) : Prop :=
(mem_iff' : âˆ€ (t : set Î±), t âˆˆ l â†” âˆƒ i (hi : p i), s i âŠ† t)

section same_type

variables {l l' : filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {t : set Î±} {i : Î¹}
  {p' : Î¹' â†’ Prop} {s' : Î¹' â†’ set Î±} {i' : Î¹'}

lemma has_basis_generate (s : set (set Î±)) : (generate s).has_basis (Î» t, finite t âˆ§ t âŠ† s) (Î» t, â‹‚â‚€ t) :=
âŸ¨begin
  intro U,
  rw mem_generate_iff,
  apply exists_congr,
  tauto
endâŸ©

/-- The smallest filter basis containing a given collection of sets. -/
def filter_basis.of_sets (s : set (set Î±)) : filter_basis Î± :=
{ sets := sInter '' { t | finite t âˆ§ t âŠ† s},
  nonempty := âŸ¨univ, âˆ…, âŸ¨âŸ¨finite_empty, empty_subset sâŸ©, sInter_emptyâŸ©âŸ©,
  inter_sets := begin
    rintros _ _ âŸ¨a, âŸ¨fina, subaâŸ©, rflâŸ© âŸ¨b, âŸ¨finb, subbâŸ©, rflâŸ©,
    exact âŸ¨â‹‚â‚€ (a âˆª b), mem_image_of_mem _ âŸ¨fina.union finb, union_subset suba subbâŸ©,
           by rw sInter_unionâŸ©,
  end }

/-- Definition of `has_basis` unfolded with implicit set argument. -/
lemma has_basis.mem_iff (hl : l.has_basis p s) : t âˆˆ l â†” âˆƒ i (hi : p i), s i âŠ† t :=
hl.mem_iff' t

protected lemma is_basis.has_basis (h : is_basis p s) : has_basis h.filter p s :=
âŸ¨Î» t, by simp only [h.mem_filter_iff, exists_prop]âŸ©

lemma has_basis.mem_of_superset (hl : l.has_basis p s) (hi : p i) (ht : s i âŠ† t) : t âˆˆ l :=
(hl.mem_iff).2 âŸ¨i, hi, htâŸ©

lemma has_basis.mem_of_mem (hl : l.has_basis p s) (hi : p i) : s i âˆˆ l :=
hl.mem_of_superset hi $ subset.refl _

lemma has_basis.is_basis (h : l.has_basis p s) : is_basis p s :=
{ nonempty := let âŸ¨i, hi, HâŸ© := h.mem_iff.mp univ_mem_sets in âŸ¨i, hiâŸ©,
  inter := Î» i j hi hj, by simpa [h.mem_iff] using l.inter_sets (h.mem_of_mem hi) (h.mem_of_mem hj) }

lemma has_basis.filter_eq (h : l.has_basis p s) : h.is_basis.filter = l :=
by { ext U, simp [h.mem_iff, is_basis.mem_filter_iff] }

lemma has_basis.eq_generate (h : l.has_basis p s) : l = generate { U | âˆƒ i, p i âˆ§ s i = U } :=
by rw [â† h.is_basis.filter_eq_generate, h.filter_eq]

lemma generate_eq_generate_inter (s : set (set Î±)) : generate s = generate (sInter '' { t | finite t âˆ§ t âŠ† s}) :=
by erw [(filter_basis.of_sets s).generate, â† (has_basis_generate s).filter_eq] ; refl

lemma of_sets_filter_eq_generate (s : set (set Î±)) : (filter_basis.of_sets s).filter = generate s :=
by rw [â† (filter_basis.of_sets s).generate, generate_eq_generate_inter s] ; refl

lemma has_basis.eventually_iff (hl : l.has_basis p s) {q : Î± â†’ Prop} :
  (âˆ€á¶  x in l, q x) â†” âˆƒ i, p i âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s i â†’ q x :=
by simpa using hl.mem_iff

lemma has_basis.forall_nonempty_iff_ne_bot (hl : l.has_basis p s) :
  (âˆ€ {i}, p i â†’ (s i).nonempty) â†” ne_bot l :=
âŸ¨Î» H, forall_sets_nonempty_iff_ne_bot.1 $
  Î» s hs, let âŸ¨i, hi, hisâŸ© := hl.mem_iff.1 hs in (H hi).mono his,
  Î» H i hi, H.nonempty_of_mem (hl.mem_of_mem hi)âŸ©

lemma basis_sets (l : filter Î±) : l.has_basis (Î» s : set Î±, s âˆˆ l) id :=
âŸ¨Î» t, exists_sets_subset_iff.symmâŸ©

lemma has_basis_self {l : filter Î±} {P : set Î± â†’ Prop} :
  has_basis l (Î» s, s âˆˆ l âˆ§ P s) id â†” âˆ€ t, (t âˆˆ l â†” âˆƒ r âˆˆ l, P r âˆ§ r âŠ† t) :=
begin
  split,
  { rintros âŸ¨hâŸ© t,
    convert h t,
    ext s,
    tauto, },
  { intro h,
    constructor,
    intro t,
    convert h t,
    ext s,
    tauto }
end

lemma at_top_basis [nonempty Î±] [semilattice_sup Î±] :
  (@at_top Î± _).has_basis (Î» _, true) Ici :=
âŸ¨Î» t, by simpa only [exists_prop, true_and] using @mem_at_top_sets Î± _ _ tâŸ©

lemma at_top_basis' [semilattice_sup Î±] (a : Î±) :
  (@at_top Î± _).has_basis (Î» x, a â‰¤ x) Ici :=
âŸ¨Î» t, (@at_top_basis Î± âŸ¨aâŸ© _).mem_iff.trans
  âŸ¨Î» âŸ¨x, _, hxâŸ©, âŸ¨x âŠ” a, le_sup_right, Î» y hy, hx (le_trans le_sup_left hy)âŸ©,
    Î» âŸ¨x, _, hxâŸ©, âŸ¨x, trivial, hxâŸ©âŸ©âŸ©

theorem has_basis.ge_iff (hl' : l'.has_basis p' s')  : l â‰¤ l' â†” âˆ€ i', p' i' â†’ s' i' âˆˆ l :=
âŸ¨Î» h i' hi', h $ hl'.mem_of_mem hi',
  Î» h s hs, let âŸ¨i', hi', hsâŸ© := hl'.mem_iff.1 hs in mem_sets_of_superset (h _ hi') hsâŸ©

theorem has_basis.le_iff (hl : l.has_basis p s) : l â‰¤ l' â†” âˆ€ t âˆˆ l', âˆƒ i (hi : p i), s i âŠ† t :=
by simp only [le_def, hl.mem_iff]

theorem has_basis.le_basis_iff (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  l â‰¤ l' â†” âˆ€ i', p' i' â†’ âˆƒ i (hi : p i), s i âŠ† s' i' :=
by simp only [hl'.ge_iff, hl.mem_iff]

lemma has_basis.inf (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  (l âŠ“ l').has_basis (Î» i : Î¹ Ã— Î¹', p i.1 âˆ§ p' i.2) (Î» i, s i.1 âˆ© s' i.2) :=
âŸ¨begin
  intro t,
  simp only [mem_inf_sets, exists_prop, hl.mem_iff, hl'.mem_iff],
  split,
  { rintros âŸ¨t, âŸ¨i, hi, htâŸ©, t', âŸ¨i', hi', ht'âŸ©, HâŸ©,
    use [(i, i'), âŸ¨hi, hi'âŸ©, subset.trans (inter_subset_inter ht ht') H] },
  { rintros âŸ¨âŸ¨i, i'âŸ©, âŸ¨hi, hi'âŸ©, HâŸ©,
    use [s i, i, hi, subset.refl _, s' i', i', hi', subset.refl _, H] }
endâŸ©

lemma has_basis.inf_principal (hl : l.has_basis p s) (s' : set Î±) :
  (l âŠ“ ğ“Ÿ s').has_basis p (Î» i, s i âˆ© s') :=
âŸ¨Î» t, by simp only [mem_inf_principal, hl.mem_iff, subset_def, mem_set_of_eq,
  mem_inter_iff, and_imp]âŸ©

lemma has_basis.eq_binfi (h : l.has_basis p s) :
  l = â¨… i (_ : p i), ğ“Ÿ (s i) :=
eq_binfi_of_mem_sets_iff_exists_mem $ Î» t, by simp only [h.mem_iff, mem_principal_sets]

lemma has_basis.eq_infi (h : l.has_basis (Î» _, true) s) :
  l = â¨… i, ğ“Ÿ (s i) :=
by simpa only [infi_true] using h.eq_binfi

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma has_basis_infi_principal {s : Î¹ â†’ set Î±} (h : directed (â‰¥) s) (ne : nonempty Î¹) :
  (â¨… i, ğ“Ÿ (s i)).has_basis (Î» _, true) s :=
âŸ¨begin
  refine Î» t, (mem_infi (h.mono_comp _ _) ne t).trans $
    by simp only [exists_prop, true_and, mem_principal_sets],
  exact Î» _ _, principal_mono.2
endâŸ©

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma has_basis_binfi_principal {s : Î² â†’ set Î±} {S : set Î²} (h : directed_on (s â»Â¹'o (â‰¥)) S)
  (ne : S.nonempty) :
  (â¨… i âˆˆ S, ğ“Ÿ (s i)).has_basis (Î» i, i âˆˆ S) s :=
âŸ¨begin
  refine Î» t, (mem_binfi _ ne).trans $ by simp only [mem_principal_sets],
  rw [directed_on_iff_directed, â† directed_comp, (âˆ˜)] at h âŠ¢,
  apply h.mono_comp _ _,
  exact Î» _ _, principal_mono.2
endâŸ©

lemma has_basis.map (f : Î± â†’ Î²) (hl : l.has_basis p s) :
  (l.map f).has_basis p (Î» i, f '' (s i)) :=
âŸ¨Î» t, by simp only [mem_map, image_subset_iff, hl.mem_iff, preimage]âŸ©

lemma has_basis.comap (f : Î² â†’ Î±) (hl : l.has_basis p s) :
  (l.comap f).has_basis p (Î» i, f â»Â¹' (s i)) :=
âŸ¨begin
  intro t,
  simp only [mem_comap_sets, exists_prop, hl.mem_iff],
  split,
  { rintros âŸ¨t', âŸ¨i, hi, ht'âŸ©, HâŸ©,
    exact âŸ¨i, hi, subset.trans (preimage_mono ht') HâŸ© },
  { rintros âŸ¨i, hi, HâŸ©,
    exact âŸ¨s i, âŸ¨i, hi, subset.refl _âŸ©, HâŸ© }
endâŸ©

lemma comap_has_basis (f : Î± â†’ Î²) (l : filter Î²) :
  has_basis (comap f l) (Î» s : set Î², s âˆˆ l) (Î» s, f â»Â¹' s) :=
âŸ¨Î» t, mem_comap_setsâŸ©

lemma has_basis.prod_self (hl : l.has_basis p s) :
  (l.prod l).has_basis p (Î» i, (s i).prod (s i)) :=
âŸ¨begin
  intro t,
  apply mem_prod_iff.trans,
  split,
  { rintros âŸ¨tâ‚, htâ‚, tâ‚‚, htâ‚‚, HâŸ©,
    rcases hl.mem_iff.1 (inter_mem_sets htâ‚ htâ‚‚) with âŸ¨i, hi, htâŸ©,
    exact âŸ¨i, hi, Î» p âŸ¨hpâ‚, hpâ‚‚âŸ©, H âŸ¨(ht hpâ‚).1, (ht hpâ‚‚).2âŸ©âŸ© },
  { rintros âŸ¨i, hi, HâŸ©,
    exact âŸ¨s i, hl.mem_of_mem hi, s i, hl.mem_of_mem hi, HâŸ© }
endâŸ©

lemma has_basis.exists_iff (hl : l.has_basis p s) {P : set Î± â†’ Prop}
  (mono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ P t â†’ P s) :
  (âˆƒ s âˆˆ l, P s) â†” âˆƒ (i) (hi : p i), P (s i) :=
âŸ¨Î» âŸ¨s, hs, hPâŸ©, let âŸ¨i, hi, hisâŸ© := hl.mem_iff.1 hs in âŸ¨i, hi, mono his hPâŸ©,
  Î» âŸ¨i, hi, hPâŸ©, âŸ¨s i, hl.mem_of_mem hi, hPâŸ©âŸ©

lemma has_basis.forall_iff (hl : l.has_basis p s) {P : set Î± â†’ Prop}
  (mono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ P s â†’ P t) :
  (âˆ€ s âˆˆ l, P s) â†” âˆ€ i, p i â†’ P (s i) :=
âŸ¨Î» H i hi, H (s i) $ hl.mem_of_mem hi,
  Î» H s hs, let âŸ¨i, hi, hisâŸ© := hl.mem_iff.1 hs in mono his (H i hi)âŸ©

lemma has_basis.sInter_sets (h : has_basis l p s) :
  â‹‚â‚€ l.sets = â‹‚ i âˆˆ set_of p, s i :=
begin
  ext x,
  suffices : (âˆ€ t âˆˆ l, x âˆˆ t) â†” âˆ€ i, p i â†’ x âˆˆ s i,
    by simpa only [mem_Inter, mem_set_of_eq, mem_sInter],
  simp_rw h.mem_iff,
  split,
  { intros h i hi,
    exact h (s i) âŸ¨i, hi, subset.refl _âŸ© },
  { rintros h _ âŸ¨i, hi, subâŸ©,
    exact sub (h i hi) },
end

variables [preorder Î¹] (l p s)

/-- `is_antimono_basis p s` means the image of `s` bounded by `p` is a filter basis
such that `s` is decreasing and `p` is increasing, ie `i â‰¤ j â†’ p i â†’ p j`. -/
structure is_antimono_basis extends is_basis p s : Prop :=
(decreasing : âˆ€ {i j}, p i â†’ p j â†’ i â‰¤ j â†’ s j âŠ† s i)
(mono : monotone p)

/-- We say that a filter `l` has a antimono basis `s : Î¹ â†’ set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`,
and `s` is decreasing and `p` is increasing, ie `i â‰¤ j â†’ p i â†’ p j`. -/
structure has_antimono_basis [preorder Î¹] (l : filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±)
  extends has_basis l p s : Prop :=
(decreasing : âˆ€ {i j}, p i â†’ p j â†’ i â‰¤ j â†’ s j âŠ† s i)
(mono : monotone p)

end same_type

section two_types

variables {la : filter Î±} {pa : Î¹ â†’ Prop} {sa : Î¹ â†’ set Î±}
  {lb : filter Î²} {pb : Î¹' â†’ Prop} {sb : Î¹' â†’ set Î²} {f : Î± â†’ Î²}

lemma has_basis.tendsto_left_iff (hla : la.has_basis pa sa) :
  tendsto f la lb â†” âˆ€ t âˆˆ lb, âˆƒ i (hi : pa i), âˆ€ x âˆˆ sa i, f x âˆˆ t :=
by { simp only [tendsto, (hla.map f).le_iff, image_subset_iff], refl }

lemma has_basis.tendsto_right_iff (hlb : lb.has_basis pb sb) :
  tendsto f la lb â†” âˆ€ i (hi : pb i), âˆ€á¶  x in la, f x âˆˆ sb i :=
by simp only [tendsto, hlb.ge_iff, mem_map, filter.eventually]

lemma has_basis.tendsto_iff (hla : la.has_basis pa sa) (hlb : lb.has_basis pb sb) :
  tendsto f la lb â†” âˆ€ ib (hib : pb ib), âˆƒ ia (hia : pa ia), âˆ€ x âˆˆ sa ia, f x âˆˆ sb ib :=
by simp [hlb.tendsto_right_iff, hla.eventually_iff]

lemma tendsto.basis_left (H : tendsto f la lb) (hla : la.has_basis pa sa) :
  âˆ€ t âˆˆ lb, âˆƒ i (hi : pa i), âˆ€ x âˆˆ sa i, f x âˆˆ t :=
hla.tendsto_left_iff.1 H

lemma tendsto.basis_right (H : tendsto f la lb) (hlb : lb.has_basis pb sb) :
  âˆ€ i (hi : pb i), âˆ€á¶  x in la, f x âˆˆ sb i :=
hlb.tendsto_right_iff.1 H

lemma tendsto.basis_both (H : tendsto f la lb) (hla : la.has_basis pa sa)
  (hlb : lb.has_basis pb sb) :
  âˆ€ ib (hib : pb ib), âˆƒ ia (hia : pa ia), âˆ€ x âˆˆ sa ia, f x âˆˆ sb ib :=
(hla.tendsto_iff hlb).1 H

lemma has_basis.prod (hla : la.has_basis pa sa) (hlb : lb.has_basis pb sb) :
  (la.prod lb).has_basis (Î» i : Î¹ Ã— Î¹', pa i.1 âˆ§ pb i.2) (Î» i, (sa i.1).prod (sb i.2)) :=
(hla.comap prod.fst).inf (hlb.comap prod.snd)

lemma has_basis.prod' {la : filter Î±} {lb : filter Î²} {Î¹ : Type*} {p : Î¹ â†’ Prop}
  {sa : Î¹ â†’ set Î±} {sb : Î¹ â†’ set Î²}
  (hla : la.has_basis p sa) (hlb : lb.has_basis p sb)
  (h_dir : âˆ€ {i j}, p i â†’ p j â†’ âˆƒ k, p k âˆ§ sa k âŠ† sa i âˆ§ sb k âŠ† sb j) :
  (la.prod lb).has_basis p (Î» i, (sa i).prod (sb i)) :=
âŸ¨begin
  intros t,
  rw mem_prod_iff,
  split,
  { rintros âŸ¨u, u_in, v, v_in, huvâŸ©,
    rcases hla.mem_iff.mp u_in with âŸ¨i, hi, siâŸ©,
    rcases hlb.mem_iff.mp v_in with âŸ¨j, hj, sjâŸ©,
    rcases h_dir hi hj with âŸ¨k, hk, ki, kjâŸ©,
    use [k, hk],
    calc
    (sa k).prod (sb k) âŠ† (sa i).prod (sb j) : set.prod_mono ki kj
                   ... âŠ† u.prod v           : set.prod_mono si sj
                   ... âŠ† t                  : huv, },
  { rintro âŸ¨i, hi, hâŸ©,
    exact âŸ¨sa i, hla.mem_of_mem hi, sb i, hlb.mem_of_mem hi, hâŸ© },
endâŸ©

lemma has_antimono_basis.tendsto [semilattice_sup Î¹] [nonempty Î¹] {l : filter Î±}
  {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} (hl : l.has_antimono_basis p s) {Ï† : Î¹ â†’ Î±}
  (h : âˆ€ i : Î¹, Ï† i âˆˆ s i) : tendsto Ï† at_top l  :=
begin
  rw hl.to_has_basis.tendsto_right_iff,
  intros i hi,
  rw eventually_at_top,
  exact âŸ¨i, Î» j hij, hl.decreasing hi (hl.mono hij hi) hij (h j)âŸ©,
end

end two_types

/-- `is_countably_generated f` means `f = generate s` for some countable `s`. -/
def is_countably_generated (f : filter Î±) : Prop :=
âˆƒ s : set (set Î±), countable s âˆ§ f = generate s

/-- `is_countable_basis p s` means the image of `s` bounded by `p` is a countable filter basis. -/
structure is_countable_basis (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±) extends is_basis p s : Prop :=
(countable : countable $ set_of p)

/-- We say that a filter `l` has a countable basis `s : Î¹ â†’ set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`, and the set
defined by `p` is countable. -/
structure has_countable_basis (l : filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±) extends has_basis l p s : Prop :=
(countable : countable $ set_of p)

/-- A countable filter basis `B` on a type `Î±` is a nonempty countable collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element
of the collection. -/
structure countable_filter_basis (Î± : Type*) extends filter_basis Î± :=
(countable : countable sets)

-- For illustration purposes, the countable filter basis defining (at_top : filter â„•)
instance nat.inhabited_countable_filter_basis : inhabited (countable_filter_basis â„•) :=
âŸ¨{ countable := countable_range (Î» n, Ici n),
   ..(default $ filter_basis â„•),}âŸ©

lemma antimono_seq_of_seq (s : â„• â†’ set Î±) :
  âˆƒ t : â„• â†’ set Î±, (âˆ€ i j, i â‰¤ j â†’ t j âŠ† t i) âˆ§ (â¨… i, ğ“Ÿ $ s i) = â¨… i, ğ“Ÿ (t i) :=
begin
  use Î» n, â‹‚ m â‰¤ n, s m, split,
  { intros i j hij a, simp, intros h i' hi'i, apply h, transitivity; assumption },
    apply le_antisymm; rw le_infi_iff; intro i,
  { rw le_principal_iff, apply Inter_mem_sets (finite_le_nat _),
    intros j hji, rw â† le_principal_iff, apply infi_le_of_le j _, apply le_refl _ },
  { apply infi_le_of_le i _, rw principal_mono, intro a, simp, intro h, apply h, refl },
end

lemma countable_binfi_eq_infi_seq [complete_lattice Î±] {B : set Î¹} (Bcbl : countable B) (Bne : B.nonempty) (f : Î¹ â†’ Î±)
 : âˆƒ (x : â„• â†’ Î¹), (â¨… t âˆˆ B, f t) = â¨… i, f (x i) :=
begin
  rw countable_iff_exists_surjective_to_subtype Bne at Bcbl,
  rcases Bcbl with âŸ¨g, gsurjâŸ©,
  rw infi_subtype',
  use (Î» n, g n), apply le_antisymm; rw le_infi_iff,
  { intro i, apply infi_le_of_le (g i) _, apply le_refl _ },
  { intros a, rcases gsurj a with i, apply infi_le_of_le i _, subst h, apply le_refl _ }
end

lemma countable_binfi_eq_infi_seq' [complete_lattice Î±] {B : set Î¹} (Bcbl : countable B) (f : Î¹ â†’ Î±)
{iâ‚€ : Î¹} (h : f iâ‚€ = âŠ¤) :
  âˆƒ (x : â„• â†’ Î¹), (â¨… t âˆˆ B, f t) = â¨… i, f (x i) :=
begin
  cases B.eq_empty_or_nonempty with hB Bnonempty,
  { rw [hB, infi_emptyset],
    use Î» n, iâ‚€,
    simp [h] },
  { exact countable_binfi_eq_infi_seq Bcbl Bnonempty f }
end

lemma countable_binfi_principal_eq_seq_infi {B : set (set Î±)} (Bcbl : countable B) :
  âˆƒ (x : â„• â†’ set Î±), (â¨… t âˆˆ B, ğ“Ÿ t) = â¨… i, ğ“Ÿ (x i) :=
countable_binfi_eq_infi_seq' Bcbl ğ“Ÿ principal_univ

namespace is_countably_generated

/-- A set generating a countably generated filter. -/
def generating_set {f : filter Î±} (h : is_countably_generated f) :=
classical.some h

lemma countable_generating_set {f : filter Î±} (h : is_countably_generated f) :
  countable h.generating_set :=
(classical.some_spec h).1

lemma eq_generate {f : filter Î±} (h : is_countably_generated f) :
  f = generate h.generating_set :=
(classical.some_spec h).2

/-- A countable filter basis for a countably generated filter. -/
def countable_filter_basis {l : filter Î±} (h : is_countably_generated l) :
  countable_filter_basis Î± :=
{ countable := (countable_set_of_finite_subset h.countable_generating_set).image _,
  ..filter_basis.of_sets (h.generating_set) }

lemma filter_basis_filter {l : filter Î±} (h : is_countably_generated l) :
h.countable_filter_basis.to_filter_basis.filter = l :=
begin
  conv_rhs { rw h.eq_generate },
  apply of_sets_filter_eq_generate,
end

lemma has_countable_basis {l : filter Î±} (h : is_countably_generated l) :
  l.has_countable_basis (Î» t, finite t âˆ§ t âŠ† h.generating_set) (Î» t, â‹‚â‚€ t) :=
âŸ¨by convert has_basis_generate _ ; exact h.eq_generate,
 countable_set_of_finite_subset h.countable_generating_setâŸ©

lemma exists_countable_infi_principal {f : filter Î±} (h : f.is_countably_generated) :
âˆƒ s : set (set Î±), countable s âˆ§ f = â¨… t âˆˆ s, ğ“Ÿ t :=
begin
  let B := h.countable_filter_basis,
  use [B.sets, B.countable],
  rw â† h.filter_basis_filter,
  rw B.to_filter_basis.eq_infi_principal,
  rw infi_subtype''
end

lemma exists_seq {f : filter Î±} (cblb : f.is_countably_generated) :
    âˆƒ x : â„• â†’ set Î±, f = â¨… i, ğ“Ÿ (x i) :=
begin
  rcases cblb.exists_countable_infi_principal with âŸ¨B, Bcbl, rflâŸ©,
  exact countable_binfi_principal_eq_seq_infi Bcbl,
end

lemma exists_antimono_seq {f : filter Î±} (cblb : f.is_countably_generated) :
  âˆƒ x : â„• â†’ set Î±, (âˆ€ i j, i â‰¤ j â†’ x j âŠ† x i) âˆ§ f = â¨… i, ğ“Ÿ (x i) :=
begin
  rcases cblb.exists_seq with âŸ¨x', hx'âŸ©,
  let x := Î» n, â‹‚ m â‰¤ n, x' m,
  use x, split,
  { intros i j hij a, simp [x], intros h i' hi'i, apply h, transitivity; assumption },
  subst hx', apply le_antisymm; rw le_infi_iff; intro i,
  { rw le_principal_iff, apply Inter_mem_sets (finite_le_nat _),
    intros j hji, rw â† le_principal_iff, apply infi_le_of_le j _, apply le_refl _ },
  { apply infi_le_of_le i _, rw principal_mono, intro a, simp [x], intro h, apply h, refl },
end

lemma has_antimono_basis {f : filter Î±} (h : f.is_countably_generated) :
 âˆƒ x : â„• â†’ set Î±, f.has_antimono_basis (Î» _, true) x :=
begin
  rcases h.exists_antimono_seq with âŸ¨x, x_dec, rflâŸ©,
  refine âŸ¨x, has_basis_infi_principal _ âŸ¨0âŸ©, _, monotone_constâŸ©,
  exacts [directed_of_sup x_dec, Î» i j _ _, x_dec i j]
end

end is_countably_generated

lemma is_countably_generated_seq (x : â„• â†’ set Î±) : is_countably_generated (â¨… i, ğ“Ÿ $ x i) :=
begin
  rcases antimono_seq_of_seq x with âŸ¨y, am, hâŸ©,
  rw h,
  use [range y, countable_range _],
  rw (has_basis_infi_principal _ _).eq_generate,
  { simp [range] },
  { exact directed_of_sup am },
  { use 0 },
end

lemma is_countably_generated_of_seq {f : filter Î±} (h : âˆƒ x : â„• â†’ set Î±, f = â¨… i, ğ“Ÿ $ x i) :
  f.is_countably_generated  :=
let âŸ¨x, hâŸ© := h in by rw h ; apply is_countably_generated_seq

lemma is_countably_generated_binfi_principal {B : set $ set Î±} (h : countable B) :
  is_countably_generated (â¨… (s âˆˆ B), ğ“Ÿ s) :=
is_countably_generated_of_seq (countable_binfi_principal_eq_seq_infi h)

lemma is_countably_generated_iff_exists_antimono_basis {f : filter Î±} : is_countably_generated f â†”
  âˆƒ x : â„• â†’ set Î±, f.has_antimono_basis (Î» _, true) x :=
begin
  split,
  { intro h,
    exact h.has_antimono_basis },
  { rintros âŸ¨x, hâŸ©,
    rw h.to_has_basis.eq_infi,
    exact is_countably_generated_seq x },
end

namespace is_countably_generated

lemma exists_antimono_seq' {f : filter Î±} (cblb : f.is_countably_generated) :
  âˆƒ x : â„• â†’ set Î±, (âˆ€ i j, i â‰¤ j â†’ x j âŠ† x i) âˆ§ âˆ€ {s}, (s âˆˆ f â†” âˆƒ i, x i âŠ† s) :=
let âŸ¨x, hxâŸ© := is_countably_generated_iff_exists_antimono_basis.mp cblb in
âŸ¨x, Î» i j, hx.decreasing trivial trivial, Î» s, by simp [hx.to_has_basis.mem_iff]âŸ©

protected lemma comap {l : filter Î²} (h : l.is_countably_generated) (f : Î± â†’ Î²) :
  (comap f l).is_countably_generated :=
begin
  rcases h.exists_seq with âŸ¨x, hxâŸ©,
  apply is_countably_generated_of_seq,
  use Î» i, f â»Â¹' x i,
  calc
    comap f l = comap f (â¨… i, ğ“Ÿ (x i))   : by rw hx
          ... = (â¨… i, comap f $ ğ“Ÿ $ x i) : comap_infi
          ... = (â¨… i, ğ“Ÿ $ f â»Â¹' x i)     : by simp_rw comap_principal,
end

/-- An abstract version of continuity of sequentially continuous functions on metric spaces:
if a filter `k` is countably generated then `tendsto f k l` iff for every sequence `u`
converging to `k`, `f âˆ˜ u` tends to `l`. -/
lemma tendsto_iff_seq_tendsto {f : Î± â†’ Î²} {k : filter Î±} {l : filter Î²}
  (hcb : k.is_countably_generated) :
  tendsto f k l â†” (âˆ€ x : â„• â†’ Î±, tendsto x at_top k â†’ tendsto (f âˆ˜ x) at_top l) :=
suffices (âˆ€ x : â„• â†’ Î±, tendsto x at_top k â†’ tendsto (f âˆ˜ x) at_top l) â†’ tendsto f k l,
  from âŸ¨by intros; apply tendsto.comp; assumption, by assumptionâŸ©,
begin
  rcases hcb.exists_antimono_seq with âŸ¨g, gmon, gbasisâŸ©,
  have gbasis : âˆ€ A, A âˆˆ k â†” âˆƒ i, g i âŠ† A,
  { intro A,
    subst gbasis,
    rw mem_infi,
    { simp only [set.mem_Union, iff_self, filter.mem_principal_sets] },
    { exact directed_of_sup (Î» i j h, principal_mono.mpr $ gmon _ _ h) },
    { apply_instance } },
  classical, contrapose,
  simp only [not_forall, not_imp, not_exists, subset_def, @tendsto_def _ _ f, gbasis],
  rintro âŸ¨B, hBl, hfBkâŸ©,
  choose x h using hfBk,
  use x, split,
  { simp only [tendsto_at_top', gbasis],
    rintros A âŸ¨i, hgiAâŸ©,
    use i,
    refine (Î» j hj, hgiA $ gmon _ _ hj _),
    simp only [h] },
  { simp only [tendsto_at_top', (âˆ˜), not_forall, not_exists],
    use [B, hBl],
    intro i, use [i, (le_refl _)],
    apply (h i).right },
end

lemma tendsto_of_seq_tendsto {f : Î± â†’ Î²} {k : filter Î±} {l : filter Î²}
  (hcb : k.is_countably_generated) :
  (âˆ€ x : â„• â†’ Î±, tendsto x at_top k â†’ tendsto (f âˆ˜ x) at_top l) â†’ tendsto f k l :=
hcb.tendsto_iff_seq_tendsto.2

lemma subseq_tendsto {f : filter Î±} (hf : is_countably_generated f)
  {u : â„• â†’ Î±}
  (hx : ne_bot (f âŠ“ map u at_top)) :
  âˆƒ (Î¸ : â„• â†’ â„•), (strict_mono Î¸) âˆ§ (tendsto (u âˆ˜ Î¸) at_top f) :=
begin
  rcases hf.has_antimono_basis with âŸ¨B, hâŸ©,
  have : âˆ€ N, âˆƒ n â‰¥ N, u n âˆˆ B N,
    from Î» N, filter.inf_map_at_top_ne_bot_iff.mp hx _ (h.to_has_basis.mem_of_mem trivial) N,
  choose Ï† hÏ† using this,
  cases forall_and_distrib.mp hÏ† with Ï†_ge Ï†_in,
  have lim_uÏ† : tendsto (u âˆ˜ Ï†) at_top f,
    from h.tendsto Ï†_in,
  have lim_Ï† : tendsto Ï† at_top at_top,
    from (tendsto_at_top_mono Ï†_ge tendsto_id),
  obtain âŸ¨Ïˆ, hÏˆ, hÏˆÏ†âŸ© : âˆƒ Ïˆ : â„• â†’ â„•, strict_mono Ïˆ âˆ§ strict_mono (Ï† âˆ˜ Ïˆ),
    from strict_mono_subseq_of_tendsto_at_top lim_Ï†,
  exact âŸ¨Ï† âˆ˜ Ïˆ, hÏˆÏ†, lim_uÏ†.comp $ strict_mono_tendsto_at_top hÏˆâŸ©,
end

end is_countably_generated

-- TODO : prove this for a encodable type
lemma is_countably_generated_at_top_finset_nat : (at_top : filter $ finset â„•).is_countably_generated :=
begin
  apply is_countably_generated_of_seq,
  use Î» N, Ici (finset.range N),
  apply eq_infi_of_mem_sets_iff_exists_mem,
  assume s,
  rw mem_at_top_sets,
  refine âŸ¨_, Î» âŸ¨N, hNâŸ©, âŸ¨finset.range N, hNâŸ©âŸ©,
  rintros âŸ¨t, htâŸ©,
  rcases mem_at_top_sets.1 (tendsto_finset_range (mem_at_top t)) with âŸ¨N, hNâŸ©,
  simp only [preimage, mem_set_of_eq] at hN,
  exact âŸ¨N, mem_principal_sets.2 $ Î» t' ht', ht t' $ le_trans (hN _ $ le_refl N) ht'âŸ©
end
end filter
