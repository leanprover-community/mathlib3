/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import order.filter.basic
import data.set.countable
import data.pprod

/-!
# Filter bases

A filter basis `B : filter_basis Î±` on a type `Î±` is a nonempty collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element of
the collection. Compared to filters, filter bases do not require that any set containing
an element of `B` belongs to `B`.
A filter basis `B` can be used to construct `B.filter : filter Î±` such that a set belongs
to `B.filter` if and only if it contains an element of `B`.

Given an indexing type `Î¹`, a predicate `p : Î¹ â†’ Prop`, and a map `s : Î¹ â†’ set Î±`,
the proposition `h : filter.is_basis p s` makes sure the range of `s` bounded by `p`
(ie. `s '' set_of p`) defines a filter basis `h.filter_basis`.

If one already has a filter `l` on `Î±`, `filter.has_basis l p s` (where `p : Î¹ â†’ Prop`
and `s : Î¹ â†’ set Î±` as above) means that a set belongs to `l` if and
only if it contains some `s i` with `p i`. It implies `h : filter.is_basis p s`, and
`l = h.filter_basis.filter`. The point of this definition is that checking statements
involving elements of `l` often reduces to checking them on the basis elements.

We define a function `has_basis.index (h : filter.has_basis l p s) (t) (ht : t âˆˆ l)` that returns
some index `i` such that `p i` and `s i âŠ† t`. This function can be useful to avoid manual
destruction of `h.mem_iff.mpr ht` using `cases` or `let`.

This file also introduces more restricted classes of bases, involving monotonicity or
countability. In particular, for `l : filter Î±`, `l.is_countably_generated` means
there is a countable set of sets which generates `s`. This is reformulated in term of bases,
and consequences are derived.

## Main statements

* `has_basis.mem_iff`, `has_basis.mem_of_superset`, `has_basis.mem_of_mem` : restate `t âˆˆ f`
  in terms of a basis;
* `basis_sets` : all sets of a filter form a basis;
* `has_basis.inf`, `has_basis.inf_principal`, `has_basis.prod`, `has_basis.prod_self`,
  `has_basis.map`, `has_basis.comap` : combinators to construct filters of `l âŠ“ l'`,
  `l âŠ“ ğ“Ÿ t`, `l Ã—á¶  l'`, `l Ã—á¶  l`, `l.map f`, `l.comap f` respectively;
* `has_basis.le_iff`, `has_basis.ge_iff`, has_basis.le_basis_iff` : restate `l â‰¤ l'` in terms
  of bases.
* `has_basis.tendsto_right_iff`, `has_basis.tendsto_left_iff`, `has_basis.tendsto_iff` : restate
  `tendsto f l l'` in terms of bases.
* `is_countably_generated_iff_exists_antitone_basis` : proves a filter is
  countably generated if and only if it admits a basis parametrized by a
  decreasing sequence of sets indexed by `â„•`.
* `tendsto_iff_seq_tendsto ` : an abstract version of "sequentially continuous implies continuous".

## Implementation notes

As with `Union`/`bUnion`/`sUnion`, there are three different approaches to filter bases:

* `has_basis l s`, `s : set (set Î±)`;
* `has_basis l s`, `s : Î¹ â†’ set Î±`;
* `has_basis l p s`, `p : Î¹ â†’ Prop`, `s : Î¹ â†’ set Î±`.

We use the latter one because, e.g., `ğ“ x` in an `emetric_space` or in a `metric_space` has a basis
of this form. The other two can be emulated using `s = id` or `p = Î» _, true`.

With this approach sometimes one needs to `simp` the statement provided by the `has_basis`
machinery, e.g., `simp only [exists_prop, true_and]` or `simp only [forall_const]` can help
with the case `p = Î» _, true`.
-/

open set filter
open_locale filter classical

section sort

variables {Î± Î² Î³ : Type*} {Î¹ Î¹' : Sort*}

/-- A filter basis `B` on a type `Î±` is a nonempty collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element
of the collection. -/
structure filter_basis (Î± : Type*) :=
(sets                   : set (set Î±))
(nonempty               : sets.nonempty)
(inter_sets {x y}       : x âˆˆ sets â†’ y âˆˆ sets â†’ âˆƒ z âˆˆ sets, z âŠ† x âˆ© y)

instance filter_basis.nonempty_sets (B : filter_basis Î±) : nonempty B.sets := B.nonempty.to_subtype

/-- If `B` is a filter basis on `Î±`, and `U` a subset of `Î±` then we can write `U âˆˆ B` as
on paper. -/
@[reducible]
instance {Î± : Type*}: has_mem (set Î±) (filter_basis Î±) := âŸ¨Î» U B, U âˆˆ B.setsâŸ©

-- For illustration purposes, the filter basis defining (at_top : filter â„•)
instance : inhabited (filter_basis â„•) :=
âŸ¨{ sets := range Ici,
  nonempty := âŸ¨Ici 0, mem_range_self 0âŸ©,
  inter_sets := begin
    rintros _ _ âŸ¨n, rflâŸ© âŸ¨m, rflâŸ©,
    refine âŸ¨Ici (max n m), mem_range_self _, _âŸ©,
    rintros p p_in,
    split ; rw mem_Ici at *,
    exact le_of_max_le_left p_in,
    exact le_of_max_le_right p_in,
  end }âŸ©

/-- `is_basis p s` means the image of `s` bounded by `p` is a filter basis. -/
protected structure filter.is_basis (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±) : Prop :=
(nonempty : âˆƒ i, p i)
(inter : âˆ€ {i j}, p i â†’ p j â†’ âˆƒ k, p k âˆ§ s k âŠ† s i âˆ© s j)

namespace filter
namespace is_basis

/-- Constructs a filter basis from an indexed family of sets satisfying `is_basis`. -/
protected def filter_basis {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} (h : is_basis p s) : filter_basis Î± :=
{ sets := {t | âˆƒ i, p i âˆ§ s i = t},
  nonempty := let âŸ¨i, hiâŸ© := h.nonempty in âŸ¨s i, âŸ¨i, hi, rflâŸ©âŸ©,
  inter_sets := by { rintros _ _ âŸ¨i, hi, rflâŸ© âŸ¨j, hj, rflâŸ©,
                     rcases h.inter hi hj with âŸ¨k, hk, hk'âŸ©,
                     exact âŸ¨_, âŸ¨k, hk, rflâŸ©, hk'âŸ© } }

variables {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} (h : is_basis p s)

lemma mem_filter_basis_iff {U : set Î±} : U âˆˆ h.filter_basis â†” âˆƒ i, p i âˆ§ s i = U :=
iff.rfl
end is_basis
end filter

namespace filter_basis

/-- The filter associated to a filter basis. -/
protected def filter (B : filter_basis Î±) : filter Î± :=
{ sets := {s | âˆƒ t âˆˆ B, t âŠ† s},
  univ_sets := let âŸ¨s, s_inâŸ© := B.nonempty in âŸ¨s, s_in, s.subset_univâŸ©,
  sets_of_superset := Î» x y âŸ¨s, s_in, hâŸ© hxy, âŸ¨s, s_in, set.subset.trans h hxyâŸ©,
  inter_sets := Î» x y âŸ¨s, s_in, hsâŸ© âŸ¨t, t_in, htâŸ©,
                let âŸ¨u, u_in, u_subâŸ© := B.inter_sets s_in t_in in
                âŸ¨u, u_in, set.subset.trans u_sub $ set.inter_subset_inter hs htâŸ© }

lemma mem_filter_iff (B : filter_basis Î±) {U : set Î±} : U âˆˆ B.filter â†” âˆƒ s âˆˆ B, s âŠ† U :=
iff.rfl

lemma mem_filter_of_mem (B : filter_basis Î±) {U : set Î±} : U âˆˆ B â†’ U âˆˆ B.filter:=
Î» U_in, âŸ¨U, U_in, subset.refl _âŸ©

lemma eq_infi_principal (B : filter_basis Î±) : B.filter = â¨… s : B.sets, ğ“Ÿ s :=
begin
  have : directed (â‰¥) (Î» (s : B.sets), ğ“Ÿ (s : set Î±)),
  { rintros âŸ¨U, U_inâŸ© âŸ¨V, V_inâŸ©,
    rcases B.inter_sets U_in V_in with âŸ¨W, W_in, W_subâŸ©,
    use [W, W_in],
    simp only [ge_iff_le, le_principal_iff, mem_principal, subtype.coe_mk],
    exact subset_inter_iff.mp W_sub },
  ext U,
  simp [mem_filter_iff, mem_infi_of_directed this]
end

protected lemma generate (B : filter_basis Î±) : generate B.sets = B.filter :=
begin
  apply le_antisymm,
  { intros U U_in,
    rcases B.mem_filter_iff.mp U_in with âŸ¨V, V_in, hâŸ©,
    exact generate_sets.superset (generate_sets.basic V_in) h },
  { rw sets_iff_generate,
    apply mem_filter_of_mem }
end
end filter_basis

namespace filter
namespace is_basis
variables {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±}

/-- Constructs a filter from an indexed family of sets satisfying `is_basis`. -/
protected def filter (h : is_basis p s) : filter Î± := h.filter_basis.filter

protected lemma mem_filter_iff (h : is_basis p s) {U : set Î±} :
  U âˆˆ h.filter â†” âˆƒ i, p i âˆ§ s i âŠ† U :=
begin
  erw [h.filter_basis.mem_filter_iff],
  simp only [mem_filter_basis_iff h, exists_prop],
  split,
  { rintros âŸ¨_, âŸ¨i, pi, rflâŸ©, hâŸ©,
    tauto },
  { tauto }
end

lemma filter_eq_generate (h : is_basis p s) : h.filter = generate {U | âˆƒ i, p i âˆ§ s i = U} :=
by erw h.filter_basis.generate ; refl
end is_basis

/-- We say that a filter `l` has a basis `s : Î¹ â†’ set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`. -/
protected structure has_basis (l : filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±) : Prop :=
(mem_iff' : âˆ€ (t : set Î±), t âˆˆ l â†” âˆƒ i (hi : p i), s i âŠ† t)

section same_type

variables {l l' : filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {t : set Î±} {i : Î¹}
  {p' : Î¹' â†’ Prop} {s' : Î¹' â†’ set Î±} {i' : Î¹'}

lemma has_basis_generate (s : set (set Î±)) :
  (generate s).has_basis (Î» t, finite t âˆ§ t âŠ† s) (Î» t, â‹‚â‚€ t) :=
âŸ¨begin
  intro U,
  rw mem_generate_iff,
  apply exists_congr,
  tauto
endâŸ©

/-- The smallest filter basis containing a given collection of sets. -/
def filter_basis.of_sets (s : set (set Î±)) : filter_basis Î± :=
{ sets := sInter '' { t | finite t âˆ§ t âŠ† s},
  nonempty := âŸ¨univ, âˆ…, âŸ¨âŸ¨finite_empty, empty_subset sâŸ©, sInter_emptyâŸ©âŸ©,
  inter_sets := begin
    rintros _ _ âŸ¨a, âŸ¨fina, subaâŸ©, rflâŸ© âŸ¨b, âŸ¨finb, subbâŸ©, rflâŸ©,
    exact âŸ¨â‹‚â‚€ (a âˆª b), mem_image_of_mem _ âŸ¨fina.union finb, union_subset suba subbâŸ©,
           by rw sInter_unionâŸ©,
  end }

/-- Definition of `has_basis` unfolded with implicit set argument. -/
lemma has_basis.mem_iff (hl : l.has_basis p s) : t âˆˆ l â†” âˆƒ i (hi : p i), s i âŠ† t :=
hl.mem_iff' t

lemma has_basis.eq_of_same_basis (hl : l.has_basis p s) (hl' : l'.has_basis p s) : l = l' :=
begin
  ext t,
  rw [hl.mem_iff, hl'.mem_iff]
end

lemma has_basis_iff : l.has_basis p s â†” âˆ€ t, t âˆˆ l â†” âˆƒ i (hi : p i), s i âŠ† t :=
âŸ¨Î» âŸ¨hâŸ©, h, Î» h, âŸ¨hâŸ©âŸ©

lemma has_basis.ex_mem (h : l.has_basis p s) : âˆƒ i, p i :=
let âŸ¨i, pi, hâŸ© := h.mem_iff.mp univ_mem in âŸ¨i, piâŸ©

protected lemma has_basis.nonempty (h : l.has_basis p s) : nonempty Î¹ :=
nonempty_of_exists h.ex_mem

protected lemma is_basis.has_basis (h : is_basis p s) : has_basis h.filter p s :=
âŸ¨Î» t, by simp only [h.mem_filter_iff, exists_prop]âŸ©

lemma has_basis.mem_of_superset (hl : l.has_basis p s) (hi : p i) (ht : s i âŠ† t) : t âˆˆ l :=
(hl.mem_iff).2 âŸ¨i, hi, htâŸ©

lemma has_basis.mem_of_mem (hl : l.has_basis p s) (hi : p i) : s i âˆˆ l :=
hl.mem_of_superset hi $ subset.refl _

/-- Index of a basis set such that `s i âŠ† t` as an element of `subtype p`. -/
noncomputable def has_basis.index (h : l.has_basis p s) (t : set Î±) (ht : t âˆˆ l) :
  {i : Î¹ // p i} :=
âŸ¨(h.mem_iff.1 ht).some, (h.mem_iff.1 ht).some_spec.fstâŸ©

lemma has_basis.property_index (h : l.has_basis p s) (ht : t âˆˆ l) : p (h.index t ht) :=
(h.index t ht).2

lemma has_basis.set_index_mem (h : l.has_basis p s) (ht : t âˆˆ l) : s (h.index t ht) âˆˆ l :=
h.mem_of_mem $ h.property_index _

lemma has_basis.set_index_subset (h : l.has_basis p s) (ht : t âˆˆ l) : s (h.index t ht) âŠ† t :=
(h.mem_iff.1 ht).some_spec.snd

lemma has_basis.is_basis (h : l.has_basis p s) : is_basis p s :=
{ nonempty := let âŸ¨i, hi, HâŸ© := h.mem_iff.mp univ_mem in âŸ¨i, hiâŸ©,
  inter := Î» i j hi hj, by simpa [h.mem_iff]
    using l.inter_sets (h.mem_of_mem hi) (h.mem_of_mem hj) }

lemma has_basis.filter_eq (h : l.has_basis p s) : h.is_basis.filter = l :=
by { ext U, simp [h.mem_iff, is_basis.mem_filter_iff] }

lemma has_basis.eq_generate (h : l.has_basis p s) : l = generate { U | âˆƒ i, p i âˆ§ s i = U } :=
by rw [â† h.is_basis.filter_eq_generate, h.filter_eq]

lemma generate_eq_generate_inter (s : set (set Î±)) :
  generate s = generate (sInter '' { t | finite t âˆ§ t âŠ† s}) :=
by erw [(filter_basis.of_sets s).generate, â† (has_basis_generate s).filter_eq] ; refl

lemma of_sets_filter_eq_generate (s : set (set Î±)) : (filter_basis.of_sets s).filter = generate s :=
by rw [â† (filter_basis.of_sets s).generate, generate_eq_generate_inter s] ; refl

protected lemma _root_.filter_basis.has_basis {Î± : Type*} (B : filter_basis Î±) :
  has_basis (B.filter) (Î» s : set Î±, s âˆˆ B) id :=
âŸ¨Î» t, B.mem_filter_iffâŸ©

lemma has_basis.to_has_basis' (hl : l.has_basis p s) (h : âˆ€ i, p i â†’ âˆƒ i', p' i' âˆ§ s' i' âŠ† s i)
  (h' : âˆ€ i', p' i' â†’ s' i' âˆˆ l) : l.has_basis p' s' :=
begin
  refine âŸ¨Î» t, âŸ¨Î» ht, _, Î» âŸ¨i', hi', htâŸ©, mem_of_superset (h' i' hi') htâŸ©âŸ©,
  rcases hl.mem_iff.1 ht with âŸ¨i, hi, htâŸ©,
  rcases h i hi with âŸ¨i', hi', hs'sâŸ©,
  exact âŸ¨i', hi', subset.trans hs's htâŸ©
end

lemma has_basis.to_has_basis (hl : l.has_basis p s) (h : âˆ€ i, p i â†’ âˆƒ i', p' i' âˆ§ s' i' âŠ† s i)
  (h' : âˆ€ i', p' i' â†’ âˆƒ i, p i âˆ§ s i âŠ† s' i') : l.has_basis p' s' :=
hl.to_has_basis' h $ Î» i' hi', let âŸ¨i, hi, hss'âŸ© := h' i' hi' in hl.mem_iff.2 âŸ¨i, hi, hss'âŸ©

lemma has_basis.to_subset (hl : l.has_basis p s) {t : Î¹ â†’ set Î±} (h : âˆ€ i, p i â†’ t i âŠ† s i)
  (ht : âˆ€ i, p i â†’ t i âˆˆ l) : l.has_basis p t :=
hl.to_has_basis' (Î» i hi, âŸ¨i, hi, h i hiâŸ©) ht

lemma has_basis.eventually_iff (hl : l.has_basis p s) {q : Î± â†’ Prop} :
  (âˆ€á¶  x in l, q x) â†” âˆƒ i, p i âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s i â†’ q x :=
by simpa using hl.mem_iff

lemma has_basis.frequently_iff (hl : l.has_basis p s) {q : Î± â†’ Prop} :
  (âˆƒá¶  x in l, q x) â†” âˆ€ i, p i â†’ âˆƒ x âˆˆ s i, q x :=
by simp [filter.frequently, hl.eventually_iff]

lemma has_basis.exists_iff (hl : l.has_basis p s) {P : set Î± â†’ Prop}
  (mono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ P t â†’ P s) :
  (âˆƒ s âˆˆ l, P s) â†” âˆƒ (i) (hi : p i), P (s i) :=
âŸ¨Î» âŸ¨s, hs, hPâŸ©, let âŸ¨i, hi, hisâŸ© := hl.mem_iff.1 hs in âŸ¨i, hi, mono his hPâŸ©,
  Î» âŸ¨i, hi, hPâŸ©, âŸ¨s i, hl.mem_of_mem hi, hPâŸ©âŸ©

lemma has_basis.forall_iff (hl : l.has_basis p s) {P : set Î± â†’ Prop}
  (mono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ P s â†’ P t) :
  (âˆ€ s âˆˆ l, P s) â†” âˆ€ i, p i â†’ P (s i) :=
âŸ¨Î» H i hi, H (s i) $ hl.mem_of_mem hi,
  Î» H s hs, let âŸ¨i, hi, hisâŸ© := hl.mem_iff.1 hs in mono his (H i hi)âŸ©

lemma has_basis.ne_bot_iff (hl : l.has_basis p s) :
  ne_bot l â†” (âˆ€ {i}, p i â†’ (s i).nonempty) :=
forall_mem_nonempty_iff_ne_bot.symm.trans $ hl.forall_iff $ Î» _ _, nonempty.mono

lemma has_basis.eq_bot_iff (hl : l.has_basis p s) :
  l = âŠ¥ â†” âˆƒ i, p i âˆ§ s i = âˆ… :=
not_iff_not.1 $ ne_bot_iff.symm.trans $ hl.ne_bot_iff.trans $
by simp only [not_exists, not_and, â† ne_empty_iff_nonempty]

lemma basis_sets (l : filter Î±) : l.has_basis (Î» s : set Î±, s âˆˆ l) id :=
âŸ¨Î» t, exists_mem_subset_iff.symmâŸ©

lemma has_basis_self {l : filter Î±} {P : set Î± â†’ Prop} :
  has_basis l (Î» s, s âˆˆ l âˆ§ P s) id â†” âˆ€ t âˆˆ l, âˆƒ r âˆˆ l, P r âˆ§ r âŠ† t :=
begin
  simp only [has_basis_iff, exists_prop, id, and_assoc],
  exact forall_congr (Î» s, âŸ¨Î» h, h.1, Î» h, âŸ¨h, Î» âŸ¨t, hl, hP, htsâŸ©, mem_of_superset hl htsâŸ©âŸ©)
end

/-- If `{s i | p i}` is a basis of a filter `l` and each `s i` includes `s j` such that
`p j âˆ§ q j`, then `{s j | p j âˆ§ q j}` is a basis of `l`. -/
lemma has_basis.restrict (h : l.has_basis p s) {q : Î¹ â†’ Prop}
  (hq : âˆ€ i, p i â†’ âˆƒ j, p j âˆ§ q j âˆ§ s j âŠ† s i) :
  l.has_basis (Î» i, p i âˆ§ q i) s :=
begin
  refine âŸ¨Î» t, âŸ¨Î» ht, _, Î» âŸ¨i, hpi, htiâŸ©, h.mem_iff.2 âŸ¨i, hpi.1, htiâŸ©âŸ©âŸ©,
  rcases h.mem_iff.1 ht with âŸ¨i, hpi, htiâŸ©,
  rcases hq i hpi with âŸ¨j, hpj, hqj, hjiâŸ©,
  exact âŸ¨j, âŸ¨hpj, hqjâŸ©, subset.trans hji htiâŸ©
end

/-- If `{s i | p i}` is a basis of a filter `l` and `V âˆˆ l`, then `{s i | p i âˆ§ s i âŠ† V}`
is a basis of `l`. -/
lemma has_basis.restrict_subset (h : l.has_basis p s) {V : set Î±} (hV : V âˆˆ l) :
  l.has_basis (Î» i, p i âˆ§ s i âŠ† V) s :=
h.restrict $ Î» i hi, (h.mem_iff.1 (inter_mem hV (h.mem_of_mem hi))).imp $
  Î» j hj, âŸ¨hj.fst, subset_inter_iff.1 hj.sndâŸ©

lemma has_basis.has_basis_self_subset {p : set Î± â†’ Prop} (h : l.has_basis (Î» s, s âˆˆ l âˆ§ p s) id)
  {V : set Î±} (hV : V âˆˆ l) : l.has_basis (Î» s, s âˆˆ l âˆ§ p s âˆ§ s âŠ† V) id :=
by simpa only [and_assoc] using h.restrict_subset hV

theorem has_basis.ge_iff (hl' : l'.has_basis p' s')  : l â‰¤ l' â†” âˆ€ i', p' i' â†’ s' i' âˆˆ l :=
âŸ¨Î» h i' hi', h $ hl'.mem_of_mem hi',
  Î» h s hs, let âŸ¨i', hi', hsâŸ© := hl'.mem_iff.1 hs in mem_of_superset (h _ hi') hsâŸ©

theorem has_basis.le_iff (hl : l.has_basis p s) : l â‰¤ l' â†” âˆ€ t âˆˆ l', âˆƒ i (hi : p i), s i âŠ† t :=
by simp only [le_def, hl.mem_iff]

theorem has_basis.le_basis_iff (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  l â‰¤ l' â†” âˆ€ i', p' i' â†’ âˆƒ i (hi : p i), s i âŠ† s' i' :=
by simp only [hl'.ge_iff, hl.mem_iff]

lemma has_basis.ext (hl : l.has_basis p s) (hl' : l'.has_basis p' s')
  (h : âˆ€ i, p i â†’ âˆƒ i', p' i' âˆ§ s' i' âŠ† s i)
  (h' : âˆ€ i', p' i' â†’ âˆƒ i, p i âˆ§ s i âŠ† s' i') : l = l' :=
begin
  apply le_antisymm,
  { rw hl.le_basis_iff hl',
    simpa using h' },
  { rw hl'.le_basis_iff hl,
    simpa using h },
end

lemma has_basis.inf' (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  (l âŠ“ l').has_basis (Î» i : pprod Î¹ Î¹', p i.1 âˆ§ p' i.2) (Î» i, s i.1 âˆ© s' i.2) :=
âŸ¨begin
  intro t,
  split,
  { simp only [mem_inf_iff, exists_prop, hl.mem_iff, hl'.mem_iff],
    rintros âŸ¨t, âŸ¨i, hi, htâŸ©, t', âŸ¨i', hi', ht'âŸ©, rflâŸ©,
    use [âŸ¨i, i'âŸ©, âŸ¨hi, hi'âŸ©, inter_subset_inter ht ht'] },
  { rintros âŸ¨âŸ¨i, i'âŸ©, âŸ¨hi, hi'âŸ©, HâŸ©,
    exact mem_inf_of_inter (hl.mem_of_mem hi) (hl'.mem_of_mem hi') H }
endâŸ©

lemma has_basis.inf {Î¹ Î¹' : Type*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {p' : Î¹' â†’ Prop}
  {s' : Î¹' â†’ set Î±} (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  (l âŠ“ l').has_basis (Î» i : Î¹ Ã— Î¹', p i.1 âˆ§ p' i.2) (Î» i, s i.1 âˆ© s' i.2) :=
(hl.inf' hl').to_has_basis (Î» i hi, âŸ¨âŸ¨i.1, i.2âŸ©, hi, subset.rflâŸ©)
  (Î» i hi, âŸ¨âŸ¨i.1, i.2âŸ©, hi, subset.rflâŸ©)

lemma has_basis_principal (t : set Î±) : (ğ“Ÿ t).has_basis (Î» i : unit, true) (Î» i, t) :=
âŸ¨Î» U, by simpâŸ©

lemma has_basis_pure (x : Î±) : (pure x : filter Î±).has_basis (Î» i : unit, true) (Î» i, {x}) :=
by simp only [â† principal_singleton, has_basis_principal]

lemma has_basis.sup' (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  (l âŠ” l').has_basis (Î» i : pprod Î¹ Î¹', p i.1 âˆ§ p' i.2) (Î» i, s i.1 âˆª s' i.2) :=
âŸ¨begin
  intros t,
  simp only [mem_sup, hl.mem_iff, hl'.mem_iff, pprod.exists, union_subset_iff, exists_prop,
    and_assoc, exists_and_distrib_left],
  simp only [â† and_assoc, exists_and_distrib_right, and_comm]
endâŸ©

lemma has_basis.sup {Î¹ Î¹' : Type*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {p' : Î¹' â†’ Prop}
  {s' : Î¹' â†’ set Î±} (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  (l âŠ” l').has_basis (Î» i : Î¹ Ã— Î¹', p i.1 âˆ§ p' i.2) (Î» i, s i.1 âˆª s' i.2) :=
(hl.sup' hl').to_has_basis (Î» i hi, âŸ¨âŸ¨i.1, i.2âŸ©, hi, subset.rflâŸ©)
  (Î» i hi, âŸ¨âŸ¨i.1, i.2âŸ©, hi, subset.rflâŸ©)

lemma has_basis_supr {Î¹ : Sort*} {Î¹' : Î¹ â†’ Type*} {l : Î¹ â†’ filter Î±}
  {p : Î  i, Î¹' i â†’ Prop} {s : Î  i, Î¹' i â†’ set Î±} (hl : âˆ€ i, (l i).has_basis (p i) (s i)) :
  (â¨† i, l i).has_basis (Î» f : Î  i, Î¹' i, âˆ€ i, p i (f i)) (Î» f : Î  i, Î¹' i, â‹ƒ i, s i (f i)) :=
has_basis_iff.mpr $ Î» t, by simp only [has_basis_iff, (hl _).mem_iff, classical.skolem,
  forall_and_distrib, Union_subset_iff, mem_supr]

lemma has_basis.sup_principal (hl : l.has_basis p s) (t : set Î±) :
  (l âŠ” ğ“Ÿ t).has_basis p (Î» i, s i âˆª t) :=
âŸ¨Î» u, by simp only [(hl.sup' (has_basis_principal t)).mem_iff, pprod.exists, exists_prop, and_true,
  unique.exists_iff]âŸ©

lemma has_basis.sup_pure (hl : l.has_basis p s) (x : Î±) :
  (l âŠ” pure x).has_basis p (Î» i, s i âˆª {x}) :=
by simp only [â† principal_singleton, hl.sup_principal]

lemma has_basis.inf_principal (hl : l.has_basis p s) (s' : set Î±) :
  (l âŠ“ ğ“Ÿ s').has_basis p (Î» i, s i âˆ© s') :=
âŸ¨Î» t, by simp only [mem_inf_principal, hl.mem_iff, subset_def, mem_set_of_eq,
  mem_inter_iff, and_imp]âŸ©

lemma has_basis.inf_basis_ne_bot_iff (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  ne_bot (l âŠ“ l') â†” âˆ€ â¦ƒiâ¦„ (hi : p i) â¦ƒi'â¦„ (hi' : p' i'), (s i âˆ© s' i').nonempty :=
(hl.inf' hl').ne_bot_iff.trans $ by simp [@forall_swap _ Î¹']

lemma has_basis.inf_ne_bot_iff (hl : l.has_basis p s) :
  ne_bot (l âŠ“ l') â†” âˆ€ â¦ƒiâ¦„ (hi : p i) â¦ƒs'â¦„ (hs' : s' âˆˆ l'), (s i âˆ© s').nonempty :=
hl.inf_basis_ne_bot_iff l'.basis_sets

lemma has_basis.inf_principal_ne_bot_iff (hl : l.has_basis p s) {t : set Î±} :
  ne_bot (l âŠ“ ğ“Ÿ t) â†” âˆ€ â¦ƒiâ¦„ (hi : p i), (s i âˆ© t).nonempty :=
(hl.inf_principal t).ne_bot_iff

lemma inf_ne_bot_iff :
  ne_bot (l âŠ“ l') â†” âˆ€ â¦ƒs : set Î±â¦„ (hs : s âˆˆ l) â¦ƒs'â¦„ (hs' : s' âˆˆ l'), (s âˆ© s').nonempty :=
l.basis_sets.inf_ne_bot_iff

lemma inf_principal_ne_bot_iff {s : set Î±} :
  ne_bot (l âŠ“ ğ“Ÿ s) â†” âˆ€ U âˆˆ l, (U âˆ© s).nonempty :=
l.basis_sets.inf_principal_ne_bot_iff

lemma inf_eq_bot_iff {f g : filter Î±} :
  f âŠ“ g = âŠ¥ â†” âˆƒ (U âˆˆ f) (V âˆˆ g), U âˆ© V = âˆ… :=
not_iff_not.1 $ ne_bot_iff.symm.trans $ inf_ne_bot_iff.trans $
by simp [â† ne_empty_iff_nonempty]

protected lemma disjoint_iff {f g : filter Î±} :
  disjoint f g â†” âˆƒ (U âˆˆ f) (V âˆˆ g), U âˆ© V = âˆ… :=
disjoint_iff.trans inf_eq_bot_iff

lemma mem_iff_inf_principal_compl {f : filter Î±} {s : set Î±} :
  s âˆˆ f â†” f âŠ“ ğ“Ÿ sá¶œ = âŠ¥ :=
begin
  refine not_iff_not.1 ((inf_principal_ne_bot_iff.trans _).symm.trans ne_bot_iff),
  exact âŸ¨Î» h hs, by simpa [empty_not_nonempty] using h s hs,
    Î» hs t ht, inter_compl_nonempty_iff.2 $ Î» hts, hs $ mem_of_superset ht htsâŸ©,
end

lemma not_mem_iff_inf_principal_compl {f : filter Î±} {s : set Î±} :
  s âˆ‰ f â†” ne_bot (f âŠ“ ğ“Ÿ sá¶œ) :=
(not_congr mem_iff_inf_principal_compl).trans ne_bot_iff.symm

lemma mem_iff_disjoint_principal_compl {f : filter Î±} {s : set Î±} :
  s âˆˆ f â†” disjoint f (ğ“Ÿ sá¶œ) :=
mem_iff_inf_principal_compl.trans disjoint_iff.symm

lemma le_iff_forall_disjoint_principal_compl {f g : filter Î±} :
  f â‰¤ g â†” âˆ€ V âˆˆ g, disjoint f (ğ“Ÿ Vá¶œ) :=
forall_congr $ Î» _, forall_congr $ Î» _, mem_iff_disjoint_principal_compl

lemma le_iff_forall_inf_principal_compl {f g : filter Î±} :
  f â‰¤ g â†” âˆ€ V âˆˆ g, f âŠ“ ğ“Ÿ Vá¶œ = âŠ¥ :=
forall_congr $ Î» _, forall_congr $ Î» _, mem_iff_inf_principal_compl

lemma inf_ne_bot_iff_frequently_left {f g : filter Î±} :
  ne_bot (f âŠ“ g) â†” âˆ€ {p : Î± â†’ Prop}, (âˆ€á¶  x in f, p x) â†’ âˆƒá¶  x in g, p x :=
by simpa only [inf_ne_bot_iff, frequently_iff, exists_prop, and_comm]

lemma inf_ne_bot_iff_frequently_right {f g : filter Î±} :
  ne_bot (f âŠ“ g) â†” âˆ€ {p : Î± â†’ Prop}, (âˆ€á¶  x in g, p x) â†’ âˆƒá¶  x in f, p x :=
by { rw inf_comm, exact inf_ne_bot_iff_frequently_left }

lemma has_basis.eq_binfi (h : l.has_basis p s) :
  l = â¨… i (_ : p i), ğ“Ÿ (s i) :=
eq_binfi_of_mem_iff_exists_mem $ Î» t, by simp only [h.mem_iff, mem_principal]

lemma has_basis.eq_infi (h : l.has_basis (Î» _, true) s) :
  l = â¨… i, ğ“Ÿ (s i) :=
by simpa only [infi_true] using h.eq_binfi

lemma has_basis_infi_principal {s : Î¹ â†’ set Î±} (h : directed (â‰¥) s) [nonempty Î¹] :
  (â¨… i, ğ“Ÿ (s i)).has_basis (Î» _, true) s :=
âŸ¨begin
  refine Î» t, (mem_infi_of_directed (h.mono_comp _ _) t).trans $
    by simp only [exists_prop, true_and, mem_principal],
  exact Î» _ _, principal_mono.2
endâŸ©

/-- If `s : Î¹ â†’ set Î±` is an indexed family of sets, then finite intersections of `s i` form a basis
of `â¨… i, ğ“Ÿ (s i)`.  -/
lemma has_basis_infi_principal_finite {Î¹ : Type*} (s : Î¹ â†’ set Î±) :
  (â¨… i, ğ“Ÿ (s i)).has_basis (Î» t : set Î¹, finite t) (Î» t, â‹‚ i âˆˆ t, s i) :=
begin
  refine âŸ¨Î» U, (mem_infi_finite _).trans _âŸ©,
  simp only [infi_principal_finset, mem_Union, mem_principal, exists_prop,
    exists_finite_iff_finset, finset.set_bInter_coe]
end

lemma has_basis_binfi_principal {s : Î² â†’ set Î±} {S : set Î²} (h : directed_on (s â»Â¹'o (â‰¥)) S)
  (ne : S.nonempty) :
  (â¨… i âˆˆ S, ğ“Ÿ (s i)).has_basis (Î» i, i âˆˆ S) s :=
âŸ¨begin
  refine Î» t, (mem_binfi_of_directed _ ne).trans $ by simp only [mem_principal],
  rw [directed_on_iff_directed, â† directed_comp, (âˆ˜)] at h âŠ¢,
  apply h.mono_comp _ _,
  exact Î» _ _, principal_mono.2
endâŸ©

lemma has_basis_binfi_principal' {Î¹ : Type*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±}
  (h : âˆ€ i, p i â†’ âˆ€ j, p j â†’ âˆƒ k (h : p k), s k âŠ† s i âˆ§ s k âŠ† s j) (ne : âˆƒ i, p i) :
  (â¨… i (h : p i), ğ“Ÿ (s i)).has_basis p s :=
filter.has_basis_binfi_principal h ne

lemma has_basis.map (f : Î± â†’ Î²) (hl : l.has_basis p s) :
  (l.map f).has_basis p (Î» i, f '' (s i)) :=
âŸ¨Î» t, by simp only [mem_map, image_subset_iff, hl.mem_iff, preimage]âŸ©

lemma has_basis.comap (f : Î² â†’ Î±) (hl : l.has_basis p s) :
  (l.comap f).has_basis p (Î» i, f â»Â¹' (s i)) :=
âŸ¨begin
  intro t,
  simp only [mem_comap, exists_prop, hl.mem_iff],
  split,
  { rintros âŸ¨t', âŸ¨i, hi, ht'âŸ©, HâŸ©,
    exact âŸ¨i, hi, subset.trans (preimage_mono ht') HâŸ© },
  { rintros âŸ¨i, hi, HâŸ©,
    exact âŸ¨s i, âŸ¨i, hi, subset.refl _âŸ©, HâŸ© }
endâŸ©

lemma comap_has_basis (f : Î± â†’ Î²) (l : filter Î²) :
  has_basis (comap f l) (Î» s : set Î², s âˆˆ l) (Î» s, f â»Â¹' s) :=
âŸ¨Î» t, mem_comapâŸ©

lemma has_basis.prod_self (hl : l.has_basis p s) :
  (l Ã—á¶  l).has_basis p (Î» i, (s i).prod (s i)) :=
âŸ¨begin
  intro t,
  apply mem_prod_iff.trans,
  split,
  { rintros âŸ¨tâ‚, htâ‚, tâ‚‚, htâ‚‚, HâŸ©,
    rcases hl.mem_iff.1 (inter_mem htâ‚ htâ‚‚) with âŸ¨i, hi, htâŸ©,
    exact âŸ¨i, hi, Î» p âŸ¨hpâ‚, hpâ‚‚âŸ©, H âŸ¨(ht hpâ‚).1, (ht hpâ‚‚).2âŸ©âŸ© },
  { rintros âŸ¨i, hi, HâŸ©,
    exact âŸ¨s i, hl.mem_of_mem hi, s i, hl.mem_of_mem hi, HâŸ© }
endâŸ©

lemma mem_prod_self_iff {s} : s âˆˆ l Ã—á¶  l â†” âˆƒ t âˆˆ l, set.prod t t âŠ† s :=
l.basis_sets.prod_self.mem_iff

lemma has_basis.sInter_sets (h : has_basis l p s) :
  â‹‚â‚€ l.sets = â‹‚ i (hi : p i), s i :=
begin
  ext x,
  suffices : (âˆ€ t âˆˆ l, x âˆˆ t) â†” âˆ€ i, p i â†’ x âˆˆ s i,
    by simpa only [mem_Inter, mem_set_of_eq, mem_sInter],
  simp_rw h.mem_iff,
  split,
  { intros h i hi,
    exact h (s i) âŸ¨i, hi, subset.refl _âŸ© },
  { rintros h _ âŸ¨i, hi, subâŸ©,
    exact sub (h i hi) },
end

variables {Î¹'' : Type*} [preorder Î¹''] (l) (s'' : Î¹'' â†’ set Î±)

/-- `is_antitone_basis s` means the image of `s` is a filter basis such that `s` is decreasing. -/
@[protect_proj] structure is_antitone_basis extends is_basis (Î» _, true) s'' : Prop :=
(antitone : antitone s'')

/-- We say that a filter `l` has an antitone basis `s : Î¹ â†’ set Î±`, if `t âˆˆ l` if and only if `t`
includes `s i` for some `i`, and `s` is decreasing. -/
@[protect_proj] structure has_antitone_basis (l : filter Î±) (s : Î¹'' â†’ set Î±)
  extends has_basis l (Î» _, true) s : Prop :=
(antitone : antitone s)

end same_type

section two_types

variables {la : filter Î±} {pa : Î¹ â†’ Prop} {sa : Î¹ â†’ set Î±}
  {lb : filter Î²} {pb : Î¹' â†’ Prop} {sb : Î¹' â†’ set Î²} {f : Î± â†’ Î²}

lemma has_basis.tendsto_left_iff (hla : la.has_basis pa sa) :
  tendsto f la lb â†” âˆ€ t âˆˆ lb, âˆƒ i (hi : pa i), maps_to f (sa i) t :=
by { simp only [tendsto, (hla.map f).le_iff, image_subset_iff], refl }

lemma has_basis.tendsto_right_iff (hlb : lb.has_basis pb sb) :
  tendsto f la lb â†” âˆ€ i (hi : pb i), âˆ€á¶  x in la, f x âˆˆ sb i :=
by simpa only [tendsto, hlb.ge_iff, mem_map, filter.eventually]

lemma has_basis.tendsto_iff (hla : la.has_basis pa sa) (hlb : lb.has_basis pb sb) :
  tendsto f la lb â†” âˆ€ ib (hib : pb ib), âˆƒ ia (hia : pa ia), âˆ€ x âˆˆ sa ia, f x âˆˆ sb ib :=
by simp [hlb.tendsto_right_iff, hla.eventually_iff]

lemma tendsto.basis_left (H : tendsto f la lb) (hla : la.has_basis pa sa) :
  âˆ€ t âˆˆ lb, âˆƒ i (hi : pa i), maps_to f (sa i) t :=
hla.tendsto_left_iff.1 H

lemma tendsto.basis_right (H : tendsto f la lb) (hlb : lb.has_basis pb sb) :
  âˆ€ i (hi : pb i), âˆ€á¶  x in la, f x âˆˆ sb i :=
hlb.tendsto_right_iff.1 H

lemma tendsto.basis_both (H : tendsto f la lb) (hla : la.has_basis pa sa)
  (hlb : lb.has_basis pb sb) :
  âˆ€ ib (hib : pb ib), âˆƒ ia (hia : pa ia), âˆ€ x âˆˆ sa ia, f x âˆˆ sb ib :=
(hla.tendsto_iff hlb).1 H

lemma has_basis.prod'' (hla : la.has_basis pa sa) (hlb : lb.has_basis pb sb) :
  (la Ã—á¶  lb).has_basis (Î» i : pprod Î¹ Î¹', pa i.1 âˆ§ pb i.2) (Î» i, (sa i.1).prod (sb i.2)) :=
(hla.comap prod.fst).inf' (hlb.comap prod.snd)

lemma has_basis.prod {Î¹ Î¹' : Type*} {pa : Î¹ â†’ Prop} {sa : Î¹ â†’ set Î±} {pb : Î¹' â†’ Prop}
  {sb : Î¹' â†’ set Î²} (hla : la.has_basis pa sa) (hlb : lb.has_basis pb sb) :
  (la Ã—á¶  lb).has_basis (Î» i : Î¹ Ã— Î¹', pa i.1 âˆ§ pb i.2) (Î» i, (sa i.1).prod (sb i.2)) :=
(hla.comap prod.fst).inf (hlb.comap prod.snd)

lemma has_basis.prod' {la : filter Î±} {lb : filter Î²} {Î¹ : Type*} {p : Î¹ â†’ Prop}
  {sa : Î¹ â†’ set Î±} {sb : Î¹ â†’ set Î²}
  (hla : la.has_basis p sa) (hlb : lb.has_basis p sb)
  (h_dir : âˆ€ {i j}, p i â†’ p j â†’ âˆƒ k, p k âˆ§ sa k âŠ† sa i âˆ§ sb k âŠ† sb j) :
  (la Ã—á¶  lb).has_basis p (Î» i, (sa i).prod (sb i)) :=
begin
  simp only [has_basis_iff, (hla.prod hlb).mem_iff],
  refine Î» t, âŸ¨_, _âŸ©,
  { rintros âŸ¨âŸ¨i, jâŸ©, âŸ¨hi, hjâŸ©, hsub : (sa i).prod (sb j) âŠ† tâŸ©,
    rcases h_dir hi hj with âŸ¨k, hk, ki, kjâŸ©,
    exact âŸ¨k, hk, (set.prod_mono ki kj).trans hsubâŸ© },
  { rintro âŸ¨i, hi, hâŸ©,
    exact âŸ¨âŸ¨i, iâŸ©, âŸ¨hi, hiâŸ©, hâŸ© },
end

end two_types

end filter

end sort

namespace filter

variables {Î± Î² Î³ Î¹ Î¹' : Type*}

/-- `is_countably_generated f` means `f = generate s` for some countable `s`. -/
class is_countably_generated (f : filter Î±) : Prop :=
(out [] : âˆƒ s : set (set Î±), countable s âˆ§ f = generate s)

/-- `is_countable_basis p s` means the image of `s` bounded by `p` is a countable filter basis. -/
structure is_countable_basis (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±) extends is_basis p s : Prop :=
(countable : countable $ set_of p)

/-- We say that a filter `l` has a countable basis `s : Î¹ â†’ set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`, and the set
defined by `p` is countable. -/
structure has_countable_basis (l : filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±)
  extends has_basis l p s : Prop :=
(countable : countable $ set_of p)

/-- A countable filter basis `B` on a type `Î±` is a nonempty countable collection of sets of `Î±`
such that the intersection of two elements of this collection contains some element
of the collection. -/
structure countable_filter_basis (Î± : Type*) extends filter_basis Î± :=
(countable : countable sets)

-- For illustration purposes, the countable filter basis defining (at_top : filter â„•)
instance nat.inhabited_countable_filter_basis : inhabited (countable_filter_basis â„•) :=
âŸ¨{ countable := countable_range (Î» n, Ici n),
   ..(default $ filter_basis â„•),}âŸ©

lemma has_countable_basis.is_countably_generated {f : filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±}
  (h : f.has_countable_basis p s) :
  f.is_countably_generated :=
âŸ¨âŸ¨{t | âˆƒ i, p i âˆ§ s i = t}, h.countable.image s, h.to_has_basis.eq_generateâŸ©âŸ©

lemma antitone_seq_of_seq (s : â„• â†’ set Î±) :
  âˆƒ t : â„• â†’ set Î±, antitone t âˆ§ (â¨… i, ğ“Ÿ $ s i) = â¨… i, ğ“Ÿ (t i) :=
begin
  use Î» n, â‹‚ m â‰¤ n, s m, split,
  { exact Î» i j hij, bInter_mono' (Iic_subset_Iic.2 hij) (Î» n hn, subset.refl _) },
  apply le_antisymm; rw le_infi_iff; intro i,
  { rw le_principal_iff, refine (bInter_mem (finite_le_nat _)).2 (Î» j hji, _),
    rw â† le_principal_iff, apply infi_le_of_le j _, apply le_refl _ },
  { apply infi_le_of_le i _, rw principal_mono, intro a, simp, intro h, apply h, refl },
end

lemma countable_binfi_eq_infi_seq [complete_lattice Î±] {B : set Î¹} (Bcbl : countable B)
  (Bne : B.nonempty) (f : Î¹ â†’ Î±) :
  âˆƒ (x : â„• â†’ Î¹), (â¨… t âˆˆ B, f t) = â¨… i, f (x i) :=
begin
  rw countable_iff_exists_surjective_to_subtype Bne at Bcbl,
  rcases Bcbl with âŸ¨g, gsurjâŸ©,
  rw infi_subtype',
  use (Î» n, g n), apply le_antisymm; rw le_infi_iff,
  { intro i, apply infi_le_of_le (g i) _, apply le_refl _ },
  { intros a, rcases gsurj a with âŸ¨i, rflâŸ©, apply infi_le }
end

lemma countable_binfi_eq_infi_seq' [complete_lattice Î±] {B : set Î¹} (Bcbl : countable B) (f : Î¹ â†’ Î±)
  {iâ‚€ : Î¹} (h : f iâ‚€ = âŠ¤) :
  âˆƒ (x : â„• â†’ Î¹), (â¨… t âˆˆ B, f t) = â¨… i, f (x i) :=
begin
  cases B.eq_empty_or_nonempty with hB Bnonempty,
  { rw [hB, infi_emptyset],
    use Î» n, iâ‚€,
    simp [h] },
  { exact countable_binfi_eq_infi_seq Bcbl Bnonempty f }
end

lemma countable_binfi_principal_eq_seq_infi {B : set (set Î±)} (Bcbl : countable B) :
  âˆƒ (x : â„• â†’ set Î±), (â¨… t âˆˆ B, ğ“Ÿ t) = â¨… i, ğ“Ÿ (x i) :=
countable_binfi_eq_infi_seq' Bcbl ğ“Ÿ principal_univ

section is_countably_generated

/-- If `f` is countably generated and `f.has_basis p s`, then `f` admits a decreasing basis
enumerated by natural numbers such that all sets have the form `s i`. More precisely, there is a
sequence `i n` such that `p (i n)` for all `n` and `s (i n)` is a decreasing sequence of sets which
forms a basis of `f`-/
lemma has_basis.exists_antitone_subbasis {f : filter Î±} [h : f.is_countably_generated]
  {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} (hs : f.has_basis p s) :
  âˆƒ x : â„• â†’ Î¹, (âˆ€ i, p (x i)) âˆ§ f.has_antitone_basis (Î» i, s (x i)) :=
begin
  obtain âŸ¨x', hx'âŸ© : âˆƒ x : â„• â†’ set Î±, f = â¨… i, ğ“Ÿ (x i),
  { unfreezingI { rcases h with âŸ¨s, hsc, rflâŸ© },
    rw generate_eq_binfi,
    exact countable_binfi_principal_eq_seq_infi hsc },
  have : âˆ€ i, x' i âˆˆ f := Î» i, hx'.symm â–¸ (infi_le (Î» i, ğ“Ÿ (x' i)) i) (mem_principal_self _),
  let x : â„• â†’ {i : Î¹ // p i} := Î» n, nat.rec_on n (hs.index _ $ this 0)
    (Î» n xn, (hs.index _ $ inter_mem (this $ n + 1) (hs.mem_of_mem xn.coe_prop))),
  have x_mono : antitone (Î» i, s (x i)),
  { refine antitone_nat_of_succ_le (Î» i, _),
    exact (hs.set_index_subset _).trans (inter_subset_right _ _) },
  have x_subset : âˆ€ i, s (x i) âŠ† x' i,
  { rintro (_|i),
    exacts [hs.set_index_subset _, subset.trans (hs.set_index_subset _) (inter_subset_left _ _)] },
  refine âŸ¨Î» i, x i, Î» i, (x i).2, _âŸ©,
  have : (â¨… i, ğ“Ÿ (s (x i))).has_antitone_basis (Î» i, s (x i)) :=
    âŸ¨has_basis_infi_principal (directed_of_sup x_mono), x_monoâŸ©,
  convert this,
  exact le_antisymm (le_infi $ Î» i, le_principal_iff.2 $ by cases i; apply hs.set_index_mem)
    (hx'.symm â–¸ le_infi (Î» i, le_principal_iff.2 $
      this.to_has_basis.mem_iff.2 âŸ¨i, trivial, x_subset iâŸ©))
end

/-- A countably generated filter admits a basis formed by an antitone sequence of sets. -/
lemma exists_antitone_basis (f : filter Î±) [f.is_countably_generated] :
  âˆƒ x : â„• â†’ set Î±, f.has_antitone_basis x :=
let âŸ¨x, hxf, hxâŸ© := f.basis_sets.exists_antitone_subbasis in âŸ¨x, hxâŸ©

lemma exists_antitone_seq (f : filter Î±) [f.is_countably_generated] :
  âˆƒ x : â„• â†’ set Î±, antitone x âˆ§ âˆ€ {s}, (s âˆˆ f â†” âˆƒ i, x i âŠ† s) :=
let âŸ¨x, hxâŸ© := f.exists_antitone_basis in
âŸ¨x, hx.antitone, Î» s, by simp [hx.to_has_basis.mem_iff]âŸ©

instance inf.is_countably_generated (f g : filter Î±) [is_countably_generated f]
  [is_countably_generated g] :
  is_countably_generated (f âŠ“ g) :=
begin
  rcases f.exists_antitone_basis with âŸ¨s, hsâŸ©,
  rcases g.exists_antitone_basis with âŸ¨t, htâŸ©,
  exact has_countable_basis.is_countably_generated
    âŸ¨hs.to_has_basis.inf ht.to_has_basis, set.countable_encodable _âŸ©
end

instance comap.is_countably_generated (l : filter Î²) [l.is_countably_generated] (f : Î± â†’ Î²) :
  (comap f l).is_countably_generated :=
let âŸ¨x, hxlâŸ© := l.exists_antitone_basis in
has_countable_basis.is_countably_generated âŸ¨hxl.to_has_basis.comap _, countable_encodable _âŸ©

instance sup.is_countably_generated (f g : filter Î±) [is_countably_generated f]
  [is_countably_generated g] :
  is_countably_generated (f âŠ” g) :=
begin
  rcases f.exists_antitone_basis with âŸ¨s, hsâŸ©,
  rcases g.exists_antitone_basis with âŸ¨t, htâŸ©,
  exact has_countable_basis.is_countably_generated
    âŸ¨hs.to_has_basis.sup ht.to_has_basis, set.countable_encodable _âŸ©
end

end is_countably_generated

@[instance] lemma is_countably_generated_seq [encodable Î²] (x : Î² â†’ set Î±) :
  is_countably_generated (â¨… i, ğ“Ÿ $ x i) :=
begin
  use [range x, countable_range x],
  rw [generate_eq_binfi, infi_range]
end

lemma is_countably_generated_of_seq {f : filter Î±} (h : âˆƒ x : â„• â†’ set Î±, f = â¨… i, ğ“Ÿ $ x i) :
  f.is_countably_generated  :=
let âŸ¨x, hâŸ© := h in by rw h ; apply is_countably_generated_seq

lemma is_countably_generated_binfi_principal {B : set $ set Î±} (h : countable B) :
  is_countably_generated (â¨… (s âˆˆ B), ğ“Ÿ s) :=
is_countably_generated_of_seq (countable_binfi_principal_eq_seq_infi h)

lemma is_countably_generated_iff_exists_antitone_basis {f : filter Î±} :
  is_countably_generated f â†” âˆƒ x : â„• â†’ set Î±, f.has_antitone_basis x :=
begin
  split,
  { introI h, exact f.exists_antitone_basis },
  { rintros âŸ¨x, hâŸ©,
    rw h.to_has_basis.eq_infi,
    exact is_countably_generated_seq x },
end

@[instance] lemma is_countably_generated_principal (s : set Î±) : is_countably_generated (ğ“Ÿ s) :=
is_countably_generated_of_seq âŸ¨Î» _, s, infi_const.symmâŸ©

@[instance] lemma is_countably_generated_bot : is_countably_generated (âŠ¥ : filter Î±) :=
@principal_empty Î± â–¸ is_countably_generated_principal _

@[instance] lemma is_countably_generated_top : is_countably_generated (âŠ¤ : filter Î±) :=
@principal_univ Î± â–¸ is_countably_generated_principal _

end filter
