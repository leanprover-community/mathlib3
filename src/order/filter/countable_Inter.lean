/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import order.filter.basic
import data.set.countable

/-!
# Filters with countable intersection property

In this file we define `countable_Inter_filter` to be the class of filters with the following
property: for any countable collection of sets `s âˆˆ l` their intersection belongs to `l` as well.

Two main examples are the `residual` filter defined in `topology.metric_space.baire` and
the `measure.ae` filter defined in `measure_theory.measure_space`.
-/

open set filter
open_locale filter

variables {Î¹ Î± : Type*}

/-- A filter `l` has the countable intersection property if for any countable collection
of sets `s âˆˆ l` their intersection belongs to `l` as well. -/
class countable_Inter_filter (l : filter Î±) : Prop :=
(countable_sInter_mem_sets' :
  âˆ€ {S : set (set Î±)} (hSc : countable S) (hS : âˆ€ s âˆˆ S, s âˆˆ l), â‹‚â‚€ S âˆˆ l)

variables {l : filter Î±} [countable_Inter_filter l]

lemma countable_sInter_mem_sets {S : set (set Î±)} (hSc : countable S) :
  â‹‚â‚€ S âˆˆ l â†” âˆ€ s âˆˆ S, s âˆˆ l :=
âŸ¨Î» hS s hs, mem_of_superset hS (sInter_subset_of_mem hs),
  countable_Inter_filter.countable_sInter_mem_sets' hScâŸ©

lemma countable_Inter_mem_sets [encodable Î¹] {s : Î¹ â†’ set Î±} :
  (â‹‚ i, s i) âˆˆ l â†” âˆ€ i, s i âˆˆ l :=
sInter_range s â–¸ (countable_sInter_mem_sets (countable_range _)).trans forall_range_iff

lemma countable_bInter_mem {S : set Î¹} (hS : countable S) {s : Î  i âˆˆ S, set Î±} :
  (â‹‚ i âˆˆ S, s i â€¹_â€º) âˆˆ l â†”  âˆ€ i âˆˆ S, s i â€¹_â€º âˆˆ l :=
begin
  rw [bInter_eq_Inter],
  haveI := hS.to_encodable,
  exact countable_Inter_mem_sets.trans subtype.forall
end

lemma eventually_countable_forall [encodable Î¹] {p : Î± â†’ Î¹ â†’ Prop} :
  (âˆ€á¶  x in l, âˆ€ i, p x i) â†” âˆ€ i, âˆ€á¶  x in l, p x i :=
by simpa only [filter.eventually, set_of_forall]
  using @countable_Inter_mem_sets _ _ l _ _ (Î» i, {x | p x i})

lemma eventually_countable_ball {S : set Î¹} (hS : countable S) {p : Î  (x : Î±) (i âˆˆ S), Prop} :
  (âˆ€á¶  x in l, âˆ€ i âˆˆ S, p x i â€¹_â€º) â†” âˆ€ i âˆˆ S, âˆ€á¶  x in l, p x i â€¹_â€º :=
by simpa only [filter.eventually, set_of_forall]
  using @countable_bInter_mem _ _ l _ _ hS (Î» i hi, {x | p x i hi})

lemma eventually_le.countable_Union [encodable Î¹] {s t : Î¹ â†’ set Î±} (h : âˆ€ i, s i â‰¤á¶ [l] t i) :
  (â‹ƒ i, s i) â‰¤á¶ [l] â‹ƒ i, t i :=
(eventually_countable_forall.2 h).mono $ Î» x hst hs, mem_Union.2 $
  (mem_Union.1 hs).imp hst

lemma eventually_eq.countable_Union [encodable Î¹] {s t : Î¹ â†’ set Î±} (h : âˆ€ i, s i =á¶ [l] t i) :
  (â‹ƒ i, s i) =á¶ [l] â‹ƒ i, t i :=
(eventually_le.countable_Union (Î» i, (h i).le)).antisymm
  (eventually_le.countable_Union (Î» i, (h i).symm.le))

lemma eventually_le.countable_bUnion {S : set Î¹} (hS : countable S) {s t : Î  i âˆˆ S, set Î±}
  (h : âˆ€ i âˆˆ S, s i â€¹_â€º â‰¤á¶ [l] t i â€¹_â€º) : (â‹ƒ i âˆˆ S, s i â€¹_â€º) â‰¤á¶ [l] â‹ƒ i âˆˆ S, t i â€¹_â€º :=
begin
  simp only [bUnion_eq_Union],
  haveI := hS.to_encodable,
  exact eventually_le.countable_Union (Î» i, h i i.2)
end

lemma eventually_eq.countable_bUnion {S : set Î¹} (hS : countable S) {s t : Î  i âˆˆ S, set Î±}
  (h : âˆ€ i âˆˆ S, s i â€¹_â€º =á¶ [l] t i â€¹_â€º) : (â‹ƒ i âˆˆ S, s i â€¹_â€º) =á¶ [l] â‹ƒ i âˆˆ S, t i â€¹_â€º :=
(eventually_le.countable_bUnion hS (Î» i hi, (h i hi).le)).antisymm
  (eventually_le.countable_bUnion hS (Î» i hi, (h i hi).symm.le))

lemma eventually_le.countable_Inter [encodable Î¹] {s t : Î¹ â†’ set Î±} (h : âˆ€ i, s i â‰¤á¶ [l] t i) :
  (â‹‚ i, s i) â‰¤á¶ [l] â‹‚ i, t i :=
(eventually_countable_forall.2 h).mono $ Î» x hst hs, mem_Inter.2 $ Î» i, hst _ (mem_Inter.1 hs i)

lemma eventually_eq.countable_Inter [encodable Î¹] {s t : Î¹ â†’ set Î±} (h : âˆ€ i, s i =á¶ [l] t i) :
  (â‹‚ i, s i) =á¶ [l] â‹‚ i, t i :=
(eventually_le.countable_Inter (Î» i, (h i).le)).antisymm
  (eventually_le.countable_Inter (Î» i, (h i).symm.le))

lemma eventually_le.countable_bInter {S : set Î¹} (hS : countable S) {s t : Î  i âˆˆ S, set Î±}
  (h : âˆ€ i âˆˆ S, s i â€¹_â€º â‰¤á¶ [l] t i â€¹_â€º) : (â‹‚ i âˆˆ S, s i â€¹_â€º) â‰¤á¶ [l] â‹‚ i âˆˆ S, t i â€¹_â€º :=
begin
  simp only [bInter_eq_Inter],
  haveI := hS.to_encodable,
  exact eventually_le.countable_Inter (Î» i, h i i.2)
end

lemma eventually_eq.countable_bInter {S : set Î¹} (hS : countable S) {s t : Î  i âˆˆ S, set Î±}
  (h : âˆ€ i âˆˆ S, s i â€¹_â€º =á¶ [l] t i â€¹_â€º) : (â‹‚ i âˆˆ S, s i â€¹_â€º) =á¶ [l] â‹‚ i âˆˆ S, t i â€¹_â€º :=
(eventually_le.countable_bInter hS (Î» i hi, (h i hi).le)).antisymm
  (eventually_le.countable_bInter hS (Î» i hi, (h i hi).symm.le))

instance countable_Inter_filter_principal (s : set Î±) : countable_Inter_filter (ğ“Ÿ s) :=
âŸ¨Î» S hSc hS, subset_sInter hSâŸ©

instance countable_Inter_filter_bot : countable_Inter_filter (âŠ¥ : filter Î±) :=
by { rw â† principal_empty, apply countable_Inter_filter_principal }

instance countable_Inter_filter_top : countable_Inter_filter (âŠ¤ : filter Î±) :=
by { rw â† principal_univ, apply countable_Inter_filter_principal }

/-- Infimum of two `countable_Inter_filter`s is a `countable_Inter_filter`. This is useful, e.g.,
to automatically get an instance for `residual Î± âŠ“ ğ“Ÿ s`. -/
instance countable_Inter_filter_inf (lâ‚ lâ‚‚ : filter Î±) [countable_Inter_filter lâ‚]
  [countable_Inter_filter lâ‚‚] :
  countable_Inter_filter (lâ‚ âŠ“ lâ‚‚) :=
begin
  refine âŸ¨Î» S hSc hS, _âŸ©,
  choose s hs t ht hst using hS,
  replace hs : (â‹‚ i âˆˆ S, s i â€¹_â€º) âˆˆ lâ‚ := (countable_bInter_mem hSc).2 hs,
  replace ht : (â‹‚ i âˆˆ S, t i â€¹_â€º) âˆˆ lâ‚‚ := (countable_bInter_mem hSc).2 ht,
  refine mem_of_superset (inter_mem_inf hs ht) (subset_sInter $ Î» i hi, _),
  rw hst i hi,
  apply inter_subset_inter ; exact Inter_subset_of_subset i (Inter_subset _ _)
end

/-- Supremum of two `countable_Inter_filter`s is a `countable_Inter_filter`. -/
instance countable_Inter_filter_sup (lâ‚ lâ‚‚ : filter Î±) [countable_Inter_filter lâ‚]
  [countable_Inter_filter lâ‚‚] :
  countable_Inter_filter (lâ‚ âŠ” lâ‚‚) :=
begin
  refine âŸ¨Î» S hSc hS, âŸ¨_, _âŸ©âŸ©; refine (countable_sInter_mem_sets hSc).2 (Î» s hs, _),
  exacts [(hS s hs).1, (hS s hs).2]
end
