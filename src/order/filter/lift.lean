/-
Copyright (c) 2019 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import order.filter.bases

/-!
# Lift filters along filter and set functions
-/

open set

open_locale classical filter

namespace filter
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î¹ : Sort*}

section lift

/-- A variant on `bind` using a function `g` taking a set instead of a member of `Î±`.
This is essentially a push-forward along a function mapping each set to a filter. -/
protected def lift (f : filter Î±) (g : set Î± â†’ filter Î²) :=
â¨…s âˆˆ f, g s

variables {f fâ‚ fâ‚‚ : filter Î±} {g gâ‚ gâ‚‚ : set Î± â†’ filter Î²}

@[simp] lemma lift_top (g : set Î± â†’ filter Î²) : (âŠ¤ : filter Î±).lift g = g univ :=
by simp [filter.lift]

/-- If `(p : Î¹ â†’ Prop, s : Î¹ â†’ set Î±)` is a basis of a filter `f`, `g` is a monotone function
`set Î± â†’ filter Î³`, and for each `i`, `(pg : Î² i â†’ Prop, sg : Î² i â†’ set Î±)` is a basis
of the filter `g (s i)`, then `(Î» (i : Î¹) (x : Î² i), p i âˆ§ pg i x, Î» (i : Î¹) (x : Î² i), sg i x)`
is a basis of the filter `f.lift g`.

This basis is parametrized by `i : Î¹` and `x : Î² i`, so in order to formulate this fact using
`has_basis` one has to use `Î£ i, Î² i` as the index type, see `filter.has_basis.lift`.
This lemma states the corresponding `mem_iff` statement without using a sigma type. -/
lemma has_basis.mem_lift_iff {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {f : filter Î±}
  (hf : f.has_basis p s) {Î² : Î¹ â†’ Type*} {pg : Î  i, Î² i â†’ Prop} {sg : Î  i, Î² i â†’ set Î³}
  {g : set Î± â†’ filter Î³} (hg : âˆ€ i, (g $ s i).has_basis (pg i) (sg i)) (gm : monotone g)
  {s : set Î³} :
  s âˆˆ f.lift g â†” âˆƒ (i : Î¹) (hi : p i) (x : Î² i) (hx : pg i x), sg i x âŠ† s :=
begin
  refine (mem_binfi_of_directed _ âŸ¨univ, univ_sets _âŸ©).trans _,
  { intros tâ‚ htâ‚ tâ‚‚ htâ‚‚,
    exact âŸ¨tâ‚ âˆ© tâ‚‚, inter_mem htâ‚ htâ‚‚, gm $ inter_subset_left _ _,
      gm $ inter_subset_right _ _âŸ© },
  { simp only [â† (hg _).mem_iff],
    exact hf.exists_iff (Î» tâ‚ tâ‚‚ ht H, gm ht H) }
end

/-- If `(p : Î¹ â†’ Prop, s : Î¹ â†’ set Î±)` is a basis of a filter `f`, `g` is a monotone function
`set Î± â†’ filter Î³`, and for each `i`, `(pg : Î² i â†’ Prop, sg : Î² i â†’ set Î±)` is a basis
of the filter `g (s i)`, then `(Î» (i : Î¹) (x : Î² i), p i âˆ§ pg i x, Î» (i : Î¹) (x : Î² i), sg i x)`
is a basis of the filter `f.lift g`.

This basis is parametrized by `i : Î¹` and `x : Î² i`, so in order to formulate this fact using
`has_basis` one has to use `Î£ i, Î² i` as the index type. See also `filter.has_basis.mem_lift_iff`
for the corresponding `mem_iff` statement formulated without using a sigma type. -/
lemma has_basis.lift {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {f : filter Î±} (hf : f.has_basis p s)
  {Î² : Î¹ â†’ Type*} {pg : Î  i, Î² i â†’ Prop} {sg : Î  i, Î² i â†’ set Î³} {g : set Î± â†’ filter Î³}
  (hg : âˆ€ i, (g $ s i).has_basis (pg i) (sg i)) (gm : monotone g) :
  (f.lift g).has_basis (Î» i : Î£ i, Î² i, p i.1 âˆ§ pg i.1 i.2) (Î» i : Î£ i, Î² i, sg i.1 i.2) :=
begin
  refine âŸ¨Î» t, (hf.mem_lift_iff hg gm).trans _âŸ©,
  simp [sigma.exists, and_assoc, exists_and_distrib_left]
end

lemma mem_lift_sets (hg : monotone g) {s : set Î²} :
  s âˆˆ f.lift g â†” âˆƒtâˆˆf, s âˆˆ g t :=
(f.basis_sets.mem_lift_iff (Î» s, (g s).basis_sets) hg).trans $
  by simp only [id, exists_mem_subset_iff]

lemma mem_lift {s : set Î²} {t : set Î±} (ht : t âˆˆ f) (hs : s âˆˆ g t) :
  s âˆˆ f.lift g :=
le_principal_iff.mp $ show f.lift g â‰¤ ğ“Ÿ s,
  from infi_le_of_le t $ infi_le_of_le ht $ le_principal_iff.mpr hs

lemma lift_le {f : filter Î±} {g : set Î± â†’ filter Î²} {h : filter Î²} {s : set Î±}
  (hs : s âˆˆ f) (hg : g s â‰¤ h) : f.lift g â‰¤ h :=
infiâ‚‚_le_of_le s hs hg

lemma le_lift {f : filter Î±} {g : set Î± â†’ filter Î²} {h : filter Î²}
  (hh : âˆ€sâˆˆf, h â‰¤ g s) : h â‰¤ f.lift g :=
le_infiâ‚‚ hh

lemma lift_mono (hf : fâ‚ â‰¤ fâ‚‚) (hg : gâ‚ â‰¤ gâ‚‚) : fâ‚.lift gâ‚ â‰¤ fâ‚‚.lift gâ‚‚ :=
infi_mono $ Î» s, infi_mono' $ Î» hs, âŸ¨hf hs, hg sâŸ©

lemma lift_mono' (hg : âˆ€s âˆˆ f, gâ‚ s â‰¤ gâ‚‚ s) : f.lift gâ‚ â‰¤ f.lift gâ‚‚ := infiâ‚‚_mono hg

lemma tendsto_lift {m : Î³ â†’ Î²} {l : filter Î³} :
  tendsto m l (f.lift g) â†” âˆ€ s âˆˆ f, tendsto m l (g s) :=
by simp only [filter.lift, tendsto_infi]

lemma map_lift_eq {m : Î² â†’ Î³} (hg : monotone g) : map m (f.lift g) = f.lift (map m âˆ˜ g) :=
have monotone (map m âˆ˜ g),
  from map_mono.comp hg,
filter.ext $ Î» s,
  by simp only [mem_lift_sets hg, mem_lift_sets this, exists_prop, mem_map, function.comp_app]

lemma comap_lift_eq {m : Î³ â†’ Î²} : comap m (f.lift g) = f.lift (comap m âˆ˜ g) :=
by simp only [filter.lift, comap_infi]

theorem comap_lift_eq2 {m : Î² â†’ Î±} {g : set Î² â†’ filter Î³} (hg : monotone g) :
  (comap m f).lift g = f.lift (g âˆ˜ preimage m) :=
le_antisymm
  (le_infiâ‚‚ $ Î» s hs, infiâ‚‚_le (m â»Â¹' s) âŸ¨s, hs, subset.rflâŸ©)
  (le_infiâ‚‚ $ Î» s âŸ¨s', hs', (h_sub : m â»Â¹' s' âŠ† s)âŸ©, infiâ‚‚_le_of_le s' hs' $ hg h_sub)

lemma map_lift_eq2 {g : set Î² â†’ filter Î³} {m : Î± â†’ Î²} (hg : monotone g) :
  (map m f).lift g = f.lift (g âˆ˜ image m) :=
le_antisymm
  (infi_mono' $ assume s, âŸ¨image m s,
    infi_mono' $ assume hs, âŸ¨
      f.sets_of_superset hs $ assume a h, mem_image_of_mem _ h,
      le_rflâŸ©âŸ©)
  (infi_mono' $ assume t, âŸ¨preimage m t,
    infi_mono' $ assume ht, âŸ¨ht,
      hg $ assume x, assume h : x âˆˆ m '' preimage m t,
        let âŸ¨y, hy, h_eqâŸ© := h in
        show x âˆˆ t, from h_eq â–¸ hyâŸ©âŸ©)

lemma lift_comm {g : filter Î²} {h : set Î± â†’ set Î² â†’ filter Î³} :
  f.lift (Î»s, g.lift (h s)) = g.lift (Î»t, f.lift (Î»s, h s t)) :=
le_antisymm
  (le_infi $ assume i, le_infi $ assume hi, le_infi $ assume j, le_infi $ assume hj,
    infi_le_of_le j $ infi_le_of_le hj $ infi_le_of_le i $ infi_le _ hi)
  (le_infi $ assume i, le_infi $ assume hi, le_infi $ assume j, le_infi $ assume hj,
    infi_le_of_le j $ infi_le_of_le hj $ infi_le_of_le i $ infi_le _ hi)

lemma lift_assoc {h : set Î² â†’ filter Î³} (hg : monotone g)  :
  (f.lift g).lift h = f.lift (Î»s, (g s).lift h) :=
le_antisymm
  (le_infi $ assume s, le_infi $ assume hs, le_infi $ assume t, le_infi $ assume ht,
    infi_le_of_le t $ infi_le _ $ (mem_lift_sets hg).mpr âŸ¨_, hs, htâŸ©)
  (le_infi $ assume t, le_infi $ assume ht,
    let âŸ¨s, hs, h'âŸ© := (mem_lift_sets hg).mp ht in
    infi_le_of_le s $ infi_le_of_le hs $ infi_le_of_le t $ infi_le _ h')

lemma lift_lift_same_le_lift {g : set Î± â†’ set Î± â†’ filter Î²} :
  f.lift (Î»s, f.lift (g s)) â‰¤ f.lift (Î»s, g s s) :=
le_infi $ assume s, le_infi $ assume hs, infi_le_of_le s $ infi_le_of_le hs $ infi_le_of_le s $
  infi_le _ hs

lemma lift_lift_same_eq_lift {g : set Î± â†’ set Î± â†’ filter Î²}
  (hgâ‚ : âˆ€s, monotone (Î»t, g s t)) (hgâ‚‚ : âˆ€t, monotone (Î»s, g s t)) :
  f.lift (Î»s, f.lift (g s)) = f.lift (Î»s, g s s) :=
le_antisymm
  lift_lift_same_le_lift
  (le_infi $ assume s, le_infi $ assume hs, le_infi $ assume t, le_infi $ assume ht,
    infi_le_of_le (s âˆ© t) $
    infi_le_of_le (inter_mem hs ht) $
    calc g (s âˆ© t) (s âˆ© t) â‰¤ g s (s âˆ© t) : hgâ‚‚ (s âˆ© t) (inter_subset_left _ _)
      ... â‰¤ g s t                        : hgâ‚ s (inter_subset_right _ _))

lemma lift_principal {s : set Î±} (hg : monotone g) :
  (ğ“Ÿ s).lift g = g s :=
le_antisymm
  (infi_le_of_le s $ infi_le _ $ subset.refl _)
  (le_infi $ assume t, le_infi $ assume hi, hg hi)

theorem monotone_lift [preorder Î³] {f : Î³ â†’ filter Î±} {g : Î³ â†’ set Î± â†’ filter Î²}
  (hf : monotone f) (hg : monotone g) : monotone (Î»c, (f c).lift (g c)) :=
assume a b h, lift_mono (hf h) (hg h)

lemma lift_ne_bot_iff (hm : monotone g) : (ne_bot $ f.lift g) â†” (âˆ€sâˆˆf, ne_bot (g s)) :=
begin
  rw [filter.lift, infi_subtype', infi_ne_bot_iff_of_directed', subtype.forall'],
  { rintros âŸ¨s, hsâŸ© âŸ¨t, htâŸ©,
    exact âŸ¨âŸ¨s âˆ© t, inter_mem hs htâŸ©, hm (inter_subset_left s t), hm (inter_subset_right s t)âŸ© }
end

@[simp] lemma lift_const {f : filter Î±} {g : filter Î²} : f.lift (Î»x, g) = g :=
le_antisymm (lift_le univ_mem $ le_refl g) (le_lift $ assume s hs, le_refl g)

@[simp] lemma lift_inf {f : filter Î±} {g h : set Î± â†’ filter Î²} :
  f.lift (Î»x, g x âŠ“ h x) = f.lift g âŠ“ f.lift h :=
by simp only [filter.lift, infi_inf_eq, eq_self_iff_true]

@[simp] lemma lift_principal2 {f : filter Î±} : f.lift ğ“Ÿ = f :=
le_antisymm
  (assume s hs, mem_lift hs (mem_principal_self s))
  (le_infi $ assume s, le_infi $ assume hs, by simp only [hs, le_principal_iff])

lemma lift_infi {f : Î¹ â†’ filter Î±} {g : set Î± â†’ filter Î²}
  [hÎ¹ : nonempty Î¹] (hg : âˆ€{s t}, g s âŠ“ g t = g (s âˆ© t)) : (infi f).lift g = (â¨…i, (f i).lift g) :=
le_antisymm
  (le_infi $ assume i, lift_mono (infi_le _ _) le_rfl)
  (assume s,
    have g_mono : monotone g,
      from assume s t h, le_of_inf_eq $ eq.trans hg $ congr_arg g $ inter_eq_self_of_subset_left h,
    have âˆ€tâˆˆ(infi f), (â¨… (i : Î¹), filter.lift (f i) g) â‰¤ g t,
      from assume t ht, infi_sets_induct ht
        (let âŸ¨iâŸ© := hÎ¹ in infi_le_of_le i $ infi_le_of_le univ $ infi_le _ univ_mem)
        (assume i sâ‚ sâ‚‚ hsâ‚ hsâ‚‚,
          @hg sâ‚ sâ‚‚ â–¸ le_inf (infi_le_of_le i $ infi_le_of_le sâ‚ $ infi_le _ hsâ‚) hsâ‚‚),
    begin
      simp only [mem_lift_sets g_mono,  exists_imp_distrib],
      exact assume t ht hs, this t ht hs
    end)

end lift

section lift'
/-- Specialize `lift` to functions `set Î± â†’ set Î²`. This can be viewed as a generalization of `map`.
This is essentially a push-forward along a function mapping each set to a set. -/
protected def lift' (f : filter Î±) (h : set Î± â†’ set Î²) :=
f.lift (ğ“Ÿ âˆ˜ h)

variables {f fâ‚ fâ‚‚ : filter Î±} {h hâ‚ hâ‚‚ : set Î± â†’ set Î²}

@[simp] lemma lift'_top (h : set Î± â†’ set Î²) : (âŠ¤ : filter Î±).lift' h = ğ“Ÿ (h univ) :=
lift_top _

lemma mem_lift' {t : set Î±} (ht : t âˆˆ f) : h t âˆˆ (f.lift' h) :=
le_principal_iff.mp $ show f.lift' h â‰¤ ğ“Ÿ (h t),
  from infi_le_of_le t $ infi_le_of_le ht $ le_rfl

lemma tendsto_lift' {m : Î³ â†’ Î²} {l : filter Î³} :
  tendsto m l (f.lift' h) â†” âˆ€ s âˆˆ f, âˆ€á¶  a in l, m a âˆˆ h s :=
by simp only [filter.lift', tendsto_lift, tendsto_principal]

lemma has_basis.lift' {Î¹} {p : Î¹ â†’ Prop} {s} (hf : f.has_basis p s) (hh : monotone h) :
  (f.lift' h).has_basis p (h âˆ˜ s) :=
begin
  refine âŸ¨Î» t, (hf.mem_lift_iff _ (monotone_principal.comp hh)).trans _âŸ©,
  show âˆ€ i, (ğ“Ÿ (h (s i))).has_basis (Î» j : unit, true) (Î» (j : unit), h (s i)),
    from Î» i, has_basis_principal _,
  simp only [exists_const]
end

lemma mem_lift'_sets (hh : monotone h) {s : set Î²} : s âˆˆ (f.lift' h) â†” (âˆƒtâˆˆf, h t âŠ† s) :=
mem_lift_sets $ monotone_principal.comp hh

lemma eventually_lift'_iff (hh : monotone h) {p : Î² â†’ Prop} :
  (âˆ€á¶  y in f.lift' h, p y) â†” (âˆƒ t âˆˆ f, âˆ€ y âˆˆ h t, p y) :=
mem_lift'_sets hh

lemma lift'_le {f : filter Î±} {g : set Î± â†’ set Î²} {h : filter Î²} {s : set Î±}
  (hs : s âˆˆ f) (hg : ğ“Ÿ (g s) â‰¤ h) : f.lift' g â‰¤ h :=
lift_le hs hg

lemma lift'_mono (hf : fâ‚ â‰¤ fâ‚‚) (hh : hâ‚ â‰¤ hâ‚‚) : fâ‚.lift' hâ‚ â‰¤ fâ‚‚.lift' hâ‚‚ :=
lift_mono hf $ assume s, principal_mono.mpr $ hh s

lemma lift'_mono' (hh : âˆ€sâˆˆf, hâ‚ s âŠ† hâ‚‚ s) : f.lift' hâ‚ â‰¤ f.lift' hâ‚‚ :=
infiâ‚‚_mono $ Î» s hs, principal_mono.mpr $ hh s hs

lemma lift'_cong (hh : âˆ€sâˆˆf, hâ‚ s = hâ‚‚ s) : f.lift' hâ‚ = f.lift' hâ‚‚ :=
le_antisymm (lift'_mono' $ assume s hs, le_of_eq $ hh s hs)
  (lift'_mono' $ assume s hs, le_of_eq $ (hh s hs).symm)

lemma map_lift'_eq {m : Î² â†’ Î³} (hh : monotone h) : map m (f.lift' h) = f.lift' (image m âˆ˜ h) :=
calc map m (f.lift' h) = f.lift (map m âˆ˜ ğ“Ÿ âˆ˜ h) :
    map_lift_eq $ monotone_principal.comp hh
  ... = f.lift' (image m âˆ˜ h) : by simp only [(âˆ˜), filter.lift', map_principal, eq_self_iff_true]

lemma map_lift'_eq2 {g : set Î² â†’ set Î³} {m : Î± â†’ Î²} (hg : monotone g) :
  (map m f).lift' g = f.lift' (g âˆ˜ image m) :=
map_lift_eq2 $ monotone_principal.comp hg

theorem comap_lift'_eq {m : Î³ â†’ Î²} : comap m (f.lift' h) = f.lift' (preimage m âˆ˜ h) :=
by simp only [filter.lift', comap_lift_eq, (âˆ˜), comap_principal]

theorem comap_lift'_eq2 {m : Î² â†’ Î±} {g : set Î² â†’ set Î³} (hg : monotone g) :
  (comap m f).lift' g = f.lift' (g âˆ˜ preimage m) :=
comap_lift_eq2 $ monotone_principal.comp hg

lemma lift'_principal {s : set Î±} (hh : monotone h) :
  (ğ“Ÿ s).lift' h = ğ“Ÿ (h s) :=
lift_principal $ monotone_principal.comp hh

lemma lift'_pure {a : Î±} (hh : monotone h) :
  (pure a : filter Î±).lift' h = ğ“Ÿ (h {a}) :=
by rw [â† principal_singleton, lift'_principal hh]

lemma lift'_bot (hh : monotone h) : (âŠ¥ : filter Î±).lift' h = ğ“Ÿ (h âˆ…) :=
by rw [â† principal_empty, lift'_principal hh]

lemma principal_le_lift' {t : set Î²} (hh : âˆ€sâˆˆf, t âŠ† h s) :
  ğ“Ÿ t â‰¤ f.lift' h :=
le_infi $ assume s, le_infi $ assume hs, principal_mono.mpr (hh s hs)

theorem monotone_lift' [preorder Î³] {f : Î³ â†’ filter Î±} {g : Î³ â†’ set Î± â†’ set Î²}
  (hf : monotone f) (hg : monotone g) : monotone (Î»c, (f c).lift' (g c)) :=
assume a b h, lift'_mono (hf h) (hg h)

lemma lift_lift'_assoc {g : set Î± â†’ set Î²} {h : set Î² â†’ filter Î³}
  (hg : monotone g) (hh : monotone h) :
  (f.lift' g).lift h = f.lift (Î»s, h (g s)) :=
calc (f.lift' g).lift h = f.lift (Î»s, (ğ“Ÿ (g s)).lift h) :
    lift_assoc (monotone_principal.comp hg)
  ... = f.lift (Î»s, h (g s)) : by simp only [lift_principal, hh, eq_self_iff_true]

lemma lift'_lift'_assoc {g : set Î± â†’ set Î²} {h : set Î² â†’ set Î³}
  (hg : monotone g) (hh : monotone h) :
  (f.lift' g).lift' h = f.lift' (Î»s, h (g s)) :=
lift_lift'_assoc hg (monotone_principal.comp hh)

lemma lift'_lift_assoc {g : set Î± â†’ filter Î²} {h : set Î² â†’ set Î³}
  (hg : monotone g) : (f.lift g).lift' h = f.lift (Î»s, (g s).lift' h) :=
lift_assoc hg

lemma lift_lift'_same_le_lift' {g : set Î± â†’ set Î± â†’ set Î²} :
  f.lift (Î»s, f.lift' (g s)) â‰¤ f.lift' (Î»s, g s s) :=
lift_lift_same_le_lift

lemma lift_lift'_same_eq_lift' {g : set Î± â†’ set Î± â†’ set Î²}
  (hgâ‚ : âˆ€s, monotone (Î»t, g s t)) (hgâ‚‚ : âˆ€t, monotone (Î»s, g s t)) :
  f.lift (Î»s, f.lift' (g s)) = f.lift' (Î»s, g s s) :=
lift_lift_same_eq_lift
  (assume s, monotone_principal.comp (hgâ‚ s))
  (assume t, monotone_principal.comp (hgâ‚‚ t))

lemma lift'_inf_principal_eq {h : set Î± â†’ set Î²} {s : set Î²} :
  f.lift' h âŠ“ ğ“Ÿ s = f.lift' (Î»t, h t âˆ© s) :=
by simp only [filter.lift', filter.lift, (âˆ˜), â† inf_principal, infi_subtype', â† infi_inf]

lemma lift'_ne_bot_iff (hh : monotone h) : (ne_bot (f.lift' h)) â†” (âˆ€sâˆˆf, (h s).nonempty) :=
calc (ne_bot (f.lift' h)) â†” (âˆ€sâˆˆf, ne_bot (ğ“Ÿ (h s))) :
    lift_ne_bot_iff (monotone_principal.comp hh)
  ... â†” (âˆ€sâˆˆf, (h s).nonempty) : by simp only [principal_ne_bot_iff]

@[simp] lemma lift'_id {f : filter Î±} : f.lift' id = f :=
lift_principal2

lemma le_lift' {f : filter Î±} {h : set Î± â†’ set Î²} {g : filter Î²}
  (h_le : âˆ€sâˆˆf, h s âˆˆ g) : g â‰¤ f.lift' h :=
le_infi $ assume s, le_infi $ assume hs,
  by simpa only [h_le, le_principal_iff, function.comp_app] using h_le s hs

lemma lift_infi' {f : Î¹ â†’ filter Î±} {g : set Î± â†’ filter Î²}
  [nonempty Î¹] (hf : directed (â‰¥) f) (hg : monotone g) : (infi f).lift g = (â¨…i, (f i).lift g) :=
le_antisymm
  (le_infi $ assume i, lift_mono (infi_le _ _) le_rfl)
  (assume s,
  begin
    rw mem_lift_sets hg,
    simp only [exists_imp_distrib, mem_infi_of_directed hf],
    exact assume t i ht hs, mem_infi_of_mem i $ mem_lift ht hs
  end)

lemma lift'_infi {f : Î¹ â†’ filter Î±} {g : set Î± â†’ set Î²}
  [nonempty Î¹] (hg : âˆ€{s t}, g s âˆ© g t = g (s âˆ© t)) : (infi f).lift' g = (â¨…i, (f i).lift' g) :=
lift_infi $ Î» s t, by simp only [principal_eq_iff_eq, inf_principal, (âˆ˜), hg]

lemma lift'_inf (f g : filter Î±) {s : set Î± â†’ set Î²} (hs : âˆ€ {tâ‚ tâ‚‚}, s tâ‚ âˆ© s tâ‚‚ = s (tâ‚ âˆ© tâ‚‚)) :
  (f âŠ“ g).lift' s = f.lift' s âŠ“ g.lift' s :=
have (â¨… b : bool, cond b f g).lift' s = â¨… b : bool, (cond b f g).lift' s :=
  lift'_infi @hs,
by simpa only [infi_bool_eq]

theorem comap_eq_lift' {f : filter Î²} {m : Î± â†’ Î²} :
  comap m f = f.lift' (preimage m) :=
filter.ext $ Î» s, (mem_lift'_sets monotone_preimage).symm

end lift'

section prod
variables {f : filter Î±}

lemma prod_def {f : filter Î±} {g : filter Î²} : f Ã—á¶  g = (f.lift $ Î» s, g.lift' $ Î» t, s Ã—Ë¢ t) :=
have âˆ€(s:set Î±) (t : set Î²),
    ğ“Ÿ (s Ã—Ë¢ t) = (ğ“Ÿ s).comap prod.fst âŠ“ (ğ“Ÿ t).comap prod.snd,
  by simp only [principal_eq_iff_eq, comap_principal, inf_principal]; intros; refl,
begin
  simp only [filter.lift', function.comp, this, lift_inf, lift_const, lift_inf],
  rw [â† comap_lift_eq, â† comap_lift_eq],
  simp only [filter.prod, lift_principal2]
end

lemma prod_same_eq : f Ã—á¶  f = f.lift' (Î» t : set Î±, t Ã—Ë¢ t) :=
by rw [prod_def];
from lift_lift'_same_eq_lift'
  (assume s, set.monotone_prod monotone_const monotone_id)
  (assume t, set.monotone_prod monotone_id monotone_const)

lemma mem_prod_same_iff {s : set (Î±Ã—Î±)} :
  s âˆˆ f Ã—á¶  f â†” (âˆƒtâˆˆf, t Ã—Ë¢ t âŠ† s) :=
by rw [prod_same_eq, mem_lift'_sets]; exact set.monotone_prod monotone_id monotone_id

lemma tendsto_prod_self_iff {f : Î± Ã— Î± â†’ Î²} {x : filter Î±} {y : filter Î²} :
  filter.tendsto f (x Ã—á¶  x) y â†”
  âˆ€ W âˆˆ y, âˆƒ U âˆˆ x, âˆ€ (x x' : Î±), x âˆˆ U â†’ x' âˆˆ U â†’ f (x, x') âˆˆ W :=
by simp only [tendsto_def, mem_prod_same_iff, prod_sub_preimage_iff, exists_prop, iff_self]

variables {Î±â‚ : Type*} {Î±â‚‚ : Type*} {Î²â‚ : Type*} {Î²â‚‚ : Type*}

lemma prod_lift_lift
  {fâ‚ : filter Î±â‚} {fâ‚‚ : filter Î±â‚‚} {gâ‚ : set Î±â‚ â†’ filter Î²â‚} {gâ‚‚ : set Î±â‚‚ â†’ filter Î²â‚‚}
  (hgâ‚ : monotone gâ‚) (hgâ‚‚ : monotone gâ‚‚) :
  (fâ‚.lift gâ‚) Ã—á¶  (fâ‚‚.lift gâ‚‚) = fâ‚.lift (Î»s, fâ‚‚.lift (Î»t, gâ‚ s Ã—á¶  gâ‚‚ t)) :=
begin
  simp only [prod_def],
  rw [lift_assoc],
  apply congr_arg, funext x,
  rw [lift_comm],
  apply congr_arg, funext y,
  rw [lift'_lift_assoc],
  exact hgâ‚‚,
  exact hgâ‚
end

lemma prod_lift'_lift'
  {fâ‚ : filter Î±â‚} {fâ‚‚ : filter Î±â‚‚} {gâ‚ : set Î±â‚ â†’ set Î²â‚} {gâ‚‚ : set Î±â‚‚ â†’ set Î²â‚‚}
  (hgâ‚ : monotone gâ‚) (hgâ‚‚ : monotone gâ‚‚) :
  fâ‚.lift' gâ‚ Ã—á¶  fâ‚‚.lift' gâ‚‚ = fâ‚.lift (Î»s, fâ‚‚.lift' (Î»t, gâ‚ s Ã—Ë¢ gâ‚‚ t)) :=
begin
  rw [prod_def, lift_lift'_assoc],
  apply congr_arg, funext x,
  rw [lift'_lift'_assoc],
  exact hgâ‚‚,
  exact set.monotone_prod monotone_const monotone_id,
  exact hgâ‚,
  exact (monotone_lift' monotone_const $ monotone_lam $
    assume x, set.monotone_prod monotone_id monotone_const)
end

end prod

end filter
