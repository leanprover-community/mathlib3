/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Alex Kontorovich
-/
import order.filter.bases

/-!
# (Co)product of a family of filters

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define two filters on `Œ† i, Œ± i` and prove some basic properties of these filters.

* `filter.pi (f : Œ† i, filter (Œ± i))` to be the maximal filter on `Œ† i, Œ± i` such that
  `‚àÄ i, filter.tendsto (function.eval i) (filter.pi f) (f i)`. It is defined as
  `Œ† i, filter.comap (function.eval i) (f i)`. This is a generalization of `filter.prod` to indexed
  products.

* `filter.Coprod (f : Œ† i, filter (Œ± i))`: a generalization of `filter.coprod`; it is the supremum
  of `comap (eval i) (f i)`.
-/

open set function
open_locale classical filter

namespace filter

variables {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} {f f‚ÇÅ f‚ÇÇ : Œ† i, filter (Œ± i)} {s : Œ† i, set (Œ± i)}

section pi

/-- The product of an indexed family of filters. -/
def pi (f : Œ† i, filter (Œ± i)) : filter (Œ† i, Œ± i) := ‚®Ö i, comap (eval i) (f i)

instance pi.is_countably_generated [countable Œπ] [‚àÄ i, is_countably_generated (f i)] :
  is_countably_generated (pi f) :=
infi.is_countably_generated _

lemma tendsto_eval_pi (f : Œ† i, filter (Œ± i)) (i : Œπ) :
  tendsto (eval i) (pi f) (f i) :=
tendsto_infi' i tendsto_comap

lemma tendsto_pi {Œ≤ : Type*} {m : Œ≤ ‚Üí Œ† i, Œ± i} {l : filter Œ≤} :
  tendsto m l (pi f) ‚Üî ‚àÄ i, tendsto (Œª x, m x i) l (f i) :=
by simp only [pi, tendsto_infi, tendsto_comap_iff]

lemma le_pi {g : filter (Œ† i, Œ± i)} : g ‚â§ pi f ‚Üî ‚àÄ i, tendsto (eval i) g (f i) := tendsto_pi

@[mono] lemma pi_mono (h : ‚àÄ i, f‚ÇÅ i ‚â§ f‚ÇÇ i) : pi f‚ÇÅ ‚â§ pi f‚ÇÇ := infi_mono $ Œª i, comap_mono $ h i

lemma mem_pi_of_mem (i : Œπ) {s : set (Œ± i)} (hs : s ‚àà f i) :
  eval i ‚Åª¬π' s ‚àà pi f :=
mem_infi_of_mem i $ preimage_mem_comap hs

lemma pi_mem_pi {I : set Œπ} (hI : I.finite) (h : ‚àÄ i ‚àà I, s i ‚àà f i) :
  I.pi s ‚àà pi f :=
begin
  rw [pi_def, bInter_eq_Inter],
  refine mem_infi_of_Inter hI (Œª i, _) subset.rfl,
  exact preimage_mem_comap (h i i.2)
end

lemma mem_pi {s : set (Œ† i, Œ± i)} : s ‚àà pi f ‚Üî
  ‚àÉ (I : set Œπ), I.finite ‚àß ‚àÉ t : Œ† i, set (Œ± i), (‚àÄ i, t i ‚àà f i) ‚àß I.pi t ‚äÜ s :=
begin
  split,
  { simp only [pi, mem_infi', mem_comap, pi_def],
    rintro ‚ü®I, If, V, hVf, hVI, rfl, -‚ü©, choose t htf htV using hVf,
    exact ‚ü®I, If, t, htf, Inter‚ÇÇ_mono (Œª i _, htV i)‚ü© },
  { rintro ‚ü®I, If, t, htf, hts‚ü©,
    exact mem_of_superset (pi_mem_pi If $ Œª i _, htf i) hts }
end

lemma mem_pi' {s : set (Œ† i, Œ± i)} : s ‚àà pi f ‚Üî
  ‚àÉ (I : finset Œπ), ‚àÉ t : Œ† i, set (Œ± i), (‚àÄ i, t i ‚àà f i) ‚àß set.pi ‚ÜëI t ‚äÜ s :=
mem_pi.trans exists_finite_iff_finset

lemma mem_of_pi_mem_pi [‚àÄ i, ne_bot (f i)] {I : set Œπ} (h : I.pi s ‚àà pi f) {i : Œπ} (hi : i ‚àà I) :
  s i ‚àà f i :=
begin
  rcases mem_pi.1 h with ‚ü®I', I'f, t, htf, hts‚ü©,
  refine mem_of_superset (htf i) (Œª x hx, _),
  have : ‚àÄ i, (t i).nonempty, from Œª i, nonempty_of_mem (htf i),
  choose g hg,
  have : update g i x ‚àà I'.pi t,
  { intros j hj, rcases eq_or_ne j i with (rfl|hne); simp * },
  simpa using hts this i hi
end

@[simp] lemma pi_mem_pi_iff [‚àÄ i, ne_bot (f i)] {I : set Œπ} (hI : I.finite) :
  I.pi s ‚àà pi f ‚Üî ‚àÄ i ‚àà I, s i ‚àà f i :=
‚ü®Œª h i hi, mem_of_pi_mem_pi h hi, pi_mem_pi hI‚ü©

lemma has_basis_pi {Œπ' : Œπ ‚Üí Type} {s : Œ† i, Œπ' i ‚Üí set (Œ± i)} {p : Œ† i, Œπ' i ‚Üí Prop}
  (h : ‚àÄ i, (f i).has_basis (p i) (s i)) :
  (pi f).has_basis (Œª If : set Œπ √ó Œ† i, Œπ' i, If.1.finite ‚àß ‚àÄ i ‚àà If.1, p i (If.2 i))
    (Œª If : set Œπ √ó Œ† i, Œπ' i, If.1.pi (Œª i, s i $ If.2 i)) :=
begin
  have : (pi f).has_basis _ _ := has_basis_infi' (Œª i, (h i).comap (eval i : (Œ† j, Œ± j) ‚Üí Œ± i)),
  convert this,
  ext,
  simp
end

@[simp] lemma pi_inf_principal_univ_pi_eq_bot :
  pi f ‚äì ùìü (set.pi univ s) = ‚ä• ‚Üî ‚àÉ i, f i ‚äì ùìü (s i) = ‚ä• :=
begin
  split,
  { simp only [inf_principal_eq_bot, mem_pi], contrapose!,
    rintros (hsf : ‚àÄ i, ‚àÉ·∂† x in f i, x ‚àà s i) I If t htf hts,
    have : ‚àÄ i, (s i ‚à© t i).nonempty, from Œª i, ((hsf i).and_eventually (htf i)).exists,
    choose x hxs hxt,
    exact hts (Œª i hi, hxt i) (mem_univ_pi.2 hxs) },
  { simp only [inf_principal_eq_bot],
    rintro ‚ü®i, hi‚ü©,
    filter_upwards [mem_pi_of_mem i hi] with x using mt (Œª h, h i trivial), },
end

@[simp] lemma pi_inf_principal_pi_eq_bot [Œ† i, ne_bot (f i)] {I : set Œπ} :
  pi f ‚äì ùìü (set.pi I s) = ‚ä• ‚Üî ‚àÉ i ‚àà I, f i ‚äì ùìü (s i) = ‚ä• :=
begin
  rw [‚Üê univ_pi_piecewise I, pi_inf_principal_univ_pi_eq_bot],
  refine exists_congr (Œª i, _),
  by_cases hi : i ‚àà I; simp [hi, (‚ÄπŒ† i, ne_bot (f i)‚Ä∫ i).ne]
end

@[simp] lemma pi_inf_principal_univ_pi_ne_bot :
  ne_bot (pi f ‚äì ùìü (set.pi univ s)) ‚Üî ‚àÄ i, ne_bot (f i ‚äì ùìü (s i)) :=
by simp [ne_bot_iff]

@[simp] lemma pi_inf_principal_pi_ne_bot [Œ† i, ne_bot (f i)] {I : set Œπ} :
  ne_bot (pi f ‚äì ùìü (I.pi s)) ‚Üî ‚àÄ i ‚àà I, ne_bot (f i ‚äì ùìü (s i)) :=
by simp [ne_bot_iff]

instance pi_inf_principal_pi.ne_bot [h : ‚àÄ i, ne_bot (f i ‚äì ùìü (s i))] {I : set Œπ} :
  ne_bot (pi f ‚äì ùìü (I.pi s)) :=
(pi_inf_principal_univ_pi_ne_bot.2 ‚Äπ_‚Ä∫).mono $ inf_le_inf_left _ $ principal_mono.2 $
  Œª x hx i hi, hx i trivial

@[simp] lemma pi_eq_bot : pi f = ‚ä• ‚Üî ‚àÉ i, f i = ‚ä• :=
by simpa using @pi_inf_principal_univ_pi_eq_bot Œπ Œ± f (Œª _, univ)

@[simp] lemma pi_ne_bot : ne_bot (pi f) ‚Üî ‚àÄ i, ne_bot (f i) := by simp [ne_bot_iff]

instance [‚àÄ i, ne_bot (f i)] : ne_bot (pi f) := pi_ne_bot.2 ‚Äπ_‚Ä∫

@[simp] lemma map_eval_pi (f : Œ† i, filter (Œ± i)) [‚àÄ i, ne_bot (f i)] (i : Œπ) :
  map (eval i) (pi f) = f i :=
begin
  refine le_antisymm (tendsto_eval_pi f i) (Œª s hs, _),
  rcases mem_pi.1 (mem_map.1 hs) with ‚ü®I, hIf, t, htf, hI‚ü©,
  rw [‚Üê image_subset_iff] at hI,
  refine mem_of_superset (htf i) ((subset_eval_image_pi _ _).trans hI),
  exact nonempty_of_mem (pi_mem_pi hIf (Œª i hi, htf i))
end

@[simp] lemma pi_le_pi [‚àÄ i, ne_bot (f‚ÇÅ i)] : pi f‚ÇÅ ‚â§ pi f‚ÇÇ ‚Üî ‚àÄ i, f‚ÇÅ i ‚â§ f‚ÇÇ i :=
‚ü®Œª h i, map_eval_pi f‚ÇÅ i ‚ñ∏ (tendsto_eval_pi _ _).mono_left h, pi_mono‚ü©

@[simp] lemma pi_inj [‚àÄ i, ne_bot (f‚ÇÅ i)] : pi f‚ÇÅ = pi f‚ÇÇ ‚Üî f‚ÇÅ = f‚ÇÇ :=
begin
  refine ‚ü®Œª h, _, congr_arg pi‚ü©,
  have hle : f‚ÇÅ ‚â§ f‚ÇÇ := pi_le_pi.1 h.le,
  haveI : ‚àÄ i, ne_bot (f‚ÇÇ i) := Œª i, ne_bot_of_le (hle i),
  exact hle.antisymm (pi_le_pi.1 h.ge)
end

end pi

/-! ### `n`-ary coproducts of filters -/

section Coprod

/-- Coproduct of filters. -/
protected def Coprod (f : Œ† i, filter (Œ± i)) : filter (Œ† i, Œ± i) :=
‚®Ü i : Œπ, comap (eval i) (f i)

lemma mem_Coprod_iff {s : set (Œ† i, Œ± i)} :
  (s ‚àà filter.Coprod f) ‚Üî (‚àÄ i : Œπ, (‚àÉ t‚ÇÅ ‚àà f i, eval i ‚Åª¬π' t‚ÇÅ ‚äÜ s)) :=
by simp [filter.Coprod]

lemma compl_mem_Coprod {s : set (Œ† i, Œ± i)} :
  s·∂ú ‚àà filter.Coprod f ‚Üî ‚àÄ i, (eval i '' s)·∂ú ‚àà f i :=
by simp only [filter.Coprod, mem_supr, compl_mem_comap]

lemma Coprod_ne_bot_iff' :
  ne_bot (filter.Coprod f) ‚Üî (‚àÄ i, nonempty (Œ± i)) ‚àß ‚àÉ d, ne_bot (f d) :=
by simp only [filter.Coprod, supr_ne_bot, ‚Üê exists_and_distrib_left, ‚Üê comap_eval_ne_bot_iff']

@[simp] lemma Coprod_ne_bot_iff [‚àÄ i, nonempty (Œ± i)] :
  ne_bot (filter.Coprod f) ‚Üî ‚àÉ d, ne_bot (f d) :=
by simp [Coprod_ne_bot_iff', *]

lemma Coprod_eq_bot_iff' : filter.Coprod f = ‚ä• ‚Üî (‚àÉ i, is_empty (Œ± i)) ‚à® f = ‚ä• :=
by simpa [not_and_distrib, funext_iff] using not_congr Coprod_ne_bot_iff'

@[simp] lemma Coprod_eq_bot_iff [‚àÄ i, nonempty (Œ± i)] : filter.Coprod f = ‚ä• ‚Üî f = ‚ä• :=
by simpa [funext_iff] using not_congr Coprod_ne_bot_iff

@[simp] lemma Coprod_bot' : filter.Coprod (‚ä• : Œ† i, filter (Œ± i)) = ‚ä• :=
Coprod_eq_bot_iff'.2 (or.inr rfl)

@[simp] lemma Coprod_bot : filter.Coprod (Œª _, ‚ä• : Œ† i, filter (Œ± i)) = ‚ä• := Coprod_bot'

lemma ne_bot.Coprod [‚àÄ i, nonempty (Œ± i)] {i : Œπ} (h : ne_bot (f i)) :
  ne_bot (filter.Coprod f) :=
Coprod_ne_bot_iff.2 ‚ü®i, h‚ü©

@[instance] lemma Coprod_ne_bot [‚àÄ i, nonempty (Œ± i)] [nonempty Œπ] (f : Œ† i, filter (Œ± i))
  [H : ‚àÄ i, ne_bot (f i)] : ne_bot (filter.Coprod f) :=
(H (classical.arbitrary Œπ)).Coprod

@[mono] lemma Coprod_mono (hf : ‚àÄ i, f‚ÇÅ i ‚â§ f‚ÇÇ i) : filter.Coprod f‚ÇÅ ‚â§ filter.Coprod f‚ÇÇ :=
supr_mono $ Œª i, comap_mono (hf i)

variables {Œ≤ : Œπ ‚Üí Type*} {m : Œ† i, Œ± i ‚Üí Œ≤ i}

lemma map_pi_map_Coprod_le :
  map (Œª (k : Œ† i, Œ± i), Œª i, m i (k i)) (filter.Coprod f) ‚â§ filter.Coprod (Œª i, map (m i) (f i)) :=
begin
  simp only [le_def, mem_map, mem_Coprod_iff],
  intros s h i,
  obtain ‚ü®t, H, hH‚ü© := h i,
  exact ‚ü®{x : Œ± i | m i x ‚àà t}, H, Œª x hx, hH hx‚ü©
end

lemma tendsto.pi_map_Coprod {g : Œ† i, filter (Œ≤ i)} (h : ‚àÄ i, tendsto (m i) (f i) (g i)) :
  tendsto (Œª (k : Œ† i, Œ± i), Œª i, m i (k i)) (filter.Coprod f) (filter.Coprod g) :=
map_pi_map_Coprod_le.trans (Coprod_mono h)

end Coprod

end filter
