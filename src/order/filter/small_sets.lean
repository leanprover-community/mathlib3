/-
Copyright (c) 2022 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Floris van Doorn, Yury Kudryashov
-/
import order.filter.lift
import order.filter.at_top_bot

/-!
# The filter of small sets

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the filter of small sets w.r.t. a filter `f`, which is the largest filter
containing all powersets of members of `f`.

`g` converges to `f.small_sets` if for all `s âˆˆ f`, eventually we have `g x âŠ† s`.

An example usage is that if `f : Î¹ â†’ E â†’ â„` is a family of nonnegative functions with integral 1,
then saying that `Î» i, support (f i)` tendsto `(ğ“ 0).small_sets` is a way of saying that
`f` tends to the Dirac delta distribution.
-/

open_locale filter
open filter set

variables {Î± Î² : Type*} {Î¹ : Sort*}

namespace filter

variables {l l' la : filter Î±} {lb : filter Î²}

/-- The filter `l.small_sets` is the largest filter containing all powersets of members of `l`. -/
def small_sets (l : filter Î±) : filter (set Î±) := l.lift' powerset

lemma small_sets_eq_generate {f : filter Î±} : f.small_sets = generate (powerset '' f.sets) :=
by { simp_rw [generate_eq_binfi, small_sets, infi_image], refl }

lemma has_basis.small_sets {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±}
  (h : has_basis l p s) : has_basis l.small_sets p (Î» i, ğ’« (s i)) :=
h.lift' monotone_powerset

lemma has_basis_small_sets (l : filter Î±) :
  has_basis l.small_sets (Î» t : set Î±, t âˆˆ l) powerset :=
l.basis_sets.small_sets

/-- `g` converges to `f.small_sets` if for all `s âˆˆ f`, eventually we have `g x âŠ† s`. -/
lemma tendsto_small_sets_iff {f : Î± â†’ set Î²} :
  tendsto f la lb.small_sets â†” âˆ€ t âˆˆ lb, âˆ€á¶  x in la, f x âŠ† t :=
(has_basis_small_sets lb).tendsto_right_iff

lemma eventually_small_sets {p : set Î± â†’ Prop} :
  (âˆ€á¶  s in l.small_sets, p s) â†” âˆƒ s âˆˆ l, âˆ€ t âŠ† s, p t :=
eventually_lift'_iff monotone_powerset

lemma eventually_small_sets' {p : set Î± â†’ Prop} (hp : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ p t â†’ p s) :
  (âˆ€á¶  s in l.small_sets, p s) â†” âˆƒ s âˆˆ l, p s :=
eventually_small_sets.trans $ existsâ‚‚_congr $ Î» s hsf,
  âŸ¨Î» H, H s subset.rfl, Î» hs t ht, hp ht hsâŸ©

lemma frequently_small_sets {p : set Î± â†’ Prop} :
  (âˆƒá¶  s in l.small_sets, p s) â†” âˆ€ t âˆˆ l, âˆƒ s âŠ† t, p s :=
l.has_basis_small_sets.frequently_iff

lemma frequently_small_sets_mem (l : filter Î±) : âˆƒá¶  s in l.small_sets, s âˆˆ l :=
frequently_small_sets.2 $ Î» t ht, âŸ¨t, subset.rfl, htâŸ©

lemma has_antitone_basis.tendsto_small_sets {Î¹} [preorder Î¹] {s : Î¹ â†’ set Î±}
  (hl : l.has_antitone_basis s) : tendsto s at_top l.small_sets :=
tendsto_small_sets_iff.2 $ Î» t ht, hl.eventually_subset ht

@[mono] lemma monotone_small_sets : monotone (@small_sets Î±) :=
monotone_lift' monotone_id monotone_const

@[simp] lemma small_sets_bot : (âŠ¥ : filter Î±).small_sets = pure âˆ… :=
by rw [small_sets, lift'_bot monotone_powerset, powerset_empty, principal_singleton]

@[simp] lemma small_sets_top : (âŠ¤ : filter Î±).small_sets = âŠ¤ :=
by rw [small_sets, lift'_top, powerset_univ, principal_univ]

@[simp] lemma small_sets_principal (s : set Î±) : (ğ“Ÿ s).small_sets = ğ“Ÿ(ğ’« s) :=
lift'_principal monotone_powerset

lemma small_sets_comap (l : filter Î²) (f : Î± â†’ Î²) :
  (comap f l).small_sets = l.lift' (powerset âˆ˜ preimage f) :=
comap_lift'_eq2 monotone_powerset

lemma comap_small_sets (l : filter Î²) (f : Î± â†’ set Î²) :
  comap f l.small_sets = l.lift' (preimage f âˆ˜ powerset) :=
comap_lift'_eq

lemma small_sets_infi {f : Î¹ â†’ filter Î±} :
  (infi f).small_sets = (â¨… i, (f i).small_sets) :=
lift'_infi_of_map_univ powerset_inter powerset_univ

lemma small_sets_inf (lâ‚ lâ‚‚ : filter Î±) :
  (lâ‚ âŠ“ lâ‚‚).small_sets = lâ‚.small_sets âŠ“ lâ‚‚.small_sets :=
lift'_inf _ _ powerset_inter

instance small_sets_ne_bot (l : filter Î±) : ne_bot l.small_sets :=
(lift'_ne_bot_iff monotone_powerset).2 $ Î» _ _, powerset_nonempty

lemma tendsto.small_sets_mono {s t : Î± â†’ set Î²}
  (ht : tendsto t la lb.small_sets) (hst : âˆ€á¶  x in la, s x âŠ† t x) :
  tendsto s la lb.small_sets :=
begin
  rw [tendsto_small_sets_iff] at ht âŠ¢,
  exact Î» u hu, (ht u hu).mp (hst.mono $ Î» a hst ht, subset.trans hst ht)
end

/-- Generalized **squeeze theorem** (also known as **sandwich theorem**). If `s : Î± â†’ set Î²` is a
family of sets that tends to `filter.small_sets lb` along `la` and `f : Î± â†’ Î²` is a function such
that `f x âˆˆ s x` eventually along `la`, then `f` tends to `lb` along `la`.

If `s x` is the closed interval `[g x, h x]` for some functions `g`, `h` that tend to the same limit
`ğ“ y`, then we obtain the standard squeeze theorem, see
`tendsto_of_tendsto_of_tendsto_of_le_of_le'`. -/
lemma tendsto.of_small_sets {s : Î± â†’ set Î²} {f : Î± â†’ Î²} (hs : tendsto s la lb.small_sets)
  (hf : âˆ€á¶  x in la, f x âˆˆ s x) : tendsto f la lb :=
Î» t ht, hf.mp $ (tendsto_small_sets_iff.mp hs t ht).mono $ Î» x hâ‚ hâ‚‚, hâ‚ hâ‚‚

@[simp] lemma eventually_small_sets_eventually {p : Î± â†’ Prop} :
  (âˆ€á¶  s in l.small_sets, âˆ€á¶  x in l', x âˆˆ s â†’ p x) â†” âˆ€á¶  x in l âŠ“ l', p x :=
calc _ â†” âˆƒ s âˆˆ l, âˆ€á¶  x in l', x âˆˆ s â†’ p x :
  eventually_small_sets' $ Î» s t hst ht, ht.mono $ Î» x hx hs, hx (hst hs)
... â†” âˆƒ (s âˆˆ l) (t âˆˆ l'), âˆ€ x, x âˆˆ t â†’ x âˆˆ s â†’ p x :
  by simp only [eventually_iff_exists_mem]
... â†” âˆ€á¶  x in l âŠ“ l', p x : by simp only [eventually_inf, and_comm, mem_inter_iff, â† and_imp]

@[simp] lemma eventually_small_sets_forall {p : Î± â†’ Prop} :
  (âˆ€á¶  s in l.small_sets, âˆ€ x âˆˆ s, p x) â†” âˆ€á¶  x in l, p x :=
by simpa only [inf_top_eq, eventually_top] using @eventually_small_sets_eventually Î± l âŠ¤ p

alias eventually_small_sets_forall â†” eventually.of_small_sets eventually.small_sets

@[simp] lemma eventually_small_sets_subset {s : set Î±} :
  (âˆ€á¶  t in l.small_sets, t âŠ† s) â†” s âˆˆ l :=
eventually_small_sets_forall

end filter
