/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Jeremy Avigad
-/
import order.filter.cofinite

/-!
# Ultrafilters

An ultrafilter is a minimal (maximal in the set order) proper filter.
In this file we define

* `is_ultrafilter`: a predicate stating that a given filter is an ultrafiler;
* `ultrafilter_of`: an ultrafilter that is less than or equal to a given filter;
* `ultrafilter`: subtype of ultrafilters;
* `ultrafilter.pure`: `pure x` as an `ultrafiler`;
* `ultrafilter.map`, `ultrafilter.bind` : operations on ultrafilters;
* `hyperfilter`: the ultra-filter extending the cofinite filter.
-/

universes u v
variables {Î± : Type u} {Î² : Type v}

namespace filter

open set zorn
open_locale classical filter

variables {f g : filter Î±}

/-- An ultrafilter is a minimal (maximal in the set order) proper filter. -/
def is_ultrafilter (f : filter Î±) := f â‰  âŠ¥ âˆ§ âˆ€g, g â‰  âŠ¥ â†’ g â‰¤ f â†’ f â‰¤ g

lemma ultrafilter_unique (hg : is_ultrafilter g) (hf : f â‰  âŠ¥) (h : f â‰¤ g) : f = g :=
le_antisymm h (hg.right _ hf h)

lemma le_of_ultrafilter {g : filter Î±} (hf : is_ultrafilter f) (h : g âŠ“ f â‰  âŠ¥) :
  f â‰¤ g :=
by { rw inf_comm at h, exact le_of_inf_eq (ultrafilter_unique hf h inf_le_left) }

/-- Equivalent characterization of ultrafilters:
  A filter f is an ultrafilter if and only if for each set s,
  -s belongs to f if and only if s does not belong to f. -/
lemma ultrafilter_iff_compl_mem_iff_not_mem :
  is_ultrafilter f â†” (âˆ€ s, sá¶œ âˆˆ f â†” s âˆ‰ f) :=
âŸ¨assume hf s,
   âŸ¨assume hns hs,
      hf.1 $ empty_in_sets_eq_bot.mp $ by convert f.inter_sets hs hns; rw [inter_compl_self],
    assume hs,
      have f â‰¤ ğ“Ÿ sá¶œ, from
        le_of_ultrafilter hf $ assume h, hs $ mem_sets_of_eq_bot $
          by rwa inf_comm,
      by simp only [le_principal_iff] at this; assumptionâŸ©,
 assume hf,
   âŸ¨mt empty_in_sets_eq_bot.mpr ((hf âˆ…).mp (by convert f.univ_sets; rw [compl_empty])),
    assume g hg g_le s hs, classical.by_contradiction $ mt (hf s).mpr $
      assume : sá¶œ âˆˆ f,
        have s âˆ© sá¶œ âˆˆ g, from inter_mem_sets hs (g_le this),
        by simp only [empty_in_sets_eq_bot, hg, inter_compl_self] at this; contradictionâŸ©âŸ©

lemma mem_or_compl_mem_of_ultrafilter (hf : is_ultrafilter f) (s : set Î±) :
  s âˆˆ f âˆ¨ sá¶œ âˆˆ f :=
classical.or_iff_not_imp_left.2 (ultrafilter_iff_compl_mem_iff_not_mem.mp hf s).mpr

lemma mem_or_mem_of_ultrafilter {s t : set Î±} (hf : is_ultrafilter f) (h : s âˆª t âˆˆ f) :
  s âˆˆ f âˆ¨ t âˆˆ f :=
(mem_or_compl_mem_of_ultrafilter hf s).imp_right
  (assume : sá¶œ âˆˆ f, by filter_upwards [this, h] assume x hnx hx, hx.resolve_left hnx)

lemma is_ultrafilter.em (hf : is_ultrafilter f) (p : Î± â†’ Prop) :
  (âˆ€á¶  x in f, p x) âˆ¨ âˆ€á¶  x in f, Â¬p x :=
mem_or_compl_mem_of_ultrafilter hf {x | p x}

lemma is_ultrafilter.eventually_or (hf : is_ultrafilter f) {p q : Î± â†’ Prop} :
  (âˆ€á¶  x in f, p x âˆ¨ q x) â†” (âˆ€á¶  x in f, p x) âˆ¨ âˆ€á¶  x in f, q x :=
âŸ¨mem_or_mem_of_ultrafilter hf, Î» H, H.elim (Î» hp, hp.mono $ Î» x, or.inl)
  (Î» hp, hp.mono $ Î» x, or.inr)âŸ©

lemma is_ultrafilter.eventually_not (hf : is_ultrafilter f) {p : Î± â†’ Prop} :
  (âˆ€á¶  x in f, Â¬p x) â†” Â¬âˆ€á¶  x in f, p x :=
ultrafilter_iff_compl_mem_iff_not_mem.1 hf {x | p x}

lemma is_ultrafilter.eventually_imp (hf : is_ultrafilter f) {p q : Î± â†’ Prop} :
  (âˆ€á¶  x in f, p x â†’ q x) â†” (âˆ€á¶  x in f, p x) â†’ âˆ€á¶  x in f, q x :=
by simp only [imp_iff_not_or, hf.eventually_or, hf.eventually_not]

lemma mem_of_finite_sUnion_ultrafilter {s : set (set Î±)} (hf : is_ultrafilter f) (hs : finite s)
  : â‹ƒâ‚€ s âˆˆ f â†’ âˆƒtâˆˆs, t âˆˆ f :=
finite.induction_on hs (by simp only [empty_in_sets_eq_bot, hf.left, mem_empty_eq, sUnion_empty,
  forall_prop_of_false, exists_false, not_false_iff, exists_prop_of_false]) $
Î» t s' ht' hs' ih, by simp only [exists_prop, mem_insert_iff, set.sUnion_insert]; exact
assume h, (mem_or_mem_of_ultrafilter hf h).elim
  (assume : t âˆˆ f, âŸ¨t, or.inl rfl, thisâŸ©)
  (assume h, let âŸ¨t, hts', htâŸ© := ih h in âŸ¨t, or.inr hts', htâŸ©)

lemma mem_of_finite_Union_ultrafilter {is : set Î²} {s : Î² â†’ set Î±}
  (hf : is_ultrafilter f) (his : finite is) (h : (â‹ƒiâˆˆis, s i) âˆˆ f) : âˆƒiâˆˆis, s i âˆˆ f :=
have his : finite (image s is), from his.image s,
have h : (â‹ƒâ‚€ image s is) âˆˆ f, from by simp only [sUnion_image, set.sUnion_image]; assumption,
let âŸ¨t, âŸ¨i, hi, h_eqâŸ©, (ht : t âˆˆ f)âŸ© := mem_of_finite_sUnion_ultrafilter hf his h in
âŸ¨i, hi, h_eq.symm â–¸ htâŸ©

lemma ultrafilter_map {f : filter Î±} {m : Î± â†’ Î²} (h : is_ultrafilter f) :
  is_ultrafilter (map m f) :=
by rw ultrafilter_iff_compl_mem_iff_not_mem at âŠ¢ h; exact assume s, h (m â»Â¹' s)

lemma ultrafilter_pure {a : Î±} : is_ultrafilter (pure a) :=
begin
  rw ultrafilter_iff_compl_mem_iff_not_mem, intro s,
  rw [mem_pure_sets, mem_pure_sets], exact iff.rfl
end

lemma ultrafilter_bind {f : filter Î±} (hf : is_ultrafilter f) {m : Î± â†’ filter Î²}
  (hm : âˆ€ a, is_ultrafilter (m a)) : is_ultrafilter (f.bind m) :=
begin
  simp only [ultrafilter_iff_compl_mem_iff_not_mem] at âŠ¢ hf hm, intro s,
  dsimp [bind, join, map, preimage],
  simp only [hm], apply hf
end

/-- The ultrafilter lemma: Any proper filter is contained in an ultrafilter. -/
lemma exists_ultrafilter (h : f â‰  âŠ¥) : âˆƒu, u â‰¤ f âˆ§ is_ultrafilter u :=
let
  Ï„                := {f' // f' â‰  âŠ¥ âˆ§ f' â‰¤ f},
  r : Ï„ â†’ Ï„ â†’ Prop := Î»tâ‚ tâ‚‚, tâ‚‚.val â‰¤ tâ‚.val,
  âŸ¨a, haâŸ©          := nonempty_of_mem_sets h univ_mem_sets,
  top : Ï„          := âŸ¨f, h, le_refl fâŸ©,
  sup : Î (c:set Ï„), chain r c â†’ Ï„ :=
    Î»c hc, âŸ¨â¨…a:{a:Ï„ // a âˆˆ insert top c}, a.val.val,
      infi_ne_bot_of_directed âŸ¨aâŸ©
        (directed_of_chain $ chain_insert hc $ assume âŸ¨b, _, hbâŸ© _ _, or.inl hb)
        (assume âŸ¨âŸ¨a, ha, _âŸ©, _âŸ©, ha),
      infi_le_of_le âŸ¨top, mem_insert _ _âŸ© (le_refl _)âŸ©
in
have âˆ€c (hc: chain r c) a (ha : a âˆˆ c), r a (sup c hc),
  from assume c hc a ha, infi_le_of_le âŸ¨a, mem_insert_of_mem _ haâŸ© (le_refl _),
have (âˆƒ (u : Ï„), âˆ€ (a : Ï„), r u a â†’ r a u),
  from exists_maximal_of_chains_bounded (assume c hc, âŸ¨sup c hc, this c hcâŸ©)
    (assume fâ‚ fâ‚‚ fâ‚ƒ hâ‚ hâ‚‚, le_trans hâ‚‚ hâ‚),
let âŸ¨uÏ„, hminâŸ© := this in
âŸ¨uÏ„.val, uÏ„.property.right, uÏ„.property.left, assume g hgâ‚ hgâ‚‚,
  hmin âŸ¨g, hgâ‚, le_trans hgâ‚‚ uÏ„.property.rightâŸ© hgâ‚‚âŸ©

/-- Construct an ultrafilter extending a given filter.
  The ultrafilter lemma is the assertion that such a filter exists;
  we use the axiom of choice to pick one. -/
noncomputable def ultrafilter_of (f : filter Î±) : filter Î± :=
if h : f = âŠ¥ then âŠ¥ else classical.epsilon (Î»u, u â‰¤ f âˆ§ is_ultrafilter u)

lemma ultrafilter_of_spec (h : f â‰  âŠ¥) : ultrafilter_of f â‰¤ f âˆ§ is_ultrafilter (ultrafilter_of f) :=
begin
  have h' := classical.epsilon_spec (exists_ultrafilter h),
  simp only [ultrafilter_of, dif_neg, h, dif_neg, not_false_iff],
  simp only at h',
  assumption
end

lemma ultrafilter_of_le : ultrafilter_of f â‰¤ f :=
if h : f = âŠ¥ then by simp only [ultrafilter_of, h, dif_pos, le_bot_iff]
  else (ultrafilter_of_spec h).left

lemma ultrafilter_ultrafilter_of (h : f â‰  âŠ¥) : is_ultrafilter (ultrafilter_of f) :=
(ultrafilter_of_spec h).right

lemma ultrafilter_of_ultrafilter (h : is_ultrafilter f) : ultrafilter_of f = f :=
ultrafilter_unique h (ultrafilter_ultrafilter_of h.left).left ultrafilter_of_le

/-- A filter equals the intersection of all the ultrafilters which contain it. -/
lemma sup_of_ultrafilters (f : filter Î±) : f = â¨† (g) (u : is_ultrafilter g) (H : g â‰¤ f), g :=
begin
  refine le_antisymm _ (supr_le $ Î» g, supr_le $ Î» u, supr_le $ Î» H, H),
  intros s hs,
  -- If s âˆ‰ f.sets, we'll apply the ultrafilter lemma to the restriction of f to -s.
  by_contradiction hs',
  let j : sá¶œ â†’ Î± := subtype.val,
  have j_inv_s : j â»Â¹' s = âˆ…, by
    erw [â†preimage_inter_range, subtype.range_coe, inter_compl_self, preimage_empty],
  let f' := comap j f,
  have : f' â‰  âŠ¥,
  { apply mt empty_in_sets_eq_bot.mpr,
    rintro âŸ¨t, htf, htâŸ©,
    suffices : t âŠ† s, from absurd (f.sets_of_superset htf this) hs',
    rw [subset_empty_iff] at ht,
    have : j '' (j â»Â¹' t) = âˆ…, by rw [ht, image_empty],
    erw [image_preimage_eq_inter_range, subtype.range_coe, â†subset_compl_iff_disjoint,
      set.compl_compl] at this,
    exact this },
  rcases exists_ultrafilter this with âŸ¨g', g'f', u'âŸ©,
  simp only [supr_sets_eq, mem_Inter] at hs,
  have := hs (g'.map subtype.val) (ultrafilter_map u') (map_le_iff_le_comap.mpr g'f'),
  rw [â†le_principal_iff, map_le_iff_le_comap, comap_principal, j_inv_s, principal_empty,
    le_bot_iff] at this,
  exact absurd this u'.1
end

/-- The `tendsto` relation can be checked on ultrafilters. -/
lemma tendsto_iff_ultrafilter (f : Î± â†’ Î²) (lâ‚ : filter Î±) (lâ‚‚ : filter Î²) :
  tendsto f lâ‚ lâ‚‚ â†” âˆ€ g, is_ultrafilter g â†’ g â‰¤ lâ‚ â†’ g.map f â‰¤ lâ‚‚ :=
âŸ¨assume h g u gx, le_trans (map_mono gx) h,
 assume h, by rw [sup_of_ultrafilters lâ‚]; simpa only [tendsto, map_supr, supr_le_iff]âŸ©

/-- The ultrafilter monad. The monad structure on ultrafilters is the
  restriction of the one on filters. -/
def ultrafilter (Î± : Type u) : Type u := {f : filter Î± // is_ultrafilter f}

/-- Push-forward for ultra-filters. -/
def ultrafilter.map (m : Î± â†’ Î²) (u : ultrafilter Î±) : ultrafilter Î² :=
âŸ¨u.val.map m, ultrafilter_map u.propertyâŸ©

/-- The principal ultra-filter associated to a point `x`. -/
def ultrafilter.pure (x : Î±) : ultrafilter Î± := âŸ¨pure x, ultrafilter_pureâŸ©

/-- Monadic bind for ultra-filters, coming from the one on filters
defined in terms of map and join.-/
def ultrafilter.bind (u : ultrafilter Î±) (m : Î± â†’ ultrafilter Î²) : ultrafilter Î² :=
âŸ¨u.val.bind (Î» a, (m a).val), ultrafilter_bind u.property (Î» a, (m a).property)âŸ©

instance ultrafilter.has_pure : has_pure ultrafilter := âŸ¨@ultrafilter.pureâŸ©
instance ultrafilter.has_bind : has_bind ultrafilter := âŸ¨@ultrafilter.bindâŸ©
instance ultrafilter.functor : functor ultrafilter := { map := @ultrafilter.map }
instance ultrafilter.monad : monad ultrafilter := { map := @ultrafilter.map }

instance ultrafilter.inhabited [inhabited Î±] : inhabited (ultrafilter Î±) := âŸ¨pure (default _)âŸ©

/-- The ultra-filter extending the cofinite filter. -/
noncomputable def hyperfilter : filter Î± := ultrafilter_of cofinite

lemma hyperfilter_le_cofinite : @hyperfilter Î± â‰¤ cofinite :=
ultrafilter_of_le

lemma is_ultrafilter_hyperfilter [infinite Î±] : is_ultrafilter (@hyperfilter Î±) :=
(ultrafilter_of_spec cofinite_ne_bot).2

@[simp] lemma hyperfilter_ne_bot [infinite Î±] : @hyperfilter Î± â‰  âŠ¥ :=
is_ultrafilter_hyperfilter.1

@[simp] lemma bot_ne_hyperfilter [infinite Î±] : âŠ¥ â‰  @hyperfilter Î± :=
is_ultrafilter_hyperfilter.1.symm

theorem nmem_hyperfilter_of_finite [infinite Î±] {s : set Î±} (hf : s.finite) :
  s âˆ‰ @hyperfilter Î± :=
Î» hy,
have hx : sá¶œ âˆ‰ hyperfilter :=
  Î» hs, (ultrafilter_iff_compl_mem_iff_not_mem.mp is_ultrafilter_hyperfilter s).mp hs hy,
have ht : sá¶œ âˆˆ cofinite.sets := by show sá¶œ âˆˆ {s | _}; rwa [set.mem_set_of_eq, compl_compl],
hx $ hyperfilter_le_cofinite ht

theorem compl_mem_hyperfilter_of_finite [infinite Î±] {s : set Î±} (hf : set.finite s) :
  sá¶œ âˆˆ @hyperfilter Î± :=
(ultrafilter_iff_compl_mem_iff_not_mem.mp is_ultrafilter_hyperfilter s).mpr $
nmem_hyperfilter_of_finite hf

theorem mem_hyperfilter_of_finite_compl [infinite Î±] {s : set Î±} (hf : set.finite sá¶œ) :
  s âˆˆ @hyperfilter Î± :=
s.compl_compl â–¸ compl_mem_hyperfilter_of_finite hf

section

local attribute [instance] filter.monad filter.is_lawful_monad

instance ultrafilter.is_lawful_monad : is_lawful_monad ultrafilter :=
{ id_map := assume Î± f, subtype.eq (id_map f.val),
  pure_bind := assume Î± Î² a f, subtype.eq (pure_bind a (subtype.val âˆ˜ f)),
  bind_assoc := assume Î± Î² Î³ f mâ‚ mâ‚‚, subtype.eq (filter_eq rfl),
  bind_pure_comp_eq_map := assume Î± Î² f x, subtype.eq (bind_pure_comp_eq_map f x.val) }

end

lemma ultrafilter.eq_iff_val_le_val {u v : ultrafilter Î±} : u = v â†” u.val â‰¤ v.val :=
âŸ¨assume h, by rw h; exact le_refl _,
 assume h, by rw subtype.ext_iff_val; apply ultrafilter_unique v.property u.property.1 hâŸ©

lemma exists_ultrafilter_iff (f : filter Î±) : (âˆƒ (u : ultrafilter Î±), u.val â‰¤ f) â†” f â‰  âŠ¥ :=
âŸ¨assume âŸ¨u, ufâŸ©, ne_bot_of_le_ne_bot u.property.1 uf,
 assume h, let âŸ¨u, uf, huâŸ© := exists_ultrafilter h in âŸ¨âŸ¨u, huâŸ©, ufâŸ©âŸ©

end filter
