/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Jeremy Avigad, Yury Kudryashov
-/
import order.filter.cofinite
import order.zorn_atoms

/-!
# Ultrafilters

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

An ultrafilter is a minimal (maximal in the set order) proper filter.
In this file we define

* `ultrafilter.of`: an ultrafilter that is less than or equal to a given filter;
* `ultrafilter`: subtype of ultrafilters;
* `ultrafilter.pure`: `pure x` as an `ultrafiler`;
* `ultrafilter.map`, `ultrafilter.bind`, `ultrafilter.comap` : operations on ultrafilters;
* `hyperfilter`: the ultrafilter extending the cofinite filter.
-/

universes u v
variables {Î± : Type u} {Î² : Type v} {Î³ : Type*}

open set filter function
open_locale classical filter

/-- `filter Î±` is an atomic type: for every filter there exists an ultrafilter that is less than or
equal to this filter. -/
instance : is_atomic (filter Î±) :=
is_atomic.of_is_chain_bounded $ Î» c hc hne hb,
  âŸ¨Inf c, (Inf_ne_bot_of_directed' hne (show is_chain (â‰¥) c, from hc.symm).directed_on hb).ne,
    Î» x hx, Inf_le hxâŸ©

/-- An ultrafilter is a minimal (maximal in the set order) proper filter. -/
@[protect_proj]
structure ultrafilter (Î± : Type*) extends filter Î± :=
(ne_bot' : ne_bot to_filter)
(le_of_le : âˆ€ g, filter.ne_bot g â†’ g â‰¤ to_filter â†’ to_filter â‰¤ g)

namespace ultrafilter

variables {f g : ultrafilter Î±} {s t : set Î±} {p q : Î± â†’ Prop}

instance : has_coe_t (ultrafilter Î±) (filter Î±) := âŸ¨ultrafilter.to_filterâŸ©

instance : has_mem (set Î±) (ultrafilter Î±) := âŸ¨Î» s f, s âˆˆ (f : filter Î±)âŸ©

lemma unique (f : ultrafilter Î±) {g : filter Î±} (h : g â‰¤ f)
  (hne : ne_bot g . tactic.apply_instance) : g = f :=
le_antisymm h $ f.le_of_le g hne h

instance ne_bot (f : ultrafilter Î±) : ne_bot (f : filter Î±) := f.ne_bot'

protected lemma is_atom (f : ultrafilter Î±) : is_atom (f : filter Î±) :=
âŸ¨f.ne_bot.ne, Î» g hgf, by_contra $ Î» hg, hgf.ne $ f.unique hgf.le âŸ¨hgâŸ©âŸ©

@[simp, norm_cast] lemma mem_coe : s âˆˆ (f : filter Î±) â†” s âˆˆ f := iff.rfl

lemma coe_injective : injective (coe : ultrafilter Î± â†’ filter Î±)
| âŸ¨f, hâ‚, hâ‚‚âŸ© âŸ¨g, hâ‚ƒ, hâ‚„âŸ© rfl := by congr

lemma eq_of_le {f g : ultrafilter Î±} (h : (f : filter Î±) â‰¤ g) : f = g :=
coe_injective (g.unique h)

@[simp, norm_cast] lemma coe_le_coe {f g : ultrafilter Î±} : (f : filter Î±) â‰¤ g â†” f = g :=
âŸ¨Î» h, eq_of_le h, Î» h, h â–¸ le_rflâŸ©

@[simp, norm_cast] lemma coe_inj : (f : filter Î±) = g â†” f = g := coe_injective.eq_iff

@[ext] lemma ext â¦ƒf g : ultrafilter Î±â¦„ (h : âˆ€ s, s âˆˆ f â†” s âˆˆ g) : f = g :=
coe_injective $ filter.ext h

lemma le_of_inf_ne_bot (f : ultrafilter Î±) {g : filter Î±} (hg : ne_bot (â†‘f âŠ“ g)) : â†‘f â‰¤ g :=
le_of_inf_eq (f.unique inf_le_left hg)

lemma le_of_inf_ne_bot' (f : ultrafilter Î±) {g : filter Î±} (hg : ne_bot (g âŠ“ f)) : â†‘f â‰¤ g :=
f.le_of_inf_ne_bot $ by rwa inf_comm

lemma inf_ne_bot_iff {f : ultrafilter Î±} {g : filter Î±} : ne_bot (â†‘f âŠ“ g) â†” â†‘f â‰¤ g :=
âŸ¨le_of_inf_ne_bot f, Î» h, (inf_of_le_left h).symm â–¸ f.ne_botâŸ©

lemma disjoint_iff_not_le {f : ultrafilter Î±} {g : filter Î±} : disjoint â†‘f g â†” Â¬â†‘f â‰¤ g :=
by rw [â† inf_ne_bot_iff, ne_bot_iff, ne.def, not_not, disjoint_iff]

@[simp] lemma compl_not_mem_iff : sá¶œ âˆ‰ f â†” s âˆˆ f :=
âŸ¨Î» hsc, le_principal_iff.1 $ f.le_of_inf_ne_bot
  âŸ¨Î» h, hsc $ mem_of_eq_bot$ by rwa compl_complâŸ©, compl_not_memâŸ©

@[simp] lemma frequently_iff_eventually : (âˆƒá¶  x in f, p x) â†” âˆ€á¶  x in f, p x :=
compl_not_mem_iff

alias frequently_iff_eventually â†” _root_.filter.frequently.eventually _

lemma compl_mem_iff_not_mem : sá¶œ âˆˆ f â†” s âˆ‰ f := by rw [â† compl_not_mem_iff, compl_compl]

lemma diff_mem_iff (f : ultrafilter Î±) : s \ t âˆˆ f â†” s âˆˆ f âˆ§ t âˆ‰ f :=
inter_mem_iff.trans $ and_congr iff.rfl compl_mem_iff_not_mem

/-- If `sá¶œ âˆ‰ f â†” s âˆˆ f`, then `f` is an ultrafilter. The other implication is given by
`ultrafilter.compl_not_mem_iff`.  -/
def of_compl_not_mem_iff (f : filter Î±) (h : âˆ€ s, sá¶œ âˆ‰ f â†” s âˆˆ f) : ultrafilter Î± :=
{ to_filter := f,
  ne_bot' := âŸ¨Î» hf, by simpa [hf] using hâŸ©,
  le_of_le := Î» g hg hgf s hs, (h s).1 $ Î» hsc, by exactI compl_not_mem hs (hgf hsc) }

/-- If `f : filter Î±` is an atom, then it is an ultrafilter. -/
def of_atom (f : filter Î±) (hf : is_atom f) : ultrafilter Î± :=
{ to_filter := f,
  ne_bot' := âŸ¨hf.1âŸ©,
  le_of_le := Î» g hg, (_root_.is_atom_iff.1 hf).2 g hg.ne }

lemma nonempty_of_mem (hs : s âˆˆ f) : s.nonempty := nonempty_of_mem hs
lemma ne_empty_of_mem (hs : s âˆˆ f) : s â‰  âˆ… := (nonempty_of_mem hs).ne_empty
@[simp] lemma empty_not_mem : âˆ… âˆ‰ f := empty_not_mem f

@[simp] lemma le_sup_iff {u : ultrafilter Î±} {f g : filter Î±} : â†‘u â‰¤ f âŠ” g â†” â†‘u â‰¤ f âˆ¨ â†‘u â‰¤ g :=
not_iff_not.1 $ by simp only [â† disjoint_iff_not_le, not_or_distrib, disjoint_sup_right]

@[simp] lemma union_mem_iff : s âˆª t âˆˆ f â†” s âˆˆ f âˆ¨ t âˆˆ f :=
by simp only [â† mem_coe, â† le_principal_iff, â† sup_principal, le_sup_iff]

lemma mem_or_compl_mem (f : ultrafilter Î±) (s : set Î±) : s âˆˆ f âˆ¨ sá¶œ âˆˆ f :=
or_iff_not_imp_left.2 compl_mem_iff_not_mem.2

protected lemma em (f : ultrafilter Î±) (p : Î± â†’ Prop) :
  (âˆ€á¶  x in f, p x) âˆ¨ âˆ€á¶  x in f, Â¬p x :=
f.mem_or_compl_mem {x | p x}

lemma eventually_or : (âˆ€á¶  x in f, p x âˆ¨ q x) â†” (âˆ€á¶  x in f, p x) âˆ¨ âˆ€á¶  x in f, q x :=
union_mem_iff

lemma eventually_not : (âˆ€á¶  x in f, Â¬p x) â†” Â¬âˆ€á¶  x in f, p x := compl_mem_iff_not_mem

lemma eventually_imp : (âˆ€á¶  x in f, p x â†’ q x) â†” (âˆ€á¶  x in f, p x) â†’ âˆ€á¶  x in f, q x :=
by simp only [imp_iff_not_or, eventually_or, eventually_not]

lemma finite_sUnion_mem_iff {s : set (set Î±)} (hs : s.finite) : â‹ƒâ‚€ s âˆˆ f â†” âˆƒtâˆˆs, t âˆˆ f :=
finite.induction_on hs (by simp) $ Î» a s ha hs his,
  by simp [union_mem_iff, his, or_and_distrib_right, exists_or_distrib]

lemma finite_bUnion_mem_iff {is : set Î²} {s : Î² â†’ set Î±} (his : is.finite) :
  (â‹ƒiâˆˆis, s i) âˆˆ f â†” âˆƒiâˆˆis, s i âˆˆ f :=
by simp only [â† sUnion_image, finite_sUnion_mem_iff (his.image s), bex_image_iff]

/-- Pushforward for ultrafilters. -/
def map (m : Î± â†’ Î²) (f : ultrafilter Î±) : ultrafilter Î² :=
of_compl_not_mem_iff (map m f) $ Î» s, @compl_not_mem_iff _ f (m â»Â¹' s)

@[simp, norm_cast] lemma coe_map (m : Î± â†’ Î²) (f : ultrafilter Î±) :
  (map m f : filter Î²) = filter.map m â†‘f := rfl

@[simp] lemma mem_map {m : Î± â†’ Î²} {f : ultrafilter Î±} {s : set Î²} :
  s âˆˆ map m f â†” m â»Â¹' s âˆˆ f := iff.rfl

@[simp] lemma map_id (f : ultrafilter Î±) : f.map id = f := coe_injective map_id
@[simp] lemma map_id' (f : ultrafilter Î±) : f.map (Î» x, x) = f := map_id _

@[simp] lemma map_map (f : ultrafilter Î±) (m : Î± â†’ Î²) (n : Î² â†’ Î³) :
  (f.map m).map n = f.map (n âˆ˜ m) :=
coe_injective map_map

/-- The pullback of an ultrafilter along an injection whose range is large with respect to the given
ultrafilter. -/
def comap {m : Î± â†’ Î²} (u : ultrafilter Î²) (inj : injective m)
  (large : set.range m âˆˆ u) : ultrafilter Î± :=
{ to_filter := comap m u,
  ne_bot' := u.ne_bot'.comap_of_range_mem large,
  le_of_le := Î» g hg hgu, by { resetI,
    simp only [â† u.unique (map_le_iff_le_comap.2 hgu), comap_map inj, le_rfl] } }

@[simp] lemma mem_comap {m : Î± â†’ Î²} (u : ultrafilter Î²) (inj : injective m)
  (large : set.range m âˆˆ u) {s : set Î±} :
  s âˆˆ u.comap inj large â†” m '' s âˆˆ u :=
mem_comap_iff inj large

@[simp, norm_cast] lemma coe_comap {m : Î± â†’ Î²} (u : ultrafilter Î²) (inj : injective m)
  (large : set.range m âˆˆ u) : (u.comap inj large : filter Î±) = filter.comap m u := rfl

@[simp] lemma comap_id (f : ultrafilter Î±) (hâ‚€ : injective (id : Î± â†’ Î±) := injective_id)
  (hâ‚ : range id âˆˆ f := by { rw range_id, exact univ_mem}) :
  f.comap hâ‚€ hâ‚ = f :=
coe_injective comap_id

@[simp] lemma comap_comap (f : ultrafilter Î³) {m : Î± â†’ Î²} {n : Î² â†’ Î³} (injâ‚€ : injective n)
  (largeâ‚€ : range n âˆˆ f) (injâ‚ : injective m) (largeâ‚ : range m âˆˆ f.comap injâ‚€ largeâ‚€)
  (injâ‚‚ : injective (n âˆ˜ m) := injâ‚€.comp injâ‚)
  (largeâ‚‚ : range (n âˆ˜ m) âˆˆ f := by { rw range_comp, exact image_mem_of_mem_comap largeâ‚€ largeâ‚ }) :
  (f.comap injâ‚€ largeâ‚€).comap injâ‚ largeâ‚ = f.comap injâ‚‚ largeâ‚‚ :=
coe_injective comap_comap

/-- The principal ultrafilter associated to a point `x`. -/
instance : has_pure ultrafilter :=
âŸ¨Î» Î± a, of_compl_not_mem_iff (pure a) $ Î» s, by simpâŸ©

@[simp] lemma mem_pure {a : Î±} {s : set Î±} : s âˆˆ (pure a : ultrafilter Î±) â†” a âˆˆ s := iff.rfl
@[simp] lemma coe_pure (a : Î±) : â†‘(pure a : ultrafilter Î±) = (pure a : filter Î±) := rfl
@[simp] lemma map_pure (m : Î± â†’ Î²) (a : Î±) : map m (pure a) = pure (m a) := rfl
@[simp] lemma comap_pure {m : Î± â†’ Î²} (a : Î±) (inj : injective m) (large) :
  comap (pure $ m a) inj large = pure a :=
coe_injective $ comap_pure.trans $
  by rw [coe_pure, â†principal_singleton, â†image_singleton, preimage_image_eq _ inj]

lemma pure_injective : injective (pure : Î± â†’ ultrafilter Î±) :=
Î» a b h, filter.pure_injective (congr_arg ultrafilter.to_filter h : _)

instance [inhabited Î±] : inhabited (ultrafilter Î±) := âŸ¨pure defaultâŸ©
instance [nonempty Î±] : nonempty (ultrafilter Î±) := nonempty.map pure infer_instance

lemma eq_pure_of_finite_mem (h : s.finite) (h' : s âˆˆ f) : âˆƒ x âˆˆ s, f = pure x :=
begin
  rw â† bUnion_of_singleton s at h',
  rcases (ultrafilter.finite_bUnion_mem_iff h).mp h' with âŸ¨a, has, hafâŸ©,
  exact âŸ¨a, has, eq_of_le (filter.le_pure_iff.2 haf)âŸ©
end

lemma eq_pure_of_finite [finite Î±] (f : ultrafilter Î±) : âˆƒ a, f = pure a :=
(eq_pure_of_finite_mem finite_univ univ_mem).imp $ Î» a âŸ¨_, haâŸ©, ha

lemma le_cofinite_or_eq_pure (f : ultrafilter Î±) : (f : filter Î±) â‰¤ cofinite âˆ¨ âˆƒ a, f = pure a :=
or_iff_not_imp_left.2 $ Î» h,
  let âŸ¨s, hs, hfinâŸ© := filter.disjoint_cofinite_right.1 (disjoint_iff_not_le.2 h),
      âŸ¨a, has, hfâŸ© := eq_pure_of_finite_mem hfin hs
  in âŸ¨a, hfâŸ©

/-- Monadic bind for ultrafilters, coming from the one on filters
defined in terms of map and join.-/
def bind (f : ultrafilter Î±) (m : Î± â†’ ultrafilter Î²) : ultrafilter Î² :=
of_compl_not_mem_iff (bind â†‘f (Î» x, â†‘(m x))) $ Î» s,
  by simp only [mem_bind', mem_coe, â† compl_mem_iff_not_mem, compl_set_of, compl_compl]

instance has_bind : has_bind ultrafilter := âŸ¨@ultrafilter.bindâŸ©
instance functor : functor ultrafilter := { map := @ultrafilter.map }
instance monad : monad ultrafilter := { map := @ultrafilter.map }

section

local attribute [instance] filter.monad filter.is_lawful_monad

instance is_lawful_monad : is_lawful_monad ultrafilter :=
{ id_map := assume Î± f, coe_injective (id_map f.1),
  pure_bind := assume Î± Î² a f, coe_injective (pure_bind a (coe âˆ˜ f)),
  bind_assoc := assume Î± Î² Î³ f mâ‚ mâ‚‚, coe_injective (filter_eq rfl),
  bind_pure_comp_eq_map := assume Î± Î² f x, coe_injective (bind_pure_comp_eq_map f x.1) }

end

/-- The ultrafilter lemma: Any proper filter is contained in an ultrafilter. -/
lemma exists_le (f : filter Î±) [h : ne_bot f] : âˆƒ u : ultrafilter Î±, â†‘u â‰¤ f :=
let âŸ¨u, hu, hufâŸ© := (eq_bot_or_exists_atom_le f).resolve_left h.ne in âŸ¨of_atom u hu, hufâŸ©

alias exists_le â† _root_.filter.exists_ultrafilter_le

/-- Construct an ultrafilter extending a given filter.
  The ultrafilter lemma is the assertion that such a filter exists;
  we use the axiom of choice to pick one. -/
noncomputable def of (f : filter Î±) [ne_bot f] : ultrafilter Î± :=
classical.some (exists_le f)

lemma of_le (f : filter Î±) [ne_bot f] : â†‘(of f) â‰¤ f := classical.some_spec (exists_le f)

lemma of_coe (f : ultrafilter Î±) : of â†‘f = f :=
coe_inj.1 $ f.unique (of_le f)

lemma exists_ultrafilter_of_finite_inter_nonempty (S : set (set Î±))
  (cond : âˆ€ T : finset (set Î±), (â†‘T : set (set Î±)) âŠ† S â†’ (â‹‚â‚€ (â†‘T : set (set Î±))).nonempty) :
  âˆƒ F : ultrafilter Î±, S âŠ† F.sets :=
begin
  haveI : ne_bot (generate S) := generate_ne_bot_iff.2
    (Î» t hts ht, ht.coe_to_finset â–¸ cond ht.to_finset (ht.coe_to_finset.symm â–¸ hts)),
  exact âŸ¨of (generate S), Î» t ht, (of_le $ generate S) $ generate_sets.basic htâŸ©
end

end ultrafilter

namespace filter
variables {f : filter Î±} {s : set Î±} {a : Î±}

open ultrafilter

lemma is_atom_pure : is_atom (pure a : filter Î±) := (pure a : ultrafilter Î±).is_atom

protected lemma ne_bot.le_pure_iff (hf : f.ne_bot) : f â‰¤ pure a â†” f = pure a :=
âŸ¨ultrafilter.unique (pure a), le_of_eqâŸ©

@[simp] lemma lt_pure_iff : f < pure a â†” f = âŠ¥ := is_atom_pure.lt_iff

lemma le_pure_iff' : f â‰¤ pure a â†” f = âŠ¥ âˆ¨ f = pure a := is_atom_pure.le_iff

@[simp] lemma Iic_pure (a : Î±) : Iic (pure a : filter Î±) = {âŠ¥, pure a} := is_atom_pure.Iic_eq

lemma mem_iff_ultrafilter : s âˆˆ f â†” âˆ€ g : ultrafilter Î±, â†‘g â‰¤ f â†’ s âˆˆ g :=
begin
  refine âŸ¨Î» hf g hg, hg hf, Î» H, by_contra $ Î» hf, _âŸ©,
  set g : filter â†¥sá¶œ := comap coe f,
  haveI : ne_bot g := comap_ne_bot_iff_compl_range.2 (by simpa [compl_set_of]),
  simpa using H ((of g).map coe) (map_le_iff_le_comap.mpr (of_le g))
end

lemma le_iff_ultrafilter {fâ‚ fâ‚‚ : filter Î±} : fâ‚ â‰¤ fâ‚‚ â†” âˆ€ g : ultrafilter Î±, â†‘g â‰¤ fâ‚ â†’ â†‘g â‰¤ fâ‚‚ :=
âŸ¨Î» h g hâ‚, hâ‚.trans h, Î» h s hs, mem_iff_ultrafilter.2 $ Î» g hg, h g hg hsâŸ©

/-- A filter equals the intersection of all the ultrafilters which contain it. -/
lemma supr_ultrafilter_le_eq (f : filter Î±) :
  (â¨† (g : ultrafilter Î±) (hg : â†‘g â‰¤ f), (g : filter Î±)) = f :=
eq_of_forall_ge_iff $ Î» f', by simp only [supr_le_iff, â† le_iff_ultrafilter]

/-- The `tendsto` relation can be checked on ultrafilters. -/
lemma tendsto_iff_ultrafilter (f : Î± â†’ Î²) (lâ‚ : filter Î±) (lâ‚‚ : filter Î²) :
  tendsto f lâ‚ lâ‚‚ â†” âˆ€ g : ultrafilter Î±, â†‘g â‰¤ lâ‚ â†’ tendsto f g lâ‚‚ :=
by simpa only [tendsto_iff_comap] using le_iff_ultrafilter

lemma exists_ultrafilter_iff {f : filter Î±} : (âˆƒ (u : ultrafilter Î±), â†‘u â‰¤ f) â†” ne_bot f :=
âŸ¨Î» âŸ¨u, ufâŸ©, ne_bot_of_le uf, Î» h, @exists_ultrafilter_le _ _ hâŸ©

lemma forall_ne_bot_le_iff {g : filter Î±} {p : filter Î± â†’ Prop} (hp : monotone p) :
  (âˆ€ f : filter Î±, ne_bot f â†’ f â‰¤ g â†’ p f) â†” âˆ€ f : ultrafilter Î±, â†‘f â‰¤ g â†’ p f :=
begin
  refine âŸ¨Î» H f hf, H f f.ne_bot hf, _âŸ©,
  introsI H f hf hfg,
  exact hp (of_le f) (H _ ((of_le f).trans hfg))
end

section hyperfilter

variables (Î±) [infinite Î±]

/-- The ultrafilter extending the cofinite filter. -/
noncomputable def hyperfilter : ultrafilter Î± := ultrafilter.of cofinite

variable {Î±}

lemma hyperfilter_le_cofinite : â†‘(hyperfilter Î±) â‰¤ @cofinite Î± :=
ultrafilter.of_le cofinite

@[simp] lemma bot_ne_hyperfilter : (âŠ¥ : filter Î±) â‰  hyperfilter Î± :=
(by apply_instance : ne_bot â†‘(hyperfilter Î±)).1.symm

theorem nmem_hyperfilter_of_finite {s : set Î±} (hf : s.finite) : s âˆ‰ hyperfilter Î± :=
Î» hy, compl_not_mem hy $ hyperfilter_le_cofinite hf.compl_mem_cofinite

alias nmem_hyperfilter_of_finite â† _root_.set.finite.nmem_hyperfilter

theorem compl_mem_hyperfilter_of_finite {s : set Î±} (hf : set.finite s) :
  sá¶œ âˆˆ hyperfilter Î± :=
compl_mem_iff_not_mem.2 hf.nmem_hyperfilter

alias compl_mem_hyperfilter_of_finite â† _root_.set.finite.compl_mem_hyperfilter

theorem mem_hyperfilter_of_finite_compl {s : set Î±} (hf : set.finite sá¶œ) :
  s âˆˆ hyperfilter Î± :=
compl_compl s â–¸ hf.compl_mem_hyperfilter

end hyperfilter

end filter

namespace ultrafilter

open filter

variables {m : Î± â†’ Î²} {s : set Î±} {g : ultrafilter Î²}

lemma comap_inf_principal_ne_bot_of_image_mem (h : m '' s âˆˆ g) :
  (filter.comap m g âŠ“ ğ“Ÿ s).ne_bot :=
filter.comap_inf_principal_ne_bot_of_image_mem g.ne_bot h

/-- Ultrafilter extending the inf of a comapped ultrafilter and a principal ultrafilter. -/
noncomputable def of_comap_inf_principal (h : m '' s âˆˆ g) : ultrafilter Î± :=
@of _ (filter.comap m g âŠ“ ğ“Ÿ s) (comap_inf_principal_ne_bot_of_image_mem h)

lemma of_comap_inf_principal_mem (h : m '' s âˆˆ g) : s âˆˆ of_comap_inf_principal h :=
begin
  let f := filter.comap m g âŠ“ ğ“Ÿ s,
  haveI : f.ne_bot := comap_inf_principal_ne_bot_of_image_mem h,
  have : s âˆˆ f := mem_inf_of_right (mem_principal_self s),
  exact le_def.mp (of_le _) s this
end

lemma of_comap_inf_principal_eq_of_map (h : m '' s âˆˆ g) :
  (of_comap_inf_principal h).map m = g :=
begin
  let f := filter.comap m g âŠ“ ğ“Ÿ s,
  haveI : f.ne_bot := comap_inf_principal_ne_bot_of_image_mem h,
  apply eq_of_le,
  calc filter.map m (of f) â‰¤ filter.map m f : map_mono (of_le _)
  ... â‰¤ (filter.map m $ filter.comap m g) âŠ“ filter.map m (ğ“Ÿ s) : map_inf_le
  ... = (filter.map m $ filter.comap m g) âŠ“ (ğ“Ÿ $ m '' s) : by rw map_principal
  ... â‰¤ g âŠ“ (ğ“Ÿ $ m '' s) : inf_le_inf_right _ map_comap_le
  ... = g : inf_of_le_left (le_principal_iff.mpr h)
end

end ultrafilter
