/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, David Loeffler
-/
import algebra.module.submodule.basic
import topology.algebra.monoid
import analysis.asymptotics.asymptotics

/-!
# Zero and Bounded at filter

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Given a filter `l` we define the notion of a function being `zero_at_filter` as well as being
`bounded_at_filter`. Alongside this we construct the `submodule`, `add_submonoid` of functions
that are `zero_at_filter`. Similarly, we construct the `submodule` and `subalgebra` of functions
that are `bounded_at_filter`.

-/

namespace filter

variables {Î± Î² : Type*}

open_locale topology

/-- If `l` is a filter on `Î±`, then a function `f : Î± â†’ Î²` is `zero_at_filter l`
  if it tends to zero along `l`. -/
def zero_at_filter [has_zero Î²] [topological_space Î²] (l : filter Î±) (f : Î± â†’ Î²) : Prop :=
filter.tendsto f l (ğ“ 0)

lemma zero_zero_at_filter [has_zero Î²] [topological_space Î²] (l : filter Î±) :
  zero_at_filter l (0 : Î± â†’ Î²) :=
tendsto_const_nhds

lemma zero_at_filter.add [topological_space Î²] [add_zero_class Î²] [has_continuous_add Î²]
  {l : filter Î±} {f g : Î± â†’ Î²} (hf : zero_at_filter l f) (hg : zero_at_filter l g) :
  zero_at_filter l (f + g) :=
by simpa using hf.add hg

lemma zero_at_filter.neg [topological_space Î²] [add_group Î²] [has_continuous_neg Î²] {l : filter Î±}
  {f : Î± â†’ Î²} (hf : zero_at_filter l f) :
  zero_at_filter l (-f) :=
by simpa using hf.neg

lemma zero_at_filter.smul {ğ•œ : Type*} [topological_space ğ•œ] [topological_space Î²] [has_zero ğ•œ]
  [has_zero Î²] [smul_with_zero ğ•œ Î²] [has_continuous_smul ğ•œ Î²]
   {l : filter Î±} {f : Î± â†’ Î²} (c : ğ•œ) (hf : zero_at_filter l f) :
  zero_at_filter l (c â€¢ f) :=
by simpa using hf.const_smul c

/-- `zero_at_filter_submodule l` is the submodule of `f : Î± â†’ Î²` which
tend to zero along `l`. -/
def zero_at_filter_submodule [topological_space Î²] [semiring Î²]
  [has_continuous_add Î²] [has_continuous_mul Î²] (l : filter Î±) : submodule Î² (Î± â†’ Î²) :=
{ carrier := zero_at_filter l,
  zero_mem' := zero_zero_at_filter l,
  add_mem' := Î» a b ha hb, ha.add hb,
  smul_mem' := Î» c f hf, hf.smul c }

/-- `zero_at_filter_add_submonoid l` is the additive submonoid of `f : Î± â†’ Î²`
which tend to zero along `l`. -/
def zero_at_filter_add_submonoid [topological_space Î²]
  [add_zero_class Î²] [has_continuous_add Î²] (l : filter Î±) : add_submonoid (Î± â†’ Î²) :=
{ carrier := zero_at_filter l,
  add_mem' := Î» a b ha hb, ha.add hb,
  zero_mem' := zero_zero_at_filter l, }

/-- If `l` is a filter on `Î±`, then a function `f: Î± â†’ Î²` is `bounded_at_filter l`
if `f =O[l] 1`. -/
def bounded_at_filter [has_norm Î²] (l : filter Î±) (f : Î± â†’ Î²) : Prop :=
asymptotics.is_O l f (1 : Î± â†’ â„)

lemma zero_at_filter.bounded_at_filter [normed_add_comm_group Î²] {l : filter Î±} {f : Î± â†’ Î²}
  (hf : zero_at_filter l f) : bounded_at_filter l f :=
begin
  rw [zero_at_filter, â† asymptotics.is_o_const_iff (one_ne_zero' â„)] at hf,
  exact hf.is_O,
end

lemma const_bounded_at_filter [normed_field Î²] (l : filter Î±) (c : Î²) :
  bounded_at_filter l (function.const Î± c : Î± â†’ Î²) :=
asymptotics.is_O_const_const c one_ne_zero l

lemma bounded_at_filter.add [normed_add_comm_group Î²] {l : filter Î±} {f g : Î± â†’ Î²}
  (hf : bounded_at_filter l f) (hg : bounded_at_filter l g) :
  bounded_at_filter l (f + g) :=
by simpa using hf.add hg

lemma bounded_at_filter.neg [normed_add_comm_group Î²] {l : filter Î±} {f : Î± â†’ Î²}
  (hf : bounded_at_filter l f) :
  bounded_at_filter l (-f) :=
hf.neg_left

lemma bounded_at_filter.smul {ğ•œ : Type*} [normed_field ğ•œ] [normed_add_comm_group Î²]
  [normed_space ğ•œ Î²] {l : filter Î±} {f : Î± â†’ Î²} (c : ğ•œ) (hf : bounded_at_filter l f) :
  bounded_at_filter l (c â€¢ f) :=
hf.const_smul_left c

lemma bounded_at_filter.mul [normed_field Î²] {l : filter Î±} {f g : Î± â†’ Î²}
  (hf : bounded_at_filter l f) (hg : bounded_at_filter l g) :
  bounded_at_filter l (f * g) :=
begin
  refine (hf.mul hg).trans _,
  convert asymptotics.is_O_refl _ l,
  ext x,
  simp,
end

/-- The submodule of functions that are bounded along a filter `l`. -/
def bounded_filter_submodule [normed_field Î²] (l : filter Î±) : submodule Î² (Î± â†’ Î²) :=
{ carrier := bounded_at_filter l,
  zero_mem' := const_bounded_at_filter l 0,
  add_mem' := Î» f g hf hg, hf.add hg,
  smul_mem' := Î» c f hf, hf.smul c }

/-- The subalgebra of functions that are bounded along a filter `l`. -/
def bounded_filter_subalgebra [normed_field Î²] (l : filter Î±) :
  subalgebra Î² (Î± â†’ Î²) :=
begin
  refine submodule.to_subalgebra (bounded_filter_submodule l) _ (Î» f g hf hg, _),
  { exact const_bounded_at_filter l (1:Î²) },
  { simpa only [pi.one_apply, mul_one, norm_mul] using hf.mul hg, },

end

end filter
