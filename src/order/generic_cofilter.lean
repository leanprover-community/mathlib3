/-
Copyright (c) 2020 David WÃ¤rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David WÃ¤rn
-/
import order.basic
import data.equiv.encodable

/-!
# Generic cofilters

This file proves the dual Rasiowaâ€“Sikorski lemma: given a preorder `P`, a term `p : P`, and
a countable family `ğ’Ÿ` of cofinal subsets of `P`, we construct a downwards closed, upwards
directed subset of `P` which contains `p` and intersects every set in `ğ’Ÿ`.

## Main definitions

- `generic.cofilter p ğ’Ÿ`: the constructed subset of `P`, a `ğ’Ÿ`-generic cofilter.
- `generic.directed_on p ğ’Ÿ`: the fact that the generic cofilter is upwards directed.
- `generic.meets p ğ’Ÿ`: the fact that the generic cofilter intersects all sets in `ğ’Ÿ`.

## Usage

This provides a framework for certain recursive constructions, similar to Zorn's lemma.
Loosely speaking, suppose we want to construct an object satisfying some countable family
of conditions. In this framework, we need to:

- Define a type `P`, whose terms should represent finitary attempts at constructing the object.
- Provide a 'starting point' `p : P` for the construction.
- Define a reflexive and transitive order `â‰¤` on `P`, where `x â‰¤ y` should mean '`y` extends `x`'.
- For each condition, define a subset of `P`, consisting of attemps which guarantee the condition,
  together with a proof that any attempt can be extended so as to guarantee the condition.
- Use `generic.cofilter` to define the desired object, as a limit of the attempts.

## References

https://en.wikipedia.org/wiki/Rasiowaâ€“Sikorski_lemma

## Tags

generic, filter, countable, cofinal, dense

-/

variables {P : Type*} [preorder P]

/-- A downwards closed set, or initial segment. -/
def downwards_closed (s : set P) : Prop := âˆ€ (x : P) (y âˆˆ s), x â‰¤ y â†’ x âˆˆ s

/-- A subset of a preorder is cofinal if it contains arbitrarily large elements. -/
def cofinal (s : set P) : Prop := âˆ€ x : P, âˆƒ y âˆˆ s, x â‰¤ y

namespace cofinal

variables {s : set P} (h : cofinal s) (x : P)

/-- An element of a cofinal set lying above a given element. -/
noncomputable def above : P :=
classical.some $ h x

lemma above_mem : above h x âˆˆ s :=
by { have := classical.some_spec (h x), tauto }

lemma le_above : x â‰¤ above h x :=
by { have := classical.some_spec (h x), tauto }

end cofinal

namespace generic

variables (p : P) {Î¹ : Type*} [encodable Î¹] (ğ’Ÿ : Î¹ â†’ { D : set P // cofinal D })

/-- Given a countable family of cofinal sets and a starting point,
  this is an increasing sequence that intersects each cofinal set. -/
noncomputable def seq : â„• â†’ P
| 0 := p
| (n+1) := match encodable.decode Î¹ n with
            | none := seq n
            | some i := cofinal.above (ğ’Ÿ i).property (seq n)
           end

lemma seq.monotone : monotone (seq p ğ’Ÿ) :=
begin
  apply monotone_of_monotone_nat,
  intros n,
  dunfold seq,
  cases encodable.decode Î¹ n,
  { refl },
  { apply cofinal.le_above },
end

lemma seq.starting_point : seq p ğ’Ÿ 0 = p := rfl

lemma seq.encode_mem (i : Î¹) : seq p ğ’Ÿ (encodable.encode i + 1) âˆˆ (ğ’Ÿ i).val :=
by { dunfold seq, rw encodable.encodek, apply cofinal.above_mem, }

/-- Given a countable family `ğ’Ÿ` of cofinal subsets of a preorder `P` and a starting point
    `p : P`, `generic.cofilter p ğ’Ÿ` is a subset of `P` which
    - contains `p`
    - is downwards closed
    - is upwards directed
    - meets every set in `ğ’Ÿ` -/
def cofilter : set P := { x : P | âˆƒ n, x â‰¤ seq p ğ’Ÿ n }

lemma starting_point_mem : p âˆˆ cofilter p ğ’Ÿ := âŸ¨0, le_refl _âŸ©

lemma downwards_closed : downwards_closed (cofilter p ğ’Ÿ) :=
Î» x y âŸ¨n, hnâŸ© hx, âŸ¨n, le_trans hx hnâŸ©

lemma directed_on : directed_on (â‰¤) (cofilter p ğ’Ÿ) :=
Î» x âŸ¨n, hnâŸ© y âŸ¨m, hmâŸ©, âŸ¨_, âŸ¨max n m, le_refl _âŸ©,
    le_trans hn $ seq.monotone p ğ’Ÿ (le_max_left _ _),
    le_trans hm $ seq.monotone p ğ’Ÿ (le_max_right _ _) âŸ©

lemma meets (i : Î¹) : (cofilter p ğ’Ÿ âˆ© (ğ’Ÿ i).val).nonempty :=
âŸ¨_, âŸ¨_, le_refl _âŸ©, seq.encode_mem p ğ’Ÿ iâŸ©

attribute [irreducible] cofilter

end generic
