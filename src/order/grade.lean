/-
Copyright (c) 2022 YaÃ«l Dillies, Violeta HernÃ¡ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Violeta HernÃ¡ndez Palacios, Grayson Burton, Vladimir Ivanov
-/
import data.finset.basic
import data.int.succ_pred

/-!
# Graded orders

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines graded orders, also known as ranked orders.

A `ğ•†`-graded order is an order `Î±` equipped with a distinguished "grade" function `Î± â†’ ğ•†` which
should be understood as giving the "height" of the elements. Usual graded orders are `â„•`-graded,
cograded orders are `â„•áµ’áµˆ`-graded, but we can also grade by `â„¤`, and polytopes are naturally
`fin n`-graded.

Visually, `grade â„• a` is the height of `a` in the Hasse diagram of `Î±`.

## Main declarations

* `grade_order`: Graded order.
* `grade_min_order`: Graded order where minimal elements have minimal grades.
* `grade_max_order`: Graded order where maximal elements have maximal grades.
* `grade_bounded_order`: Graded order where minimal elements have minimal grades and maximal
  elements have maximal grades.
* `grade`: The grade of an element. Because an order can admit several gradings, the first argument
  is the order we grade by.
* `grade_max_order`: Graded orders with maximal elements. All maximal elements have the same grade.
* `max_grade`: The maximum grade in a `grade_max_order`.
* `order_embedding.grade`: The grade of an element in a linear order as an order embedding.

## How to grade your order

Here are the translations between common references and our `grade_order`:
* [Stanley][stanley2012] defines a graded order of rank `n` as an order where all maximal chains
  have "length" `n` (so the number of elements of a chain is `n + 1`). This corresponds to
  `grade_bounded_order (fin (n + 1)) Î±`.
* [Engel][engel1997]'s ranked orders are somewhere between `grade_order â„• Î±` and
  `grade_min_order â„• Î±`, in that he requires `âˆƒ a, is_min a âˆ§ grade â„• a + 0` rather than
  `âˆ€ a, is_min a â†’ grade â„• a = 0`. He defines a graded order as an order where all minimal elements
  have grade `0` and all maximal elements have the same grade. This is roughly a less bundled
  version of `grade_bounded_order (fin n) Î±`, assuming we discard orders with infinite chains.

## Implementation notes

One possible definition of graded orders is as the bounded orders whose flags (maximal chains)
all have the same finite length (see Stanley p. 99). However, this means that all graded orders must
have minimal and maximal elements and that the grade is not data.

Instead, we define graded orders by their grade function, without talking about flags yet.

## References

* [Konrad Engel, *Sperner Theory*][engel1997]
* [Richard Stanley, *Enumerative Combinatorics*][stanley2012]
-/

set_option old_structure_cmd true

open finset nat order_dual

variables {ğ•† â„™ Î± Î² : Type*}

/-- An `ğ•†`-graded order is an order `Î±` equipped with a strictly monotone function `grade ğ•† : Î± â†’ ğ•†`
which preserves order covering (`covby`). -/
class grade_order (ğ•† Î± : Type*) [preorder ğ•†] [preorder Î±] :=
(grade : Î± â†’ ğ•†)
(grade_strict_mono : strict_mono grade)
(covby_grade â¦ƒa b : Î±â¦„ : a â‹– b â†’ grade a â‹– grade b)

/-- A `ğ•†`-graded order where minimal elements have minimal grades. -/
class grade_min_order (ğ•† Î± : Type*) [preorder ğ•†] [preorder Î±] extends grade_order ğ•† Î± :=
(is_min_grade â¦ƒa : Î±â¦„ : is_min a â†’ is_min (grade a))

/-- A `ğ•†`-graded order where maximal elements have maximal grades. -/
class grade_max_order (ğ•† Î± : Type*) [preorder ğ•†] [preorder Î±] extends grade_order ğ•† Î± :=
(is_max_grade â¦ƒa : Î±â¦„ : is_max a â†’ is_max (grade a))

/-- A `ğ•†`-graded order where minimal elements have minimal grades and maximal elements have maximal
grades. -/
class grade_bounded_order (ğ•† Î± : Type*) [preorder ğ•†] [preorder Î±]
  extends grade_min_order ğ•† Î±, grade_max_order ğ•† Î±

section preorder -- grading
variables [preorder ğ•†]

section preorder -- graded order
variables [preorder Î±]

section grade_order
variables (ğ•†) [grade_order ğ•† Î±] {a b : Î±}

/-- The grade of an element in a graded order. Morally, this is the number of elements you need to
go down by to get to `âŠ¥`. -/
def grade : Î± â†’ ğ•† := grade_order.grade

protected lemma covby.grade (h : a â‹– b) : grade ğ•† a â‹– grade ğ•† b := grade_order.covby_grade h

variables {ğ•†}

lemma grade_strict_mono : strict_mono (grade ğ•† : Î± â†’ ğ•†) := grade_order.grade_strict_mono

lemma covby_iff_lt_covby_grade : a â‹– b â†” a < b âˆ§ grade ğ•† a â‹– grade ğ•† b :=
âŸ¨Î» h, âŸ¨h.1, h.grade _âŸ©, and.imp_right $ Î» h c ha hb,
  h.2 (grade_strict_mono ha) $ grade_strict_mono hbâŸ©

end grade_order

section grade_min_order
variables (ğ•†) [grade_min_order ğ•† Î±] {a : Î±}

protected lemma is_min.grade (h : is_min a) : is_min (grade ğ•† a) := grade_min_order.is_min_grade h

variables {ğ•†}

@[simp] lemma is_min_grade_iff : is_min (grade ğ•† a) â†” is_min a :=
âŸ¨grade_strict_mono.is_min_of_apply, is_min.grade _âŸ©

end grade_min_order

section grade_max_order
variables (ğ•†) [grade_max_order ğ•† Î±] {a : Î±}

protected lemma is_max.grade (h : is_max a) : is_max (grade ğ•† a) := grade_max_order.is_max_grade h

variables {ğ•†}

@[simp] lemma is_max_grade_iff : is_max (grade ğ•† a) â†” is_max a :=
âŸ¨grade_strict_mono.is_max_of_apply, is_max.grade _âŸ©

end grade_max_order
end preorder -- graded order

lemma grade_mono [partial_order Î±] [grade_order ğ•† Î±] : monotone (grade ğ•† : Î± â†’ ğ•†) :=
grade_strict_mono.monotone

section linear_order -- graded order
variables [linear_order Î±] [grade_order ğ•† Î±] {a b : Î±}

lemma grade_injective : function.injective (grade ğ•† : Î± â†’ ğ•†) := grade_strict_mono.injective
@[simp] lemma grade_le_grade_iff : grade ğ•† a â‰¤ grade ğ•† b â†” a â‰¤ b := grade_strict_mono.le_iff_le
@[simp] lemma grade_lt_grade_iff : grade ğ•† a < grade ğ•† b â†” a < b := grade_strict_mono.lt_iff_lt
@[simp] lemma grade_eq_grade_iff : grade ğ•† a = grade ğ•† b â†” a = b := grade_injective.eq_iff
lemma grade_ne_grade_iff : grade ğ•† a â‰  grade ğ•† b â†” a â‰  b := grade_injective.ne_iff

lemma grade_covby_grade_iff : grade ğ•† a â‹– grade ğ•† b â†” a â‹– b :=
(covby_iff_lt_covby_grade.trans $ and_iff_right_of_imp $ Î» h, grade_lt_grade_iff.1 h.1).symm

end linear_order -- graded order
end preorder -- grading

section partial_order
variables [partial_order ğ•†] [preorder Î±]

@[simp] lemma grade_bot [order_bot ğ•†] [order_bot Î±] [grade_min_order ğ•† Î±] : grade ğ•† (âŠ¥ : Î±) = âŠ¥ :=
(is_min_bot.grade _).eq_bot

@[simp] lemma grade_top [order_top ğ•†] [order_top Î±] [grade_max_order ğ•† Î±] : grade ğ•† (âŠ¤ : Î±) = âŠ¤ :=
(is_max_top.grade _).eq_top

end partial_order

/-! ### Instances -/

variables [preorder ğ•†] [preorder â„™] [preorder Î±] [preorder Î²]

instance preorder.to_grade_bounded_order : grade_bounded_order Î± Î± :=
{ grade := id,
  is_min_grade := Î» _, id,
  is_max_grade := Î» _, id,
  grade_strict_mono := strict_mono_id,
  covby_grade := Î» a b, id }

@[simp] lemma grade_self (a : Î±) : grade Î± a = a := rfl

/-! #### Dual -/

instance [grade_order ğ•† Î±] : grade_order ğ•†áµ’áµˆ Î±áµ’áµˆ :=
{ grade := to_dual âˆ˜ grade ğ•† âˆ˜ of_dual,
  grade_strict_mono := grade_strict_mono.dual,
  covby_grade := Î» a b h, (h.of_dual.grade _).to_dual }

instance [grade_max_order ğ•† Î±] : grade_min_order ğ•†áµ’áµˆ Î±áµ’áµˆ :=
{ is_min_grade := Î» _, is_max.grade _,
  ..order_dual.grade_order }

instance [grade_min_order ğ•† Î±] : grade_max_order ğ•†áµ’áµˆ Î±áµ’áµˆ :=
{ is_max_grade := Î» _, is_min.grade _,
  ..order_dual.grade_order }

instance [grade_bounded_order ğ•† Î±] : grade_bounded_order ğ•†áµ’áµˆ Î±áµ’áµˆ :=
{ ..order_dual.grade_min_order, ..order_dual.grade_max_order }

@[simp] lemma grade_to_dual [grade_order ğ•† Î±] (a : Î±) :
  grade ğ•†áµ’áµˆ (to_dual a) = to_dual (grade ğ•† a) := rfl
@[simp] lemma grade_of_dual [grade_order ğ•† Î±] (a : Î±áµ’áµˆ) :
  grade ğ•† (of_dual a) = of_dual (grade ğ•†áµ’áµˆ a) := rfl

/-! #### Lifting a graded order -/

/-- Lifts a graded order along a strictly monotone function. -/
@[reducible] -- See note [reducible non-instances]
def grade_order.lift_left [grade_order ğ•† Î±] (f : ğ•† â†’ â„™) (hf : strict_mono f)
  (hcovby : âˆ€ a b, a â‹– b â†’ f a â‹– f b) : grade_order â„™ Î± :=
{ grade := f âˆ˜ grade ğ•†,
  grade_strict_mono := hf.comp grade_strict_mono,
  covby_grade := Î» a b h, hcovby _ _ $ h.grade _ }

/-- Lifts a graded order along a strictly monotone function. -/
@[reducible] -- See note [reducible non-instances]
def grade_min_order.lift_left [grade_min_order ğ•† Î±] (f : ğ•† â†’ â„™) (hf : strict_mono f)
  (hcovby : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmin : âˆ€ a, is_min a â†’ is_min (f a)) :
  grade_min_order â„™ Î± :=
{ is_min_grade := Î» a ha, hmin _ $ ha.grade _,
  ..grade_order.lift_left f hf hcovby }

/-- Lifts a graded order along a strictly monotone function. -/
@[reducible] -- See note [reducible non-instances]
def grade_max_order.lift_left [grade_max_order ğ•† Î±] (f : ğ•† â†’ â„™) (hf : strict_mono f)
  (hcovby : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmax : âˆ€ a, is_max a â†’ is_max (f a)) :
  grade_max_order â„™ Î± :=
{ is_max_grade := Î» a ha, hmax _ $ ha.grade _,
  ..grade_order.lift_left f hf hcovby }

/-- Lifts a graded order along a strictly monotone function. -/
@[reducible] -- See note [reducible non-instances]
def grade_bounded_order.lift_left [grade_bounded_order ğ•† Î±] (f : ğ•† â†’ â„™) (hf : strict_mono f)
  (hcovby : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmin : âˆ€ a, is_min a â†’ is_min (f a))
  (hmax : âˆ€ a, is_max a â†’ is_max (f a)) :
  grade_bounded_order â„™ Î± :=
{ ..grade_min_order.lift_left f hf hcovby hmin, ..grade_max_order.lift_left f hf hcovby hmax }

/-- Lifts a graded order along a strictly monotone function. -/
@[reducible] -- See note [reducible non-instances]
def grade_order.lift_right [grade_order ğ•† Î²] (f : Î± â†’ Î²) (hf : strict_mono f)
  (hcovby : âˆ€ a b, a â‹– b â†’ f a â‹– f b) : grade_order ğ•† Î± :=
{ grade := grade ğ•† âˆ˜ f,
  grade_strict_mono := grade_strict_mono.comp hf,
  covby_grade := Î» a b h, (hcovby _ _ h).grade _ }

/-- Lifts a graded order along a strictly monotone function. -/
@[reducible] -- See note [reducible non-instances]
def grade_min_order.lift_right [grade_min_order ğ•† Î²] (f : Î± â†’ Î²) (hf : strict_mono f)
  (hcovby : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmin : âˆ€ a, is_min a â†’ is_min (f a)) :
  grade_min_order ğ•† Î± :=
{ is_min_grade := Î» a ha, (hmin _ ha).grade _,
  ..grade_order.lift_right f hf hcovby }

/-- Lifts a graded order along a strictly monotone function. -/
@[reducible] -- See note [reducible non-instances]
def grade_max_order.lift_right [grade_max_order ğ•† Î²] (f : Î± â†’ Î²) (hf : strict_mono f)
  (hcovby : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmax : âˆ€ a, is_max a â†’ is_max (f a)) :
  grade_max_order ğ•† Î± :=
{ is_max_grade := Î» a ha, (hmax _ ha).grade _,
  ..grade_order.lift_right f hf hcovby }

/-- Lifts a graded order along a strictly monotone function. -/
@[reducible] -- See note [reducible non-instances]
def grade_bounded_order.lift_right [grade_bounded_order ğ•† Î²] (f : Î± â†’ Î²) (hf : strict_mono f)
  (hcovby : âˆ€ a b, a â‹– b â†’ f a â‹– f b) (hmin : âˆ€ a, is_min a â†’ is_min (f a))
  (hmax : âˆ€ a, is_max a â†’ is_max (f a)) : grade_bounded_order ğ•† Î± :=
{ ..grade_min_order.lift_right f hf hcovby hmin, ..grade_max_order.lift_right f hf hcovby hmax }

/-! #### `fin n`-graded to `â„•`-graded to `â„¤`-graded -/

/-- A `fin n`-graded order is also `â„•`-graded. We do not mark this an instance because `n` is not
inferrable. -/
@[reducible] -- See note [reducible non-instances]
def grade_order.fin_to_nat (n : â„•) [grade_order (fin n) Î±] : grade_order â„• Î± :=
grade_order.lift_left (_ : fin n â†’ â„•) fin.coe_strict_mono $ Î» _ _, covby.coe_fin

/-- A `fin n`-graded order is also `â„•`-graded. We do not mark this an instance because `n` is not
inferrable. -/
@[reducible] -- See note [reducible non-instances]
def grade_min_order.fin_to_nat (n : â„•) [grade_min_order (fin n) Î±] : grade_min_order â„• Î± :=
grade_min_order.lift_left (_ : fin n â†’ â„•) fin.coe_strict_mono (Î» _ _, covby.coe_fin) $ Î» a h, begin
    unfreezingI { cases n },
    { exact (@fin.elim0 (Î» _, false) $ grade (fin 0) a).elim },
    rw [h.eq_bot, fin.bot_eq_zero],
    exact is_min_bot,
  end

instance grade_order.nat_to_int [grade_order â„• Î±] : grade_order â„¤ Î± :=
grade_order.lift_left _ int.coe_nat_strict_mono $ Î» _ _, covby.cast_int
