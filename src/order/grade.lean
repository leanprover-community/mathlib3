/-
Copyright (c) 2022 YaÃ«l Dillies, Violeta HernÃ¡ndez Palacios. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Violeta HernÃ¡ndez Palacios, Grayson Burton, Vladimir Ivanov
-/
import data.nat.interval
import data.int.succ_pred
import order.atoms

/-!
# Graded orders

This file defines graded orders, also known as ranked orders.

A `ğ•†`-graded order is an order `Î±` equipped with a distinguished "grade" function `Î± â†’ ğ•†` which
should be understood as giving the "height" of the elements. Usual graded orders are `â„•`-graded,
cograded orders are `order_dual â„•`-graded, but we can also grade by `â„¤`, and polytopes are naturally
`fin n`-graded.

Visually, `grade â„• a` is the height of `a` in the Hasse diagram of `Î±`.

## Main declarations

* `grade_order`: Graded order.
* `grade_min_order`: Graded order where minimal elements have minimal grades.
* `grade_max_order`: Graded order where maximal elements have maximal grades.
* `grade_bounded_order`: Graded order where minimal elements have minimal grades and maximal
  elements have maximal grades.
* `grade`: The grade of an element. Because an order can admit several gradings, the first argument
  is the order we grade by.
* `grade_max_order`: Graded orders with maximal elements. All maximal elements have the same grade.
* `max_grade`: The maximum grade in a `grade_max_order`.
* `order_embedding.grade`: The grade of an element in a linear order as an order embedding.

## Implementation notes

One possible definition of graded orders is as the bounded orders whose flags (maximal chains)
all have the same finite length (see Stanley p. 99). However, this means that all graded orders must
have minimal and maximal elements and that the grade is not data.

Instead, we define graded orders by their grade function, without talking about flags yet.

## References

* [Konrad Engel, *Sperner Theory*][engel1997]
* [Richard Stanley, *Enumerative Combinatorics*][stanley2012]
-/

set_option old_structure_cmd true

open finset nat order_dual

variables {ğ•† Î± Î² : Type*}

section
variables {a b : Prop}

lemma iff.not_left (h : a â†” Â¬ b) : Â¬ a â†” b := h.not.trans not_not
lemma iff.not_right (h : Â¬ a â†” b) : a â†” Â¬ b := not_not.symm.trans h.not

end

section
variables [preorder ğ•†] [preorder Î±] {f : Î± â†’ ğ•†} {a : Î±}

lemma strict_mono.is_max_of_apply (hf : strict_mono f) (ha : is_max (f a)) : is_max a :=
by { by_contra, obtain âŸ¨b, hbâŸ© := not_is_max_iff.1 h, exact (hf hb).not_is_max ha }

lemma strict_mono.is_min_of_apply (hf : strict_mono f) (ha : is_min (f a)) : is_min a :=
by { by_contra, obtain âŸ¨b, hbâŸ© := not_is_min_iff.1 h, exact (hf hb).not_is_min ha }

lemma strict_anti.is_max_of_apply (hf : strict_anti f) (ha : is_min (f a)) : is_max a :=
by { by_contra, obtain âŸ¨b, hbâŸ© := not_is_max_iff.1 h, exact (hf hb).not_is_min ha }

lemma strict_anti.is_min_of_apply (hf : strict_anti f) (ha : is_max (f a)) : is_min a :=
by { by_contra, obtain âŸ¨b, hbâŸ© := not_is_min_iff.1 h, exact (hf hb).not_is_max ha }

end

section order_top
variables [partial_order Î±] [preorder ğ•†] [order_top Î±] {f : Î± â†’ ğ•†} {a : Î±}

@[simp] lemma not_lt_top_iff : Â¬ a < âŠ¤ â†” a = âŠ¤ := lt_top_iff_ne_top.not_left

lemma strict_mono.apply_eq_top_iff (hf : strict_mono f) : f a = f âŠ¤ â†” a = âŠ¤ :=
âŸ¨Î» h, not_lt_top_iff.1 $ Î» ha, (hf ha).ne h, congr_arg _âŸ©

lemma strict_anti.apply_eq_top_iff (hf : strict_anti f) : f a = f âŠ¤ â†” a = âŠ¤ :=
âŸ¨Î» h, not_lt_top_iff.1 $ Î» ha, (hf ha).ne' h, congr_arg _âŸ©

end order_top

section order_bot
variables [partial_order Î±] [preorder ğ•†] [order_bot Î±] {f : Î± â†’ ğ•†} {a : Î±}

@[simp] lemma not_bot_lt_iff : Â¬ âŠ¥ < a â†” a = âŠ¥ := bot_lt_iff_ne_bot.not_left

lemma strict_mono.apply_eq_bot_iff (hf : strict_mono f) : f a = f âŠ¥ â†” a = âŠ¥ :=
âŸ¨Î» h, not_bot_lt_iff.1 $ Î» ha, (hf ha).ne' h, congr_arg _âŸ©

lemma strict_anti.apply_eq_bot_iff (hf : strict_anti f) : f a = f âŠ¥ â†” a = âŠ¥ :=
âŸ¨Î» h, not_bot_lt_iff.1 $ Î» ha, (hf ha).ne h, congr_arg _âŸ©

end order_bot

lemma fin.coe_strict_mono {n : â„•} : strict_mono (coe : fin n â†’ â„•) := Î» _ _, id
lemma nat.cast_strict_mono [ordered_semiring Î±] [nontrivial Î±] : strict_mono (coe : â„• â†’ Î±) :=
Î» _ _, nat.cast_lt.2
lemma int.coe_nat_strict_mono : strict_mono (coe : â„• â†’ â„¤) := Î» _ _, int.coe_nat_lt.2

/-- A strictly monotone function from a linear order as an order embedding. -/
protected def strict_mono.order_embedding [linear_order Î±] [preorder Î²] (f : Î± â†’ Î²)
  (hf : strict_mono f) : Î± â†ªo Î² := âŸ¨âŸ¨f, hf.injectiveâŸ©, Î» _ _, hf.le_iff_leâŸ©

/-- An `ğ•†`-graded order is an order `Î±` equipped with a strictly monotone function `grade ğ•† : Î± â†’ ğ•†`
which preserves order covering (`covby`). -/
class grade_order (ğ•† Î± : Type*) [preorder ğ•†] [preorder Î±] :=
(grade : Î± â†’ ğ•†)
(grade_strict_mono : strict_mono grade)
(covby_grade â¦ƒa b : Î±â¦„ : a â‹– b â†’ grade a â‹– grade b)

/-- A `ğ•†`-graded order where minimal elements have minimal grades. -/
class grade_min_order (ğ•† Î± : Type*) [preorder ğ•†] [preorder Î±] extends grade_order ğ•† Î± :=
(is_min_grade â¦ƒa : Î±â¦„ : is_min a â†’ is_min (grade a))

/-- A `ğ•†`-graded order where maximal elements have maximal grades. -/
class grade_max_order (ğ•† Î± : Type*) [preorder ğ•†] [preorder Î±] extends grade_order ğ•† Î± :=
(is_max_grade â¦ƒa : Î±â¦„ : is_max a â†’ is_max (grade a))

/-- A `ğ•†`-graded order where minimal elements have minimal grades and maximal elements have maximal
grades. -/
class grade_bounded_order (ğ•† Î± : Type*) [preorder ğ•†] [preorder Î±]
  extends grade_min_order ğ•† Î±, grade_max_order ğ•† Î±

section preorder
variables [preorder ğ•†]

section preorder
variables [preorder Î±]

section grade_order
variables (ğ•†) [grade_order ğ•† Î±] {a b : Î±}

/-- The grade of an element in a graded order. Morally, this is the number of elements you need to
go down by to get to `âŠ¥`. -/
def grade (a : Î±) : ğ•† := grade_order.grade a

protected lemma covby.grade (h : a â‹– b) : grade ğ•† a â‹– grade ğ•† b := grade_order.covby_grade h

variables {ğ•†}

lemma grade_strict_mono : strict_mono (grade ğ•† : Î± â†’ ğ•†) := grade_order.grade_strict_mono

lemma covby_iff_lt_covby_grade : a â‹– b â†” a < b âˆ§ grade ğ•† a â‹– grade ğ•† b :=
âŸ¨Î» h, âŸ¨h.1, h.grade _âŸ©, and.imp_right $ Î» h c ha hb,
  h.2 (grade_strict_mono ha) $ grade_strict_mono hbâŸ©

end grade_order

section grade_min_order
variables (ğ•†) [grade_min_order ğ•† Î±] {a : Î±}

protected lemma is_min.grade (h : is_min a) : is_min (grade ğ•† a) := grade_min_order.is_min_grade h

variables {ğ•†}

@[simp] lemma is_min_grade_iff : is_min (grade ğ•† a) â†” is_min a :=
âŸ¨grade_strict_mono.is_min_of_apply, is_min.grade _âŸ©

end grade_min_order

section grade_max_order
variables (ğ•†) [grade_max_order ğ•† Î±] {a : Î±}

protected lemma is_max.grade (h : is_max a) : is_max (grade ğ•† a) := grade_max_order.is_max_grade h

variables {ğ•†}

@[simp] lemma is_max_grade_iff : is_max (grade ğ•† a) â†” is_max a :=
âŸ¨grade_strict_mono.is_max_of_apply, is_max.grade _âŸ©

end grade_max_order
end preorder

lemma grade_mono [partial_order Î±] [grade_order ğ•† Î±] : monotone (grade ğ•† : Î± â†’ ğ•†) :=
grade_strict_mono.monotone

section linear_order
variables [linear_order Î±] [grade_order ğ•† Î±] {a b : Î±}

lemma grade_injective : function.injective (grade ğ•† : Î± â†’ ğ•†) := grade_strict_mono.injective
@[simp] lemma grade_le_grade_iff : grade ğ•† a â‰¤ grade ğ•† b â†” a â‰¤ b := grade_strict_mono.le_iff_le
@[simp] lemma grade_lt_grade_iff : grade ğ•† a < grade ğ•† b â†” a < b := grade_strict_mono.lt_iff_lt
@[simp] lemma grade_eq_grade_iff : grade ğ•† a = grade ğ•† b â†” a = b := grade_injective.eq_iff
lemma grade_ne_grade_iff : grade ğ•† a â‰  grade ğ•† b â†” a â‰  b := grade_injective.ne_iff

lemma grade_covby_grade_iff : grade ğ•† a â‹– grade ğ•† b â†” a â‹– b :=
(covby_iff_lt_covby_grade.trans $ and_iff_right_of_imp $ Î» h, grade_lt_grade_iff.1 h.1).symm

end linear_order
end preorder

section partial_order
variables [partial_order ğ•†] [preorder Î±]

@[simp] lemma grade_bot [order_bot ğ•†] [order_bot Î±] [grade_min_order ğ•† Î±] : grade ğ•† (âŠ¥ : Î±) = âŠ¥ :=
(is_min_bot.grade _).eq_bot

@[simp] lemma grade_top [order_top ğ•†] [order_top Î±] [grade_max_order ğ•† Î±] : grade ğ•† (âŠ¤ : Î±) = âŠ¤ :=
(is_max_top.grade _).eq_top

end partial_order

/-! ### Instances -/

variables [preorder ğ•†] [preorder Î±] [preorder Î²]

instance preorder.to_grade_bounded_order : grade_bounded_order Î± Î± :=
{ grade := id,
  is_min_grade := Î» _, id,
  is_max_grade := Î» _, id,
  grade_strict_mono := strict_mono_id,
  covby_grade := Î» a b, id }

@[simp] lemma grade_self (a : Î±) : grade Î± a = a := rfl

/-! #### Dual -/

instance [grade_order ğ•† Î±] : grade_order (order_dual ğ•†) (order_dual Î±) :=
{ grade := to_dual âˆ˜ grade ğ•† âˆ˜ of_dual,
  grade_strict_mono := grade_strict_mono.dual,
  covby_grade := Î» a b h, (h.of_dual.grade _).to_dual }

instance [grade_max_order ğ•† Î±] : grade_min_order (order_dual ğ•†) (order_dual Î±) :=
{ is_min_grade := Î» _, is_max.grade _,
  ..order_dual.grade_order }

instance [grade_min_order ğ•† Î±] : grade_max_order (order_dual ğ•†) (order_dual Î±) :=
{ is_max_grade := Î» _, is_min.grade _,
  ..order_dual.grade_order }

instance [grade_bounded_order ğ•† Î±] : grade_bounded_order (order_dual ğ•†) (order_dual Î±) :=
{ ..order_dual.grade_min_order, ..order_dual.grade_max_order }

@[simp] lemma grade_to_dual [grade_order ğ•† Î±] (a : Î±) :
  grade (order_dual ğ•†) (to_dual a) = to_dual (grade ğ•† a) := rfl
@[simp] lemma grade_of_dual [grade_order ğ•† Î±] (a : order_dual Î±) :
  grade ğ•† (of_dual a) = of_dual (grade (order_dual ğ•†) a) := rfl

/-! #### `fin n`-graded to `â„•`-graded to `â„¤`-graded -/

/-- A `fin n`-graded order is also `â„•`-graded. We do not mark this an instance because `n` is not
inferrable. -/
@[reducible] -- See note [reducible non-instances]
def grade_order.fin_to_nat {n : â„•} [grade_order (fin n) Î±] : grade_order â„• Î± :=
{ grade := coe âˆ˜ grade (fin n),
  grade_strict_mono := fin.coe_strict_mono.comp grade_strict_mono,
  covby_grade := Î» a b h, (h.grade $ fin n).coe_fin }

/-- A `fin n`-graded order is also `â„•`-graded. We do not mark this an instance because `n` is not
inferrable. -/
@[reducible] -- See note [reducible non-instances]
def grade_min_order.fin_to_nat {n : â„•} [grade_min_order (fin n) Î±] : grade_min_order â„• Î± :=
{ grade := coe âˆ˜ grade (fin n),
  is_min_grade := Î» a h, begin
    unfreezingI { cases n },
    { exact (@fin.elim0 (Î» _, false) $ grade (fin 0) a).elim },
    dsimp,
    rw [(h.grade _).eq_bot, fin.bot_eq_zero],
    exact is_min_bot,
  end,
  ..grade_order.fin_to_nat }

instance grade_order.nat_to_int [grade_order â„• Î±] : grade_order â„¤ Î± :=
{ grade := coe âˆ˜ grade â„•,
  grade_strict_mono := int.coe_nat_strict_mono.comp grade_strict_mono,
  covby_grade := Î» a b h, (h.grade _).cast_int }

/-! #### Lifting a graded order -/

/-- Lifts a graded order along an order embedding. -/
def grade_order.lift [grade_order ğ•† Î²] {f : Î± â†ªo Î²} (hf : (set.range f).ord_connected) :
  grade_order ğ•† Î± :=
{ grade := Î» a, grade ğ•† (f a),
  grade_strict_mono := grade_strict_mono.comp f.strict_mono,
  covby_grade := Î» a b hab, (hf.image_covby_image_iff.2 hab).grade _ }
