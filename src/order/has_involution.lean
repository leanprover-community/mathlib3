/-
Copyright (c) 2022 Peter Nelson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Peter Nelson
-/
import order.antichain

/-!
# Orders with involution

This file concerns orders that admit an order-reversing involution. In the case of a lattice,
these are sometimes referred to as 'i-lattices' or 'lattices with involution'. Such an involution
is more general than a `boolean_algebra` complement, but retains many of its properties. Other than
a boolean algebra, an example is the subspace lattice of the vector space `ùïÇ‚Åø` for `ùïÇ` of nonzero
characteristic, where for each subspace `W` we have `invo W = {x ‚àà V | ‚àÄ w ‚àà W, w·µÄx = 0}`; this is
not a complement in the stronger sense because `invo W` can intersect `W`.

## Main declarations

* `has_involution`: typeclass applying to types with a `preorder` that admit an antitone involution.

* `‚Å±` : postfix notation for the function `invo : Œ± ‚Üí Œ±` given a type `Œ±` with `[has_involution Œ±]`

## TODO

Provide instances other than the one from `boolean_algebra`.
-/

universe u


class has_involution (Œ± : Type u) [preorder Œ±]  :=
(invo : Œ± ‚Üí Œ±)
(invo_antitone' : ‚àÄ (x y : Œ±), x ‚â§ y ‚Üí invo y ‚â§ invo x)
(invo_involutive' : function.involutive invo)

open has_involution

variables {Œ± : Type u}

postfix `‚Å±`:(max+1) := invo

section preorder

variables [preorder Œ±] [has_involution Œ±] {x y : Œ±}

@[simp] lemma invo_invo (x : Œ±) : x‚Å±‚Å± = x :=  invo_involutive' x

lemma invo_eq_iff_invo_eq : x‚Å± = y ‚Üî y‚Å± = x :=
by {rw [eq_comm], exact invo_involutive'.eq_iff.symm}

lemma eq_invo_iff_eq_invo : x = y‚Å± ‚Üî y = x‚Å± :=
by rw [‚Üê invo_invo x, invo_eq_iff_invo_eq, invo_invo, invo_invo]

lemma invo_le_invo (hxy : x ‚â§ y) : y‚Å± ‚â§ x‚Å± := invo_antitone' _ _ hxy

lemma le_of_invo_le (hx : x‚Å± ‚â§ y‚Å±) : y ‚â§ x :=
by {rw [‚Üêinvo_invo x, ‚Üêinvo_invo y], exact invo_le_invo hx,}

lemma invo_le_invo_iff_le : x‚Å± ‚â§ y‚Å± ‚Üî y ‚â§ x := ‚ü®le_of_invo_le, invo_le_invo‚ü©

lemma le_invo_iff_le_invo : x ‚â§ y‚Å± ‚Üî y ‚â§ x‚Å± := by rw [‚Üêinvo_le_invo_iff_le, invo_invo]

lemma invo_le_iff_invo_le : x‚Å± ‚â§ y ‚Üî y‚Å± ‚â§ x := by rw [‚Üêinvo_le_invo_iff_le, invo_invo]

lemma invo_inj (h : x‚Å± = y‚Å±) : x = y := invo_involutive'.injective h

lemma invo_lt_invo_iff_lt : x‚Å± < y‚Å± ‚Üî y < x := by simp [lt_iff_le_not_le, invo_le_invo_iff_le]

lemma lt_invo_iff_lt_invo : x < y‚Å± ‚Üî y < x‚Å± := by rw [‚Üêinvo_lt_invo_iff_lt, invo_invo]

lemma invo_lt_iff_invo_lt : x‚Å± < y ‚Üî y‚Å± < x := by rw [‚Üêinvo_lt_invo_iff_lt, invo_invo]

lemma le_invo_of_le_invo (h : y ‚â§ x‚Å±) : x ‚â§ y‚Å± := le_invo_iff_le_invo.mp h

lemma invo_le_of_invo_le (h : y‚Å± ‚â§ x) : x‚Å± ‚â§ y := invo_le_iff_invo_le.mp h

lemma invo_involutive : function.involutive (has_involution.invo : Œ± ‚Üí Œ±) := invo_invo

lemma invo_bijective : function.bijective (invo : Œ± ‚Üí Œ±) := invo_involutive.bijective

lemma invo_surjective : function.surjective (invo : Œ± ‚Üí Œ±) := invo_involutive.surjective

lemma invo_injective : function.injective (invo : Œ± ‚Üí Œ±) := invo_involutive.injective

lemma invo_antitone : antitone (invo: Œ± ‚Üí Œ±) := Œª a b, invo_le_invo

@[simp] lemma invo_inj_iff : x‚Å± = y‚Å± ‚Üî x = y := invo_injective.eq_iff

lemma invo_comp_invo : invo ‚àò invo = @id Œ± := funext invo_invo

end preorder

section lattice

variables [lattice Œ±] [has_involution Œ±]

@[simp] lemma invo_inf (x y : Œ±) : (x ‚äì y)‚Å± = x‚Å± ‚äî y‚Å± :=
le_antisymm (invo_le_iff_invo_le.mpr (le_inf (invo_le_iff_invo_le.mp le_sup_left)
    ((invo_le_iff_invo_le.mp le_sup_right))))
      (sup_le (invo_le_invo inf_le_left) (invo_le_invo inf_le_right))

@[simp] lemma invo_sup (x y : Œ±) : (x ‚äî y)‚Å± = x‚Å± ‚äì y‚Å± :=
by rw [invo_eq_iff_invo_eq, invo_inf, invo_invo, invo_invo]

end lattice

section boolean_algebra

@[priority 100]
instance boolean_algebra.to_has_involution [boolean_algebra Œ±] : has_involution Œ± :=
{ invo := compl,
  invo_antitone' := Œª _ _, compl_le_compl,
  invo_involutive' := compl_involutive }

end boolean_algebra

section hom

variables (Œ±) [preorder Œ±] [has_involution Œ±]

instance order_dual.has_involution : has_involution Œ±·µí·µà :=
{ invo := Œª x, order_dual.to_dual (order_dual.of_dual x)‚Å±,
  invo_antitone' := Œª a b h, @invo_antitone' Œ± _ _ b a h,
  invo_involutive' := invo_involutive' }

/-- Taking the involution as an order isomorphism to the order dual. -/
@[simps]
def order_iso.invo : Œ± ‚âÉo Œ±·µí·µà :=
{ to_fun := order_dual.to_dual ‚àò invo,
  inv_fun := invo ‚àò order_dual.of_dual,
  left_inv := invo_invo,
  right_inv := invo_invo,
  map_rel_iff' := Œª _ _, invo_le_invo_iff_le }

lemma invo_strict_anti : strict_anti (invo : Œ± ‚Üí Œ±) := (order_iso.invo Œ±).strict_mono

end hom

section antichain

variables [preorder Œ±] [has_involution Œ±] {s : set Œ±}

lemma image_invo (hs : is_antichain (‚â§) s) :
  is_antichain (‚â§) (invo '' s) :=
(hs.image_embedding (order_iso.invo Œ±).to_order_embedding).flip

lemma preimage_invo (hs : is_antichain (‚â§) s) :
  is_antichain (‚â§) (invo ‚Åª¬π' s) :=
Œª a ha a' ha' hne hle, hs ha' ha (Œª h, hne (invo_inj_iff.mp h.symm)) (invo_le_invo hle)

end antichain
