/-
Copyright (c) 2020 David WÃ¤rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David WÃ¤rn
-/
import order.basic
import data.equiv.encodable.basic
import order.atoms

/-!
# Order ideals, cofinal sets, and the Rasiowaâ€“Sikorski lemma

## Main definitions

Throughout this file, `P` is at least a preorder, but some sections require more
structure, such as a bottom element, a top element, or a join-semilattice structure.

- `ideal P`: the type of upward directed, downward closed subsets of `P`.
             Dual to the notion of a filter on a preorder.
- `is_prime`: the type of prime ideals.
- `is_maximal` : the type of maximal ideals.
- `cofinal P`: the type of subsets of `P` containing arbitrarily large elements.
               Dual to the notion of 'dense set' used in forcing.
- `ideal_of_cofinals p ğ’Ÿ`, where `p : P`, and `ğ’Ÿ` is a countable family of cofinal
  subsets of P: an ideal in `P` which contains `p` and intersects every set in `ğ’Ÿ`.

## References

- <https://en.wikipedia.org/wiki/Ideal_(order_theory)>
- <https://en.wikipedia.org/wiki/Cofinal_(mathematics)>
- <https://en.wikipedia.org/wiki/Rasiowa%E2%80%93Sikorski_lemma>

Note that for the Rasiowaâ€“Sikorski lemma, Wikipedia uses the opposite ordering on `P`,
in line with most presentations of forcing.

## Tags

ideal, cofinal, dense, countable, generic

-/

namespace order

variables {P : Type*}

/-- An ideal on a preorder `P` is a subset of `P` that is
  - nonempty
  - upward directed
  - downward closed. -/
structure ideal (P) [preorder P] :=
(carrier   : set P)
(nonempty  : carrier.nonempty)
(directed  : directed_on (â‰¤) carrier)
(mem_of_le : âˆ€ {x y : P}, x â‰¤ y â†’ y âˆˆ carrier â†’ x âˆˆ carrier)

namespace ideal

section preorder
variables [preorder P] {x : P} {I J : ideal P}

/-- The smallest ideal containing a given element. -/
def principal (p : P) : ideal P :=
{ carrier   := { x | x â‰¤ p },
  nonempty  := âŸ¨p, le_refl _âŸ©,
  directed  := Î» x hx y hy, âŸ¨p, le_refl _, hx, hyâŸ©,
  mem_of_le := Î» x y hxy hy, le_trans hxy hy, }

instance [inhabited P] : inhabited (ideal P) :=
âŸ¨ideal.principal $ default PâŸ©

/-- An ideal of `P` can be viewed as a subset of `P`. -/
instance : has_coe (ideal P) (set P) := âŸ¨carrierâŸ©

/-- For the notation `x âˆˆ I`. -/
instance : has_mem P (ideal P) := âŸ¨Î» x I, x âˆˆ (I : set P)âŸ©

/-- Two ideals are equal when their underlying sets are equal. -/
@[ext] lemma ext : âˆ€ (I J : ideal P), (I : set P) = J â†’ I = J
| âŸ¨_, _, _, _âŸ© âŸ¨_, _, _, _âŸ© rfl := rfl

@[simp, norm_cast] lemma ext_set_eq {I J : ideal P} : (I : set P) = J â†” I = J :=
âŸ¨by ext, congr_arg _âŸ©

lemma ext'_iff {I J : ideal P} : I = J â†” (I : set P) = J := ext_set_eq.symm

/-- The partial ordering by subset inclusion, inherited from `set P`. -/
instance : partial_order (ideal P) := partial_order.lift coe ext

@[trans] lemma mem_of_mem_of_le : x âˆˆ I â†’ I â‰¤ J â†’ x âˆˆ J :=
@set.mem_of_mem_of_subset P x I J

@[simp] lemma principal_le_iff : principal x â‰¤ I â†” x âˆˆ I :=
âŸ¨Î» (h : âˆ€ {y}, y â‰¤ x â†’ y âˆˆ I), h (le_refl x),
 Î» h_mem y (h_le : y â‰¤ x), I.mem_of_le h_le h_memâŸ©

/-- A proper ideal is one that is not the whole set.
    Note that the whole set might not be an ideal. -/
class proper (I : ideal P) : Prop := (ne_univ : (I : set P) â‰  set.univ)

lemma proper_iff {I : ideal P} : proper I â†” (I : set P) â‰  set.univ := âŸ¨Î» h, h.1, Î» h, âŸ¨hâŸ©âŸ©

lemma proper_of_not_mem {I : ideal P} {p : P} (nmem : p âˆ‰ I) : proper I :=
âŸ¨Î» hp, begin
  change p âˆ‰ â†‘I at nmem,
  rw hp at nmem,
  exact nmem (set.mem_univ p),
endâŸ©

/-- A maximal ideal if it is maximal in the collection of proper ideals.
  Note that we cannot use the `is_coatom` class because `P` might not have a `top` element.
-/
def is_maximal (I : ideal P) : Prop :=
  proper I âˆ§ âˆ€ J : ideal P, I < J â†’ J.carrier = âŠ¤

lemma is_maximal_iff {I : ideal P} :
  is_maximal I â†” proper I âˆ§ âˆ€ J : ideal P, I < J â†’ J.carrier = âŠ¤ :=
âŸ¨Î» h, âŸ¨h.1, h.2âŸ©, Î» h, âŸ¨h.1, h.2âŸ©âŸ©

lemma is_maximal.proper {I : ideal P} (hI : is_maximal I) : proper I := hI.1

lemma is_maximal.maximal_proper {I : ideal P} (hI : is_maximal I) :
  âˆ€ J : ideal P, I < J â†’ J.carrier = âŠ¤
:= hI.2

end preorder

section order_bot
variables [order_bot P] {I : ideal P}

/-- A specific witness of `I.nonempty` when `P` has a bottom element. -/
@[simp] lemma bot_mem : âŠ¥ âˆˆ I :=
I.mem_of_le bot_le I.nonempty.some_mem

/-- There is a bottom ideal when `P` has a bottom element. -/
instance : order_bot (ideal P) :=
{ bot := principal âŠ¥,
  bot_le := by simp,
  .. ideal.partial_order }

end order_bot

section order_top

variables [order_top P]

/-- There is a top ideal when `P` has a top element. -/
instance : order_top (ideal P) :=
{ top := principal âŠ¤,
  le_top := Î» I x h, le_top,
  .. ideal.partial_order }

@[simp] lemma top_carrier : (âŠ¤ : ideal P).carrier = set.univ :=
set.univ_subset_iff.1 (Î» p _, le_top)

@[simp] lemma top_coe : ((âŠ¤ : ideal P) : set P) = set.univ := top_carrier

lemma top_of_mem_top {I : ideal P} (topmem : âŠ¤ âˆˆ I) : I = âŠ¤ :=
begin
  ext,
  change x âˆˆ I.carrier â†” x âˆˆ (âŠ¤ : ideal P).carrier,
  split,
  { simp [top_carrier] },
  { exact Î» _, I.mem_of_le le_top topmem }
end

lemma proper_of_ne_top {I : ideal P} (ne_top : I â‰  âŠ¤) : proper I :=
proper_of_not_mem (Î» h, ne_top (top_of_mem_top h))

lemma proper.ne_top {I : ideal P} (hI : proper I) : I â‰  âŠ¤ :=
begin
  intro h,
  rw [ext'_iff, top_coe] at h,
  apply hI.ne_univ,
  assumption,
end

lemma proper_iff_ne_top {I : ideal P} : proper I â†” I â‰  âŠ¤ :=
  âŸ¨Î» h, h.ne_top, Î» h, proper_of_ne_top hâŸ©

lemma is_maximal.is_coatom {I : ideal P} (hI : is_maximal I) : is_coatom I :=
âŸ¨hI.proper.ne_top, Î» J hJ, by {rw [ext'_iff, top_coe], exact hI.2 J hJ}âŸ©

lemma is_maximal_of_is_coatom {I : ideal P} (hI : is_coatom I) : is_maximal I :=
  âŸ¨proper_of_ne_top hI.1, Î» J hJ, by simp [hI.2 _ hJ]âŸ©

end order_top

section semilattice_sup
variables [semilattice_sup P] {x y : P} {I : ideal P}

/-- A specific witness of `I.directed` when `P` has joins. -/
lemma sup_mem (x y âˆˆ I) : x âŠ” y âˆˆ I :=
let âŸ¨z, h_mem, hx, hyâŸ© := I.directed x â€¹_â€º y â€¹_â€º in
I.mem_of_le (sup_le hx hy) h_mem

@[simp] lemma sup_mem_iff : x âŠ” y âˆˆ I â†” x âˆˆ I âˆ§ y âˆˆ I :=
âŸ¨Î» h, âŸ¨I.mem_of_le le_sup_left h, I.mem_of_le le_sup_right hâŸ©,
 Î» h, sup_mem x y h.left h.rightâŸ©

end semilattice_sup

section semilattice_sup_bot
variables [semilattice_sup_bot P] (I J K : ideal P)

/-- The intersection of two ideals is an ideal, when `P` has joins and a bottom. -/
def inf (I J : ideal P) : ideal P :=
{ carrier   := I âˆ© J,
  nonempty  := âŸ¨âŠ¥, bot_mem, bot_memâŸ©,
  directed  := Î» x âŸ¨_, _âŸ© y âŸ¨_, _âŸ©, âŸ¨x âŠ” y, âŸ¨sup_mem x y â€¹_â€º â€¹_â€º, sup_mem x y â€¹_â€º â€¹_â€ºâŸ©, by simpâŸ©,
  mem_of_le := Î» x y h âŸ¨_, _âŸ©, âŸ¨mem_of_le I h â€¹_â€º, mem_of_le J h â€¹_â€ºâŸ© }

/-- There is a smallest ideal containing two ideals, when `P` has joins and a bottom. -/
def sup (I J : ideal P) : ideal P :=
{ carrier   := {x | âˆƒ (i âˆˆ I) (j âˆˆ J), x â‰¤ i âŠ” j},
  nonempty  := âŸ¨âŠ¥, âŠ¥, bot_mem, âŠ¥, bot_mem, bot_leâŸ©,
  directed  := Î» x âŸ¨xi, _, xj, _, _âŸ© y âŸ¨yi, _, yj, _, _âŸ©,
    âŸ¨x âŠ” y,
     âŸ¨xi âŠ” yi, sup_mem xi yi â€¹_â€º â€¹_â€º,
      xj âŠ” yj, sup_mem xj yj â€¹_â€º â€¹_â€º,
      sup_le
        (calc x â‰¤ xi âŠ” xj               : â€¹_â€º
         ...    â‰¤ (xi âŠ” yi) âŠ” (xj âŠ” yj) : sup_le_sup le_sup_left le_sup_left)
        (calc y â‰¤ yi âŠ” yj               : â€¹_â€º
         ...    â‰¤ (xi âŠ” yi) âŠ” (xj âŠ” yj) : sup_le_sup le_sup_right le_sup_right)âŸ©,
     le_sup_left, le_sup_rightâŸ©,
  mem_of_le := Î» x y _ âŸ¨yi, _, yj, _, _âŸ©, âŸ¨yi, â€¹_â€º, yj, â€¹_â€º, le_trans â€¹x â‰¤ yâ€º â€¹_â€ºâŸ© }

lemma sup_le : I â‰¤ K â†’ J â‰¤ K â†’ sup I J â‰¤ K :=
Î» hIK hJK x âŸ¨i, hiI, j, hjJ, hxijâŸ©,
K.mem_of_le hxij $ sup_mem i j (mem_of_mem_of_le hiI hIK) (mem_of_mem_of_le hjJ hJK)

instance : lattice (ideal P) :=
{ sup          := sup,
  le_sup_left  := Î» I J (i âˆˆ I), âŸ¨i, â€¹_â€º, âŠ¥, bot_mem, by simp only [sup_bot_eq]âŸ©,
  le_sup_right := Î» I J (j âˆˆ J), âŸ¨âŠ¥, bot_mem, j, â€¹_â€º, by simp only [bot_sup_eq]âŸ©,
  sup_le       := sup_le,
  inf          := inf,
  inf_le_left  := Î» I J, set.inter_subset_left I J,
  inf_le_right := Î» I J, set.inter_subset_right I J,
  le_inf       := Î» I J K, set.subset_inter,
  .. ideal.partial_order }

@[simp] lemma mem_inf {x : P} : x âˆˆ I âŠ“ J â†” x âˆˆ I âˆ§ x âˆˆ J := iff_of_eq rfl

@[simp] lemma mem_sup {x : P} : x âˆˆ I âŠ” J â†” âˆƒ (i âˆˆ I) (j âˆˆ J), x â‰¤ i âŠ” j := iff_of_eq rfl

end semilattice_sup_bot

section semilattice_inf

variable [semilattice_inf P]

/-- A prime ideal is an ideal that satisfies `x âŠ“ y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I`
-/
def is_prime (I : ideal P) : Prop :=
proper I âˆ§ âˆ€ {x y : P}, x âŠ“ y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I

variable {I : ideal P}

lemma is_prime_iff :
  is_prime I â†” proper I âˆ§ âˆ€ {x y : P}, x âŠ“ y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I :=
âŸ¨Î» h, âŸ¨h.1, h.2âŸ©, Î» h, âŸ¨h.1, h.2âŸ©âŸ©

lemma is_prime.proper (hI : is_prime I) : proper I := hI.1

lemma is_prime.mem_or_mem (hI : is_prime I) :
  âˆ€ {x y : P}, x âŠ“ y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I :=
hI.2

lemma is_prime_of_is_maximal (hI : is_maximal I) : is_prime I :=
begin
  use hI.proper,
  intros x y hxy,
  contrapose! hxy,
  sorry,
end

end semilattice_inf

end ideal

/-- For a preorder `P`, `cofinal P` is the type of subsets of `P`
  containing arbitrarily large elements. They are the dense sets in
  the topology whose open sets are terminal segments. -/
structure cofinal (P) [preorder P] :=
(carrier : set P)
(mem_gt  : âˆ€ x : P, âˆƒ y âˆˆ carrier, x â‰¤ y)

namespace cofinal

variables [preorder P]

instance : inhabited (cofinal P) :=
âŸ¨{ carrier := set.univ, mem_gt := Î» x, âŸ¨x, trivial, le_refl _âŸ©}âŸ©

instance : has_mem P (cofinal P) := âŸ¨Î» x D, x âˆˆ D.carrierâŸ©

variables (D : cofinal P) (x : P)
/-- A (noncomputable) element of a cofinal set lying above a given element. -/
noncomputable def above : P := classical.some $ D.mem_gt x

lemma above_mem : D.above x âˆˆ D :=
exists.elim (classical.some_spec $ D.mem_gt x) $ Î» a _, a

lemma le_above : x â‰¤ D.above x :=
exists.elim (classical.some_spec $ D.mem_gt x) $ Î» _ b, b

end cofinal

section ideal_of_cofinals

variables [preorder P] (p : P) {Î¹ : Type*} [encodable Î¹] (ğ’Ÿ : Î¹ â†’ cofinal P)

/-- Given a starting point, and a countable family of cofinal sets,
  this is an increasing sequence that intersects each cofinal set. -/
noncomputable def sequence_of_cofinals : â„• â†’ P
| 0 := p
| (n+1) := match encodable.decode Î¹ n with
           | none   := sequence_of_cofinals n
           | some i := (ğ’Ÿ i).above (sequence_of_cofinals n)
           end

lemma sequence_of_cofinals.monotone : monotone (sequence_of_cofinals p ğ’Ÿ) :=
by { apply monotone_of_monotone_nat, intros n, dunfold sequence_of_cofinals,
  cases encodable.decode Î¹ n, { refl }, { apply cofinal.le_above }, }

lemma sequence_of_cofinals.encode_mem (i : Î¹) :
  sequence_of_cofinals p ğ’Ÿ (encodable.encode i + 1) âˆˆ ğ’Ÿ i :=
by { dunfold sequence_of_cofinals, rw encodable.encodek, apply cofinal.above_mem, }

/-- Given an element `p : P` and a family `ğ’Ÿ` of cofinal subsets of a preorder `P`,
  indexed by a countable type, `ideal_of_cofinals p ğ’Ÿ` is an ideal in `P` which
  - contains `p`, according to `mem_ideal_of_cofinals p ğ’Ÿ`, and
  - intersects every set in `ğ’Ÿ`, according to `cofinal_meets_ideal_of_cofinals p ğ’Ÿ`.

  This proves the Rasiowaâ€“Sikorski lemma. -/
def ideal_of_cofinals : ideal P :=
{ carrier   := { x : P | âˆƒ n, x â‰¤ sequence_of_cofinals p ğ’Ÿ n },
  nonempty  := âŸ¨p, 0, le_refl _âŸ©,
  directed  := Î» x âŸ¨n, hnâŸ© y âŸ¨m, hmâŸ©,
               âŸ¨_, âŸ¨max n m, le_refl _âŸ©,
               le_trans hn $ sequence_of_cofinals.monotone p ğ’Ÿ (le_max_left _ _),
               le_trans hm $ sequence_of_cofinals.monotone p ğ’Ÿ (le_max_right _ _) âŸ©,
  mem_of_le := Î» x y hxy âŸ¨n, hnâŸ©, âŸ¨n, le_trans hxy hnâŸ©, }

lemma mem_ideal_of_cofinals : p âˆˆ ideal_of_cofinals p ğ’Ÿ := âŸ¨0, le_refl _âŸ©

/-- `ideal_of_cofinals p ğ’Ÿ` is `ğ’Ÿ`-generic. -/
lemma cofinal_meets_ideal_of_cofinals (i : Î¹) : âˆƒ x : P, x âˆˆ ğ’Ÿ i âˆ§ x âˆˆ ideal_of_cofinals p ğ’Ÿ :=
âŸ¨_, sequence_of_cofinals.encode_mem p ğ’Ÿ i, _, le_refl _âŸ©

end ideal_of_cofinals

end order
