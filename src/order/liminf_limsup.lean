/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Johannes H√∂lzl, R√©my Degenne
-/
import order.filter.cofinite
import order.hom.complete_lattice

/-!
# liminfs and limsups of functions and filters

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Defines the Liminf/Limsup of a function taking values in a conditionally complete lattice, with
respect to an arbitrary filter.

We define `Limsup f` (`Liminf f`) where `f` is a filter taking values in a conditionally complete
lattice. `Limsup f` is the smallest element `a` such that, eventually, `u ‚â§ a` (and vice versa for
`Liminf f`). To work with the Limsup along a function `u` use `Limsup (map u f)`.

Usually, one defines the Limsup as `Inf (Sup s)` where the Inf is taken over all sets in the filter.
For instance, in ‚Ñï along a function `u`, this is `Inf_n (Sup_{k ‚â• n} u k)` (and the latter quantity
decreases with `n`, so this is in fact a limit.). There is however a difficulty: it is well possible
that `u` is not bounded on the whole space, only eventually (think of `Limsup (Œªx, 1/x)` on ‚Ñù. Then
there is no guarantee that the quantity above really decreases (the value of the `Sup` beforehand is
not really well defined, as one can not use ‚àû), so that the Inf could be anything. So one can not
use this `Inf Sup ...` definition in conditionally complete lattices, and one has to use a less
tractable definition.

In conditionally complete lattices, the definition is only useful for filters which are eventually
bounded above (otherwise, the Limsup would morally be +‚àû, which does not belong to the space) and
which are frequently bounded below (otherwise, the Limsup would morally be -‚àû, which is not in the
space either). We start with definitions of these concepts for arbitrary filters, before turning to
the definitions of Limsup and Liminf.

In complete lattices, however, it coincides with the `Inf Sup` definition.
-/

open filter set
open_locale filter

variables {Œ± Œ≤ Œ≥ Œπ : Type*}
namespace filter

section relation

/-- `f.is_bounded (‚â∫)`: the filter `f` is eventually bounded w.r.t. the relation `‚â∫`, i.e.
eventually, it is bounded by some uniform bound.
`r` will be usually instantiated with `‚â§` or `‚â•`. -/
def is_bounded (r : Œ± ‚Üí Œ± ‚Üí Prop) (f : filter Œ±) := ‚àÉ b, ‚àÄ·∂† x in f, r x b

/-- `f.is_bounded_under (‚â∫) u`: the image of the filter `f` under `u` is eventually bounded w.r.t.
the relation `‚â∫`, i.e. eventually, it is bounded by some uniform bound. -/
def is_bounded_under (r : Œ± ‚Üí Œ± ‚Üí Prop) (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±) := (map u f).is_bounded r

variables {r : Œ± ‚Üí Œ± ‚Üí Prop} {f g : filter Œ±}

/-- `f` is eventually bounded if and only if, there exists an admissible set on which it is
bounded. -/
lemma is_bounded_iff : f.is_bounded r ‚Üî (‚àÉs‚ààf.sets, ‚àÉb, s ‚äÜ {x | r x b}) :=
iff.intro
  (assume ‚ü®b, hb‚ü©, ‚ü®{a | r a b}, hb, b, subset.refl _‚ü©)
  (assume ‚ü®s, hs, b, hb‚ü©, ‚ü®b, mem_of_superset hs hb‚ü©)

/-- A bounded function `u` is in particular eventually bounded. -/
lemma is_bounded_under_of {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} :
  (‚àÉb, ‚àÄx, r (u x) b) ‚Üí f.is_bounded_under r u
| ‚ü®b, hb‚ü© := ‚ü®b, show ‚àÄ·∂† x in f, r (u x) b, from eventually_of_forall hb‚ü©

lemma is_bounded_bot : is_bounded r ‚ä• ‚Üî nonempty Œ± :=
by simp [is_bounded, exists_true_iff_nonempty]

lemma is_bounded_top : is_bounded r ‚ä§ ‚Üî (‚àÉt, ‚àÄx, r x t) :=
by simp [is_bounded, eq_univ_iff_forall]

lemma is_bounded_principal (s : set Œ±) : is_bounded r (ùìü s) ‚Üî (‚àÉt, ‚àÄx‚ààs, r x t) :=
by simp [is_bounded, subset_def]

lemma is_bounded_sup [is_trans Œ± r] (hr : ‚àÄb‚ÇÅ b‚ÇÇ, ‚àÉb, r b‚ÇÅ b ‚àß r b‚ÇÇ b) :
  is_bounded r f ‚Üí is_bounded r g ‚Üí is_bounded r (f ‚äî g)
| ‚ü®b‚ÇÅ, h‚ÇÅ‚ü© ‚ü®b‚ÇÇ, h‚ÇÇ‚ü© := let ‚ü®b, rb‚ÇÅb, rb‚ÇÇb‚ü© := hr b‚ÇÅ b‚ÇÇ in
  ‚ü®b, eventually_sup.mpr ‚ü®h‚ÇÅ.mono (Œª x h, trans h rb‚ÇÅb), h‚ÇÇ.mono (Œª x h, trans h rb‚ÇÇb)‚ü©‚ü©

lemma is_bounded.mono (h : f ‚â§ g) : is_bounded r g ‚Üí is_bounded r f
| ‚ü®b, hb‚ü© := ‚ü®b, h hb‚ü©

lemma is_bounded_under.mono {f g : filter Œ≤} {u : Œ≤ ‚Üí Œ±} (h : f ‚â§ g) :
  g.is_bounded_under r u ‚Üí f.is_bounded_under r u :=
Œª hg, hg.mono (map_mono h)

lemma is_bounded_under.mono_le [preorder Œ≤] {l : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (hu : is_bounded_under (‚â§) l u) (hv : v ‚â§·∂†[l] u) : is_bounded_under (‚â§) l v :=
hu.imp $ Œª b hb, (eventually_map.1 hb).mp $ hv.mono $ Œª x, le_trans

lemma is_bounded_under.mono_ge [preorder Œ≤] {l : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (hu : is_bounded_under (‚â•) l u) (hv : u ‚â§·∂†[l] v) : is_bounded_under (‚â•) l v :=
@is_bounded_under.mono_le Œ± Œ≤·µí·µà _ _ _ _ hu hv

lemma is_bounded_under_const [is_refl Œ± r] {l : filter Œ≤} {a : Œ±} : is_bounded_under r l (Œª _, a) :=
‚ü®a, eventually_map.2 $ eventually_of_forall $ Œª _, refl _‚ü©

lemma is_bounded.is_bounded_under {q : Œ≤ ‚Üí Œ≤ ‚Üí Prop} {u : Œ± ‚Üí Œ≤}
  (hf : ‚àÄa‚ÇÄ a‚ÇÅ, r a‚ÇÄ a‚ÇÅ ‚Üí q (u a‚ÇÄ) (u a‚ÇÅ)) : f.is_bounded r ‚Üí f.is_bounded_under q u
| ‚ü®b, h‚ü© := ‚ü®u b, show ‚àÄ·∂† x in f, q (u x) (u b), from h.mono (Œª x, hf x b)‚ü©

lemma not_is_bounded_under_of_tendsto_at_top [preorder Œ≤] [no_max_order Œ≤] {f : Œ± ‚Üí Œ≤}
  {l : filter Œ±} [l.ne_bot] (hf : tendsto f l at_top) :
  ¬¨ is_bounded_under (‚â§) l f :=
begin
  rintro ‚ü®b, hb‚ü©,
  rw eventually_map at hb,
  obtain ‚ü®b', h‚ü© := exists_gt b,
  have hb' := (tendsto_at_top.mp hf) b',
  have : {x : Œ± | f x ‚â§ b} ‚à© {x : Œ± | b' ‚â§ f x} = ‚àÖ :=
    eq_empty_of_subset_empty (Œª x hx, (not_le_of_lt h) (le_trans hx.2 hx.1)),
  exact (nonempty_of_mem (hb.and hb')).ne_empty this
end

lemma not_is_bounded_under_of_tendsto_at_bot [preorder Œ≤] [no_min_order Œ≤] {f : Œ± ‚Üí Œ≤}
  {l : filter Œ±} [l.ne_bot](hf : tendsto f l at_bot) :
  ¬¨ is_bounded_under (‚â•) l f :=
@not_is_bounded_under_of_tendsto_at_top Œ± Œ≤·µí·µà _ _ _ _ _ hf

lemma is_bounded_under.bdd_above_range_of_cofinite [semilattice_sup Œ≤] {f : Œ± ‚Üí Œ≤}
  (hf : is_bounded_under (‚â§) cofinite f) : bdd_above (range f) :=
begin
  rcases hf with ‚ü®b, hb‚ü©,
  haveI : nonempty Œ≤ := ‚ü®b‚ü©,
  rw [‚Üê image_univ, ‚Üê union_compl_self {x | f x ‚â§ b}, image_union, bdd_above_union],
  exact ‚ü®‚ü®b, ball_image_iff.2 $ Œª x, id‚ü©, (hb.image f).bdd_above‚ü©
end

lemma is_bounded_under.bdd_below_range_of_cofinite [semilattice_inf Œ≤] {f : Œ± ‚Üí Œ≤}
  (hf : is_bounded_under (‚â•) cofinite f) : bdd_below (range f) :=
@is_bounded_under.bdd_above_range_of_cofinite Œ± Œ≤·µí·µà _ _ hf

lemma is_bounded_under.bdd_above_range [semilattice_sup Œ≤] {f : ‚Ñï ‚Üí Œ≤}
  (hf : is_bounded_under (‚â§) at_top f) : bdd_above (range f) :=
by { rw ‚Üê nat.cofinite_eq_at_top at hf, exact hf.bdd_above_range_of_cofinite }

lemma is_bounded_under.bdd_below_range [semilattice_inf Œ≤] {f : ‚Ñï ‚Üí Œ≤}
  (hf : is_bounded_under (‚â•) at_top f) : bdd_below (range f) :=
@is_bounded_under.bdd_above_range Œ≤·µí·µà _ _ hf

/-- `is_cobounded (‚â∫) f` states that the filter `f` does not tend to infinity w.r.t. `‚â∫`. This is
also called frequently bounded. Will be usually instantiated with `‚â§` or `‚â•`.

There is a subtlety in this definition: we want `f.is_cobounded` to hold for any `f` in the case of
complete lattices. This will be relevant to deduce theorems on complete lattices from their
versions on conditionally complete lattices with additional assumptions. We have to be careful in
the edge case of the trivial filter containing the empty set: the other natural definition
  `¬¨ ‚àÄ a, ‚àÄ·∂† n in f, a ‚â§ n`
would not work as well in this case.
-/
def is_cobounded (r : Œ± ‚Üí Œ± ‚Üí Prop) (f : filter Œ±) := ‚àÉb, ‚àÄa, (‚àÄ·∂† x in f, r x a) ‚Üí r b a

/-- `is_cobounded_under (‚â∫) f u` states that the image of the filter `f` under the map `u` does not
tend to infinity w.r.t. `‚â∫`. This is also called frequently bounded. Will be usually instantiated
with `‚â§` or `‚â•`. -/
def is_cobounded_under (r : Œ± ‚Üí Œ± ‚Üí Prop) (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±) := (map u f).is_cobounded r

/-- To check that a filter is frequently bounded, it suffices to have a witness
which bounds `f` at some point for every admissible set.

This is only an implication, as the other direction is wrong for the trivial filter.-/
lemma is_cobounded.mk [is_trans Œ± r] (a : Œ±) (h : ‚àÄs‚ààf, ‚àÉx‚ààs, r a x) : f.is_cobounded r :=
‚ü®a, assume y s, let ‚ü®x, h‚ÇÅ, h‚ÇÇ‚ü© := h _ s in trans h‚ÇÇ h‚ÇÅ‚ü©

/-- A filter which is eventually bounded is in particular frequently bounded (in the opposite
direction). At least if the filter is not trivial. -/
lemma is_bounded.is_cobounded_flip [is_trans Œ± r] [ne_bot f] :
  f.is_bounded r ‚Üí f.is_cobounded (flip r)
| ‚ü®a, ha‚ü© := ‚ü®a, assume b hb,
  let ‚ü®x, rxa, rbx‚ü© := (ha.and hb).exists in
  show r b a, from trans rbx rxa‚ü©

lemma is_bounded.is_cobounded_ge [preorder Œ±] [ne_bot f] (h : f.is_bounded (‚â§)) :
  f.is_cobounded (‚â•) :=
h.is_cobounded_flip

lemma is_bounded.is_cobounded_le [preorder Œ±] [ne_bot f] (h : f.is_bounded (‚â•)) :
  f.is_cobounded (‚â§) :=
h.is_cobounded_flip

lemma is_cobounded_bot : is_cobounded r ‚ä• ‚Üî (‚àÉb, ‚àÄx, r b x) :=
by simp [is_cobounded]

lemma is_cobounded_top : is_cobounded r ‚ä§ ‚Üî nonempty Œ± :=
by simp [is_cobounded, eq_univ_iff_forall, exists_true_iff_nonempty] {contextual := tt}

lemma is_cobounded_principal (s : set Œ±) :
  (ùìü s).is_cobounded r ‚Üî (‚àÉb, ‚àÄa, (‚àÄx‚ààs, r x a) ‚Üí r b a) :=
by simp [is_cobounded, subset_def]

lemma is_cobounded.mono (h : f ‚â§ g) : f.is_cobounded r ‚Üí g.is_cobounded r
| ‚ü®b, hb‚ü© := ‚ü®b, assume a ha, hb a (h ha)‚ü©

end relation

lemma is_cobounded_le_of_bot [preorder Œ±] [order_bot Œ±] {f : filter Œ±} : f.is_cobounded (‚â§) :=
‚ü®‚ä•, assume a h, bot_le‚ü©

lemma is_cobounded_ge_of_top [preorder Œ±] [order_top Œ±] {f : filter Œ±} : f.is_cobounded (‚â•) :=
‚ü®‚ä§, assume a h, le_top‚ü©

lemma is_bounded_le_of_top [preorder Œ±] [order_top Œ±] {f : filter Œ±} : f.is_bounded (‚â§) :=
‚ü®‚ä§, eventually_of_forall $ Œª _, le_top‚ü©

lemma is_bounded_ge_of_bot [preorder Œ±] [order_bot Œ±] {f : filter Œ±} : f.is_bounded (‚â•) :=
‚ü®‚ä•, eventually_of_forall $ Œª _, bot_le‚ü©

@[simp] lemma _root_.order_iso.is_bounded_under_le_comp [preorder Œ±] [preorder Œ≤] (e : Œ± ‚âÉo Œ≤)
  {l : filter Œ≥} {u : Œ≥ ‚Üí Œ±} :
  is_bounded_under (‚â§) l (Œª x, e (u x)) ‚Üî is_bounded_under (‚â§) l u :=
e.surjective.exists.trans $ exists_congr $ Œª a, by simp only [eventually_map, e.le_iff_le]

@[simp] lemma _root_.order_iso.is_bounded_under_ge_comp [preorder Œ±] [preorder Œ≤] (e : Œ± ‚âÉo Œ≤)
  {l : filter Œ≥} {u : Œ≥ ‚Üí Œ±} :
  is_bounded_under (‚â•) l (Œª x, e (u x)) ‚Üî is_bounded_under (‚â•) l u :=
e.dual.is_bounded_under_le_comp

@[simp, to_additive]
lemma is_bounded_under_le_inv [ordered_comm_group Œ±] {l : filter Œ≤} {u : Œ≤ ‚Üí Œ±} :
  is_bounded_under (‚â§) l (Œª x, (u x)‚Åª¬π) ‚Üî is_bounded_under (‚â•) l u :=
(order_iso.inv Œ±).is_bounded_under_ge_comp

@[simp, to_additive]
lemma is_bounded_under_ge_inv [ordered_comm_group Œ±] {l : filter Œ≤} {u : Œ≤ ‚Üí Œ±} :
  is_bounded_under (‚â•) l (Œª x, (u x)‚Åª¬π) ‚Üî is_bounded_under (‚â§) l u :=
(order_iso.inv Œ±).is_bounded_under_le_comp

lemma is_bounded_under.sup [semilattice_sup Œ±] {f : filter Œ≤} {u v : Œ≤ ‚Üí Œ±} :
  f.is_bounded_under (‚â§) u ‚Üí f.is_bounded_under (‚â§) v ‚Üí f.is_bounded_under (‚â§) (Œªa, u a ‚äî v a)
| ‚ü®bu, (hu : ‚àÄ·∂† x in f, u x ‚â§ bu)‚ü© ‚ü®bv, (hv : ‚àÄ·∂† x in f, v x ‚â§ bv)‚ü© :=
  ‚ü®bu ‚äî bv, show ‚àÄ·∂† x in f, u x ‚äî v x ‚â§ bu ‚äî bv,
    by filter_upwards [hu, hv] with _ using sup_le_sup‚ü©

@[simp] lemma is_bounded_under_le_sup [semilattice_sup Œ±] {f : filter Œ≤} {u v : Œ≤ ‚Üí Œ±} :
  f.is_bounded_under (‚â§) (Œª a, u a ‚äî v a) ‚Üî f.is_bounded_under (‚â§) u ‚àß f.is_bounded_under (‚â§) v :=
‚ü®Œª h, ‚ü®h.mono_le $ eventually_of_forall $ Œª _, le_sup_left,
  h.mono_le $ eventually_of_forall $ Œª _, le_sup_right‚ü©, Œª h, h.1.sup h.2‚ü©

lemma is_bounded_under.inf [semilattice_inf Œ±] {f : filter Œ≤} {u v : Œ≤ ‚Üí Œ±} :
  f.is_bounded_under (‚â•) u ‚Üí f.is_bounded_under (‚â•) v ‚Üí f.is_bounded_under (‚â•) (Œªa, u a ‚äì v a) :=
@is_bounded_under.sup Œ±·µí·µà Œ≤ _ _ _ _

@[simp] lemma is_bounded_under_ge_inf [semilattice_inf Œ±] {f : filter Œ≤} {u v : Œ≤ ‚Üí Œ±} :
  f.is_bounded_under (‚â•) (Œª a, u a ‚äì v a) ‚Üî f.is_bounded_under (‚â•) u ‚àß f.is_bounded_under (‚â•) v :=
@is_bounded_under_le_sup Œ±·µí·µà _ _ _ _ _

lemma is_bounded_under_le_abs [linear_ordered_add_comm_group Œ±] {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} :
  f.is_bounded_under (‚â§) (Œª a, |u a|) ‚Üî f.is_bounded_under (‚â§) u ‚àß f.is_bounded_under (‚â•) u :=
is_bounded_under_le_sup.trans $ and_congr iff.rfl is_bounded_under_le_neg

/-- Filters are automatically bounded or cobounded in complete lattices. To use the same statements
in complete and conditionally complete lattices but let automation fill automatically the
boundedness proofs in complete lattices, we use the tactic `is_bounded_default` in the statements,
in the form `(hf : f.is_bounded (‚â•) . is_bounded_default)`. -/
meta def is_bounded_default : tactic unit :=
tactic.applyc ``is_cobounded_le_of_bot <|>
tactic.applyc ``is_cobounded_ge_of_top <|>
tactic.applyc ``is_bounded_le_of_top <|>
tactic.applyc ``is_bounded_ge_of_bot


section conditionally_complete_lattice
variables [conditionally_complete_lattice Œ±]

/-- The `Limsup` of a filter `f` is the infimum of the `a` such that, eventually for `f`,
holds `x ‚â§ a`. -/
def Limsup (f : filter Œ±) : Œ± := Inf { a | ‚àÄ·∂† n in f, n ‚â§ a }

/-- The `Liminf` of a filter `f` is the supremum of the `a` such that, eventually for `f`,
holds `x ‚â• a`. -/
def Liminf (f : filter Œ±) : Œ± := Sup { a | ‚àÄ·∂† n in f, a ‚â§ n }

/-- The `limsup` of a function `u` along a filter `f` is the infimum of the `a` such that,
eventually for `f`, holds `u x ‚â§ a`. -/
def limsup (u : Œ≤ ‚Üí Œ±) (f : filter Œ≤) : Œ± := Limsup (map u f)

/-- The `liminf` of a function `u` along a filter `f` is the supremum of the `a` such that,
eventually for `f`, holds `u x ‚â• a`. -/
def liminf (u : Œ≤ ‚Üí Œ±) (f : filter Œ≤) : Œ± := Liminf (map u f)

/-- The `blimsup` of a function `u` along a filter `f`, bounded by a predicate `p`, is the infimum
of the `a` such that, eventually for `f`, `u x ‚â§ a` whenever `p x` holds. -/
def blimsup (u : Œ≤ ‚Üí Œ±) (f : filter Œ≤) (p : Œ≤ ‚Üí Prop) :=
Inf { a | ‚àÄ·∂† x in f, p x ‚Üí u x ‚â§ a }

/-- The `bliminf` of a function `u` along a filter `f`, bounded by a predicate `p`, is the supremum
of the `a` such that, eventually for `f`, `a ‚â§ u x` whenever `p x` holds. -/
def bliminf (u : Œ≤ ‚Üí Œ±) (f : filter Œ≤) (p : Œ≤ ‚Üí Prop) :=
Sup { a | ‚àÄ·∂† x in f, p x ‚Üí a ‚â§ u x }

section

variables {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} {p : Œ≤ ‚Üí Prop}

theorem limsup_eq : limsup u f = Inf { a | ‚àÄ·∂† n in f, u n ‚â§ a } := rfl

theorem liminf_eq : liminf u f = Sup { a | ‚àÄ·∂† n in f, a ‚â§ u n } := rfl

theorem blimsup_eq : blimsup u f p = Inf { a | ‚àÄ·∂† x in f, p x ‚Üí u x ‚â§ a } := rfl

theorem bliminf_eq : bliminf u f p = Sup { a | ‚àÄ·∂† x in f, p x ‚Üí a ‚â§ u x } := rfl

end

@[simp] lemma blimsup_true (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±) :
  blimsup u f (Œª x, true) = limsup u f :=
by simp [blimsup_eq, limsup_eq]

@[simp] lemma bliminf_true (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±) :
  bliminf u f (Œª x, true) = liminf u f :=
by simp [bliminf_eq, liminf_eq]

lemma blimsup_eq_limsup_subtype {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} {p : Œ≤ ‚Üí Prop} :
  blimsup u f p = limsup (u ‚àò (coe : {x | p x} ‚Üí Œ≤)) (comap coe f) :=
begin
  simp only [blimsup_eq, limsup_eq, function.comp_app, eventually_comap, set_coe.forall,
    subtype.coe_mk, mem_set_of_eq],
  congr,
  ext a,
  exact eventually_congr (eventually_of_forall
    (Œª x, ‚ü®Œª hx y hy hxy, hxy.symm ‚ñ∏ (hx (hxy ‚ñ∏ hy)), Œª hx hx', hx x hx' rfl‚ü©)),
end

lemma bliminf_eq_liminf_subtype {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} {p : Œ≤ ‚Üí Prop} :
  bliminf u f p = liminf (u ‚àò (coe : {x | p x} ‚Üí Œ≤)) (comap coe f) :=
@blimsup_eq_limsup_subtype Œ±·µí·µà Œ≤ _ f u p

theorem Limsup_le_of_le {f : filter Œ±} {a}
  (hf : f.is_cobounded (‚â§) . is_bounded_default) (h : ‚àÄ·∂† n in f, n ‚â§ a) : Limsup f ‚â§ a :=
cInf_le hf h

theorem le_Liminf_of_le {f : filter Œ±} {a}
  (hf : f.is_cobounded (‚â•) . is_bounded_default) (h : ‚àÄ·∂† n in f, a ‚â§ n) : a ‚â§ Liminf f :=
le_cSup hf h

theorem limsup_le_of_le {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} {a}
  (hf : f.is_cobounded_under (‚â§) u . is_bounded_default) (h : ‚àÄ·∂† n in f, u n ‚â§ a) :
  limsup u f ‚â§ a :=
cInf_le hf h

theorem le_liminf_of_le {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} {a}
  (hf : f.is_cobounded_under (‚â•) u . is_bounded_default) (h : ‚àÄ·∂† n in f, a ‚â§ u n) :
    a ‚â§ liminf u f :=
le_cSup hf h

theorem le_Limsup_of_le {f : filter Œ±} {a}
  (hf : f.is_bounded (‚â§) . is_bounded_default) (h : ‚àÄ b, (‚àÄ·∂† n in f, n ‚â§ b) ‚Üí a ‚â§ b) :
  a ‚â§ Limsup f :=
le_cInf hf h

theorem Liminf_le_of_le {f : filter Œ±} {a}
  (hf : f.is_bounded (‚â•) . is_bounded_default) (h : ‚àÄ b, (‚àÄ·∂† n in f, b ‚â§ n) ‚Üí b ‚â§ a) :
  Liminf f ‚â§ a :=
cSup_le hf h

theorem le_limsup_of_le {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} {a}
  (hf : f.is_bounded_under (‚â§) u . is_bounded_default) (h : ‚àÄ b, (‚àÄ·∂† n in f, u n ‚â§ b) ‚Üí a ‚â§ b) :
  a ‚â§ limsup u f :=
le_cInf hf h

theorem liminf_le_of_le {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} {a}
  (hf : f.is_bounded_under (‚â•) u . is_bounded_default) (h : ‚àÄ b, (‚àÄ·∂† n in f, b ‚â§ u n) ‚Üí b ‚â§ a) :
  liminf u f ‚â§ a :=
cSup_le hf h

theorem Liminf_le_Limsup {f : filter Œ±} [ne_bot f]
  (h‚ÇÅ : f.is_bounded (‚â§) . is_bounded_default) (h‚ÇÇ : f.is_bounded (‚â•) . is_bounded_default) :
  Liminf f ‚â§ Limsup f :=
Liminf_le_of_le h‚ÇÇ $ assume a‚ÇÄ ha‚ÇÄ, le_Limsup_of_le h‚ÇÅ $ assume a‚ÇÅ ha‚ÇÅ,
  show a‚ÇÄ ‚â§ a‚ÇÅ, from let ‚ü®b, hb‚ÇÄ, hb‚ÇÅ‚ü© := (ha‚ÇÄ.and ha‚ÇÅ).exists in le_trans hb‚ÇÄ hb‚ÇÅ

lemma liminf_le_limsup {f : filter Œ≤} [ne_bot f] {u : Œ≤ ‚Üí Œ±}
  (h : f.is_bounded_under (‚â§) u . is_bounded_default)
  (h' : f.is_bounded_under (‚â•) u . is_bounded_default) :
  liminf u f ‚â§ limsup u f :=
Liminf_le_Limsup h h'

lemma Limsup_le_Limsup {f g : filter Œ±}
  (hf : f.is_cobounded (‚â§) . is_bounded_default) (hg : g.is_bounded (‚â§) . is_bounded_default)
  (h : ‚àÄ a, (‚àÄ·∂† n in g, n ‚â§ a) ‚Üí ‚àÄ·∂† n in f, n ‚â§ a) : Limsup f ‚â§ Limsup g :=
cInf_le_cInf hf hg h

lemma Liminf_le_Liminf {f g : filter Œ±}
  (hf : f.is_bounded (‚â•) . is_bounded_default) (hg : g.is_cobounded (‚â•) . is_bounded_default)
  (h : ‚àÄ a, (‚àÄ·∂† n in f, a ‚â§ n) ‚Üí ‚àÄ·∂† n in g, a ‚â§ n) : Liminf f ‚â§ Liminf g :=
cSup_le_cSup hg hf h

lemma limsup_le_limsup {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (h : u ‚â§·∂†[f] v)
  (hu : f.is_cobounded_under (‚â§) u . is_bounded_default)
  (hv : f.is_bounded_under (‚â§) v . is_bounded_default) :
  limsup u f ‚â§ limsup v f :=
Limsup_le_Limsup hu hv $ assume b, h.trans

lemma liminf_le_liminf {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (h : ‚àÄ·∂† a in f, u a ‚â§ v a)
  (hu : f.is_bounded_under (‚â•) u . is_bounded_default)
  (hv : f.is_cobounded_under (‚â•) v . is_bounded_default) :
  liminf u f ‚â§ liminf v f :=
@limsup_le_limsup Œ≤·µí·µà Œ± _ _ _ _ h hv hu

lemma Limsup_le_Limsup_of_le {f g : filter Œ±} (h : f ‚â§ g)
  (hf : f.is_cobounded (‚â§) . is_bounded_default) (hg : g.is_bounded (‚â§) . is_bounded_default) :
  Limsup f ‚â§ Limsup g :=
Limsup_le_Limsup hf hg (assume a ha, h ha)

lemma Liminf_le_Liminf_of_le {f g : filter Œ±} (h : g ‚â§ f)
  (hf : f.is_bounded (‚â•) . is_bounded_default) (hg : g.is_cobounded (‚â•) . is_bounded_default) :
  Liminf f ‚â§ Liminf g :=
Liminf_le_Liminf hf hg (assume a ha, h ha)

lemma limsup_le_limsup_of_le {Œ± Œ≤} [conditionally_complete_lattice Œ≤] {f g : filter Œ±} (h : f ‚â§ g)
  {u : Œ± ‚Üí Œ≤} (hf : f.is_cobounded_under (‚â§) u . is_bounded_default)
  (hg : g.is_bounded_under (‚â§) u . is_bounded_default) :
  limsup u f ‚â§ limsup u g :=
Limsup_le_Limsup_of_le (map_mono h) hf hg

lemma liminf_le_liminf_of_le {Œ± Œ≤} [conditionally_complete_lattice Œ≤] {f g : filter Œ±} (h : g ‚â§ f)
  {u : Œ± ‚Üí Œ≤} (hf : f.is_bounded_under (‚â•) u . is_bounded_default)
  (hg : g.is_cobounded_under (‚â•) u . is_bounded_default) :
  liminf u f ‚â§ liminf u g :=
Liminf_le_Liminf_of_le (map_mono h) hf hg

theorem Limsup_principal {s : set Œ±} (h : bdd_above s) (hs : s.nonempty) :
  Limsup (ùìü s) = Sup s :=
by simp [Limsup]; exact cInf_upper_bounds_eq_cSup h hs

theorem Liminf_principal {s : set Œ±} (h : bdd_below s) (hs : s.nonempty) :
  Liminf (ùìü s) = Inf s :=
@Limsup_principal Œ±·µí·µà _ s h hs

lemma limsup_congr {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (h : ‚àÄ·∂† a in f, u a = v a) : limsup u f = limsup v f :=
begin
  rw limsup_eq,
  congr' with b,
  exact eventually_congr (h.mono $ Œª x hx, by simp [hx])
end

lemma blimsup_congr {f : filter Œ≤} {u v : Œ≤ ‚Üí Œ±} {p : Œ≤ ‚Üí Prop} (h : ‚àÄ·∂† a in f, p a ‚Üí u a = v a) :
  blimsup u f p = blimsup v f p :=
begin
  rw blimsup_eq,
  congr' with b,
  refine eventually_congr (h.mono $ Œª x hx, ‚ü®Œª h‚ÇÅ h‚ÇÇ, _, Œª h‚ÇÅ h‚ÇÇ, _‚ü©),
  { rw ‚Üê hx h‚ÇÇ, exact h‚ÇÅ h‚ÇÇ, },
  { rw hx h‚ÇÇ, exact h‚ÇÅ h‚ÇÇ, },
end

lemma bliminf_congr {f : filter Œ≤} {u v : Œ≤ ‚Üí Œ±} {p : Œ≤ ‚Üí Prop} (h : ‚àÄ·∂† a in f, p a ‚Üí u a = v a) :
  bliminf u f p = bliminf v f p :=
@blimsup_congr Œ±·µí·µà _ _ _ _ _ _ h

lemma liminf_congr {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (h : ‚àÄ·∂† a in f, u a = v a) : liminf u f = liminf v f :=
@limsup_congr Œ≤·µí·µà _ _ _ _ _ h

lemma limsup_const {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} [ne_bot f]
  (b : Œ≤) : limsup (Œª x, b) f = b :=
by simpa only [limsup_eq, eventually_const] using cInf_Ici

lemma liminf_const {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} [ne_bot f]
  (b : Œ≤) : liminf (Œª x, b) f = b :=
@limsup_const Œ≤·µí·µà Œ± _ f _ b


end conditionally_complete_lattice

section complete_lattice
variables [complete_lattice Œ±]

@[simp] theorem Limsup_bot : Limsup (‚ä• : filter Œ±) = ‚ä• :=
bot_unique $ Inf_le $ by simp

@[simp] theorem Liminf_bot : Liminf (‚ä• : filter Œ±) = ‚ä§ :=
top_unique $ le_Sup $ by simp

@[simp] theorem Limsup_top : Limsup (‚ä§ : filter Œ±) = ‚ä§ :=
top_unique $ le_Inf $
  by simp [eq_univ_iff_forall]; exact assume b hb, (top_unique $ hb _)

@[simp] theorem Liminf_top : Liminf (‚ä§ : filter Œ±) = ‚ä• :=
bot_unique $ Sup_le $
  by simp [eq_univ_iff_forall]; exact assume b hb, (bot_unique $ hb _)

@[simp] lemma blimsup_false {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} :
  blimsup u f (Œª x, false) = ‚ä• :=
by simp [blimsup_eq]

@[simp] lemma bliminf_false {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} :
  bliminf u f (Œª x, false) = ‚ä§ :=
by simp [bliminf_eq]

/-- Same as limsup_const applied to `‚ä•` but without the `ne_bot f` assumption -/
lemma limsup_const_bot {f : filter Œ≤} : limsup (Œª x : Œ≤, (‚ä• : Œ±)) f = (‚ä• : Œ±) :=
begin
  rw [limsup_eq, eq_bot_iff],
  exact Inf_le (eventually_of_forall (Œª x, le_rfl)),
end

/-- Same as limsup_const applied to `‚ä§` but without the `ne_bot f` assumption -/
lemma liminf_const_top {f : filter Œ≤} : liminf (Œª x : Œ≤, (‚ä§ : Œ±)) f = (‚ä§ : Œ±) :=
@limsup_const_bot Œ±·µí·µà Œ≤ _ _

theorem has_basis.Limsup_eq_infi_Sup {Œπ} {p : Œπ ‚Üí Prop} {s} {f : filter Œ±} (h : f.has_basis p s) :
  Limsup f = ‚®Ö i (hi : p i), Sup (s i) :=
le_antisymm
  (le_infi‚ÇÇ $ Œª i hi, Inf_le $ h.eventually_iff.2 ‚ü®i, hi, Œª x, le_Sup‚ü©)
  (le_Inf $ assume a ha, let ‚ü®i, hi, ha‚ü© := h.eventually_iff.1 ha in
    infi‚ÇÇ_le_of_le _ hi $ Sup_le ha)

theorem has_basis.Liminf_eq_supr_Inf {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí set Œ±} {f : filter Œ±}
  (h : f.has_basis p s) : Liminf f = ‚®Ü i (hi : p i), Inf (s i) :=
@has_basis.Limsup_eq_infi_Sup Œ±·µí·µà _ _ _ _ _ h

theorem Limsup_eq_infi_Sup {f : filter Œ±} : Limsup f = ‚®Ö s ‚àà f, Sup s :=
f.basis_sets.Limsup_eq_infi_Sup

theorem Liminf_eq_supr_Inf {f : filter Œ±} : Liminf f = ‚®Ü s ‚àà f, Inf s :=
@Limsup_eq_infi_Sup Œ±·µí·µà _ _

theorem limsup_le_supr {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} : limsup u f ‚â§ ‚®Ü n, u n :=
limsup_le_of_le (by is_bounded_default) (eventually_of_forall (le_supr u))

theorem infi_le_liminf {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} : (‚®Ö n, u n) ‚â§ liminf u f :=
le_liminf_of_le (by is_bounded_default) (eventually_of_forall (infi_le u))

/-- In a complete lattice, the limsup of a function is the infimum over sets `s` in the filter
of the supremum of the function over `s` -/
theorem limsup_eq_infi_supr {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} : limsup u f = ‚®Ö s ‚àà f, ‚®Ü a ‚àà s, u a :=
(f.basis_sets.map u).Limsup_eq_infi_Sup.trans $
  by simp only [Sup_image, id]

lemma limsup_eq_infi_supr_of_nat {u : ‚Ñï ‚Üí Œ±} : limsup u at_top = ‚®Ö n : ‚Ñï, ‚®Ü i ‚â• n, u i :=
(at_top_basis.map u).Limsup_eq_infi_Sup.trans $
  by simp only [Sup_image, infi_const]; refl

lemma limsup_eq_infi_supr_of_nat' {u : ‚Ñï ‚Üí Œ±} : limsup u at_top = ‚®Ö n : ‚Ñï, ‚®Ü i : ‚Ñï, u (i + n) :=
by simp only [limsup_eq_infi_supr_of_nat, supr_ge_eq_supr_nat_add]

theorem has_basis.limsup_eq_infi_supr {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí set Œ≤} {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±}
  (h : f.has_basis p s) : limsup u f = ‚®Ö i (hi : p i), ‚®Ü a ‚àà s i, u a :=
(h.map u).Limsup_eq_infi_Sup.trans $ by simp only [Sup_image, id]

lemma blimsup_congr' {f : filter Œ≤} {p q : Œ≤ ‚Üí Prop} {u : Œ≤ ‚Üí Œ±}
  (h : ‚àÄ·∂† x in f, u x ‚â† ‚ä• ‚Üí (p x ‚Üî q x)) :
  blimsup u f p = blimsup u f q :=
begin
  simp only [blimsup_eq],
  congr,
  ext a,
  refine eventually_congr (h.mono $ Œª b hb, _),
  cases eq_or_ne (u b) ‚ä• with hu hu, { simp [hu], },
  rw hb hu,
end

lemma bliminf_congr' {f : filter Œ≤} {p q : Œ≤ ‚Üí Prop} {u : Œ≤ ‚Üí Œ±}
  (h : ‚àÄ·∂† x in f, u x ‚â† ‚ä§ ‚Üí (p x ‚Üî q x)) :
  bliminf u f p = bliminf u f q :=
@blimsup_congr' Œ±·µí·µà Œ≤ _ _ _ _ _ h

lemma blimsup_eq_infi_bsupr {f : filter Œ≤} {p : Œ≤ ‚Üí Prop} {u : Œ≤ ‚Üí Œ±} :
  blimsup u f p = ‚®Ö s ‚àà f, ‚®Ü b (hb : p b ‚àß b ‚àà s), u b :=
begin
  refine le_antisymm (Inf_le_Inf _) (infi_le_iff.mpr $ Œª a ha, le_Inf_iff.mpr $ Œª a' ha', _),
  { rintros - ‚ü®s, rfl‚ü©,
    simp only [mem_set_of_eq, le_infi_iff],
    conv { congr, funext, rw imp.swap, },
    refine eventually_imp_distrib_left.mpr (Œª h, eventually_iff_exists_mem.2 ‚ü®s, h, Œª x h‚ÇÅ h‚ÇÇ, _‚ü©),
    exact @le_supr‚ÇÇ Œ± Œ≤ (Œª b, p b ‚àß b ‚àà s) _ (Œª b hb, u b) x ‚ü®h‚ÇÇ, h‚ÇÅ‚ü©, },
  { obtain ‚ü®s, hs, hs'‚ü© := eventually_iff_exists_mem.mp ha',
    simp_rw imp.swap at hs',
    exact (le_infi_iff.mp (ha s) hs).trans (by simpa only [supr‚ÇÇ_le_iff, and_imp]), },
end

lemma blimsup_eq_infi_bsupr_of_nat {p : ‚Ñï ‚Üí Prop} {u : ‚Ñï ‚Üí Œ±} :
  blimsup u at_top p = ‚®Ö i, ‚®Ü j (hj : p j ‚àß i ‚â§ j), u j :=
by simp only [blimsup_eq_limsup_subtype, mem_preimage, mem_Ici, function.comp_app, cinfi_pos,
  supr_subtype, (at_top_basis.comap (coe : {x | p x} ‚Üí ‚Ñï)).limsup_eq_infi_supr, mem_set_of_eq,
  subtype.coe_mk, supr_and]

/-- In a complete lattice, the liminf of a function is the infimum over sets `s` in the filter
of the supremum of the function over `s` -/
theorem liminf_eq_supr_infi {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} : liminf u f = ‚®Ü s ‚àà f, ‚®Ö a ‚àà s, u a :=
@limsup_eq_infi_supr Œ±·µí·µà Œ≤ _ _ _

lemma liminf_eq_supr_infi_of_nat {u : ‚Ñï ‚Üí Œ±} : liminf u at_top = ‚®Ü n : ‚Ñï, ‚®Ö i ‚â• n, u i :=
@limsup_eq_infi_supr_of_nat Œ±·µí·µà _ u

lemma liminf_eq_supr_infi_of_nat' {u : ‚Ñï ‚Üí Œ±} : liminf u at_top = ‚®Ü n : ‚Ñï, ‚®Ö i : ‚Ñï, u (i + n) :=
@limsup_eq_infi_supr_of_nat' Œ±·µí·µà _ _

theorem has_basis.liminf_eq_supr_infi {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí set Œ≤} {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±}
  (h : f.has_basis p s) : liminf u f = ‚®Ü i (hi : p i), ‚®Ö a ‚àà s i, u a :=
@has_basis.limsup_eq_infi_supr Œ±·µí·µà _ _ _ _ _ _ _ h

lemma bliminf_eq_supr_binfi {f : filter Œ≤} {p : Œ≤ ‚Üí Prop} {u : Œ≤ ‚Üí Œ±} :
  bliminf u f p = ‚®Ü s ‚àà f, ‚®Ö b (hb : p b ‚àß b ‚àà s), u b :=
@blimsup_eq_infi_bsupr Œ±·µí·µà Œ≤ _ f p u

lemma bliminf_eq_supr_binfi_of_nat {p : ‚Ñï ‚Üí Prop} {u : ‚Ñï ‚Üí Œ±} :
  bliminf u at_top p = ‚®Ü i, ‚®Ö j (hj : p j ‚àß i ‚â§ j), u j :=
@blimsup_eq_infi_bsupr_of_nat Œ±·µí·µà _ p u

lemma limsup_eq_Inf_Sup {Œπ R : Type*} (F : filter Œπ) [complete_lattice R] (a : Œπ ‚Üí R) :
  limsup a F = Inf ((Œª I, Sup (a '' I)) '' F.sets) :=
begin
  refine le_antisymm _ _,
  { rw limsup_eq,
    refine Inf_le_Inf (Œª x hx, _),
    rcases (mem_image _ F.sets x).mp hx with ‚ü®I, ‚ü®I_mem_F, hI‚ü©‚ü©,
    filter_upwards [I_mem_F] with i hi,
    exact hI ‚ñ∏ le_Sup (mem_image_of_mem _ hi), },
  { refine le_Inf_iff.mpr (Œª b hb, Inf_le_of_le (mem_image_of_mem _ $ filter.mem_sets.mpr hb)
      $ Sup_le _),
    rintros _ ‚ü®_, h, rfl‚ü©,
    exact h, },
end

lemma liminf_eq_Sup_Inf {Œπ R : Type*} (F : filter Œπ) [complete_lattice R] (a : Œπ ‚Üí R) :
  liminf a F = Sup ((Œª I, Inf (a '' I)) '' F.sets) :=
@filter.limsup_eq_Inf_Sup Œπ (order_dual R) _ _ a

@[simp] lemma liminf_nat_add (f : ‚Ñï ‚Üí Œ±) (k : ‚Ñï) :
  liminf (Œª i, f (i + k)) at_top = liminf f at_top :=
by { simp_rw liminf_eq_supr_infi_of_nat, exact supr_infi_ge_nat_add f k }

@[simp] lemma limsup_nat_add (f : ‚Ñï ‚Üí Œ±) (k : ‚Ñï) :
  limsup (Œª i, f (i + k)) at_top = limsup f at_top :=
@liminf_nat_add Œ±·µí·µà _ f k

lemma liminf_le_of_frequently_le' {Œ± Œ≤} [complete_lattice Œ≤]
  {f : filter Œ±} {u : Œ± ‚Üí Œ≤} {x : Œ≤} (h : ‚àÉ·∂† a in f, u a ‚â§ x) :
  liminf u f ‚â§ x :=
begin
  rw liminf_eq,
  refine Sup_le (Œª b hb, _),
  have hbx : ‚àÉ·∂† a in f, b ‚â§ x,
  { revert h,
    rw [‚Üênot_imp_not, not_frequently, not_frequently],
    exact Œª h, hb.mp (h.mono (Œª a hbx hba hax, hbx (hba.trans hax))), },
  exact hbx.exists.some_spec,
end

lemma le_limsup_of_frequently_le' {Œ± Œ≤} [complete_lattice Œ≤]
  {f : filter Œ±} {u : Œ± ‚Üí Œ≤} {x : Œ≤} (h : ‚àÉ·∂† a in f, x ‚â§ u a) :
  x ‚â§ limsup u f :=
@liminf_le_of_frequently_le' _ Œ≤·µí·µà _ _ _ _ h

/-- If `f : Œ± ‚Üí Œ±` is a morphism of complete lattices, then the limsup of its iterates of any
`a : Œ±` is a fixed point. -/
@[simp] lemma complete_lattice_hom.apply_limsup_iterate (f : complete_lattice_hom Œ± Œ±) (a : Œ±) :
  f (limsup (Œª n, f^[n] a) at_top) = limsup (Œª n, f^[n] a) at_top :=
begin
  rw [limsup_eq_infi_supr_of_nat', map_infi],
  simp_rw [_root_.map_supr, ‚Üê function.comp_apply f, ‚Üê function.iterate_succ' f, ‚Üê nat.add_succ],
  conv_rhs { rw infi_split _ ((<) (0 : ‚Ñï)), },
  simp only [not_lt, le_zero_iff, infi_infi_eq_left, add_zero, infi_nat_gt_zero_eq, left_eq_inf],
  refine (infi_le (Œª i, ‚®Ü j, (f^[j + (i + 1)]) a) 0).trans _,
  simp only [zero_add, function.comp_app, supr_le_iff],
  exact Œª i, le_supr (Œª i, (f^[i] a)) (i + 1),
end

/-- If `f : Œ± ‚Üí Œ±` is a morphism of complete lattices, then the liminf of its iterates of any
`a : Œ±` is a fixed point. -/
lemma complete_lattice_hom.apply_liminf_iterate (f : complete_lattice_hom Œ± Œ±) (a : Œ±) :
  f (liminf (Œª n, f^[n] a) at_top) = liminf (Œª n, f^[n] a) at_top :=
(complete_lattice_hom.dual f).apply_limsup_iterate _
variables {f g : filter Œ≤} {p q : Œ≤ ‚Üí Prop} {u v : Œ≤ ‚Üí Œ±}

lemma blimsup_mono (h : ‚àÄ x, p x ‚Üí q x) :
  blimsup u f p ‚â§ blimsup u f q :=
Inf_le_Inf $ Œª a ha, ha.mono $ by tauto

lemma bliminf_antitone (h : ‚àÄ x, p x ‚Üí q x) :
  bliminf u f q ‚â§ bliminf u f p :=
Sup_le_Sup $ Œª a ha, ha.mono $ by tauto

lemma mono_blimsup' (h : ‚àÄ·∂† x in f, p x ‚Üí u x ‚â§ v x) :
  blimsup u f p ‚â§ blimsup v f p :=
Inf_le_Inf $ Œª a ha, (ha.and h).mono $ Œª x hx hx', (hx.2 hx').trans (hx.1 hx')

lemma mono_blimsup (h : ‚àÄ x, p x ‚Üí u x ‚â§ v x) :
  blimsup u f p ‚â§ blimsup v f p :=
mono_blimsup' $ eventually_of_forall h

lemma mono_bliminf' (h : ‚àÄ·∂† x in f, p x ‚Üí u x ‚â§ v x) :
  bliminf u f p ‚â§ bliminf v f p :=
Sup_le_Sup $ Œª a ha, (ha.and h).mono $ Œª x hx hx', (hx.1 hx').trans (hx.2 hx')

lemma mono_bliminf (h : ‚àÄ x, p x ‚Üí u x ‚â§ v x) :
  bliminf u f p ‚â§ bliminf v f p :=
mono_bliminf' $ eventually_of_forall h

lemma bliminf_antitone_filter (h : f ‚â§ g) :
  bliminf u g p ‚â§ bliminf u f p :=
Sup_le_Sup $ Œª a ha, ha.filter_mono h

lemma blimsup_monotone_filter (h : f ‚â§ g) :
  blimsup u f p ‚â§ blimsup u g p :=
Inf_le_Inf $ Œª a ha, ha.filter_mono h

@[simp] lemma blimsup_and_le_inf :
  blimsup u f (Œª x, p x ‚àß q x) ‚â§ blimsup u f p ‚äì blimsup u f q :=
le_inf (blimsup_mono $ by tauto) (blimsup_mono $ by tauto)

@[simp] lemma bliminf_sup_le_and :
  bliminf u f p ‚äî bliminf u f q ‚â§ bliminf u f (Œª x, p x ‚àß q x) :=
@blimsup_and_le_inf Œ±·µí·µà Œ≤ _ f p q u

/-- See also `filter.blimsup_or_eq_sup`. -/
@[simp] lemma blimsup_sup_le_or :
  blimsup u f p ‚äî blimsup u f q ‚â§ blimsup u f (Œª x, p x ‚à® q x) :=
sup_le (blimsup_mono $ by tauto) (blimsup_mono $ by tauto)

/-- See also `filter.bliminf_or_eq_inf`. -/
@[simp] lemma bliminf_or_le_inf :
  bliminf u f (Œª x, p x ‚à® q x) ‚â§ bliminf u f p ‚äì bliminf u f q :=
@blimsup_sup_le_or Œ±·µí·µà Œ≤ _ f p q u

lemma order_iso.apply_blimsup [complete_lattice Œ≥] (e : Œ± ‚âÉo Œ≥) :
  e (blimsup u f p) = blimsup (e ‚àò u) f p :=
begin
  simp only [blimsup_eq, map_Inf, function.comp_app],
  congr,
  ext c,
  obtain ‚ü®a, rfl‚ü© := e.surjective c,
  simp,
end

lemma order_iso.apply_bliminf [complete_lattice Œ≥] (e : Œ± ‚âÉo Œ≥) :
  e (bliminf u f p) = bliminf (e ‚àò u) f p :=
@order_iso.apply_blimsup Œ±·µí·µà Œ≤ Œ≥·µí·µà _ f p u _ e.dual

lemma Sup_hom.apply_blimsup_le [complete_lattice Œ≥] (g : Sup_hom Œ± Œ≥) :
  g (blimsup u f p) ‚â§ blimsup (g ‚àò u) f p :=
begin
  simp only [blimsup_eq_infi_bsupr],
  refine ((order_hom_class.mono g).map_infi‚ÇÇ_le _).trans _,
  simp only [_root_.map_supr],
end

lemma Inf_hom.le_apply_bliminf [complete_lattice Œ≥] (g : Inf_hom Œ± Œ≥) :
  bliminf (g ‚àò u) f p ‚â§ g (bliminf u f p) :=
@Sup_hom.apply_blimsup_le Œ±·µí·µà Œ≤ Œ≥·µí·µà _ f p u _ g.dual

end complete_lattice

section complete_distrib_lattice

variables [complete_distrib_lattice Œ±] {f : filter Œ≤} {p q : Œ≤ ‚Üí Prop} {u : Œ≤ ‚Üí Œ±}

@[simp] lemma blimsup_or_eq_sup :
  blimsup u f (Œª x, p x ‚à® q x) = blimsup u f p ‚äî blimsup u f q :=
begin
  refine le_antisymm _ blimsup_sup_le_or,
  simp only [blimsup_eq, Inf_sup_eq, sup_Inf_eq, le_infi‚ÇÇ_iff, mem_set_of_eq],
  refine Œª a' ha' a ha, Inf_le ((ha.and ha').mono $ Œª b h hb, _),
  exact or.elim hb (Œª hb, le_sup_of_le_left $ h.1 hb) (Œª hb, le_sup_of_le_right $ h.2 hb),
end

@[simp] lemma bliminf_or_eq_inf :
  bliminf u f (Œª x, p x ‚à® q x) = bliminf u f p ‚äì bliminf u f q :=
@blimsup_or_eq_sup Œ±·µí·µà Œ≤ _ f p q u

lemma sup_limsup [ne_bot f] (a : Œ±) :
  a ‚äî limsup u f = limsup (Œª x, a ‚äî u x) f :=
begin
  simp only [limsup_eq_infi_supr, supr_sup_eq, sup_binfi_eq],
  congr, ext s, congr, ext hs, congr,
  exact (bsupr_const (nonempty_of_mem hs)).symm,
end

lemma inf_liminf [ne_bot f] (a : Œ±) :
  a ‚äì liminf u f = liminf (Œª x, a ‚äì u x) f :=
@sup_limsup Œ±·µí·µà Œ≤ _ f _ _ _

lemma sup_liminf (a : Œ±) :
  a ‚äî liminf u f = liminf (Œª x, a ‚äî u x) f :=
begin
  simp only [liminf_eq_supr_infi],
  rw [sup_comm, bsupr_sup (‚ü®univ, univ_mem‚ü© : ‚àÉ (i : set Œ≤), i ‚àà f)],
  simp_rw [binfi_sup_eq, @sup_comm _ _ a],
end

lemma inf_limsup (a : Œ±) :
  a ‚äì limsup u f = limsup (Œª x, a ‚äì u x) f :=
@sup_liminf Œ±·µí·µà Œ≤ _ f _ _

end complete_distrib_lattice

section complete_boolean_algebra

variables [complete_boolean_algebra Œ±] (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±)

lemma limsup_compl :
  (limsup u f)·∂ú = liminf (compl ‚àò u) f :=
by simp only [limsup_eq_infi_supr, liminf_eq_supr_infi, compl_infi, compl_supr]

lemma liminf_compl :
  (liminf u f)·∂ú = limsup (compl ‚àò u) f :=
by simp only [limsup_eq_infi_supr, liminf_eq_supr_infi, compl_infi, compl_supr]

lemma limsup_sdiff (a : Œ±) :
  (limsup u f) \ a = limsup (Œª b, (u b) \ a) f :=
begin
  simp only [limsup_eq_infi_supr, sdiff_eq],
  rw binfi_inf (‚ü®univ, univ_mem‚ü© : ‚àÉ (i : set Œ≤), i ‚àà f),
  simp_rw [inf_comm, inf_bsupr_eq, inf_comm],
end

lemma liminf_sdiff [ne_bot f] (a : Œ±) :
  (liminf u f) \ a = liminf (Œª b, (u b) \ a) f :=
by simp only [sdiff_eq, @inf_comm _ _ _ a·∂ú, inf_liminf]

lemma sdiff_limsup [ne_bot f] (a : Œ±) :
  a \ limsup u f = liminf (Œª b, a \ u b) f :=
begin
  rw ‚Üê compl_inj_iff,
  simp only [sdiff_eq, liminf_compl, (‚àò), compl_inf, compl_compl, sup_limsup],
end

lemma sdiff_liminf (a : Œ±) :
  a \ liminf u f = limsup (Œª b, a \ u b) f :=
begin
  rw ‚Üê compl_inj_iff,
  simp only [sdiff_eq, limsup_compl, (‚àò), compl_inf, compl_compl, sup_liminf],
end

end complete_boolean_algebra

section set_lattice

variables {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí set Œ±}

lemma cofinite.blimsup_set_eq :
  blimsup s cofinite p = { x | { n | p n ‚àß x ‚àà s n }.infinite } :=
begin
  simp only [blimsup_eq, le_eq_subset, eventually_cofinite, not_forall, Inf_eq_sInter, exists_prop],
  ext x,
  refine ‚ü®Œª h, _, Œª hx t h, _‚ü©;
  contrapose! h,
  { simp only [mem_sInter, mem_set_of_eq, not_forall, exists_prop],
    exact ‚ü®{x}·∂ú, by simpa using h, by simp‚ü©, },
  { exact hx.mono (Œª i hi, ‚ü®hi.1, Œª hit, h (hit hi.2)‚ü©), },
end

lemma cofinite.bliminf_set_eq :
  bliminf s cofinite p = { x | { n | p n ‚àß x ‚àâ s n }.finite } :=
begin
  rw ‚Üê compl_inj_iff,
  simpa only [bliminf_eq_supr_binfi, compl_infi, compl_supr, ‚Üê blimsup_eq_infi_bsupr,
    cofinite.blimsup_set_eq],
end

/-- In other words, `limsup cofinite s` is the set of elements lying inside the family `s`
infinitely often. -/
lemma cofinite.limsup_set_eq :
  limsup s cofinite = { x | { n | x ‚àà s n }.infinite } :=
by simp only [‚Üê cofinite.blimsup_true s, cofinite.blimsup_set_eq, true_and]

/-- In other words, `liminf cofinite s` is the set of elements lying outside the family `s`
finitely often. -/
lemma cofinite.liminf_set_eq :
  liminf s cofinite = { x | { n | x ‚àâ s n }.finite } :=
by simp only [‚Üê cofinite.bliminf_true s, cofinite.bliminf_set_eq, true_and]

lemma exists_forall_mem_of_has_basis_mem_blimsup
  {l : filter Œ≤} {b : Œπ ‚Üí set Œ≤} {q : Œπ ‚Üí Prop} (hl : l.has_basis q b)
  {u : Œ≤ ‚Üí set Œ±} {p : Œ≤ ‚Üí Prop} {x : Œ±} (hx : x ‚àà blimsup u l p) :
  ‚àÉ f : {i | q i} ‚Üí Œ≤, ‚àÄ i, x ‚àà u (f i) ‚àß p (f i) ‚àß f i ‚àà b i :=
begin
  rw blimsup_eq_infi_bsupr at hx,
  simp only [supr_eq_Union, infi_eq_Inter, mem_Inter, mem_Union, exists_prop] at hx,
  choose g hg hg' using hx,
  refine ‚ü®Œª (i : {i | q i}), g (b i) (hl.mem_of_mem i.2), Œª i, ‚ü®_, _‚ü©‚ü©,
  { exact hg' (b i) (hl.mem_of_mem i.2), },
  { exact hg (b i) (hl.mem_of_mem i.2), },
end

lemma exists_forall_mem_of_has_basis_mem_blimsup'
  {l : filter Œ≤} {b : Œπ ‚Üí set Œ≤} (hl : l.has_basis (Œª _, true) b)
  {u : Œ≤ ‚Üí set Œ±} {p : Œ≤ ‚Üí Prop} {x : Œ±} (hx : x ‚àà blimsup u l p) :
  ‚àÉ f : Œπ ‚Üí Œ≤, ‚àÄ i, x ‚àà u (f i) ‚àß p (f i) ‚àß f i ‚àà b i :=
begin
  obtain ‚ü®f, hf‚ü© := exists_forall_mem_of_has_basis_mem_blimsup hl hx,
  exact ‚ü®Œª i, f ‚ü®i, trivial‚ü©, Œª i, hf ‚ü®i, trivial‚ü©‚ü©,
end

end set_lattice

section conditionally_complete_linear_order

lemma frequently_lt_of_lt_Limsup {f : filter Œ±} [conditionally_complete_linear_order Œ±] {a : Œ±}
  (hf : f.is_cobounded (‚â§) . is_bounded_default) (h : a < Limsup f) : ‚àÉ·∂† n in f, a < n :=
begin
  contrapose! h,
  simp only [not_frequently, not_lt] at h,
  exact Limsup_le_of_le hf h,
end

lemma frequently_lt_of_Liminf_lt {f : filter Œ±} [conditionally_complete_linear_order Œ±] {a : Œ±}
  (hf : f.is_cobounded (‚â•) . is_bounded_default) (h : Liminf f < a) : ‚àÉ·∂† n in f, n < a :=
@frequently_lt_of_lt_Limsup (order_dual Œ±) f _ a hf h

lemma eventually_lt_of_lt_liminf {f : filter Œ±} [conditionally_complete_linear_order Œ≤]
  {u : Œ± ‚Üí Œ≤} {b : Œ≤} (h : b < liminf u f) (hu : f.is_bounded_under (‚â•) u . is_bounded_default) :
  ‚àÄ·∂† a in f, b < u a :=
begin
  obtain ‚ü®c, hc, hbc‚ü© : ‚àÉ (c : Œ≤) (hc : c ‚àà {c : Œ≤ | ‚àÄ·∂† (n : Œ±) in f, c ‚â§ u n}), b < c :=
    exists_lt_of_lt_cSup hu h,
  exact hc.mono (Œª x hx, lt_of_lt_of_le hbc hx)
end

lemma eventually_lt_of_limsup_lt {f : filter Œ±} [conditionally_complete_linear_order Œ≤]
  {u : Œ± ‚Üí Œ≤} {b : Œ≤} (h : limsup u f < b) (hu : f.is_bounded_under (‚â§) u . is_bounded_default) :
  ‚àÄ·∂† a in f, u a < b :=
@eventually_lt_of_lt_liminf _ Œ≤·µí·µà _ _ _ _ h hu

lemma le_limsup_of_frequently_le {Œ± Œ≤} [conditionally_complete_linear_order Œ≤] {f : filter Œ±}
  {u : Œ± ‚Üí Œ≤}  {b : Œ≤} (hu_le : ‚àÉ·∂† x in f, b ‚â§ u x)
  (hu : f.is_bounded_under (‚â§) u . is_bounded_default) :
  b ‚â§ limsup u f :=
begin
  revert hu_le,
  rw [‚Üênot_imp_not, not_frequently],
  simp_rw ‚Üêlt_iff_not_ge,
  exact Œª h, eventually_lt_of_limsup_lt h hu,
end

lemma liminf_le_of_frequently_le  {Œ± Œ≤} [conditionally_complete_linear_order Œ≤] {f : filter Œ±}
  {u : Œ± ‚Üí Œ≤}  {b : Œ≤} (hu_le : ‚àÉ·∂† x in f, u x ‚â§ b)
  (hu : f.is_bounded_under (‚â•) u . is_bounded_default) :
  liminf u f ‚â§ b :=
@le_limsup_of_frequently_le _ Œ≤·µí·µà _ f u b hu_le hu

lemma frequently_lt_of_lt_limsup {Œ± Œ≤} [conditionally_complete_linear_order Œ≤] {f : filter Œ±}
  {u : Œ± ‚Üí Œ≤}  {b : Œ≤}
  (hu : f.is_cobounded_under (‚â§) u . is_bounded_default) (h : b < limsup u f) :
  ‚àÉ·∂† x in f, b < u x :=
begin
  contrapose! h,
  apply Limsup_le_of_le hu,
  simpa using h,
end

lemma frequently_lt_of_liminf_lt {Œ± Œ≤} [conditionally_complete_linear_order Œ≤] {f : filter Œ±}
  {u : Œ± ‚Üí Œ≤}  {b : Œ≤}
  (hu : f.is_cobounded_under (‚â•) u . is_bounded_default) (h : liminf u f < b) :
  ‚àÉ·∂† x in f, u x < b :=
@frequently_lt_of_lt_limsup _ Œ≤·µí·µà _ f u b hu h

end conditionally_complete_linear_order

end filter

section order
open filter

lemma monotone.is_bounded_under_le_comp [nonempty Œ≤] [linear_order Œ≤] [preorder Œ≥]
  [no_max_order Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {l : filter Œ±} (hg : monotone g)
  (hg' : tendsto g at_top at_top) :
  is_bounded_under (‚â§) l (g ‚àò f) ‚Üî is_bounded_under (‚â§) l f :=
begin
  refine ‚ü®_, Œª h, h.is_bounded_under hg‚ü©,
  rintro ‚ü®c, hc‚ü©, rw eventually_map at hc,
  obtain ‚ü®b, hb‚ü© : ‚àÉ b, ‚àÄ a ‚â• b, c < g a := eventually_at_top.1 (hg'.eventually_gt_at_top c),
  exact ‚ü®b, hc.mono $ Œª x hx, not_lt.1 (Œª h, (hb _ h.le).not_le hx)‚ü©
end

lemma monotone.is_bounded_under_ge_comp [nonempty Œ≤] [linear_order Œ≤] [preorder Œ≥]
  [no_min_order Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {l : filter Œ±} (hg : monotone g)
  (hg' : tendsto g at_bot at_bot) :
  is_bounded_under (‚â•) l (g ‚àò f) ‚Üî is_bounded_under (‚â•) l f :=
hg.dual.is_bounded_under_le_comp hg'

lemma antitone.is_bounded_under_le_comp [nonempty Œ≤] [linear_order Œ≤] [preorder Œ≥]
  [no_max_order Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {l : filter Œ±} (hg : antitone g)
  (hg' : tendsto g at_bot at_top) :
  is_bounded_under (‚â§) l (g ‚àò f) ‚Üî is_bounded_under (‚â•) l f :=
hg.dual_right.is_bounded_under_ge_comp hg'

lemma antitone.is_bounded_under_ge_comp [nonempty Œ≤] [linear_order Œ≤] [preorder Œ≥]
  [no_min_order Œ≥] {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {l : filter Œ±} (hg : antitone g)
  (hg' : tendsto g at_top at_bot) :
  is_bounded_under (‚â•) l (g ‚àò f) ‚Üî is_bounded_under (‚â§) l f :=
hg.dual_right.is_bounded_under_le_comp hg'

lemma galois_connection.l_limsup_le [conditionally_complete_lattice Œ≤]
  [conditionally_complete_lattice Œ≥] {f : filter Œ±} {v : Œ± ‚Üí Œ≤}
  {l : Œ≤ ‚Üí Œ≥} {u : Œ≥ ‚Üí Œ≤} (gc : galois_connection l u)
  (hlv : f.is_bounded_under (‚â§) (Œª x, l (v x)) . is_bounded_default)
  (hv_co : f.is_cobounded_under (‚â§) v . is_bounded_default) :
  l (limsup v f) ‚â§ limsup (Œª x, l (v x)) f :=
begin
  refine le_Limsup_of_le hlv (Œª c hc, _),
  rw filter.eventually_map at hc,
  simp_rw (gc _ _) at hc ‚ä¢,
  exact Limsup_le_of_le hv_co hc,
end

lemma order_iso.limsup_apply {Œ≥} [conditionally_complete_lattice Œ≤]
  [conditionally_complete_lattice Œ≥] {f : filter Œ±} {u : Œ± ‚Üí Œ≤} (g : Œ≤ ‚âÉo Œ≥)
  (hu : f.is_bounded_under (‚â§) u . is_bounded_default)
  (hu_co : f.is_cobounded_under (‚â§) u . is_bounded_default)
  (hgu : f.is_bounded_under (‚â§) (Œª x, g (u x)) . is_bounded_default)
  (hgu_co : f.is_cobounded_under (‚â§) (Œª x, g (u x)) . is_bounded_default) :
  g (limsup u f) = limsup (Œª x, g (u x)) f :=
begin
  refine le_antisymm (g.to_galois_connection.l_limsup_le hgu hu_co) _,
  rw [‚Üê(g.symm.symm_apply_apply $ limsup (Œª x, g (u x)) f), g.symm_symm],
  refine g.monotone _,
  have hf : u = Œª i, g.symm (g (u i)), from funext (Œª i, (g.symm_apply_apply (u i)).symm),
  nth_rewrite 0 hf,
  refine g.symm.to_galois_connection.l_limsup_le _ hgu_co,
  simp_rw g.symm_apply_apply,
  exact hu,
end

lemma order_iso.liminf_apply {Œ≥} [conditionally_complete_lattice Œ≤]
  [conditionally_complete_lattice Œ≥] {f : filter Œ±} {u : Œ± ‚Üí Œ≤} (g : Œ≤ ‚âÉo Œ≥)
  (hu : f.is_bounded_under (‚â•) u . is_bounded_default)
  (hu_co : f.is_cobounded_under (‚â•) u . is_bounded_default)
  (hgu : f.is_bounded_under (‚â•) (Œª x, g (u x)) . is_bounded_default)
  (hgu_co : f.is_cobounded_under (‚â•) (Œª x, g (u x)) . is_bounded_default) :
  g (liminf u f) = liminf (Œª x, g (u x)) f :=
@order_iso.limsup_apply Œ± Œ≤·µí·µà Œ≥·µí·µà _ _ f u g.dual hu hu_co hgu hgu_co

end order
