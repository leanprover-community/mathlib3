/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Johannes H√∂lzl, R√©my Degenne
-/
import order.filter.partial
import order.filter.at_top_bot

/-!
# liminfs and limsups of functions and filters

Defines the Liminf/Limsup of a function taking values in a conditionally complete lattice, with
respect to an arbitrary filter.

We define `f.Limsup` (`f.Liminf`) where `f` is a filter taking values in a conditionally complete
lattice. `f.Limsup` is the smallest element `a` such that, eventually, `u ‚â§ a` (and vice versa for
`f.Liminf`). To work with the Limsup along a function `u` use `(f.map u).Limsup`.

Usually, one defines the Limsup as `Inf (Sup s)` where the Inf is taken over all sets in the filter.
For instance, in ‚Ñï along a function `u`, this is `Inf_n (Sup_{k ‚â• n} u k)` (and the latter quantity
decreases with `n`, so this is in fact a limit.). There is however a difficulty: it is well possible
that `u` is not bounded on the whole space, only eventually (think of `Limsup (Œªx, 1/x)` on ‚Ñù. Then
there is no guarantee that the quantity above really decreases (the value of the `Sup` beforehand is
not really well defined, as one can not use ‚àû), so that the Inf could be anything. So one can not
use this `Inf Sup ...` definition in conditionally complete lattices, and one has to use a less
tractable definition.

In conditionally complete lattices, the definition is only useful for filters which are eventually
bounded above (otherwise, the Limsup would morally be +‚àû, which does not belong to the space) and
which are frequently bounded below (otherwise, the Limsup would morally be -‚àû, which is not in the
space either). We start with definitions of these concepts for arbitrary filters, before turning to
the definitions of Limsup and Liminf.

In complete lattices, however, it coincides with the `Inf Sup` definition.
-/

open filter set
open_locale filter

variables {Œ± Œ≤ Œπ : Type*}
namespace filter

section relation

/-- `f.is_bounded (‚â∫)`: the filter `f` is eventually bounded w.r.t. the relation `‚â∫`, i.e.
eventually, it is bounded by some uniform bound.
`r` will be usually instantiated with `‚â§` or `‚â•`. -/
def is_bounded (r : Œ± ‚Üí Œ± ‚Üí Prop) (f : filter Œ±) := ‚àÉ b, ‚àÄ·∂† x in f, r x b

/-- `f.is_bounded_under (‚â∫) u`: the image of the filter `f` under `u` is eventually bounded w.r.t.
the relation `‚â∫`, i.e. eventually, it is bounded by some uniform bound. -/
def is_bounded_under (r : Œ± ‚Üí Œ± ‚Üí Prop) (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±) := (f.map u).is_bounded r

variables {r : Œ± ‚Üí Œ± ‚Üí Prop} {f g : filter Œ±}

/-- `f` is eventually bounded if and only if, there exists an admissible set on which it is
bounded. -/
lemma is_bounded_iff : f.is_bounded r ‚Üî (‚àÉs‚ààf.sets, ‚àÉb, s ‚äÜ {x | r x b}) :=
iff.intro
  (assume ‚ü®b, hb‚ü©, ‚ü®{a | r a b}, hb, b, subset.refl _‚ü©)
  (assume ‚ü®s, hs, b, hb‚ü©, ‚ü®b, mem_of_superset hs hb‚ü©)

/-- A bounded function `u` is in particular eventually bounded. -/
lemma is_bounded_under_of {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} :
  (‚àÉb, ‚àÄx, r (u x) b) ‚Üí f.is_bounded_under r u
| ‚ü®b, hb‚ü© := ‚ü®b, show ‚àÄ·∂† x in f, r (u x) b, from eventually_of_forall hb‚ü©

lemma is_bounded_bot : is_bounded r ‚ä• ‚Üî nonempty Œ± :=
by simp [is_bounded, exists_true_iff_nonempty]

lemma is_bounded_top : is_bounded r ‚ä§ ‚Üî (‚àÉt, ‚àÄx, r x t) :=
by simp [is_bounded, eq_univ_iff_forall]

lemma is_bounded_principal (s : set Œ±) : is_bounded r (ùìü s) ‚Üî (‚àÉt, ‚àÄx‚ààs, r x t) :=
by simp [is_bounded, subset_def]

lemma is_bounded_sup [is_trans Œ± r] (hr : ‚àÄb‚ÇÅ b‚ÇÇ, ‚àÉb, r b‚ÇÅ b ‚àß r b‚ÇÇ b) :
  is_bounded r f ‚Üí is_bounded r g ‚Üí is_bounded r (f ‚äî g)
| ‚ü®b‚ÇÅ, h‚ÇÅ‚ü© ‚ü®b‚ÇÇ, h‚ÇÇ‚ü© := let ‚ü®b, rb‚ÇÅb, rb‚ÇÇb‚ü© := hr b‚ÇÅ b‚ÇÇ in
  ‚ü®b, eventually_sup.mpr ‚ü®h‚ÇÅ.mono (Œª x h, trans h rb‚ÇÅb), h‚ÇÇ.mono (Œª x h, trans h rb‚ÇÇb)‚ü©‚ü©

lemma is_bounded.mono (h : f ‚â§ g) : is_bounded r g ‚Üí is_bounded r f
| ‚ü®b, hb‚ü© := ‚ü®b, h hb‚ü©

lemma is_bounded_under.mono {f g : filter Œ≤} {u : Œ≤ ‚Üí Œ±} (h : f ‚â§ g) :
  g.is_bounded_under r u ‚Üí f.is_bounded_under r u :=
Œª hg, hg.mono (map_mono h)

lemma is_bounded.is_bounded_under {q : Œ≤ ‚Üí Œ≤ ‚Üí Prop} {u : Œ± ‚Üí Œ≤}
  (hf : ‚àÄa‚ÇÄ a‚ÇÅ, r a‚ÇÄ a‚ÇÅ ‚Üí q (u a‚ÇÄ) (u a‚ÇÅ)) : f.is_bounded r ‚Üí f.is_bounded_under q u
| ‚ü®b, h‚ü© := ‚ü®u b, show ‚àÄ·∂† x in f, q (u x) (u b), from h.mono (Œª x, hf x b)‚ü©

lemma not_is_bounded_under_of_tendsto_at_top [nonempty Œ±] [semilattice_sup Œ±]
  [preorder Œ≤] [no_top_order Œ≤] {f : Œ± ‚Üí Œ≤} (hf : tendsto f at_top at_top) :
  ¬¨ is_bounded_under (‚â§) at_top f :=
begin
  rintro ‚ü®b, hb‚ü©,
  rw eventually_map at hb,
  obtain ‚ü®b', h‚ü© := no_top b,
  have hb' := (tendsto_at_top.mp hf) b',
  have : {x : Œ± | f x ‚â§ b} ‚à© {x : Œ± | b' ‚â§ f x} = ‚àÖ :=
    eq_empty_of_subset_empty (Œª x hx, (not_le_of_lt h) (le_trans hx.2 hx.1)),
  exact at_top.empty_not_mem (this ‚ñ∏ filter.inter_mem hb hb' : ‚àÖ ‚àà (at_top : filter Œ±)),
end

lemma not_is_bounded_under_of_tendsto_at_bot [nonempty Œ±] [semilattice_sup Œ±]
  [preorder Œ≤] [no_bot_order Œ≤] {f : Œ± ‚Üí Œ≤} (hf : tendsto f at_top at_bot) :
  ¬¨ is_bounded_under (‚â•) at_top f :=
begin
  rintro ‚ü®b, hb‚ü©,
  rw eventually_map at hb,
  obtain ‚ü®b', h‚ü© := no_bot b,
  have hb' := (tendsto_at_bot.mp hf) b',
  have : {x : Œ± | b ‚â§ f x} ‚à© {x : Œ± | f x ‚â§ b'} = ‚àÖ :=
    eq_empty_of_subset_empty (Œª x hx, (not_le_of_lt h) (le_trans hx.1 hx.2)),
  exact at_top.empty_not_mem (this ‚ñ∏ filter.inter_mem hb hb' : ‚àÖ ‚àà (at_top : filter Œ±)),
end

/-- `is_cobounded (‚â∫) f` states that the filter `f` does not tend to infinity w.r.t. `‚â∫`. This is
also called frequently bounded. Will be usually instantiated with `‚â§` or `‚â•`.

There is a subtlety in this definition: we want `f.is_cobounded` to hold for any `f` in the case of
complete lattices. This will be relevant to deduce theorems on complete lattices from their
versions on conditionally complete lattices with additional assumptions. We have to be careful in
the edge case of the trivial filter containing the empty set: the other natural definition
  `¬¨ ‚àÄ a, ‚àÄ·∂† n in f, a ‚â§ n`
would not work as well in this case.
-/
def is_cobounded (r : Œ± ‚Üí Œ± ‚Üí Prop) (f : filter Œ±) := ‚àÉb, ‚àÄa, (‚àÄ·∂† x in f, r x a) ‚Üí r b a

/-- `is_cobounded_under (‚â∫) f u` states that the image of the filter `f` under the map `u` does not
tend to infinity w.r.t. `‚â∫`. This is also called frequently bounded. Will be usually instantiated
with `‚â§` or `‚â•`. -/
def is_cobounded_under (r : Œ± ‚Üí Œ± ‚Üí Prop) (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±) := (f.map u).is_cobounded r

/-- To check that a filter is frequently bounded, it suffices to have a witness
which bounds `f` at some point for every admissible set.

This is only an implication, as the other direction is wrong for the trivial filter.-/
lemma is_cobounded.mk [is_trans Œ± r] (a : Œ±) (h : ‚àÄs‚ààf, ‚àÉx‚ààs, r a x) : f.is_cobounded r :=
‚ü®a, assume y s, let ‚ü®x, h‚ÇÅ, h‚ÇÇ‚ü© := h _ s in trans h‚ÇÇ h‚ÇÅ‚ü©

/-- A filter which is eventually bounded is in particular frequently bounded (in the opposite
direction). At least if the filter is not trivial. -/
lemma is_bounded.is_cobounded_flip [is_trans Œ± r] [ne_bot f] :
  f.is_bounded r ‚Üí f.is_cobounded (flip r)
| ‚ü®a, ha‚ü© := ‚ü®a, assume b hb,
  let ‚ü®x, rxa, rbx‚ü© := (ha.and hb).exists in
  show r b a, from trans rbx rxa‚ü©

lemma is_bounded.is_cobounded_ge [preorder Œ±] [ne_bot f] (h : f.is_bounded (‚â§)) :
  f.is_cobounded (‚â•) :=
h.is_cobounded_flip

lemma is_bounded.is_cobounded_le [preorder Œ±] [ne_bot f] (h : f.is_bounded (‚â•)) :
  f.is_cobounded (‚â§) :=
h.is_cobounded_flip

lemma is_cobounded_bot : is_cobounded r ‚ä• ‚Üî (‚àÉb, ‚àÄx, r b x) :=
by simp [is_cobounded]

lemma is_cobounded_top : is_cobounded r ‚ä§ ‚Üî nonempty Œ± :=
by simp [is_cobounded, eq_univ_iff_forall, exists_true_iff_nonempty] {contextual := tt}

lemma is_cobounded_principal (s : set Œ±) :
  (ùìü s).is_cobounded r ‚Üî (‚àÉb, ‚àÄa, (‚àÄx‚ààs, r x a) ‚Üí r b a) :=
by simp [is_cobounded, subset_def]

lemma is_cobounded.mono (h : f ‚â§ g) : f.is_cobounded r ‚Üí g.is_cobounded r
| ‚ü®b, hb‚ü© := ‚ü®b, assume a ha, hb a (h ha)‚ü©

end relation

lemma is_cobounded_le_of_bot [order_bot Œ±] {f : filter Œ±} : f.is_cobounded (‚â§) :=
‚ü®‚ä•, assume a h, bot_le‚ü©

lemma is_cobounded_ge_of_top [order_top Œ±] {f : filter Œ±} : f.is_cobounded (‚â•) :=
‚ü®‚ä§, assume a h, le_top‚ü©

lemma is_bounded_le_of_top [order_top Œ±] {f : filter Œ±} : f.is_bounded (‚â§) :=
‚ü®‚ä§, eventually_of_forall $ Œª _, le_top‚ü©

lemma is_bounded_ge_of_bot [order_bot Œ±] {f : filter Œ±} : f.is_bounded (‚â•) :=
‚ü®‚ä•, eventually_of_forall $ Œª _, bot_le‚ü©

lemma is_bounded_under_sup [semilattice_sup Œ±] {f : filter Œ≤} {u v : Œ≤ ‚Üí Œ±} :
  f.is_bounded_under (‚â§) u ‚Üí f.is_bounded_under (‚â§) v ‚Üí f.is_bounded_under (‚â§) (Œªa, u a ‚äî v a)
| ‚ü®bu, (hu : ‚àÄ·∂† x in f, u x ‚â§ bu)‚ü© ‚ü®bv, (hv : ‚àÄ·∂† x in f, v x ‚â§ bv)‚ü© :=
  ‚ü®bu ‚äî bv, show ‚àÄ·∂† x in f, u x ‚äî v x ‚â§ bu ‚äî bv,
    by filter_upwards [hu, hv] assume x, sup_le_sup‚ü©

lemma is_bounded_under_inf [semilattice_inf Œ±] {f : filter Œ≤} {u v : Œ≤ ‚Üí Œ±} :
  f.is_bounded_under (‚â•) u ‚Üí f.is_bounded_under (‚â•) v ‚Üí f.is_bounded_under (‚â•) (Œªa, u a ‚äì v a)
| ‚ü®bu, (hu : ‚àÄ·∂† x in f, u x ‚â• bu)‚ü© ‚ü®bv, (hv : ‚àÄ·∂† x in f, v x ‚â• bv)‚ü© :=
  ‚ü®bu ‚äì bv, show ‚àÄ·∂† x in f, u x ‚äì v x ‚â• bu ‚äì bv,
    by filter_upwards [hu, hv] assume x, inf_le_inf‚ü©

/-- Filters are automatically bounded or cobounded in complete lattices. To use the same statements
in complete and conditionally complete lattices but let automation fill automatically the
boundedness proofs in complete lattices, we use the tactic `is_bounded_default` in the statements,
in the form `(hf : f.is_bounded (‚â•) . is_bounded_default)`. -/
meta def is_bounded_default : tactic unit :=
tactic.applyc ``is_cobounded_le_of_bot <|>
tactic.applyc ``is_cobounded_ge_of_top <|>
tactic.applyc ``is_bounded_le_of_top <|>
tactic.applyc ``is_bounded_ge_of_bot


section conditionally_complete_lattice
variables [conditionally_complete_lattice Œ±]

/-- The `Limsup` of a filter `f` is the infimum of the `a` such that, eventually for `f`,
holds `x ‚â§ a`. -/
def Limsup (f : filter Œ±) : Œ± := Inf { a | ‚àÄ·∂† n in f, n ‚â§ a }

/-- The `Liminf` of a filter `f` is the supremum of the `a` such that, eventually for `f`,
holds `x ‚â• a`. -/
def Liminf (f : filter Œ±) : Œ± := Sup { a | ‚àÄ·∂† n in f, a ‚â§ n }

/-- The `limsup` of a function `u` along a filter `f` is the infimum of the `a` such that,
eventually for `f`, holds `u x ‚â§ a`. -/
def limsup (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±) : Œ± := (f.map u).Limsup

/-- The `liminf` of a function `u` along a filter `f` is the supremum of the `a` such that,
eventually for `f`, holds `u x ‚â• a`. -/
def liminf (f : filter Œ≤) (u : Œ≤ ‚Üí Œ±) : Œ± := (f.map u).Liminf

section
variables {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±}
theorem limsup_eq : f.limsup u = Inf { a | ‚àÄ·∂† n in f, u n ‚â§ a } := rfl
theorem liminf_eq : f.liminf u = Sup { a | ‚àÄ·∂† n in f, a ‚â§ u n } := rfl
end

theorem Limsup_le_of_le {f : filter Œ±} {a}
  (hf : f.is_cobounded (‚â§) . is_bounded_default) (h : ‚àÄ·∂† n in f, n ‚â§ a) : f.Limsup ‚â§ a :=
cInf_le hf h

theorem le_Liminf_of_le {f : filter Œ±} {a}
  (hf : f.is_cobounded (‚â•) . is_bounded_default) (h : ‚àÄ·∂† n in f, a ‚â§ n) : a ‚â§ f.Liminf :=
le_cSup hf h

theorem le_Limsup_of_le {f : filter Œ±} {a}
  (hf : f.is_bounded (‚â§) . is_bounded_default) (h : ‚àÄ b, (‚àÄ·∂† n in f, n ‚â§ b) ‚Üí a ‚â§ b) :
  a ‚â§ f.Limsup :=
le_cInf hf h

theorem Liminf_le_of_le {f : filter Œ±} {a}
  (hf : f.is_bounded (‚â•) . is_bounded_default) (h : ‚àÄ b, (‚àÄ·∂† n in f, b ‚â§ n) ‚Üí b ‚â§ a) :
  f.Liminf ‚â§ a :=
cSup_le hf h

theorem Liminf_le_Limsup {f : filter Œ±} [ne_bot f]
  (h‚ÇÅ : f.is_bounded (‚â§) . is_bounded_default) (h‚ÇÇ : f.is_bounded (‚â•) . is_bounded_default) :
  f.Liminf ‚â§ f.Limsup :=
Liminf_le_of_le h‚ÇÇ $ assume a‚ÇÄ ha‚ÇÄ, le_Limsup_of_le h‚ÇÅ $ assume a‚ÇÅ ha‚ÇÅ,
  show a‚ÇÄ ‚â§ a‚ÇÅ, from let ‚ü®b, hb‚ÇÄ, hb‚ÇÅ‚ü© := (ha‚ÇÄ.and ha‚ÇÅ).exists in le_trans hb‚ÇÄ hb‚ÇÅ

lemma Liminf_le_Liminf {f g : filter Œ±}
  (hf : f.is_bounded (‚â•) . is_bounded_default) (hg : g.is_cobounded (‚â•) . is_bounded_default)
  (h : ‚àÄ a, (‚àÄ·∂† n in f, a ‚â§ n) ‚Üí ‚àÄ·∂† n in g, a ‚â§ n) : f.Liminf ‚â§ g.Liminf :=
cSup_le_cSup hg hf h

lemma Limsup_le_Limsup {f g : filter Œ±}
  (hf : f.is_cobounded (‚â§) . is_bounded_default) (hg : g.is_bounded (‚â§) . is_bounded_default)
  (h : ‚àÄ a, (‚àÄ·∂† n in g, n ‚â§ a) ‚Üí ‚àÄ·∂† n in f, n ‚â§ a) : f.Limsup ‚â§ g.Limsup :=
cInf_le_cInf hf hg h

lemma Limsup_le_Limsup_of_le {f g : filter Œ±} (h : f ‚â§ g)
  (hf : f.is_cobounded (‚â§) . is_bounded_default) (hg : g.is_bounded (‚â§) . is_bounded_default) :
  f.Limsup ‚â§ g.Limsup :=
Limsup_le_Limsup hf hg (assume a ha, h ha)

lemma Liminf_le_Liminf_of_le {f g : filter Œ±} (h : g ‚â§ f)
  (hf : f.is_bounded (‚â•) . is_bounded_default) (hg : g.is_cobounded (‚â•) . is_bounded_default) :
  f.Liminf ‚â§ g.Liminf :=
Liminf_le_Liminf hf hg (assume a ha, h ha)

lemma limsup_le_limsup {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (h : u ‚â§·∂†[f] v)
  (hu : f.is_cobounded_under (‚â§) u . is_bounded_default)
  (hv : f.is_bounded_under (‚â§) v . is_bounded_default) :
  f.limsup u ‚â§ f.limsup v :=
Limsup_le_Limsup hu hv $ assume b, h.trans

lemma liminf_le_liminf {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (h : ‚àÄ·∂† a in f, u a ‚â§ v a)
  (hu : f.is_bounded_under (‚â•) u . is_bounded_default)
  (hv : f.is_cobounded_under (‚â•) v . is_bounded_default) :
  f.liminf u ‚â§ f.liminf v :=
@limsup_le_limsup (order_dual Œ≤) Œ± _ _ _ _ h hv hu

lemma limsup_le_limsup_of_le {Œ± Œ≤} [conditionally_complete_lattice Œ≤] {f g : filter Œ±} (h : f ‚â§ g)
  {u : Œ± ‚Üí Œ≤} (hf : f.is_cobounded_under (‚â§) u . is_bounded_default)
  (hg : g.is_bounded_under (‚â§) u . is_bounded_default) :
  f.limsup u ‚â§ g.limsup u :=
Limsup_le_Limsup_of_le (map_mono h) hf hg

lemma liminf_le_liminf_of_le {Œ± Œ≤} [conditionally_complete_lattice Œ≤] {f g : filter Œ±} (h : g ‚â§ f)
  {u : Œ± ‚Üí Œ≤} (hf : f.is_bounded_under (‚â•) u . is_bounded_default)
  (hg : g.is_cobounded_under (‚â•) u . is_bounded_default) :
  f.liminf u ‚â§ g.liminf u :=
Liminf_le_Liminf_of_le (map_mono h) hf hg

theorem Limsup_principal {s : set Œ±} (h : bdd_above s) (hs : s.nonempty) :
  (ùìü s).Limsup = Sup s :=
by simp [Limsup]; exact cInf_upper_bounds_eq_cSup h hs

theorem Liminf_principal {s : set Œ±} (h : bdd_below s) (hs : s.nonempty) :
  (ùìü s).Liminf = Inf s :=
@Limsup_principal (order_dual Œ±) _ s h hs

lemma limsup_congr {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (h : ‚àÄ·∂† a in f, u a = v a) : limsup f u = limsup f v :=
begin
  rw limsup_eq,
  congr' with b,
  exact eventually_congr (h.mono $ Œª x hx, by simp [hx])
end

lemma liminf_congr {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} {u v : Œ± ‚Üí Œ≤}
  (h : ‚àÄ·∂† a in f, u a = v a) : liminf f u = liminf f v :=
@limsup_congr (order_dual Œ≤) _ _ _ _ _ h

lemma limsup_const {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} [ne_bot f]
  (b : Œ≤) : limsup f (Œª x, b) = b :=
by simpa only [limsup_eq, eventually_const] using cInf_Ici

lemma liminf_const {Œ± : Type*} [conditionally_complete_lattice Œ≤] {f : filter Œ±} [ne_bot f]
  (b : Œ≤) : liminf f (Œª x, b) = b :=
@limsup_const (order_dual Œ≤) Œ± _ f _ b

lemma liminf_le_limsup {f : filter Œ≤} [ne_bot f] {u : Œ≤ ‚Üí Œ±}
  (h : f.is_bounded_under (‚â§) u . is_bounded_default)
  (h' : f.is_bounded_under (‚â•) u . is_bounded_default) :
  liminf f u ‚â§ limsup f u :=
Liminf_le_Limsup h h'

end conditionally_complete_lattice

section complete_lattice
variables [complete_lattice Œ±]

@[simp] theorem Limsup_bot : (‚ä• : filter Œ±).Limsup = ‚ä• :=
bot_unique $ Inf_le $ by simp

@[simp] theorem Liminf_bot : (‚ä• : filter Œ±).Liminf = ‚ä§ :=
top_unique $ le_Sup $ by simp

@[simp] theorem Limsup_top : (‚ä§ : filter Œ±).Limsup = ‚ä§ :=
top_unique $ le_Inf $
  by simp [eq_univ_iff_forall]; exact assume b hb, (top_unique $ hb _)

@[simp] theorem Liminf_top : (‚ä§ : filter Œ±).Liminf = ‚ä• :=
bot_unique $ Sup_le $
  by simp [eq_univ_iff_forall]; exact assume b hb, (bot_unique $ hb _)

/-- Same as limsup_const applied to `‚ä•` but without the `ne_bot f` assumption -/
lemma limsup_const_bot {f : filter Œ≤} : limsup f (Œª x : Œ≤, (‚ä• : Œ±)) = (‚ä• : Œ±) :=
begin
  rw [limsup_eq, eq_bot_iff],
  exact Inf_le (eventually_of_forall (Œª x, le_refl _)),
end

/-- Same as limsup_const applied to `‚ä§` but without the `ne_bot f` assumption -/
lemma liminf_const_top {f : filter Œ≤} : liminf f (Œª x : Œ≤, (‚ä§ : Œ±)) = (‚ä§ : Œ±) :=
@limsup_const_bot (order_dual Œ±) Œ≤ _ _

theorem has_basis.Limsup_eq_infi_Sup {Œπ} {p : Œπ ‚Üí Prop} {s} {f : filter Œ±} (h : f.has_basis p s) :
  f.Limsup = ‚®Ö i (hi : p i), Sup (s i) :=
le_antisymm
  (le_binfi $ Œª i hi, Inf_le $ h.eventually_iff.2 ‚ü®i, hi, Œª x, le_Sup‚ü©)
  (le_Inf $ assume a ha, let ‚ü®i, hi, ha‚ü© := h.eventually_iff.1 ha in
    infi_le_of_le _ $ infi_le_of_le hi $ Sup_le ha)

theorem has_basis.Liminf_eq_supr_Inf {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí set Œ±} {f : filter Œ±}
  (h : f.has_basis p s) : f.Liminf = ‚®Ü i (hi : p i), Inf (s i) :=
@has_basis.Limsup_eq_infi_Sup (order_dual Œ±) _ _ _ _ _ h

theorem Limsup_eq_infi_Sup {f : filter Œ±} : f.Limsup = ‚®Ö s ‚àà f, Sup s :=
f.basis_sets.Limsup_eq_infi_Sup

theorem Liminf_eq_supr_Inf {f : filter Œ±} : f.Liminf = ‚®Ü s ‚àà f, Inf s :=
@Limsup_eq_infi_Sup (order_dual Œ±) _ _

/-- In a complete lattice, the limsup of a function is the infimum over sets `s` in the filter
of the supremum of the function over `s` -/
theorem limsup_eq_infi_supr {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} : f.limsup u = ‚®Ö s ‚àà f, ‚®Ü a ‚àà s, u a :=
(f.basis_sets.map u).Limsup_eq_infi_Sup.trans $
  by simp only [Sup_image, id]

lemma limsup_eq_infi_supr_of_nat {u : ‚Ñï ‚Üí Œ±} : limsup at_top u = ‚®Ö n : ‚Ñï, ‚®Ü i ‚â• n, u i :=
(at_top_basis.map u).Limsup_eq_infi_Sup.trans $
  by simp only [Sup_image, infi_const]; refl

lemma limsup_eq_infi_supr_of_nat' {u : ‚Ñï ‚Üí Œ±} : limsup at_top u = ‚®Ö n : ‚Ñï, ‚®Ü i : ‚Ñï, u (i + n) :=
by simp only [limsup_eq_infi_supr_of_nat, supr_ge_eq_supr_nat_add]

theorem has_basis.limsup_eq_infi_supr {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí set Œ≤} {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±}
  (h : f.has_basis p s) : f.limsup u = ‚®Ö i (hi : p i), ‚®Ü a ‚àà s i, u a :=
(h.map u).Limsup_eq_infi_Sup.trans $ by simp only [Sup_image, id]

/-- In a complete lattice, the liminf of a function is the infimum over sets `s` in the filter
of the supremum of the function over `s` -/
theorem liminf_eq_supr_infi {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±} : f.liminf u = ‚®Ü s ‚àà f, ‚®Ö a ‚àà s, u a :=
@limsup_eq_infi_supr (order_dual Œ±) Œ≤ _ _ _

lemma liminf_eq_supr_infi_of_nat {u : ‚Ñï ‚Üí Œ±} : liminf at_top u = ‚®Ü n : ‚Ñï, ‚®Ö i ‚â• n, u i :=
@limsup_eq_infi_supr_of_nat (order_dual Œ±) _ u

lemma liminf_eq_supr_infi_of_nat' {u : ‚Ñï ‚Üí Œ±} : liminf at_top u = ‚®Ü n : ‚Ñï, ‚®Ö i : ‚Ñï, u (i + n) :=
@limsup_eq_infi_supr_of_nat' (order_dual Œ±) _ _

theorem has_basis.liminf_eq_supr_infi {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí set Œ≤} {f : filter Œ≤} {u : Œ≤ ‚Üí Œ±}
  (h : f.has_basis p s) : f.liminf u = ‚®Ü i (hi : p i), ‚®Ö a ‚àà s i, u a :=
@has_basis.limsup_eq_infi_supr (order_dual Œ±) _ _ _ _ _ _ _ h

@[simp] lemma liminf_nat_add (f : ‚Ñï ‚Üí Œ±) (k : ‚Ñï) :
  at_top.liminf (Œª i, f (i + k)) = at_top.liminf f :=
by { simp_rw liminf_eq_supr_infi_of_nat, exact supr_infi_ge_nat_add f k }

@[simp] lemma limsup_nat_add (f : ‚Ñï ‚Üí Œ±) (k : ‚Ñï) :
  at_top.limsup (Œª i, f (i + k)) = at_top.limsup f :=
@liminf_nat_add (order_dual Œ±) _ f k

lemma liminf_le_of_frequently_le' {Œ± Œ≤} [complete_lattice Œ≤]
  {f : filter Œ±} {u : Œ± ‚Üí Œ≤} {x : Œ≤} (h : ‚àÉ·∂† a in f, u a ‚â§ x) :
  f.liminf u ‚â§ x :=
begin
  rw liminf_eq,
  refine Sup_le (Œª b hb, _),
  have hbx : ‚àÉ·∂† a in f, b ‚â§ x,
  { revert h,
    rw [‚Üênot_imp_not, not_frequently, not_frequently],
    exact Œª h, hb.mp (h.mono (Œª a hbx hba hax, hbx (hba.trans hax))), },
  exact hbx.exists.some_spec,
end

lemma le_limsup_of_frequently_le' {Œ± Œ≤} [complete_lattice Œ≤]
  {f : filter Œ±} {u : Œ± ‚Üí Œ≤} {x : Œ≤} (h : ‚àÉ·∂† a in f, x ‚â§ u a) :
  x ‚â§ f.limsup u :=
@liminf_le_of_frequently_le' _ (order_dual Œ≤) _ _ _ _ h

end complete_lattice

section conditionally_complete_linear_order

lemma eventually_lt_of_lt_liminf {f : filter Œ±} [conditionally_complete_linear_order Œ≤]
  {u : Œ± ‚Üí Œ≤} {b : Œ≤} (h : b < liminf f u) (hu : f.is_bounded_under (‚â•) u . is_bounded_default) :
  ‚àÄ·∂† a in f, b < u a :=
begin
  obtain ‚ü®c, hc, hbc‚ü© : ‚àÉ (c : Œ≤) (hc : c ‚àà {c : Œ≤ | ‚àÄ·∂† (n : Œ±) in f, c ‚â§ u n}), b < c :=
    exists_lt_of_lt_cSup hu h,
  exact hc.mono (Œª x hx, lt_of_lt_of_le hbc hx)
end

lemma eventually_lt_of_limsup_lt {f : filter Œ±} [conditionally_complete_linear_order Œ≤]
  {u : Œ± ‚Üí Œ≤} {b : Œ≤} (h : limsup f u < b) (hu : f.is_bounded_under (‚â§) u . is_bounded_default) :
  ‚àÄ·∂† a in f, u a < b :=
@eventually_lt_of_lt_liminf _ (order_dual Œ≤) _ _ _ _ h hu

lemma le_limsup_of_frequently_le {Œ± Œ≤} [conditionally_complete_linear_order Œ≤] {f : filter Œ±}
  {u : Œ± ‚Üí Œ≤}  {b : Œ≤} (hu_le : ‚àÉ·∂† x in f, b ‚â§ u x)
  (hu : f.is_bounded_under (‚â§) u . is_bounded_default) :
  b ‚â§ f.limsup u :=
begin
  revert hu_le,
  rw [‚Üênot_imp_not, not_frequently],
  simp_rw ‚Üêlt_iff_not_ge,
  exact Œª h, eventually_lt_of_limsup_lt h hu,
end

lemma liminf_le_of_frequently_le  {Œ± Œ≤} [conditionally_complete_linear_order Œ≤] {f : filter Œ±}
  {u : Œ± ‚Üí Œ≤}  {b : Œ≤} (hu_le : ‚àÉ·∂† x in f, u x ‚â§ b)
  (hu : f.is_bounded_under (‚â•) u . is_bounded_default) :
  f.liminf u ‚â§ b :=
@le_limsup_of_frequently_le _ (order_dual Œ≤) _ f u b hu_le hu

lemma frequently_lt_of_lt_limsup {Œ± Œ≤} [conditionally_complete_linear_order Œ≤] {f : filter Œ±}
  {u : Œ± ‚Üí Œ≤}  {b : Œ≤}
  (hu : f.is_cobounded_under (‚â§) u . is_bounded_default) (h : b < f.limsup u) :
  ‚àÉ·∂† x in f, b < u x :=
begin
  contrapose! h,
  apply Limsup_le_of_le hu,
  simpa using h,
end

lemma frequently_lt_of_liminf_lt {Œ± Œ≤} [conditionally_complete_linear_order Œ≤] {f : filter Œ±}
  {u : Œ± ‚Üí Œ≤}  {b : Œ≤}
  (hu : f.is_cobounded_under (‚â•) u . is_bounded_default) (h : f.liminf u < b) :
  ‚àÉ·∂† x in f, u x < b :=
@frequently_lt_of_lt_limsup _ (order_dual Œ≤) _ f u b hu h

end conditionally_complete_linear_order

end filter

section order
open filter

lemma galois_connection.l_limsup_le {Œ± Œ≤ Œ≥} [conditionally_complete_lattice Œ≤]
  [conditionally_complete_lattice Œ≥] {f : filter Œ±} {v : Œ± ‚Üí Œ≤}
  {l : Œ≤ ‚Üí Œ≥} {u : Œ≥ ‚Üí Œ≤} (gc : galois_connection l u)
  (hlv : f.is_bounded_under (‚â§) (Œª x, l (v x)) . is_bounded_default)
  (hv_co : f.is_cobounded_under (‚â§) v . is_bounded_default) :
  l (f.limsup v) ‚â§ f.limsup (Œª x, l (v x)) :=
begin
  refine le_Limsup_of_le hlv (Œª c hc, _),
  rw filter.eventually_map at hc,
  simp_rw (gc _ _) at hc ‚ä¢,
  exact Limsup_le_of_le hv_co hc,
end

lemma order_iso.limsup_apply {Œ≥} [conditionally_complete_lattice Œ≤]
  [conditionally_complete_lattice Œ≥] {f : filter Œ±} {u : Œ± ‚Üí Œ≤} (g : Œ≤ ‚âÉo Œ≥)
  (hu : f.is_bounded_under (‚â§) u . is_bounded_default)
  (hu_co : f.is_cobounded_under (‚â§) u . is_bounded_default)
  (hgu : f.is_bounded_under (‚â§) (Œª x, g (u x)) . is_bounded_default)
  (hgu_co : f.is_cobounded_under (‚â§) (Œª x, g (u x)) . is_bounded_default) :
  g (f.limsup u) = f.limsup (Œª x, g (u x)) :=
begin
  refine le_antisymm (g.to_galois_connection.l_limsup_le hgu hu_co) _,
  rw [‚Üê(g.symm.symm_apply_apply (f.limsup (Œª (x : Œ±), g (u x)))), g.symm_symm],
  refine g.monotone _,
  have hf : u = Œª i, g.symm (g (u i)), from funext (Œª i, (g.symm_apply_apply (u i)).symm),
  nth_rewrite 0 hf,
  refine g.symm.to_galois_connection.l_limsup_le _ hgu_co,
  simp_rw g.symm_apply_apply,
  exact hu,
end

lemma order_iso.liminf_apply {Œ≥} [conditionally_complete_lattice Œ≤]
  [conditionally_complete_lattice Œ≥] {f : filter Œ±} {u : Œ± ‚Üí Œ≤} (g : Œ≤ ‚âÉo Œ≥)
  (hu : f.is_bounded_under (‚â•) u . is_bounded_default)
  (hu_co : f.is_cobounded_under (‚â•) u . is_bounded_default)
  (hgu : f.is_bounded_under (‚â•) (Œª x, g (u x)) . is_bounded_default)
  (hgu_co : f.is_cobounded_under (‚â•) (Œª x, g (u x)) . is_bounded_default) :
  g (f.liminf u) = f.liminf (Œª x, g (u x)) :=
@order_iso.limsup_apply Œ± (order_dual Œ≤) (order_dual Œ≥) _ _ f u g.dual hu hu_co hgu hgu_co

end order
