/-
Copyright (c) 2020 Simon Hudon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon
-/
import control.monad.basic
import data.part
import order.hom.lattice
import tactic.monotonicity
import tactic.wlog

/-!
# Omega Complete Partial Orders

An omega-complete partial order is a partial order with a supremum
operation on increasing sequences indexed by natural numbers (which we
call `Ï‰Sup`). In this sense, it is strictly weaker than join complete
semi-lattices as only Ï‰-sized totally ordered sets have a supremum.

The concept of an omega-complete partial order (Ï‰CPO) is useful for the
formalization of the semantics of programming languages. Its notion of
supremum helps define the meaning of recursive procedures.

## Main definitions

 * class `omega_complete_partial_order`
 * `ite`, `map`, `bind`, `seq` as continuous morphisms

## Instances of `omega_complete_partial_order`

 * `part`
 * every `complete_lattice`
 * pi-types
 * product types
 * `monotone_hom`
 * `continuous_hom` (with notation â†’ğ’„)
   * an instance of `omega_complete_partial_order (Î± â†’ğ’„ Î²)`
 * `continuous_hom.of_fun`
 * `continuous_hom.of_mono`
 * continuous functions:
   * `id`
   * `ite`
   * `const`
   * `part.bind`
   * `part.map`
   * `part.seq`

## References

 * [Chain-complete posets and directed sets with applications][markowsky1976]
 * [Recursive definitions of partial functions and their computations][cadiou1972]
 * [Semantics of Programming Languages: Structures and Techniques][gunter1992]
-/

universes u v

local attribute [-simp] part.bind_eq_bind part.map_eq_map
open_locale classical

namespace order_hom

variables (Î± : Type*) (Î² : Type*) {Î³ : Type*} {Ï† : Type*}
variables [preorder Î±] [preorder Î²] [preorder Î³] [preorder Ï†]

variables {Î² Î³}

variables {Î±} {Î±' : Type*} {Î²' : Type*} [preorder Î±'] [preorder Î²']

/-- `part.bind` as a monotone function -/
@[simps]
def bind {Î² Î³} (f : Î± â†’o part Î²) (g : Î± â†’o Î² â†’ part Î³) : Î± â†’o part Î³ :=
{ to_fun := Î» x, f x >>= g x,
  monotone' :=
  begin
    intros x y h a,
    simp only [and_imp, exists_prop, part.bind_eq_bind, part.mem_bind_iff,
               exists_imp_distrib],
    intros b hb ha,
    refine âŸ¨b, f.monotone h _ hb, g.monotone h _ _ haâŸ©,
  end }

end order_hom

namespace omega_complete_partial_order

/-- A chain is a monotone sequence.

See the definition on page 114 of [gunter1992]. -/
def chain (Î± : Type u) [preorder Î±] :=
â„• â†’o Î±

namespace chain

variables {Î± : Type u} {Î² : Type v} {Î³ : Type*}
variables [preorder Î±] [preorder Î²] [preorder Î³]

instance : has_coe_to_fun (chain Î±) (Î» _, â„• â†’ Î±) := order_hom.has_coe_to_fun

instance [inhabited Î±] : inhabited (chain Î±) :=
âŸ¨ âŸ¨ Î» _, default _, Î» _ _ _, le_refl _ âŸ© âŸ©

instance : has_mem Î± (chain Î±) :=
âŸ¨Î»a (c : â„• â†’o Î±), âˆƒ i, a = c iâŸ©

variables (c c' : chain Î±)
variables (f : Î± â†’o Î²)
variables (g : Î² â†’o Î³)

instance : has_le (chain Î±) :=
{ le := Î» x y, âˆ€ i, âˆƒ j, x i â‰¤ y j }

/-- `map` function for `chain` -/
@[simps {fully_applied := ff}] def map : chain Î² :=
f.comp c

variables {f}

lemma mem_map (x : Î±) : x âˆˆ c â†’ f x âˆˆ chain.map c f :=
Î» âŸ¨i,hâŸ©, âŸ¨i, h.symm â–¸ rflâŸ©

lemma exists_of_mem_map {b : Î²} : b âˆˆ c.map f â†’ âˆƒ a, a âˆˆ c âˆ§ f a = b :=
Î» âŸ¨i,hâŸ©, âŸ¨c i, âŸ¨i, rflâŸ©, h.symmâŸ©

lemma mem_map_iff {b : Î²} : b âˆˆ c.map f â†” âˆƒ a, a âˆˆ c âˆ§ f a = b :=
âŸ¨ exists_of_mem_map _, Î» h, by { rcases h with âŸ¨w,h,h'âŸ©, subst b, apply mem_map c _ h, } âŸ©

@[simp]
lemma map_id : c.map order_hom.id = c :=
order_hom.comp_id _

lemma map_comp : (c.map f).map g = c.map (g.comp f) := rfl

@[mono]
lemma map_le_map {g : Î± â†’o Î²} (h : f â‰¤ g) : c.map f â‰¤ c.map g :=
Î» i, by simp [mem_map_iff]; intros; existsi i; apply h

/-- `chain.zip` pairs up the elements of two chains that have the same index -/
@[simps]
def zip (câ‚€ : chain Î±) (câ‚ : chain Î²) : chain (Î± Ã— Î²) :=
order_hom.prod câ‚€ câ‚

end chain

end omega_complete_partial_order

open omega_complete_partial_order

section prio
set_option extends_priority 50

/-- An omega-complete partial order is a partial order with a supremum
operation on increasing sequences indexed by natural numbers (which we
call `Ï‰Sup`). In this sense, it is strictly weaker than join complete
semi-lattices as only Ï‰-sized totally ordered sets have a supremum.

See the definition on page 114 of [gunter1992]. -/
class omega_complete_partial_order (Î± : Type*) extends partial_order Î± :=
(Ï‰Sup     : chain Î± â†’ Î±)
(le_Ï‰Sup  : âˆ€(c:chain Î±), âˆ€ i, c i â‰¤ Ï‰Sup c)
(Ï‰Sup_le  : âˆ€(c:chain Î±) x, (âˆ€ i, c i â‰¤ x) â†’ Ï‰Sup c â‰¤ x)

end prio

namespace omega_complete_partial_order
variables {Î± : Type u} {Î² : Type v} {Î³ : Type*}
variables [omega_complete_partial_order Î±]

/-- Transfer a `omega_complete_partial_order` on `Î²` to a `omega_complete_partial_order` on `Î±`
using a strictly monotone function `f : Î² â†’o Î±`, a definition of Ï‰Sup and a proof that `f` is
continuous with regard to the provided `Ï‰Sup` and the Ï‰CPO on `Î±`. -/
@[reducible]
protected def lift [partial_order Î²] (f : Î² â†’o Î±)
  (Ï‰Supâ‚€ : chain Î² â†’ Î²)
  (h : âˆ€ x y, f x â‰¤ f y â†’ x â‰¤ y)
  (h' : âˆ€ c, f (Ï‰Supâ‚€ c) = Ï‰Sup (c.map f)) : omega_complete_partial_order Î² :=
{ Ï‰Sup := Ï‰Supâ‚€,
  Ï‰Sup_le := Î» c x hx, h _ _ (by rw h'; apply Ï‰Sup_le; intro; apply f.monotone (hx i)),
  le_Ï‰Sup := Î» c i, h _ _ (by rw h'; apply le_Ï‰Sup (c.map f)) }

lemma le_Ï‰Sup_of_le {c : chain Î±} {x : Î±} (i : â„•) (h : x â‰¤ c i) : x â‰¤ Ï‰Sup c :=
le_trans h (le_Ï‰Sup c _)

lemma Ï‰Sup_total {c : chain Î±} {x : Î±} (h : âˆ€ i, c i â‰¤ x âˆ¨ x â‰¤ c i) : Ï‰Sup c â‰¤ x âˆ¨ x â‰¤ Ï‰Sup c :=
classical.by_cases
  (assume : âˆ€ i, c i â‰¤ x, or.inl (Ï‰Sup_le _ _ this))
  (assume : Â¬ âˆ€ i, c i â‰¤ x,
    have âˆƒ i, Â¬ c i â‰¤ x,
      by simp only [not_forall] at this âŠ¢; assumption,
    let âŸ¨i, hxâŸ© := this in
    have x â‰¤ c i, from (h i).resolve_left hx,
    or.inr $ le_Ï‰Sup_of_le _ this)

@[mono]
lemma Ï‰Sup_le_Ï‰Sup_of_le {câ‚€ câ‚ : chain Î±} (h : câ‚€ â‰¤ câ‚) : Ï‰Sup câ‚€ â‰¤ Ï‰Sup câ‚ :=
Ï‰Sup_le _ _ $
Î» i, Exists.rec_on (h i) $
Î» j h, le_trans h (le_Ï‰Sup _ _)

lemma Ï‰Sup_le_iff (c : chain Î±) (x : Î±) : Ï‰Sup c â‰¤ x â†” (âˆ€ i, c i â‰¤ x) :=
begin
  split; intros,
  { transitivity Ï‰Sup c,
    exact le_Ï‰Sup _ _, assumption },
  exact Ï‰Sup_le _ _ â€¹_â€º,
end

/-- A subset `p : Î± â†’ Prop` of the type closed under `Ï‰Sup` induces an
`omega_complete_partial_order` on the subtype `{a : Î± // p a}`. -/
def subtype {Î± : Type*} [omega_complete_partial_order Î±] (p : Î± â†’ Prop)
  (hp : âˆ€ (c : chain Î±), (âˆ€ i âˆˆ c, p i) â†’ p (Ï‰Sup c)) :
  omega_complete_partial_order (subtype p) :=
omega_complete_partial_order.lift
  (order_hom.subtype.val p)
  (Î» c, âŸ¨Ï‰Sup _, hp (c.map (order_hom.subtype.val p)) (Î» i âŸ¨n, qâŸ©, q.symm â–¸ (c n).2)âŸ©)
  (Î» x y h, h)
  (Î» c, rfl)

section continuity
open chain

variables [omega_complete_partial_order Î²]
variables [omega_complete_partial_order Î³]

/-- A monotone function `f : Î± â†’o Î²` is continuous if it distributes over Ï‰Sup.

In order to distinguish it from the (more commonly used) continuity from topology
(see topology/basic.lean), the present definition is often referred to as
"Scott-continuity" (referring to Dana Scott). It corresponds to continuity
in Scott topological spaces (not defined here). -/
def continuous (f : Î± â†’o Î²) : Prop :=
âˆ€ c : chain Î±, f (Ï‰Sup c) = Ï‰Sup (c.map f)

/-- `continuous' f` asserts that `f` is both monotone and continuous. -/
def continuous' (f : Î± â†’ Î²) : Prop :=
âˆƒ hf : monotone f, continuous âŸ¨f, hfâŸ©

lemma continuous'.to_monotone {f : Î± â†’ Î²} (hf : continuous' f) : monotone f := hf.fst

lemma continuous.of_bundled (f : Î± â†’ Î²) (hf : monotone f)
  (hf' : continuous âŸ¨f, hfâŸ©) : continuous' f := âŸ¨hf, hf'âŸ©

lemma continuous.of_bundled' (f : Î± â†’o Î²) (hf' : continuous f) : continuous' f :=
âŸ¨f.mono, hf'âŸ©

lemma continuous'.to_bundled (f : Î± â†’ Î²) (hf : continuous' f) :
  continuous âŸ¨f, hf.to_monotoneâŸ© := hf.snd

@[simp, norm_cast] lemma continuous'_coe : âˆ€ {f : Î± â†’o Î²}, continuous' f â†” continuous f
| âŸ¨f, hfâŸ© := âŸ¨Î» âŸ¨hf', hcâŸ©, hc, Î» hc, âŸ¨hf, hcâŸ©âŸ©

variables (f : Î± â†’o Î²) (g : Î² â†’o Î³)

lemma continuous_id : continuous (@order_hom.id Î± _) :=
by intro; rw c.map_id; refl

lemma continuous_comp (hfc : continuous f) (hgc : continuous g) : continuous (g.comp f):=
begin
  dsimp [continuous] at *, intro,
  rw [hfc,hgc,chain.map_comp]
end

lemma id_continuous' : continuous' (@id Î±) :=
continuous_id.of_bundled' _

lemma continuous_const (x : Î²) : continuous (order_hom.const Î± x) :=
Î» c, eq_of_forall_ge_iff $ Î» z, by simp [Ï‰Sup_le_iff]

lemma const_continuous' (x: Î²) : continuous' (function.const Î± x) :=
continuous.of_bundled' (order_hom.const Î± x) (continuous_const x)

end continuity

end omega_complete_partial_order

namespace part

variables {Î± : Type u} {Î² : Type v} {Î³ : Type*}
open omega_complete_partial_order

lemma eq_of_chain {c : chain (part Î±)} {a b : Î±} (ha : some a âˆˆ c) (hb : some b âˆˆ c) : a = b :=
begin
  cases ha with i ha, replace ha := ha.symm,
  cases hb with j hb, replace hb := hb.symm,
  wlog h : i â‰¤ j := le_total i j using [a b i j, b a j i],
  rw [eq_some_iff] at ha hb,
  have := c.monotone h _ ha, apply mem_unique this hb
end

/-- The (noncomputable) `Ï‰Sup` definition for the `Ï‰`-CPO structure on `part Î±`. -/
protected noncomputable def Ï‰Sup (c : chain (part Î±)) : part Î± :=
if h : âˆƒa, some a âˆˆ c then some (classical.some h) else none

lemma Ï‰Sup_eq_some {c : chain (part Î±)} {a : Î±} (h : some a âˆˆ c) : part.Ï‰Sup c = some a :=
have âˆƒa, some a âˆˆ c, from âŸ¨a, hâŸ©,
have a' : some (classical.some this) âˆˆ c, from classical.some_spec this,
calc part.Ï‰Sup c = some (classical.some this) : dif_pos this
                ... = some a : congr_arg _ (eq_of_chain a' h)

lemma Ï‰Sup_eq_none {c : chain (part Î±)} (h : Â¬âˆƒa, some a âˆˆ c) : part.Ï‰Sup c = none :=
dif_neg h

lemma mem_chain_of_mem_Ï‰Sup {c : chain (part Î±)} {a : Î±} (h : a âˆˆ part.Ï‰Sup c) : some a âˆˆ c :=
begin
  simp [part.Ï‰Sup] at h, split_ifs at h,
  { have h' := classical.some_spec h_1,
    rw â† eq_some_iff at h, rw â† h, exact h' },
  { rcases h with âŸ¨ âŸ¨ âŸ© âŸ© }
end

noncomputable instance omega_complete_partial_order : omega_complete_partial_order (part Î±) :=
{ Ï‰Sup    := part.Ï‰Sup,
  le_Ï‰Sup := Î» c i, by { intros x hx, rw â† eq_some_iff at hx âŠ¢,
                         rw [Ï‰Sup_eq_some, â† hx], rw â† hx, exact âŸ¨i,rflâŸ© },
  Ï‰Sup_le := by { rintros c x hx a ha, replace ha := mem_chain_of_mem_Ï‰Sup ha,
                  cases ha with i ha, apply hx i, rw â† ha, apply mem_some } }

section inst

lemma mem_Ï‰Sup (x : Î±) (c : chain (part Î±)) : x âˆˆ Ï‰Sup c â†” some x âˆˆ c :=
begin
  simp [omega_complete_partial_order.Ï‰Sup,part.Ï‰Sup],
  split,
  { split_ifs, swap, rintro âŸ¨âŸ¨âŸ©âŸ©,
    intro h', have hh := classical.some_spec h,
    simp at h', subst x, exact hh },
  { intro h,
    have h' : âˆƒ (a : Î±), some a âˆˆ c := âŸ¨_,hâŸ©,
    rw dif_pos h', have hh := classical.some_spec h',
    rw eq_of_chain hh h, simp }
end

end inst

end part

namespace pi

variables {Î± : Type*} {Î² : Î± â†’ Type*} {Î³ : Type*}

open omega_complete_partial_order omega_complete_partial_order.chain

instance [âˆ€a, omega_complete_partial_order (Î² a)] : omega_complete_partial_order (Î a, Î² a) :=
{ Ï‰Sup    := Î»c a, Ï‰Sup (c.map (pi.eval_order_hom a)),
  Ï‰Sup_le := assume c f hf a, Ï‰Sup_le _ _ $ by { rintro i, apply hf },
  le_Ï‰Sup := assume c i x, le_Ï‰Sup_of_le _ $ le_refl _ }

namespace omega_complete_partial_order

variables [âˆ€ x, omega_complete_partial_order $ Î² x]
variables [omega_complete_partial_order Î³]

lemma flipâ‚_continuous'
  (f : âˆ€ x : Î±, Î³ â†’ Î² x) (a : Î±) (hf : continuous' (Î» x y, f y x)) :
  continuous' (f a) :=
continuous.of_bundled _
  (Î» x y h, hf.to_monotone h a)
  (Î» c, congr_fun (hf.to_bundled _ c) a)

lemma flipâ‚‚_continuous'
  (f : Î³ â†’ Î  x, Î² x) (hf : âˆ€ x, continuous' (Î» g, f g x)) : continuous' f :=
continuous.of_bundled _
  (Î» x y h a, (hf a).to_monotone h)
  (by intro c; ext a; apply (hf a).to_bundled _ c)

end omega_complete_partial_order

end pi

namespace prod

open omega_complete_partial_order
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*}
variables [omega_complete_partial_order Î±]
variables [omega_complete_partial_order Î²]
variables [omega_complete_partial_order Î³]

/-- The supremum of a chain in the product `Ï‰`-CPO. -/
@[simps]
protected def Ï‰Sup (c : chain (Î± Ã— Î²)) : Î± Ã— Î² :=
(Ï‰Sup (c.map order_hom.fst), Ï‰Sup (c.map order_hom.snd))

@[simps Ï‰Sup_fst Ï‰Sup_snd]
instance : omega_complete_partial_order (Î± Ã— Î²) :=
{ Ï‰Sup := prod.Ï‰Sup,
  Ï‰Sup_le := Î» c âŸ¨x,x'âŸ© h, âŸ¨Ï‰Sup_le _ _ $ Î» i, (h i).1, Ï‰Sup_le _ _ $ Î» i, (h i).2âŸ©,
  le_Ï‰Sup := Î» c i,
    âŸ¨le_Ï‰Sup (c.map order_hom.fst) i, le_Ï‰Sup (c.map order_hom.snd) iâŸ© }

end prod

namespace complete_lattice
variables (Î± : Type u)

/-- Any complete lattice has an `Ï‰`-CPO structure where the countable supremum is a special case
of arbitrary suprema. -/

@[priority 100] -- see Note [lower instance priority]
instance [complete_lattice Î±] : omega_complete_partial_order Î± :=
{ Ï‰Sup    := Î»c, â¨† i, c i,
  Ï‰Sup_le := Î» âŸ¨c, _âŸ© s hs, by simp only [supr_le_iff, order_hom.coe_fun_mk] at âŠ¢ hs;
    intros i; apply hs i,
  le_Ï‰Sup := assume âŸ¨c, _âŸ© i, by simp only [order_hom.coe_fun_mk]; apply le_supr_of_le i; refl }

variables {Î±} {Î² : Type v} [omega_complete_partial_order Î±] [complete_lattice Î²]
open omega_complete_partial_order

lemma inf_continuous [is_total Î² (â‰¤)] (f g : Î± â†’o Î²) (hf : continuous f) (hg : continuous g) :
  continuous (f âŠ“ g) :=
begin
  intro c,
  apply eq_of_forall_ge_iff, intro z,
  simp only [inf_le_iff, hf c, hg c, Ï‰Sup_le_iff, â†forall_or_distrib_left, â†forall_or_distrib_right,
             function.comp_app, chain.map_coe, order_hom.has_inf_inf_coe],
  split,
  { introv h, apply h },
  { intros h i j,
    apply or.imp _ _ (h (max i j)); apply le_trans; mono*; try { exact le_rfl },
    { apply le_max_left },
    { apply le_max_right }, },
end

lemma Sup_continuous (s : set $ Î± â†’o Î²) (hs : âˆ€ f âˆˆ s, continuous f) :
  continuous (Sup s) :=
begin
  intro c, apply eq_of_forall_ge_iff, intro z,
  suffices : (âˆ€ (f âˆˆ s) n, (f : _) (c n) â‰¤ z) â†” (âˆ€ n (f âˆˆ s), (f : _) (c n) â‰¤ z),
    by simpa [Ï‰Sup_le_iff, hs _ _ _] { contextual := tt },
  exact âŸ¨Î» H n f hf, H f hf n, Î» H f hf n, H n f hfâŸ©
end

lemma supr_continuous {Î¹ : Sort*} {f : Î¹ â†’ Î± â†’o Î²} (h : âˆ€ i, continuous (f i)) :
  continuous (â¨† i, f i) :=
Sup_continuous _ $ set.forall_range_iff.2 h

theorem Sup_continuous' (s : set (Î± â†’ Î²)) (hc : âˆ€ f âˆˆ s, continuous' f) :
  continuous' (Sup s) :=
begin
  lift s to set (Î± â†’o Î²) using Î» f hf, (hc f hf).to_monotone,
  simp only [set.ball_image_iff, continuous'_coe] at hc,
  rw [Sup_image],
  norm_cast,
  exact supr_continuous (Î» f, supr_continuous (Î» hf, hc f hf)),
end

lemma sup_continuous {f g : Î± â†’o Î²} (hf : continuous f) (hg : continuous g) :
  continuous (f âŠ” g) :=
begin
  rw â† Sup_pair, apply Sup_continuous,
  rintro f (rfl|rfl|_); assumption
end

lemma top_continuous :
  continuous (âŠ¤ : Î± â†’o Î²) :=
begin
  intro c, apply eq_of_forall_ge_iff, intro z,
  simp only [Ï‰Sup_le_iff, forall_const, chain.map_coe, (âˆ˜), function.const,
             order_hom.has_top_top, order_hom.const_coe_coe],
end

lemma bot_continuous :
  continuous (âŠ¥ : Î± â†’o Î²) :=
begin
  rw â† Sup_empty,
  exact Sup_continuous _ (Î» f hf, hf.elim),
end

end complete_lattice

namespace omega_complete_partial_order

variables {Î± : Type u} {Î±' : Type*} {Î² : Type v} {Î²' : Type*} {Î³ : Type*} {Ï† : Type*}

variables [omega_complete_partial_order Î±] [omega_complete_partial_order Î²]
variables [omega_complete_partial_order Î³] [omega_complete_partial_order Ï†]
variables [omega_complete_partial_order Î±'] [omega_complete_partial_order Î²']

namespace order_hom

/-- The `Ï‰Sup` operator for monotone functions. -/
@[simps]
protected def Ï‰Sup (c : chain (Î± â†’o Î²)) : Î± â†’o Î² :=
{ to_fun := Î» a, Ï‰Sup (c.map (order_hom.apply a)),
  monotone' := Î» x y h, Ï‰Sup_le_Ï‰Sup_of_le (chain.map_le_map _ $ Î» a, a.monotone h) }

@[simps Ï‰Sup_coe]
instance omega_complete_partial_order : omega_complete_partial_order (Î± â†’o Î²) :=
omega_complete_partial_order.lift order_hom.coe_fn_hom order_hom.Ï‰Sup
  (Î» x y h, h) (Î» c, rfl)

end order_hom

section
variables (Î± Î²)

/-- A monotone function on `Ï‰`-continuous partial orders is said to be continuous
if for every chain `c : chain Î±`, `f (âŠ” i, c i) = âŠ” i, f (c i)`.
This is just the bundled version of `order_hom.continuous`. -/
structure continuous_hom extends order_hom Î± Î² :=
(cont : continuous (order_hom.mk to_fun monotone'))

attribute [nolint doc_blame] continuous_hom.to_order_hom

infixr ` â†’ğ’„ `:25 := continuous_hom -- Input: \r\MIc

instance : has_coe_to_fun (Î± â†’ğ’„ Î²) (Î» _, Î± â†’ Î²) := âŸ¨Î» f, f.to_order_hom.to_funâŸ©

instance : has_coe (Î± â†’ğ’„ Î²) (Î± â†’o Î²) :=
{ coe :=  continuous_hom.to_order_hom }

instance : partial_order (Î± â†’ğ’„ Î²) :=
partial_order.lift (Î» f, f.to_order_hom.to_fun) $ by rintro âŸ¨âŸ¨âŸ©âŸ© âŸ¨âŸ¨âŸ©âŸ© h; congr; exact h

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def continuous_hom.simps.apply (h : Î± â†’ğ’„ Î²) : Î± â†’ Î² := h

initialize_simps_projections continuous_hom
  (to_order_hom_to_fun â†’ apply, -to_order_hom)

end

namespace continuous_hom

theorem congr_fun {f g : Î± â†’ğ’„ Î²} (h : f = g) (x : Î±) : f x = g x :=
congr_arg (Î» h : Î± â†’ğ’„ Î², h x) h

theorem congr_arg (f : Î± â†’ğ’„ Î²) {x y : Î±} (h : x = y) : f x = f y :=
congr_arg (Î» x : Î±, f x) h

protected lemma monotone (f : Î± â†’ğ’„ Î²) : monotone f := f.monotone'

@[mono] lemma apply_mono {f g : Î± â†’ğ’„ Î²} {x y : Î±} (hâ‚ : f â‰¤ g) (hâ‚‚ : x â‰¤ y) : f x â‰¤ g y :=
order_hom.apply_mono (show (f : Î± â†’o Î²) â‰¤ g, from hâ‚) hâ‚‚

lemma ite_continuous' {p : Prop} [hp : decidable p] (f g : Î± â†’ Î²)
  (hf : continuous' f) (hg : continuous' g) : continuous' (Î» x, if p then f x else g x) :=
by split_ifs; simp *

lemma Ï‰Sup_bind {Î² Î³ : Type v} (c : chain Î±) (f : Î± â†’o part Î²) (g : Î± â†’o Î² â†’ part Î³) :
  Ï‰Sup (c.map (f.bind g)) = Ï‰Sup (c.map f) >>= Ï‰Sup (c.map g) :=
begin
  apply eq_of_forall_ge_iff, intro x,
  simp only [Ï‰Sup_le_iff, part.bind_le, chain.mem_map_iff, and_imp, order_hom.bind_coe,
    exists_imp_distrib],
  split; intro h''',
  { intros b hb, apply Ï‰Sup_le _ _ _,
    rintros i y hy, simp only [part.mem_Ï‰Sup] at hb,
    rcases hb with âŸ¨j,hbâŸ©, replace hb := hb.symm,
    simp only [part.eq_some_iff, chain.map_coe, function.comp_app, order_hom.apply_coe]
      at hy hb,
    replace hb : b âˆˆ f (c (max i j))   := f.mono (c.mono (le_max_right i j)) _ hb,
    replace hy : y âˆˆ g (c (max i j)) b := g.mono (c.mono (le_max_left i j)) _ _ hy,
    apply h''' (max i j),
    simp only [exists_prop, part.bind_eq_bind, part.mem_bind_iff, chain.map_coe,
               function.comp_app, order_hom.bind_coe],
    exact âŸ¨_,hb,hyâŸ©, },
  { intros i, intros y hy,
    simp only [exists_prop, part.bind_eq_bind, part.mem_bind_iff, chain.map_coe,
               function.comp_app, order_hom.bind_coe] at hy,
    rcases hy with âŸ¨b,hbâ‚€,hbâ‚âŸ©,
    apply h''' b _,
    { apply le_Ï‰Sup (c.map g) _ _ _ hbâ‚ },
    { apply le_Ï‰Sup (c.map f) i _ hbâ‚€ } },
end

lemma bind_continuous' {Î² Î³ : Type v} (f : Î± â†’ part Î²) (g : Î± â†’ Î² â†’ part Î³) :
  continuous' f â†’ continuous' g â†’
  continuous' (Î» x, f x >>= g x)
| âŸ¨hf,hf'âŸ© âŸ¨hg,hg'âŸ© :=
continuous.of_bundled' (order_hom.bind âŸ¨f,hfâŸ© âŸ¨g,hgâŸ©)
  (by intro c; rw [Ï‰Sup_bind, â† hf', â† hg']; refl)

lemma map_continuous' {Î² Î³ : Type v} (f : Î² â†’ Î³) (g : Î± â†’ part Î²)
  (hg : continuous' g) :
  continuous' (Î» x, f <$> g x) :=
by simp only [map_eq_bind_pure_comp];
   apply bind_continuous' _ _ hg;
   apply const_continuous'

lemma seq_continuous' {Î² Î³ : Type v} (f : Î± â†’ part (Î² â†’ Î³)) (g : Î± â†’ part Î²)
  (hf : continuous' f) (hg : continuous' g) :
  continuous' (Î» x, f x <*> g x) :=
by simp only [seq_eq_bind_map];
   apply bind_continuous' _ _ hf;
   apply pi.omega_complete_partial_order.flipâ‚‚_continuous'; intro;
   apply map_continuous' _ _ hg

lemma continuous (F : Î± â†’ğ’„ Î²) (C : chain Î±) : F (Ï‰Sup C) = Ï‰Sup (C.map F) :=
continuous_hom.cont _ _

/-- Construct a continuous function from a bare function, a continuous function, and a proof that
they are equal. -/
@[simps, reducible]
def of_fun (f : Î± â†’ Î²) (g : Î± â†’ğ’„ Î²) (h : f = g) : Î± â†’ğ’„ Î² :=
by refine {to_order_hom := {to_fun := f, ..}, ..}; subst h; rcases g with âŸ¨âŸ¨âŸ©âŸ©; assumption

/-- Construct a continuous function from a monotone function with a proof of continuity. -/
@[simps, reducible]
def of_mono (f : Î± â†’o Î²) (h : âˆ€ c : chain Î±, f (Ï‰Sup c) = Ï‰Sup (c.map f)) : Î± â†’ğ’„ Î² :=
{ to_fun := f,
  monotone' := f.monotone,
  cont := h }

/-- The identity as a continuous function. -/
@[simps]
def id : Î± â†’ğ’„ Î± :=
of_mono order_hom.id continuous_id

/-- The composition of continuous functions. -/
@[simps]
def comp (f : Î² â†’ğ’„ Î³) (g : Î± â†’ğ’„ Î²) : Î± â†’ğ’„ Î³ :=
of_mono (order_hom.comp (â†‘f) (â†‘g)) (continuous_comp _ _ g.cont f.cont)

@[ext]
protected lemma ext (f g : Î± â†’ğ’„ Î²) (h : âˆ€ x, f x = g x) : f = g :=
by cases f; cases g; congr; ext; apply h

protected lemma coe_inj (f g : Î± â†’ğ’„ Î²) (h : (f : Î± â†’ Î²) = g) : f = g :=
continuous_hom.ext _ _ $ _root_.congr_fun h

@[simp]
lemma comp_id (f : Î² â†’ğ’„ Î³) : f.comp id = f := by ext; refl

@[simp]
lemma id_comp (f : Î² â†’ğ’„ Î³) : id.comp f = f := by ext; refl

@[simp]
lemma comp_assoc (f : Î³ â†’ğ’„ Ï†) (g : Î² â†’ğ’„ Î³) (h : Î± â†’ğ’„ Î²) : f.comp (g.comp h) = (f.comp g).comp h :=
by ext; refl

@[simp]
lemma coe_apply (a : Î±) (f : Î± â†’ğ’„ Î²) : (f : Î± â†’o Î²) a = f a := rfl

/-- `function.const` is a continuous function. -/
def const (x : Î²) : Î± â†’ğ’„ Î² :=
of_mono (order_hom.const _ x) (continuous_const x)

@[simp] theorem const_apply (f : Î²) (a : Î±) : const f a = f := rfl

instance [inhabited Î²] : inhabited (Î± â†’ğ’„ Î²) :=
âŸ¨ const (default Î²) âŸ©

namespace prod

/-- The application of continuous functions as a monotone function.

(It would make sense to make it a continuous function, but we are currently constructing a
`omega_complete_partial_order` instance for `Î± â†’ğ’„ Î²`, and we cannot use it as the domain or image
of a continuous function before we do.) -/
@[simps]
def apply : (Î± â†’ğ’„ Î²) Ã— Î± â†’o Î² :=
{ to_fun := Î» f, f.1 f.2,
  monotone' := Î» x y h, by dsimp; transitivity y.fst x.snd; [apply h.1, apply y.1.monotone h.2] }

end prod

/-- The map from continuous functions to monotone functions is itself a monotone function. -/
@[simps]
def to_mono : (Î± â†’ğ’„ Î²) â†’o (Î± â†’o Î²) :=
{ to_fun := Î» f, f,
  monotone' := Î» x y h, h }

/-- When proving that a chain of applications is below a bound `z`, it suffices to consider the
functions and values being selected from the same index in the chains.

This lemma is more specific than necessary, i.e. `câ‚€` only needs to be a
chain of monotone functions, but it is only used with continuous functions. -/
@[simp]
lemma forall_forall_merge (câ‚€ : chain (Î± â†’ğ’„ Î²)) (câ‚ : chain Î±) (z : Î²) :
  (âˆ€ (i j : â„•), (câ‚€ i) (câ‚ j) â‰¤ z) â†” âˆ€ (i : â„•), (câ‚€ i) (câ‚ i) â‰¤ z :=
begin
  split; introv h,
  { apply h },
  { apply le_trans _ (h (max i j)),
    transitivity câ‚€ i (câ‚ (max i j)),
    { apply (câ‚€ i).monotone, apply câ‚.monotone, apply le_max_right },
    { apply câ‚€.monotone, apply le_max_left } }
end

@[simp]
lemma forall_forall_merge' (câ‚€ : chain (Î± â†’ğ’„ Î²)) (câ‚ : chain Î±) (z : Î²) :
  (âˆ€ (j i : â„•), (câ‚€ i) (câ‚ j) â‰¤ z) â†” âˆ€ (i : â„•), (câ‚€ i) (câ‚ i) â‰¤ z :=
by rw [forall_swap,forall_forall_merge]

/-- The `Ï‰Sup` operator for continuous functions, which takes the pointwise countable supremum
of the functions in the `Ï‰`-chain. -/
@[simps]
protected def Ï‰Sup (c : chain (Î± â†’ğ’„ Î²)) : Î± â†’ğ’„ Î² :=
continuous_hom.of_mono (Ï‰Sup $ c.map to_mono)
begin
  intro c',
  apply eq_of_forall_ge_iff, intro z,
  simp only [Ï‰Sup_le_iff, (c _).continuous, chain.map_coe, order_hom.apply_coe,
    to_mono_coe, coe_apply, order_hom.omega_complete_partial_order_Ï‰Sup_coe,
    forall_forall_merge, forall_forall_merge', (âˆ˜), function.eval],
end

@[simps Ï‰Sup]
instance : omega_complete_partial_order (Î± â†’ğ’„ Î²) :=
omega_complete_partial_order.lift continuous_hom.to_mono continuous_hom.Ï‰Sup
  (Î» x y h, h) (Î» c, rfl)

lemma Ï‰Sup_def (c : chain (Î± â†’ğ’„ Î²)) (x : Î±) : Ï‰Sup c x = continuous_hom.Ï‰Sup c x := rfl

lemma Ï‰Sup_Ï‰Sup (câ‚€ : chain (Î± â†’ğ’„ Î²)) (câ‚ : chain Î±) :
  Ï‰Sup câ‚€ (Ï‰Sup câ‚) = Ï‰Sup (continuous_hom.prod.apply.comp $ câ‚€.zip câ‚) :=
begin
  apply eq_of_forall_ge_iff, intro z,
  simp only [Ï‰Sup_le_iff, (câ‚€ _).continuous, chain.map_coe, to_mono_coe, coe_apply,
    order_hom.omega_complete_partial_order_Ï‰Sup_coe, Ï‰Sup_def, forall_forall_merge,
    chain.zip_coe, order_hom.prod_map_coe, order_hom.diag_coe, prod.map_mk,
    order_hom.apply_coe, function.comp_app, prod.apply_coe,
    order_hom.comp_coe, Ï‰Sup_apply, function.eval],
end

/-- A family of continuous functions yields a continuous family of functions. -/
@[simps]
def flip {Î± : Type*} (f : Î± â†’ Î² â†’ğ’„ Î³) : Î² â†’ğ’„ Î± â†’ Î³ :=
{ to_fun := Î» x y, f y x,
  monotone' := Î» x y h a, (f a).monotone h,
  cont := by intro; ext; change f x _ = _; rw [(f x).continuous ]; refl, }

/-- `part.bind` as a continuous function. -/
@[simps { rhs_md := reducible }]
noncomputable def bind {Î² Î³ : Type v}
  (f : Î± â†’ğ’„ part Î²) (g : Î± â†’ğ’„ Î² â†’ part Î³) : Î± â†’ğ’„ part Î³ :=
of_mono (order_hom.bind (â†‘f) (â†‘g)) $ Î» c, begin
  rw [order_hom.bind, â† order_hom.bind, Ï‰Sup_bind, â† f.continuous, â† g.continuous],
  refl
end

/-- `part.map` as a continuous function. -/
@[simps {rhs_md := reducible}]
noncomputable def map {Î² Î³ : Type v} (f : Î² â†’ Î³) (g : Î± â†’ğ’„ part Î²) : Î± â†’ğ’„ part Î³ :=
of_fun (Î» x, f <$> g x) (bind g (const (pure âˆ˜ f))) $
by ext; simp only [map_eq_bind_pure_comp, bind_apply, order_hom.bind_coe, const_apply,
  order_hom.const_coe_coe, coe_apply]

/-- `part.seq` as a continuous function. -/
@[simps {rhs_md := reducible}]
noncomputable def seq {Î² Î³ : Type v} (f : Î± â†’ğ’„ part (Î² â†’ Î³)) (g : Î± â†’ğ’„ part Î²) :
  Î± â†’ğ’„ part Î³ :=
of_fun (Î» x, f x <*> g x) (bind f $ (flip $ _root_.flip map g))
  (by ext; simp only [seq_eq_bind_map, flip, part.bind_eq_bind, map_apply, part.mem_bind_iff,
                      bind_apply, order_hom.bind_coe, coe_apply, flip_apply]; refl)

end continuous_hom

end omega_complete_partial_order
