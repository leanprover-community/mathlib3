/-
Copyright (c) 2022 Ian Jauslin and Alex Kontorovich. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ian Jauslin, Alex Kontorovich
-/
import measure_theory.measure.probability_measure

/-!
# Bell's Inequality

This file proves Bell's Inequality as a probabilistic statement in several forms.

Bell's inequality is the cornerstone of Bell's theorem, which states that quantum mechanics is a
non-local theory. The proof of Bell's theorem, established by J.S. Bell in 1964, first uses the
Einstein-Podolsky-Rosen argument to establish that either quantum mechanics is non-local, or all
quantum observables simultaneously have values (in other words, quantum mechanics is a
non-contextual hidden variable theory). Bell's inequality is a necessary condition for all the
observables in an abstract probabilistic theory to simultaneously have values. By showing that, in
quantum mechanics, Bell's inequality is violated, Bell proved that quantum mechanics is non-local.

The violation of Bell's inequality has also been verified experimentally in many different setups.

## Bell's 1964 inequality

We first prove Bell's original statement of the inequality, which was published in 1964, and will
thus be called `bell_inequality_1964` in this file.

Consider two observers, $$A$$ and $$B$$, performing three measurements that take values in
$${-1, 1}$$. Assuming perfect anticorrelation of their measurements, we can represent the outcomes
by $$a, b, c$$ (for $$A$$) and $$-a, -b, -c$$ (for $$B$$). Bell's inequality states that
$$|ğ”¼[a * -b] - ğ”¼[a * -c]| â‰¤ 1 + ğ”¼[b * -c]$$.

## TODO

J.S. Bell generalized the inequality in 1975 to include more probabilistic theories. In particular,
the 1975 statement does not require observables to simultaneously have values. Instead, it is solely
based on the requirement of locality. The 1975 inequality thus gives a more direct proof of Bell's
theorem.

## References

* [J.S. Bell, *On the Einstein Podolsky Rosen Paradox*, 1964][MR3790629]
* [J.S. Bell, *The theory of local beables*, 1975,
  reproduced in chapter 7 of *Speakable and unspeakable in quantum mechanics*][MR915338]

## See also

`CHSH_inequality_of_comm` is a star-algebra version of Bell's inequality.
-/

open filter measure_theory

namespace bell_inequality
variables {Î© : Type*} [measurable_space Î©] {â„™ : measure Î©} [is_probability_measure â„™] {f g : Î© â†’ â„¤Ë£}
  {a b c : Î© â†’ â„¤Ë£}

private lemma norm_aux (a : â„¤Ë£) : â€–(a : â„)â€– â‰¤ 1 :=
by obtain rfl | rfl := int.units_eq_one_or a; simp

/-- The precise version of the CHSH inequality we need. -/
private lemma CHSH_aux (a b c : â„¤Ë£) : (a : â„) * -b - a * -c â‰¤ 1 + b * -c :=
by obtain rfl | rfl := int.units_eq_one_or a; obtain rfl | rfl := int.units_eq_one_or b;
  obtain rfl | rfl := int.units_eq_one_or c; norm_num

private lemma ae_strongly_measurable_aux (hf : ae_measurable f â„™) :
  ae_strongly_measurable (Î» Ï‰, (f Ï‰ : â„)) â„™ :=
begin
  refine (measurable.comp_ae_measurable (Î» s hs, _) hf).ae_strongly_measurable,
  exact âŸ¨coe â»Â¹' s, trivial, rflâŸ©,
end

private lemma integrable_aux (hf : ae_measurable f â„™) : integrable (Î» Ï‰, (f Ï‰ : â„)) â„™ :=
âŸ¨ae_strongly_measurable_aux hf, has_finite_integral_of_bounded $ eventually_of_forall $ Î» _,
  norm_aux _âŸ©

private lemma integrable_mul_aux (hf : ae_measurable f â„™) (hg : ae_measurable g â„™) :
  integrable (Î» Ï‰, (f Ï‰ * g Ï‰ : â„)) â„™ :=
(integrable_aux hg).bdd_mul (ae_strongly_measurable_aux hf) âŸ¨1, Î» _, norm_aux _âŸ©

/-- Given three random variables `a b c` taking values in `Â±1`, we have that
`ğ”¼[a * -b] - ğ”¼[a * -c] â‰¤ 1 + ğ”¼[b * -c]`. -/
private lemma bell_aux (ha : ae_measurable a â„™) (hb : ae_measurable b â„™) (hc : ae_measurable c â„™) :
  (âˆ« Ï‰, a Ï‰ * -b Ï‰ âˆ‚â„™ : â„) - âˆ« Ï‰, a Ï‰ * -c Ï‰ âˆ‚â„™ â‰¤ 1 + âˆ« Ï‰, b Ï‰ * -c Ï‰ âˆ‚â„™ :=
begin
  have integral_one : âˆ« Ï‰, (1 : â„) âˆ‚â„™ = 1, by simp,
  have anticorrelation : (âˆ« Ï‰, c Ï‰ * c Ï‰ âˆ‚â„™ : â„) = 1, by simp [â†units.coe_mul, â†int.cast_mul],
  rw [â†integral_one, â†integral_sub, â†integral_add],
  refine integral_mono _ _ (Î» _, CHSH_aux _ _ _),
  all_goals -- discharge all the integrability hypotheses
  { try { simp only [coe_coe, â†int.cast_neg, â†units.coe_neg] },
    apply_rules [integrable.add, integrable.neg, integrable_mul_aux, integrable_const,
      ae_measurable.neg, ha, hb, hc] },
end

/-- **Bell's inequality (1964 version)**. Given three random variables `a b c` taking values in
`Â±1`, we have that `|ğ”¼[a * -b] - ğ”¼[a * -c]| â‰¤ 1 + ğ”¼[b * -c]`. -/
theorem bell_inequality_1964 (ha : ae_measurable a â„™) (hb : ae_measurable b â„™)
  (hc : ae_measurable c â„™) :
  |(âˆ« Ï‰, a Ï‰ * -b Ï‰ âˆ‚â„™ - âˆ« Ï‰, a Ï‰ * -c Ï‰ âˆ‚â„™ : â„)| â‰¤ 1 + âˆ« Ï‰, b Ï‰ * -c Ï‰ âˆ‚â„™ :=
abs_sub_le_iff.2 âŸ¨bell_aux ha hb hc, (bell_aux ha hc hb).trans_eq $ by simp_rw [mul_neg, mul_comm]âŸ©

end bell_inequality
