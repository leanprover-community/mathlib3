/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.martingale.borel_cantelli
import probability.independence

/-!

# The second Borel-Cantelli lemma

This file contains the second Borel-Cantelli lemma which states that, given a sequence of
independent sets `(sâ‚™)` in a probability space, if `âˆ‘ n, Î¼ sâ‚™ = âˆ`, then the limsup of `sâ‚™` has
measure 1. We employ a proof using LÃ©vy's generalized Borel-Cantelli by choosing an appropriate
filtration.

## Main result

- `probability_theory.measure_limsup_eq_one`: the second Borel-Cantelli lemma.

-/

open_locale measure_theory probability_theory ennreal big_operators topological_space

open measure_theory probability_theory measurable_space topological_space

namespace probability_theory

variables {Î© : Type*} {m0 : measurable_space Î©} {Î¼ : measure Î©} {s : â„• â†’ set Î©}

section borel_cantelli

section move

variables {Î² : Type*} [mÎ² : measurable_space Î²]

lemma measurable.comap_le_of_measurable {f : Î© â†’ Î²} (hf : measurable f) :
  mÎ².comap f â‰¤ m0 :=
begin
  rintro s âŸ¨t, ht, rflâŸ©,
  exact hf ht
end

lemma measurable_space.comap_measurable (f : Î© â†’ Î²) :
  measurable[mÎ².comap f] f :=
Î» s hs, âŸ¨s, hs, rflâŸ©

variables [normed_add_comm_group Î²] [borel_space Î²]

variables [is_probability_measure Î¼]

lemma Indep_fun.indep_comap_succ_natural {f : â„• â†’ Î© â†’ Î²}
  (hf : âˆ€ (i : â„•), strongly_measurable (f i))
  (hfi : Indep_fun (Î» (n : â„•), mÎ²) f Î¼) (n : â„•) :
  indep (measurable_space.comap (f (n + 1)) mÎ²) (filtration.natural f hf n) Î¼ :=
begin
  suffices : indep (â¨† k âˆˆ {n + 1}, measurable_space.comap (f k) mÎ²)
    (â¨† k âˆˆ {k | k â‰¤ n}, measurable_space.comap (f k) mÎ²) Î¼,
  { rwa supr_singleton at this },
  refine indep_supr_of_disjoint (Î» k, (hf k).measurable.comap_le_of_measurable) hfi _,
  simp
end

lemma Indep_fun.condexp_succ_natrual_ae_eq
  [second_countable_topology Î²] [complete_space Î²] [normed_space â„ Î²]
  {f : â„• â†’ Î© â†’ Î²} (hf : âˆ€ i, strongly_measurable (f i)) (hfi : Indep_fun (Î» n, mÎ²) f Î¼) (n : â„•) :
  Î¼[f (n + 1) | filtration.natural f hf n] =áµ[Î¼] Î» Ï‰, Î¼[f (n + 1)] :=
condexp_indep_eq (hf $ n + 1).measurable.comap_le_of_measurable
  (filtration.le _ _) (measurable_space.comap_measurable $ f $ n + 1).strongly_measurable
  (hfi.indep_comap_succ_natural hf n)

end move

lemma Indep_set.Indep_fun_indicator (hs : Indep_set s Î¼) :
  Indep_fun (Î» n, real.measurable_space) (Î» n, (s n).indicator (Î» Ï‰, 1)) Î¼ :=
begin
  classical,
  rw Indep_fun_iff_measure_inter_preimage_eq_mul,
  rintro S Ï€ hÏ€,
  simp_rw set.indicator_const_preimage_eq_union,
  refine @hs S (Î» i, ite (1 âˆˆ Ï€ i) (s i) âˆ… âˆª ite ((0 : â„) âˆˆ Ï€ i) (s i)á¶œ âˆ…) _,
  rintros i hi,
  simp only [set.mem_set_of],
  split_ifs,
  { simp only [set.union_compl_self, measurable_set.univ] },
  { rw set.union_empty,
    exact measurable_set_generate_from (set.mem_singleton _) },
  { rw set.empty_union,
    exact (measurable_set_generate_from (set.mem_singleton _)).compl },
  { simp only [set.empty_union, measurable_set.empty] }
end

variables [is_probability_measure Î¼]

lemma Indep_set.condexp_indicator_filtration_of_set_ae_eq
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) (n : â„•) :
  Î¼[(s (n + 1)).indicator (Î» Ï‰, 1 : Î© â†’ â„) | filtration_of_set hsm n] =áµ[Î¼]
    Î» Ï‰, (Î¼ (s (n + 1))).to_real :=
begin
  rw filtration.filtration_of_set_eq_natural hsm,
  refine (Indep_fun.condexp_succ_natrual_ae_eq _ hs.Indep_fun_indicator n).trans _,
  simp only [integral_indicator_const _ (hsm _), algebra.id.smul_eq_mul, mul_one],
end

lemma Indep_set.condexp_indicator_filtration_of_set_ae_eq'
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) :
  âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, Î¼[(s (n + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm n] Ï‰ =
    (Î¼ (s (n + 1))).to_real :=
ae_all_iff.2 (hs.condexp_indicator_filtration_of_set_ae_eq hsm)

open filter

/-- **The second Borel-Cantelli lemma**: Given a sequence of independent sets `(sâ‚™)` such that
`âˆ‘ n, Î¼ sâ‚™ = âˆ`, `limsup sâ‚™` has measure 1. -/
lemma measure_limsup_eq_one
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) (hs' : âˆ‘' n, Î¼ (s n) = âˆ) :
  Î¼ (limsup s at_top) = 1 :=
begin
  rw measure_congr (eventually_eq_set.2 (ae_mem_limsup_at_top_iff Î¼ $
    measurable_set_filtration_of_set' hsm) :
      (limsup s at_top : set Î©) =áµ[Î¼] {Ï‰ | tendsto (Î» n, âˆ‘ k in finset.range n,
        Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm k] Ï‰) at_top at_top}),
  suffices : {Ï‰ | tendsto (Î» n, âˆ‘ k in finset.range n,
    Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm k] Ï‰) at_top at_top} =áµ[Î¼] set.univ,
  { rw [measure_congr this, measure_univ] },
  filter_upwards [hs.condexp_indicator_filtration_of_set_ae_eq' hsm] with Ï‰ hÏ‰,
  refine eq_true_intro (_ : tendsto _ _ _),
  simp_rw hÏ‰,
  have htends : tendsto (Î» n, âˆ‘ k in finset.range n, Î¼ (s (k + 1))) at_top (ğ“ âˆ),
  { rw â† ennreal.tsum_add_one_eq_top hs' (measure_ne_top _ _),
    exact ennreal.tendsto_nat_tsum _ },
  rw ennreal.tendsto_nhds_top_iff_nnreal at htends,
  refine tendsto_at_top_at_top_of_monotone' _ _,
  { refine monotone_nat_of_le_succ (Î» n, _),
    rw [â† sub_nonneg, finset.sum_range_succ_sub_sum],
    exact ennreal.to_real_nonneg },
  { rintro âŸ¨B, hBâŸ©,
    refine not_eventually.2 _ (htends B.to_nnreal),
    refine frequently_of_forall (Î» n, not_lt.2 _),
    rw mem_upper_bounds at hB,
    specialize hB (âˆ‘ (k : â„•) in finset.range n, Î¼ (s (k + 1))).to_real _,
    { refine âŸ¨n, _âŸ©,
      rw ennreal.to_real_sum,
      exact Î» _ _, measure_ne_top _ _ },
    { rw â† ennreal.to_real_le_to_real (ennreal.sum_lt_top _).ne ennreal.coe_ne_top,
      { exact hB.trans (by simp) },
      { exact Î» _ _, measure_ne_top _ _ } } }
end

end borel_cantelli

end probability_theory
