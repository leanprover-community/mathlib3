/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.martingale.borel_cantelli
import probability.independence

/-!

# The second Borel-Cantelli lemma

This file contains the second Borel-Cantelli lemma which states that, given a sequence of
independent sets `(sâ‚™)` in a probability space, if `âˆ‘ n, Î¼ sâ‚™ = âˆ`, then the limsup of `sâ‚™` has
measure 1. We employ a proof using LÃ©vy's generalized Borel-Cantelli by choosing an appropriate
filtration.

## Main result

- `probability_theory.measure_limsup_eq_one`: the second Borel-Cantelli lemma.

-/

open_locale measure_theory probability_theory ennreal big_operators topological_space

open measure_theory probability_theory measurable_space

namespace probability_theory

variables {Î© : Type*} {m0 : measurable_space Î©} {Î¼ : measure Î©} {s : â„• â†’ set Î©}

section pi_system

lemma Indep_set.indep_generate_from_le [is_probability_measure Î¼]
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) (n : â„•) :
  indep (generate_from {s (n + 1)}) (generate_from {t | âˆƒ k â‰¤ n, s k = t}) Î¼ :=
begin
  have := hs.indep_generate_from_of_disjoint _ hsm {n + 1} {k | k â‰¤ n} (Î» x âŸ¨hxâ‚, hxâ‚‚âŸ©,
    not_lt.2 (set.mem_set_of.1 hxâ‚‚) ((set.mem_singleton_iff.1 hxâ‚).symm â–¸ nat.lt_succ_self n)),
  convert this,
  simp only [set.mem_singleton_iff, exists_prop, exists_eq_left, set.set_of_eq_eq_singleton'],
end

end pi_system

section borel_cantelli

/-- Given a sequence of measurable sets `(sâ‚™)`, `filtration_of_set` is the smallest filtration
such that `sâ‚™` is measurable with respect to the `n`-the sub-Ïƒ-algebra in `filtration_of_set`. -/
def filtration_of_set {s : â„• â†’ set Î©} (hsm : âˆ€ n, measurable_set (s n)) : filtration â„• m0 :=
{ seq := Î» n, generate_from {t | âˆƒ k â‰¤ n, s k = t},
  mono' := Î» n m hnm, generate_from_mono (Î» t âŸ¨k, hkâ‚, hkâ‚‚âŸ©, âŸ¨k, hkâ‚.trans hnm, hkâ‚‚âŸ©),
  le' := Î» n, generate_from_le (Î» t âŸ¨k, hkâ‚, hkâ‚‚âŸ©, hkâ‚‚ â–¸ hsm k) }

lemma measurable_set_filtration_of_set {s : â„• â†’ set Î©}
  (hsm : âˆ€ n, measurable_set[m0] (s n)) (n : â„•) {k : â„•} (hk : k â‰¤ n) :
  measurable_set[filtration_of_set hsm n] (s k) :=
measurable_set_generate_from âŸ¨k, hk, rflâŸ©

lemma measurable_set_filtration_of_set' {s : â„• â†’ set Î©}
  (hsm : âˆ€ n, measurable_set[m0] (s n)) (n : â„•) :
  measurable_set[filtration_of_set hsm n] (s n) :=
measurable_set_filtration_of_set hsm n le_rfl

variables [is_probability_measure Î¼]

lemma Indep_set.filtration_of_set_indep
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) (n : â„•) :
  indep (generate_from {s (n + 1)}) (filtration_of_set hsm n) Î¼ :=
hs.indep_generate_from_le hsm _

lemma Indep_set.condexp_indicator_filtration_of_set_ae_eq
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) (n : â„•) :
  Î¼[(s (n + 1)).indicator (Î» Ï‰, 1 : Î© â†’ â„) | filtration_of_set hsm n] =áµ[Î¼]
    Î» Ï‰, (Î¼ (s (n + 1))).to_real :=
begin
  refine (condexp_indep_eq (generate_from_le
    (Î» t ht, (set.mem_singleton_iff.1 ht).symm â–¸ hsm _) : generate_from {s (n + 1)} â‰¤ m0)
    ((filtration_of_set hsm).le n)
    (strongly_measurable_one.indicator (measurable_set_generate_from (set.mem_singleton _)))
    (hs.indep_generate_from_le hsm n)).trans (ae_of_all Î¼ (Î» Ï‰, _)),
  convert integral_indicator_const (1 : â„) (hsm (n + 1)),
  rw [smul_eq_mul, mul_one],
end

lemma Indep_set.condexp_indicator_filtration_of_set_ae_eq'
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) :
  âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, Î¼[(s (n + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm n] Ï‰ =
    (Î¼ (s (n + 1))).to_real :=
ae_all_iff.2 (hs.condexp_indicator_filtration_of_set_ae_eq hsm)

open filter

/-- **The second Borel-Cantelli lemma**: Given a sequence of independent sets `(sâ‚™)` such that
`âˆ‘ n, Î¼ sâ‚™ = âˆ`, `limsup sâ‚™` has measure 1. -/
lemma measure_limsup_eq_one
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) (hs' : âˆ‘' n, Î¼ (s n) = âˆ) :
  Î¼ (limsup s at_top) = 1 :=
begin
  rw measure_congr (eventually_eq_set.2 (ae_mem_limsup_at_top_iff Î¼ $
    measurable_set_filtration_of_set' hsm) :
      (limsup s at_top : set Î©) =áµ[Î¼] {Ï‰ | tendsto (Î» n, âˆ‘ k in finset.range n,
        Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm k] Ï‰) at_top at_top}),
  suffices : {Ï‰ | tendsto (Î» n, âˆ‘ k in finset.range n,
    Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm k] Ï‰) at_top at_top} =áµ[Î¼] set.univ,
  { rw [measure_congr this, measure_univ] },
  filter_upwards [hs.condexp_indicator_filtration_of_set_ae_eq' hsm] with Ï‰ hÏ‰,
  refine eq_true_intro (_ : tendsto _ _ _),
  simp_rw hÏ‰,
  have htends : tendsto (Î» n, âˆ‘ k in finset.range n, Î¼ (s (k + 1))) at_top (ğ“ âˆ),
  { rw â† ennreal.tsum_add_one_eq_top hs' (measure_ne_top _ _),
    exact ennreal.tendsto_nat_tsum _ },
  rw ennreal.tendsto_nhds_top_iff_nnreal at htends,
  refine tendsto_at_top_at_top_of_monotone' _ _,
  { refine monotone_nat_of_le_succ (Î» n, _),
    rw [â† sub_nonneg, finset.sum_range_succ_sub_sum],
    exact ennreal.to_real_nonneg },
  { rintro âŸ¨B, hBâŸ©,
    refine not_eventually.2 _ (htends B.to_nnreal),
    refine frequently_of_forall (Î» n, not_lt.2 _),
    rw mem_upper_bounds at hB,
    specialize hB (âˆ‘ (k : â„•) in finset.range n, Î¼ (s (k + 1))).to_real _,
    { refine âŸ¨n, _âŸ©,
      rw ennreal.to_real_sum,
      exact Î» _ _, measure_ne_top _ _ },
    { rw â† ennreal.to_real_le_to_real (ennreal.sum_lt_top _).ne ennreal.coe_ne_top,
      { exact hB.trans (by simp) },
      { exact Î» _ _, measure_ne_top _ _ } } }
end

end borel_cantelli

end probability_theory
