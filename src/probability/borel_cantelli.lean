/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.martingale.borel_cantelli
import probability.conditional_expectation
import probability.independence

/-!

# The second Borel-Cantelli lemma

This file contains the second Borel-Cantelli lemma which states that, given a sequence of
independent sets `(sâ‚™)` in a probability space, if `âˆ‘ n, Î¼ sâ‚™ = âˆž`, then the limsup of `sâ‚™` has
measure 1. We employ a proof using LÃ©vy's generalized Borel-Cantelli by choosing an appropriate
filtration.

## Main result

- `probability_theory.measure_limsup_eq_one`: the second Borel-Cantelli lemma.

-/

open_locale measure_theory probability_theory ennreal big_operators topology

open measure_theory probability_theory measurable_space topological_space

namespace probability_theory

variables {Î© : Type*} {m0 : measurable_space Î©} {Î¼ : measure Î©}
  [is_probability_measure Î¼]

section borel_cantelli

variables {Î¹ Î² : Type*} [linear_order Î¹] [mÎ² : measurable_space Î²] [normed_add_comm_group Î²]
  [borel_space Î²] {f : Î¹ â†’ Î© â†’ Î²} {i j : Î¹} {s : Î¹ â†’ set Î©}

lemma Indep_fun.indep_comap_natural_of_lt (hf : âˆ€ i, strongly_measurable (f i))
  (hfi : Indep_fun (Î» i, mÎ²) f Î¼) (hij : i < j) :
  indep (measurable_space.comap (f j) mÎ²) (filtration.natural f hf i) Î¼ :=
begin
  suffices : indep (â¨† k âˆˆ {j}, measurable_space.comap (f k) mÎ²)
    (â¨† k âˆˆ {k | k â‰¤ i}, measurable_space.comap (f k) mÎ²) Î¼,
  { rwa supr_singleton at this },
  exact indep_supr_of_disjoint (Î» k, (hf k).measurable.comap_le) hfi (by simpa),
end

lemma Indep_fun.condexp_natrual_ae_eq_of_lt
  [second_countable_topology Î²] [complete_space Î²] [normed_space â„ Î²]
  (hf : âˆ€ i, strongly_measurable (f i)) (hfi : Indep_fun (Î» i, mÎ²) f Î¼) (hij : i < j) :
  Î¼[f j | filtration.natural f hf i] =áµ[Î¼] Î» Ï‰, Î¼[f j] :=
condexp_indep_eq (hf j).measurable.comap_le (filtration.le _ _)
  (comap_measurable $ f j).strongly_measurable
  (hfi.indep_comap_natural_of_lt hf hij)

lemma Indep_set.condexp_indicator_filtration_of_set_ae_eq
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) (hij : i < j) :
  Î¼[(s j).indicator (Î» Ï‰, 1 : Î© â†’ â„) | filtration_of_set hsm i] =áµ[Î¼] Î» Ï‰, (Î¼ (s j)).to_real :=
begin
  rw filtration.filtration_of_set_eq_natural hsm,
  refine (Indep_fun.condexp_natrual_ae_eq_of_lt _ hs.Indep_fun_indicator hij).trans _,
  { simp only [integral_indicator_const _ (hsm _), algebra.id.smul_eq_mul, mul_one] },
  { apply_instance }
end

open filter

/-- **The second Borel-Cantelli lemma**: Given a sequence of independent sets `(sâ‚™)` such that
`âˆ‘ n, Î¼ sâ‚™ = âˆž`, `limsup sâ‚™` has measure 1. -/
lemma measure_limsup_eq_one {s : â„• â†’ set Î©}
  (hsm : âˆ€ n, measurable_set (s n)) (hs : Indep_set s Î¼) (hs' : âˆ‘' n, Î¼ (s n) = âˆž) :
  Î¼ (limsup s at_top) = 1 :=
begin
  rw measure_congr (eventually_eq_set.2 (ae_mem_limsup_at_top_iff Î¼ $
    measurable_set_filtration_of_set' hsm) :
      (limsup s at_top : set Î©) =áµ[Î¼] {Ï‰ | tendsto (Î» n, âˆ‘ k in finset.range n,
        Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm k] Ï‰) at_top at_top}),
  suffices : {Ï‰ | tendsto (Î» n, âˆ‘ k in finset.range n,
    Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm k] Ï‰) at_top at_top} =áµ[Î¼] set.univ,
  { rw [measure_congr this, measure_univ] },
  have : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, Î¼[(s (n + 1)).indicator (1 : Î© â†’ â„) | filtration_of_set hsm n] Ï‰ = _ :=
    ae_all_iff.2 (Î» n, hs.condexp_indicator_filtration_of_set_ae_eq hsm n.lt_succ_self),
  filter_upwards [this] with Ï‰ hÏ‰,
  refine eq_true_intro (_ : tendsto _ _ _),
  simp_rw hÏ‰,
  have htends : tendsto (Î» n, âˆ‘ k in finset.range n, Î¼ (s (k + 1))) at_top (ð“ âˆž),
  { rw â† ennreal.tsum_add_one_eq_top hs' (measure_ne_top _ _),
    exact ennreal.tendsto_nat_tsum _ },
  rw ennreal.tendsto_nhds_top_iff_nnreal at htends,
  refine tendsto_at_top_at_top_of_monotone' _ _,
  { refine monotone_nat_of_le_succ (Î» n, _),
    rw [â† sub_nonneg, finset.sum_range_succ_sub_sum],
    exact ennreal.to_real_nonneg },
  { rintro âŸ¨B, hBâŸ©,
    refine not_eventually.2 (frequently_of_forall $ Î» n, _) (htends B.to_nnreal),
    rw mem_upper_bounds at hB,
    specialize hB (âˆ‘ (k : â„•) in finset.range n, Î¼ (s (k + 1))).to_real _,
    { refine âŸ¨n, _âŸ©,
      rw ennreal.to_real_sum,
      exact Î» _ _, measure_ne_top _ _ },
    { rw [not_lt, â† ennreal.to_real_le_to_real (ennreal.sum_lt_top _).ne ennreal.coe_ne_top],
      { exact hB.trans (by simp) },
      { exact Î» _ _, measure_ne_top _ _ } } }
end

end borel_cantelli

end probability_theory
