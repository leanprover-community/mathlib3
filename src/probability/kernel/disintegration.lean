/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import probability.kernel.cond_cdf

/-!
# Disintegration of product measures

Let `Ï` be a finite measure on `Î± Ã— â„`. For any measurable space `Î³`, there exists a kernel
`cond_kernel Ï : kernel Î± â„` such that we have a disintegration of the constant kernel from `Î³` with
value `Ï`: `kernel.const Î³ Ï = (kernel.const Î³ Ï.fst) âŠ—â‚– (kernel.prod_mk_left (cond_kernel Ï) Î³)`,
where `Ï.fst` is the marginal measure of `Ï` on `Î±`. In particular,
`Ï = ((kernel.const unit Ï.fst) âŠ—â‚– (kernel.prod_mk_left (cond_kernel Ï) unit)) (unit.star)`

## Main definitions

* `probability_theory.cond_kernel Ï : kernel Î± â„`: conditional kernel described above.

## Main statements

* `probability_theory.kernel.const_eq_comp_prod`:
  `kernel.const Î³ Ï = (kernel.const Î³ Ï.fst) âŠ—â‚– (kernel.prod_mk_left (cond_kernel Ï) Î³)`
* `probability_theory.measure_eq_comp_prod`:
  `Ï = ((kernel.const unit Ï.fst) âŠ—â‚– (kernel.prod_mk_left (cond_kernel Ï) unit)) (unit.star)`
* `probability_theory.lintegral_cond_kernel`:
  `âˆ«â» a, âˆ«â» y, f (a, y) âˆ‚(cond_kernel Ï a) âˆ‚Ï.fst = âˆ«â» x, f x âˆ‚Ï`

## TODO

* We can obtain a disintegration for measures on `Î± Ã— Î©` for a standard Borel space `Î©` by using
  that `Î©` is measurably equivalent to `â„`, `â„¤` or a finite set.
* The finite measure hypothesis can be weakened to Ïƒ-finite. The proof uses the finite case.
* Beyond measures, we can find a disintegration for a kernel `Î± â†’ Î© Ã— Î©'` by applying the
  construction used here for all `a : Î±` and showing additional measurability properties of the map
  we obtain.

-/

open measure_theory set filter

open_locale ennreal measure_theory topology probability_theory

namespace probability_theory

variables {Î± : Type*} {mÎ± : measurable_space Î±}

include mÎ±

/-- Conditional measure on the second space of the product given the value on the first. This is an
auxiliary definition used to build `cond_kernel`. -/
noncomputable def cond_measure (Ï : measure (Î± Ã— â„)) (a : Î±) : measure â„ := (cond_cdf Ï a).measure

lemma cond_measure_Iic (Ï : measure (Î± Ã— â„)) (a : Î±) (x : â„) :
  cond_measure Ï a (Iic x) = ennreal.of_real (cond_cdf Ï a x) :=
begin
  rw [cond_measure, â† sub_zero (cond_cdf Ï a x)],
  exact stieltjes_function.measure_Iic _ (tendsto_cond_cdf_at_bot Ï a) _,
end

lemma cond_measure_univ (Ï : measure (Î± Ã— â„)) (a : Î±) :
  cond_measure Ï a univ = 1 :=
begin
  rw [â† ennreal.of_real_one, â† sub_zero (1 : â„)],
  exact stieltjes_function.measure_univ _ (tendsto_cond_cdf_at_bot Ï a)
    (tendsto_cond_cdf_at_top Ï a),
end

instance (Ï : measure (Î± Ã— â„)) (a : Î±) : is_probability_measure (cond_measure Ï a) :=
âŸ¨cond_measure_univ Ï aâŸ©

/-- The function `a â†¦ cond_measure Ï a` is measurable. This allows us to build a kernel from these
measures. -/
lemma measurable_cond_measure (Ï : measure (Î± Ã— â„)) :
  measurable (cond_measure Ï) :=
begin
  rw measure.measurable_measure,
  refine Î» s hs, measurable_space.induction_on_inter
    real.borel_eq_generate_from_Iic real.is_pi_system_Iic _ _ _ _ hs,
  { simp only [measure_empty, measurable_const], },
  { rintros S âŸ¨u, rflâŸ©,
    simp_rw cond_measure_Iic Ï _ u,
    exact (measurable_cond_cdf Ï u).ennreal_of_real, },
  { intros t ht ht_cd_meas,
    have : (Î» a, cond_measure Ï a tá¶œ) = (Î» a, cond_measure Ï a univ) - (Î» a, cond_measure Ï a t),
    { ext1 a,
      rw [measure_compl ht (measure_ne_top (cond_measure Ï a) _), pi.sub_apply], },
    simp_rw [this, cond_measure_univ Ï],
    exact measurable.sub measurable_const ht_cd_meas, },
  { intros f hf_disj hf_meas hf_cd_meas,
    simp_rw measure_Union hf_disj hf_meas,
    exact measurable.ennreal_tsum hf_cd_meas, },
end

/-- Conditional measure on the second space of the product given the value on the first, as a
kernel. -/
noncomputable
def cond_kernel (Ï : measure (Î± Ã— â„)) : kernel Î± â„ :=
{ val := Î» a, cond_measure Ï a,
  property := measurable_cond_measure Ï }

instance (Ï : measure (Î± Ã— â„)) : is_markov_kernel (cond_kernel Ï) :=
âŸ¨Î» a, by { rw cond_kernel, apply_instance, } âŸ©

lemma cond_kernel_Iic (Ï : measure (Î± Ã— â„)) (a : Î±) (x : â„) :
  cond_kernel Ï a (Iic x) = ennreal.of_real (cond_cdf Ï a x) :=
cond_measure_Iic Ï a x

lemma set_lintegral_cond_kernel_Iic (Ï : measure (Î± Ã— â„)) [is_finite_measure Ï] (x : â„)
  {s : set Î±} (hs : measurable_set s) :
  âˆ«â» a in s, cond_kernel Ï a (Iic x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x) :=
by { simp_rw [cond_kernel_Iic], exact set_lintegral_cond_cdf_Iic Ï x hs, }

lemma set_lintegral_cond_kernel_univ (Ï : measure (Î± Ã— â„)) [is_finite_measure Ï]
  {s : set Î±} (hs : measurable_set s) :
  âˆ«â» a in s, cond_kernel Ï a univ âˆ‚Ï.fst = Ï (s Ã—Ë¢ univ) :=
begin
  rw â† real.Union_Iic_rat,
  have h_tendsto1 : tendsto (Î» n : â„š, âˆ«â» a in s, cond_kernel Ï a (Iic n) âˆ‚Ï.fst) at_top
    (ğ“ (âˆ«â» a in s, cond_kernel Ï a (â‹ƒ r : â„š, Iic r) âˆ‚Ï.fst)),
  { refine tendsto_lintegral_filter_of_dominated_convergence (Î» _, 1) _ _ _ _,
    { exact eventually_of_forall (Î» n, kernel.measurable_coe _ measurable_set_Iic), },
    { refine eventually_of_forall (Î» n, eventually_of_forall (Î» a, _)),
      refine (measure_mono (subset_univ _)).trans_eq measure_univ, },
    { simp only [lintegral_one, measure.restrict_apply, measurable_set.univ, univ_inter, ne.def],
      exact measure_ne_top _ _, },
    { refine eventually_of_forall (Î» a, tendsto_measure_Union (Î» n m hnm x, _)),
      simp only [mem_Iic],
      refine Î» hxn, hxn.trans _,
      exact_mod_cast hnm, }, },
  have h_tendsto2 : tendsto (Î» n : â„š, âˆ«â» a in s, cond_kernel Ï a (Iic n) âˆ‚Ï.fst) at_top
    (ğ“ (Ï (s Ã—Ë¢ â‹ƒ r : â„š, Iic r))),
  { simp_rw [set_lintegral_cond_kernel_Iic _ _ hs, prod_Union],
    refine tendsto_measure_Union (Î» n m hnm x, _),
    simp only [rat.cast_coe_nat, mem_prod, mem_Iic, and_imp],
    refine Î» hxs hxn, âŸ¨hxs, hxn.trans _âŸ©,
    exact_mod_cast hnm, },
  exact tendsto_nhds_unique h_tendsto1 h_tendsto2,
end

lemma lintegral_cond_kernel_univ (Ï : measure (Î± Ã— â„)) [is_finite_measure Ï] :
  âˆ«â» a, cond_kernel Ï a univ âˆ‚Ï.fst = Ï univ :=
by rw [â† set_lintegral_univ, set_lintegral_cond_kernel_univ Ï measurable_set.univ, univ_prod_univ]

lemma set_lintegral_cond_kernel_prod (Ï : measure (Î± Ã— â„)) [is_finite_measure Ï]
  {s : set Î±} (hs : measurable_set s) {t : set â„} (ht : measurable_set t) :
  âˆ«â» a in s, cond_kernel Ï a t âˆ‚Ï.fst = Ï (s Ã—Ë¢ t) :=
begin
  -- `set_lintegral_cond_kernel_Iic` gives the result for `t = Iic x`. These sets form a
  -- Ï€-system that generate the borel Ïƒ-algebra, hence we can get the same equality for any
  -- measurable set `t`.
  refine measurable_space.induction_on_inter real.borel_eq_generate_from_Iic
    real.is_pi_system_Iic _ _ _ _ ht,
  { simp only [measure_empty, lintegral_const, zero_mul, prod_empty], },
  { rintros t âŸ¨q, rflâŸ©,
    exact set_lintegral_cond_kernel_Iic Ï q hs, },
  { intros t ht ht_lintegral,
    calc âˆ«â» a in s, cond_kernel Ï a tá¶œ âˆ‚Ï.fst
        = âˆ«â» a in s, (cond_kernel Ï a univ) - cond_kernel Ï a t âˆ‚Ï.fst :
      by { congr' with a, rw measure_compl ht (measure_ne_top (cond_kernel Ï a) _), }
    ... = âˆ«â» a in s, (cond_kernel Ï a univ) âˆ‚Ï.fst - âˆ«â» a in s, cond_kernel Ï a t âˆ‚Ï.fst :
      begin
        rw lintegral_sub (kernel.measurable_coe (cond_kernel Ï) ht),
        { rw ht_lintegral,
          exact measure_ne_top Ï _, },
        { exact eventually_of_forall (Î» a, measure_mono (subset_univ _)), },
      end
    ... = Ï (s Ã—Ë¢ univ) - Ï (s Ã—Ë¢ t) : by rw [set_lintegral_cond_kernel_univ Ï hs, ht_lintegral]
    ... = Ï (s Ã—Ë¢ tá¶œ) :
      begin
        rw â† measure_diff _ (hs.prod ht) (measure_ne_top Ï _),
        { rw [prod_diff_prod, compl_eq_univ_diff],
          simp only [diff_self, empty_prod, union_empty], },
        { rw prod_subset_prod_iff,
          exact or.inl âŸ¨subset_rfl, subset_univ tâŸ©, },
      end, },
  { intros f hf_disj hf_meas hf_eq,
    simp_rw measure_Union hf_disj hf_meas,
    rw [lintegral_tsum (Î» i, (kernel.measurable_coe _ (hf_meas i)).ae_measurable.restrict),
      prod_Union, measure_Union],
    { congr' with i : 1,
      exact hf_eq i, },
    { intros i j hij,
      rw [function.on_fun, disjoint_prod],
      exact or.inr (hf_disj hij), },
    { exact Î» i, measurable_set.prod hs (hf_meas i), }, },
end

lemma lintegral_cond_kernel_mem (Ï : measure (Î± Ã— â„)) [is_finite_measure Ï]
  {s : set (Î± Ã— â„)} (hs : measurable_set s) :
  âˆ«â» a, cond_kernel Ï a {x | (a, x) âˆˆ s} âˆ‚Ï.fst = Ï s :=
begin
  -- `set_lintegral_cond_kernel_prod` gives the result for sets of the form `tâ‚ Ã— tâ‚‚`. These sets
  -- form a Ï€-system that generate the product Ïƒ-algebra, hence we can get the same equality for any
  -- measurable set `s`.
  refine measurable_space.induction_on_inter generate_from_prod.symm is_pi_system_prod _ _ _ _ hs,
  { simp only [mem_empty_iff_false, set_of_false, measure_empty, lintegral_const, zero_mul], },
  { intros t ht,
    rw mem_image2 at ht,
    obtain âŸ¨tâ‚, tâ‚‚, htâ‚, htâ‚‚, rflâŸ© := ht,
    have h_prod_eq_snd : âˆ€ a âˆˆ tâ‚, {x : â„ | (a, x) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} = tâ‚‚,
    { intros a ha,
      simp only [ha, prod_mk_mem_set_prod_eq, true_and, set_of_mem_eq], },
    cases eq_empty_or_nonempty tâ‚‚ with h h,
    { simp only [h, prod_empty, mem_empty_iff_false, set_of_false, measure_empty, lintegral_const,
        zero_mul], },
    have h_int_eq : âˆ«â» a, cond_kernel Ï a {x : â„ | (a, x) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} âˆ‚Ï.fst
      = âˆ«â» a in tâ‚, cond_kernel Ï a tâ‚‚ âˆ‚Ï.fst,
    { rw â† lintegral_add_compl _ htâ‚,
      have h_eq1 : âˆ«â» a in tâ‚, cond_kernel Ï a {x : â„ | (a, x) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} âˆ‚Ï.fst
        = âˆ«â» a in tâ‚, cond_kernel Ï a tâ‚‚ âˆ‚Ï.fst,
      { refine set_lintegral_congr_fun htâ‚ (eventually_of_forall (Î» a ha, _)),
        rw h_prod_eq_snd a ha, },
      have h_eq2 : âˆ«â» a in tâ‚á¶œ, cond_kernel Ï a {x : â„ | (a, x) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} âˆ‚Ï.fst = 0,
      { suffices h_eq_zero : âˆ€ a âˆˆ tâ‚á¶œ, cond_kernel Ï a {x : â„ | (a, x) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} = 0,
        { rw set_lintegral_congr_fun htâ‚.compl (eventually_of_forall h_eq_zero),
          simp only [lintegral_const, zero_mul], },
        intros a hatâ‚,
        suffices : {x : â„ | (a, x) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} = âˆ…, by rw [this, measure_empty],
        ext1 x,
        simp only [prod_mk_mem_set_prod_eq, mem_set_of_eq, mem_empty_iff_false, iff_false, not_and],
        exact Î» ha, absurd ha hatâ‚, },
      rw [h_eq1, h_eq2, add_zero], },
    rw h_int_eq,
    exact set_lintegral_cond_kernel_prod Ï htâ‚ htâ‚‚, },
  { intros t ht ht_eq,
    calc âˆ«â» a, cond_kernel Ï a {x : â„ | (a, x) âˆˆ tá¶œ} âˆ‚Ï.fst
        = âˆ«â» a, cond_kernel Ï a {x : â„ | (a, x) âˆˆ t}á¶œ âˆ‚Ï.fst : rfl
    ... = âˆ«â» a, cond_kernel Ï a univ - cond_kernel Ï a {x : â„ | (a, x) âˆˆ t} âˆ‚Ï.fst :
      begin
        congr' with a : 1,
        rw measure_compl _ (measure_ne_top (cond_kernel Ï a) _),
        exact measurable_prod_mk_left ht,
      end
    ... = âˆ«â» a, cond_kernel Ï a univ âˆ‚Ï.fst - âˆ«â» a, cond_kernel Ï a {x : â„ | (a, x) âˆˆ t} âˆ‚Ï.fst :
      begin
        have h_le : (Î» a, cond_kernel Ï a {x : â„ | (a, x) âˆˆ t}) â‰¤áµ[Ï.fst] Î» a, cond_kernel Ï a univ,
        { exact eventually_of_forall (Î» a, measure_mono (subset_univ _)), },
        rw lintegral_sub _ _ h_le,
        { exact kernel.measurable_prod_mk_mem _ ht, },
        { refine ((lintegral_mono_ae h_le).trans_lt _).ne,
          rw lintegral_cond_kernel_univ,
          exact measure_lt_top Ï univ, },
      end
    ... = Ï univ - Ï t : by rw [ht_eq, lintegral_cond_kernel_univ]
    ... = Ï tá¶œ : (measure_compl ht (measure_ne_top _ _)).symm, },
  { intros f hf_disj hf_meas hf_eq,
    have h_eq : âˆ€ a, {x | (a, x) âˆˆ â‹ƒ i, f i} = â‹ƒ i, {x | (a, x) âˆˆ f i},
    { intros a,
      ext1 x,
      simp only [mem_Union, mem_set_of_eq], },
    simp_rw h_eq,
    have h_disj : âˆ€ a, pairwise (disjoint on (Î» i, {x | (a, x) âˆˆ f i})),
    { intros a i j hij,
      have h_disj := hf_disj hij,
      rw [function.on_fun, disjoint_iff_inter_eq_empty] at h_disj âŠ¢,
      ext1 x,
      simp only [mem_inter_iff, mem_set_of_eq, mem_empty_iff_false, iff_false],
      intros h_mem_both,
      suffices : (a, x) âˆˆ âˆ…, by rwa mem_empty_iff_false at this,
      rwa [â† h_disj, mem_inter_iff], },
    have h_meas : âˆ€ a i, measurable_set {x | (a, x) âˆˆ f i},
    { exact Î» a i, measurable_prod_mk_left (hf_meas i), },
    calc âˆ«â» a, cond_kernel Ï a (â‹ƒ i, {x | (a, x) âˆˆ f i}) âˆ‚Ï.fst
        = âˆ«â» a, âˆ‘' i, cond_kernel Ï a {x | (a, x) âˆˆ f i} âˆ‚Ï.fst :
          by { congr' with a : 1, rw measure_Union (h_disj a) (h_meas a), }
    ... = âˆ‘' i, âˆ«â» a, cond_kernel Ï a {x | (a, x) âˆˆ f i} âˆ‚Ï.fst :
          begin
            rw lintegral_tsum (Î» i : â„•, measurable.ae_measurable _),
            exact kernel.measurable_prod_mk_mem _ (hf_meas i),
          end
    ... = âˆ‘' i, Ï (f i) : by { congr' with i : 1, exact hf_eq i, }
    ... = Ï (Union f) : (measure_Union hf_disj hf_meas).symm, },
end

/-- **Disintegration** of constant kernels. A constant kernel on a product space `Î± Ã— â„` can be
written as the composition-product of the constant kernel with value `Ï.fst` (marginal measure over
`Î±`) and a Markov kernel from `Î±` to `â„`. We call that Markov kernel `cond_kernel Ï`.
-/
theorem kernel.const_eq_comp_prod (Ï : measure (Î± Ã— â„)) [is_finite_measure Ï]
  (Î³ : Type*) [measurable_space Î³] :
  kernel.const Î³ Ï = (kernel.const Î³ Ï.fst) âŠ—â‚– (kernel.prod_mk_left (cond_kernel Ï) Î³) :=
begin
  ext a s hs : 2,
  rw [kernel.comp_prod_apply _ _ _ hs, kernel.const_apply, kernel.const_apply],
  simp_rw kernel.prod_mk_left_apply,
  rw lintegral_cond_kernel_mem Ï hs,
end

/-- **Disintegration** of finite product measures on `Î± Ã— â„`. Such a measure can be written as the
composition-product of the constant kernel with value `Ï.fst` (marginal measure over `Î±`) and a
Markov kernel from `Î±` to `â„`. We call that Markov kernel `cond_kernel Ï`. -/
theorem measure_eq_comp_prod (Ï : measure (Î± Ã— â„)) [is_finite_measure Ï] :
  Ï = ((kernel.const unit Ï.fst) âŠ—â‚– (kernel.prod_mk_left (cond_kernel Ï) unit)) (unit.star) :=
by rw [â† kernel.const_eq_comp_prod Ï unit, kernel.const_apply]

lemma lintegral_cond_kernel (Ï : measure (Î± Ã— â„)) [is_finite_measure Ï]
  {f : Î± Ã— â„ â†’ â„â‰¥0âˆ} (hf : measurable f) :
  âˆ«â» a, âˆ«â» y, f (a, y) âˆ‚(cond_kernel Ï a) âˆ‚Ï.fst = âˆ«â» x, f x âˆ‚Ï :=
begin
  nth_rewrite 1 measure_eq_comp_prod Ï,
  rw [kernel.lintegral_comp_prod _ _ _ hf, kernel.const_apply],
  simp_rw kernel.prod_mk_left_apply,
end

end probability_theory
