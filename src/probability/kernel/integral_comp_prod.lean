/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import probability.kernel.composition

/-!
# Bochner integral of a function against the composition-product of two kernels

We prove properties of the composition-product of two kernels. If `Îº` is an s-finite kernel from
`Î±` to `Î²` and `Î·` is an s-finite kernel from `Î± Ã— Î²` to `Î³`, we can form their composition-product
`Îº âŠ—â‚– Î· : kernel Î± (Î² Ã— Î³)`. We proved in `probability.kernel.lintegral_comp_prod` that it verifies
`âˆ«â» bc, f bc âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ«â» b, âˆ«â» c, f (b, c) âˆ‚(Î· (a, b)) âˆ‚(Îº a)`. In this file, we prove the
same equality for the Bochner integral.

## Main statements

* `probability_theory.integral_comp_prod`: the integral against the composition-product is
  `âˆ« z, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a)`

## Implementation details

This file is to a large extent a copy of part of `measure_theory.constructions.prod`. The product of
two measures is a particular case of composition-product of kernels and it turns out that once the
measurablity of the Lebesgue integral of a kernel is proved, almost all proofs about integral
against products of measures extend with minimal modifications to the composition-product of two
kernels.
-/

noncomputable theory
open_locale topology ennreal measure_theory probability_theory
open set function real ennreal measure_theory filter probability_theory

variables {Î± Î² Î³ E : Type*}
  {mÎ± : measurable_space Î±} {mÎ² : measurable_space Î²} {mÎ³ : measurable_space Î³}
  [normed_add_comm_group E]
  {Îº : kernel Î± Î²} [is_s_finite_kernel Îº]
  {Î· : kernel (Î± Ã— Î²) Î³} [is_s_finite_kernel Î·]
  {a : Î±}

namespace probability_theory

lemma le_comp_prod_apply {Î± Î² Î³ : Type*} {mÎ± : measurable_space Î±} {mÎ² : measurable_space Î²}
  {mÎ³ : measurable_space Î³}
  (Îº : kernel Î± Î²) [is_s_finite_kernel Îº] (Î· : kernel (Î± Ã— Î²) Î³)
  [is_s_finite_kernel Î·] (a : Î±) (s : set (Î² Ã— Î³)) :
  âˆ«â» b, Î· (a, b) {c | (b, c) âˆˆ s} âˆ‚(Îº a) â‰¤ (Îº âŠ—â‚– Î·) a s :=
calc âˆ«â» b, Î· (a, b) {c | (b, c) âˆˆ s} âˆ‚(Îº a)
    â‰¤ âˆ«â» b, Î· (a, b) {c | (b, c) âˆˆ (to_measurable ((Îº âŠ—â‚– Î·) a) s)} âˆ‚(Îº a) :
      lintegral_mono (Î» b, measure_mono (Î» _ h_mem, subset_to_measurable _ _ h_mem))
... = (Îº âŠ—â‚– Î·) a (to_measurable ((Îº âŠ—â‚– Î·) a) s) :
      (kernel.comp_prod_apply_eq_comp_prod_fun Îº Î· a (measurable_set_to_measurable _ _)).symm
... = (Îº âŠ—â‚– Î·) a s : measure_to_measurable s

lemma ae_kernel_lt_top (a : Î±) {s : set (Î² Ã— Î³)} (h2s : (Îº âŠ—â‚– Î·) a s â‰  âˆ) :
  âˆ€áµ b âˆ‚(Îº a), Î· (a, b) (prod.mk b â»Â¹' s) < âˆ :=
begin
  let t := to_measurable ((Îº âŠ—â‚– Î·) a) s,
  have : âˆ€ (b : Î²), Î· (a, b) (prod.mk b â»Â¹' s) â‰¤ Î· (a, b) (prod.mk b â»Â¹' t),
  { exact Î» b, measure_mono (preimage_mono (subset_to_measurable _ _)), },
  have ht : measurable_set t := measurable_set_to_measurable _ _,
  have h2t : (Îº âŠ—â‚– Î·) a t â‰  âˆ, by rwa measure_to_measurable,
  have ht_lt_top : âˆ€áµ b âˆ‚(Îº a), Î· (a, b) (prod.mk b â»Â¹' t) < âˆ,
  { rw kernel.comp_prod_apply _ _ _ ht at h2t,
    exact ae_lt_top (kernel.measurable_kernel_prod_mk_left' ht a) h2t, },
  filter_upwards [ht_lt_top] with b hb,
  exact (this b).trans_lt hb,
end

lemma comp_prod_null (a : Î±) {s : set (Î² Ã— Î³)} (hs : measurable_set s) :
  (Îº âŠ—â‚– Î·) a s = 0 â†” (Î» b, Î· (a, b) (prod.mk b â»Â¹' s)) =áµ[Îº a] 0 :=
begin
  rw [kernel.comp_prod_apply _ _ _ hs, lintegral_eq_zero_iff],
  { refl, },
  { exact kernel.measurable_kernel_prod_mk_left' hs a, },
end

lemma ae_null_of_comp_prod_null {s : set (Î² Ã— Î³)} (h : (Îº âŠ—â‚– Î·) a s = 0) :
  (Î» b, Î· (a, b) (prod.mk b â»Â¹' s)) =áµ[Îº a] 0 :=
begin
  obtain âŸ¨t, hst, mt, htâŸ© := exists_measurable_superset_of_null h,
  simp_rw [comp_prod_null a mt] at ht,
  rw [eventually_le_antisymm_iff],
  exact âŸ¨eventually_le.trans_eq
    (eventually_of_forall $ Î» x, (measure_mono (preimage_mono hst) : _)) ht,
    eventually_of_forall $ Î» x, zero_le _âŸ©
end

lemma ae_ae_of_ae_comp_prod {p : Î² Ã— Î³ â†’ Prop} (h : âˆ€áµ bc âˆ‚((Îº âŠ—â‚– Î·) a), p bc) :
  âˆ€áµ b âˆ‚(Îº a), âˆ€áµ c âˆ‚(Î· (a, b)), p (b, c) :=
ae_null_of_comp_prod_null h

lemma has_finite_integral_prod_mk_left (a : Î±) {s : set (Î² Ã— Î³)} (h2s : (Îº âŠ—â‚– Î·) a s â‰  âˆ) :
  has_finite_integral (Î» b, (Î· (a, b) (prod.mk b â»Â¹' s)).to_real) (Îº a) :=
begin
  let t := to_measurable ((Îº âŠ—â‚– Î·) a) s,
  have h2t : (Îº âŠ—â‚– Î·) a t â‰  âˆ, by rwa measure_to_measurable,
  simp_rw [has_finite_integral, ennnorm_eq_of_real to_real_nonneg],
  refine lt_of_le_of_lt _ h2t.lt_top,
  refine le_trans _ (le_comp_prod_apply _ _ _ _),
  refine lintegral_mono_ae _,
  filter_upwards [ae_kernel_lt_top a h2s] with b hb,
  rw of_real_to_real hb.ne,
  exact measure_mono (preimage_mono (subset_to_measurable _ _)),
end

lemma integrable_kernel_prod_mk_left (a : Î±)
  {s : set (Î² Ã— Î³)} (hs : measurable_set s) (h2s : (Îº âŠ—â‚– Î·) a s â‰  âˆ) :
  integrable (Î» b, (Î· (a, b) (prod.mk b â»Â¹' s)).to_real) (Îº a) :=
begin
  split,
  { exact (kernel.measurable_kernel_prod_mk_left' hs a).ennreal_to_real.ae_strongly_measurable },
  { exact has_finite_integral_prod_mk_left a h2s, },
end

lemma comp_prod_restrict {s : set Î²} {t : set Î³} (hs : measurable_set s) (ht : measurable_set t) :
  (kernel.restrict Îº hs) âŠ—â‚– (kernel.restrict Î· ht) = kernel.restrict (Îº âŠ—â‚– Î·) (hs.prod ht) :=
begin
  ext a u hu : 2,
  rw [kernel.comp_prod_apply _ _ _ hu, kernel.restrict_apply' _ _ _ hu,
    kernel.comp_prod_apply _ _ _ (hu.inter (hs.prod ht))],
  simp only [kernel.restrict_apply, measure.restrict_apply' ht, mem_inter_iff,
    prod_mk_mem_set_prod_eq],
  have : âˆ€ b, Î· (a, b) {c : Î³ | (b, c) âˆˆ u âˆ§ b âˆˆ s âˆ§ c âˆˆ t}
    = s.indicator (Î» b, Î· (a, b) ({c : Î³ | (b, c) âˆˆ u} âˆ© t)) b,
  { intro b,
    classical,
    rw indicator_apply,
    split_ifs with h,
    { simp only [h, true_and],
      refl, },
    { simp only [h, false_and, and_false, set_of_false, measure_empty], }, },
  simp_rw this,
  rw lintegral_indicator _ hs,
end

lemma comp_prod_restrict_left {s : set Î²} (hs : measurable_set s) :
  (kernel.restrict Îº hs) âŠ—â‚– Î· = kernel.restrict (Îº âŠ—â‚– Î·) (hs.prod measurable_set.univ) :=
by { rw â† comp_prod_restrict, congr, exact kernel.restrict_univ.symm, }

lemma comp_prod_restrict_right {t : set Î³} (ht : measurable_set t) :
  Îº âŠ—â‚– (kernel.restrict Î· ht) = kernel.restrict (Îº âŠ—â‚– Î·) (measurable_set.univ.prod ht) :=
by { rw â† comp_prod_restrict, congr, exact kernel.restrict_univ.symm, }

lemma _root_.measure_theory.ae_strongly_measurable.integral_kernel_prod_right'
  [normed_space â„ E] [complete_space E]
  â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : ae_strongly_measurable f ((Îº âŠ—â‚– Î·) a)) :
  ae_strongly_measurable (Î» x, âˆ« y, f (x, y) âˆ‚(Î· (a, x))) (Îº a) :=
âŸ¨Î» x, âˆ« y, hf.mk f (x, y) âˆ‚(Î· (a, x)), hf.strongly_measurable_mk.integral_kernel_prod_right'',
  by { filter_upwards [ae_ae_of_ae_comp_prod hf.ae_eq_mk] with _ hx using integral_congr_ae hx }âŸ©

lemma _root_.measure_theory.ae_strongly_measurable.comp_prod_mk_left
  {Î´ : Type*} [topological_space Î´] {f : Î² Ã— Î³ â†’ Î´}
  (hf : ae_strongly_measurable f ((Îº âŠ—â‚– Î·) a)) :
  âˆ€áµ x âˆ‚(Îº a), ae_strongly_measurable (Î» y, f (x, y)) (Î· (a, x)) :=
begin
  filter_upwards [ae_ae_of_ae_comp_prod hf.ae_eq_mk] with x hx,
  exact âŸ¨Î» y, hf.mk f (x, y), hf.strongly_measurable_mk.comp_measurable measurable_prod_mk_left, hxâŸ©
end

/-! ### Lebesgue integral -/

lemma kernel.lintegral_comp_prodâ‚€ (f : Î² Ã— Î³ â†’ â„â‰¥0âˆ) (hf : ae_measurable f ((Îº âŠ—â‚– Î·) a)) :
  âˆ«â» z, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ«â» x, âˆ«â» y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
begin
  have A : âˆ«â» z, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ«â» z, hf.mk f z âˆ‚((Îº âŠ—â‚– Î·) a) :=
    lintegral_congr_ae hf.ae_eq_mk,
  have B : âˆ«â» x, âˆ«â» y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) = âˆ«â» x, âˆ«â» y, hf.mk f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a),
  { apply lintegral_congr_ae,
    filter_upwards [ae_ae_of_ae_comp_prod hf.ae_eq_mk] with _ ha using lintegral_congr_ae ha, },
  rw [A, B, kernel.lintegral_comp_prod],
  exact hf.measurable_mk,
end

/-! ### Integrability -/

lemma has_finite_integral_comp_prod_iff â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (h1f : strongly_measurable f) :
  has_finite_integral f ((Îº âŠ—â‚– Î·) a)
    â†” (âˆ€áµ x âˆ‚(Îº a), has_finite_integral (Î» y, f (x, y)) (Î· (a, x))) âˆ§
      has_finite_integral (Î» x, âˆ« y, â€–f (x, y)â€– âˆ‚(Î· (a, x))) (Îº a) :=
begin
  simp only [has_finite_integral],
  rw kernel.lintegral_comp_prod _ _ _ h1f.ennnorm,
  have : âˆ€ x, âˆ€áµ y âˆ‚(Î· (a, x)), 0 â‰¤ â€–f (x, y)â€– := Î» x, eventually_of_forall (Î» y, norm_nonneg _),
  simp_rw [integral_eq_lintegral_of_nonneg_ae (this _)
    (h1f.norm.comp_measurable measurable_prod_mk_left).ae_strongly_measurable,
    ennnorm_eq_of_real to_real_nonneg, of_real_norm_eq_coe_nnnorm],
  have : âˆ€ {p q r : Prop} (h1 : r â†’ p), (r â†” p âˆ§ q) â†” (p â†’ (r â†” q)) :=
  Î» p q r h1, by rw [â† and.congr_right_iff, and_iff_right_of_imp h1],
  rw [this],
  { intro h2f, rw lintegral_congr_ae,
    refine h2f.mp _, apply eventually_of_forall, intros x hx, dsimp only,
    rw [of_real_to_real], rw [â† lt_top_iff_ne_top], exact hx },
  { intro h2f, refine ae_lt_top _ h2f.ne, exact h1f.ennnorm.lintegral_kernel_prod_right'' },
end

lemma has_finite_integral_comp_prod_iff' â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„
  (h1f : ae_strongly_measurable f ((Îº âŠ—â‚– Î·) a)) :
  has_finite_integral f ((Îº âŠ—â‚– Î·) a)
    â†” (âˆ€áµ x âˆ‚(Îº a), has_finite_integral (Î» y, f (x, y)) (Î· (a, x))) âˆ§
      has_finite_integral (Î» x, âˆ« y, â€–f (x, y)â€– âˆ‚(Î· (a, x))) (Îº a) :=
begin
  rw [has_finite_integral_congr h1f.ae_eq_mk,
    has_finite_integral_comp_prod_iff h1f.strongly_measurable_mk],
  apply and_congr,
  { apply eventually_congr,
    filter_upwards [ae_ae_of_ae_comp_prod h1f.ae_eq_mk.symm],
    assume x hx,
    exact has_finite_integral_congr hx },
  { apply has_finite_integral_congr,
    filter_upwards [ae_ae_of_ae_comp_prod h1f.ae_eq_mk.symm] with _ hx
      using integral_congr_ae (eventually_eq.fun_comp hx _), },
end

lemma integrable_comp_prod_iff â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (h1f : ae_strongly_measurable f ((Îº âŠ—â‚– Î·) a)) :
  integrable f ((Îº âŠ—â‚– Î·) a) â†”
    (âˆ€áµ x âˆ‚(Îº a), integrable (Î» y, f (x, y)) (Î· (a, x)))
    âˆ§ integrable (Î» x, âˆ« y, â€–f (x, y)â€– âˆ‚(Î· (a, x))) (Îº a) :=
by simp only [integrable, has_finite_integral_comp_prod_iff' h1f,
  h1f.norm.integral_kernel_prod_right', h1f, h1f.comp_prod_mk_left, eventually_and, true_and]

lemma _root_.measure_theory.integrable.comp_prod_right_ae
  â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : integrable f ((Îº âŠ—â‚– Î·) a)) :
  âˆ€áµ x âˆ‚(Îº a), integrable (Î» y, f (x, y)) (Î· (a, x)) :=
((integrable_comp_prod_iff hf.ae_strongly_measurable).mp hf).1

lemma _root_.measure_theory.integrable.integral_norm_comp_prod_left
  â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : integrable f ((Îº âŠ—â‚– Î·) a)) :
  integrable (Î» x, âˆ« y, â€–f (x, y)â€– âˆ‚(Î· (a, x))) (Îº a) :=
((integrable_comp_prod_iff hf.ae_strongly_measurable).mp hf).2

lemma _root_.measure_theory.integrable.integral_comp_prod_left [normed_space â„ E] [complete_space E]
  â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : integrable f ((Îº âŠ—â‚– Î·) a)) :
  integrable (Î» x, âˆ« y, f (x, y) âˆ‚(Î· (a, x))) (Îº a) :=
integrable.mono hf.integral_norm_comp_prod_left
  hf.ae_strongly_measurable.integral_kernel_prod_right' $
  eventually_of_forall $ Î» x, (norm_integral_le_integral_norm _).trans_eq $
  (norm_of_nonneg $ integral_nonneg_of_ae $ eventually_of_forall $
  Î» y, (norm_nonneg (f (x, y)) : _)).symm

/-! ### Bochner integral -/

variables [normed_space â„ E] [complete_space E]
  {E' : Type*} [normed_add_comm_group E'] [complete_space E'] [normed_space â„ E']

lemma kernel.integral_fn_integral_add â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (F : E â†’ E')
  (hf : integrable f ((Îº âŠ—â‚– Î·) a)) (hg : integrable g ((Îº âŠ—â‚– Î·) a)) :
  âˆ« x, F (âˆ« y, f (x, y) + g (x, y) âˆ‚(Î· (a, x))) âˆ‚(Îº a)
    = âˆ« x, F (âˆ« y, f (x, y) âˆ‚(Î· (a, x)) + âˆ« y, g (x, y) âˆ‚(Î· (a, x))) âˆ‚(Îº a) :=
begin
  refine integral_congr_ae _,
  filter_upwards [hf.comp_prod_right_ae, hg.comp_prod_right_ae] with _ h2f h2g,
  simp [integral_add h2f h2g],
end

lemma kernel.integral_fn_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (F : E â†’ E')
  (hf : integrable f ((Îº âŠ—â‚– Î·) a)) (hg : integrable g ((Îº âŠ—â‚– Î·) a)) :
  âˆ« x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚(Î· (a, x))) âˆ‚(Îº a)
    = âˆ« x, F (âˆ« y, f (x, y) âˆ‚(Î· (a, x)) - âˆ« y, g (x, y) âˆ‚(Î· (a, x))) âˆ‚(Îº a) :=
begin
  refine integral_congr_ae _,
  filter_upwards [hf.comp_prod_right_ae, hg.comp_prod_right_ae] with _ h2f h2g,
  simp [integral_sub h2f h2g],
end

lemma kernel.lintegral_fn_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„
  (F : E â†’ â„â‰¥0âˆ) (hf : integrable f ((Îº âŠ—â‚– Î·) a)) (hg : integrable g ((Îº âŠ—â‚– Î·) a)) :
  âˆ«â» x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚(Î· (a, x))) âˆ‚(Îº a)
    = âˆ«â» x, F (âˆ« y, f (x, y) âˆ‚(Î· (a, x)) - âˆ« y, g (x, y) âˆ‚(Î· (a, x))) âˆ‚(Îº a) :=
begin
  refine lintegral_congr_ae _,
  filter_upwards [hf.comp_prod_right_ae, hg.comp_prod_right_ae] with _ h2f h2g,
  simp [integral_sub h2f h2g],
end

lemma kernel.integral_integral_add â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„
  (hf : integrable f ((Îº âŠ—â‚– Î·) a)) (hg : integrable g ((Îº âŠ—â‚– Î·) a)) :
  âˆ« x, âˆ« y, f (x, y) + g (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a)
    = âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) + âˆ« x, âˆ« y, g (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
(kernel.integral_fn_integral_add id hf hg).trans $
  integral_add hf.integral_comp_prod_left hg.integral_comp_prod_left

lemma kernel.integral_integral_add' â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„
  (hf : integrable f ((Îº âŠ—â‚– Î·) a)) (hg : integrable g ((Îº âŠ—â‚– Î·) a)) :
  âˆ« x, âˆ« y, (f + g) (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a)
    = âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) + âˆ« x, âˆ« y, g (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
kernel.integral_integral_add hf hg

lemma kernel.integral_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„
  (hf : integrable f ((Îº âŠ—â‚– Î·) a)) (hg : integrable g ((Îº âŠ—â‚– Î·) a)) :
  âˆ« x, âˆ« y, f (x, y) - g (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a)
    = âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) - âˆ« x, âˆ« y, g (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
(kernel.integral_fn_integral_sub id hf hg).trans $
  integral_sub hf.integral_comp_prod_left hg.integral_comp_prod_left

lemma kernel.integral_integral_sub' â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„
  (hf : integrable f ((Îº âŠ—â‚– Î·) a)) (hg : integrable g ((Îº âŠ—â‚– Î·) a)) :
  âˆ« x, âˆ« y, (f - g) (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a)
    = âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) - âˆ« x, âˆ« y, g (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
kernel.integral_integral_sub hf hg

lemma kernel.continuous_integral_integral :
  continuous (Î» (f : Î± Ã— Î² â†’â‚[(Îº âŠ—â‚– Î·) a] E), âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a)) :=
begin
  rw [continuous_iff_continuous_at], intro g,
  refine tendsto_integral_of_L1 _ (L1.integrable_coe_fn g).integral_comp_prod_left
    (eventually_of_forall $ Î» h, (L1.integrable_coe_fn h).integral_comp_prod_left) _,
  simp_rw [â† kernel.lintegral_fn_integral_sub (Î» x, (â€–xâ€–â‚Š : â„â‰¥0âˆ)) (L1.integrable_coe_fn _)
    (L1.integrable_coe_fn g)],
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds _ (Î» i, zero_le _) _,
  { exact Î» i, âˆ«â» x, âˆ«â» y, â€–i (x, y) - g (x, y)â€–â‚Š âˆ‚(Î· (a, x)) âˆ‚(Îº a) },
  swap, { exact Î» i, lintegral_mono (Î» x, ennnorm_integral_le_lintegral_ennnorm _) },
  show tendsto (Î» (i : Î² Ã— Î³ â†’â‚[(Îº âŠ—â‚– Î·) a] E),
    âˆ«â» x, âˆ«â» (y : Î³), â€–i (x, y) - g (x, y)â€–â‚Š âˆ‚(Î· (a, x)) âˆ‚(Îº a)) (ğ“ g) (ğ“ 0),
  have : âˆ€ (i : Î± Ã— Î² â†’â‚[(Îº âŠ—â‚– Î·) a] E), measurable (Î» z, (â€–i z - g zâ€–â‚Š : â„â‰¥0âˆ)) :=
  Î» i, ((Lp.strongly_measurable i).sub (Lp.strongly_measurable g)).ennnorm,
  simp_rw [â† kernel.lintegral_comp_prod _ _ _ (this _), â† L1.of_real_norm_sub_eq_lintegral,
    â† of_real_zero],
  refine (continuous_of_real.tendsto 0).comp _,
  rw [â† tendsto_iff_norm_tendsto_zero], exact tendsto_id
end

lemma integral_comp_prod : âˆ€ {f : Î² Ã— Î³ â†’ E} (hf : integrable f ((Îº âŠ—â‚– Î·) a)),
  âˆ« z, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
begin
  apply integrable.induction,
  { intros c s hs h2s,
    simp_rw [integral_indicator hs, â† indicator_comp_right,
      function.comp, integral_indicator (measurable_prod_mk_left hs),
      set_integral_const, integral_smul_const],
    congr' 1,
    rw integral_to_real,
    rotate,
    { exact (kernel.measurable_kernel_prod_mk_left' hs _).ae_measurable, },
    { exact (ae_kernel_lt_top a h2s.ne), },
    rw kernel.comp_prod_apply _ _ _ hs,
    refl, },
  { intros f g hfg i_f i_g hf hg,
    simp_rw [integral_add' i_f i_g, kernel.integral_integral_add' i_f i_g, hf, hg] },
  { exact is_closed_eq continuous_integral kernel.continuous_integral_integral },
  { intros f g hfg i_f hf, convert hf using 1,
    { exact integral_congr_ae hfg.symm },
    { refine integral_congr_ae _,
      refine (ae_ae_of_ae_comp_prod hfg).mp _,
      apply eventually_of_forall, intros x hfgx,
      exact integral_congr_ae (ae_eq_symm hfgx) } }
end

lemma set_integral_comp_prod {f : Î² Ã— Î³ â†’ E} {s : set Î²} {t : set Î³}
  (hs : measurable_set s) (ht : measurable_set t) (hf : integrable_on f (s Ã—Ë¢ t) ((Îº âŠ—â‚– Î·) a)) :
  âˆ« z in s Ã—Ë¢ t, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ« x in s, âˆ« y in t, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
begin
  rw [â† kernel.restrict_apply (Îº âŠ—â‚– Î·) (hs.prod ht), â† comp_prod_restrict, integral_comp_prod],
  { simp_rw kernel.restrict_apply, },
  { rw [comp_prod_restrict, kernel.restrict_apply], exact hf, },
end

lemma set_integral_comp_prod_univ_right (f : Î² Ã— Î³ â†’ E) {s : set Î²}
  (hs : measurable_set s) (hf : integrable_on f (s Ã—Ë¢ univ) ((Îº âŠ—â‚– Î·) a)) :
  âˆ« z in s Ã—Ë¢ univ, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ« x in s, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
by { rw set_integral_comp_prod hs measurable_set.univ hf, simp_rw measure.restrict_univ, }

lemma set_integral_comp_prod_univ_left (f : Î² Ã— Î³ â†’ E) {t : set Î³}
  (ht : measurable_set t) (hf : integrable_on f (univ Ã—Ë¢ t) ((Îº âŠ—â‚– Î·) a)) :
  âˆ« z in univ Ã—Ë¢ t, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ« x, âˆ« y in t, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a) :=
by { rw set_integral_comp_prod measurable_set.univ ht hf, simp_rw measure.restrict_univ, }

end probability_theory
