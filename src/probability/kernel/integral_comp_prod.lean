/-
Copyright (c) 2023 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import probability.kernel.composition

/-!
# Integral of a function against the `comp_prod` kernel

## Main statements

* `foo_bar_unique`

## Implementation details

This file is to a large extent a copy of `measure_theory.constructions.prod`. The product of two
measures is a particular case of composition-product of kernels and it turns out that once the
measurablity of the Lebesgue integral of a kernel is proved, almost all proofs about integral
against products of measures extend with minimal modifications to the composition-product of two
kernels.
-/

noncomputable theory
open_locale topology ennreal measure_theory probability_theory
open set function real ennreal measure_theory filter probability_theory

variables {Œ± Œ≤ Œ≥ E : Type*}
  {mŒ± : measurable_space Œ±} {mŒ≤ : measurable_space Œ≤} {mŒ≥ : measurable_space Œ≥}
  [normed_add_comm_group E]
  {Œ∫ : kernel Œ± Œ≤} [kernel.is_s_finite_kernel Œ∫]
  {Œ∑ : kernel (Œ± √ó Œ≤) Œ≥} [kernel.is_s_finite_kernel Œ∑]
  {a : Œ±}

/-! ### The product measure -/

namespace probability_theory

lemma ae_kernel_lt_top (a : Œ±) {s : set (Œ≤ √ó Œ≥)} (hs : measurable_set s)
  (h2s : (Œ∫ ‚äó‚Çñ Œ∑) a s ‚â† ‚àû) :
  ‚àÄ·µê b ‚àÇ(Œ∫ a), Œ∑ (a, b) (prod.mk b ‚Åª¬π' s) < ‚àû :=
begin
  rw kernel.comp_prod_apply _ _ _ hs at h2s,
  exact ae_lt_top (kernel.measurable_kernel_prod_mk_left' hs a) h2s,
end

lemma integrable_kernel_prod_mk_left (a : Œ±)
  {s : set (Œ≤ √ó Œ≥)} (hs : measurable_set s) (h2s : (Œ∫ ‚äó‚Çñ Œ∑) a s ‚â† ‚àû) :
  integrable (Œª b, (Œ∑ (a, b) (prod.mk b ‚Åª¬π' s)).to_real) (Œ∫ a) :=
begin
  refine ‚ü®(kernel.measurable_kernel_prod_mk_left' hs a).ennreal_to_real.ae_strongly_measurable,
    _‚ü©,
  simp_rw [has_finite_integral, ennnorm_eq_of_real to_real_nonneg],
  convert h2s.lt_top using 1,
  rw kernel.comp_prod_apply _ _ _ hs,
  apply lintegral_congr_ae,
  refine (ae_kernel_lt_top a hs h2s).mp _,
  apply eventually_of_forall,
  intros x hx,
  rw [lt_top_iff_ne_top] at hx,
  simp only,
  rw of_real_to_real hx,
  refl,
end

lemma comp_prod_null (a : Œ±) {s : set (Œ≤ √ó Œ≥)} (hs : measurable_set s) :
  (Œ∫ ‚äó‚Çñ Œ∑) a s = 0 ‚Üî (Œª b, Œ∑ (a, b) (prod.mk b ‚Åª¬π' s)) =·µê[Œ∫ a] 0 :=
begin
  rw [kernel.comp_prod_apply _ _ _ hs,
    lintegral_eq_zero_iff (kernel.measurable_kernel_prod_mk_left' hs a)],
  { refl, },
  { apply_instance, },
end

lemma ae_null_of_comp_prod_null {s : set (Œ≤ √ó Œ≥)} (h : (Œ∫ ‚äó‚Çñ Œ∑) a s = 0) :
  (Œª b, Œ∑ (a, b) (prod.mk b ‚Åª¬π' s)) =·µê[Œ∫ a] 0 :=
begin
  obtain ‚ü®t, hst, mt, ht‚ü© := exists_measurable_superset_of_null h,
  simp_rw [comp_prod_null a mt] at ht,
  rw [eventually_le_antisymm_iff],
  exact ‚ü®eventually_le.trans_eq
    (eventually_of_forall $ Œª x, (measure_mono (preimage_mono hst) : _)) ht,
    eventually_of_forall $ Œª x, zero_le _‚ü©
end

lemma ae_ae_of_ae_comp_prod {p : Œ≤ √ó Œ≥ ‚Üí Prop} (h : ‚àÄ·µê bc ‚àÇ((Œ∫ ‚äó‚Çñ Œ∑) a), p bc) :
  ‚àÄ·µê b ‚àÇ(Œ∫ a), ‚àÄ·µê c ‚àÇ(Œ∑ (a, b)), p (b, c) :=
ae_null_of_comp_prod_null h

lemma comp_prod_restrict {s : set Œ≤} {t : set Œ≥} (hs : measurable_set s) (ht : measurable_set t) :
  (kernel.restrict Œ∫ hs) ‚äó‚Çñ (kernel.restrict Œ∑ ht) = kernel.restrict (Œ∫ ‚äó‚Çñ Œ∑) (hs.prod ht) :=
begin
  ext a u hu : 2,
  rw [kernel.comp_prod_apply _ _ _ hu, kernel.restrict_apply' _ _ _ hu,
    kernel.comp_prod_apply _ _ _ (hu.inter (hs.prod ht))],
  simp only [kernel.restrict_apply, measure.restrict_apply' ht, mem_inter_iff,
    prod_mk_mem_set_prod_eq],
  have : ‚àÄ b, Œ∑ (a, b) {c : Œ≥ | (b, c) ‚àà u ‚àß b ‚àà s ‚àß c ‚àà t}
    = s.indicator (Œª b, Œ∑ (a, b) ({c : Œ≥ | (b, c) ‚àà u} ‚à© t)) b,
  { intro b,
    classical,
    rw indicator_apply,
    split_ifs with h,
    { simp only [h, true_and],
      refl, },
    { simp only [h, false_and, and_false, set_of_false, measure_empty], }, },
  simp_rw this,
  rw lintegral_indicator _ hs,
end

lemma restrict_comp_prod_eq_comp_prod_univ {s : set Œ≤} (hs : measurable_set s) :
  (kernel.restrict Œ∫ hs) ‚äó‚Çñ Œ∑ = kernel.restrict (Œ∫ ‚äó‚Çñ Œ∑) (hs.prod measurable_set.univ) :=
by { rw ‚Üê comp_prod_restrict, congr, exact kernel.restrict_univ.symm, }

end probability_theory

open probability_theory

namespace measure_theory

lemma ae_strongly_measurable.integral_kernel_prod_right'
  [normed_space ‚Ñù E] [complete_space E]
  ‚¶Éf : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ (hf : ae_strongly_measurable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ae_strongly_measurable (Œª x, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x))) (Œ∫ a) :=
‚ü®Œª x, ‚à´ y, hf.mk f (x, y) ‚àÇ(Œ∑ (a, x)), hf.strongly_measurable_mk.integral_kernel_prod_right'',
  by { filter_upwards [ae_ae_of_ae_comp_prod hf.ae_eq_mk] with _ hx using integral_congr_ae hx }‚ü©

lemma ae_strongly_measurable.comp_prod_mk_left
  {Œ¥ : Type*} [topological_space Œ¥] {f : Œ≤ √ó Œ≥ ‚Üí Œ¥}
  (hf : ae_strongly_measurable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚àÄ·µê x ‚àÇ(Œ∫ a), ae_strongly_measurable (Œª y, f (x, y)) (Œ∑ (a , x)) :=
begin
  filter_upwards [ae_ae_of_ae_comp_prod hf.ae_eq_mk] with x hx,
  exact ‚ü®Œª y, hf.mk f (x, y), hf.strongly_measurable_mk.comp_measurable measurable_prod_mk_left, hx‚ü©
end

end measure_theory

namespace probability_theory

/-! ### The Lebesgue integral on a product -/

lemma kernel.lintegral_comp_prod'' (f : Œ≤ √ó Œ≥ ‚Üí ‚Ñù‚â•0‚àû) (hf : ae_measurable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´‚Åª z, f z ‚àÇ((Œ∫ ‚äó‚Çñ Œ∑) a) = ‚à´‚Åª x, ‚à´‚Åª y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) :=
begin
  have A : ‚à´‚Åª z, f z ‚àÇ((Œ∫ ‚äó‚Çñ Œ∑) a) = ‚à´‚Åª z, hf.mk f z ‚àÇ((Œ∫ ‚äó‚Çñ Œ∑) a) :=
    lintegral_congr_ae hf.ae_eq_mk,
  have B : ‚à´‚Åª x, ‚à´‚Åª y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) = ‚à´‚Åª x, ‚à´‚Åª y, hf.mk f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a),
  { apply lintegral_congr_ae,
    filter_upwards [ae_ae_of_ae_comp_prod hf.ae_eq_mk] with _ ha using lintegral_congr_ae ha, },
  rw [A, B, kernel.lintegral_comp_prod],
  exact hf.measurable_mk,
end

/-! ### Integrability on a product -/

lemma has_finite_integral_comp_prod_iff ‚¶Éf : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ (h1f : strongly_measurable f) :
  has_finite_integral f ((Œ∫ ‚äó‚Çñ Œ∑) a)
    ‚Üî (‚àÄ·µê x ‚àÇ(Œ∫ a), has_finite_integral (Œª y, f (x, y)) (Œ∑ (a, x))) ‚àß
      has_finite_integral (Œª x, ‚à´ y, ‚Äñf (x, y)‚Äñ ‚àÇ(Œ∑ (a, x))) (Œ∫ a) :=
begin
  simp only [has_finite_integral],
  rw kernel.lintegral_comp_prod _ _ _ h1f.ennnorm,
  have : ‚àÄ x, ‚àÄ·µê y ‚àÇ(Œ∑ (a, x)), 0 ‚â§ ‚Äñf (x, y)‚Äñ := Œª x, eventually_of_forall (Œª y, norm_nonneg _),
  simp_rw [integral_eq_lintegral_of_nonneg_ae (this _)
    (h1f.norm.comp_measurable measurable_prod_mk_left).ae_strongly_measurable,
    ennnorm_eq_of_real to_real_nonneg, of_real_norm_eq_coe_nnnorm],
  have : ‚àÄ {p q r : Prop} (h1 : r ‚Üí p), (r ‚Üî p ‚àß q) ‚Üî (p ‚Üí (r ‚Üî q)) :=
  Œª p q r h1, by rw [‚Üê and.congr_right_iff, and_iff_right_of_imp h1],
  rw [this],
  { intro h2f, rw lintegral_congr_ae,
    refine h2f.mp _, apply eventually_of_forall, intros x hx, dsimp only,
    rw [of_real_to_real], rw [‚Üê lt_top_iff_ne_top], exact hx },
  { intro h2f, refine ae_lt_top _ h2f.ne, exact h1f.ennnorm.lintegral_kernel_prod_right'' },
end

lemma has_finite_integral_comp_prod_iff' ‚¶Éf : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ
  (h1f : ae_strongly_measurable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  has_finite_integral f ((Œ∫ ‚äó‚Çñ Œ∑) a)
    ‚Üî (‚àÄ·µê x ‚àÇ(Œ∫ a), has_finite_integral (Œª y, f (x, y)) (Œ∑ (a, x))) ‚àß
      has_finite_integral (Œª x, ‚à´ y, ‚Äñf (x, y)‚Äñ ‚àÇ(Œ∑ (a, x))) (Œ∫ a) :=
begin
  rw [has_finite_integral_congr h1f.ae_eq_mk,
    has_finite_integral_comp_prod_iff h1f.strongly_measurable_mk],
  apply and_congr,
  { apply eventually_congr,
    filter_upwards [ae_ae_of_ae_comp_prod h1f.ae_eq_mk.symm],
    assume x hx,
    exact has_finite_integral_congr hx },
  { apply has_finite_integral_congr,
    filter_upwards [ae_ae_of_ae_comp_prod h1f.ae_eq_mk.symm] with _ hx
      using integral_congr_ae (eventually_eq.fun_comp hx _), },
end

lemma integrable_comp_prod_iff ‚¶Éf : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ (h1f : ae_strongly_measurable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a) ‚Üî
    (‚àÄ·µê x ‚àÇ(Œ∫ a), integrable (Œª y, f (x, y)) (Œ∑ (a, x)))
    ‚àß integrable (Œª x, ‚à´ y, ‚Äñf (x, y)‚Äñ ‚àÇ(Œ∑ (a, x))) (Œ∫ a) :=
by simp only [integrable, has_finite_integral_comp_prod_iff' h1f,
  h1f.norm.integral_kernel_prod_right', h1f, h1f.comp_prod_mk_left, eventually_and, true_and]

lemma _root_.measure_theory.integrable.comp_prod_right_ae
  ‚¶Éf : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚àÄ·µê x ‚àÇ(Œ∫ a), integrable (Œª y, f (x, y)) (Œ∑ (a, x)) :=
((integrable_comp_prod_iff hf.ae_strongly_measurable).mp hf).1

lemma _root_.measure_theory.integrable.integral_norm_comp_prod_left
  ‚¶Éf : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  integrable (Œª x, ‚à´ y, ‚Äñf (x, y)‚Äñ ‚àÇ(Œ∑ (a, x))) (Œ∫ a) :=
((integrable_comp_prod_iff hf.ae_strongly_measurable).mp hf).2

lemma _root_.measure_theory.integrable.integral_comp_prod_left [normed_space ‚Ñù E] [complete_space E]
  ‚¶Éf : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  integrable (Œª x, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x))) (Œ∫ a) :=
integrable.mono hf.integral_norm_comp_prod_left
  hf.ae_strongly_measurable.integral_kernel_prod_right' $
  eventually_of_forall $ Œª x, (norm_integral_le_integral_norm _).trans_eq $
  (norm_of_nonneg $ integral_nonneg_of_ae $ eventually_of_forall $
  Œª y, (norm_nonneg (f (x, y)) : _)).symm

/-! ### The Bochner integral on a product -/

variables [normed_space ‚Ñù E] [complete_space E]
  {E' : Type*} [normed_add_comm_group E'] [complete_space E'] [normed_space ‚Ñù E']

lemma kernel.integral_fn_integral_add ‚¶Éf g : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ (F : E ‚Üí E')
  (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) (hg : integrable g ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´ x, F (‚à´ y, f (x, y) + g (x, y) ‚àÇ(Œ∑ (a, x))) ‚àÇ(Œ∫ a)
    = ‚à´ x, F (‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) + ‚à´ y, g (x, y) ‚àÇ(Œ∑ (a, x))) ‚àÇ(Œ∫ a) :=
begin
  refine integral_congr_ae _,
  filter_upwards [hf.comp_prod_right_ae, hg.comp_prod_right_ae] with _ h2f h2g,
  simp [integral_add h2f h2g],
end

lemma kernel.integral_fn_integral_sub ‚¶Éf g : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ (F : E ‚Üí E')
  (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) (hg : integrable g ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´ x, F (‚à´ y, f (x, y) - g (x, y) ‚àÇ(Œ∑ (a, x))) ‚àÇ(Œ∫ a)
    = ‚à´ x, F (‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) - ‚à´ y, g (x, y) ‚àÇ(Œ∑ (a, x))) ‚àÇ(Œ∫ a) :=
begin
  refine integral_congr_ae _,
  filter_upwards [hf.comp_prod_right_ae, hg.comp_prod_right_ae] with _ h2f h2g,
  simp [integral_sub h2f h2g],
end

lemma kernel.lintegral_fn_integral_sub ‚¶Éf g : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ
  (F : E ‚Üí ‚Ñù‚â•0‚àû) (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) (hg : integrable g ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´‚Åª x, F (‚à´ y, f (x, y) - g (x, y) ‚àÇ(Œ∑ (a, x))) ‚àÇ(Œ∫ a)
    = ‚à´‚Åª x, F (‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) - ‚à´ y, g (x, y) ‚àÇ(Œ∑ (a, x))) ‚àÇ(Œ∫ a) :=
begin
  refine lintegral_congr_ae _,
  filter_upwards [hf.comp_prod_right_ae, hg.comp_prod_right_ae] with _ h2f h2g,
  simp [integral_sub h2f h2g],
end

lemma kernel.integral_integral_add ‚¶Éf g : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ
  (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) (hg : integrable g ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´ x, ‚à´ y, f (x, y) + g (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a)
    = ‚à´ x, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) + ‚à´ x, ‚à´ y, g (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) :=
(kernel.integral_fn_integral_add id hf hg).trans $
  integral_add hf.integral_comp_prod_left hg.integral_comp_prod_left

lemma kernel.integral_integral_add' ‚¶Éf g : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ
  (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) (hg : integrable g ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´ x, ‚à´ y, (f + g) (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a)
    = ‚à´ x, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) + ‚à´ x, ‚à´ y, g (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) :=
kernel.integral_integral_add hf hg

lemma kernel.integral_integral_sub ‚¶Éf g : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ
  (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) (hg : integrable g ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´ x, ‚à´ y, f (x, y) - g (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a)
    = ‚à´ x, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) - ‚à´ x, ‚à´ y, g (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) :=
(kernel.integral_fn_integral_sub id hf hg).trans $
  integral_sub hf.integral_comp_prod_left hg.integral_comp_prod_left

lemma kernel.integral_integral_sub' ‚¶Éf g : Œ≤ √ó Œ≥ ‚Üí E‚¶Ñ
  (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)) (hg : integrable g ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´ x, ‚à´ y, (f - g) (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a)
    = ‚à´ x, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) - ‚à´ x, ‚à´ y, g (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) :=
kernel.integral_integral_sub hf hg

lemma kernel.continuous_integral_integral :
  continuous (Œª (f : Œ± √ó Œ≤ ‚Üí‚ÇÅ[(Œ∫ ‚äó‚Çñ Œ∑) a] E), ‚à´ x, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a)) :=
begin
  rw [continuous_iff_continuous_at], intro g,
  refine tendsto_integral_of_L1 _ (L1.integrable_coe_fn g).integral_comp_prod_left
    (eventually_of_forall $ Œª h, (L1.integrable_coe_fn h).integral_comp_prod_left) _,
  simp_rw [‚Üê kernel.lintegral_fn_integral_sub (Œª x, (‚Äñx‚Äñ‚Çä : ‚Ñù‚â•0‚àû)) (L1.integrable_coe_fn _)
    (L1.integrable_coe_fn g)],
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds _ (Œª i, zero_le _) _,
  { exact Œª i, ‚à´‚Åª x, ‚à´‚Åª y, ‚Äñi (x, y) - g (x, y)‚Äñ‚Çä ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) },
  swap, { exact Œª i, lintegral_mono (Œª x, ennnorm_integral_le_lintegral_ennnorm _) },
  show tendsto (Œª (i : Œ≤ √ó Œ≥ ‚Üí‚ÇÅ[(Œ∫ ‚äó‚Çñ Œ∑) a] E),
    ‚à´‚Åª x, ‚à´‚Åª (y : Œ≥), ‚Äñi (x, y) - g (x, y)‚Äñ‚Çä ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a)) (ùìù g) (ùìù 0),
  have : ‚àÄ (i : Œ± √ó Œ≤ ‚Üí‚ÇÅ[(Œ∫ ‚äó‚Çñ Œ∑) a] E), measurable (Œª z, (‚Äñi z - g z‚Äñ‚Çä : ‚Ñù‚â•0‚àû)) :=
  Œª i, ((Lp.strongly_measurable i).sub (Lp.strongly_measurable g)).ennnorm,
  simp_rw [‚Üê kernel.lintegral_comp_prod _ _ _ (this _), ‚Üê L1.of_real_norm_sub_eq_lintegral,
    ‚Üê of_real_zero],
  refine (continuous_of_real.tendsto 0).comp _,
  rw [‚Üê tendsto_iff_norm_tendsto_zero], exact tendsto_id
end

lemma integral_comp_prod : ‚àÄ (f : Œ≤ √ó Œ≥ ‚Üí E) (hf : integrable f ((Œ∫ ‚äó‚Çñ Œ∑) a)),
  ‚à´ z, f z ‚àÇ((Œ∫ ‚äó‚Çñ Œ∑) a) = ‚à´ x, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) :=
begin
  apply integrable.induction,
  { intros c s hs h2s,
    simp_rw [integral_indicator hs, ‚Üê indicator_comp_right,
      function.comp, integral_indicator (measurable_prod_mk_left hs),
      set_integral_const, integral_smul_const],
    congr' 1,
    rw integral_to_real,
    rotate,
    { exact (kernel.measurable_kernel_prod_mk_left' hs _).ae_measurable, },
    { exact (ae_kernel_lt_top a hs h2s.ne), },
    rw kernel.comp_prod_apply _ _ _ hs,
    refl, },
  { intros f g hfg i_f i_g hf hg,
    simp_rw [integral_add' i_f i_g, kernel.integral_integral_add' i_f i_g, hf, hg] },
  { exact is_closed_eq continuous_integral kernel.continuous_integral_integral },
  { intros f g hfg i_f hf, convert hf using 1,
    { exact integral_congr_ae hfg.symm },
    { refine integral_congr_ae _,
      refine (ae_ae_of_ae_comp_prod hfg).mp _,
      apply eventually_of_forall, intros x hfgx,
      exact integral_congr_ae (ae_eq_symm hfgx) } }
end

lemma set_integral_comp_prod (f : Œ≤ √ó Œ≥ ‚Üí E) {s : set Œ≤} {t : set Œ≥}
  (hs : measurable_set s) (ht : measurable_set t) (hf : integrable_on f (s √óÀ¢ t) ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´ z in s √óÀ¢ t, f z ‚àÇ((Œ∫ ‚äó‚Çñ Œ∑) a) = ‚à´ x in s, ‚à´ y in t, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) :=
begin
  rw [‚Üê kernel.restrict_apply (Œ∫ ‚äó‚Çñ Œ∑) (hs.prod ht), ‚Üê comp_prod_restrict, integral_comp_prod],
  { simp_rw kernel.restrict_apply, },
  { rw [comp_prod_restrict, kernel.restrict_apply], exact hf, },
end

lemma set_integral_comp_prod_univ (f : Œ≤ √ó Œ≥ ‚Üí E) {s : set Œ≤}
  (hs : measurable_set s) (hf : integrable_on f (s √óÀ¢ univ) ((Œ∫ ‚äó‚Çñ Œ∑) a)) :
  ‚à´ z in s √óÀ¢ univ, f z ‚àÇ((Œ∫ ‚äó‚Çñ Œ∑) a) = ‚à´ x in s, ‚à´ y, f (x, y) ‚àÇ(Œ∑ (a, x)) ‚àÇ(Œ∫ a) :=
by { rw set_integral_comp_prod f hs measurable_set.univ hf, simp_rw measure.restrict_univ, }

end probability_theory
