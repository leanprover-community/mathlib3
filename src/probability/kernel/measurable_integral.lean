/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import probability.kernel.basic

/-!
# Measurability of the integral against a kernel

The Lebesgue integral of a measurable function against a kernel is measurable. The Bochner integral
is strongly measurable.

## Main statements

* `probability_theory.measurable.lintegral_kernel_prod_right`: the function `a â†¦ âˆ«â» b, f a b âˆ‚(Îº a)`
  is measurable, for an s-finite kernel `Îº : kernel Î± Î²` and a function `f : Î± â†’ Î² â†’ â„â‰¥0âˆ` such that
  `uncurry f` is measurable.
* `measure_theory.strongly_measurable.integral_kernel_prod_right`: the function
  `a â†¦ âˆ« b, f a b âˆ‚(Îº a)` is measurable, for an s-finite kernel `Îº : kernel Î± Î²` and a function
  `f : Î± â†’ Î² â†’ E` such that `uncurry f` is measurable.

-/

open measure_theory probability_theory function set filter

open_locale measure_theory ennreal topology

variables {Î± Î² Î³ : Type*} {mÎ± : measurable_space Î±} {mÎ² : measurable_space Î²}
  {mÎ³ : measurable_space Î³}
  {Îº : kernel Î± Î²} {Î· : kernel (Î± Ã— Î²) Î³} {a : Î±}

namespace probability_theory
namespace kernel

/-- This is an auxiliary lemma for `measurable_kernel_prod_mk_left`. -/
lemma measurable_kernel_prod_mk_left_of_finite {t : set (Î± Ã— Î²)} (ht : measurable_set t)
  (hÎºs : âˆ€ a, is_finite_measure (Îº a)) :
  measurable (Î» a, Îº a (prod.mk a â»Â¹' t)) :=
begin
  -- `t` is a measurable set in the product `Î± Ã— Î²`: we use that the product Ïƒ-algebra is generated
  -- by boxes to prove the result by induction.
  refine measurable_space.induction_on_inter generate_from_prod.symm is_pi_system_prod _ _ _ _ ht,
  { -- case `t = âˆ…`
    simp only [preimage_empty, measure_empty, measurable_const], },
  { -- case of a box: `t = tâ‚ Ã—Ë¢ tâ‚‚` for measurable sets `tâ‚` and `tâ‚‚`
    intros t' ht',
    simp only [set.mem_image2, set.mem_set_of_eq, exists_and_distrib_left] at ht',
    obtain âŸ¨tâ‚, htâ‚, tâ‚‚, htâ‚‚, rflâŸ© := ht',
    classical,
    simp_rw mk_preimage_prod_right_eq_if,
    have h_eq_ite : (Î» a, Îº a (ite (a âˆˆ tâ‚) tâ‚‚ âˆ…)) = Î» a, ite (a âˆˆ tâ‚) (Îº a tâ‚‚) 0,
    { ext1 a,
      split_ifs,
      exacts [rfl, measure_empty], },
    rw h_eq_ite,
    exact measurable.ite htâ‚ (kernel.measurable_coe Îº htâ‚‚) measurable_const },
  { -- we assume that the result is true for `t` and we prove it for `tá¶œ`
    intros t' ht' h_meas,
    have h_eq_sdiff : âˆ€ a, (prod.mk a â»Â¹' t'á¶œ) = set.univ \ (prod.mk a â»Â¹' t'),
    { intro a,
      ext1 b,
      simp only [mem_compl_iff, mem_preimage, mem_diff, mem_univ, true_and], },
    simp_rw h_eq_sdiff,
    have : (Î» a, Îº a (set.univ \ (prod.mk a â»Â¹' t')))
      = (Î» a, (Îº a set.univ - Îº a (prod.mk a â»Â¹' t'))),
    { ext1 a,
      rw [â† set.diff_inter_self_eq_diff, set.inter_univ, measure_diff (set.subset_univ _)],
      { exact (@measurable_prod_mk_left Î± Î² _ _ a) t' ht', },
      { exact measure_ne_top _ _, }, },
    rw this,
    exact measurable.sub (kernel.measurable_coe Îº measurable_set.univ) h_meas, },
  { -- we assume that the result is true for a family of disjoint sets and prove it for their union
    intros f h_disj hf_meas hf,
    have h_Union : (Î» a, Îº a (prod.mk a â»Â¹' â‹ƒ i, f i)) = Î» a, Îº a (â‹ƒ i, prod.mk a â»Â¹' f i),
    { ext1 a,
      congr' with b,
      simp only [mem_Union, mem_preimage], },
    rw h_Union,
    have h_tsum : (Î» a, Îº a (â‹ƒ i, prod.mk a â»Â¹' f i)) = Î» a, âˆ‘' i, Îº a (prod.mk a â»Â¹' f i),
    { ext1 a,
      rw measure_Union,
      { intros i j hij s hsi hsj b hbs,
        have habi : {(a, b)} âŠ† f i, by { rw set.singleton_subset_iff, exact hsi hbs, },
        have habj : {(a, b)} âŠ† f j, by { rw set.singleton_subset_iff, exact hsj hbs, },
        simpa only [set.bot_eq_empty, set.le_eq_subset, set.singleton_subset_iff,
          set.mem_empty_iff_false] using h_disj hij habi habj, },
      { exact Î» i, (@measurable_prod_mk_left Î± Î² _ _ a) _ (hf_meas i), }, },
    rw h_tsum,
    exact measurable.ennreal_tsum hf, },
end

lemma measurable_kernel_prod_mk_left [is_s_finite_kernel Îº]
  {t : set (Î± Ã— Î²)} (ht : measurable_set t) :
  measurable (Î» a, Îº a (prod.mk a â»Â¹' t)) :=
begin
  rw â† kernel_sum_seq Îº,
  have : âˆ€ a, kernel.sum (seq Îº) a (prod.mk a â»Â¹' t) = âˆ‘' n, seq Îº n a (prod.mk a â»Â¹' t),
    from Î» a, kernel.sum_apply' _ _ (measurable_prod_mk_left ht),
  simp_rw this,
  refine measurable.ennreal_tsum (Î» n, _),
  exact measurable_kernel_prod_mk_left_of_finite ht infer_instance,
end

lemma measurable_kernel_prod_mk_left' [is_s_finite_kernel Î·]
  {s : set (Î² Ã— Î³)} (hs : measurable_set s) (a : Î±) :
  measurable (Î» b, Î· (a, b) (prod.mk b â»Â¹' s)) :=
begin
  have : âˆ€ b, prod.mk b â»Â¹' s = {c | ((a, b), c) âˆˆ {p : (Î± Ã— Î²) Ã— Î³ | (p.1.2, p.2) âˆˆ s}},
  { intro b, refl, },
  simp_rw this,
  refine (measurable_kernel_prod_mk_left _).comp measurable_prod_mk_left,
  exact (measurable_fst.snd.prod_mk measurable_snd) hs,
end

lemma measurable_kernel_prod_mk_right [is_s_finite_kernel Îº]
  {s : set (Î² Ã— Î±)} (hs : measurable_set s) :
  measurable (Î» y, Îº y ((Î» x, (x, y)) â»Â¹' s)) :=
measurable_kernel_prod_mk_left (measurable_set_swap_iff.mpr hs)

end kernel

open probability_theory.kernel

section lintegral

variables [is_s_finite_kernel Îº] [is_s_finite_kernel Î·]

/-- Auxiliary lemma for `measurable.lintegral_kernel_prod_right`. -/
lemma kernel.measurable_lintegral_indicator_const {t : set (Î± Ã— Î²)} (ht : measurable_set t)
  (c : â„â‰¥0âˆ) :
  measurable (Î» a, âˆ«â» b, t.indicator (function.const (Î± Ã— Î²) c) (a, b) âˆ‚(Îº a)) :=
begin
  simp_rw lintegral_indicator_const_comp measurable_prod_mk_left ht _,
  exact measurable.const_mul (measurable_kernel_prod_mk_left ht) c,
end

/-- For an s-finite kernel `Îº` and a function `f : Î± â†’ Î² â†’ â„â‰¥0âˆ` which is measurable when seen as a
map from `Î± Ã— Î²` (hypothesis `measurable (uncurry f)`), the integral `a â†¦ âˆ«â» b, f a b âˆ‚(Îº a)` is
measurable. -/
theorem measurable.lintegral_kernel_prod_right {f : Î± â†’ Î² â†’ â„â‰¥0âˆ} (hf : measurable (uncurry f)) :
  measurable (Î» a, âˆ«â» b, f a b âˆ‚(Îº a)) :=
begin
  let F : â„• â†’ simple_func (Î± Ã— Î²) â„â‰¥0âˆ := simple_func.eapprox (uncurry f),
  have h : âˆ€ a, (â¨† n, F n a) = uncurry f a,
    from simple_func.supr_eapprox_apply (uncurry f) hf,
  simp only [prod.forall, uncurry_apply_pair] at h,
  simp_rw â† h,
  have : âˆ€ a, âˆ«â» b, (â¨† n, F n (a, b)) âˆ‚(Îº a) = â¨† n, âˆ«â» b, F n (a, b) âˆ‚(Îº a),
  { intro a,
    rw lintegral_supr,
    { exact Î» n, (F n).measurable.comp measurable_prod_mk_left, },
    { exact Î» i j hij b, simple_func.monotone_eapprox (uncurry f) hij _, }, },
  simp_rw this,
  refine measurable_supr (Î» n, simple_func.induction _ _ (F n)),
  { intros c t ht,
    simp only [simple_func.const_zero, simple_func.coe_piecewise, simple_func.coe_const,
      simple_func.coe_zero, set.piecewise_eq_indicator],
    exact kernel.measurable_lintegral_indicator_const ht c, },
  { intros gâ‚ gâ‚‚ h_disj hmâ‚ hmâ‚‚,
    simp only [simple_func.coe_add, pi.add_apply],
    have h_add : (Î» a, âˆ«â» b, gâ‚ (a, b) + gâ‚‚ (a, b) âˆ‚(Îº a))
      = (Î» a, âˆ«â» b, gâ‚ (a, b) âˆ‚(Îº a)) + (Î» a, âˆ«â» b, gâ‚‚ (a, b) âˆ‚(Îº a)),
    { ext1 a,
      rw [pi.add_apply, lintegral_add_left (gâ‚.measurable.comp measurable_prod_mk_left)], },
    rw h_add,
    exact measurable.add hmâ‚ hmâ‚‚, },
end

lemma measurable.lintegral_kernel_prod_right' {f : (Î± Ã— Î²) â†’ â„â‰¥0âˆ} (hf : measurable f) :
  measurable (Î» a, âˆ«â» b, f (a, b) âˆ‚(Îº a)) :=
begin
  refine measurable.lintegral_kernel_prod_right _,
  have : uncurry (Î» (a : Î±) (b : Î²), f (a, b)) = f,
  { ext x, rw [â† @prod.mk.eta _ _ x, uncurry_apply_pair], },
  rwa this,
end

lemma measurable.lintegral_kernel_prod_right'' {f : Î² Ã— Î³ â†’ â„â‰¥0âˆ} (hf : measurable f) :
  measurable (Î» x, âˆ«â» y, f (x, y) âˆ‚(Î· (a, x))) :=
begin
  change measurable ((Î» x, âˆ«â» y, (Î» u : (Î± Ã— Î²) Ã— Î³, f (u.1.2, u.2)) (x, y) âˆ‚(Î· x))
    âˆ˜ (Î» x, (a, x))),
  refine (measurable.lintegral_kernel_prod_right' _).comp measurable_prod_mk_left,
  exact hf.comp (measurable_fst.snd.prod_mk measurable_snd),
end

lemma measurable.set_lintegral_kernel_prod_right
  {f : Î± â†’ Î² â†’ â„â‰¥0âˆ} (hf : measurable (uncurry f)) {s : set Î²} (hs : measurable_set s) :
  measurable (Î» a, âˆ«â» b in s, f a b âˆ‚(Îº a)) :=
by { simp_rw â† lintegral_restrict Îº hs, exact hf.lintegral_kernel_prod_right }

lemma measurable.lintegral_kernel_prod_left' {f : Î² Ã— Î± â†’ â„â‰¥0âˆ} (hf : measurable f) :
  measurable (Î» y, âˆ«â» x, f (x, y) âˆ‚(Îº y)) :=
(measurable_swap_iff.mpr hf).lintegral_kernel_prod_right'

lemma measurable.lintegral_kernel_prod_left
  {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} (hf : measurable (uncurry f)) :
  measurable (Î» y, âˆ«â» x, f x y âˆ‚(Îº y)) :=
hf.lintegral_kernel_prod_left'

lemma measurable.set_lintegral_kernel_prod_left
  {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} (hf : measurable (uncurry f)) {s : set Î²} (hs : measurable_set s) :
  measurable (Î» b, âˆ«â» a in s, f a b âˆ‚(Îº b)) :=
by { simp_rw â† lintegral_restrict Îº hs, exact hf.lintegral_kernel_prod_left }

lemma measurable.lintegral_kernel {f : Î² â†’ â„â‰¥0âˆ} (hf : measurable f) :
  measurable (Î» a, âˆ«â» b, f b âˆ‚(Îº a)) :=
measurable.lintegral_kernel_prod_right (hf.comp measurable_snd)

lemma measurable.set_lintegral_kernel
  {f : Î² â†’ â„â‰¥0âˆ} (hf : measurable f) {s : set Î²} (hs : measurable_set s) :
  measurable (Î» a, âˆ«â» b in s, f b âˆ‚(Îº a)) :=
measurable.set_lintegral_kernel_prod_right (hf.comp measurable_snd) hs

end lintegral

variables {E : Type*} [normed_add_comm_group E] [is_s_finite_kernel Îº] [is_s_finite_kernel Î·]

lemma measurable_set_kernel_integrable â¦ƒf : Î± â†’ Î² â†’ Eâ¦„ (hf : strongly_measurable (uncurry f)) :
  measurable_set {x | integrable (f x) (Îº x)} :=
begin
  simp_rw [integrable, hf.of_uncurry_left.ae_strongly_measurable, true_and],
  exact measurable_set_lt (measurable.lintegral_kernel_prod_right hf.ennnorm) measurable_const
end

end probability_theory

open probability_theory probability_theory.kernel

namespace measure_theory

variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [complete_space E]
  [is_s_finite_kernel Îº] [is_s_finite_kernel Î·]

lemma strongly_measurable.integral_kernel_prod_right
  â¦ƒf : Î± â†’ Î² â†’ Eâ¦„ (hf : strongly_measurable (uncurry f)) :
  strongly_measurable (Î» x, âˆ« y, f x y âˆ‚(Îº x)) :=
begin
  classical,
  borelize E,
  haveI : topological_space.separable_space (range (uncurry f) âˆª {0} : set E) :=
    hf.separable_space_range_union_singleton,
  let s : â„• â†’ simple_func (Î± Ã— Î²) E := simple_func.approx_on _ hf.measurable
    (range (uncurry f) âˆª {0}) 0 (by simp),
  let s' : â„• â†’ Î± â†’ simple_func Î² E := Î» n x, (s n).comp (prod.mk x) measurable_prod_mk_left,
  let f' : â„• â†’ Î± â†’ E := Î» n, {x | integrable (f x) (Îº x)}.indicator
    (Î» x, (s' n x).integral (Îº x)),
  have hf' : âˆ€ n, strongly_measurable (f' n),
  { intro n, refine strongly_measurable.indicator _ (measurable_set_kernel_integrable hf),
    have : âˆ€ x, (s' n x).range.filter (Î» x, x â‰  0) âŠ† (s n).range,
    { intros x, refine finset.subset.trans (finset.filter_subset _ _) _, intro y,
      simp_rw [simple_func.mem_range], rintro âŸ¨z, rflâŸ©, exact âŸ¨(x, z), rflâŸ© },
    simp only [simple_func.integral_eq_sum_of_subset (this _)],
    refine finset.strongly_measurable_sum _ (Î» x _, _),
    refine (measurable.ennreal_to_real _).strongly_measurable.smul_const _,
    simp only [simple_func.coe_comp, preimage_comp] {single_pass := tt},
    apply measurable_kernel_prod_mk_left,
    exact (s n).measurable_set_fiber x },
  have h2f' : tendsto f' at_top (ğ“ (Î» (x : Î±), âˆ« (y : Î²), f x y âˆ‚(Îº x))),
  { rw [tendsto_pi_nhds], intro x,
    by_cases hfx : integrable (f x) (Îº x),
    { have : âˆ€ n, integrable (s' n x) (Îº x),
      { intro n, apply (hfx.norm.add hfx.norm).mono' (s' n x).ae_strongly_measurable,
        apply eventually_of_forall, intro y,
        simp_rw [s', simple_func.coe_comp], exact simple_func.norm_approx_on_zero_le _ _ (x, y) n },
      simp only [f', hfx, simple_func.integral_eq_integral _ (this _), indicator_of_mem,
        mem_set_of_eq],
      refine tendsto_integral_of_dominated_convergence (Î» y, â€–f x yâ€– + â€–f x yâ€–)
        (Î» n, (s' n x).ae_strongly_measurable) (hfx.norm.add hfx.norm) _ _,
      { exact Î» n, eventually_of_forall (Î» y, simple_func.norm_approx_on_zero_le _ _ (x, y) n) },
      { refine eventually_of_forall (Î» y, simple_func.tendsto_approx_on _ _ _),
        apply subset_closure,
        simp [-uncurry_apply_pair], } },
    { simp [f', hfx, integral_undef], } },
  exact strongly_measurable_of_tendsto _ hf' h2f',
end

lemma strongly_measurable.integral_kernel_prod_right'
  â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : strongly_measurable f) :
  strongly_measurable (Î» x, âˆ« y, f (x, y) âˆ‚(Îº x)) :=
by { rw [â† uncurry_curry f] at hf, exact hf.integral_kernel_prod_right }

lemma strongly_measurable.integral_kernel_prod_right''
  {f : Î² Ã— Î³ â†’ E} (hf : strongly_measurable f) :
  strongly_measurable (Î» x, âˆ« y, f (x, y) âˆ‚(Î· (a, x))) :=
begin
  change strongly_measurable ((Î» x, âˆ« y, (Î» u : (Î± Ã— Î²) Ã— Î³, f (u.1.2, u.2)) (x, y) âˆ‚(Î· x))
    âˆ˜ (Î» x, (a, x))),
  refine strongly_measurable.comp_measurable _ measurable_prod_mk_left,
  refine measure_theory.strongly_measurable.integral_kernel_prod_right' _,
  exact hf.comp_measurable (measurable_fst.snd.prod_mk measurable_snd),
end

lemma strongly_measurable.integral_kernel_prod_left
  â¦ƒf : Î² â†’ Î± â†’ Eâ¦„ (hf : strongly_measurable (uncurry f)) :
  strongly_measurable (Î» y, âˆ« x, f x y âˆ‚(Îº y)) :=
(hf.comp_measurable measurable_swap).integral_kernel_prod_right'

lemma strongly_measurable.integral_kernel_prod_left'
  â¦ƒf : Î² Ã— Î± â†’ Eâ¦„ (hf : strongly_measurable f) :
  strongly_measurable (Î» y, âˆ« x, f (x, y) âˆ‚(Îº y)) :=
(hf.comp_measurable measurable_swap).integral_kernel_prod_right'

lemma strongly_measurable.integral_kernel_prod_left''
  {f : Î³ Ã— Î² â†’ E} (hf : strongly_measurable f) :
  strongly_measurable (Î» y, âˆ« x, f (x, y) âˆ‚(Î· (a, y))) :=
begin
  change strongly_measurable ((Î» y, âˆ« x, (Î» u : Î³ Ã— (Î± Ã— Î²), f (u.1, u.2.2)) (x, y) âˆ‚(Î· y))
    âˆ˜ (Î» x, (a, x))),
  refine strongly_measurable.comp_measurable _ measurable_prod_mk_left,
  refine measure_theory.strongly_measurable.integral_kernel_prod_left' _,
  exact hf.comp_measurable (measurable_fst.prod_mk measurable_snd.snd),
end

end measure_theory
