/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/
import probability.notation
import probability.process.stopping

/-!
# Martingales

A family of functions `f : Î¹ â†’ Î© â†’ E` is a martingale with respect to a filtration `â„±` if every
`f i` is integrable, `f` is adapted with respect to `â„±` and for all `i â‰¤ j`,
`Î¼[f j | â„± i] =áµ[Î¼] f i`. On the other hand, `f : Î¹ â†’ Î© â†’ E` is said to be a supermartingale
with respect to the filtration `â„±` if `f i` is integrable, `f` is adapted with resepct to `â„±`
and for all `i â‰¤ j`, `Î¼[f j | â„± i] â‰¤áµ[Î¼] f i`. Finally, `f : Î¹ â†’ Î© â†’ E` is said to be a
submartingale with respect to the filtration `â„±` if `f i` is integrable, `f` is adapted with
resepct to `â„±` and for all `i â‰¤ j`, `f i â‰¤áµ[Î¼] Î¼[f j | â„± i]`.

The definitions of filtration and adapted can be found in `probability.stopping`.

### Definitions

* `measure_theory.martingale f â„± Î¼`: `f` is a martingale with respect to filtration `â„±` and
  measure `Î¼`.
* `measure_theory.supermartingale f â„± Î¼`: `f` is a supermartingale with respect to
  filtration `â„±` and measure `Î¼`.
* `measure_theory.submartingale f â„± Î¼`: `f` is a submartingale with respect to filtration `â„±` and
  measure `Î¼`.

### Results

* `measure_theory.martingale_condexp f â„± Î¼`: the sequence `Î» i, Î¼[f | â„± i, â„±.le i])` is a
  martingale with respect to `â„±` and `Î¼`.

-/

open topological_space filter
open_locale nnreal ennreal measure_theory probability_theory big_operators

namespace measure_theory

variables {Î© E Î¹ : Type*} [preorder Î¹]
  {m0 : measurable_space Î©} {Î¼ : measure Î©}
  [normed_add_comm_group E] [normed_space â„ E] [complete_space E]
  {f g : Î¹ â†’ Î© â†’ E} {â„± : filtration Î¹ m0}

/-- A family of functions `f : Î¹ â†’ Î© â†’ E` is a martingale with respect to a filtration `â„±` if `f`
is adapted with respect to `â„±` and for all `i â‰¤ j`, `Î¼[f j | â„± i] =áµ[Î¼] f i`. -/
def martingale (f : Î¹ â†’ Î© â†’ E) (â„± : filtration Î¹ m0) (Î¼ : measure Î© . volume_tac) : Prop :=
adapted â„± f âˆ§ âˆ€ i j, i â‰¤ j â†’ Î¼[f j | â„± i] =áµ[Î¼] f i

/-- A family of integrable functions `f : Î¹ â†’ Î© â†’ E` is a supermartingale with respect to a
filtration `â„±` if `f` is adapted with respect to `â„±` and for all `i â‰¤ j`,
`Î¼[f j | â„±.le i] â‰¤áµ[Î¼] f i`. -/
def supermartingale [has_le E] (f : Î¹ â†’ Î© â†’ E) (â„± : filtration Î¹ m0) (Î¼ : measure Î© . volume_tac) :
  Prop :=
adapted â„± f âˆ§ (âˆ€ i j, i â‰¤ j â†’ Î¼[f j | â„± i] â‰¤áµ[Î¼] f i) âˆ§ âˆ€ i, integrable (f i) Î¼

/-- A family of integrable functions `f : Î¹ â†’ Î© â†’ E` is a submartingale with respect to a
filtration `â„±` if `f` is adapted with respect to `â„±` and for all `i â‰¤ j`,
`f i â‰¤áµ[Î¼] Î¼[f j | â„±.le i]`. -/
def submartingale [has_le E] (f : Î¹ â†’ Î© â†’ E) (â„± : filtration Î¹ m0) (Î¼ : measure Î© . volume_tac) :
  Prop :=
adapted â„± f âˆ§ (âˆ€ i j, i â‰¤ j â†’ f i â‰¤áµ[Î¼] Î¼[f j | â„± i]) âˆ§ âˆ€ i, integrable (f i) Î¼

lemma martingale_const (â„± : filtration Î¹ m0) (Î¼ : measure Î©) [is_finite_measure Î¼] (x : E) :
  martingale (Î» _ _, x) â„± Î¼ :=
âŸ¨adapted_const â„± _, Î» i j hij, by rw condexp_const (â„±.le _)âŸ©

lemma martingale_const_fun [order_bot Î¹]
  (â„± : filtration Î¹ m0) (Î¼ : measure Î©) [is_finite_measure Î¼]
  {f : Î© â†’ E} (hf : strongly_measurable[â„± âŠ¥] f) (hfint : integrable f Î¼) :
  martingale (Î» _, f) â„± Î¼ :=
begin
  refine âŸ¨Î» i, hf.mono $ â„±.mono bot_le, Î» i j hij, _âŸ©,
  rw condexp_of_strongly_measurable (â„±.le _) (hf.mono $ â„±.mono bot_le) hfint,
  apply_instance,
end

variables (E)
lemma martingale_zero (â„± : filtration Î¹ m0) (Î¼ : measure Î©) :
  martingale (0 : Î¹ â†’ Î© â†’ E) â„± Î¼ :=
âŸ¨adapted_zero E â„±, Î» i j hij, by { rw [pi.zero_apply, condexp_zero], simp, }âŸ©
variables {E}

namespace martingale

@[protected]
lemma adapted (hf : martingale f â„± Î¼) : adapted â„± f := hf.1

@[protected]
lemma strongly_measurable (hf : martingale f â„± Î¼) (i : Î¹) : strongly_measurable[â„± i] (f i) :=
hf.adapted i

lemma condexp_ae_eq (hf : martingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) :
  Î¼[f j | â„± i] =áµ[Î¼] f i :=
hf.2 i j hij

@[protected]
lemma integrable (hf : martingale f â„± Î¼) (i : Î¹) : integrable (f i) Î¼ :=
integrable_condexp.congr (hf.condexp_ae_eq (le_refl i))

lemma set_integral_eq [sigma_finite_filtration Î¼ â„±] (hf : martingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j)
  {s : set Î©} (hs : measurable_set[â„± i] s) :
  âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ :=
begin
  rw â† @set_integral_condexp _ _ _ _ _ (â„± i) m0 _ _ _ (â„±.le i) _ (hf.integrable j) hs,
  refine set_integral_congr_ae (â„±.le i s hs) _,
  filter_upwards [hf.2 i j hij] with _ heq _ using heq.symm,
end

lemma add (hf : martingale f â„± Î¼) (hg : martingale g â„± Î¼) : martingale (f + g) â„± Î¼ :=
begin
  refine âŸ¨hf.adapted.add hg.adapted, Î» i j hij, _âŸ©,
  exact (condexp_add (hf.integrable j) (hg.integrable j)).trans
    ((hf.2 i j hij).add (hg.2 i j hij)),
end

lemma neg (hf : martingale f â„± Î¼) : martingale (-f) â„± Î¼ :=
âŸ¨hf.adapted.neg, Î» i j hij, (condexp_neg (f j)).trans ((hf.2 i j hij).neg)âŸ©

lemma sub (hf : martingale f â„± Î¼) (hg : martingale g â„± Î¼) : martingale (f - g) â„± Î¼ :=
by { rw sub_eq_add_neg, exact hf.add hg.neg, }

lemma smul (c : â„) (hf : martingale f â„± Î¼) : martingale (c â€¢ f) â„± Î¼ :=
begin
  refine âŸ¨hf.adapted.smul c, Î» i j hij, _âŸ©,
  refine (condexp_smul c (f j)).trans ((hf.2 i j hij).mono (Î» x hx, _)),
  rw [pi.smul_apply, hx, pi.smul_apply, pi.smul_apply],
end

lemma supermartingale [preorder E] (hf : martingale f â„± Î¼) : supermartingale f â„± Î¼ :=
âŸ¨hf.1, Î» i j hij, (hf.2 i j hij).le, Î» i, hf.integrable iâŸ©

lemma submartingale [preorder E] (hf : martingale f â„± Î¼) : submartingale f â„± Î¼ :=
âŸ¨hf.1, Î» i j hij, (hf.2 i j hij).symm.le, Î» i, hf.integrable iâŸ©

end martingale

lemma martingale_iff [partial_order E] : martingale f â„± Î¼ â†”
  supermartingale f â„± Î¼ âˆ§ submartingale f â„± Î¼ :=
âŸ¨Î» hf, âŸ¨hf.supermartingale, hf.submartingaleâŸ©,
 Î» âŸ¨hfâ‚, hfâ‚‚âŸ©, âŸ¨hfâ‚.1, Î» i j hij, (hfâ‚.2.1 i j hij).antisymm (hfâ‚‚.2.1 i j hij)âŸ©âŸ©

lemma martingale_condexp (f : Î© â†’ E) (â„± : filtration Î¹ m0) (Î¼ : measure Î©)
  [sigma_finite_filtration Î¼ â„±] :
  martingale (Î» i, Î¼[f | â„± i]) â„± Î¼ :=
âŸ¨Î» i, strongly_measurable_condexp, Î» i j hij, condexp_condexp_of_le (â„±.mono hij) (â„±.le j)âŸ©

namespace supermartingale

@[protected]
lemma adapted [has_le E] (hf : supermartingale f â„± Î¼) : adapted â„± f := hf.1

@[protected]
lemma strongly_measurable [has_le E] (hf : supermartingale f â„± Î¼) (i : Î¹) :
  strongly_measurable[â„± i] (f i) :=
hf.adapted i

@[protected]
lemma integrable [has_le E] (hf : supermartingale f â„± Î¼) (i : Î¹) : integrable (f i) Î¼ := hf.2.2 i

lemma condexp_ae_le [has_le E] (hf : supermartingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) :
  Î¼[f j | â„± i] â‰¤áµ[Î¼] f i :=
hf.2.1 i j hij

lemma set_integral_le [sigma_finite_filtration Î¼ â„±] {f : Î¹ â†’ Î© â†’ â„} (hf : supermartingale f â„± Î¼)
  {i j : Î¹} (hij : i â‰¤ j) {s : set Î©} (hs : measurable_set[â„± i] s) :
  âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ :=
begin
  rw â† set_integral_condexp (â„±.le i) (hf.integrable j) hs,
  refine set_integral_mono_ae integrable_condexp.integrable_on (hf.integrable i).integrable_on _,
  filter_upwards [hf.2.1 i j hij] with _ heq using heq,
end

lemma add [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : supermartingale f â„± Î¼) (hg : supermartingale g â„± Î¼) : supermartingale (f + g) â„± Î¼ :=
begin
  refine âŸ¨hf.1.add hg.1, Î» i j hij, _, Î» i, (hf.2.2 i).add (hg.2.2 i)âŸ©,
  refine (condexp_add (hf.integrable j) (hg.integrable j)).le.trans _,
  filter_upwards [hf.2.1 i j hij, hg.2.1 i j hij],
  intros,
  refine add_le_add _ _; assumption,
end

lemma add_martingale [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : supermartingale f â„± Î¼) (hg : martingale g â„± Î¼) : supermartingale (f + g) â„± Î¼ :=
hf.add hg.supermartingale

lemma neg [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : supermartingale f â„± Î¼) : submartingale (-f) â„± Î¼ :=
begin
  refine âŸ¨hf.1.neg, Î» i j hij, _, Î» i, (hf.2.2 i).negâŸ©,
  refine eventually_le.trans _ (condexp_neg (f j)).symm.le,
  filter_upwards [hf.2.1 i j hij] with _ _,
  simpa,
end

end supermartingale

namespace submartingale

@[protected]
lemma adapted [has_le E] (hf : submartingale f â„± Î¼) : adapted â„± f := hf.1

@[protected]
lemma strongly_measurable [has_le E] (hf : submartingale f â„± Î¼) (i : Î¹) :
  strongly_measurable[â„± i] (f i) :=
hf.adapted i

@[protected]
lemma integrable [has_le E] (hf : submartingale f â„± Î¼) (i : Î¹) : integrable (f i) Î¼ := hf.2.2 i

lemma ae_le_condexp [has_le E] (hf : submartingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) :
  f i â‰¤áµ[Î¼] Î¼[f j | â„± i] :=
hf.2.1 i j hij

lemma add [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : submartingale f â„± Î¼) (hg : submartingale g â„± Î¼) : submartingale (f + g) â„± Î¼ :=
begin
  refine âŸ¨hf.1.add hg.1, Î» i j hij, _, Î» i, (hf.2.2 i).add (hg.2.2 i)âŸ©,
  refine eventually_le.trans _ (condexp_add (hf.integrable j) (hg.integrable j)).symm.le,
  filter_upwards [hf.2.1 i j hij, hg.2.1 i j hij],
  intros,
  refine add_le_add _ _; assumption,
end

lemma add_martingale [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : submartingale f â„± Î¼) (hg : martingale g â„± Î¼) : submartingale (f + g) â„± Î¼ :=
hf.add hg.submartingale

lemma neg [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : submartingale f â„± Î¼) : supermartingale (-f) â„± Î¼ :=
begin
  refine âŸ¨hf.1.neg, Î» i j hij, (condexp_neg (f j)).le.trans _, Î» i, (hf.2.2 i).negâŸ©,
  filter_upwards [hf.2.1 i j hij] with _ _,
  simpa,
end

/-- The converse of this lemma is `measure_theory.submartingale_of_set_integral_le`. -/
lemma set_integral_le [sigma_finite_filtration Î¼ â„±] {f : Î¹ â†’ Î© â†’ â„} (hf : submartingale f â„± Î¼)
  {i j : Î¹} (hij : i â‰¤ j) {s : set Î©} (hs : measurable_set[â„± i] s) :
  âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ :=
begin
  rw [â† neg_le_neg_iff, â† integral_neg, â† integral_neg],
  exact supermartingale.set_integral_le hf.neg hij hs,
end

lemma sub_supermartingale [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : submartingale f â„± Î¼) (hg : supermartingale g â„± Î¼) : submartingale (f - g) â„± Î¼ :=
by { rw sub_eq_add_neg, exact hf.add hg.neg }

lemma sub_martingale [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : submartingale f â„± Î¼) (hg : martingale g â„± Î¼) : submartingale (f - g) â„± Î¼ :=
hf.sub_supermartingale hg.supermartingale

protected lemma sup {f g : Î¹ â†’ Î© â†’ â„} (hf : submartingale f â„± Î¼) (hg : submartingale g â„± Î¼) :
  submartingale (f âŠ” g) â„± Î¼ :=
begin
  refine âŸ¨Î» i, @strongly_measurable.sup _ _ _ _ (â„± i) _ _ _ (hf.adapted i) (hg.adapted i),
    Î» i j hij, _, Î» i, integrable.sup (hf.integrable _) (hg.integrable _)âŸ©,
  refine eventually_le.sup_le _ _,
  { exact eventually_le.trans (hf.2.1 i j hij)
      (condexp_mono (hf.integrable _) (integrable.sup (hf.integrable j) (hg.integrable j))
      (eventually_of_forall (Î» x, le_max_left _ _))) },
  { exact eventually_le.trans (hg.2.1 i j hij)
      (condexp_mono (hg.integrable _) (integrable.sup (hf.integrable j) (hg.integrable j))
      (eventually_of_forall (Î» x, le_max_right _ _))) }
end

protected lemma pos {f : Î¹ â†’ Î© â†’ â„} (hf : submartingale f â„± Î¼) :
  submartingale (fâº) â„± Î¼ :=
hf.sup (martingale_zero _ _ _).submartingale

end submartingale

section submartingale

lemma submartingale_of_set_integral_le [is_finite_measure Î¼]
  {f : Î¹ â†’ Î© â†’ â„} (hadp : adapted â„± f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i j : Î¹, i â‰¤ j â†’ âˆ€ s : set Î©, measurable_set[â„± i] s â†’
    âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼) :
  submartingale f â„± Î¼ :=
begin
  refine âŸ¨hadp, Î» i j hij, _, hintâŸ©,
  suffices : f i â‰¤áµ[Î¼.trim (â„±.le i)] Î¼[f j| â„± i],
  { exact ae_le_of_ae_le_trim this },
  suffices : 0 â‰¤áµ[Î¼.trim (â„±.le i)] Î¼[f j| â„± i] - f i,
  { filter_upwards [this] with x hx,
    rwa â† sub_nonneg },
  refine ae_nonneg_of_forall_set_integral_nonneg
    ((integrable_condexp.sub (hint i)).trim _ (strongly_measurable_condexp.sub $ hadp i))
    (Î» s hs h's, _),
  specialize hf i j hij s hs,
  rwa [â† set_integral_trim _ (strongly_measurable_condexp.sub $ hadp i) hs,
    integral_sub' integrable_condexp.integrable_on (hint i).integrable_on, sub_nonneg,
    set_integral_condexp (â„±.le i) (hint j) hs],
end

lemma submartingale_of_condexp_sub_nonneg [is_finite_measure Î¼]
  {f : Î¹ â†’ Î© â†’ â„} (hadp : adapted â„± f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i j, i â‰¤ j â†’ 0 â‰¤áµ[Î¼] Î¼[f j - f i | â„± i]) :
  submartingale f â„± Î¼ :=
begin
  refine âŸ¨hadp, Î» i j hij, _, hintâŸ©,
  rw [â† condexp_of_strongly_measurable (â„±.le _) (hadp _) (hint _), â† eventually_sub_nonneg],
  exact eventually_le.trans (hf i j hij) (condexp_sub (hint _) (hint _)).le,
  apply_instance
end

lemma submartingale.condexp_sub_nonneg
  {f : Î¹ â†’ Î© â†’ â„} (hf : submartingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) :
  0 â‰¤áµ[Î¼] Î¼[f j - f i | â„± i] :=
begin
  by_cases h : sigma_finite (Î¼.trim (â„±.le i)),
  swap, { rw condexp_of_not_sigma_finite (â„±.le i) h },
  refine eventually_le.trans _ (condexp_sub (hf.integrable _) (hf.integrable _)).symm.le,
  rw [eventually_sub_nonneg,
    condexp_of_strongly_measurable (â„±.le _) (hf.adapted _) (hf.integrable _)],
  { exact hf.2.1 i j hij },
  { exact h }
end

lemma submartingale_iff_condexp_sub_nonneg [is_finite_measure Î¼] {f : Î¹ â†’ Î© â†’ â„} :
  submartingale f â„± Î¼ â†” adapted â„± f âˆ§ (âˆ€ i, integrable (f i) Î¼) âˆ§ âˆ€ i j, i â‰¤ j â†’
  0 â‰¤áµ[Î¼] Î¼[f j - f i | â„± i] :=
âŸ¨Î» h, âŸ¨h.adapted, h.integrable, Î» i j, h.condexp_sub_nonnegâŸ©,
 Î» âŸ¨hadp, hint, hâŸ©, submartingale_of_condexp_sub_nonneg hadp hint hâŸ©

end submartingale

namespace supermartingale

lemma sub_submartingale [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : supermartingale f â„± Î¼) (hg : submartingale g â„± Î¼) : supermartingale (f - g) â„± Î¼ :=
by { rw sub_eq_add_neg, exact hf.add hg.neg }

lemma sub_martingale [preorder E] [covariant_class E E (+) (â‰¤)]
  (hf : supermartingale f â„± Î¼) (hg : martingale g â„± Î¼) : supermartingale (f - g) â„± Î¼ :=
hf.sub_submartingale hg.submartingale

section

variables {F : Type*} [normed_lattice_add_comm_group F]
  [normed_space â„ F] [complete_space F] [ordered_smul â„ F]

lemma smul_nonneg {f : Î¹ â†’ Î© â†’ F}
  {c : â„} (hc : 0 â‰¤ c) (hf : supermartingale f â„± Î¼) :
  supermartingale (c â€¢ f) â„± Î¼ :=
begin
  refine âŸ¨hf.1.smul c, Î» i j hij, _, Î» i, (hf.2.2 i).smul câŸ©,
  refine (condexp_smul c (f j)).le.trans _,
  filter_upwards [hf.2.1 i j hij] with _ hle,
  simp,
  exact smul_le_smul_of_nonneg hle hc,
end

lemma smul_nonpos {f : Î¹ â†’ Î© â†’ F}
  {c : â„} (hc : c â‰¤ 0) (hf : supermartingale f â„± Î¼) :
  submartingale (c â€¢ f) â„± Î¼ :=
begin
  rw [â† neg_neg c, (by { ext i x, simp } : - -c â€¢ f = -(-c â€¢ f))],
  exact (hf.smul_nonneg $ neg_nonneg.2 hc).neg,
end

end

end supermartingale

namespace submartingale

section

variables {F : Type*} [normed_lattice_add_comm_group F]
  [normed_space â„ F] [complete_space F] [ordered_smul â„ F]

lemma smul_nonneg {f : Î¹ â†’ Î© â†’ F}
  {c : â„} (hc : 0 â‰¤ c) (hf : submartingale f â„± Î¼) :
  submartingale (c â€¢ f) â„± Î¼ :=
begin
  rw [â† neg_neg c, (by { ext i x, simp } : - -c â€¢ f = -(c â€¢ -f))],
  exact supermartingale.neg (hf.neg.smul_nonneg hc),
end

lemma smul_nonpos {f : Î¹ â†’ Î© â†’ F}
  {c : â„} (hc : c â‰¤ 0) (hf : submartingale f â„± Î¼) :
  supermartingale (c â€¢ f) â„± Î¼ :=
begin
  rw [â† neg_neg c, (by { ext i x, simp } : - -c â€¢ f = -(-c â€¢ f))],
  exact (hf.smul_nonneg $ neg_nonneg.2 hc).neg,
end

end

end submartingale

section nat

variables {ğ’¢ : filtration â„• m0}

lemma submartingale_of_set_integral_le_succ [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, âˆ€ s : set Î©, measurable_set[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼) :
  submartingale f ğ’¢ Î¼ :=
begin
  refine submartingale_of_set_integral_le hadp hint (Î» i j hij s hs, _),
  induction hij with k hkâ‚ hkâ‚‚,
  { exact le_rfl },
  { exact le_trans hkâ‚‚ (hf k s (ğ’¢.mono hkâ‚ _ hs)) }
end

lemma supermartingale_of_set_integral_succ_le [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, âˆ€ s : set Î©, measurable_set[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼) :
  supermartingale f ğ’¢ Î¼ :=
begin
  rw â† neg_neg f,
  refine (submartingale_of_set_integral_le_succ hadp.neg (Î» i, (hint i).neg) _).neg,
  simpa only [integral_neg, pi.neg_apply, neg_le_neg_iff],
end

lemma martingale_of_set_integral_eq_succ [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, âˆ€ s : set Î©, measurable_set[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼) :
  martingale f ğ’¢ Î¼ :=
martingale_iff.2
  âŸ¨supermartingale_of_set_integral_succ_le hadp hint $ Î» i s hs, (hf i s hs).ge,
   submartingale_of_set_integral_le_succ hadp hint $ Î» i s hs, (hf i s hs).leâŸ©

lemma submartingale_nat [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, f i â‰¤áµ[Î¼] Î¼[f (i + 1) | ğ’¢ i]) :
  submartingale f ğ’¢ Î¼ :=
begin
  refine submartingale_of_set_integral_le_succ hadp hint (Î» i s hs, _),
  have : âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, Î¼[f (i + 1)|ğ’¢ i] Ï‰ âˆ‚Î¼ :=
    (set_integral_condexp (ğ’¢.le i) (hint _) hs).symm,
  rw this,
  exact set_integral_mono_ae (hint i).integrable_on integrable_condexp.integrable_on (hf i),
end

lemma supermartingale_nat [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, Î¼[f (i + 1) | ğ’¢ i] â‰¤áµ[Î¼] f i) :
  supermartingale f ğ’¢ Î¼ :=
begin
  rw â† neg_neg f,
  refine (submartingale_nat hadp.neg (Î» i, (hint i).neg) $ Î» i,
    eventually_le.trans _ (condexp_neg _).symm.le).neg,
  filter_upwards [hf i] with x hx using neg_le_neg hx,
end

lemma martingale_nat [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, f i =áµ[Î¼] Î¼[f (i + 1) | ğ’¢ i]) :
  martingale f ğ’¢ Î¼ :=
martingale_iff.2 âŸ¨supermartingale_nat hadp hint $ Î» i, (hf i).symm.le,
  submartingale_nat hadp hint $ Î» i, (hf i).leâŸ©

lemma submartingale_of_condexp_sub_nonneg_nat [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, 0 â‰¤áµ[Î¼] Î¼[f (i + 1) - f i | ğ’¢ i]) :
  submartingale f ğ’¢ Î¼ :=
begin
  refine submartingale_nat hadp hint (Î» i, _),
  rw [â† condexp_of_strongly_measurable (ğ’¢.le _) (hadp _) (hint _), â† eventually_sub_nonneg],
  exact eventually_le.trans (hf i) (condexp_sub (hint _) (hint _)).le,
  apply_instance
end

lemma supermartingale_of_condexp_sub_nonneg_nat [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, 0 â‰¤áµ[Î¼] Î¼[f i - f (i + 1) | ğ’¢ i]) :
  supermartingale f ğ’¢ Î¼ :=
begin
  rw â† neg_neg f,
  refine (submartingale_of_condexp_sub_nonneg_nat hadp.neg (Î» i, (hint i).neg) _).neg,
  simpa only [pi.zero_apply, pi.neg_apply, neg_sub_neg]
end

lemma martingale_of_condexp_sub_eq_zero_nat [is_finite_measure Î¼]
  {f : â„• â†’ Î© â†’ â„} (hadp : adapted ğ’¢ f) (hint : âˆ€ i, integrable (f i) Î¼)
  (hf : âˆ€ i, Î¼[f (i + 1) - f i | ğ’¢ i] =áµ[Î¼] 0) :
  martingale f ğ’¢ Î¼ :=
begin
  refine martingale_iff.2 âŸ¨supermartingale_of_condexp_sub_nonneg_nat hadp hint $ Î» i, _,
    submartingale_of_condexp_sub_nonneg_nat hadp hint $ Î» i, (hf i).symm.leâŸ©,
  rw â† neg_sub,
  refine (eventually_eq.trans _ (condexp_neg _).symm).le,
  filter_upwards [hf i] with x hx,
  simpa only [pi.zero_apply, pi.neg_apply, zero_eq_neg],
end

-- Note that one cannot use `submartingale.zero_le_of_predictable` to prove the other two
-- corresponding lemmas without imposing more restrictions to the ordering of `E`
/-- A predictable submartingale is a.e. greater equal than its initial state. -/
lemma submartingale.zero_le_of_predictable [preorder E] [sigma_finite_filtration Î¼ ğ’¢]
  {f : â„• â†’ Î© â†’ E} (hfmgle : submartingale f ğ’¢ Î¼) (hfadp : adapted ğ’¢ (Î» n, f (n + 1))) (n : â„•) :
  f 0 â‰¤áµ[Î¼] f n :=
begin
  induction n with k ih,
  { refl },
  { exact ih.trans ((hfmgle.2.1 k (k + 1) k.le_succ).trans_eq $ germ.coe_eq.mp $ congr_arg coe $
      condexp_of_strongly_measurable (ğ’¢.le _) (hfadp _) $ hfmgle.integrable _) }
end

/-- A predictable supermartingale is a.e. less equal than its initial state. -/
lemma supermartingale.le_zero_of_predictable [preorder E] [sigma_finite_filtration Î¼ ğ’¢]
  {f : â„• â†’ Î© â†’ E} (hfmgle : supermartingale f ğ’¢ Î¼) (hfadp : adapted ğ’¢ (Î» n, f (n + 1))) (n : â„•) :
  f n â‰¤áµ[Î¼] f 0 :=
begin
  induction n with k ih,
  { refl },
  { exact ((germ.coe_eq.mp $ congr_arg coe $ condexp_of_strongly_measurable (ğ’¢.le _) (hfadp _) $
      hfmgle.integrable _).symm.trans_le (hfmgle.2.1 k (k + 1) k.le_succ)).trans ih }
end

/-- A predictable martingale is a.e. equal to its initial state. -/
lemma martingale.eq_zero_of_predictable [sigma_finite_filtration Î¼ ğ’¢]
  {f : â„• â†’ Î© â†’ E} (hfmgle : martingale f ğ’¢ Î¼) (hfadp : adapted ğ’¢ (Î» n, f (n + 1))) (n : â„•) :
  f n =áµ[Î¼] f 0 :=
begin
  induction n with k ih,
  { refl },
  { exact ((germ.coe_eq.mp (congr_arg coe $ condexp_of_strongly_measurable (ğ’¢.le _) (hfadp _)
      (hfmgle.integrable _))).symm.trans (hfmgle.2 k (k + 1) k.le_succ)).trans ih }
end

namespace submartingale

@[protected]
lemma integrable_stopped_value [has_le E] {f : â„• â†’ Î© â†’ E} (hf : submartingale f ğ’¢ Î¼) {Ï„ : Î© â†’ â„•}
  (hÏ„ : is_stopping_time ğ’¢ Ï„) {N : â„•} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ N) :
  integrable (stopped_value f Ï„) Î¼ :=
integrable_stopped_value â„• hÏ„ hf.integrable hbdd

end submartingale

lemma submartingale.sum_mul_sub [is_finite_measure Î¼] {R : â„} {Î¾ f : â„• â†’ Î© â†’ â„}
  (hf : submartingale f ğ’¢ Î¼) (hÎ¾ : adapted ğ’¢ Î¾)
  (hbdd : âˆ€ n Ï‰, Î¾ n Ï‰ â‰¤ R) (hnonneg : âˆ€ n Ï‰, 0 â‰¤ Î¾ n Ï‰) :
  submartingale (Î» n, âˆ‘ k in finset.range n, Î¾ k * (f (k + 1) - f k)) ğ’¢ Î¼ :=
begin
  have hÎ¾bdd : âˆ€ i, âˆƒ C, âˆ€ Ï‰, |Î¾ i Ï‰| â‰¤ C :=
    Î» i, âŸ¨R, Î» Ï‰, (abs_of_nonneg (hnonneg i Ï‰)).trans_le (hbdd i Ï‰)âŸ©,
  have hint : âˆ€ m, integrable (âˆ‘ k in finset.range m, Î¾ k * (f (k + 1) - f k)) Î¼ :=
    Î» m, integrable_finset_sum' _
      (Î» i hi, integrable.bdd_mul ((hf.integrable _).sub (hf.integrable _))
      hÎ¾.strongly_measurable.ae_strongly_measurable (hÎ¾bdd _)),
  have hadp : adapted ğ’¢ (Î» n, âˆ‘ k in finset.range n, Î¾ k * (f (k + 1) - f k)),
  { intro m,
    refine finset.strongly_measurable_sum' _ (Î» i hi, _),
    rw finset.mem_range at hi,
    exact (hÎ¾.strongly_measurable_le hi.le).mul
      ((hf.adapted.strongly_measurable_le (nat.succ_le_of_lt hi)).sub
      (hf.adapted.strongly_measurable_le hi.le)) },
  refine submartingale_of_condexp_sub_nonneg_nat hadp hint (Î» i, _),
  simp only [â† finset.sum_Ico_eq_sub _ (nat.le_succ _), finset.sum_apply, pi.mul_apply,
    pi.sub_apply, nat.Ico_succ_singleton, finset.sum_singleton],
  exact eventually_le.trans (eventually_le.mul_nonneg (eventually_of_forall (hnonneg _))
    (hf.condexp_sub_nonneg (nat.le_succ _))) (condexp_strongly_measurable_mul (hÎ¾ _)
    (((hf.integrable _).sub (hf.integrable _)).bdd_mul
      hÎ¾.strongly_measurable.ae_strongly_measurable (hÎ¾bdd _))
    ((hf.integrable _).sub (hf.integrable _))).symm.le,
end

/-- Given a discrete submartingale `f` and a predictable process `Î¾` (i.e. `Î¾ (n + 1)` is adapted)
the process defined by `Î» n, âˆ‘ k in finset.range n, Î¾ (k + 1) * (f (k + 1) - f k)` is also a
submartingale. -/
lemma submartingale.sum_mul_sub' [is_finite_measure Î¼] {R : â„} {Î¾ f : â„• â†’ Î© â†’ â„}
  (hf : submartingale f ğ’¢ Î¼) (hÎ¾ : adapted ğ’¢ (Î» n, Î¾ (n + 1)))
  (hbdd : âˆ€ n Ï‰, Î¾ n Ï‰ â‰¤ R) (hnonneg : âˆ€ n Ï‰, 0 â‰¤ Î¾ n Ï‰) :
  submartingale (Î» n, âˆ‘ k in finset.range n, Î¾ (k + 1) * (f (k + 1) - f k)) ğ’¢ Î¼ :=
hf.sum_mul_sub hÎ¾ (Î» n, hbdd _) (Î» n, hnonneg _)

end nat

end measure_theory
