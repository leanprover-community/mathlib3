/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.martingale.convergence
import probability.conditional_expectation

/-!

# Generalized Borel-Cantelli lemma

This file proves L√©vy's generalized Borel-Cantelli lemma which is a generalization of the
Borel-Cantelli lemmas. With this generalization, one can easily deduce the Borel-Cantelli lemmas
by choosing appropriate filtrations. This file also contains the one sided martingale bound which
is required to prove the generalized Borel-Cantelli.

## Main results

- `measure_theory.submartingale.bdd_above_iff_exists_tendsto`: the one sided martingale bound: given
  a submartingale `f` with uniformly bounded differences, the set for which `f` converges is almost
  everywhere equal to the set for which it is bounded.
- `measure_theory.ae_mem_limsup_at_top_iff`: L√©vy's generalized Borel-Cantelli:
  given a filtration `‚Ñ±` and a sequence of sets `s` such that `s n ‚àà ‚Ñ± n` for all `n`,
  `limsup at_top s` is almost everywhere equal to the set for which `‚àë ‚Ñô[s (n + 1)‚à£‚Ñ± n] = ‚àû`.

## TODO

Prove the missing second Borel-Cantelli lemma using this generalized version.

-/

open filter
open_locale nnreal ennreal measure_theory probability_theory big_operators topological_space

namespace measure_theory

variables {Œ© : Type*} {m0 : measurable_space Œ©} {Œº : measure Œ©}
  {‚Ñ± : filtration ‚Ñï m0} {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} {œâ : Œ©}

/-!
### One sided martingale bound
-/

-- TODO: `least_ge` should be defined taking values in `with_top ‚Ñï` once the `stopped_process`
-- refactor is complete
/-- `least_ge f r n` is the stopping time corresponding to the first time `f ‚â• r`. -/
noncomputable
def least_ge (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (r : ‚Ñù) (n : ‚Ñï) := hitting f (set.Ici r) 0 n

lemma adapted.is_stopping_time_least_ge (r : ‚Ñù) (n : ‚Ñï) (hf : adapted ‚Ñ± f) :
  is_stopping_time ‚Ñ± (least_ge f r n) :=
hitting_is_stopping_time hf measurable_set_Ici

lemma least_ge_le {i : ‚Ñï} {r : ‚Ñù} (œâ : Œ©) : least_ge f r i œâ ‚â§ i :=
hitting_le œâ

lemma least_ge_eq_lt_iff {n : ‚Ñï} {r : ‚Ñù} {k : ‚Ñï} (hk : k < n) :
  least_ge f r n œâ = k ‚Üî least_ge f r (n + 1) œâ = k :=
begin
  split; intro h,
  { rw [‚Üê h, eq_comm],
    rw [‚Üê h, least_ge, hitting_lt_iff n le_rfl] at hk,
    obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := hk,
    exact hitting_eq_hitting_of_exists n.le_succ ‚ü®j, ‚ü®zero_le _, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü© },
  { rw ‚Üê h,
    rw [‚Üê h, least_ge, hitting_lt_iff _ n.le_succ] at hk,
    obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := hk,
    exact hitting_eq_hitting_of_exists n.le_succ ‚ü®j, ‚ü®zero_le _, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü© }
end

lemma least_ge_succ_eq_iff (n : ‚Ñï) {r : ‚Ñù} :
  least_ge f r (n + 1) œâ = n ‚Üî least_ge f r n œâ = n ‚àß r ‚â§ f n œâ :=
begin
  split,
  { intro h,
    have : least_ge f r (n + 1) œâ < n + 1 := h.symm ‚ñ∏ n.lt_succ_self,
    refine ‚ü®_, (h ‚ñ∏ hitting_mem_set _ : f n œâ ‚àà set.Ici r)‚ü©,
    { rw ‚Üê h,
      refine hitting_eq_hitting_of_exists (hitting_le _) _,
      rw [least_ge, hitting_lt_iff (n + 1) le_rfl] at this,
      obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := this,
      exact ‚ü®j, ‚ü®zero_le _, h.symm ‚ñ∏ nat.le_of_lt_succ hj‚ÇÅ.2‚ü©, hj‚ÇÇ‚ü© },
    { rw [least_ge, hitting_lt_iff (n + 1) le_rfl] at this,
      obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := this,
      exact ‚ü®j, ‚ü®zero_le _, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü© } },
  { rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
    rw [‚Üê h‚ÇÅ, eq_comm],
    exact hitting_eq_hitting_of_exists (h‚ÇÅ.symm ‚ñ∏ n.le_succ)
      ‚ü®n, ‚ü®zero_le _, le_rfl‚ü©, h‚ÇÇ‚ü© }
end

lemma least_ge_succ_eq_iff' (n : ‚Ñï) {r : ‚Ñù} :
  least_ge f r (n + 1) œâ = n + 1 ‚Üî least_ge f r n œâ = n ‚àß f n œâ < r :=
begin
  split,
  { intro h,
    have : least_ge f r n œâ = n,
    { refine le_antisymm (hitting_le _) _,
      by_contra hlt,
      rw [not_le, least_ge] at hlt,
      refine ne_of_lt _ h,
      rw [least_ge, hitting_lt_iff (n + 1) le_rfl],
      exact ‚ü®least_ge f r n œâ, ‚ü®zero_le _, nat.lt_succ_of_le (hitting_le _)‚ü©,
        hitting_mem_set_of_hitting_lt hlt‚ü© },
    refine ‚ü®this, _‚ü©,
    by_contra h',
    rw not_lt at h',
    rw ((least_ge_succ_eq_iff n).2 ‚ü®this, h'‚ü©) at h,
    norm_num at h },
  { rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
    refine le_antisymm (hitting_le _) (nat.succ_le_of_lt _),
    by_contra h,
    have : least_ge f r (n + 1) œâ = least_ge f r n œâ :=
      le_antisymm (h‚ÇÅ.symm ‚ñ∏ not_lt.1 h) (hitting_mono n.le_succ),
    rw h‚ÇÅ at this,
    refine not_lt.2 _ h‚ÇÇ,
    refine this ‚ñ∏ hitting_mem_set_of_hitting_lt _,
    rw [‚Üê least_ge, this],
    exact n.lt_succ_self },
end

-- The following four lemmas shows `least_ge` behaves like a stopped process. Ideally we should
-- define `least_ge` as a stopping time and take its stopped process. However, we can't do that
-- with our current definition since a stopping time takes only finite indicies. An upcomming
-- refactor should hopefully make it possible to have stopping times taking infinity as a value
lemma least_ge_mono {n m : ‚Ñï} (hnm : n ‚â§ m) (r : ‚Ñù) (œâ : Œ©) :
  least_ge f r n œâ ‚â§ least_ge f r m œâ :=
hitting_mono hnm

lemma least_ge_eq_min (œÄ : Œ© ‚Üí ‚Ñï) (r : ‚Ñù) (œâ : Œ©)
  {n : ‚Ñï} (hœÄn : ‚àÄ œâ, œÄ œâ ‚â§ n) :
  least_ge f r (œÄ œâ) œâ = min (œÄ œâ) (least_ge f r n œâ) :=
begin
  classical,
  refine le_antisymm (le_min (least_ge_le _) (least_ge_mono (hœÄn œâ) r œâ)) _,
  by_cases hle : œÄ œâ ‚â§ least_ge f r n œâ,
  { rw [min_eq_left hle, least_ge],
    by_cases h : ‚àÉ j ‚àà set.Icc 0 (œÄ œâ), f j œâ ‚àà set.Ici r,
    { refine hle.trans (eq.le _),
      rw [least_ge, ‚Üê hitting_eq_hitting_of_exists (hœÄn œâ) h] },
    { simp only [hitting, if_neg h] } },
  { rw [min_eq_right (not_le.1 hle).le, least_ge, least_ge,
      ‚Üê hitting_eq_hitting_of_exists (hœÄn œâ) _],
    rw [not_le, least_ge, hitting_lt_iff _ (hœÄn œâ)] at hle,
    exact let ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := hle in ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü© }
end

lemma least_ge_apply_eq_stopped_value (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (œÄ : Œ© ‚Üí ‚Ñï) (r : ‚Ñù) (œâ : Œ©)
  {n : ‚Ñï} (hœÄn : ‚àÄ œâ, œÄ œâ ‚â§ n) :
  f (least_ge f r (œÄ œâ) œâ) œâ = stopped_value f (Œª œâ, min (œÄ œâ) (least_ge f r n œâ)) œâ :=
by { rw least_ge_eq_min _ _ _ hœÄn, simp_rw [stopped_value], }

lemma stopped_value_stopped_value_least_ge (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (œÄ : Œ© ‚Üí ‚Ñï) (r : ‚Ñù)
  {n : ‚Ñï} (hœÄn : ‚àÄ œâ, œÄ œâ ‚â§ n) :
  stopped_value (Œª i, stopped_value f (least_ge f r i)) œÄ
    = (Œª œâ, stopped_value f (Œª œâ, min (œÄ œâ) (least_ge f r n œâ)) œâ) :=
begin
  ext1 œâ,
  exact least_ge_apply_eq_stopped_value f œÄ r œâ hœÄn,
end

lemma submartingale.stopped_value_least_ge [is_finite_measure Œº]
  (hf : submartingale f ‚Ñ± Œº) (r : ‚Ñù) :
  submartingale (Œª i, stopped_value f (least_ge f r i)) ‚Ñ± Œº :=
begin
  rw submartingale_iff_expected_stopped_value_mono,
  { intros œÉ œÄ hœÉ hœÄ hœÉ_le_œÄ hœÄ_bdd,
    obtain ‚ü®n, hœÄ_le_n‚ü© := hœÄ_bdd,
    simp_rw stopped_value_stopped_value_least_ge f œÉ r (Œª i, (hœÉ_le_œÄ i).trans (hœÄ_le_n i)),
    simp_rw stopped_value_stopped_value_least_ge f œÄ r hœÄ_le_n,
    refine hf.expected_stopped_value_mono _ _ _ (Œª œâ, (min_le_left _ _).trans (hœÄ_le_n œâ)),
    { exact hœÉ.min (hf.adapted.is_stopping_time_least_ge _ _), },
    { exact hœÄ.min (hf.adapted.is_stopping_time_least_ge _ _), },
    { exact Œª œâ, min_le_min (hœÉ_le_œÄ œâ) le_rfl, }, },
  { exact Œª i, strongly_measurable_stopped_value_of_le hf.adapted.prog_measurable_of_nat
      (hf.adapted.is_stopping_time_least_ge _ _) least_ge_le, },
  { exact Œª i, integrable_stopped_value ((hf.adapted.is_stopping_time_least_ge _ _))
      (hf.integrable) least_ge_le, },
end

variables {r : ‚Ñù} {R : ‚Ñù‚â•0}

lemma norm_stopped_value_least_ge_le (hr : 0 ‚â§ r) (hf0 : f 0 = 0)
  (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) (i : ‚Ñï) :
  ‚àÄ·µê œâ ‚àÇŒº, stopped_value f (least_ge f r i) œâ ‚â§ r + R :=
begin
  filter_upwards [hbdd] with œâ hbddœâ,
  change f (least_ge f r i œâ) œâ ‚â§ r + R,
  by_cases heq : least_ge f r i œâ = 0,
  { rw [heq, hf0, pi.zero_apply],
    exact add_nonneg hr R.coe_nonneg },
  { obtain ‚ü®k, hk‚ü© := nat.exists_eq_succ_of_ne_zero heq,
    rw [hk, add_comm, ‚Üê sub_le_iff_le_add],
    have := not_mem_of_lt_hitting (hk.symm ‚ñ∏ k.lt_succ_self : k < least_ge f r i œâ) (zero_le _),
    simp only [set.mem_union_eq, set.mem_Iic, set.mem_Ici, not_or_distrib, not_le] at this,
    exact (sub_lt_sub_left this _).le.trans ((le_abs_self _).trans (hbddœâ _)) }
end

lemma snorm_one_le_of_le {r : ‚Ñù‚â•0} {f : Œ© ‚Üí ‚Ñù}
  (hfint : integrable f Œº) (hfint' : 0 ‚â§ Œº[f]) (hf : ‚àÄ·µê œâ ‚àÇŒº, f œâ ‚â§ r) :
  snorm f 1 Œº ‚â§ 2 * Œº set.univ * r :=
begin
  by_cases hr : r = 0,
  { suffices : f =·µê[Œº] 0,
    { rw [snorm_congr_ae this, snorm_zero, hr, ennreal.coe_zero, mul_zero],
      exact le_rfl },
    rw [hr, nonneg.coe_zero] at hf,
    have hnegf : Œº[-f] = 0,
    { rw [integral_neg', neg_eq_zero],
      exact le_antisymm (integral_nonpos_of_ae hf) hfint' },
    have := (integral_eq_zero_iff_of_nonneg_ae _ hfint.neg).1 hnegf,
    { filter_upwards [this] with œâ hœâ,
      rwa [pi.neg_apply, pi.zero_apply, neg_eq_zero] at hœâ },
    { filter_upwards [hf] with œâ hœâ,
      rwa [pi.zero_apply, pi.neg_apply, right.nonneg_neg_iff] } },
  by_cases hŒº : is_finite_measure Œº,
  swap,
  { have : Œº set.univ = ‚àû,
    { by_contra hŒº',
      exact hŒº (is_finite_measure.mk $ lt_top_iff_ne_top.2 hŒº') },
    rw [this, ennreal.mul_top, if_neg, ennreal.top_mul, if_neg],
    { exact le_top },
    { simp [hr] },
    { norm_num } },
  haveI := hŒº,
  rw [integral_eq_integral_pos_part_sub_integral_neg_part hfint, sub_nonneg] at hfint',
  have hposbdd : ‚à´ œâ, max (f œâ) 0 ‚àÇŒº ‚â§ (Œº set.univ).to_real ‚Ä¢ r,
  { rw ‚Üê integral_const,
    refine integral_mono_ae hfint.real_to_nnreal (integrable_const r) _,
    filter_upwards [hf] with œâ hœâ using real.to_nnreal_le_iff_le_coe.2 hœâ },
  rw [mem_‚Ñíp.snorm_eq_integral_rpow_norm one_ne_zero ennreal.one_ne_top
      (mem_‚Ñíp_one_iff_integrable.2 hfint),
    ennreal.of_real_le_iff_le_to_real (ennreal.mul_ne_top
      (ennreal.mul_ne_top ennreal.two_ne_top $ @measure_ne_top _ _ _ hŒº _) ennreal.coe_ne_top)],
  simp_rw [ennreal.one_to_real, inv_one, real.rpow_one, real.norm_eq_abs,
    ‚Üê max_zero_add_max_neg_zero_eq_abs_self, ‚Üê real.coe_to_nnreal'],
  rw integral_add hfint.real_to_nnreal,
  { simp only [real.coe_to_nnreal', ennreal.to_real_mul, ennreal.to_real_bit0,
    ennreal.one_to_real, ennreal.coe_to_real] at hfint' ‚ä¢,
    refine (add_le_add_left hfint' _).trans _,
    rwa [‚Üê two_mul, mul_assoc, mul_le_mul_left (two_pos : (0 : ‚Ñù) < 2)] },
  { exact hfint.neg.sup (integrable_zero _ _ Œº) }
end

lemma snorm_one_le_of_le' {r : ‚Ñù} {f : Œ© ‚Üí ‚Ñù}
  (hfint : integrable f Œº) (hfint' : 0 ‚â§ Œº[f]) (hf : ‚àÄ·µê œâ ‚àÇŒº, f œâ ‚â§ r) :
  snorm f 1 Œº ‚â§ 2 * Œº set.univ * ennreal.of_real r :=
begin
  refine snorm_one_le_of_le hfint hfint' _,
  simp only [real.coe_to_nnreal', le_max_iff],
  filter_upwards [hf] with œâ hœâ using or.inl hœâ,
end

lemma submartingale.stopped_value_least_ge_snorm_le [is_finite_measure Œº]
  (hf : submartingale f ‚Ñ± Œº) (hr : 0 ‚â§ r) (hf0 : f 0 = 0)
  (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) (i : ‚Ñï) :
  snorm (stopped_value f (least_ge f r i)) 1 Œº ‚â§ 2 * Œº set.univ * ennreal.of_real (r + R) :=
begin
  refine snorm_one_le_of_le' ((hf.stopped_value_least_ge r).integrable _) _
    (norm_stopped_value_least_ge_le hr hf0 hbdd i),
  rw ‚Üê integral_univ,
  refine le_trans _ ((hf.stopped_value_least_ge r).set_integral_le (zero_le _)
    measurable_set.univ),
  simp_rw [stopped_value, least_ge, hitting_of_le le_rfl, hf0, integral_zero']
end

lemma submartingale.stopped_value_least_ge_snorm_le' [is_finite_measure Œº]
  (hf : submartingale f ‚Ñ± Œº) (hr : 0 ‚â§ r) (hf0 : f 0 = 0)
  (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) (i : ‚Ñï) :
  snorm (stopped_value f (least_ge f r i)) 1 Œº ‚â§
    ennreal.to_nnreal (2 * Œº set.univ * ennreal.of_real (r + R)) :=
begin
  refine (hf.stopped_value_least_ge_snorm_le hr hf0 hbdd i).trans _,
  simp [ennreal.coe_to_nnreal (measure_ne_top Œº _), ennreal.coe_to_nnreal],
end

/-- This lemma is superceded by `submartingale.bdd_above_iff_exists_tendsto`. -/
lemma submartingale.exists_tendsto_of_abs_bdd_above_aux [is_finite_measure Œº]
  (hf : submartingale f ‚Ñ± Œº) (hf0 : f 0 = 0)
  (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
  ‚àÄ·µê œâ ‚àÇŒº, bdd_above (set.range $ Œª n, f n œâ) ‚Üí ‚àÉ c, tendsto (Œª n, f n œâ) at_top (ùìù c) :=
begin
  have ht : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i : ‚Ñï, ‚àÉ c, tendsto (Œª n, stopped_value f (least_ge f i n) œâ) at_top (ùìù c),
  { rw ae_all_iff,
    exact Œª i, submartingale.exists_ae_tendsto_of_bdd (hf.stopped_value_least_ge i)
      (hf.stopped_value_least_ge_snorm_le' i.cast_nonneg hf0 hbdd) },
  filter_upwards [ht] with œâ hœâ hœâb,
  rw bdd_above at hœâb,
  obtain ‚ü®i, hi‚ü© := exists_nat_gt hœâb.some,
  have hib : ‚àÄ n, f n œâ < i,
  { intro n,
    exact lt_of_le_of_lt ((mem_upper_bounds.1 hœâb.some_mem) _ ‚ü®n, rfl‚ü©) hi },
  have heq : ‚àÄ n, stopped_value f (least_ge f i n) œâ = f n œâ,
  { intro n,
    rw [least_ge, hitting, stopped_value],
    simp only,
    rw if_neg,
    simp only [set.mem_Icc, set.mem_union, set.mem_Ici],
    push_neg,
    exact Œª j _, hib j },
  simp only [‚Üê heq, hœâ i],
end

lemma submartingale.bdd_above_iff_exists_tendsto_aux [is_finite_measure Œº]
  (hf : submartingale f ‚Ñ± Œº) (hf0 : f 0 = 0)
  (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
  ‚àÄ·µê œâ ‚àÇŒº, bdd_above (set.range $ Œª n, f n œâ) ‚Üî ‚àÉ c, tendsto (Œª n, f n œâ) at_top (ùìù c) :=
by filter_upwards [hf.exists_tendsto_of_abs_bdd_above_aux hf0 hbdd] with œâ hœâ using
  ‚ü®hœâ, Œª ‚ü®c, hc‚ü©, hc.bdd_above_range‚ü©

/-- One sided martingale bound: If `f` is a submartingale which has uniformly bounded differences,
then for almost every `œâ`, `f n œâ` is bounded above (in `n`) if and only if it converges. -/
lemma submartingale.bdd_above_iff_exists_tendsto [is_finite_measure Œº]
  (hf : submartingale f ‚Ñ± Œº) (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
  ‚àÄ·µê œâ ‚àÇŒº, bdd_above (set.range $ Œª n, f n œâ) ‚Üî ‚àÉ c, tendsto (Œª n, f n œâ) at_top (ùìù c) :=
begin
  set g : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù := Œª n œâ, f n œâ - f 0 œâ with hgdef,
  have hg : submartingale g ‚Ñ± Œº :=
    hf.sub_martingale (martingale_const_fun _ _ (hf.adapted 0) (hf.integrable 0)),
  have hg0 : g 0 = 0,
  { ext œâ,
    simp only [hgdef, sub_self, pi.zero_apply] },
  have hgbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ (i : ‚Ñï), |g (i + 1) œâ - g i œâ| ‚â§ ‚ÜëR,
  { simpa only [sub_sub_sub_cancel_right] },
  filter_upwards [hg.bdd_above_iff_exists_tendsto_aux hg0 hgbdd] with œâ hœâ,
  convert hœâ using 1; rw eq_iff_iff,
  { simp only [hgdef],
    refine ‚ü®Œª h, _, Œª h, _‚ü©;
    obtain ‚ü®b, hb‚ü© := h;
    refine ‚ü®b + |f 0 œâ|, Œª y hy, _‚ü©;
    obtain ‚ü®n, rfl‚ü© := hy,
    { simp_rw [sub_eq_add_neg],
      exact add_le_add (hb ‚ü®n, rfl‚ü©) (neg_le_abs_self _) },
    { exact sub_le_iff_le_add.1 (le_trans (sub_le_sub_left (le_abs_self _) _) (hb ‚ü®n, rfl‚ü©)) } },
  { simp only [hgdef],
    refine ‚ü®Œª h, _, Œª h, _‚ü©;
    obtain ‚ü®c, hc‚ü© := h,
    { exact ‚ü®c - f 0 œâ, hc.sub_const _‚ü© },
    { refine ‚ü®c + f 0 œâ, _‚ü©,
      have := hc.add_const (f 0 œâ),
      simpa only [sub_add_cancel] } }
end

/-!
### L√©vy's generalization of the Borel-Cantelli lemma

L√©vy's generalization of the Borel-Cantelli lemma states that: given a natural number indexed
filtration $(\mathcal{F}_n)$, and a sequence of sets $(s_n)$ such that for all
$n$, $s_n \in \mathcal{F}_n$, $limsup_n s_n$ is almost everywhere equal to the set for which
$\sum_n \mathbb{P}[s_n \mid \mathcal{F}_n] = \infty$.

The proof strategy follows by constructing a martingale satisfying the one sided martingale bound.
In particular, we define
$$
  f_n := \sum_{k < n} \mathbf{1}_{s_{n + 1}} - \mathbb{P}[s_{n + 1} \mid \mathcal{F}_n].
$$
Then, as a martingale is both a sub and a super-martingale, the set for which it is unbounded from
above must agree with the set for which it is unbounded from below almost everywhere. Thus, it
can only converge to $\pm \infty$ with probability 0. Thus, by considering
$$
  \limsup_n s_n = \{\sum_n \mathbf{1}_{s_n} = \infty\}
$$
almost everywhere, the result follows.
-/

lemma martingale.bdd_above_range_iff_bdd_below_range [is_finite_measure Œº]
  (hf : martingale f ‚Ñ± Œº) (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
  ‚àÄ·µê œâ ‚àÇŒº, bdd_above (set.range (Œª n, f n œâ)) ‚Üî bdd_below (set.range (Œª n, f n œâ)) :=
begin
  have hbdd' : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |(-f) (i + 1) œâ - (-f) i œâ| ‚â§ R,
  { filter_upwards [hbdd] with œâ hœâ i,
    erw [‚Üê abs_neg, neg_sub, sub_neg_eq_add, neg_add_eq_sub],
    exact hœâ i },
  have hup := hf.submartingale.bdd_above_iff_exists_tendsto hbdd,
  have hdown := hf.neg.submartingale.bdd_above_iff_exists_tendsto hbdd',
  filter_upwards [hup, hdown] with œâ hœâ‚ÇÅ hœâ‚ÇÇ,
  have : (‚àÉ c, tendsto (Œª n, f n œâ) at_top (ùìù c)) ‚Üî ‚àÉ c, tendsto (Œª n, (-f) n œâ) at_top (ùìù c),
  { split; rintro ‚ü®c, hc‚ü©,
    { exact ‚ü®-c, hc.neg‚ü© },
    { refine ‚ü®-c, _‚ü©,
      convert hc.neg,
      simp only [neg_neg, pi.neg_apply] } },
  rw [hœâ‚ÇÅ, this, ‚Üê hœâ‚ÇÇ],
  split; rintro ‚ü®c, hc‚ü©; refine ‚ü®-c, Œª œâ hœâ, _‚ü©,
  { rw mem_upper_bounds at hc,
    rw set.mem_range at hœâ,
    refine neg_le.2 (hc _ _),
    simpa only [pi.neg_apply, set.mem_range, neg_inj] },
  { rw mem_lower_bounds at hc,
    simp_rw [set.mem_range, pi.neg_apply, neg_eq_iff_neg_eq, eq_comm] at hœâ,
    refine le_neg.1 (hc _ _),
    simpa only [set.mem_range] }
end

lemma martingale.ae_not_tendsto_at_top_at_top [is_finite_measure Œº]
  (hf : martingale f ‚Ñ± Œº) (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
  ‚àÄ·µê œâ ‚àÇŒº, ¬¨ tendsto (Œª n, f n œâ) at_top at_top :=
begin
  filter_upwards [hf.bdd_above_range_iff_bdd_below_range hbdd] with œâ hœâ htop using
    unbounded_of_tendsto_at_top htop (hœâ.2 $ bdd_below_range_of_tendsto_at_top_at_top htop),
end

lemma martingale.ae_not_tendsto_at_top_at_bot [is_finite_measure Œº]
  (hf : martingale f ‚Ñ± Œº) (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
  ‚àÄ·µê œâ ‚àÇŒº, ¬¨ tendsto (Œª n, f n œâ) at_top at_bot :=
begin
  filter_upwards [hf.bdd_above_range_iff_bdd_below_range hbdd] with œâ hœâ htop using
    unbounded_of_tendsto_at_bot htop (hœâ.1 $ bdd_above_range_of_tendsto_at_top_at_bot htop),
end

namespace borel_cantelli

/-- Auxiliary definition required to prove L√©vy's generalization of the Borel-Cantelli lemmas.
The sum of the differences of the indicator functions with their conditional expectation forms a
martingale satisfying the conditions of the one sided martingale bound. -/
noncomputable
def mgale (‚Ñ± : filtration ‚Ñï m0) (Œº : measure Œ©) (s : ‚Ñï ‚Üí set Œ©) (n : ‚Ñï) : Œ© ‚Üí ‚Ñù :=
‚àë k in finset.range n, ((s (k + 1)).indicator 1 - Œº[(s (k + 1)).indicator 1 | ‚Ñ± k])

variables {s : ‚Ñï ‚Üí set Œ©}

lemma mgale_succ (n : ‚Ñï) :
  mgale ‚Ñ± Œº s (n + 1) =
    mgale ‚Ñ± Œº s n + ((s (n + 1)).indicator 1 - Œº[(s (n + 1)).indicator 1 | ‚Ñ± n]) :=
begin
  rw [mgale, finset.sum_range_succ],
  refl,
end

lemma adapted_mgale (hs : ‚àÄ n, measurable_set[‚Ñ± n] (s n)) :
  adapted ‚Ñ± (mgale ‚Ñ± Œº s) :=
Œª n, finset.strongly_measurable_sum' _ (Œª k hk, (strongly_measurable_one.indicator
  (‚Ñ±.mono (nat.succ_le_of_lt (finset.mem_range.1 hk)) _ (hs _))).sub
  (strongly_measurable_condexp.mono (‚Ñ±.mono (finset.mem_range.1 hk).le)))

variables [is_finite_measure Œº]

lemma integrable_mgale (hs : ‚àÄ n, measurable_set[‚Ñ± n] (s n)) (n : ‚Ñï) :
  integrable (mgale ‚Ñ± Œº s n) Œº :=
integrable_finset_sum' _ (Œª k hk,
  ((integrable_indicator_iff (‚Ñ±.le (k + 1) _ (hs $ k + 1))).2
  (integrable_const 1).integrable_on).sub integrable_condexp)

lemma martingale_mgale
  (Œº : measure Œ©) [is_finite_measure Œº] (hs : ‚àÄ n, measurable_set[‚Ñ± n] (s n)) :
  martingale (mgale ‚Ñ± Œº s) ‚Ñ± Œº :=
begin
  refine martingale_nat (adapted_mgale hs) (integrable_mgale hs)
    (Œª n, eventually_eq.symm $ (condexp_finset_sum _).trans $
    (@eventually_eq_sum _ _ _ _ _ _ _
    (Œª k, (Œº[(s (k + 1)).indicator 1|‚Ñ± n] - Œº[(s (k + 1)).indicator 1|‚Ñ± k])) _).trans _),
  { intros k hk,
    exact ((integrable_indicator_iff (‚Ñ±.le (k + 1) _ (hs $ k + 1))).2
      (integrable_const 1).integrable_on).sub integrable_condexp },
  { intros k hk,
    rw finset.mem_range_succ_iff at hk,
    refine (condexp_sub ((integrable_indicator_iff (‚Ñ±.le (k + 1) _ (hs $ k + 1))).2
      (integrable_const 1).integrable_on) integrable_condexp).trans
      ((ae_eq_refl _).sub _),
    rw (condexp_of_strongly_measurable (‚Ñ±.le _)
      (strongly_measurable.mono strongly_measurable_condexp (‚Ñ±.mono hk)) integrable_condexp),
    apply_instance },
  simp_rw [finset.sum_range_succ, sub_self, add_zero, mgale],
  refine eventually_eq_sum (Œª i hi, eventually_eq.sub _ $ ae_eq_refl _),
  rw [finset.mem_range, ‚Üê nat.succ_le_iff] at hi,
  rw condexp_of_strongly_measurable (‚Ñ±.le _)
    (strongly_measurable_one.indicator (‚Ñ±.mono hi _ $ hs _)),
  { exact (integrable_indicator_iff (‚Ñ±.le _ _ (hs $ _))).2 (integrable_const 1).integrable_on },
  { apply_instance },
end

lemma mgale_diff_le (hs : ‚àÄ n, measurable_set[‚Ñ± n] (s n)) (n : ‚Ñï) :
  ‚àÄ·µê œâ ‚àÇŒº, |mgale ‚Ñ± Œº s (n + 1) œâ - mgale ‚Ñ± Œº s n œâ| ‚â§ 1 :=
begin
  simp_rw [mgale, finset.sum_apply, finset.sum_range_succ_sub_sum],
  have h‚ÇÅ : Œº[(s (n + 1)).indicator 1|‚Ñ± n] ‚â§·µê[Œº] 1,
  { change _ ‚â§·µê[Œº] (Œª œâ, 1 : Œ© ‚Üí ‚Ñù),
    rw ‚Üê @condexp_const _ _ _ _ _ _ _ Œº (‚Ñ±.le n) (1 : ‚Ñù),
    refine condexp_mono ((integrable_indicator_iff (‚Ñ±.le _ _ (hs $ _))).2
      (integrable_const 1).integrable_on) (integrable_const 1)
      (eventually_of_forall $ Œª œâ, set.indicator_le_self' (Œª _ _, zero_le_one) œâ) },
  have h‚ÇÇ : (0 : Œ© ‚Üí ‚Ñù) ‚â§·µê[Œº] Œº[(s (n + 1)).indicator 1|‚Ñ± n],
  { rw ‚Üê @condexp_zero Œ© ‚Ñù _ _ _ (‚Ñ± n) _ Œº,
    exact condexp_mono (integrable_zero _ _ _)
      ((integrable_indicator_iff (‚Ñ±.le _ _ (hs $ _))).2 (integrable_const 1).integrable_on)
      (eventually_of_forall $ Œª œâ, set.indicator_nonneg (Œª _ _, zero_le_one) _) },
  filter_upwards [h‚ÇÅ, h‚ÇÇ] with œâ hœâ‚ÇÅ hœâ‚ÇÇ,
  rw [abs_le, neg_le, pi.sub_apply, neg_sub, tsub_le_iff_right, tsub_le_iff_right,
    add_comm (1 : ‚Ñù), add_comm (1 : ‚Ñù)],
  exact ‚ü®le_add_of_nonneg_of_le (set.indicator_nonneg (Œª _ _, zero_le_one) _) hœâ‚ÇÅ,
    le_add_of_nonneg_of_le hœâ‚ÇÇ (set.indicator_le' (Œª _ _, le_rfl) (Œª _ _, zero_le_one) œâ)‚ü©,
end

lemma mgale_diff_le' (hs : ‚àÄ n, measurable_set[‚Ñ± n] (s n)) :
  ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ n, |mgale ‚Ñ± Œº s (n + 1) œâ - mgale ‚Ñ± Œº s n œâ| ‚â§ (1 : ‚Ñù‚â•0) :=
begin
  rw [ae_all_iff, nonneg.coe_one],
  exact mgale_diff_le hs ,
end

lemma limsup_eq_tendsto_sum_indicator_at_top (s : ‚Ñï ‚Üí set Œ©) :
  limsup at_top s =
    {œâ | tendsto (Œª n, ‚àë k in finset.range n, (s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñï) œâ) at_top at_top} :=
begin
  ext œâ,
  simp only [limsup_eq_infi_supr_of_nat, ge_iff_le, set.supr_eq_Union,
      set.infi_eq_Inter, set.mem_Inter, set.mem_Union, exists_prop],
  split,
  { intro hœâ,
    refine tendsto_at_top_at_top_of_monotone' (Œª n m hnm, finset.sum_mono_set_of_nonneg
      (Œª i, set.indicator_nonneg (Œª _ _, zero_le_one) _) (finset.range_mono hnm)) _,
    rintro ‚ü®i, h‚ü©,
    simp only [mem_upper_bounds, set.mem_range, forall_exists_index, forall_apply_eq_imp_iff'] at h,
    induction i with k hk,
    { obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := hœâ 1,
      refine not_lt.2 (h $ j + 1) (lt_of_le_of_lt
        (finset.sum_const_zero.symm : 0 = ‚àë k in finset.range (j + 1), 0).le _),
      refine finset.sum_lt_sum (Œª m _, set.indicator_nonneg (Œª _ _, zero_le_one) _)
        ‚ü®j - 1, finset.mem_range.2 (lt_of_le_of_lt (nat.sub_le _ _) j.lt_succ_self), _‚ü©,
      rw [nat.sub_add_cancel hj‚ÇÅ, set.indicator_of_mem hj‚ÇÇ],
      exact zero_lt_one },
    { rw imp_false at hk,
      push_neg at hk,
      obtain ‚ü®i, hi‚ü© := hk,
      obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := hœâ (i + 1),
      replace hi : ‚àë k in finset.range i, (s (k + 1)).indicator 1 œâ = k + 1 := le_antisymm (h i) hi,
      refine not_lt.2 (h $ j + 1) _,
      rw [‚Üê finset.sum_range_add_sum_Ico _ (i.le_succ.trans (hj‚ÇÅ.trans j.le_succ)), hi],
      refine lt_add_of_pos_right _ _,
      rw (finset.sum_const_zero.symm : 0 = ‚àë k in finset.Ico i (j + 1), 0),
      refine finset.sum_lt_sum (Œª m _, set.indicator_nonneg (Œª _ _, zero_le_one) _)
        ‚ü®j - 1, finset.mem_Ico.2
        ‚ü®(nat.le_sub_iff_right (le_trans ((le_add_iff_nonneg_left _).2 zero_le') hj‚ÇÅ)).2 hj‚ÇÅ,
          lt_of_le_of_lt (nat.sub_le _ _) j.lt_succ_self‚ü©, _‚ü©,
      rw [nat.sub_add_cancel (le_trans ((le_add_iff_nonneg_left _).2 zero_le') hj‚ÇÅ),
        set.indicator_of_mem hj‚ÇÇ],
      exact zero_lt_one } },
  { rintro hœâ i,
    rw [set.mem_set_of_eq, tendsto_at_top_at_top] at hœâ,
    by_contra hcon,
    push_neg at hcon,
    obtain ‚ü®j, h‚ü© := hœâ (i + 1),
    have : ‚àë k in finset.range j, (s (k + 1)).indicator 1 œâ ‚â§ i,
    { have hle : ‚àÄ j ‚â§ i, ‚àë k in finset.range j, (s (k + 1)).indicator 1 œâ ‚â§ i,
      { refine Œª j hij, (finset.sum_le_card_nsmul _ _ _ _ : _ ‚â§ (finset.range j).card ‚Ä¢ 1).trans _,
        { exact Œª m hm, set.indicator_apply_le' (Œª _, le_rfl) (Œª _, zero_le_one) },
        { simpa only [finset.card_range, algebra.id.smul_eq_mul, mul_one] } },
      by_cases hij : j < i,
      { exact hle _ hij.le },
      { rw ‚Üê finset.sum_range_add_sum_Ico _ (not_lt.1 hij),
        suffices : ‚àë k in finset.Ico i j, (s (k + 1)).indicator 1 œâ = 0,
        { rw [this, add_zero],
          exact hle _ le_rfl },
        rw finset.sum_eq_zero (Œª m hm, _),
        exact set.indicator_of_not_mem (hcon _ $ (finset.mem_Ico.1 hm).1.trans m.le_succ) _ } },
    exact not_le.2 (lt_of_lt_of_le i.lt_succ_self $ h _ le_rfl) this }
end

lemma limsup_eq_tendsto_sum_indicator_at_top' (s : ‚Ñï ‚Üí set Œ©) :
  limsup at_top s =
    {œâ | tendsto (Œª n, ‚àë k in finset.range n, (s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù) œâ) at_top at_top} :=
begin
  rw limsup_eq_tendsto_sum_indicator_at_top s,
  ext œâ,
  simp only [set.mem_set_of_eq],
  rw (_ : (Œª n, ‚àë k in finset.range n, (s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù) œâ) =
    (Œª n, ‚Üë(‚àë k in finset.range n, (s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñï) œâ))),
  { exact tendsto_coe_nat_at_top_iff.symm },
  { ext n,
    simp only [set.indicator, pi.one_apply, finset.sum_boole, nat.cast_id] }
end

end borel_cantelli

open borel_cantelli

lemma tendsto_sum_indicator_at_top_iff
  (Œº : measure Œ©) [is_finite_measure Œº] {s : ‚Ñï ‚Üí set Œ©} (hs : ‚àÄ n, measurable_set[‚Ñ± n] (s n)) :
  ‚àÄ·µê œâ ‚àÇŒº,
    tendsto (Œª n, ‚àë k in finset.range n, (s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù) œâ) at_top at_top ‚Üî
    tendsto (Œª n, ‚àë k in finset.range n, Œº[(s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù) | ‚Ñ± k] œâ)
      at_top at_top :=
begin
  have h‚ÇÅ := (martingale_mgale Œº hs).ae_not_tendsto_at_top_at_top (mgale_diff_le' hs),
  have h‚ÇÇ := (martingale_mgale Œº hs).ae_not_tendsto_at_top_at_bot (mgale_diff_le' hs),
  have h‚ÇÉ : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ k, (0 : ‚Ñù) ‚â§ Œº[(s (k + 1)).indicator 1|‚Ñ± k] œâ,
  { rw ae_all_iff,
    exact Œª n, condexp_nonneg (eventually_of_forall $ set.indicator_nonneg $ Œª _ _, zero_le_one) },
  filter_upwards [h‚ÇÅ, h‚ÇÇ, h‚ÇÉ] with œâ hœâ‚ÇÅ hœâ‚ÇÇ hœâ‚ÇÉ,
  split; intro ht,
  { refine tendsto_at_top_at_top_of_monotone'
      (Œª n m hnm, finset.sum_mono_set_of_nonneg hœâ‚ÇÉ $ finset.range_mono hnm) _,
    rintro ‚ü®b, hbdd‚ü©,
    rw ‚Üê tendsto_neg_at_bot_iff at ht,
    simp_rw [mgale, finset.sum_apply, pi.sub_apply, finset.sum_sub_distrib, sub_eq_add_neg] at hœâ‚ÇÅ,
    exact hœâ‚ÇÅ (tendsto_at_top_add_right_of_le _ (-b)
      ((tendsto_neg_at_bot_iff at_top).1 ht) $ Œª n, neg_le_neg (hbdd ‚ü®n, rfl‚ü©)) },
  { refine tendsto_at_top_at_top_of_monotone'
      (Œª n m hnm, finset.sum_mono_set_of_nonneg (Œª i, set.indicator_nonneg (Œª _ _, zero_le_one) _) $
      finset.range_mono hnm) _,
    rintro ‚ü®b, hbdd‚ü©,
    simp_rw [mgale, finset.sum_apply, pi.sub_apply, finset.sum_sub_distrib, sub_eq_add_neg] at hœâ‚ÇÇ,
    exact hœâ‚ÇÇ (tendsto_at_bot_add_left_of_ge _ b (Œª n, hbdd ‚ü®n, rfl‚ü©) $
      (tendsto_neg_at_bot_iff at_top).2 ht) },
end

/-- **L√©vy's generalization of the Borel-Cantelli lemma**: given a sequence of sets `s` and a
filtration `‚Ñ±` such that for all `n`, `s n` is `‚Ñ± n`-measurable, `at_top.limsup s` is almost
everywhere equal to the set for which `‚àë k, ‚Ñô(s (k + 1) | ‚Ñ± k) = ‚àû`. -/
theorem ae_mem_limsup_at_top_iff [is_finite_measure Œº]
  {s : ‚Ñï ‚Üí set Œ©} (hs : ‚àÄ n, measurable_set[‚Ñ± n] (s n)) :
  ‚àÄ·µê œâ ‚àÇŒº, œâ ‚àà limsup at_top s ‚Üî
    tendsto (Œª n, ‚àë k in finset.range n, Œº[(s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù) | ‚Ñ± k] œâ)
      at_top at_top :=
(borel_cantelli.limsup_eq_tendsto_sum_indicator_at_top' s).symm ‚ñ∏
  tendsto_sum_indicator_at_top_iff Œº hs

end measure_theory
