/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.martingale.convergence
import probability.martingale.optional_stopping
import probability.martingale.centering

/-!

# Generalized Borel-Cantelli lemma

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file proves LÃ©vy's generalized Borel-Cantelli lemma which is a generalization of the
Borel-Cantelli lemmas. With this generalization, one can easily deduce the Borel-Cantelli lemmas
by choosing appropriate filtrations. This file also contains the one sided martingale bound which
is required to prove the generalized Borel-Cantelli.

## Main results

- `measure_theory.submartingale.bdd_above_iff_exists_tendsto`: the one sided martingale bound: given
  a submartingale `f` with uniformly bounded differences, the set for which `f` converges is almost
  everywhere equal to the set for which it is bounded.
- `measure_theory.ae_mem_limsup_at_top_iff`: LÃ©vy's generalized Borel-Cantelli:
  given a filtration `â„±` and a sequence of sets `s` such that `s n âˆˆ â„± n` for all `n`,
  `limsup at_top s` is almost everywhere equal to the set for which `âˆ‘ â„™[s (n + 1)âˆ£â„± n] = âˆ`.

-/

open filter
open_locale nnreal ennreal measure_theory probability_theory big_operators topology

namespace measure_theory

variables {Î© : Type*} {m0 : measurable_space Î©} {Î¼ : measure Î©}
  {â„± : filtration â„• m0} {f : â„• â†’ Î© â†’ â„} {Ï‰ : Î©}

/-!
### One sided martingale bound
-/

-- TODO: `least_ge` should be defined taking values in `with_top â„•` once the `stopped_process`
-- refactor is complete
/-- `least_ge f r n` is the stopping time corresponding to the first time `f â‰¥ r`. -/
noncomputable
def least_ge (f : â„• â†’ Î© â†’ â„) (r : â„) (n : â„•) := hitting f (set.Ici r) 0 n

lemma adapted.is_stopping_time_least_ge (r : â„) (n : â„•) (hf : adapted â„± f) :
  is_stopping_time â„± (least_ge f r n) :=
hitting_is_stopping_time hf measurable_set_Ici

lemma least_ge_le {i : â„•} {r : â„} (Ï‰ : Î©) : least_ge f r i Ï‰ â‰¤ i :=
hitting_le Ï‰

-- The following four lemmas shows `least_ge` behaves like a stopped process. Ideally we should
-- define `least_ge` as a stopping time and take its stopped process. However, we can't do that
-- with our current definition since a stopping time takes only finite indicies. An upcomming
-- refactor should hopefully make it possible to have stopping times taking infinity as a value
lemma least_ge_mono {n m : â„•} (hnm : n â‰¤ m) (r : â„) (Ï‰ : Î©) :
  least_ge f r n Ï‰ â‰¤ least_ge f r m Ï‰ :=
hitting_mono hnm

lemma least_ge_eq_min (Ï€ : Î© â†’ â„•) (r : â„) (Ï‰ : Î©)
  {n : â„•} (hÏ€n : âˆ€ Ï‰, Ï€ Ï‰ â‰¤ n) :
  least_ge f r (Ï€ Ï‰) Ï‰ = min (Ï€ Ï‰) (least_ge f r n Ï‰) :=
begin
  classical,
  refine le_antisymm (le_min (least_ge_le _) (least_ge_mono (hÏ€n Ï‰) r Ï‰)) _,
  by_cases hle : Ï€ Ï‰ â‰¤ least_ge f r n Ï‰,
  { rw [min_eq_left hle, least_ge],
    by_cases h : âˆƒ j âˆˆ set.Icc 0 (Ï€ Ï‰), f j Ï‰ âˆˆ set.Ici r,
    { refine hle.trans (eq.le _),
      rw [least_ge, â† hitting_eq_hitting_of_exists (hÏ€n Ï‰) h] },
    { simp only [hitting, if_neg h] } },
  { rw [min_eq_right (not_le.1 hle).le, least_ge, least_ge,
      â† hitting_eq_hitting_of_exists (hÏ€n Ï‰) _],
    rw [not_le, least_ge, hitting_lt_iff _ (hÏ€n Ï‰)] at hle,
    exact let âŸ¨j, hjâ‚, hjâ‚‚âŸ© := hle in âŸ¨j, âŸ¨hjâ‚.1, hjâ‚.2.leâŸ©, hjâ‚‚âŸ© }
end

lemma stopped_value_stopped_value_least_ge (f : â„• â†’ Î© â†’ â„) (Ï€ : Î© â†’ â„•) (r : â„)
  {n : â„•} (hÏ€n : âˆ€ Ï‰, Ï€ Ï‰ â‰¤ n) :
  stopped_value (Î» i, stopped_value f (least_ge f r i)) Ï€
    = stopped_value (stopped_process f (least_ge f r n)) Ï€ :=
by { ext1 Ï‰, simp_rw [stopped_process, stopped_value], rw least_ge_eq_min _ _ _ hÏ€n, }

lemma submartingale.stopped_value_least_ge [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (r : â„) :
  submartingale (Î» i, stopped_value f (least_ge f r i)) â„± Î¼ :=
begin
  rw submartingale_iff_expected_stopped_value_mono,
  { intros Ïƒ Ï€ hÏƒ hÏ€ hÏƒ_le_Ï€ hÏ€_bdd,
    obtain âŸ¨n, hÏ€_le_nâŸ© := hÏ€_bdd,
    simp_rw stopped_value_stopped_value_least_ge f Ïƒ r (Î» i, (hÏƒ_le_Ï€ i).trans (hÏ€_le_n i)),
    simp_rw stopped_value_stopped_value_least_ge f Ï€ r hÏ€_le_n,
    refine hf.expected_stopped_value_mono _ _ _ (Î» Ï‰, (min_le_left _ _).trans (hÏ€_le_n Ï‰)),
    { exact hÏƒ.min (hf.adapted.is_stopping_time_least_ge _ _), },
    { exact hÏ€.min (hf.adapted.is_stopping_time_least_ge _ _), },
    { exact Î» Ï‰, min_le_min (hÏƒ_le_Ï€ Ï‰) le_rfl, }, },
  { exact Î» i, strongly_measurable_stopped_value_of_le hf.adapted.prog_measurable_of_discrete
      (hf.adapted.is_stopping_time_least_ge _ _) least_ge_le, },
  { exact Î» i, integrable_stopped_value _ ((hf.adapted.is_stopping_time_least_ge _ _))
      (hf.integrable) least_ge_le, },
end

variables {r : â„} {R : â„â‰¥0}

lemma norm_stopped_value_least_ge_le (hr : 0 â‰¤ r) (hf0 : f 0 = 0)
  (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) (i : â„•) :
  âˆ€áµ Ï‰ âˆ‚Î¼, stopped_value f (least_ge f r i) Ï‰ â‰¤ r + R :=
begin
  filter_upwards [hbdd] with Ï‰ hbddÏ‰,
  change f (least_ge f r i Ï‰) Ï‰ â‰¤ r + R,
  by_cases heq : least_ge f r i Ï‰ = 0,
  { rw [heq, hf0, pi.zero_apply],
    exact add_nonneg hr R.coe_nonneg },
  { obtain âŸ¨k, hkâŸ© := nat.exists_eq_succ_of_ne_zero heq,
    rw [hk, add_comm, â† sub_le_iff_le_add],
    have := not_mem_of_lt_hitting (hk.symm â–¸ k.lt_succ_self : k < least_ge f r i Ï‰) (zero_le _),
    simp only [set.mem_union, set.mem_Iic, set.mem_Ici, not_or_distrib, not_le] at this,
    exact (sub_lt_sub_left this _).le.trans ((le_abs_self _).trans (hbddÏ‰ _)) }
end

lemma submartingale.stopped_value_least_ge_snorm_le [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hr : 0 â‰¤ r) (hf0 : f 0 = 0)
  (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) (i : â„•) :
  snorm (stopped_value f (least_ge f r i)) 1 Î¼ â‰¤ 2 * Î¼ set.univ * ennreal.of_real (r + R) :=
begin
  refine snorm_one_le_of_le' ((hf.stopped_value_least_ge r).integrable _) _
    (norm_stopped_value_least_ge_le hr hf0 hbdd i),
  rw â† integral_univ,
  refine le_trans _ ((hf.stopped_value_least_ge r).set_integral_le (zero_le _)
    measurable_set.univ),
  simp_rw [stopped_value, least_ge, hitting_of_le le_rfl, hf0, integral_zero']
end

lemma submartingale.stopped_value_least_ge_snorm_le' [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hr : 0 â‰¤ r) (hf0 : f 0 = 0)
  (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) (i : â„•) :
  snorm (stopped_value f (least_ge f r i)) 1 Î¼ â‰¤
    ennreal.to_nnreal (2 * Î¼ set.univ * ennreal.of_real (r + R)) :=
begin
  refine (hf.stopped_value_least_ge_snorm_le hr hf0 hbdd i).trans _,
  simp [ennreal.coe_to_nnreal (measure_ne_top Î¼ _), ennreal.coe_to_nnreal],
end

/-- This lemma is superceded by `submartingale.bdd_above_iff_exists_tendsto`. -/
lemma submartingale.exists_tendsto_of_abs_bdd_above_aux [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hf0 : f 0 = 0)
  (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, bdd_above (set.range $ Î» n, f n Ï‰) â†’ âˆƒ c, tendsto (Î» n, f n Ï‰) at_top (ğ“ c) :=
begin
  have ht : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i : â„•, âˆƒ c, tendsto (Î» n, stopped_value f (least_ge f i n) Ï‰) at_top (ğ“ c),
  { rw ae_all_iff,
    exact Î» i, submartingale.exists_ae_tendsto_of_bdd (hf.stopped_value_least_ge i)
      (hf.stopped_value_least_ge_snorm_le' i.cast_nonneg hf0 hbdd) },
  filter_upwards [ht] with Ï‰ hÏ‰ hÏ‰b,
  rw bdd_above at hÏ‰b,
  obtain âŸ¨i, hiâŸ© := exists_nat_gt hÏ‰b.some,
  have hib : âˆ€ n, f n Ï‰ < i,
  { intro n,
    exact lt_of_le_of_lt ((mem_upper_bounds.1 hÏ‰b.some_mem) _ âŸ¨n, rflâŸ©) hi },
  have heq : âˆ€ n, stopped_value f (least_ge f i n) Ï‰ = f n Ï‰,
  { intro n,
    rw [least_ge, hitting, stopped_value],
    simp only,
    rw if_neg,
    simp only [set.mem_Icc, set.mem_union, set.mem_Ici],
    push_neg,
    exact Î» j _, hib j },
  simp only [â† heq, hÏ‰ i],
end

lemma submartingale.bdd_above_iff_exists_tendsto_aux [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hf0 : f 0 = 0)
  (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, bdd_above (set.range $ Î» n, f n Ï‰) â†” âˆƒ c, tendsto (Î» n, f n Ï‰) at_top (ğ“ c) :=
by filter_upwards [hf.exists_tendsto_of_abs_bdd_above_aux hf0 hbdd] with Ï‰ hÏ‰ using
  âŸ¨hÏ‰, Î» âŸ¨c, hcâŸ©, hc.bdd_above_rangeâŸ©

/-- One sided martingale bound: If `f` is a submartingale which has uniformly bounded differences,
then for almost every `Ï‰`, `f n Ï‰` is bounded above (in `n`) if and only if it converges. -/
lemma submartingale.bdd_above_iff_exists_tendsto [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, bdd_above (set.range $ Î» n, f n Ï‰) â†” âˆƒ c, tendsto (Î» n, f n Ï‰) at_top (ğ“ c) :=
begin
  set g : â„• â†’ Î© â†’ â„ := Î» n Ï‰, f n Ï‰ - f 0 Ï‰ with hgdef,
  have hg : submartingale g â„± Î¼ :=
    hf.sub_martingale (martingale_const_fun _ _ (hf.adapted 0) (hf.integrable 0)),
  have hg0 : g 0 = 0,
  { ext Ï‰,
    simp only [hgdef, sub_self, pi.zero_apply] },
  have hgbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ (i : â„•), |g (i + 1) Ï‰ - g i Ï‰| â‰¤ â†‘R,
  { simpa only [sub_sub_sub_cancel_right] },
  filter_upwards [hg.bdd_above_iff_exists_tendsto_aux hg0 hgbdd] with Ï‰ hÏ‰,
  convert hÏ‰ using 1; rw eq_iff_iff,
  { simp only [hgdef],
    refine âŸ¨Î» h, _, Î» h, _âŸ©;
    obtain âŸ¨b, hbâŸ© := h;
    refine âŸ¨b + |f 0 Ï‰|, Î» y hy, _âŸ©;
    obtain âŸ¨n, rflâŸ© := hy,
    { simp_rw [sub_eq_add_neg],
      exact add_le_add (hb âŸ¨n, rflâŸ©) (neg_le_abs_self _) },
    { exact sub_le_iff_le_add.1 (le_trans (sub_le_sub_left (le_abs_self _) _) (hb âŸ¨n, rflâŸ©)) } },
  { simp only [hgdef],
    refine âŸ¨Î» h, _, Î» h, _âŸ©;
    obtain âŸ¨c, hcâŸ© := h,
    { exact âŸ¨c - f 0 Ï‰, hc.sub_const _âŸ© },
    { refine âŸ¨c + f 0 Ï‰, _âŸ©,
      have := hc.add_const (f 0 Ï‰),
      simpa only [sub_add_cancel] } }
end

/-!
### LÃ©vy's generalization of the Borel-Cantelli lemma

LÃ©vy's generalization of the Borel-Cantelli lemma states that: given a natural number indexed
filtration $(\mathcal{F}_n)$, and a sequence of sets $(s_n)$ such that for all
$n$, $s_n \in \mathcal{F}_n$, $limsup_n s_n$ is almost everywhere equal to the set for which
$\sum_n \mathbb{P}[s_n \mid \mathcal{F}_n] = \infty$.

The proof strategy follows by constructing a martingale satisfying the one sided martingale bound.
In particular, we define
$$
  f_n := \sum_{k < n} \mathbf{1}_{s_{n + 1}} - \mathbb{P}[s_{n + 1} \mid \mathcal{F}_n].
$$
Then, as a martingale is both a sub and a super-martingale, the set for which it is unbounded from
above must agree with the set for which it is unbounded from below almost everywhere. Thus, it
can only converge to $\pm \infty$ with probability 0. Thus, by considering
$$
  \limsup_n s_n = \{\sum_n \mathbf{1}_{s_n} = \infty\}
$$
almost everywhere, the result follows.
-/

lemma martingale.bdd_above_range_iff_bdd_below_range [is_finite_measure Î¼]
  (hf : martingale f â„± Î¼) (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, bdd_above (set.range (Î» n, f n Ï‰)) â†” bdd_below (set.range (Î» n, f n Ï‰)) :=
begin
  have hbdd' : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |(-f) (i + 1) Ï‰ - (-f) i Ï‰| â‰¤ R,
  { filter_upwards [hbdd] with Ï‰ hÏ‰ i,
    erw [â† abs_neg, neg_sub, sub_neg_eq_add, neg_add_eq_sub],
    exact hÏ‰ i },
  have hup := hf.submartingale.bdd_above_iff_exists_tendsto hbdd,
  have hdown := hf.neg.submartingale.bdd_above_iff_exists_tendsto hbdd',
  filter_upwards [hup, hdown] with Ï‰ hÏ‰â‚ hÏ‰â‚‚,
  have : (âˆƒ c, tendsto (Î» n, f n Ï‰) at_top (ğ“ c)) â†” âˆƒ c, tendsto (Î» n, (-f) n Ï‰) at_top (ğ“ c),
  { split; rintro âŸ¨c, hcâŸ©,
    { exact âŸ¨-c, hc.negâŸ© },
    { refine âŸ¨-c, _âŸ©,
      convert hc.neg,
      simp only [neg_neg, pi.neg_apply] } },
  rw [hÏ‰â‚, this, â† hÏ‰â‚‚],
  split; rintro âŸ¨c, hcâŸ©; refine âŸ¨-c, Î» Ï‰ hÏ‰, _âŸ©,
  { rw mem_upper_bounds at hc,
    refine neg_le.2 (hc _ _),
    simpa only [pi.neg_apply, set.mem_range, neg_inj] },
  { rw mem_lower_bounds at hc,
    simp_rw [set.mem_range, pi.neg_apply, neg_eq_iff_eq_neg] at hÏ‰,
    refine le_neg.1 (hc _ _),
    simpa only [set.mem_range] }
end

lemma martingale.ae_not_tendsto_at_top_at_top [is_finite_measure Î¼]
  (hf : martingale f â„± Î¼) (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, Â¬ tendsto (Î» n, f n Ï‰) at_top at_top :=
by filter_upwards [hf.bdd_above_range_iff_bdd_below_range hbdd] with Ï‰ hÏ‰ htop using
    unbounded_of_tendsto_at_top htop (hÏ‰.2 $ bdd_below_range_of_tendsto_at_top_at_top htop)

lemma martingale.ae_not_tendsto_at_top_at_bot [is_finite_measure Î¼]
  (hf : martingale f â„± Î¼) (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, Â¬ tendsto (Î» n, f n Ï‰) at_top at_bot :=
by filter_upwards [hf.bdd_above_range_iff_bdd_below_range hbdd] with Ï‰ hÏ‰ htop using
    unbounded_of_tendsto_at_bot htop (hÏ‰.1 $ bdd_above_range_of_tendsto_at_top_at_bot htop)

namespace borel_cantelli

/-- Auxiliary definition required to prove LÃ©vy's generalization of the Borel-Cantelli lemmas for
which we will take the martingale part. -/
noncomputable
def process (s : â„• â†’ set Î©) (n : â„•) : Î© â†’ â„ :=
âˆ‘ k in finset.range n, (s (k + 1)).indicator 1

variables {s : â„• â†’ set Î©}

lemma process_zero : process s 0 = 0 :=
by rw [process, finset.range_zero, finset.sum_empty]

lemma adapted_process (hs : âˆ€ n, measurable_set[â„± n] (s n)) :
  adapted â„± (process s) :=
Î» n, finset.strongly_measurable_sum' _ $ Î» k hk, strongly_measurable_one.indicator $
  â„±.mono (finset.mem_range.1 hk) _ $ hs _

lemma martingale_part_process_ae_eq (â„± : filtration â„• m0) (Î¼ : measure Î©) (s : â„• â†’ set Î©) (n : â„•) :
  martingale_part (process s) â„± Î¼ n =
  âˆ‘ k in finset.range n, ((s (k + 1)).indicator 1 - Î¼[(s (k + 1)).indicator 1 | â„± k]) :=
begin
  simp only [martingale_part_eq_sum, process_zero, zero_add],
  refine finset.sum_congr rfl (Î» k hk, _),
  simp only [process, finset.sum_range_succ_sub_sum],
end

lemma predictable_part_process_ae_eq (â„± : filtration â„• m0) (Î¼ : measure Î©) (s : â„• â†’ set Î©) (n : â„•) :
  predictable_part (process s) â„± Î¼ n =
  âˆ‘ k in finset.range n, Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | â„± k] :=
begin
  have := martingale_part_process_ae_eq â„± Î¼ s n,
  simp_rw [martingale_part, process, finset.sum_sub_distrib] at this,
  exact sub_right_injective this,
end

lemma process_difference_le (s : â„• â†’ set Î©) (Ï‰ : Î©) (n : â„•) :
  |process s (n + 1) Ï‰ - process s n Ï‰| â‰¤ (1 : â„â‰¥0) :=
begin
  rw [nonneg.coe_one, process, process, finset.sum_apply, finset.sum_apply,
    finset.sum_range_succ_sub_sum, â† real.norm_eq_abs, norm_indicator_eq_indicator_norm],
  refine set.indicator_le' (Î» _ _, _) (Î» _ _, zero_le_one) _,
  rw [pi.one_apply, norm_one]
end

lemma integrable_process (Î¼ : measure Î©) [is_finite_measure Î¼]
  (hs : âˆ€ n, measurable_set[â„± n] (s n)) (n : â„•) :
  integrable (process s n) Î¼ :=
integrable_finset_sum' _ $ Î» k hk,
  integrable_on.integrable_indicator (integrable_const 1) $ â„±.le _ _ $ hs _

end borel_cantelli

open borel_cantelli

/-- An a.e. monotone adapted process `f` with uniformly bounded differences converges to `+âˆ` if
and only if its predictable part also converges to `+âˆ`. -/
lemma tendsto_sum_indicator_at_top_iff [is_finite_measure Î¼]
  (hfmono : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, f n Ï‰ â‰¤ f (n + 1) Ï‰)
  (hf : adapted â„± f) (hint : âˆ€ n, integrable (f n) Î¼)
  (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, |f (n + 1) Ï‰ - f n Ï‰| â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, tendsto (Î» n, f n Ï‰) at_top at_top â†”
    tendsto (Î» n, predictable_part f â„± Î¼ n Ï‰) at_top at_top :=
begin
  have hâ‚ := (martingale_martingale_part hf hint).ae_not_tendsto_at_top_at_top
    (martingale_part_bdd_difference â„± hbdd),
  have hâ‚‚ := (martingale_martingale_part hf hint).ae_not_tendsto_at_top_at_bot
    (martingale_part_bdd_difference â„± hbdd),
  have hâ‚ƒ : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, 0 â‰¤ Î¼[f (n + 1) - f n | â„± n] Ï‰,
  { refine ae_all_iff.2 (Î» n, condexp_nonneg _),
    filter_upwards [ae_all_iff.1 hfmono n] with Ï‰ hÏ‰ using sub_nonneg.2 hÏ‰ },
  filter_upwards [hâ‚, hâ‚‚, hâ‚ƒ, hfmono] with Ï‰ hÏ‰â‚ hÏ‰â‚‚ hÏ‰â‚ƒ hÏ‰â‚„,
  split; intro ht,
  { refine tendsto_at_top_at_top_of_monotone' _ _,
    { intros n m hnm,
      simp only [predictable_part, finset.sum_apply],
      refine finset.sum_mono_set_of_nonneg hÏ‰â‚ƒ (finset.range_mono hnm) },
    rintro âŸ¨b, hbddâŸ©,
    rw â† tendsto_neg_at_bot_iff at ht,
    simp only [martingale_part, sub_eq_add_neg] at hÏ‰â‚,
    exact hÏ‰â‚ (tendsto_at_top_add_right_of_le _ (-b)
      (tendsto_neg_at_bot_iff.1 ht) $ Î» n, neg_le_neg (hbdd âŸ¨n, rflâŸ©)) },
  { refine tendsto_at_top_at_top_of_monotone' (monotone_nat_of_le_succ hÏ‰â‚„) _,
    rintro âŸ¨b, hbddâŸ©,
    exact hÏ‰â‚‚ (tendsto_at_bot_add_left_of_ge _ b (Î» n, hbdd âŸ¨n, rflâŸ©) $
      tendsto_neg_at_bot_iff.2 ht) },
end

open borel_cantelli

lemma tendsto_sum_indicator_at_top_iff' [is_finite_measure Î¼]
  {s : â„• â†’ set Î©} (hs : âˆ€ n, measurable_set[â„± n] (s n)) :
  âˆ€áµ Ï‰ âˆ‚Î¼,
    tendsto (Î» n, âˆ‘ k in finset.range n, (s (k + 1)).indicator (1 : Î© â†’ â„) Ï‰) at_top at_top â†”
    tendsto (Î» n, âˆ‘ k in finset.range n, Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | â„± k] Ï‰)
      at_top at_top :=
begin
  have := tendsto_sum_indicator_at_top_iff (eventually_of_forall $ Î» Ï‰ n, _) (adapted_process hs)
    (integrable_process Î¼ hs) (eventually_of_forall $ process_difference_le s),
  swap,
  { rw [process, process, â† sub_nonneg, finset.sum_apply, finset.sum_apply,
      finset.sum_range_succ_sub_sum],
    exact set.indicator_nonneg (Î» _ _, zero_le_one) _ },
  simp_rw [process, predictable_part_process_ae_eq] at this,
  simpa using this,
end

/-- **LÃ©vy's generalization of the Borel-Cantelli lemma**: given a sequence of sets `s` and a
filtration `â„±` such that for all `n`, `s n` is `â„± n`-measurable, `at_top.limsup s` is almost
everywhere equal to the set for which `âˆ‘ k, â„™(s (k + 1) | â„± k) = âˆ`. -/
theorem ae_mem_limsup_at_top_iff (Î¼ : measure Î©) [is_finite_measure Î¼]
  {s : â„• â†’ set Î©} (hs : âˆ€ n, measurable_set[â„± n] (s n)) :
  âˆ€áµ Ï‰ âˆ‚Î¼, Ï‰ âˆˆ limsup s at_top â†”
    tendsto (Î» n, âˆ‘ k in finset.range n, Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„) | â„± k] Ï‰)
      at_top at_top :=
(limsup_eq_tendsto_sum_indicator_at_top â„ s).symm â–¸ tendsto_sum_indicator_at_top_iff' hs

end measure_theory
