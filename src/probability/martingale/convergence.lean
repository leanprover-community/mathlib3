/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.martingale.upcrossing
import measure_theory.function.uniform_integrable
import measure_theory.constructions.polish

/-!

# Martingale convergence theorems

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

The martingale convergence theorems are a collection of theorems characterizing the convergence
of a martingale provided it satisfies some boundedness conditions. This file contains the
almost everywhere martingale convergence theorem which provides an almost everywhere limit to
an LÂ¹ bounded submartingale. It also contains the LÂ¹ martingale convergence theorem which provides
an LÂ¹ limit to a uniformly integrable submartingale. Finally, it also contains the LÃ©vy upwards
theorems.

## Main results

* `measure_theory.submartingale.ae_tendsto_limit_process`: the almost everywhere martingale
  convergence theorem: an LÂ¹-bounded submartingale adapted to the filtration `â„±` converges almost
  everywhere to its limit process.
* `measure_theory.submartingale.mem_â„’p_limit_process`: the limit process of an Láµ–-bounded
  submartingale is Láµ–.
* `measure_theory.submartingale.tendsto_snorm_one_limit_process`: part a of the LÂ¹ martingale
  convergence theorem: a uniformly integrable submartingale adapted to the filtration `â„±` converges
  almost everywhere and in LÂ¹ to an integrable function which is measurable with respect to
  the Ïƒ-algebra `â¨† n, â„± n`.
* `measure_theory.martingale.ae_eq_condexp_limit_process`: part b the LÂ¹ martingale convergence
  theorem: if `f` is a uniformly integrable martingale adapted to the filtration `â„±`, then
  `f n` equals `ğ”¼[g | â„± n]` almost everywhere where `g` is the limiting process of `f`.
* `measure_theory.integrable.tendsto_ae_condexp`: part c the LÂ¹ martingale convergence theorem:
  given a `â¨† n, â„± n`-measurable function `g` where `â„±` is a filtration, `ğ”¼[g | â„± n]` converges
  almost everywhere to `g`.
* `measure_theory.integrable.tendsto_snorm_condexp`: part c the LÂ¹ martingale convergence theorem:
  given a `â¨† n, â„± n`-measurable function `g` where `â„±` is a filtration, `ğ”¼[g | â„± n]` converges in
  LÂ¹ to `g`.

-/

open topological_space filter measure_theory.filtration
open_locale nnreal ennreal measure_theory probability_theory big_operators topology

namespace measure_theory

variables {Î© Î¹ : Type*} {m0 : measurable_space Î©} {Î¼ : measure Î©} {â„± : filtration â„• m0}
variables {a b : â„} {f : â„• â†’ Î© â†’ â„} {Ï‰ : Î©} {R : â„â‰¥0}

section ae_convergence

/-!

### Almost everywhere martingale convergence theorem

We will now prove the almost everywhere martingale convergence theorem.

The a.e. martingale convergence theorem states: if `f` is an LÂ¹-bounded `â„±`-submartingale, then
it converges almost everywhere to an integrable function which is measurable with respect to
the Ïƒ-algebra `â„±âˆ := â¨† n, â„± n`.

Mathematically, we proceed by first noting that a real sequence $(x_n)$ converges if
(a) $\limsup_{n \to \infty} |x_n| < \infty$, (b) for all $a < b \in \mathbb{Q}$ we have the
number of upcrossings of $(x_n)$ from below $a$ to above $b$ is finite.
Thus, for all $\omega$ satisfying $\limsup_{n \to \infty} |f_n(\omega)| < \infty$ and the number of
upcrossings of $(f_n(\omega))$ from below $a$ to above $b$ is finite for all $a < b \in \mathbb{Q}$,
we have $(f_n(\omega))$ is convergent.

Hence, assuming $(f_n)$ is LÂ¹-bounded, using Fatou's lemma, we have
$$
  \mathbb{E} \limsup_{n \to \infty} |f_n| \le \limsup_{n \to \infty} \mathbb{E}|f_n| < \infty
$$
implying $\limsup_{n \to \infty} |f_n| < \infty$ a.e. Furthermore, by the upcrossing estimate,
the number of upcrossings is finite almost everywhere implying $f$ converges pointwise almost
everywhere.

Thus, denoting $g$ the a.e. limit of $(f_n)$, $g$ is $\mathcal{F}_\infty$-measurable as for all
$n$, $f_n$ is $\mathcal{F}_n$-measurable and $\mathcal{F}_n \le \mathcal{F}_\infty$. Finally, $g$
is integrable as $|g| \le \liminf_{n \to \infty} |f_n|$ so
$$
  \mathbb{E}|g| \le \mathbb{E} \limsup_{n \to \infty} |f_n| \le
    \limsup_{n \to \infty} \mathbb{E}|f_n| < \infty
$$
as required.

Implementation wise, we have `tendsto_of_no_upcrossings` which showed that
a bounded sequence converges if it does not visit below $a$ and above $b$ infinitely often
for all $a, b âˆˆ s$ for some dense set $s$. So, we may skip the first step provided we can prove
that the realizations are bounded almost everywhere. Indeed, suppose $(|f_n(\omega)|)$ is not
bounded, then either $f_n(\omega) \to \pm \infty$ or one of $\limsup f_n(\omega)$ or
$\liminf f_n(\omega)$ equals $\pm \infty$ while the other is finite. But the first case
contradicts $\liminf |f_n(\omega)| < \infty$ while the second case contradicts finite upcrossings.

Furthermore, we introduced `filtration.limit_process` which chooses the limiting random variable
of a stochastic process if it exists, otherwise it returns 0. Hence, instead of showing an
existence statement, we phrased the a.e. martingale convergence theorem by showed that a
submartingale converges to its `limit_process` almost everywhere.

-/

/-- If a stochastic process has bounded upcrossing from below `a` to above `b`,
then it does not frequently visit both below `a` and above `b`. -/
lemma not_frequently_of_upcrossings_lt_top (hab : a < b) (hÏ‰ : upcrossings a b f Ï‰ â‰  âˆ) :
  Â¬((âˆƒá¶  n in at_top, f n Ï‰ < a) âˆ§ (âˆƒá¶  n in at_top, b < f n Ï‰)) :=
begin
  rw [â† lt_top_iff_ne_top, upcrossings_lt_top_iff] at hÏ‰,
  replace hÏ‰ : âˆƒ k, âˆ€ N, upcrossings_before a b f N Ï‰ < k,
  { obtain âŸ¨k, hkâŸ© := hÏ‰,
    exact âŸ¨k + 1, Î» N, lt_of_le_of_lt (hk N) k.lt_succ_selfâŸ© },
  rintro âŸ¨hâ‚, hâ‚‚âŸ©,
  rw frequently_at_top at hâ‚ hâ‚‚,
  refine not_not.2 hÏ‰ _,
  push_neg,
  intro k,
  induction k with k ih,
  { simp only [zero_le', exists_const] },
  { obtain âŸ¨N, hNâŸ© := ih,
    obtain âŸ¨Nâ‚, hNâ‚, hNâ‚'âŸ© := hâ‚ N,
    obtain âŸ¨Nâ‚‚, hNâ‚‚, hNâ‚‚'âŸ© := hâ‚‚ Nâ‚,
    exact âŸ¨(Nâ‚‚ + 1), nat.succ_le_of_lt $ lt_of_le_of_lt hN
      (upcrossings_before_lt_of_exists_upcrossing hab hNâ‚ hNâ‚' hNâ‚‚ hNâ‚‚')âŸ© }
end

/-- A stochastic process that frequently visits below `a` and above `b` have infinite
upcrossings. -/
lemma upcrossings_eq_top_of_frequently_lt (hab : a < b)
  (hâ‚ : âˆƒá¶  n in at_top, f n Ï‰ < a) (hâ‚‚ : âˆƒá¶  n in at_top, b < f n Ï‰) :
  upcrossings a b f Ï‰ = âˆ :=
classical.by_contradiction (Î» h, not_frequently_of_upcrossings_lt_top hab h âŸ¨hâ‚, hâ‚‚âŸ©)

/-- A realization of a stochastic process with bounded upcrossings and bounded liminfs is
convergent.

We use the spelling `< âˆ` instead of the standard `â‰  âˆ` in the assumptions since it is not as easy
to change `<` to `â‰ ` under binders. -/
lemma tendsto_of_uncrossing_lt_top
  (hfâ‚ : liminf (Î» n, (â€–f n Ï‰â€–â‚Š : â„â‰¥0âˆ)) at_top < âˆ)
  (hfâ‚‚ : âˆ€ a b : â„š, a < b â†’ upcrossings a b f Ï‰ < âˆ) :
  âˆƒ c, tendsto (Î» n, f n Ï‰) at_top (ğ“ c) :=
begin
  by_cases h : is_bounded_under (â‰¤) at_top (Î» n, |f n Ï‰|),
  { rw is_bounded_under_le_abs at h,
    refine tendsto_of_no_upcrossings rat.dense_range_cast _ h.1 h.2,
    { intros a ha b hb hab,
      obtain âŸ¨âŸ¨a, rflâŸ©, âŸ¨b, rflâŸ©âŸ© := âŸ¨ha, hbâŸ©,
      exact not_frequently_of_upcrossings_lt_top hab (hfâ‚‚ a b (rat.cast_lt.1 hab)).ne } },
  { obtain âŸ¨a, b, hab, hâ‚, hâ‚‚âŸ© := ennreal.exists_upcrossings_of_not_bounded_under hfâ‚.ne h,
    exact false.elim ((hfâ‚‚ a b hab).ne
      (upcrossings_eq_top_of_frequently_lt (rat.cast_lt.2 hab) hâ‚ hâ‚‚)) }
end

/-- An LÂ¹-bounded submartingale has bounded upcrossings almost everywhere. -/
lemma submartingale.upcrossings_ae_lt_top' [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) (hab : a < b) :
  âˆ€áµ Ï‰ âˆ‚Î¼, upcrossings a b f Ï‰ < âˆ :=
begin
  refine ae_lt_top (hf.adapted.measurable_upcrossings hab) _,
  have := hf.mul_lintegral_upcrossings_le_lintegral_pos_part a b,
  rw [mul_comm, â† ennreal.le_div_iff_mul_le] at this,
  { refine (lt_of_le_of_lt this (ennreal.div_lt_top _ _)).ne,
    { have hR' : âˆ€ n, âˆ«â» Ï‰, â€–f n Ï‰ - aâ€–â‚Š âˆ‚Î¼ â‰¤ R + â€–aâ€–â‚Š * Î¼ set.univ,
      { simp_rw snorm_one_eq_lintegral_nnnorm at hbdd,
        intro n,
        refine (lintegral_mono _ : âˆ«â» Ï‰, â€–f n Ï‰ - aâ€–â‚Š âˆ‚Î¼ â‰¤ âˆ«â» Ï‰, â€–f n Ï‰â€–â‚Š + â€–aâ€–â‚Š âˆ‚Î¼).trans _,
        { intro Ï‰,
          simp_rw [sub_eq_add_neg, â† nnnorm_neg a, â† ennreal.coe_add, ennreal.coe_le_coe],
          exact nnnorm_add_le _ _ },
        { simp_rw [ lintegral_add_right _ measurable_const, lintegral_const],
          exact add_le_add (hbdd _) le_rfl } },
      refine ne_of_lt (supr_lt_iff.2 âŸ¨R + â€–aâ€–â‚Š * Î¼ set.univ, ennreal.add_lt_top.2
          âŸ¨ennreal.coe_lt_top, ennreal.mul_lt_top ennreal.coe_lt_top.ne (measure_ne_top _ _)âŸ©,
          Î» n, le_trans _ (hR' n)âŸ©),
      refine lintegral_mono (Î» Ï‰, _),
      rw [ennreal.of_real_le_iff_le_to_real, ennreal.coe_to_real, coe_nnnorm],
      by_cases hnonneg : 0 â‰¤ f n Ï‰ - a,
      { rw [lattice_ordered_comm_group.pos_of_nonneg _ hnonneg,
          real.norm_eq_abs, abs_of_nonneg hnonneg] },
      { rw lattice_ordered_comm_group.pos_of_nonpos _ (not_le.1 hnonneg).le,
        exact norm_nonneg _ },
      { simp only [ne.def, ennreal.coe_ne_top, not_false_iff] } },
    { simp only [hab, ne.def, ennreal.of_real_eq_zero, sub_nonpos, not_le] } },
  { simp only [hab, ne.def, ennreal.of_real_eq_zero, sub_nonpos, not_le, true_or]},
  { simp only [ne.def, ennreal.of_real_ne_top, not_false_iff, true_or] }
end

lemma submartingale.upcrossings_ae_lt_top [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ a b : â„š, a < b â†’ upcrossings a b f Ï‰ < âˆ :=
begin
  simp only [ae_all_iff, eventually_imp_distrib_left],
  rintro a b hab,
  exact hf.upcrossings_ae_lt_top' hbdd (rat.cast_lt.2 hab),
end

/-- An LÂ¹-bounded submartingale converges almost everywhere. -/
lemma submartingale.exists_ae_tendsto_of_bdd [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, âˆƒ c, tendsto (Î» n, f n Ï‰) at_top (ğ“ c) :=
begin
  filter_upwards [hf.upcrossings_ae_lt_top hbdd, ae_bdd_liminf_at_top_of_snorm_bdd one_ne_zero
    (Î» n, (hf.strongly_measurable n).measurable.mono (â„±.le n) le_rfl) hbdd] with Ï‰ hâ‚ hâ‚‚,
  exact tendsto_of_uncrossing_lt_top hâ‚‚ hâ‚,
end

lemma submartingale.exists_ae_trim_tendsto_of_bdd [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚(Î¼.trim (Sup_le (Î» m âŸ¨n, hnâŸ©, hn â–¸ â„±.le _) : (â¨† n, â„± n) â‰¤ m0)),
    âˆƒ c, tendsto (Î» n, f n Ï‰) at_top (ğ“ c) :=
begin
  rw [ae_iff, trim_measurable_set_eq],
  { exact hf.exists_ae_tendsto_of_bdd hbdd },
  { exact measurable_set.compl (@measurable_set_exists_tendsto _ _ _ _ _ _ (â¨† n, â„± n) _ _ _ _ _
    (Î» n, ((hf.strongly_measurable n).measurable.mono (le_Sup âŸ¨n, rflâŸ©) le_rfl))) }
end

/-- **Almost everywhere martingale convergence theorem**: An LÂ¹-bounded submartingale converges
almost everywhere to a `â¨† n, â„± n`-measurable function. -/
lemma submartingale.ae_tendsto_limit_process [is_finite_measure Î¼]
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆ€áµ Ï‰ âˆ‚Î¼, tendsto (Î» n, f n Ï‰) at_top (ğ“ (â„±.limit_process f Î¼ Ï‰)) :=
begin
  classical,
  suffices : âˆƒ g, strongly_measurable[â¨† n, â„± n] g âˆ§ âˆ€áµ Ï‰ âˆ‚Î¼, tendsto (Î» n, f n Ï‰) at_top (ğ“ (g Ï‰)),
  { rw [limit_process, dif_pos this],
    exact (classical.some_spec this).2 },
  set g' : Î© â†’ â„ := Î» Ï‰, if h : âˆƒ c, tendsto (Î» n, f n Ï‰) at_top (ğ“ c) then h.some else 0,
  have hle : (â¨† n, â„± n) â‰¤ m0 := Sup_le (Î» m âŸ¨n, hnâŸ©, hn â–¸ â„±.le _),
  have hg' : âˆ€áµ Ï‰ âˆ‚(Î¼.trim hle), tendsto (Î» n, f n Ï‰) at_top (ğ“ (g' Ï‰)),
  { filter_upwards [hf.exists_ae_trim_tendsto_of_bdd hbdd] with Ï‰ hÏ‰,
    simp_rw [g', dif_pos hÏ‰],
    exact hÏ‰.some_spec },
  have hg'm : @ae_strongly_measurable _ _ _ (â¨† n, â„± n) g' (Î¼.trim hle) :=
    (@ae_measurable_of_tendsto_metrizable_ae' _ _ (â¨† n, â„± n) _ _ _ _ _ _ _
      (Î» n, ((hf.strongly_measurable n).measurable.mono
      (le_Sup âŸ¨n, rflâŸ© : â„± n â‰¤ â¨† n, â„± n) le_rfl).ae_measurable) hg').ae_strongly_measurable,
  obtain âŸ¨g, hgm, haeâŸ© := hg'm,
  have hg : âˆ€áµ Ï‰ âˆ‚Î¼.trim hle, tendsto (Î» n, f n Ï‰) at_top (ğ“ (g Ï‰)),
  { filter_upwards [hae, hg'] with Ï‰ hÏ‰ hg'Ï‰,
    exact hÏ‰ â–¸ hg'Ï‰ },
  exact âŸ¨g, hgm, measure_eq_zero_of_trim_eq_zero hle hgâŸ©,
end

/-- The limiting process of an Láµ–-bounded submartingale is Láµ–. -/
lemma submartingale.mem_â„’p_limit_process {p : â„â‰¥0âˆ}
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) p Î¼ â‰¤ R) :
  mem_â„’p (â„±.limit_process f Î¼) p Î¼ :=
mem_â„’p_limit_process_of_snorm_bdd
  (Î» n, ((hf.strongly_measurable n).mono (â„±.le n)).ae_strongly_measurable) hbdd

end ae_convergence

section L1_convergence

variables [is_finite_measure Î¼] {g : Î© â†’ â„}

/-!

### LÂ¹ martingale convergence theorem

We will now prove the LÂ¹ martingale convergence theorems.

The LÂ¹ martingale convergence theorem states that:
(a) if `f` is a uniformly integrable (in the probability sense) submartingale adapted to the
  filtration `â„±`, it converges in LÂ¹ to an integrable function `g` which is measurable with
  respect to `â„±âˆ := â¨† n, â„± n` and
(b) if `f` is actually a martingale, `f n = ğ”¼[g | â„± n]` almost everywhere.
(c) Finally, if `h` is integrable and measurable with respect to `â„±âˆ`, `(ğ”¼[h | â„± n])â‚™` is a
  uniformly integrable martingale which converges to `h` almost everywhere and in LÂ¹.

The proof is quite simple. (a) follows directly from the a.e. martingale convergence theorem
and the Vitali convergence theorem as our definition of uniform integrability (in the probability
sense) directly implies LÂ¹-uniform boundedness. We note that our definition of uniform
integrability is slightly non-standard but is equivalent to the usual literary definition. This
equivalence is provided by `measure_theory.uniform_integrable_iff`.

(b) follows since given $n$, we have for all $m \ge n$,
$$
  \|f_n - \mathbb{E}[g \mid \mathcal{F}_n]\|_1 =
    \|\mathbb{E}[f_m - g \mid \mathcal{F}_n]\|_1 \le \|\|f_m - g\|_1.
$$
Thus, taking $m \to \infty$ provides the almost everywhere equality.

Finally, to prove (c), we define $f_n := \mathbb{E}[h \mid \mathcal{F}_n]$. It is clear that
$(f_n)_n$ is a martingale by the tower property for conditional expectations. Furthermore,
$(f_n)_n$ is uniformly integrable in the probability sense. Indeed, as a single function is
uniformly integrable in the measure theory sense, for all $\epsilon > 0$, there exists some
$\delta > 0$ such that for all measurable set $A$ with $\mu(A) < Î´$, we have
$\mathbb{E}|h|\mathbf{1}_A < \epsilon$. So, since for sufficently large $\lambda$, by the Markov
inequality, we have for all $n$,
$$
  \mu(|f_n| \ge \lambda) \le \lambda^{-1}\mathbb{E}|f_n| \le \lambda^{-1}\mathbb|g| < \delta,
$$
we have for sufficently large $\lambda$, for all $n$,
$$
  \mathbb{E}|f_n|\mathbf{1}_{|f_n| \ge \lambda} \le
    \mathbb|g|\mathbf{1}_{|f_n| \ge \lambda} < \epsilon,
$$
implying $(f_n)_n$ is uniformly integrable. Now, to prove $f_n \to h$ almost everywhere and in
LÂ¹, it suffices to show that $h = g$ almost everywhere where $g$ is the almost everywhere and LÂ¹
limit of $(f_n)_n$ from part (b) of the theorem. By noting that, for all $s \in \mathcal{F}_n$,
we have
$$
  \mathbb{E}g\mathbf{1}_s = \mathbb{E}[\mathbb{E}[g \mid \mathcal{F}_n]\mathbf{1}_s] =
    \mathbb{E}[\mathbb{E}[h \mid \mathcal{F}_n]\mathbf{1}_s] = \mathbb{E}h\mathbf{1}_s
$$
where $\mathbb{E}[g \mid \mathcal{F}_n = \mathbb{E}[h \mid \mathcal{F}_n]$ almost everywhere
by part (b); the equality also holds for all $s \in \mathcal{F}_\infty$ by Dynkin's theorem.
Thus, as both $h$ and $g$ are $\mathcal{F}_\infty$-measurable, $h = g$ almost everywhere as
required.

Similar to the a.e. martingale convergence theorem, rather than showing the existence of the
limiting process, we phrased the LÂ¹-martingale convergence theorem by proving that a submartingale
does converge in LÂ¹ to its `limit_process`. However, in contrast to the a.e. martingale convergence
theorem, we do not need to introduce a LÂ¹ version of `filtration.limit_process` as the LÂ¹ limit
and the a.e. limit of a submartingale coincide.

-/

/-- Part a of the **LÂ¹ martingale convergence theorem**: a uniformly integrable submartingale
adapted to the filtration `â„±` converges a.e. and in LÂ¹ to an integrable function which is
measurable with respect to the Ïƒ-algebra `â¨† n, â„± n`. -/
lemma submartingale.tendsto_snorm_one_limit_process
  (hf : submartingale f â„± Î¼) (hunif : uniform_integrable f 1 Î¼) :
  tendsto (Î» n, snorm (f n - â„±.limit_process f Î¼) 1 Î¼) at_top (ğ“ 0) :=
begin
  obtain âŸ¨R, hRâŸ© := hunif.2.2,
  have hmeas : âˆ€ n, ae_strongly_measurable (f n) Î¼ :=
    Î» n, ((hf.strongly_measurable n).mono (â„±.le _)).ae_strongly_measurable,
  exact tendsto_Lp_of_tendsto_in_measure _ le_rfl ennreal.one_ne_top hmeas
    (mem_â„’p_limit_process_of_snorm_bdd hmeas hR) hunif.2.1
    (tendsto_in_measure_of_tendsto_ae hmeas $ hf.ae_tendsto_limit_process hR),
end

lemma submartingale.ae_tendsto_limit_process_of_uniform_integrable
  (hf : submartingale f â„± Î¼) (hunif : uniform_integrable f 1 Î¼) :
  âˆ€áµ Ï‰ âˆ‚Î¼, tendsto (Î» n, f n Ï‰) at_top (ğ“ (â„±.limit_process f Î¼ Ï‰)) :=
let âŸ¨R, hRâŸ© := hunif.2.2 in hf.ae_tendsto_limit_process hR

/-- If a martingale `f` adapted to `â„±` converges in LÂ¹ to `g`, then for all `n`, `f n` is almost
everywhere equal to `ğ”¼[g | â„± n]`. -/
lemma martingale.eq_condexp_of_tendsto_snorm {Î¼ : measure Î©}
  (hf : martingale f â„± Î¼) (hg : integrable g Î¼)
  (hgtends : tendsto (Î» n, snorm (f n - g) 1 Î¼) at_top (ğ“ 0)) (n : â„•) :
  f n =áµ[Î¼] Î¼[g | â„± n] :=
begin
  rw [â† sub_ae_eq_zero, â† snorm_eq_zero_iff ((((hf.strongly_measurable n).mono (â„±.le _)).sub
    (strongly_measurable_condexp.mono (â„±.le _))).ae_strongly_measurable) one_ne_zero],
  have ht : tendsto (Î» m, snorm (Î¼[f m - g | â„± n]) 1 Î¼) at_top (ğ“ 0),
  { have hint : âˆ€ m, integrable (f m - g) Î¼ := Î» m, (hf.integrable m).sub hg,
    exact tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds hgtends (Î» m, zero_le _)
      (Î» m, snorm_one_condexp_le_snorm _) },
  have hev : âˆ€ m â‰¥ n, snorm (Î¼[f m - g | â„± n]) 1 Î¼ = snorm (f n - Î¼[g | â„± n]) 1 Î¼,
  { refine Î» m hm, snorm_congr_ae
      ((condexp_sub (hf.integrable m) hg).trans _),
    filter_upwards [hf.2 n m hm] with x hx,
    simp only [hx, pi.sub_apply] },
  exact tendsto_nhds_unique (tendsto_at_top_of_eventually_const hev) ht,
end

/-- Part b of the **LÂ¹ martingale convergence theorem**: if `f` is a uniformly integrable martingale
adapted to the filtration `â„±`, then for all `n`, `f n` is almost everywhere equal to the conditional
expectation of its limiting process wrt. `â„± n`. -/
lemma martingale.ae_eq_condexp_limit_process
  (hf : martingale f â„± Î¼) (hbdd : uniform_integrable f 1 Î¼) (n : â„•) :
  f n =áµ[Î¼] Î¼[â„±.limit_process f Î¼ | â„± n] :=
let âŸ¨R, hRâŸ© := hbdd.2.2 in hf.eq_condexp_of_tendsto_snorm
  ((mem_â„’p_limit_process_of_snorm_bdd hbdd.1 hR).integrable le_rfl)
  (hf.submartingale.tendsto_snorm_one_limit_process hbdd) n

/-- Part c of the **LÂ¹ martingale convergnce theorem**: Given a integrable function `g` which
is measurable with respect to `â¨† n, â„± n` where `â„±` is a filtration, the martingale defined by
`ğ”¼[g | â„± n]` converges almost everywhere to `g`.

This martingale also converges to `g` in LÂ¹ and this result is provided by
`measure_theory.integrable.tendsto_snorm_condexp` -/
lemma integrable.tendsto_ae_condexp
  (hg : integrable g Î¼) (hgmeas : strongly_measurable[â¨† n, â„± n] g) :
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, Î¼[g | â„± n] x) at_top (ğ“ (g x)) :=
begin
  have hle : (â¨† n, â„± n) â‰¤ m0 := Sup_le (Î» m âŸ¨n, hnâŸ©, hn â–¸ â„±.le _),
  have hunif : uniform_integrable (Î» n, Î¼[g | â„± n]) 1 Î¼ := hg.uniform_integrable_condexp_filtration,
  obtain âŸ¨R, hRâŸ© := hunif.2.2,
  have hlimint : integrable (â„±.limit_process (Î» n, Î¼[g | â„± n]) Î¼) Î¼ :=
    (mem_â„’p_limit_process_of_snorm_bdd hunif.1 hR).integrable le_rfl,
  suffices : g =áµ[Î¼] â„±.limit_process (Î» n x, Î¼[g | â„± n] x) Î¼,
  { filter_upwards [this, (martingale_condexp g â„± Î¼).submartingale.ae_tendsto_limit_process hR]
      with x heq ht,
    rwa heq },
  have : âˆ€ n s, measurable_set[â„± n] s â†’ âˆ« x in s, g x âˆ‚Î¼ =
    âˆ« x in s, â„±.limit_process (Î» n x, Î¼[g | â„± n] x) Î¼ x âˆ‚Î¼,
  { intros n s hs,
    rw [â† set_integral_condexp (â„±.le n) hg hs, â† set_integral_condexp (â„±.le n) hlimint hs],
    refine set_integral_congr_ae (â„±.le _ _ hs) _,
    filter_upwards [(martingale_condexp g â„± Î¼).ae_eq_condexp_limit_process hunif n] with x hx _,
    rwa hx },
  refine ae_eq_of_forall_set_integral_eq_of_sigma_finite' hle
    (Î» s _ _, hg.integrable_on) (Î» s _ _, hlimint.integrable_on) (Î» s hs, _)
    hgmeas.ae_strongly_measurable' strongly_measurable_limit_process.ae_strongly_measurable',
  refine @measurable_space.induction_on_inter _ _ _ (â¨† n, â„± n)
    (measurable_space.measurable_space_supr_eq â„±) _ _ _ _ _ _ hs,
  { rintro s âŸ¨n, hsâŸ© t âŸ¨m, htâŸ© -,
    by_cases hnm : n â‰¤ m,
    { exact âŸ¨m, (â„±.mono hnm _ hs).inter htâŸ© },
    { exact âŸ¨n, hs.inter (â„±.mono (not_le.1 hnm).le _ ht)âŸ© } },
  { simp only [measure_empty, with_top.zero_lt_top, measure.restrict_empty,
      integral_zero_measure, forall_true_left] },
  { rintro t âŸ¨n, htâŸ© -,
    exact this n _ ht },
  { rintro t htmeas ht -,
    have hgeq := @integral_add_compl _ _ (â¨† n, â„± n) _ _ _ _ _ _ htmeas (hg.trim hle hgmeas),
    have hheq := @integral_add_compl _ _ (â¨† n, â„± n) _ _ _ _ _ _ htmeas
      (hlimint.trim hle strongly_measurable_limit_process),
    rw [add_comm, â† eq_sub_iff_add_eq] at hgeq hheq,
    rw [set_integral_trim hle hgmeas htmeas.compl,
      set_integral_trim hle strongly_measurable_limit_process htmeas.compl,
      hgeq, hheq, â† set_integral_trim hle hgmeas htmeas,
      â† set_integral_trim hle strongly_measurable_limit_process htmeas,
      â† integral_trim hle hgmeas, â† integral_trim hle strongly_measurable_limit_process,
      â† integral_univ, this 0 _ measurable_set.univ, integral_univ, ht (measure_lt_top _ _)] },
  { rintro f hf hfmeas heq -,
    rw [integral_Union (Î» n, hle _ (hfmeas n)) hf hg.integrable_on,
      integral_Union (Î» n, hle _ (hfmeas n)) hf hlimint.integrable_on],
    exact tsum_congr (Î» n, heq _ (measure_lt_top _ _)) }
end

/-- Part c of the **LÂ¹ martingale convergnce theorem**: Given a integrable function `g` which
is measurable with respect to `â¨† n, â„± n` where `â„±` is a filtration, the martingale defined by
`ğ”¼[g | â„± n]` converges in LÂ¹ to `g`.

This martingale also converges to `g` almost everywhere and this result is provided by
`measure_theory.integrable.tendsto_ae_condexp` -/
lemma integrable.tendsto_snorm_condexp
  (hg : integrable g Î¼) (hgmeas : strongly_measurable[â¨† n, â„± n] g) :
  tendsto (Î» n, snorm (Î¼[g | â„± n] - g) 1 Î¼) at_top (ğ“ 0) :=
tendsto_Lp_of_tendsto_in_measure _ le_rfl ennreal.one_ne_top
  (Î» n, (strongly_measurable_condexp.mono (â„±.le n)).ae_strongly_measurable)
  (mem_â„’p_one_iff_integrable.2 hg) (hg.uniform_integrable_condexp_filtration).2.1
    (tendsto_in_measure_of_tendsto_ae
    (Î» n,(strongly_measurable_condexp.mono (â„±.le n)).ae_strongly_measurable)
      (hg.tendsto_ae_condexp hgmeas))

/-- **LÃ©vy's upward theorem**, almost everywhere version: given a function `g` and a filtration
`â„±`, the sequence defined by `ğ”¼[g | â„± n]` converges almost everywhere to `ğ”¼[g | â¨† n, â„± n]`. -/
lemma tendsto_ae_condexp (g : Î© â†’ â„) :
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, Î¼[g | â„± n] x) at_top (ğ“ (Î¼[g | â¨† n, â„± n] x)) :=
begin
  have ht : âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, Î¼[Î¼[g | â¨† n, â„± n] | â„± n] x) at_top (ğ“ (Î¼[g | â¨† n, â„± n] x)) :=
    integrable_condexp.tendsto_ae_condexp strongly_measurable_condexp,
  have heq : âˆ€ n, âˆ€áµ x âˆ‚Î¼, Î¼[Î¼[g | â¨† n, â„± n] | â„± n] x = Î¼[g | â„± n] x :=
    Î» n, condexp_condexp_of_le (le_supr _ n) (supr_le (Î» n, â„±.le n)),
  rw â† ae_all_iff at heq,
  filter_upwards [heq, ht] with x hxeq hxt,
  exact hxt.congr hxeq,
end

/-- **LÃ©vy's upward theorem**, LÂ¹ version: given a function `g` and a filtration `â„±`, the
sequence defined by `ğ”¼[g | â„± n]` converges in LÂ¹ to `ğ”¼[g | â¨† n, â„± n]`. -/
lemma tendsto_snorm_condexp (g : Î© â†’ â„) :
  tendsto (Î» n, snorm (Î¼[g | â„± n] - Î¼[g | â¨† n, â„± n]) 1 Î¼) at_top (ğ“ 0) :=
begin
  have ht : tendsto (Î» n, snorm (Î¼[Î¼[g | â¨† n, â„± n] | â„± n] - Î¼[g | â¨† n, â„± n]) 1 Î¼) at_top (ğ“ 0) :=
    integrable_condexp.tendsto_snorm_condexp strongly_measurable_condexp,
  have heq : âˆ€ n, âˆ€áµ x âˆ‚Î¼, Î¼[Î¼[g | â¨† n, â„± n] | â„± n] x = Î¼[g | â„± n] x :=
    Î» n, condexp_condexp_of_le (le_supr _ n) (supr_le (Î» n, â„±.le n)),
  refine ht.congr (Î» n, snorm_congr_ae _),
  filter_upwards [heq n] with x hxeq,
  simp only [hxeq, pi.sub_apply],
end

end L1_convergence

end measure_theory
