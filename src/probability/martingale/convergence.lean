/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.martingale.upcrossing
import measure_theory.function.uniform_integrable
import measure_theory.constructions.polish

/-!

# Martingale convergence theorems

The martingale convergence theorems are a collection of theorems characterizing the convergence
of a martingale provided it satisfy some boundedness conditions. This file contains the
almost everywhere martingale convergence theorem which provides an almost everywhere limit to
an LÂ¹ bounded submartingale. It also contains the LÂ¹ martingale convergence theorem which provides
an LÂ¹ limit to a uniformly integrable submartingale.

## Main results

* `measure_theory.submartingale.exists_mem_â„’1_ae_tendsto_of_bdd`: the almost everywhere martingale
  convergence theorem: a LÂ¹-bounded submartingale adapted to the filtration `â„±` converges almost
  everywhere to an integrable function which is measurable with respect to the Ïƒ-algebra `â¨† n, â„± n`.
* `measure_theory.submartingale.exists_mem_â„’1_tendsto_snorm`: part a of the LÂ¹ martingale
  convergence theorem: a uniformly integrable submartingale adapted to the filtration `â„±` converges
  almost everywhere and in LÂ¹ to an integrable function which is measurable with respect to
  the Ïƒ-algebra `â¨† n, â„± n`.
* `measure_theory.martingale.exists_mem_â„’1_tendsto_snorm`: part b the LÂ¹ martingale convergence
  theorem: a uniformly integrable martingale `f` adapted to the filtration `â„±` converges almost
  everywhere and in LÂ¹ to an integrable function `g` which is measurable with respect to the
  Ïƒ-algebra `â¨† n, â„± n`. Furthermore, for all `n`, `f n` is almost everywhere equal to `ğ”¼[g | â„± n]`.
* `measure_theory.mem_â„’p.tendsto_ae_condexp`: part c the LÂ¹ martingale convergence theorem:
  given a `â¨† n, â„± n`-measurable function `g` where `â„±` is a filtration, `ğ”¼[g | â„± n]` converges
  almost everywhere to `g`.
* `measure_theory.mem_â„’p.tendsto_snorm_condexp`: part c the LÂ¹ martingale convergence theorem:
  given a `â¨† n, â„± n`-measurable function `g` where `â„±` is a filtration, `ğ”¼[g | â„± n]` converges in
  LÂ¹ to `g`.

-/

open topological_space filter
open_locale nnreal ennreal measure_theory probability_theory big_operators topological_space

namespace measure_theory

variables {Î± Î¹ : Type*} {m0 : measurable_space Î±} {Î¼ : measure Î±} {â„± : filtration â„• m0}
variables {a b : â„} {f : â„• â†’ Î± â†’ â„} {x : Î±} {R : â„â‰¥0}

/-!

### Almost everywhere martingale convergence theorem

We will now prove almost everywhere the martingale convergence theorem.

The a.e. martingale convergence theorem states: if `f` is a LÂ¹-bounded `â„±`-submartingale, then
it converges almost everywhere to a integrable function which is measurable with respect to
the Ïƒ-algebra `â„±âˆ := â¨† n, â„± n`.

Mathematically, we proceed by first noting that a real sequence $(x_n)$ converges if
(a) $\limsup_{n \to \infty} |x_n| < \infty$, (b) for all $a < b \in \mathbb{Q}$ we have the
number of upcrossings of $(x_n)$ from below $a$ to above $b$ is finite.
Thus, for all $x$ satisfying $\limsup_{n \to \infty} |f_n(x)| < \infty$ and the number of
upcrossings of $(f_n(x))$ from below $a$ to above $b$ is finite for all $a < b \in \mathbb{Q}$,
we have $(f_n(x))$ is convergent.

Hence, assuming $(f_n)$ is LÂ¹-bounded, using Fatou's lemma, we have
$$
  \mathbb{E] \limsup_{n \to \infty} |f_n| \le \limsup_{n \to \infty} \mathbb{E}|f_n| < \infty
$$
implying $\limsup_{n \to \infty} |f_n| < \infty$ a.e. Furthermore, by the upcrossing estimate,
the number of upcrossings is finite almost everywhere implying $f$ converges pointwise almost
everywhere.

Thus, denoting $g$ the a.e. limit of $(f_n)$, $g$ is $\mathcal{F}_\infty$-measurable as for all
$n$, $f_n$ is $\mathcal{F}_n$-measurable and $\mathcal{F}_n \le \mathcal{F}_\infty$. Finally, $g$
is integrable as $|g| \le \liminf_{n \to \infty} |f_n|$ so
$$
  \mathbb{E}|g| \le \mathbb{E} \limsup_{n \to \infty} |f_n| \le
    \limsup_{n \to \infty} \mathbb{E}|f_n| < \infty
$$
as required.

Implementation wise, a previous PR has given us `tendsto_of_no_upcrossings` which showed that
a bounded sequence converges if it does not visit below $a$ and above $b$ infinitely often
for all $a, b âˆˆ s$ for some dense set $s$. So, we may skip the first step provided we can prove
that the realizations are bounded almost everywhere. Indeed, suppose $(|f_n(x)|)$ is not bounded,
then either $f_n(x) \to \pm \infty$ or one of $\limsup f_n(x)$ or $\liminf f_n(x)$ equals
$\pm \infty$ while the other is finite. But the first case contradicts $\liminf |f_n(x)| < \infty$
while the second case contradicts finite upcrossings.

-/

/-- If a stochastic process has bounded upcrossing from below `a` to above `b`,
then it does not frequently visit both below `a` and above `b`. -/
lemma not_frequently_of_upcrossing_lt_top (hab : a < b) (hx : upcrossing a b f x â‰  âˆ) :
  Â¬((âˆƒá¶  n in at_top, f n x < a) âˆ§ (âˆƒá¶  n in at_top, b < f n x)) :=
begin
  rw [â† lt_top_iff_ne_top, upcrossing_lt_top_iff] at hx,
  replace hx : âˆƒ k, âˆ€ N, upcrossing_before a b f N x < k,
  { obtain âŸ¨k, hkâŸ© := hx,
    exact âŸ¨k + 1, Î» N, lt_of_le_of_lt (hk N) k.lt_succ_selfâŸ© },
  rintro âŸ¨hâ‚, hâ‚‚âŸ©,
  rw frequently_at_top at hâ‚ hâ‚‚,
  refine not_not.2 hx _,
  push_neg,
  intro k,
  induction k with k ih,
  { simp only [zero_le', exists_const] },
  { obtain âŸ¨N, hNâŸ© := ih,
    obtain âŸ¨Nâ‚, hNâ‚, hNâ‚'âŸ© := hâ‚ N,
    obtain âŸ¨Nâ‚‚, hNâ‚‚, hNâ‚‚'âŸ© := hâ‚‚ Nâ‚,
    exact âŸ¨(Nâ‚‚ + 1), nat.succ_le_of_lt $ lt_of_le_of_lt hN
      (upcrossing_lt_upcrossing_of_exists_upcrossing hab hNâ‚ hNâ‚' hNâ‚‚ hNâ‚‚')âŸ© }
end

/-- A stochastic process that frequently visits below `a` and above `b` have infinite
upcrossings. -/
lemma upcrossing_eq_top_of_frequently_lt (hab : a < b)
  (hâ‚ : âˆƒá¶  n in at_top, f n x < a) (hâ‚‚ : âˆƒá¶  n in at_top, b < f n x) :
  upcrossing a b f x = âˆ :=
classical.by_contradiction (Î» h, not_frequently_of_upcrossing_lt_top hab h âŸ¨hâ‚, hâ‚‚âŸ©)

lemma exists_frequently_lt_of_liminf_ne_top
  {x : â„• â†’ â„} (hx : at_top.liminf (Î» n, (âˆ¥x nâˆ¥â‚Š : â„â‰¥0âˆ)) â‰  âˆ) :
  âˆƒ R, âˆƒá¶  n in at_top, x n < R :=
begin
  by_contra h,
  simp_rw [not_exists, not_frequently, not_lt, eventually_at_top] at h,
  refine hx (ennreal.eq_top_of_forall_nnreal_le $ Î» r, _),
  obtain âŸ¨N, hNâŸ© := h r,
  exact le_Liminf_of_le (by is_bounded_default) (eventually_at_top.2
    âŸ¨N, Î» n hn, ennreal.coe_le_coe.2 $ nnreal.coe_le_coe.1 $ (hN n hn).trans (le_abs_self _)âŸ©),
end

lemma exists_frequently_lt_of_liminf_ne_top'
  {x : â„• â†’ â„} (hx : at_top.liminf (Î» n, (âˆ¥x nâˆ¥â‚Š : â„â‰¥0âˆ)) â‰  âˆ) :
  âˆƒ R, âˆƒá¶  n in at_top, R < x n :=
begin
  by_contra h,
  simp_rw [not_exists, not_frequently, not_lt, eventually_at_top] at h,
  refine hx (ennreal.eq_top_of_forall_nnreal_le $ Î» r, _),
  obtain âŸ¨N, hNâŸ© := h (-r),
  refine le_Liminf_of_le (by is_bounded_default) (eventually_at_top.2
    âŸ¨N, Î» n hn, ennreal.coe_le_coe.2 $ nnreal.coe_le_coe.1 $ (le_neg.1 $ hN n hn).trans _âŸ©),
  rw [coe_nnnorm, real.norm_eq_abs, â† abs_neg],
  exact le_abs_self _,
end

lemma exists_upcrossings_of_not_bounded_under
  (hf : at_top.liminf (Î» n, (âˆ¥f n xâˆ¥â‚Š : â„â‰¥0âˆ)) â‰  âˆ)
  (hbdd : Â¬ is_bounded_under (â‰¤) at_top (Î» n, |f n x|)) :
  âˆƒ a b : â„š, a < b âˆ§ (âˆƒá¶  n in at_top, f n x < a) âˆ§ (âˆƒá¶  n in at_top, â†‘b < f n x) :=
begin
  rw [is_bounded_under_le_abs, not_and_distrib] at hbdd,
  obtain hbdd | hbdd := hbdd,
  { obtain âŸ¨R, hRâŸ© := exists_frequently_lt_of_liminf_ne_top hf,
    obtain âŸ¨q, hqâŸ© := exists_rat_gt R,
    refine âŸ¨q, q + 1, (lt_add_iff_pos_right _).2 zero_lt_one, _, _âŸ©,
    { rw frequently_at_top at hR âŠ¢,
      intro a,
      obtain âŸ¨b, hbâ‚, hbâ‚‚âŸ© := hR a,
      exact âŸ¨b, hbâ‚, hbâ‚‚.trans hqâŸ© },
    { simp only [is_bounded_under, is_bounded, eventually_map, eventually_at_top,
        ge_iff_le, not_exists, not_forall, not_le, exists_prop] at hbdd,
      rw frequently_at_top,
      exact Î» a, let âŸ¨b, hbâ‚, hbâ‚‚âŸ© := hbdd â†‘(q + 1) a in âŸ¨b, hbâ‚, hbâ‚‚âŸ© } },
  { obtain âŸ¨R, hRâŸ© := exists_frequently_lt_of_liminf_ne_top' hf,
    obtain âŸ¨q, hqâŸ© := exists_rat_lt R,
    refine âŸ¨q - 1, q, (sub_lt_self_iff _).2 zero_lt_one, _, _âŸ©,
    { simp only [is_bounded_under, is_bounded, eventually_map, eventually_at_top,
        ge_iff_le, not_exists, not_forall, not_le, exists_prop] at hbdd,
      rw frequently_at_top,
      exact Î» a, let âŸ¨b, hbâ‚, hbâ‚‚âŸ© := hbdd â†‘(q - 1) a in âŸ¨b, hbâ‚, hbâ‚‚âŸ© },
    { rw frequently_at_top at hR âŠ¢,
      intro a,
      obtain âŸ¨b, hbâ‚, hbâ‚‚âŸ© := hR a,
      exact âŸ¨b, hbâ‚, hq.trans hbâ‚‚âŸ© } }
end

/-- A realization of a stochastic process with bounded upcrossings and bounded limit infimums is
convergent.

We use the spelling `< âˆ` instead of the standard `â‰  âˆ` in the assumptions since it is difficult
to manipulate under binders. -/
lemma tendsto_of_uncrossing_lt_top {x : Î±}
  (hfâ‚ : at_top.liminf (Î» n, (âˆ¥f n xâˆ¥â‚Š : â„â‰¥0âˆ)) < âˆ)
  (hfâ‚‚ : âˆ€ a b : â„š, a < b â†’ upcrossing a b f x < âˆ) :
  âˆƒ c, tendsto (Î» n, f n x) at_top (ğ“ c) :=
begin
  by_cases h : is_bounded_under (â‰¤) at_top (Î» n, |f n x|),
  { rw is_bounded_under_le_abs at h,
    refine tendsto_of_no_upcrossings rat.dense_range_cast _ h.1 h.2,
    { intros a ha b hb hab,
      obtain âŸ¨âŸ¨a, rflâŸ©, âŸ¨b, rflâŸ©âŸ© := âŸ¨ha, hbâŸ©,
      exact not_frequently_of_upcrossing_lt_top hab (hfâ‚‚ a b (rat.cast_lt.1 hab)).ne } },
  { obtain âŸ¨a, b, hab, hâ‚, hâ‚‚âŸ© := exists_upcrossings_of_not_bounded_under hfâ‚.ne h,
    exact false.elim ((hfâ‚‚ a b hab).ne
      (upcrossing_eq_top_of_frequently_lt (rat.cast_lt.2 hab) hâ‚ hâ‚‚)) }
end

lemma liminf_at_top_ae_bdd_of_snorm_bdd
  (hfmeas : âˆ€ n, measurable (f n)) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆ€áµ x âˆ‚Î¼, at_top.liminf (Î» n, (âˆ¥f n xâˆ¥â‚Š : â„â‰¥0âˆ)) < âˆ :=
begin
  refine ae_lt_top (measurable_liminf (Î» n, (hfmeas n).nnnorm.coe_nnreal_ennreal))
    (lt_of_le_of_lt (lintegral_liminf_le (Î» n, (hfmeas n).nnnorm.coe_nnreal_ennreal))
    (lt_of_le_of_lt _ (ennreal.coe_lt_top : â†‘R < âˆ))).ne,
  simp_rw [â† snorm_one_eq_lintegral_nnnorm, liminf_eq, eventually_at_top],
  exact Sup_le (Î» b âŸ¨a, haâŸ©, (ha a le_rfl).trans (hbdd _)),
end

variables [is_finite_measure Î¼]

/-- An LÂ¹-bounded submartingale has bounded upcrossings almost everywhere. -/
lemma submartingale.upcrossing_ae_lt_top'
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) (hab : a < b) :
  âˆ€áµ x âˆ‚Î¼, upcrossing a b f x < âˆ :=
begin
  refine ae_lt_top (hf.adapted.measurable_upcrossing hab) _,
  have := hf.mul_lintegral_upcrossing_le_lintegral_pos_part a b,
  rw [mul_comm, â† ennreal.le_div_iff_mul_le] at this,
  { refine (lt_of_le_of_lt this (ennreal.div_lt_top _ _)).ne,
    { have hR' : âˆ€ n, âˆ«â» (x : Î±), âˆ¥f n x - aâˆ¥â‚Š âˆ‚Î¼ â‰¤ R + âˆ¥aâˆ¥â‚Š * Î¼ set.univ,
      { simp_rw snorm_one_eq_lintegral_nnnorm at hbdd,
        intro n,
        refine (lintegral_mono _ : âˆ«â» x, âˆ¥f n x - aâˆ¥â‚Š âˆ‚Î¼ â‰¤ âˆ«â» x, âˆ¥f n xâˆ¥â‚Š + âˆ¥aâˆ¥â‚Š âˆ‚Î¼).trans _,
        { intro x,
          simp_rw [sub_eq_add_neg, â† nnnorm_neg a, â† ennreal.coe_add, ennreal.coe_le_coe],
          exact nnnorm_add_le _ _ },
        { simp_rw [ lintegral_add_right _ measurable_const, lintegral_const],
          exact add_le_add (hbdd _) le_rfl } },
      refine ne_of_lt (supr_lt_iff.2 âŸ¨R + âˆ¥aâˆ¥â‚Š * Î¼ set.univ, ennreal.add_lt_top.2
          âŸ¨ennreal.coe_lt_top, ennreal.mul_lt_top ennreal.coe_lt_top.ne (measure_ne_top _ _)âŸ©,
          Î» n, le_trans _ (hR' n)âŸ©),
      refine lintegral_mono (Î» x, _),
      rw [ennreal.of_real_le_iff_le_to_real, ennreal.coe_to_real, coe_nnnorm],
      by_cases hnonneg : 0 â‰¤ f n x - a,
      { rw [lattice_ordered_comm_group.pos_of_nonneg _ hnonneg,
          real.norm_eq_abs, abs_of_nonneg hnonneg] },
      { rw lattice_ordered_comm_group.pos_of_nonpos _ (not_le.1 hnonneg).le,
        exact norm_nonneg _ },
      { simp only [ne.def, ennreal.coe_ne_top, not_false_iff] } },
    { simp only [hab, ne.def, ennreal.of_real_eq_zero, sub_nonpos, not_le] } },
  { simp only [hab, ne.def, ennreal.of_real_eq_zero, sub_nonpos, not_le, true_or]},
  { simp only [ne.def, ennreal.of_real_ne_top, not_false_iff, true_or] }
end

lemma submartingale.upcrossing_ae_lt_top
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆ€áµ x âˆ‚Î¼, âˆ€ a b : â„š, a < b â†’ upcrossing a b f x < âˆ :=
begin
  simp only [ae_all_iff, eventually_imp_distrib_left],
  rintro a b hab,
  exact hf.upcrossing_ae_lt_top' hbdd (rat.cast_lt.2 hab),
end

/-- An LÂ¹-bounded submartingale converges almost everywhere. -/
lemma submartingale.exists_ae_tendsto_of_bdd
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆ€áµ x âˆ‚Î¼, âˆƒ c, tendsto (Î» n, f n x) at_top (ğ“ c) :=
begin
  filter_upwards [hf.upcrossing_ae_lt_top hbdd, liminf_at_top_ae_bdd_of_snorm_bdd
    (Î» n, (hf.strongly_measurable n).measurable.mono (â„±.le n) le_rfl) hbdd] with x hâ‚ hâ‚‚,
  exact tendsto_of_uncrossing_lt_top hâ‚‚ hâ‚,
end

lemma submartingale.exists_ae_trim_tendsto_of_bdd
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆ€áµ x âˆ‚(Î¼.trim (Sup_le (Î» m âŸ¨n, hnâŸ©, hn â–¸ â„±.le _) : (â¨† n, â„± n) â‰¤ m0)),
    âˆƒ c, tendsto (Î» n, f n x) at_top (ğ“ c) :=
begin
  rw [ae_iff, trim_measurable_set_eq],
  { exact hf.exists_ae_tendsto_of_bdd hbdd },
  { exact measurable_set.compl (@measurable_set_exists_tendsto _ _ _ _ _ _ (â¨† n, â„± n) _ _ _ _ _
    (Î» n, ((hf.strongly_measurable n).measurable.mono (le_Sup âŸ¨n, rflâŸ©) le_rfl))) }
end

/-- **Almost everywhere martingale convergence theorem**: An LÂ¹-bounded submartingale converges
almost everywhere to a LÂ¹-function which is measurable with respect to `â¨† n, â„± n`. -/
lemma submartingale.exists_mem_â„’1_ae_tendsto_of_bdd
  (hf : submartingale f â„± Î¼) (hbdd : âˆ€ n, snorm (f n) 1 Î¼ â‰¤ R) :
  âˆƒ g : Î± â†’ â„, mem_â„’p g 1 Î¼ âˆ§ strongly_measurable[â¨† n, â„± n] g âˆ§
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, f n x) at_top (ğ“ (g x)) :=
begin
  classical,
  set g' : Î± â†’ â„ := Î» x, if h : âˆƒ c, tendsto (Î» n, f n x) at_top (ğ“ c) then h.some else 0,
  have hle : (â¨† n, â„± n) â‰¤ m0 := Sup_le (Î» m âŸ¨n, hnâŸ©, hn â–¸ â„±.le _),
  have hg' : âˆ€áµ x âˆ‚(Î¼.trim hle), tendsto (Î» n, f n x) at_top (ğ“ (g' x)),
  { filter_upwards [hf.exists_ae_trim_tendsto_of_bdd hbdd] with x hx,
    simp_rw [g', dif_pos hx],
    exact hx.some_spec },
  have hg'm : @ae_strongly_measurable _ _ _ (â¨† n, â„± n) g' (Î¼.trim hle) :=
    (@ae_measurable_of_tendsto_metrizable_ae' _ _ (â¨† n, â„± n) _ _ _ _ _ _ _
      (Î» n, ((hf.strongly_measurable n).measurable.mono
      (le_Sup âŸ¨n, rflâŸ© : â„± n â‰¤ â¨† n, â„± n) le_rfl).ae_measurable) hg').ae_strongly_measurable,
  obtain âŸ¨g, hgm, haeâŸ© := hg'm,
  have hg : âˆ€áµ x âˆ‚Î¼.trim hle, tendsto (Î» n, f n x) at_top (ğ“ (g x)),
  { filter_upwards [hae, hg'] with x hx hg'x,
    exact hx â–¸ hg'x },
  refine âŸ¨g, âŸ¨(hgm.mono hle).ae_strongly_measurable, lt_of_le_of_lt (Lp.snorm_lim_le_liminf_snorm
      (Î» n, ((hf.strongly_measurable n).measurable.mono (â„±.le n) le_rfl).ae_strongly_measurable)
      g (measure_eq_zero_of_trim_eq_zero hle hg))
      (lt_of_le_of_lt _ (ennreal.coe_lt_top : â†‘R < âˆ))âŸ©,
    hgm, measure_eq_zero_of_trim_eq_zero hle hgâŸ©,
  simp_rw [liminf_eq, eventually_at_top],
  exact Sup_le (Î» b âŸ¨a, haâŸ©, (ha a le_rfl).trans (hbdd _)),
end

/-!

### LÂ¹ martingale convergence theorem

We will now prove the LÂ¹ martingale convergence theorems.

The LÂ¹ martingale convergence theorem states that:
(a) if `f` is a uniformly integrable (in the probability sense) submartingale adapted to the
  filtration `â„±`, it converges in LÂ¹ to an integrable function `g` which is measurable with
  respect to `â„±âˆ := â¨† n, â„± n` and
(b) if `f` is actually a martingale, `f n = ğ”¼[g | â„± n]` almost everywhere.
(c) Finally, if `h` is integrable and measurable with respect to `â„±âˆ`, `(ğ”¼[h | â„± n])â‚™` is a
  uniformly integrable martingale which converges to `h` almost everywhere and in LÂ¹.

The proof is quite simple. (a) follows directly from the a.e. martingale convergence theorem
and the Vitali convergence theorem. Mathematically, one first have to observe that uniform
integrability implies uniform boundedness in LÂ¹. Indeed, if
$$
  \lim_{\lambda \to \infty} \sup_{n \ge 0} \mathbb{E}(|f_n|\mathbf{1}_{\{|f_n| > \lambda\}}) = 0,
$$
then there exists some $\lambda$ such that
$\sup_{n \ge 0} \mathbb{E}(|f_n|\mathbf{1}_{\{|f_n| > \lambda\}}) \le 1$. So,
$$
  \sup_{n \ge 0} \mathbb{E}|f_n| \le
    \sup_{n \ge 0} \mathbb{E}|f_n|\mathbf{1}_{\{|f_n| \le \lambda\}} +
    \sup_{n \ge 0} \mathbb{E}|f_n|\mathbf{1}_{\{|f_n| > \lambda\}} \le
    \lambda \mu(\Omega) + 1 < \infty.
$$
However, by the very definition we used for uniform integrability in the probability sense,
uniform integrability in Lean directly requires LÂ¹ boundedness and so the above is unnecessary.

(b) follows since given $n$, we have for all $m \ge n$,
$$
  \|f_n - \mathbb{E}[g \mid \mathcal{F}_n]\|_1 =
    \|\mathbb{E}[f_m - g \mid \mathcal{F}_n]\|_1 \le \|\|f_m - g\|_1.
$$
Thus, taking $m \to \infty$ provides the almost everywhere equality.

Finally, to prove (c), we define $f_n := \mathbb{E}[h \mid \mathcal{F}_n]$. It is clear that
$(f_n)_n$ is a martingale by the tower property for conditional expectations. Furthermore,
$(f_n)_n$ is uniformly integrable in the probability sense. Indeed, as a single function is
uniformly integrable in the measure theory sense, for all $\epsilon > 0$, there exists some
$\delta > 0$ such that for all measurable set $A$ with $\mu(A) < Î´$, we have
$\mathbb{E}|h|\mathbf{1}_A < \epsilon$. So, since for sufficently large $\lambda$, by the Markov
inequality, we have for all $n$,
$$
  \mu(|f_n| \ge \lambda) \le \lambda^{-1}\mathbb{E}|f_n| \le \lambda^{-1}\mathbb|g| < \delta,
$$
we have for sufficently large $\lambda$, for all $n$,
$$
  \mathbb{E}|f_n|\mathbf{1}_{|f_n| \ge \lambda} \le
    \mathbb|g|\mathbf{1}_{|f_n| \ge \lambda} < \epsilon,
$$
implying $(f_n)_n$ is uniformly integrable. Now, to prove $f_n \to h$ almost everywhere and in
LÂ¹, it suffices to show that $h = g$ almost everywhere where $g$Â is the almost everywhere and LÂ¹
limit of $(f_n)_n$ from part (b) of the theorem. By noting that, for all $s \in \mathcal{F}_n$,
we have
$$
  \mathbb{E}g\mathbf{1}_s = \mathbb{E}[\mathbb{E}[g \mid \mathcal{F}_n]\mathbf{1}_s] =
    \mathbb{E}[\mathbb{E}[h \mid \mathcal{F}_n]\mathbf{1}_s] = \mathbb{E}h\mathbf{1}_s
$$
where $\mathbb{E}[g \mid \mathcal{F}_n = \mathbb{E}[h \mid \mathcal{F}_n]$ almost everywhere
by part (b); the equality also holds for all $s \in \mathcal{F}_\infty$ by Dynkin's theorem.
Thus, as both $h$Â and $g$ are $\mathcal{F}_\infty$-measurable, $h = g$ almost everywhere as
required.

-/

/-- Part a of the **LÂ¹ martingale convergence theorem**: a uniformly integrable submartingale
adapted to the filtration `â„±` converges a.e. and in LÂ¹ to an integrable function which is
measurable with respect to the Ïƒ-algebra `â¨† n, â„± n`. -/
lemma submartingale.exists_mem_â„’1_tendsto_snorm
  (hf : submartingale f â„± Î¼) (hunif : uniform_integrable f 1 Î¼) :
  âˆƒ g : Î± â†’ â„, mem_â„’p g 1 Î¼ âˆ§ strongly_measurable[â¨† n, â„± n] g âˆ§
  tendsto (Î» n, snorm (f n - g) 1 Î¼) at_top (ğ“ 0) âˆ§
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, f n x) at_top (ğ“ (g x)) :=
begin
  obtain âŸ¨R, hRâŸ© := hunif.2.2,
  obtain âŸ¨g, hgâ‚, hgâ‚‚, htendsâŸ© := hf.exists_mem_â„’1_ae_tendsto_of_bdd hR,
  have hmeas : âˆ€ n, ae_strongly_measurable (f n) Î¼ :=
    Î» n, ((hf.strongly_measurable n).mono (â„±.le _)).ae_strongly_measurable,
  exact âŸ¨g, hgâ‚, hgâ‚‚, tendsto_Lp_of_tendsto_in_measure _ le_rfl ennreal.one_ne_top
    hmeas hgâ‚ hunif.2.1 (tendsto_in_measure_of_tendsto_ae hmeas htends), htendsâŸ©,
end

section PRed

lemma integrable.snorm_one_condexp_le_snorm {m : measurable_space Î±}
  {f : Î± â†’ â„} (hf : integrable f Î¼) (hm : m â‰¤ m0) :
  snorm (Î¼[f | m]) 1 Î¼ â‰¤ snorm f 1 Î¼ :=
calc snorm (Î¼[f | m]) 1 Î¼ â‰¤ snorm (Î¼[|f| | m]) 1 Î¼ :
begin
  refine snorm_mono_ae _,
  filter_upwards [@condexp_mono _ m m0 _ _ _ _ _ _ _ _ hf hf.abs
      (@ae_of_all _ m0 _ Î¼ (Î» x, le_abs_self (f x) : âˆ€ x, f x â‰¤ |f x|)),
    eventually_le.trans (condexp_neg f).symm.le
      (@condexp_mono _ m m0 _ _ _ _ _ _ _ _ hf.neg hf.abs
      (@ae_of_all _ m0 _ Î¼ (Î» x, neg_le_abs_self (f x) : âˆ€ x, -f x â‰¤ |f x|)))] with x hxâ‚ hxâ‚‚,
  exact abs_le_abs hxâ‚ hxâ‚‚,
end
  ... = snorm f 1 Î¼ :
begin
  rw [snorm_one_eq_lintegral_nnnorm, snorm_one_eq_lintegral_nnnorm,
    â† ennreal.to_real_eq_to_real (ne_of_lt integrable_condexp.2) (ne_of_lt hf.2),
    â† integral_norm_eq_lintegral_nnnorm
      (strongly_measurable_condexp.mono hm).ae_strongly_measurable,
    â† integral_norm_eq_lintegral_nnnorm hf.1],
  simp_rw [real.norm_eq_abs],
  rw â† @integral_condexp _ _ _ _ _ m m0 Î¼ _ hm _ hf.abs,
  refine integral_congr_ae _,
  have : (Î» x, (0 : â„)) â‰¤áµ[Î¼] Î¼[|f| | m],
  { rw â† @condexp_const _ _ _ _ _ _ _ Î¼ hm (0 : â„),
    exact condexp_mono (integrable_zero _ _ _) hf.abs
      (@ae_of_all _ m0 _ Î¼ (Î» x, abs_nonneg (f x) : âˆ€ x, 0 â‰¤ |f x|)) },
  filter_upwards [this] with x hx,
  exact abs_eq_self.2 hx,
end

-- PR contain a more general version
lemma mem_â„’p.uniform_integrable_condexp_filtration
  {g : Î± â†’ â„} (hg : mem_â„’p g 1 Î¼) :
  uniform_integrable (Î» n, Î¼[g | â„± n]) 1 Î¼ :=
begin
  have hmeas : âˆ€ n, âˆ€ C, measurable_set {x | C â‰¤ âˆ¥Î¼[g | â„± n] xâˆ¥â‚Š} :=
    Î» n C, measurable_set_le measurable_const
      (strongly_measurable_condexp.mono (â„±.le _)).measurable.nnnorm,
  have hint : integrable g Î¼ := mem_â„’p_one_iff_integrable.1 hg,
  refine uniform_integrable_of le_rfl ennreal.one_ne_top
    (Î» n, (strongly_measurable_condexp.mono (â„±.le n)).ae_strongly_measurable) (Î» Îµ hÎµ, _),
  by_cases hne : snorm g 1 Î¼ = 0,
  { rw snorm_eq_zero_iff hg.1 one_ne_zero at hne,
    refine âŸ¨0, Î» n, (le_of_eq $ (snorm_eq_zero_iff
      ((strongly_measurable_condexp.mono (â„±.le _)).ae_strongly_measurable.indicator (hmeas n 0))
      one_ne_zero).2 _).trans (zero_le _)âŸ©,
    filter_upwards [@condexp_congr_ae _ _ _ _ _ (â„± n) m0 Î¼ _ _ hne] with x hx,
    simp only [zero_le', set.set_of_true, set.indicator_univ, pi.zero_apply, hx, condexp_zero] },
  obtain âŸ¨Î´, hÎ´, hâŸ© := hg.snorm_indicator_le Î¼ le_rfl ennreal.one_ne_top hÎµ,
  set C : â„â‰¥0 := âŸ¨Î´, hÎ´.leâŸ©â»Â¹ * (snorm g 1 Î¼).to_nnreal with hC,
  have hCpos : 0 < C :=
    mul_pos (nnreal.inv_pos.2 hÎ´) (ennreal.to_nnreal_pos hne hg.snorm_lt_top.ne),
  have : âˆ€ n, Î¼ {x : Î± | C â‰¤ âˆ¥Î¼[g | â„± n] xâˆ¥â‚Š} â‰¤ ennreal.of_real Î´,
  { intro n,
    have := mul_meas_ge_le_pow_snorm' Î¼ one_ne_zero ennreal.one_ne_top
      ((@strongly_measurable_condexp _ _ _ _ _ (â„± n) _ Î¼ g).mono
        (â„±.le n)).ae_strongly_measurable C,
    rw [ennreal.one_to_real, ennreal.rpow_one, ennreal.rpow_one, mul_comm,
      â† ennreal.le_div_iff_mul_le (or.inl (ennreal.coe_ne_zero.2 hCpos.ne.symm))
        (or.inl ennreal.coe_lt_top.ne)] at this,
    simp_rw [ennreal.coe_le_coe] at this,
    refine this.trans _,
    rw [ennreal.div_le_iff_le_mul (or.inl (ennreal.coe_ne_zero.2 hCpos.ne.symm))
        (or.inl ennreal.coe_lt_top.ne), hC, nonneg.inv_mk, ennreal.coe_mul,
      ennreal.coe_to_nnreal hg.snorm_lt_top.ne, â† mul_assoc, â† ennreal.of_real_eq_coe_nnreal,
      â† ennreal.of_real_mul hÎ´.le, mul_inv_cancel hÎ´.ne.symm, ennreal.of_real_one, one_mul],
    exact hint.snorm_one_condexp_le_snorm (â„±.le n) },
  refine âŸ¨C, Î» n, le_trans _ (h {x : Î± | C â‰¤ âˆ¥Î¼[g | â„± n] xâˆ¥â‚Š} (hmeas n C) (this n))âŸ©,
  have hmeasâ„± : measurable_set[â„± n] {x : Î± | C â‰¤ âˆ¥Î¼[g | â„± n] xâˆ¥â‚Š} :=
    @measurable_set_le _ _ _ _ _ (â„± n) _ _ _ _ _ measurable_const
      (@measurable.nnnorm _ _ _ _ _ (â„± n) _ strongly_measurable_condexp.measurable),
  rw â† snorm_congr_ae (condexp_indicator hint hmeasâ„±),
  exact (integrable.snorm_one_condexp_le_snorm (hint.indicator (hmeas n C)) (â„±.le _)),
end

end PRed

/-- If a martingale `f` adapted to `â„±` converges in LÂ¹ to `g`, then for all `n`, `f n` is almost
everywhere equal to `ğ”¼[g | â„± n]`. -/
lemma martingale.eq_condexp_lim_of_tendsto_snorm
  (hf : martingale f â„± Î¼) {g : Î± â†’ â„} (hgâ„’1 : mem_â„’p g 1 Î¼)
  (hgtends : tendsto (Î» n, snorm (f n - g) 1 Î¼) at_top (ğ“ 0)) (n : â„•) :
  f n =áµ[Î¼] Î¼[g | â„± n] :=
begin
  rw [â† sub_ae_eq_zero, â† snorm_eq_zero_iff ((((hf.strongly_measurable n).mono (â„±.le _)).sub
    (strongly_measurable_condexp.mono (â„±.le _))).ae_strongly_measurable) one_ne_zero],
  have ht : tendsto (Î» m, snorm (Î¼[f m - g | â„± n]) 1 Î¼) at_top (ğ“ 0),
  { have hint : âˆ€ m, integrable (f m - g) Î¼ := Î» m, (hf.integrable m).sub (hgâ„’1.integrable le_rfl),
    exact tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds hgtends (Î» m, zero_le _)
      (Î» m, (hint m).snorm_one_condexp_le_snorm (â„±.le _)) },
  have hev : âˆ€ m â‰¥ n, snorm (Î¼[f m - g | â„± n]) 1 Î¼ = snorm (f n - Î¼[g | â„± n]) 1 Î¼,
  { refine Î» m hm, snorm_congr_ae
      ((condexp_sub (hf.integrable m) (hgâ„’1.integrable le_rfl)).trans _),
    filter_upwards [hf.2 n m hm] with x hx,
    simp only [hx, pi.sub_apply] },
  exact tendsto_nhds_unique (tendsto_at_top_of_eventually_const hev) ht,
end

/-- Part b of the **LÂ¹ martingale convergence theorem**: a uniformly integrable martingale `f`
adapted to the filtration `â„±` converges a.e. and in LÂ¹ to some integrable function `g` which is
measurable with respect to the Ïƒ-algebra `â¨† n, â„± n`. Furthermore, for all `n`, `f n` is almost
everywhere equal to `ğ”¼[g | â„± n]`. -/
lemma martingale.exists_mem_â„’1_tendsto_snorm
  (hf : martingale f â„± Î¼) (hbdd : uniform_integrable f 1 Î¼) :
  âˆƒ g : Î± â†’ â„, mem_â„’p g 1 Î¼ âˆ§ strongly_measurable[â¨† n, â„± n] g âˆ§ (âˆ€ n, f n =áµ[Î¼] Î¼[g | â„± n]) âˆ§
  tendsto (Î» n, snorm (f n - g) 1 Î¼) at_top (ğ“ 0) âˆ§
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, f n x) at_top (ğ“ (g x)) :=
let âŸ¨g, hgâ‚, hgâ‚‚, hgâ‚ƒ, hgâ‚„âŸ© := hf.submartingale.exists_mem_â„’1_tendsto_snorm hbdd in
  âŸ¨g, hgâ‚, hgâ‚‚, Î» n, hf.eq_condexp_lim_of_tendsto_snorm hgâ‚ hgâ‚ƒ n, hgâ‚ƒ, hgâ‚„âŸ©

/-- Part c of the **LÂ¹ martingale convergnce theorem**: Given a integrable function `g` which
is measurable with respect to `â¨† n, â„± n` where `â„±` is a filtration, the martingale defined by
`ğ”¼[g | â„± n]` converges almost everywhere to `g`.

This martingale also converges to `g` in LÂ¹ and this result is provided by
`measure_theory.mem_â„’p.tendsto_snorm_condexp` -/
lemma mem_â„’p.tendsto_ae_condexp
  {g : Î± â†’ â„} (hg : mem_â„’p g 1 Î¼) (hgmeas : strongly_measurable[â¨† n, â„± n] g) :
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, Î¼[g | â„± n] x) at_top (ğ“ (g x)) :=
begin
  have hle : (â¨† n, â„± n) â‰¤ m0 := Sup_le (Î» m âŸ¨n, hnâŸ©, hn â–¸ â„±.le _),
  obtain âŸ¨h, hhâ‚, hhmeas, hhâ‚‚, hhâ‚ƒ, hhâ‚„âŸ© := (martingale_condexp g â„± Î¼).exists_mem_â„’1_tendsto_snorm
    hg.uniform_integrable_condexp_filtration,
  have hintg : integrable g Î¼ := mem_â„’p_one_iff_integrable.1 hg,
  have hinth : integrable h Î¼ := mem_â„’p_one_iff_integrable.1 hhâ‚,
  suffices : g =áµ[Î¼] h,
  { filter_upwards [this, hhâ‚„] with x heq ht,
    rwa heq },
  have : âˆ€ n, âˆ€ s, measurable_set[â„± n] s â†’ âˆ« x in s, g x âˆ‚Î¼ = âˆ« x in s, h x âˆ‚Î¼,
  { intros n s hs,
    rw [â† set_integral_condexp (â„±.le n) hintg hs, â† set_integral_condexp (â„±.le n) hinth hs],
    refine set_integral_congr_ae (â„±.le _ _ hs) _,
    filter_upwards [hhâ‚‚ n] with x hx _,
    rwa hx },
  refine ae_eq_of_forall_set_integral_eq_of_sigma_finite' hle
    (Î» s _ _, hintg.integrable_on) (Î» s _ _, hinth.integrable_on) (Î» s hs, _)
    hgmeas.ae_strongly_measurable' hhmeas.ae_strongly_measurable',
  have hgen : (â¨† n, â„± n) = measurable_space.generate_from {s : set Î± | âˆƒ n, measurable_set[â„± n] s},
  { ext s,
    rw measurable_space.measurable_set_Sup,
    simp_rw set.mem_range,
    change _ â†” measurable_space.generate_measurable {s : set Î± | âˆƒ n, measurable_set[â„± n] s} s,
    simp only [exists_prop, exists_exists_eq_and] },
  refine @measurable_space.induction_on_inter _ _ _ (â¨† n, â„± n) hgen _ _ _ _ _ _ hs,
  { rintro s âŸ¨n, hsâŸ© t âŸ¨m, htâŸ© -,
    by_cases hnm : n â‰¤ m,
    { exact âŸ¨m, (â„±.mono hnm _ hs).inter htâŸ© },
    { exact âŸ¨n, hs.inter (â„±.mono (not_le.1 hnm).le _ ht)âŸ© } },
  { simp only [measure_empty, with_top.zero_lt_top, measure.restrict_empty,
      integral_zero_measure, forall_true_left] },
  { rintro t âŸ¨n, htâŸ© -,
    exact this n _ ht },
  { rintro t htmeas ht -,
    have hgeq := @integral_add_compl _ _ (â¨† n, â„± n) _ _ _ _ _ _ htmeas (hintg.trim hle hgmeas),
    have hheq := @integral_add_compl _ _ (â¨† n, â„± n) _ _ _ _ _ _ htmeas (hinth.trim hle hhmeas),
    rw [add_comm, â† eq_sub_iff_add_eq] at hgeq hheq,
    rw [set_integral_trim hle hgmeas htmeas.compl, set_integral_trim hle hhmeas htmeas.compl,
      hgeq, hheq, â† set_integral_trim hle hgmeas htmeas, â† set_integral_trim hle hhmeas htmeas,
      â† integral_trim hle hgmeas, â† integral_trim hle hhmeas, â† integral_univ,
      this 0 _ measurable_set.univ, integral_univ, ht (measure_lt_top _ _)] },
  { rintro f hf hfmeas heq -,
    rw [integral_Union (Î» n, hle _ (hfmeas n)) hf hintg.integrable_on,
      integral_Union (Î» n, hle _ (hfmeas n)) hf hinth.integrable_on],
    exact tsum_congr (Î» n, heq _ (measure_lt_top _ _)) }
end

/-- Part c of the **LÂ¹ martingale convergnce theorem**: Given a integrable function `g` which
is measurable with respect to `â¨† n, â„± n` where `â„±` is a filtration, the martingale defined by
`ğ”¼[g | â„± n]` converges in LÂ¹ to `g`.

This martingale also converges to `g` almost everywhere and this result is provided by
`measure_theory.mem_â„’p.tendsto_ae_condexp` -/
lemma mem_â„’p.tendsto_snorm_condexp
  {g : Î± â†’ â„} (hg : mem_â„’p g 1 Î¼) (hgmeas : strongly_measurable[â¨† n, â„± n] g) :
  tendsto (Î» n, snorm (Î¼[g | â„± n] - g) 1 Î¼) at_top (ğ“ 0) :=
tendsto_Lp_of_tendsto_in_measure _ le_rfl ennreal.one_ne_top
  (Î» n, (strongly_measurable_condexp.mono (â„±.le n)).ae_strongly_measurable) hg
  hg.uniform_integrable_condexp_filtration.2.1 (tendsto_in_measure_of_tendsto_ae
    (Î» n,(strongly_measurable_condexp.mono (â„±.le n)).ae_strongly_measurable)
      (hg.tendsto_ae_condexp hgmeas))

/-- **LÃ©vy's upward theorem**, almost everywhere version: given a function `g` and a filtration
`â„±`, the sequence defined by `ğ”¼[g | â„± n]` converges almost everywhere to `ğ”¼[g | â¨† n, â„± n]`. -/
lemma mem_â„’p.tendsto_ae_condexp' {g : Î± â†’ â„} :
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, Î¼[g | â„± n] x) at_top (ğ“ (Î¼[g | â¨† n, â„± n] x)) :=
begin
  have ht : âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, Î¼[Î¼[g | â¨† n, â„± n] | â„± n] x) at_top (ğ“ (Î¼[g | â¨† n, â„± n] x)) :=
    mem_â„’p.tendsto_ae_condexp (mem_â„’p_one_iff_integrable.2 integrable_condexp)
      strongly_measurable_condexp,
  have heq : âˆ€ n, âˆ€áµ x âˆ‚Î¼, Î¼[Î¼[g | â¨† n, â„± n] | â„± n] x = Î¼[g | â„± n] x :=
    Î» n, condexp_condexp_of_le (le_supr _ n) (supr_le (Î» n, â„±.le n)),
  rw â† ae_all_iff at heq,
  filter_upwards [heq, ht] with x hxeq hxt,
  exact hxt.congr hxeq,
end

/-- **LÃ©vy's upward theorem**, LÂ¹ version: given a function `g` and a filtration `â„±`, the
sequence defined by `ğ”¼[g | â„± n]` converges in LÂ¹ to `ğ”¼[g | â¨† n, â„± n]`. -/
lemma mem_â„’p.tendsto_snorm_condexp' {g : Î± â†’ â„} :
  tendsto (Î» n, snorm (Î¼[g | â„± n] - Î¼[g | â¨† n, â„± n]) 1 Î¼) at_top (ğ“ 0) :=
begin
  have ht : tendsto (Î» n, snorm (Î¼[Î¼[g | â¨† n, â„± n] | â„± n] - Î¼[g | â¨† n, â„± n]) 1 Î¼) at_top (ğ“ 0) :=
    mem_â„’p.tendsto_snorm_condexp (mem_â„’p_one_iff_integrable.2 integrable_condexp)
      strongly_measurable_condexp,
  have heq : âˆ€ n, âˆ€áµ x âˆ‚Î¼, Î¼[Î¼[g | â¨† n, â„± n] | â„± n] x = Î¼[g | â„± n] x :=
    Î» n, condexp_condexp_of_le (le_supr _ n) (supr_le (Î» n, â„±.le n)),
  refine ht.congr (Î» n, snorm_congr_ae _),
  filter_upwards [heq n] with x hxeq,
  simp only [hxeq, pi.sub_apply],
end

end measure_theory
