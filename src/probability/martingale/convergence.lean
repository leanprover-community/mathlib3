/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.martingale.upcrossing
import measure_theory.function.uniform_integrable

/-!

# Martingale convergence theorems

The martingale convergence theorems are a collection of theorems characterizing the convergence
of a martingale provided it satisfy some boundedness conditions. In particular, we have proved the
almost everywhere martingale convergence theorem which states that, given a L¬π-bounded
submartingale adapted to the filtration `‚Ñ±`, it converges almost everywhere to an integrable
function which is measurable with respect to the œÉ-algebra `‚®Ü n, ‚Ñ± n`.

## Main results

* `measure_theory.submartingale.exists_mem_‚Ñí1_ae_tendsto_of_bdd`: a L¬π-bounded submartingale
  adapted to the filtration `‚Ñ±` converges almost everywhere to an integrable function which is
  measurable with respect to the œÉ-algebra `‚®Ü n, ‚Ñ± n`.

-/

open topological_space filter
open_locale nnreal ennreal measure_theory probability_theory big_operators topological_space

namespace measure_theory

variables {Œ± Œπ : Type*} {m0 : measurable_space Œ±} {Œº : measure Œ±} {‚Ñ± : filtration ‚Ñï m0}
variables {a b : ‚Ñù} {f : ‚Ñï ‚Üí Œ± ‚Üí ‚Ñù} {N : ‚Ñï} {n m : ‚Ñï} {x : Œ±} {R : ‚Ñù‚â•0}

/-!

### Almost everywhere martingale convergence theorem

We will now prove almost everywhere the martingale convergence theorem.

The a.e. martingale convergence theorem states: if `f` is a L¬π-bounded `‚Ñ±`-submartingale, then
it converges almost everywhere to a integrable function which is measurable with respect to
the œÉ-algebra `‚Ñ±‚àû := ‚®Ü n, ‚Ñ± n`.

Mathematically, we proceed by first noting that a real sequence `x` converges if
(a) `limsup |x| < ‚àû`,
(b) For all `a < b : ‚Ñö` we have `upcrossing a b f x < ‚àû`.
Thus, for all `x` satisfying `limsup |Œª n, f n x| < ‚àû` and for all
`a < b : ‚Ñö`, `upcrossing a b f x < ‚àû`, we have `(f n x)‚Çô` converges.

Hence, assuming `f` is L¬π-bounded, using Fatou's lemma, we have
`ùîº[limsup |f|] ‚â§ limsup ùîº[|f|] < ‚àû` implying `limsup |f| < ‚àû a.e`. Furthermore, by
the upcrossing estimate, `upcrossing a b f N < ‚àû a.e.` and so, `f` converges
pointwise almost everywhere.

Thus, denoting `g` the a.e. limit of `f`, `g` is `‚Ñ±‚àû`-measurable as for all `n`,
`f n` is `‚Ñ± n`-measurable and `‚Ñ± n ‚â§ ‚Ñ±‚àû`. Finally, `g` is also integrable as
`|g| ‚â§ liminf |f n|` so `ùîº[|g|] ‚â§ ùîº[limsup |f|] ‚â§ limsup ùîº[|f|] < ‚àû` as required.

Implementation wise, a previous PR has given us `tendsto_of_no_upcrossings` which showed that
a bounded sequence converges if it does not visit below `a` and above `b` infinitely often
for all `a, b ‚àà s` for some dense set `s`. So, we may skip the first step provided we can prove
that the realizations are bounded almost everywhere. Indeed, suppose `(|f n x|)‚Çô` is not bounded,
then either `f n x ‚Üí ¬±‚àû` or one of `limsup f n x` or `liminf f n x` equals `¬±‚àû` while the other
is finite. But the first case contradicts `liminf |f n x| < ‚àû` while the second
case contradicts finite upcrossings and so, `(|f n x|)‚Çô` is bounded if `limsup |x| < ‚àû` and
`upcrossing a b f x < ‚àû` which is precisely the assumptions we have.

-/

/-- If a stochastic process has bounded upcrossing from below `a` to above `b`,
then it does not frequently visit both below `a` and above `b`. -/
lemma not_frequently_of_upcrossing_lt_top (hab : a < b) (hx : upcrossing a b f x ‚â† ‚àû) :
  ¬¨((‚àÉ·∂† n in at_top, f n x < a) ‚àß (‚àÉ·∂† n in at_top, b < f n x)) :=
begin
  rw [‚Üê lt_top_iff_ne_top, upcrossing_lt_top_iff] at hx,
  replace hx : ‚àÉ k, ‚àÄ N, upcrossing_before a b f N x < k,
  { obtain ‚ü®k, hk‚ü© := hx,
    exact ‚ü®k + 1, Œª N, lt_of_le_of_lt (hk N) k.lt_succ_self‚ü© },
  rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
  rw frequently_at_top at h‚ÇÅ h‚ÇÇ,
  refine not_not.2 hx _,
  push_neg,
  intro k,
  induction k with k ih,
  { simp only [zero_le', exists_const] },
  { obtain ‚ü®N, hN‚ü© := ih,
    obtain ‚ü®N‚ÇÅ, hN‚ÇÅ, hN‚ÇÅ'‚ü© := h‚ÇÅ N,
    obtain ‚ü®N‚ÇÇ, hN‚ÇÇ, hN‚ÇÇ'‚ü© := h‚ÇÇ N‚ÇÅ,
    exact ‚ü®(N‚ÇÇ + 1), nat.succ_le_of_lt $ lt_of_le_of_lt hN
      (upcrossing_lt_upcrossing_of_exists_upcrossing hab hN‚ÇÅ hN‚ÇÅ' hN‚ÇÇ hN‚ÇÇ')‚ü© }
end

/-- A stochastic process that frequently visits below `a` and above `b` have infinite
upcrossings. -/
lemma upcrossing_eq_top_of_frequently_lt (hab : a < b)
  (h‚ÇÅ : ‚àÉ·∂† n in at_top, f n x < a) (h‚ÇÇ : ‚àÉ·∂† n in at_top, b < f n x) :
  upcrossing a b f x = ‚àû :=
classical.by_contradiction (Œª h, not_frequently_of_upcrossing_lt_top hab h ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©)

lemma exists_frequently_lt_of_liminf_ne_top
  {x : ‚Ñï ‚Üí ‚Ñù} (hx : at_top.liminf (Œª n, (‚à•x n‚à•‚Çä : ‚Ñù‚â•0‚àû)) ‚â† ‚àû) :
  ‚àÉ R, ‚àÉ·∂† n in at_top, x n < R :=
begin
  by_contra h,
  simp_rw [not_exists, not_frequently, not_lt, eventually_at_top] at h,
  refine hx (ennreal.eq_top_of_forall_nnreal_le $ Œª r, _),
  obtain ‚ü®N, hN‚ü© := h r,
  exact le_Liminf_of_le (by is_bounded_default) (eventually_at_top.2
    ‚ü®N, Œª n hn, ennreal.coe_le_coe.2 $ nnreal.coe_le_coe.1 $ (hN n hn).trans (le_abs_self _)‚ü©),
end

lemma exists_frequently_lt_of_liminf_ne_top'
  {x : ‚Ñï ‚Üí ‚Ñù} (hx : at_top.liminf (Œª n, (‚à•x n‚à•‚Çä : ‚Ñù‚â•0‚àû)) ‚â† ‚àû) :
  ‚àÉ R, ‚àÉ·∂† n in at_top, R < x n :=
begin
  by_contra h,
  simp_rw [not_exists, not_frequently, not_lt, eventually_at_top] at h,
  refine hx (ennreal.eq_top_of_forall_nnreal_le $ Œª r, _),
  obtain ‚ü®N, hN‚ü© := h (-r),
  refine le_Liminf_of_le (by is_bounded_default) (eventually_at_top.2
    ‚ü®N, Œª n hn, ennreal.coe_le_coe.2 $ nnreal.coe_le_coe.1 $ (le_neg.1 $ hN n hn).trans _‚ü©),
  rw [coe_nnnorm, real.norm_eq_abs, ‚Üê abs_neg],
  exact le_abs_self _,
end

lemma exists_upcrossings_of_not_bounded_under
  (hf : at_top.liminf (Œª n, (‚à•f n x‚à•‚Çä : ‚Ñù‚â•0‚àû)) ‚â† ‚àû)
  (hbdd : ¬¨ is_bounded_under (‚â§) at_top (Œª n, |f n x|)) :
  ‚àÉ a b : ‚Ñö, a < b ‚àß (‚àÉ·∂† n in at_top, f n x < a) ‚àß (‚àÉ·∂† n in at_top, ‚Üëb < f n x) :=
begin
  rw [is_bounded_under_le_abs, not_and_distrib] at hbdd,
  obtain hbdd | hbdd := hbdd,
  { obtain ‚ü®R, hR‚ü© := exists_frequently_lt_of_liminf_ne_top hf,
    obtain ‚ü®q, hq‚ü© := exists_rat_gt R,
    refine ‚ü®q, q + 1, (lt_add_iff_pos_right _).2 zero_lt_one, _, _‚ü©,
    { rw frequently_at_top at hR ‚ä¢,
      intro a,
      obtain ‚ü®b, hb‚ÇÅ, hb‚ÇÇ‚ü© := hR a,
      exact ‚ü®b, hb‚ÇÅ, hb‚ÇÇ.trans hq‚ü© },
    { simp only [is_bounded_under, is_bounded, eventually_map, eventually_at_top,
        ge_iff_le, not_exists, not_forall, not_le, exists_prop] at hbdd,
      rw frequently_at_top,
      exact Œª a, let ‚ü®b, hb‚ÇÅ, hb‚ÇÇ‚ü© := hbdd ‚Üë(q + 1) a in ‚ü®b, hb‚ÇÅ, hb‚ÇÇ‚ü© } },
  { obtain ‚ü®R, hR‚ü© := exists_frequently_lt_of_liminf_ne_top' hf,
    obtain ‚ü®q, hq‚ü© := exists_rat_lt R,
    refine ‚ü®q - 1, q, (sub_lt_self_iff _).2 zero_lt_one, _, _‚ü©,
    { simp only [is_bounded_under, is_bounded, eventually_map, eventually_at_top,
        ge_iff_le, not_exists, not_forall, not_le, exists_prop] at hbdd,
      rw frequently_at_top,
      exact Œª a, let ‚ü®b, hb‚ÇÅ, hb‚ÇÇ‚ü© := hbdd ‚Üë(q - 1) a in ‚ü®b, hb‚ÇÅ, hb‚ÇÇ‚ü© },
    { rw frequently_at_top at hR ‚ä¢,
      intro a,
      obtain ‚ü®b, hb‚ÇÅ, hb‚ÇÇ‚ü© := hR a,
      exact ‚ü®b, hb‚ÇÅ, hq.trans hb‚ÇÇ‚ü© } }
end

/-- A realization of a stochastic process with bounded upcrossings and bounded limit infimums is
convergent.

We use the spelling `< ‚àû` instead of the standard `‚â† ‚àû` in the assumptions since it is difficult
to manipulate under binders. -/
lemma tendsto_of_uncrossing_lt_top {x : Œ±}
  (hf‚ÇÅ : at_top.liminf (Œª n, (‚à•f n x‚à•‚Çä : ‚Ñù‚â•0‚àû)) < ‚àû)
  (hf‚ÇÇ : ‚àÄ a b : ‚Ñö, a < b ‚Üí upcrossing a b f x < ‚àû) :
  ‚àÉ c, tendsto (Œª n, f n x) at_top (ùìù c) :=
begin
  by_cases h : is_bounded_under (‚â§) at_top (Œª n, |f n x|),
  { rw is_bounded_under_le_abs at h,
    refine tendsto_of_no_upcrossings rat.dense_range_cast _ h.1 h.2,
    { intros a ha b hb hab,
      obtain ‚ü®‚ü®a, rfl‚ü©, ‚ü®b, rfl‚ü©‚ü© := ‚ü®ha, hb‚ü©,
      exact not_frequently_of_upcrossing_lt_top hab (hf‚ÇÇ a b (rat.cast_lt.1 hab)).ne } },
  { obtain ‚ü®a, b, hab, h‚ÇÅ, h‚ÇÇ‚ü© := exists_upcrossings_of_not_bounded_under hf‚ÇÅ.ne h,
    exact false.elim ((hf‚ÇÇ a b hab).ne
      (upcrossing_eq_top_of_frequently_lt (rat.cast_lt.2 hab) h‚ÇÅ h‚ÇÇ)) }
end

lemma liminf_at_top_ae_bdd_of_snorm_bdd
  (hfmeas : ‚àÄ n, measurable (f n)) (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) :
  ‚àÄ·µê x ‚àÇŒº, at_top.liminf (Œª n, (‚à•f n x‚à•‚Çä : ‚Ñù‚â•0‚àû)) < ‚àû :=
begin
  refine ae_lt_top (measurable_liminf (Œª n, (hfmeas n).nnnorm.coe_nnreal_ennreal))
    (lt_of_le_of_lt (lintegral_liminf_le (Œª n, (hfmeas n).nnnorm.coe_nnreal_ennreal))
    (lt_of_le_of_lt _ (ennreal.coe_lt_top : ‚ÜëR < ‚àû))).ne,
  simp_rw [‚Üê snorm_one_eq_lintegral_nnnorm, liminf_eq, eventually_at_top],
  exact Sup_le (Œª b ‚ü®a, ha‚ü©, (ha a le_rfl).trans (hbdd _)),
end

variables [is_finite_measure Œº]

/-- An L¬π-bounded submartingale has bounded upcrossings almost everywhere. -/
lemma submartingale.upcrossing_ae_lt_top'
  (hf : submartingale f ‚Ñ± Œº) (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) (hab : a < b) :
  ‚àÄ·µê x ‚àÇŒº, upcrossing a b f x < ‚àû :=
begin
  refine ae_lt_top (hf.adapted.measurable_upcrossing hab) _,
  have := hf.mul_lintegral_upcrossing_le_lintegral_pos_part a b,
  rw [mul_comm, ‚Üê ennreal.le_div_iff_mul_le] at this,
  { refine (lt_of_le_of_lt this (ennreal.div_lt_top _ _)).ne,
    { have hR' : ‚àÄ n, ‚à´‚Åª (x : Œ±), ‚à•f n x - a‚à•‚Çä ‚àÇŒº ‚â§ R + ‚à•a‚à•‚Çä * Œº set.univ,
      { simp_rw snorm_one_eq_lintegral_nnnorm at hbdd,
        intro n,
        refine (lintegral_mono _ : ‚à´‚Åª x, ‚à•f n x - a‚à•‚Çä ‚àÇŒº ‚â§ ‚à´‚Åª x, ‚à•f n x‚à•‚Çä + ‚à•a‚à•‚Çä ‚àÇŒº).trans _,
        { intro x,
          simp_rw [sub_eq_add_neg, ‚Üê nnnorm_neg a, ‚Üê ennreal.coe_add, ennreal.coe_le_coe],
          exact nnnorm_add_le _ _ },
        { simp_rw [ lintegral_add_right _ measurable_const, lintegral_const],
          exact add_le_add (hbdd _) le_rfl } },
      refine ne_of_lt (supr_lt_iff.2 ‚ü®R + ‚à•a‚à•‚Çä * Œº set.univ, ennreal.add_lt_top.2
          ‚ü®ennreal.coe_lt_top, ennreal.mul_lt_top ennreal.coe_lt_top.ne (measure_ne_top _ _)‚ü©,
          Œª n, le_trans _ (hR' n)‚ü©),
      refine lintegral_mono (Œª x, _),
      rw [ennreal.of_real_le_iff_le_to_real, ennreal.coe_to_real, coe_nnnorm],
      by_cases hnonneg : 0 ‚â§ f n x - a,
      { rw [lattice_ordered_comm_group.pos_of_nonneg _ hnonneg,
          real.norm_eq_abs, abs_of_nonneg hnonneg] },
      { rw lattice_ordered_comm_group.pos_of_nonpos _ (not_le.1 hnonneg).le,
        exact norm_nonneg _ },
      { simp only [ne.def, ennreal.coe_ne_top, not_false_iff] } },
    { simp only [hab, ne.def, ennreal.of_real_eq_zero, sub_nonpos, not_le] } },
  { simp only [hab, ne.def, ennreal.of_real_eq_zero, sub_nonpos, not_le, true_or]},
  { simp only [ne.def, ennreal.of_real_ne_top, not_false_iff, true_or] }
end

lemma submartingale.upcrossing_ae_lt_top
  (hf : submartingale f ‚Ñ± Œº) (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) :
  ‚àÄ·µê x ‚àÇŒº, ‚àÄ a b : ‚Ñö, a < b ‚Üí upcrossing a b f x < ‚àû :=
begin
  suffices : ‚àÄ a b : ‚Ñö, a < b ‚Üí ‚àÄ·µê x ‚àÇŒº, upcrossing a b f x < ‚àû,
  { simp_rw ae_iff at this ‚ä¢,
    push_neg at this ‚ä¢,
    rw set.set_of_exists,
    refine nonpos_iff_eq_zero.1 ((measure_Union_le _).trans
      (((tsum_eq_zero_iff ennreal.summable).2 (Œª a, _)).le)),
    rw set.set_of_exists,
    refine nonpos_iff_eq_zero.1 ((measure_Union_le _).trans
      (((tsum_eq_zero_iff ennreal.summable).2 (Œª b, _)).le)),
    rw set.set_of_and,
    by_cases hab : a < b,
    { simp only [hab, set.set_of_true, set.univ_inter, this a b] },
    { simp only [hab, set.set_of_false, set.empty_inter, measure_empty] } },
  rintro a b hab,
  exact hf.upcrossing_ae_lt_top' hbdd (rat.cast_lt.2 hab),
end

/-- An L¬π-bounded submartingale converges almost everywhere. -/
lemma submartingale.exists_ae_tendsto_of_bdd
  (hf : submartingale f ‚Ñ± Œº) (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) :
  ‚àÄ·µê x ‚àÇŒº, ‚àÉ c, tendsto (Œª n, f n x) at_top (ùìù c) :=
begin
  filter_upwards [hf.upcrossing_ae_lt_top hbdd, liminf_at_top_ae_bdd_of_snorm_bdd
    (Œª n, (hf.strongly_measurable n).measurable.mono (‚Ñ±.le n) le_rfl) hbdd] with x h‚ÇÅ h‚ÇÇ,
  exact tendsto_of_uncrossing_lt_top h‚ÇÇ h‚ÇÅ,
end

section PRed

lemma metric.cauchy_seq_iff'' {Œ± Œ≤ : Type*}
  [pseudo_metric_space Œ±] [nonempty Œ≤] [semilattice_sup Œ≤] {u : Œ≤ ‚Üí Œ±} :
  cauchy_seq u ‚Üî ‚àÄ K : ‚Ñï, ‚àÉ N, ‚àÄ n ‚â• N, dist (u n) (u N) < (K + 1)‚Åª¬π :=
begin
  rw metric.cauchy_seq_iff',
  refine ‚ü®Œª h K, h (K + 1)‚Åª¬π (inv_pos.2 K.cast_add_one_pos), Œª h Œµ hŒµ, _‚ü©,
  obtain ‚ü®K, hK‚ü© := exists_nat_gt Œµ‚Åª¬π,
  obtain ‚ü®N, hN‚ü© := h K,
  refine ‚ü®N, Œª n hn, lt_of_lt_of_le (hN n hn) _‚ü©,
  rw [inv_le (K.cast_add_one_pos : (0 : ‚Ñù) < K + 1) hŒµ, ‚Üê nat.cast_one, ‚Üê nat.cast_add],
  exact hK.le.trans (nat.cast_le.2 K.le_succ),
end

lemma measurable_set_exists_tendsto_at_top {Œ± Œ≤ Œπ : Type*} {m0 : measurable_space Œ±}
  [measurable_space Œ≤] [pseudo_metric_space Œ≤] [opens_measurable_space Œ≤]
  [second_countable_topology Œ≤] [complete_space Œ≤] [nonempty Œπ] [semilattice_sup Œπ] [encodable Œπ]
  {f : Œπ ‚Üí Œ± ‚Üí Œ≤} (hf : ‚àÄ i, measurable (f i)) :
  measurable_set {x | ‚àÉ c, tendsto (Œª n, f n x) at_top (ùìù c)} :=
begin
  simp_rw ‚Üê cauchy_map_iff_exists_tendsto,
  change measurable_set {x | cauchy_seq (Œª n, f n x)},
  simp_rw metric.cauchy_seq_iff'',
  rw set.set_of_forall,
  refine measurable_set.Inter (Œª K, _),
  rw set.set_of_exists,
  refine measurable_set.Union (Œª N, _),
  rw set.set_of_forall,
  refine measurable_set.Inter (Œª n, _),
  by_cases hNn : N ‚â§ n,
  { simp only [hNn, ge_iff_le, forall_true_left],
    exact measurable_set_lt (measurable.dist (hf n) (hf N)) measurable_const },
  { simp only [hNn, ge_iff_le, forall_false_left, set.set_of_true, measurable_set.univ] }
end

end PRed

lemma submartingale.exists_ae_trim_tendsto_of_bdd
  (hf : submartingale f ‚Ñ± Œº) (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) :
  ‚àÄ·µê x ‚àÇ(Œº.trim (Sup_le (Œª m ‚ü®n, hn‚ü©, hn ‚ñ∏ ‚Ñ±.le _) : (‚®Ü n, ‚Ñ± n) ‚â§ m0)),
    ‚àÉ c, tendsto (Œª n, f n x) at_top (ùìù c) :=
begin
  rw [ae_iff, trim_measurable_set_eq],
  { exact hf.exists_ae_tendsto_of_bdd hbdd },
  { exact measurable_set.compl (measurable_set_exists_tendsto_at_top (Œª n,
      ((hf.strongly_measurable n).measurable.mono (le_Sup ‚ü®n, rfl‚ü©) le_rfl))) }
end

/-- **Almost everywhere martingale convergence theorem**: An L¬π-bounded submartingale converges
almost everywhere to a L¬π-function which is measurable with respect to `‚®Ü n, ‚Ñ± n`. -/
lemma submartingale.exists_mem_‚Ñí1_ae_tendsto_of_bdd
  (hf : submartingale f ‚Ñ± Œº) (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) :
  ‚àÉ g : Œ± ‚Üí ‚Ñù, mem_‚Ñíp g 1 Œº ‚àß strongly_measurable[‚®Ü n, ‚Ñ± n] g ‚àß
  ‚àÄ·µê x ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (g x)) :=
begin
  classical,
  set g' : Œ± ‚Üí ‚Ñù := Œª x, if h : ‚àÉ c, tendsto (Œª n, f n x) at_top (ùìù c) then h.some else 0,
  have hle : (‚®Ü n, ‚Ñ± n) ‚â§ m0 := Sup_le (Œª m ‚ü®n, hn‚ü©, hn ‚ñ∏ ‚Ñ±.le _),
  have hg' : ‚àÄ·µê x ‚àÇ(Œº.trim hle), tendsto (Œª n, f n x) at_top (ùìù (g' x)),
  { filter_upwards [hf.exists_ae_trim_tendsto_of_bdd hbdd] with x hx,
    simp_rw [g', dif_pos hx],
    exact hx.some_spec },
  have hg'm : @ae_strongly_measurable _ _ _ (‚®Ü n, ‚Ñ± n) g' (Œº.trim hle) :=
    (@ae_measurable_of_tendsto_metrizable_ae' _ _ (‚®Ü n, ‚Ñ± n) _ _ _ _ _ _ _
      (Œª n, ((hf.strongly_measurable n).measurable.mono
      (le_Sup ‚ü®n, rfl‚ü© : ‚Ñ± n ‚â§ ‚®Ü n, ‚Ñ± n) le_rfl).ae_measurable) hg').ae_strongly_measurable,
  obtain ‚ü®g, hgm, hae‚ü© := hg'm,
  have hg : ‚àÄ·µê x ‚àÇŒº.trim hle, tendsto (Œª n, f n x) at_top (ùìù (g x)),
  { filter_upwards [hae, hg'] with x hx hg'x,
    exact hx ‚ñ∏ hg'x },
  refine ‚ü®g, ‚ü®(hgm.mono hle).ae_strongly_measurable, lt_of_le_of_lt (Lp.snorm_lim_le_liminf_snorm
      (Œª n, ((hf.strongly_measurable n).measurable.mono (‚Ñ±.le n) le_rfl).ae_strongly_measurable)
      g (measure_eq_zero_of_trim_eq_zero hle hg))
      (lt_of_le_of_lt _ (ennreal.coe_lt_top : ‚ÜëR < ‚àû))‚ü©,
    hgm, measure_eq_zero_of_trim_eq_zero hle hg‚ü©,
  simp_rw [liminf_eq, eventually_at_top],
  exact Sup_le (Œª b ‚ü®a, ha‚ü©, (ha a le_rfl).trans (hbdd _)),
end

/-!

### L¬π martingale convergence theorem

We will now prove the L¬π martingale convergence theorems.

The L¬π martingale convergence theorem states that:
(a) if `f` is a uniformly integrable (in the probability sense) submartingale adapted to the
  filtration `‚Ñ±`, it converges in L¬π to an integrable function `g` which is measurable with
  respect to `‚Ñ±‚àû := ‚®Ü n, ‚Ñ± n` and
(b) if `f` is actually a martingale, `f n = ùîº[g | ‚Ñ± n]` almost everywhere.
(c) Finally, if `h` is integrable and measurable with respect to `‚Ñ±‚àû`, `(ùîº[h | ‚Ñ± n])‚Çô` is a
  uniformly integrable martingale which converges to `h` almost everywhere and in L¬π.

The proof is quite simple. (a) follows directly from the a.e. martingale convergence theorem
and the Vitali convergence theorem. Mathematically, one first have to observe that uniform
integrability implies uniform boundedness in L¬π. Indeed, if
$$
  \lim_{\lambda \to \infty} \sup_{n \ge 0} \mathbb{E}(|f_n|\mathbf{1}_{\{|f_n| > \lambda\}}) = 0,
$$
then there exists some $\lambda$ such that
$\sup_{n \ge 0} \mathbb{E}(|f_n|\mathbf{1}_{\{|f_n| > \lambda\}}) \le 1$. So,
$$
  \sup_{n \ge 0} \mathbb{E}|f_n| \le
    \sup_{n \ge 0} \mathbb{E}|f_n|\mathbf{1}_{\{|f_n| \le \lambda\}} +
    \sup_{n \ge 0} \mathbb{E}|f_n|\mathbf{1}_{\{|f_n| > \lambda\}} \le
    \lambda \mu(\Omega) + 1 < \infty.
$$
However, by the very definition we used for uniform integrability in the probability sense,
uniform integrability in Lean directly requires L¬π boundedness and so the above is unnecessary.

(b) follows since give $n$, we have for all $m \ge n$,
$$
  \|f_n - \mathbb{E}[g \mid \mathcal{F}_n]\|_1 =
    \|\mathbb{E}[f_m \mid \mathcal{F}_n] - \mathbb{E}[g \mid \mathcal{F}_n]\|_1 =
    \|\mathbb{E}[f_m - g \mid \mathcal{F}_n]\|_1 \le \|f_m - g\|_1
$$
where the inequality is due to the conditional Jensen's inequality. Thus, taking $m \to \infty$
provides the almost everywhere equality.

(c) TODO.

-/

/-- Part 1 of the **L¬π martingale convergence theorem**: a uniformly integrable submartingale
adapted to the filtration `‚Ñ±` converges in L¬π to an integrable function which is measurable
with respect to the œÉ-algebra `‚®Ü n, ‚Ñ± n`. -/
lemma submartingale.exists_mem_‚Ñí1_tendsto_snorm
  (hf : submartingale f ‚Ñ± Œº) (hbdd : uniform_integrable f 1 Œº) :
  ‚àÉ g : Œ± ‚Üí ‚Ñù, mem_‚Ñíp g 1 Œº ‚àß strongly_measurable[‚®Ü n, ‚Ñ± n] g ‚àß
  tendsto (Œª n, snorm (f n - g) 1 Œº) at_top (ùìù 0) :=
begin
  obtain ‚ü®R, hR‚ü© := hbdd.2.2,
  obtain ‚ü®g, hg‚ÇÅ, hg‚ÇÇ, htends‚ü© := hf.exists_mem_‚Ñí1_ae_tendsto_of_bdd hR,
  have hmeas : ‚àÄ n, ae_strongly_measurable (f n) Œº :=
    Œª n, ((hf.strongly_measurable n).mono (‚Ñ±.le _)).ae_strongly_measurable,
  exact ‚ü®g, hg‚ÇÅ, hg‚ÇÇ, tendsto_Lp_of_tendsto_in_measure _ le_rfl ennreal.one_ne_top
    hmeas hg‚ÇÅ hbdd.2.1 (tendsto_in_measure_of_tendsto_ae hmeas htends)‚ü©,
end

end measure_theory
