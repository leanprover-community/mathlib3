/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import probability.process.hitting_time
import probability.martingale.basic

/-!

# Doob's upcrossing estimate

Given a discrete real-valued submartingale $(f_n)_{n \in \mathbb{N}}$, denoting $U_N(a, b)$ the
number of times $f_n$ crossed from below $a$ to above $b$ before time $N$, Doob's upcrossing
estimate (also known as Doob's inequality) states that
$$(b - a) \mathbb{E}[U_N(a, b)] \le \mathbb{E}[(f_N - a)^+].$$
Doob's upcrossing estimate is an important inequality and is central in proving the martingale
convergence theorems.

## Main definitions

* `measure_theory.upper_crossing_time a b f N n`: is the stopping time corresponding to `f`
  crossing above `b` the `n`-th time before time `N` (if this does not occur then the value is
  taken to be `N`).
* `measure_theory.lower_crossing_time a b f N n`: is the stopping time corresponding to `f`
  crossing below `a` the `n`-th time before time `N` (if this does not occur then the value is
  taken to be `N`).
* `measure_theory.upcrossing_strat a b f N`: is the predictable process which is 1 if `n` is
  between a consecutive pair of lower and upper crossing and is 0 otherwise. Intuitively
  one might think of the `upcrossing_strat` as the strategy of buying 1 share whenever the process
  crosses below `a` for the first time after selling and selling 1 share whenever the process
  crosses above `b` for the first time after buying.
* `measure_theory.upcrossings_before a b f N`: is the number of times `f` crosses from below `a` to
  above `b` before time `N`.
* `measure_theory.upcrossings a b f`: is the number of times `f` crosses from below `a` to above
  `b`. This takes value in `‚Ñù‚â•0‚àû` and so is allowed to be `‚àû`.

## Main results

* `measure_theory.adapted.is_stopping_time_upper_crossing_time`: `upper_crossing_time` is a
  stopping time whenever the process it is associated to is adapted.
* `measure_theory.adapted.is_stopping_time_lower_crossing_time`: `lower_crossing_time` is a
  stopping time whenever the process it is associated to is adapted.
* `measure_theory.submartingale.mul_integral_upcrossings_before_le_integral_pos_part`: Doob's
  upcrossing estimate.
* `measure_theory.submartingale.mul_lintegral_upcrossings_le_lintegral_pos_part`: the inequality
  obtained by taking the supremum on both sides of Doob's upcrossing estimate.

### References

We mostly follow the proof from [Kallenberg, *Foundations of modern probability*][kallenberg2021]

-/

open topological_space filter
open_locale nnreal ennreal measure_theory probability_theory big_operators topology

namespace measure_theory

variables {Œ© Œπ : Type*} {m0 : measurable_space Œ©} {Œº : measure Œ©}

/-!

## Proof outline

In this section, we will denote $U_N(a, b)$ the number of upcrossings of $(f_n)$ from below $a$ to
above $b$ before time $N$.

To define $U_N(a, b)$, we will construct two stopping times corresponding to when $(f_n)$ crosses
below $a$ and above $b$. Namely, we define
$$
  \sigma_n := \inf \{n \ge \tau_n \mid f_n \le a\} \wedge N;
$$
$$
  \tau_{n + 1} := \inf \{n \ge \sigma_n \mid f_n \ge b\} \wedge N.
$$
These are `lower_crossing_time` and `upper_crossing_time` in our formalization which are defined
using `measure_theory.hitting` allowing us to specify a starting and ending time.
Then, we may simply define $U_N(a, b) := \sup \{n \mid \tau_n < N\}$.

Fixing $a < b \in \mathbb{R}$, we will first prove the theorem in the special case that
$0 \le f_0$ and $a \le f_N$. In particular, we will show
$$
  (b - a) \mathbb{E}[U_N(a, b)] \le \mathbb{E}[f_N].
$$
This is `measure_theory.integral_mul_upcrossings_before_le_integral` in our formalization.

To prove this, we use the fact that given a non-negative, bounded, predictable process $(C_n)$
(i.e. $(C_{n + 1})$ is adapted), $(C \bullet f)_n := \sum_{k \le n} C_{k + 1}(f_{k + 1} - f_k)$ is
a submartingale if $(f_n)$ is.

Define $C_n := \sum_{k \le n} \mathbf{1}_{[\sigma_k, \tau_{k + 1})}(n)$. It is easy to see that
$(1 - C_n)$ is non-negative, bounded and predictable, and hence, given a submartingale $(f_n)$,
$(1 - C) \bullet f$ is also a submartingale. Thus, by the submartingale property,
$0 \le \mathbb{E}[((1 - C) \bullet f)_0] \le \mathbb{E}[((1 - C) \bullet f)_N]$ implying
$$
  \mathbb{E}[(C \bullet f)_N] \le \mathbb{E}[(1 \bullet f)_N] = \mathbb{E}[f_N] - \mathbb{E}[f_0].
$$

Furthermore,
\begin{align}
    (C \bullet f)_N & =
      \sum_{n \le N} \sum_{k \le N} \mathbf{1}_{[\sigma_k, \tau_{k + 1})}(n)(f_{n + 1} - f_n)\\
    & = \sum_{k \le N} \sum_{n \le N} \mathbf{1}_{[\sigma_k, \tau_{k + 1})}(n)(f_{n + 1} - f_n)\\
    & = \sum_{k \le N} (f_{\sigma_k + 1} - f_{\sigma_k} + f_{\sigma_k + 2} - f_{\sigma_k + 1}
      + \cdots + f_{\tau_{k + 1}} - f_{\tau_{k + 1} - 1})\\
    & = \sum_{k \le N} (f_{\tau_{k + 1}} - f_{\sigma_k})
      \ge \sum_{k < U_N(a, b)} (b - a) = (b - a) U_N(a, b)
\end{align}
where the inequality follows since for all $k < U_N(a, b)$,
$f_{\tau_{k + 1}} - f_{\sigma_k} \ge b - a$ while for all $k > U_N(a, b)$,
$f_{\tau_{k + 1}} = f_{\sigma_k} = f_N$ and
$f_{\tau_{U_N(a, b) + 1}} - f_{\sigma_{U_N(a, b)}} = f_N - a \ge 0$. Hence, we have
$$
  (b - a) \mathbb{E}[U_N(a, b)] \le \mathbb{E}[(C \bullet f)_N]
  \le \mathbb{E}[f_N] - \mathbb{E}[f_0] \le \mathbb{E}[f_N],
$$
as required.

To obtain the general case, we simply apply the above to $((f_n - a)^+)_n$.

-/

/-- `lower_crossing_time_aux a f c N` is the first time `f` reached below `a` after time `c` before
time `N`. -/
noncomputable
def lower_crossing_time_aux [preorder Œπ] [has_Inf Œπ] (a : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù) (c N : Œπ) : Œ© ‚Üí Œπ :=
hitting f (set.Iic a) c N

/-- `upper_crossing_time a b f N n` is the first time before time `N`, `f` reaches
above `b` after `f` reached below `a` for the `n - 1`-th time. -/
noncomputable
def upper_crossing_time [preorder Œπ] [order_bot Œπ] [has_Inf Œπ]
  (a b : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù) (N : Œπ) : ‚Ñï ‚Üí Œ© ‚Üí Œπ
| 0 := ‚ä•
| (n + 1) := Œª œâ, hitting f (set.Ici b)
    (lower_crossing_time_aux a f (upper_crossing_time n œâ) N œâ) N œâ

/-- `lower_crossing_time a b f N n` is the first time before time `N`, `f` reaches
below `a` after `f` reached above `b` for the `n`-th time. -/
noncomputable
def lower_crossing_time [preorder Œπ] [order_bot Œπ] [has_Inf Œπ]
  (a b : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù) (N : Œπ) (n : ‚Ñï) : Œ© ‚Üí Œπ :=
Œª œâ, hitting f (set.Iic a) (upper_crossing_time a b f N n œâ) N œâ

section

variables [preorder Œπ] [order_bot Œπ] [has_Inf Œπ]
variables {a b : ‚Ñù} {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} {N : Œπ} {n m : ‚Ñï} {œâ : Œ©}

@[simp]
lemma upper_crossing_time_zero : upper_crossing_time a b f N 0 = ‚ä• := rfl

@[simp]
lemma lower_crossing_time_zero : lower_crossing_time a b f N 0 = hitting f (set.Iic a) ‚ä• N := rfl

lemma upper_crossing_time_succ :
  upper_crossing_time a b f N (n + 1) œâ =
  hitting f (set.Ici b) (lower_crossing_time_aux a f (upper_crossing_time a b f N n œâ) N œâ) N œâ :=
by rw upper_crossing_time

lemma upper_crossing_time_succ_eq (œâ : Œ©) :
  upper_crossing_time a b f N (n + 1) œâ =
  hitting f (set.Ici b) (lower_crossing_time a b f N n œâ) N œâ :=
begin
  simp only [upper_crossing_time_succ],
  refl,
end

end

section conditionally_complete_linear_order_bot

variables [conditionally_complete_linear_order_bot Œπ]
variables {a b : ‚Ñù} {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} {N : Œπ} {n m : ‚Ñï} {œâ : Œ©}

lemma upper_crossing_time_le : upper_crossing_time a b f N n œâ ‚â§ N :=
begin
  cases n,
  { simp only [upper_crossing_time_zero, pi.bot_apply, bot_le] },
  { simp only [upper_crossing_time_succ, hitting_le] },
end

@[simp]
lemma upper_crossing_time_zero' : upper_crossing_time a b f ‚ä• n œâ = ‚ä• :=
eq_bot_iff.2 upper_crossing_time_le

lemma lower_crossing_time_le : lower_crossing_time a b f N n œâ ‚â§ N :=
by simp only [lower_crossing_time, hitting_le œâ]

lemma upper_crossing_time_le_lower_crossing_time :
  upper_crossing_time a b f N n œâ ‚â§ lower_crossing_time a b f N n œâ :=
by simp only [lower_crossing_time, le_hitting upper_crossing_time_le œâ]

lemma lower_crossing_time_le_upper_crossing_time_succ :
  lower_crossing_time a b f N n œâ ‚â§ upper_crossing_time a b f N (n + 1) œâ :=
begin
  rw upper_crossing_time_succ,
  exact le_hitting lower_crossing_time_le œâ,
end

lemma lower_crossing_time_mono (hnm : n ‚â§ m) :
  lower_crossing_time a b f N n œâ ‚â§ lower_crossing_time a b f N m œâ :=
begin
  suffices : monotone (Œª n, lower_crossing_time a b f N n œâ),
  { exact this hnm },
  exact monotone_nat_of_le_succ
    (Œª n, le_trans lower_crossing_time_le_upper_crossing_time_succ
    upper_crossing_time_le_lower_crossing_time)
end

lemma upper_crossing_time_mono (hnm : n ‚â§ m) :
  upper_crossing_time a b f N n œâ ‚â§ upper_crossing_time a b f N m œâ :=
begin
  suffices : monotone (Œª n, upper_crossing_time a b f N n œâ),
  { exact this hnm },
  exact monotone_nat_of_le_succ
    (Œª n, le_trans upper_crossing_time_le_lower_crossing_time
    lower_crossing_time_le_upper_crossing_time_succ),
end

end conditionally_complete_linear_order_bot

variables {a b : ‚Ñù} {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} {N : ‚Ñï} {n m : ‚Ñï} {œâ : Œ©}

lemma stopped_value_lower_crossing_time (h : lower_crossing_time a b f N n œâ ‚â† N) :
  stopped_value f (lower_crossing_time a b f N n) œâ ‚â§ a :=
begin
  obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© :=
    (hitting_le_iff_of_lt _ (lt_of_le_of_ne lower_crossing_time_le h)).1 le_rfl,
  exact stopped_value_hitting_mem ‚ü®j, ‚ü®hj‚ÇÅ.1, le_trans hj‚ÇÅ.2 lower_crossing_time_le‚ü©, hj‚ÇÇ‚ü©,
end

lemma stopped_value_upper_crossing_time (h : upper_crossing_time a b f N (n + 1) œâ ‚â† N) :
  b ‚â§ stopped_value f (upper_crossing_time a b f N (n + 1)) œâ :=
begin
  obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© :=
    (hitting_le_iff_of_lt _ (lt_of_le_of_ne upper_crossing_time_le h)).1 le_rfl,
  exact stopped_value_hitting_mem ‚ü®j, ‚ü®hj‚ÇÅ.1, le_trans hj‚ÇÅ.2 (hitting_le _)‚ü©, hj‚ÇÇ‚ü©,
end

lemma upper_crossing_time_lt_lower_crossing_time
  (hab : a < b) (hn : lower_crossing_time a b f N (n + 1) œâ ‚â† N) :
  upper_crossing_time a b f N (n + 1) œâ < lower_crossing_time a b f N (n + 1) œâ :=
begin
  refine lt_of_le_of_ne upper_crossing_time_le_lower_crossing_time
    (Œª h, not_le.2 hab $ le_trans _ (stopped_value_lower_crossing_time hn)),
  simp only [stopped_value],
  rw ‚Üê h,
  exact stopped_value_upper_crossing_time (h.symm ‚ñ∏ hn),
end

lemma lower_crossing_time_lt_upper_crossing_time
  (hab : a < b) (hn : upper_crossing_time a b f N (n + 1) œâ ‚â† N) :
  lower_crossing_time a b f N n œâ < upper_crossing_time a b f N (n + 1) œâ :=
begin
  refine lt_of_le_of_ne lower_crossing_time_le_upper_crossing_time_succ
    (Œª h, not_le.2 hab $ le_trans (stopped_value_upper_crossing_time hn) _),
  simp only [stopped_value],
  rw ‚Üê h,
  exact stopped_value_lower_crossing_time (h.symm ‚ñ∏ hn),
end

lemma upper_crossing_time_lt_succ (hab : a < b) (hn : upper_crossing_time a b f N (n + 1) œâ ‚â† N) :
  upper_crossing_time a b f N n œâ < upper_crossing_time a b f N (n + 1) œâ :=
lt_of_le_of_lt upper_crossing_time_le_lower_crossing_time
  (lower_crossing_time_lt_upper_crossing_time hab hn)

lemma lower_crossing_time_stabilize (hnm : n ‚â§ m) (hn : lower_crossing_time a b f N n œâ = N) :
  lower_crossing_time a b f N m œâ = N :=
le_antisymm lower_crossing_time_le (le_trans (le_of_eq hn.symm) (lower_crossing_time_mono hnm))

lemma upper_crossing_time_stabilize (hnm : n ‚â§ m) (hn : upper_crossing_time a b f N n œâ = N) :
  upper_crossing_time a b f N m œâ = N :=
le_antisymm upper_crossing_time_le (le_trans (le_of_eq hn.symm) (upper_crossing_time_mono hnm))

lemma lower_crossing_time_stabilize' (hnm : n ‚â§ m) (hn : N ‚â§ lower_crossing_time a b f N n œâ) :
  lower_crossing_time a b f N m œâ = N :=
lower_crossing_time_stabilize hnm (le_antisymm lower_crossing_time_le hn)

lemma upper_crossing_time_stabilize' (hnm : n ‚â§ m) (hn : N ‚â§ upper_crossing_time a b f N n œâ) :
  upper_crossing_time a b f N m œâ = N :=
upper_crossing_time_stabilize hnm (le_antisymm upper_crossing_time_le hn)

-- `upper_crossing_time_bound_eq` provides an explicit bound
lemma exists_upper_crossing_time_eq (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (N : ‚Ñï) (œâ : Œ©) (hab : a < b) :
  ‚àÉ n, upper_crossing_time a b f N n œâ = N :=
begin
  by_contra h, push_neg at h,
  have : strict_mono (Œª n, upper_crossing_time a b f N n œâ) :=
    strict_mono_nat_of_lt_succ (Œª n, upper_crossing_time_lt_succ hab (h _)),
  obtain ‚ü®_, ‚ü®k, rfl‚ü©, hk‚ü© :
    ‚àÉ m (hm : m ‚àà set.range (Œª n, upper_crossing_time a b f N n œâ)), N < m :=
    ‚ü®upper_crossing_time a b f N (N + 1) œâ, ‚ü®N + 1, rfl‚ü©,
      lt_of_lt_of_le (N.lt_succ_self) (strict_mono.id_le this (N + 1))‚ü©,
  exact not_le.2 hk upper_crossing_time_le
end

lemma upper_crossing_time_lt_bdd_above (hab : a < b) :
  bdd_above {n | upper_crossing_time a b f N n œâ < N} :=
begin
  obtain ‚ü®k, hk‚ü© := exists_upper_crossing_time_eq f N œâ hab,
  refine ‚ü®k, Œª n (hn : upper_crossing_time a b f N n œâ < N), _‚ü©,
  by_contra hn',
  exact hn.ne (upper_crossing_time_stabilize (not_le.1 hn').le hk)
end

lemma upper_crossing_time_lt_nonempty (hN : 0 < N) :
  {n | upper_crossing_time a b f N n œâ < N}.nonempty :=
‚ü®0, hN‚ü©

lemma upper_crossing_time_bound_eq (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (N : ‚Ñï) (œâ : Œ©) (hab : a < b) :
  upper_crossing_time a b f N N œâ = N :=
begin
  by_cases hN' : N < nat.find (exists_upper_crossing_time_eq f N œâ hab),
  { refine le_antisymm upper_crossing_time_le _,
    have hmono : strict_mono_on (Œª n, upper_crossing_time a b f N n œâ)
      (set.Iic (nat.find (exists_upper_crossing_time_eq f N œâ hab)).pred),
    { refine strict_mono_on_Iic_of_lt_succ (Œª m hm, upper_crossing_time_lt_succ hab _),
      rw nat.lt_pred_iff at hm,
      convert nat.find_min _ hm },
    convert strict_mono_on.Iic_id_le hmono N (nat.le_pred_of_lt hN') },
  { rw not_lt at hN',
    exact upper_crossing_time_stabilize hN'
      (nat.find_spec (exists_upper_crossing_time_eq f N œâ hab)) }
end

lemma upper_crossing_time_eq_of_bound_le (hab : a < b) (hn : N ‚â§ n) :
  upper_crossing_time a b f N n œâ = N :=
le_antisymm upper_crossing_time_le
  ((le_trans (upper_crossing_time_bound_eq f N œâ hab).symm.le (upper_crossing_time_mono hn)))

variables {‚Ñ± : filtration ‚Ñï m0}

lemma adapted.is_stopping_time_crossing (hf : adapted ‚Ñ± f) :
  is_stopping_time ‚Ñ± (upper_crossing_time a b f N n) ‚àß
  is_stopping_time ‚Ñ± (lower_crossing_time a b f N n) :=
begin
  induction n with k ih,
  { refine ‚ü®is_stopping_time_const _ 0, _‚ü©,
    simp [hitting_is_stopping_time hf measurable_set_Iic] },
  { obtain ‚ü®ih‚ÇÅ, ih‚ÇÇ‚ü© := ih,
    have : is_stopping_time ‚Ñ± (upper_crossing_time a b f N (k + 1)),
    { intro n,
      simp_rw upper_crossing_time_succ_eq,
      exact is_stopping_time_hitting_is_stopping_time ih‚ÇÇ (Œª _, lower_crossing_time_le)
        measurable_set_Ici hf _ },
    refine ‚ü®this, _‚ü©,
    { intro n,
      exact is_stopping_time_hitting_is_stopping_time this (Œª _, upper_crossing_time_le)
        measurable_set_Iic hf _ } }
end

lemma adapted.is_stopping_time_upper_crossing_time (hf : adapted ‚Ñ± f) :
  is_stopping_time ‚Ñ± (upper_crossing_time a b f N n) :=
hf.is_stopping_time_crossing.1

lemma adapted.is_stopping_time_lower_crossing_time (hf : adapted ‚Ñ± f) :
  is_stopping_time ‚Ñ± (lower_crossing_time a b f N n) :=
hf.is_stopping_time_crossing.2

/-- `upcrossing_strat a b f N n` is 1 if `n` is between a consecutive pair of lower and upper
crossings and is 0 otherwise. `upcrossing_strat` is shifted by one index so that it is adapted
rather than predictable. -/
noncomputable
def upcrossing_strat (a b : ‚Ñù) (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (N n : ‚Ñï) (œâ : Œ©) : ‚Ñù :=
‚àë k in finset.range N,
  (set.Ico (lower_crossing_time a b f N k œâ) (upper_crossing_time a b f N (k + 1) œâ)).indicator 1 n

lemma upcrossing_strat_nonneg : 0 ‚â§ upcrossing_strat a b f N n œâ :=
finset.sum_nonneg (Œª i hi, set.indicator_nonneg (Œª œâ hœâ, zero_le_one) _)

lemma upcrossing_strat_le_one : upcrossing_strat a b f N n œâ ‚â§ 1 :=
begin
  rw [upcrossing_strat, ‚Üê set.indicator_finset_bUnion_apply],
  { exact set.indicator_le_self' (Œª _ _, zero_le_one) _ },
  { intros i hi j hj hij,
    rw set.Ico_disjoint_Ico,
    obtain (hij' | hij') := lt_or_gt_of_ne hij,
    { rw [min_eq_left ((upper_crossing_time_mono (nat.succ_le_succ hij'.le)) :
          upper_crossing_time a b f N _ œâ ‚â§ upper_crossing_time a b f N _ œâ),
          max_eq_right (lower_crossing_time_mono hij'.le :
          lower_crossing_time a b f N _ _ ‚â§ lower_crossing_time _ _ _ _ _ _)],
      refine le_trans upper_crossing_time_le_lower_crossing_time (lower_crossing_time_mono
        (nat.succ_le_of_lt hij')) },
    { rw gt_iff_lt at hij',
      rw [min_eq_right ((upper_crossing_time_mono (nat.succ_le_succ hij'.le)) :
          upper_crossing_time a b f N _ œâ ‚â§ upper_crossing_time a b f N _ œâ),
          max_eq_left (lower_crossing_time_mono hij'.le :
          lower_crossing_time a b f N _ _ ‚â§ lower_crossing_time _ _ _ _ _ _)],
      refine le_trans upper_crossing_time_le_lower_crossing_time
        (lower_crossing_time_mono (nat.succ_le_of_lt hij')) } }
end

lemma adapted.upcrossing_strat_adapted (hf : adapted ‚Ñ± f) :
  adapted ‚Ñ± (upcrossing_strat a b f N) :=
begin
  intro n,
  change strongly_measurable[‚Ñ± n] (Œª œâ, ‚àë k in finset.range N,
    ({n | lower_crossing_time a b f N k œâ ‚â§ n} ‚à©
     {n | n < upper_crossing_time a b f N (k + 1) œâ}).indicator 1 n),
  refine finset.strongly_measurable_sum _ (Œª i hi,
    strongly_measurable_const.indicator ((hf.is_stopping_time_lower_crossing_time n).inter _)),
  simp_rw ‚Üê not_le,
  exact (hf.is_stopping_time_upper_crossing_time n).compl,
end

lemma submartingale.sum_upcrossing_strat_mul [is_finite_measure Œº] (hf : submartingale f ‚Ñ± Œº)
  (a b : ‚Ñù) (N : ‚Ñï) :
  submartingale
    (Œª n : ‚Ñï, ‚àë k in finset.range n, upcrossing_strat a b f N k * (f (k + 1) - f k)) ‚Ñ± Œº :=
hf.sum_mul_sub hf.adapted.upcrossing_strat_adapted
  (Œª _ _, upcrossing_strat_le_one) (Œª _ _, upcrossing_strat_nonneg)

lemma submartingale.sum_sub_upcrossing_strat_mul [is_finite_measure Œº] (hf : submartingale f ‚Ñ± Œº)
  (a b : ‚Ñù) (N : ‚Ñï) :
  submartingale
    (Œª n : ‚Ñï, ‚àë k in finset.range n, (1 - upcrossing_strat a b f N k) * (f (k + 1) - f k)) ‚Ñ± Œº :=
begin
  refine hf.sum_mul_sub (Œª n, (adapted_const ‚Ñ± 1 n).sub (hf.adapted.upcrossing_strat_adapted n))
    (_ : ‚àÄ n œâ, (1 - upcrossing_strat a b f N n) œâ ‚â§ 1) _,
  { exact Œª n œâ, sub_le_self _ upcrossing_strat_nonneg },
  { intros n œâ,
    simp [upcrossing_strat_le_one] }
end

lemma submartingale.sum_mul_upcrossing_strat_le [is_finite_measure Œº] (hf : submartingale f ‚Ñ± Œº) :
  Œº[‚àë k in finset.range n, upcrossing_strat a b f N k * (f (k + 1) - f k)] ‚â§
  Œº[f n] - Œº[f 0] :=
begin
  have h‚ÇÅ : (0 : ‚Ñù) ‚â§
    Œº[‚àë k in finset.range n, (1 - upcrossing_strat a b f N k) * (f (k + 1) - f k)],
  { have := (hf.sum_sub_upcrossing_strat_mul a b N).set_integral_le (zero_le n) measurable_set.univ,
    rw [integral_univ, integral_univ] at this,
    refine le_trans _ this,
    simp only [finset.range_zero, finset.sum_empty, integral_zero'] },
  have h‚ÇÇ : Œº[‚àë k in finset.range n, (1 - upcrossing_strat a b f N k) * (f (k + 1) - f k)] =
    Œº[‚àë k in finset.range n, (f (k + 1) - f k)] -
    Œº[‚àë k in finset.range n, upcrossing_strat a b f N k * (f (k + 1) - f k)],
  { simp only [sub_mul, one_mul, finset.sum_sub_distrib, pi.sub_apply,
      finset.sum_apply, pi.mul_apply],
    refine integral_sub (integrable.sub (integrable_finset_sum _ (Œª i hi, hf.integrable _))
      (integrable_finset_sum _ (Œª i hi, hf.integrable _))) _,
    convert (hf.sum_upcrossing_strat_mul a b N).integrable n,
    ext, simp },
  rw [h‚ÇÇ, sub_nonneg] at h‚ÇÅ,
  refine le_trans h‚ÇÅ _,
  simp_rw [finset.sum_range_sub, integral_sub' (hf.integrable _) (hf.integrable _)],
end

/-- The number of upcrossings (strictly) before time `N`. -/
noncomputable
def upcrossings_before [preorder Œπ] [order_bot Œπ] [has_Inf Œπ]
  (a b : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù) (N : Œπ) (œâ : Œ©) : ‚Ñï :=
Sup {n | upper_crossing_time a b f N n œâ < N}

@[simp]
lemma upcrossings_before_bot [preorder Œπ] [order_bot Œπ] [has_Inf Œπ]
  {a b : ‚Ñù} {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} {œâ : Œ©} :
  upcrossings_before a b f ‚ä• œâ = ‚ä• :=
by simp [upcrossings_before]

lemma upcrossings_before_zero :
  upcrossings_before a b f 0 œâ = 0 :=
by simp [upcrossings_before]

@[simp] lemma upcrossings_before_zero' :
  upcrossings_before a b f 0 = 0 :=
by { ext œâ, exact upcrossings_before_zero }

lemma upper_crossing_time_lt_of_le_upcrossings_before
  (hN : 0 < N) (hab : a < b) (hn : n ‚â§ upcrossings_before a b f N œâ) :
  upper_crossing_time a b f N n œâ < N :=
begin
  have : upper_crossing_time a b f N (upcrossings_before a b f N œâ) œâ < N :=
    (upper_crossing_time_lt_nonempty hN).cSup_mem
    ((order_bot.bdd_below _).finite_of_bdd_above (upper_crossing_time_lt_bdd_above hab)),
  exact lt_of_le_of_lt (upper_crossing_time_mono hn) this,
end

lemma upper_crossing_time_eq_of_upcrossings_before_lt
  (hab : a < b) (hn : upcrossings_before a b f N œâ < n) :
  upper_crossing_time a b f N n œâ = N :=
begin
  refine le_antisymm upper_crossing_time_le (not_lt.1 _),
  convert not_mem_of_cSup_lt hn (upper_crossing_time_lt_bdd_above hab),
end

lemma upcrossings_before_le (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (œâ : Œ©) (hab : a < b) :
  upcrossings_before a b f N œâ ‚â§ N :=
begin
  by_cases hN : N = 0,
  { subst hN,
    rw upcrossings_before_zero },
  { refine cSup_le ‚ü®0, zero_lt_iff.2 hN‚ü© (Œª n (hn : _ < _), _),
    by_contra hnN,
    exact hn.ne (upper_crossing_time_eq_of_bound_le hab (not_le.1 hnN).le) },
end

lemma crossing_eq_crossing_of_lower_crossing_time_lt {M : ‚Ñï} (hNM : N ‚â§ M)
  (h : lower_crossing_time a b f N n œâ < N) :
  upper_crossing_time a b f M n œâ = upper_crossing_time a b f N n œâ ‚àß
  lower_crossing_time a b f M n œâ = lower_crossing_time a b f N n œâ :=
begin
  have h' : upper_crossing_time a b f N n œâ < N :=
    lt_of_le_of_lt upper_crossing_time_le_lower_crossing_time h,
  induction n with k ih,
  { simp only [nat.nat_zero_eq_zero, upper_crossing_time_zero, bot_eq_zero', eq_self_iff_true,
      lower_crossing_time_zero, true_and, eq_comm],
    refine hitting_eq_hitting_of_exists hNM _,
    simp only [lower_crossing_time, hitting_lt_iff] at h,
    obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := h,
    exact ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü© },
  { specialize ih (lt_of_le_of_lt (lower_crossing_time_mono (nat.le_succ _)) h)
      (lt_of_le_of_lt (upper_crossing_time_mono (nat.le_succ _)) h'),
    have : upper_crossing_time a b f M k.succ œâ = upper_crossing_time a b f N k.succ œâ,
    { simp only [upper_crossing_time_succ_eq, hitting_lt_iff] at h' ‚ä¢,
      obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := h',
      rw [eq_comm, ih.2],
      exact hitting_eq_hitting_of_exists hNM ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü© },
    refine ‚ü®this, _‚ü©,
    simp only [lower_crossing_time, eq_comm, this],
    refine hitting_eq_hitting_of_exists hNM _,
    rw [lower_crossing_time, hitting_lt_iff _ le_rfl] at h,
    swap, { apply_instance },
    obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := h,
    exact ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü© }
end

lemma crossing_eq_crossing_of_upper_crossing_time_lt {M : ‚Ñï} (hNM : N ‚â§ M)
  (h : upper_crossing_time a b f N (n + 1) œâ < N) :
  upper_crossing_time a b f M (n + 1) œâ = upper_crossing_time a b f N (n + 1) œâ ‚àß
  lower_crossing_time a b f M n œâ = lower_crossing_time a b f N n œâ :=
begin
  have := (crossing_eq_crossing_of_lower_crossing_time_lt hNM
    (lt_of_le_of_lt lower_crossing_time_le_upper_crossing_time_succ h)).2,
  refine ‚ü®_, this‚ü©,
  rw [upper_crossing_time_succ_eq, upper_crossing_time_succ_eq, eq_comm, this],
  refine hitting_eq_hitting_of_exists hNM _,
  simp only [upper_crossing_time_succ_eq, hitting_lt_iff] at h,
  obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := h,
  exact ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü©
end

lemma upper_crossing_time_eq_upper_crossing_time_of_lt {M : ‚Ñï} (hNM : N ‚â§ M)
  (h : upper_crossing_time a b f N n œâ < N) :
  upper_crossing_time a b f M n œâ = upper_crossing_time a b f N n œâ :=
begin
  cases n,
  { simp },
  { exact (crossing_eq_crossing_of_upper_crossing_time_lt hNM h).1 }
end

lemma upcrossings_before_mono (hab : a < b) :
  monotone (Œª N œâ, upcrossings_before a b f N œâ) :=
begin
  intros N M hNM œâ,
  simp only [upcrossings_before],
  by_cases hemp : {n : ‚Ñï | upper_crossing_time a b f N n œâ < N}.nonempty,
  { refine cSup_le_cSup (upper_crossing_time_lt_bdd_above hab) hemp (Œª n hn, _),
    rw [set.mem_set_of_eq, upper_crossing_time_eq_upper_crossing_time_of_lt hNM hn],
    exact lt_of_lt_of_le hn hNM },
  { rw set.not_nonempty_iff_eq_empty at hemp,
    simp [hemp, cSup_empty, bot_eq_zero', zero_le'] }
end

lemma upcrossings_before_lt_of_exists_upcrossing (hab : a < b) {N‚ÇÅ N‚ÇÇ : ‚Ñï}
  (hN‚ÇÅ: N ‚â§ N‚ÇÅ) (hN‚ÇÅ': f N‚ÇÅ œâ < a) (hN‚ÇÇ: N‚ÇÅ ‚â§ N‚ÇÇ) (hN‚ÇÇ': b < f N‚ÇÇ œâ) :
  upcrossings_before a b f N œâ < upcrossings_before a b f (N‚ÇÇ + 1) œâ :=
begin
  refine lt_of_lt_of_le (nat.lt_succ_self _) (le_cSup (upper_crossing_time_lt_bdd_above hab) _),
  rw [set.mem_set_of_eq, upper_crossing_time_succ_eq, hitting_lt_iff _ le_rfl],
  swap,
  { apply_instance },
  { refine ‚ü®N‚ÇÇ, ‚ü®_, nat.lt_succ_self _‚ü©, hN‚ÇÇ'.le‚ü©,
    rw [lower_crossing_time, hitting_le_iff_of_lt _ (nat.lt_succ_self _)],
    refine ‚ü®N‚ÇÅ, ‚ü®le_trans _ hN‚ÇÅ, hN‚ÇÇ‚ü©, hN‚ÇÅ'.le‚ü©,
    by_cases hN : 0 < N,
    { have : upper_crossing_time a b f N (upcrossings_before a b f N œâ) œâ < N :=
        nat.Sup_mem (upper_crossing_time_lt_nonempty hN) (upper_crossing_time_lt_bdd_above hab),
      rw upper_crossing_time_eq_upper_crossing_time_of_lt
        (hN‚ÇÅ.trans (hN‚ÇÇ.trans $ nat.le_succ _)) this,
      exact this.le },
    { rw [not_lt, le_zero_iff] at hN,
      rw [hN, upcrossings_before_zero, upper_crossing_time_zero],
      refl } },
end

lemma lower_crossing_time_lt_of_lt_upcrossings_before
  (hN : 0 < N) (hab : a < b) (hn : n < upcrossings_before a b f N œâ) :
  lower_crossing_time a b f N n œâ < N :=
lt_of_le_of_lt lower_crossing_time_le_upper_crossing_time_succ
  (upper_crossing_time_lt_of_le_upcrossings_before hN hab hn)

lemma le_sub_of_le_upcrossings_before
  (hN : 0 < N) (hab : a < b) (hn : n < upcrossings_before a b f N œâ) :
  b - a ‚â§
  stopped_value f (upper_crossing_time a b f N (n + 1)) œâ -
  stopped_value f (lower_crossing_time a b f N n) œâ :=
sub_le_sub (stopped_value_upper_crossing_time
  (upper_crossing_time_lt_of_le_upcrossings_before hN hab hn).ne)
  (stopped_value_lower_crossing_time (lower_crossing_time_lt_of_lt_upcrossings_before hN hab hn).ne)

lemma sub_eq_zero_of_upcrossings_before_lt (hab : a < b) (hn : upcrossings_before a b f N œâ < n) :
  stopped_value f (upper_crossing_time a b f N (n + 1)) œâ -
  stopped_value f (lower_crossing_time a b f N n) œâ = 0 :=
begin
  have : N ‚â§ upper_crossing_time a b f N n œâ,
  { rw upcrossings_before at hn,
    rw ‚Üê not_lt,
    exact Œª h, not_le.2 hn (le_cSup (upper_crossing_time_lt_bdd_above hab) h) },
  simp [stopped_value, upper_crossing_time_stabilize' (nat.le_succ n) this,
    lower_crossing_time_stabilize' le_rfl
      (le_trans this upper_crossing_time_le_lower_crossing_time)]
end

lemma mul_upcrossings_before_le (hf : a ‚â§ f N œâ) (hab : a < b) :
  (b - a) * upcrossings_before a b f N œâ ‚â§
  ‚àë k in finset.range N, upcrossing_strat a b f N k œâ * (f (k + 1) - f k) œâ :=
begin
  classical,
  by_cases hN : N = 0,
  { simp [hN] },
  simp_rw [upcrossing_strat, finset.sum_mul, ‚Üê set.indicator_mul_left, pi.one_apply,
    pi.sub_apply, one_mul],
  rw finset.sum_comm,
  have h‚ÇÅ : ‚àÄ k, ‚àë n in finset.range N,
    (set.Ico (lower_crossing_time a b f N k œâ) (upper_crossing_time a b f N (k + 1) œâ)).indicator
    (Œª m, f (m + 1) œâ - f m œâ) n =
    stopped_value f (upper_crossing_time a b f N (k + 1)) œâ -
    stopped_value f (lower_crossing_time a b f N k) œâ,
  { intro k,
    rw [finset.sum_indicator_eq_sum_filter, (_ : (finset.filter
      (Œª i, i ‚àà set.Ico (lower_crossing_time a b f N k œâ) (upper_crossing_time a b f N (k + 1) œâ))
      (finset.range N)) =
      finset.Ico (lower_crossing_time a b f N k œâ) (upper_crossing_time a b f N (k + 1) œâ)),
      finset.sum_Ico_eq_add_neg _ lower_crossing_time_le_upper_crossing_time_succ,
      finset.sum_range_sub (Œª n, f n œâ), finset.sum_range_sub (Œª n, f n œâ), neg_sub,
      sub_add_sub_cancel],
    { refl },
    { ext i,
      simp only [set.mem_Ico, finset.mem_filter, finset.mem_range, finset.mem_Ico,
        and_iff_right_iff_imp, and_imp],
      exact Œª _ h, lt_of_lt_of_le h upper_crossing_time_le } },
  simp_rw [h‚ÇÅ],
  have h‚ÇÇ : ‚àë k in finset.range (upcrossings_before a b f N œâ), (b - a) ‚â§
    ‚àë k in finset.range N,
    (stopped_value f (upper_crossing_time a b f N (k + 1)) œâ -
    stopped_value f (lower_crossing_time a b f N k) œâ),
  { calc ‚àë k in finset.range (upcrossings_before a b f N œâ), (b - a)
       ‚â§ ‚àë k in finset.range (upcrossings_before a b f N œâ),
          (stopped_value f (upper_crossing_time a b f N (k + 1)) œâ -
           stopped_value f (lower_crossing_time a b f N k) œâ) :
    begin
      refine finset.sum_le_sum (Œª i hi, le_sub_of_le_upcrossings_before (zero_lt_iff.2 hN) hab _),
      rwa finset.mem_range at hi,
    end
    ...‚â§ ‚àë k in finset.range N,
          (stopped_value f (upper_crossing_time a b f N (k + 1)) œâ -
           stopped_value f (lower_crossing_time a b f N k) œâ) :
    begin
      refine finset.sum_le_sum_of_subset_of_nonneg
        (finset.range_subset.2 (upcrossings_before_le f œâ hab)) (Œª i _ hi, _),
      by_cases hi' : i = upcrossings_before a b f N œâ,
      { subst hi',
        simp only [stopped_value],
        rw upper_crossing_time_eq_of_upcrossings_before_lt hab (nat.lt_succ_self _),
        by_cases heq : lower_crossing_time a b f N (upcrossings_before a b f N œâ) œâ = N,
        { rw [heq, sub_self] },
        { rw sub_nonneg,
          exact le_trans (stopped_value_lower_crossing_time heq) hf } },
      { rw sub_eq_zero_of_upcrossings_before_lt hab,
        rw [finset.mem_range, not_lt] at hi,
        exact lt_of_le_of_ne hi (ne.symm hi') },
    end },
  refine le_trans _ h‚ÇÇ,
  rw [finset.sum_const, finset.card_range, nsmul_eq_mul, mul_comm],
end

lemma integral_mul_upcrossings_before_le_integral [is_finite_measure Œº]
  (hf : submartingale f ‚Ñ± Œº) (hfN : ‚àÄ œâ, a ‚â§ f N œâ) (hfzero : 0 ‚â§ f 0)  (hab : a < b) :
  (b - a) * Œº[upcrossings_before a b f N] ‚â§ Œº[f N] :=
calc (b - a) * Œº[upcrossings_before a b f N]
    ‚â§ Œº[‚àë k in finset.range N, upcrossing_strat a b f N k * (f (k + 1) - f k)] :
begin
  rw ‚Üê integral_mul_left,
  refine integral_mono_of_nonneg _ ((hf.sum_upcrossing_strat_mul a b N).integrable N) _,
  { exact eventually_of_forall (Œª œâ, mul_nonneg (sub_nonneg.2 hab.le) (nat.cast_nonneg _)) },
  { refine eventually_of_forall (Œª œâ, _),
    simpa using mul_upcrossings_before_le (hfN œâ) hab },
end
  ...‚â§ Œº[f N] - Œº[f 0] : hf.sum_mul_upcrossing_strat_le
  ...‚â§ Œº[f N] : (sub_le_self_iff _).2 (integral_nonneg hfzero)

lemma crossing_pos_eq (hab : a < b) :
  upper_crossing_time 0 (b - a) (Œª n œâ, (f n œâ - a)‚Å∫) N n = upper_crossing_time a b f N n ‚àß
  lower_crossing_time 0 (b - a) (Œª n œâ, (f n œâ - a)‚Å∫) N n = lower_crossing_time a b f N n :=
begin
  have hab' : 0 < b - a := sub_pos.2 hab,
  have hf : ‚àÄ œâ i, b - a ‚â§ (f i œâ - a)‚Å∫ ‚Üî b ‚â§ f i œâ,
  { intros i œâ,
    refine ‚ü®Œª h, _, Œª h, _‚ü©,
    { rwa [‚Üê sub_le_sub_iff_right a,
        ‚Üê lattice_ordered_comm_group.pos_eq_self_of_pos_pos (lt_of_lt_of_le hab' h)] },
    { rw ‚Üê sub_le_sub_iff_right a at h,
      rwa lattice_ordered_comm_group.pos_of_nonneg _ (le_trans hab'.le h) } },
  have hf' : ‚àÄ œâ i, (f i œâ - a)‚Å∫ ‚â§ 0 ‚Üî f i œâ ‚â§ a,
  { intros œâ i,
    rw [lattice_ordered_comm_group.pos_nonpos_iff, sub_nonpos] },
  induction n with k ih,
  { refine ‚ü®rfl, _‚ü©,
    simp only [lower_crossing_time_zero, hitting, set.mem_Icc, set.mem_Iic],
    ext œâ,
    split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ,
    { simp_rw [hf'] },
    { simp_rw [set.mem_Iic, ‚Üê hf' _ _] at h‚ÇÇ,
      exact false.elim (h‚ÇÇ h‚ÇÅ) },
    { simp_rw [set.mem_Iic, hf' _ _] at h‚ÇÅ,
      exact false.elim (h‚ÇÅ h‚ÇÇ) },
    { refl } },
  { have : upper_crossing_time 0 (b - a) (Œª n œâ, (f n œâ - a)‚Å∫) N (k + 1) =
      upper_crossing_time a b f N (k + 1),
    { ext œâ,
      simp only [upper_crossing_time_succ_eq, ‚Üê ih.2, hitting, set.mem_Ici, tsub_le_iff_right],
      split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ,
      { simp_rw [‚Üê sub_le_iff_le_add, hf œâ] },
      { simp_rw [set.mem_Ici, ‚Üê hf _ _] at h‚ÇÇ,
        exact false.elim (h‚ÇÇ h‚ÇÅ) },
      { simp_rw [set.mem_Ici, hf _ _] at h‚ÇÅ,
        exact false.elim (h‚ÇÅ h‚ÇÇ) },
      { refl } },
    refine ‚ü®this, _‚ü©,
    ext œâ,
    simp only [lower_crossing_time, this, hitting, set.mem_Iic],
    split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ,
    { simp_rw [hf' œâ] },
    { simp_rw [set.mem_Iic, ‚Üê hf' _ _] at h‚ÇÇ,
      exact false.elim (h‚ÇÇ h‚ÇÅ) },
    { simp_rw [set.mem_Iic, hf' _ _] at h‚ÇÅ,
      exact false.elim (h‚ÇÅ h‚ÇÇ) },
    { refl } }
end

lemma upcrossings_before_pos_eq (hab : a < b) :
  upcrossings_before 0 (b - a) (Œª n œâ, (f n œâ - a)‚Å∫) N œâ = upcrossings_before a b f N œâ :=
by simp_rw [upcrossings_before, (crossing_pos_eq hab).1]

lemma mul_integral_upcrossings_before_le_integral_pos_part_aux [is_finite_measure Œº]
  (hf : submartingale f ‚Ñ± Œº) (hab : a < b) :
  (b - a) * Œº[upcrossings_before a b f N] ‚â§ Œº[Œª œâ, (f N œâ - a)‚Å∫] :=
begin
  refine le_trans (le_of_eq _) (integral_mul_upcrossings_before_le_integral
    (hf.sub_martingale (martingale_const _ _ _)).pos
    (Œª œâ, lattice_ordered_comm_group.pos_nonneg _)
    (Œª œâ, lattice_ordered_comm_group.pos_nonneg _) (sub_pos.2 hab)),
  simp_rw [sub_zero, ‚Üê upcrossings_before_pos_eq hab],
  refl,
end

/-- **Doob's upcrossing estimate**: given a real valued discrete submartingale `f` and real
values `a` and `b`, we have `(b - a) * ùîº[upcrossings_before a b f N] ‚â§ ùîº[(f N - a)‚Å∫]` where
`upcrossings_before a b f N` is the number of times the process `f` crossed from below `a` to above
`b` before the time `N`. -/
theorem submartingale.mul_integral_upcrossings_before_le_integral_pos_part [is_finite_measure Œº]
  (a b : ‚Ñù) (hf : submartingale f ‚Ñ± Œº) (N : ‚Ñï) :
  (b - a) * Œº[upcrossings_before a b f N] ‚â§ Œº[Œª œâ, (f N œâ - a)‚Å∫] :=
begin
  by_cases hab : a < b,
  { exact mul_integral_upcrossings_before_le_integral_pos_part_aux hf hab },
  { rw [not_lt, ‚Üê sub_nonpos] at hab,
    exact le_trans (mul_nonpos_of_nonpos_of_nonneg hab (integral_nonneg (Œª œâ, nat.cast_nonneg _)))
      (integral_nonneg (Œª œâ, lattice_ordered_comm_group.pos_nonneg _)) }
end

/-!

### Variant of the upcrossing estimate

Now, we would like to prove a variant of the upcrossing estimate obtained by taking the supremum
over $N$ of the original upcrossing estimate. Namely, we want the inequality
$$
  (b - a) \sup_N \mathbb{E}[U_N(a, b)] \le \sup_N \mathbb{E}[f_N].
$$
This inequality is central for the martingale convergence theorem as it provides a uniform bound
for the upcrossings.

We note that on top of taking the supremum on both sides of the inequality, we had also used
the monotone convergence theorem on the left hand side to take the supremum outside of the
integral. To do this, we need to make sure $U_N(a, b)$ is measurable and integrable. Integrability
is easy to check as $U_N(a, b) ‚â§ N$ and so it suffices to show measurability. Indeed, by
noting that
$$
  U_N(a, b) = \sum_{i = 1}^N \mathbf{1}_{\{U_N(a, b) < N\}}
$$
$U_N(a, b)$ is measurable as $\{U_N(a, b) < N\}$ is a measurable set since $U_N(a, b)$ is a
stopping time.

-/

lemma upcrossings_before_eq_sum (hab : a < b) :
  upcrossings_before a b f N œâ =
  ‚àë i in finset.Ico 1 (N + 1), {n | upper_crossing_time a b f N n œâ < N}.indicator 1 i :=
begin
  by_cases hN : N = 0,
  { simp [hN] },
  rw ‚Üê finset.sum_Ico_consecutive _ (nat.succ_le_succ zero_le')
    (nat.succ_le_succ (upcrossings_before_le f œâ hab)),
  have h‚ÇÅ : ‚àÄ k ‚àà finset.Ico 1 (upcrossings_before a b f N œâ + 1),
    {n : ‚Ñï | upper_crossing_time a b f N n œâ < N}.indicator 1 k = 1,
  { rintro k hk,
    rw finset.mem_Ico at hk,
    rw set.indicator_of_mem,
    { refl },
    { exact upper_crossing_time_lt_of_le_upcrossings_before (zero_lt_iff.2 hN) hab
        (nat.lt_succ_iff.1 hk.2) } },
  have h‚ÇÇ : ‚àÄ k ‚àà finset.Ico (upcrossings_before a b f N œâ + 1) (N + 1),
    {n : ‚Ñï | upper_crossing_time a b f N n œâ < N}.indicator 1 k = 0,
  { rintro k hk,
    rw [finset.mem_Ico, nat.succ_le_iff] at hk,
    rw set.indicator_of_not_mem,
    simp only [set.mem_set_of_eq, not_lt],
    exact (upper_crossing_time_eq_of_upcrossings_before_lt hab hk.1).symm.le },
  rw [finset.sum_congr rfl h‚ÇÅ, finset.sum_congr rfl h‚ÇÇ, finset.sum_const, finset.sum_const,
    smul_eq_mul, mul_one, smul_eq_mul, mul_zero, nat.card_Ico, nat.add_succ_sub_one,
    add_zero, add_zero],
end

lemma adapted.measurable_upcrossings_before (hf : adapted ‚Ñ± f) (hab : a < b) :
  measurable (upcrossings_before a b f N) :=
begin
  have : upcrossings_before a b f N =
    Œª œâ, ‚àë i in finset.Ico 1 (N + 1), {n | upper_crossing_time a b f N n œâ < N}.indicator 1 i,
  { ext œâ,
    exact upcrossings_before_eq_sum hab },
  rw this,
  exact finset.measurable_sum _ (Œª i hi, measurable.indicator measurable_const $
    ‚Ñ±.le N _ (hf.is_stopping_time_upper_crossing_time.measurable_set_lt_of_pred N))
end

lemma adapted.integrable_upcrossings_before [is_finite_measure Œº]
  (hf : adapted ‚Ñ± f) (hab : a < b) :
  integrable (Œª œâ, (upcrossings_before a b f N œâ : ‚Ñù)) Œº :=
begin
  have : ‚àÄ·µê œâ ‚àÇŒº, ‚Äñ(upcrossings_before a b f N œâ : ‚Ñù)‚Äñ ‚â§ N,
  { refine eventually_of_forall (Œª œâ, _),
    rw [real.norm_eq_abs, nat.abs_cast, nat.cast_le],
    refine upcrossings_before_le _ _ hab },
  exact ‚ü®measurable.ae_strongly_measurable
    (measurable_from_top.comp (hf.measurable_upcrossings_before hab)),
    has_finite_integral_of_bounded this‚ü©
end

/-- The number of upcrossings of a realization of a stochastic process (`upcrossing` takes value
in `‚Ñù‚â•0‚àû` and so is allowed to be `‚àû`). -/
noncomputable def upcrossings [preorder Œπ] [order_bot Œπ] [has_Inf Œπ]
  (a b : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù) (œâ : Œ©) : ‚Ñù‚â•0‚àû :=
‚®Ü N, (upcrossings_before a b f N œâ : ‚Ñù‚â•0‚àû)

lemma adapted.measurable_upcrossings (hf : adapted ‚Ñ± f) (hab : a < b) :
  measurable (upcrossings a b f) :=
measurable_supr (Œª N, measurable_from_top.comp (hf.measurable_upcrossings_before hab))

lemma upcrossings_lt_top_iff :
  upcrossings a b f œâ < ‚àû ‚Üî ‚àÉ k, ‚àÄ N, upcrossings_before a b f N œâ ‚â§ k :=
begin
  have : upcrossings a b f œâ < ‚àû ‚Üî ‚àÉ k : ‚Ñù‚â•0, upcrossings a b f œâ ‚â§ k,
  { split,
    { intro h,
      lift upcrossings a b f œâ to ‚Ñù‚â•0 using h.ne with r hr,
      exact ‚ü®r, le_rfl‚ü© },
    { rintro ‚ü®k, hk‚ü©,
      exact lt_of_le_of_lt hk ennreal.coe_lt_top } },
  simp_rw [this, upcrossings, supr_le_iff],
  split; rintro ‚ü®k, hk‚ü©,
  { obtain ‚ü®m, hm‚ü© := exists_nat_ge k,
    refine ‚ü®m, Œª N, nat.cast_le.1 ((hk N).trans _)‚ü©,
    rwa [‚Üê ennreal.coe_nat, ennreal.coe_le_coe] },
  { refine ‚ü®k, Œª N, _‚ü©,
    simp only [ennreal.coe_nat, nat.cast_le, hk N] }
end

/-- A variant of Doob's upcrossing estimate obtained by taking the supremum on both sides. -/
lemma submartingale.mul_lintegral_upcrossings_le_lintegral_pos_part [is_finite_measure Œº]
  (a b : ‚Ñù) (hf : submartingale f ‚Ñ± Œº) :
  ennreal.of_real (b - a) * ‚à´‚Åª œâ, upcrossings a b f œâ ‚àÇŒº ‚â§
  ‚®Ü N, ‚à´‚Åª œâ, ennreal.of_real ((f N œâ - a)‚Å∫) ‚àÇŒº :=
begin
  by_cases hab : a < b,
  { simp_rw [upcrossings],
    have : ‚àÄ N, ‚à´‚Åª œâ, ennreal.of_real ((f N œâ - a)‚Å∫) ‚àÇŒº = ennreal.of_real (‚à´ œâ, (f N œâ - a)‚Å∫ ‚àÇŒº),
    { intro N,
      rw of_real_integral_eq_lintegral_of_real,
      { exact (hf.sub_martingale (martingale_const _ _ _)).pos.integrable _ },
      { exact eventually_of_forall (Œª œâ, lattice_ordered_comm_group.pos_nonneg _) } },
    rw lintegral_supr',
    { simp_rw [this, ennreal.mul_supr, supr_le_iff],
      intro N,
      rw [(by simp : ‚à´‚Åª œâ, upcrossings_before a b f N œâ ‚àÇŒº =
        ‚à´‚Åª œâ, ‚Üë(upcrossings_before a b f N œâ : ‚Ñù‚â•0) ‚àÇŒº), lintegral_coe_eq_integral,
        ‚Üê ennreal.of_real_mul (sub_pos.2 hab).le],
      { simp_rw [nnreal.coe_nat_cast],
        exact (ennreal.of_real_le_of_real
          (hf.mul_integral_upcrossings_before_le_integral_pos_part a b N)).trans (le_supr _ N) },
      { simp only [nnreal.coe_nat_cast, hf.adapted.integrable_upcrossings_before hab] } },
    { exact Œª n, measurable_from_top.comp_ae_measurable
        (hf.adapted.measurable_upcrossings_before  hab).ae_measurable },
    { refine eventually_of_forall (Œª œâ N M hNM, _),
      rw nat.cast_le,
      exact upcrossings_before_mono hab hNM œâ } },
  { rw [not_lt, ‚Üê sub_nonpos] at hab,
    rw [ennreal.of_real_of_nonpos hab, zero_mul],
    exact zero_le _ }
end

end measure_theory
