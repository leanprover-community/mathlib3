/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying, R√©my Degenne
-/
import measure_theory.function.conditional_expectation.real

/-!
# Filtrations

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines filtrations of a measurable space and œÉ-finite filtrations.

## Main definitions

* `measure_theory.filtration`: a filtration on a measurable space. That is, a monotone sequence of
  sub-œÉ-algebras.
* `measure_theory.sigma_finite_filtration`: a filtration `f` is œÉ-finite with respect to a measure
  `Œº` if for all `i`, `Œº.trim (f.le i)` is œÉ-finite.
* `measure_theory.filtration.natural`: the smallest filtration that makes a process adapted. That
  notion `adapted` is not defined yet in this file. See `measure_theory.adapted`.

## Main results

* `measure_theory.filtration.complete_lattice`: filtrations are a complete lattice.

## Tags

filtration, stochastic process

-/

open filter order topological_space
open_locale classical measure_theory nnreal ennreal topology big_operators

namespace measure_theory


/-- A `filtration` on a measurable space `Œ©` with œÉ-algebra `m` is a monotone
sequence of sub-œÉ-algebras of `m`. -/
structure filtration {Œ© : Type*} (Œπ : Type*) [preorder Œπ] (m : measurable_space Œ©) :=
(seq   : Œπ ‚Üí measurable_space Œ©)
(mono' : monotone seq)
(le'   : ‚àÄ i : Œπ, seq i ‚â§ m)

variables {Œ© Œ≤ Œπ : Type*} {m : measurable_space Œ©}

instance [preorder Œπ] : has_coe_to_fun (filtration Œπ m) (Œª _, Œπ ‚Üí measurable_space Œ©) :=
‚ü®Œª f, f.seq‚ü©

namespace filtration
variables [preorder Œπ]

protected lemma mono {i j : Œπ} (f : filtration Œπ m) (hij : i ‚â§ j) : f i ‚â§ f j := f.mono' hij

protected lemma le (f : filtration Œπ m) (i : Œπ) : f i ‚â§ m := f.le' i

@[ext] protected lemma ext {f g : filtration Œπ m} (h : (f : Œπ ‚Üí measurable_space Œ©) = g) : f = g :=
by { cases f, cases g, simp only, exact h, }

variable (Œπ)
/-- The constant filtration which is equal to `m` for all `i : Œπ`. -/
def const (m' : measurable_space Œ©) (hm' : m' ‚â§ m) : filtration Œπ m :=
‚ü®Œª _, m', monotone_const, Œª _, hm'‚ü©
variable {Œπ}

@[simp]
lemma const_apply {m' : measurable_space Œ©} {hm' : m' ‚â§ m} (i : Œπ) : const Œπ m' hm' i = m' := rfl

instance : inhabited (filtration Œπ m) := ‚ü®const Œπ m le_rfl‚ü©

instance : has_le (filtration Œπ m) := ‚ü®Œª f g, ‚àÄ i, f i ‚â§ g i‚ü©

instance : has_bot (filtration Œπ m) := ‚ü®const Œπ ‚ä• bot_le‚ü©

instance : has_top (filtration Œπ m) := ‚ü®const Œπ m le_rfl‚ü©

instance : has_sup (filtration Œπ m) := ‚ü®Œª f g,
{ seq   := Œª i, f i ‚äî g i,
  mono' := Œª i j hij, sup_le ((f.mono hij).trans le_sup_left) ((g.mono hij).trans le_sup_right),
  le'   := Œª i, sup_le (f.le i) (g.le i) }‚ü©

@[norm_cast] lemma coe_fn_sup {f g : filtration Œπ m} : ‚áë(f ‚äî g) = f ‚äî g := rfl

instance : has_inf (filtration Œπ m) := ‚ü®Œª f g,
{ seq   := Œª i, f i ‚äì g i,
  mono' := Œª i j hij, le_inf (inf_le_left.trans (f.mono hij)) (inf_le_right.trans (g.mono hij)),
  le'   := Œª i, inf_le_left.trans (f.le i) }‚ü©

@[norm_cast] lemma coe_fn_inf {f g : filtration Œπ m} : ‚áë(f ‚äì g) = f ‚äì g := rfl

instance : has_Sup (filtration Œπ m) := ‚ü®Œª s,
{ seq   := Œª i, Sup ((Œª f : filtration Œπ m, f i) '' s),
  mono' := Œª i j hij,
  begin
    refine Sup_le (Œª m' hm', _),
    rw [set.mem_image] at hm',
    obtain ‚ü®f, hf_mem, hfm'‚ü© := hm',
    rw ‚Üê hfm',
    refine (f.mono hij).trans _,
    have hfj_mem : f j ‚àà ((Œª g : filtration Œπ m, g j) '' s), from ‚ü®f, hf_mem, rfl‚ü©,
    exact le_Sup hfj_mem,
  end,
  le'   := Œª i,
  begin
    refine Sup_le (Œª m' hm', _),
    rw [set.mem_image] at hm',
    obtain ‚ü®f, hf_mem, hfm'‚ü© := hm',
    rw ‚Üê hfm',
    exact f.le i,
  end, }‚ü©

lemma Sup_def (s : set (filtration Œπ m)) (i : Œπ) :
  Sup s i = Sup ((Œª f : filtration Œπ m, f i) '' s) :=
rfl

noncomputable
instance : has_Inf (filtration Œπ m) := ‚ü®Œª s,
{ seq   := Œª i, if set.nonempty s then Inf ((Œª f : filtration Œπ m, f i) '' s) else m,
  mono' := Œª i j hij,
  begin
    by_cases h_nonempty : set.nonempty s,
    swap, { simp only [h_nonempty, set.nonempty_image_iff, if_false, le_refl], },
    simp only [h_nonempty, if_true, le_Inf_iff, set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ],
    refine Œª f hf_mem, le_trans _ (f.mono hij),
    have hfi_mem : f i ‚àà ((Œª g : filtration Œπ m, g i) '' s), from ‚ü®f, hf_mem, rfl‚ü©,
    exact Inf_le hfi_mem,
  end,
  le'   := Œª i,
  begin
    by_cases h_nonempty : set.nonempty s,
    swap, { simp only [h_nonempty, if_false, le_refl], },
    simp only [h_nonempty, if_true],
    obtain ‚ü®f, hf_mem‚ü© := h_nonempty,
    exact le_trans (Inf_le ‚ü®f, hf_mem, rfl‚ü©) (f.le i),
  end, }‚ü©

lemma Inf_def (s : set (filtration Œπ m)) (i : Œπ) :
  Inf s i = if set.nonempty s then Inf ((Œª f : filtration Œπ m, f i) '' s) else m :=
rfl

noncomputable
instance : complete_lattice (filtration Œπ m) :=
{ le           := (‚â§),
  le_refl      := Œª f i, le_rfl,
  le_trans     := Œª f g h h_fg h_gh i, (h_fg i).trans (h_gh i),
  le_antisymm  := Œª f g h_fg h_gf, filtration.ext $ funext $ Œª i, (h_fg i).antisymm (h_gf i),
  sup          := (‚äî),
  le_sup_left  := Œª f g i, le_sup_left,
  le_sup_right := Œª f g i, le_sup_right,
  sup_le       := Œª f g h h_fh h_gh i, sup_le (h_fh i) (h_gh _),
  inf          := (‚äì),
  inf_le_left  := Œª f g i, inf_le_left,
  inf_le_right := Œª f g i, inf_le_right,
  le_inf       := Œª f g h h_fg h_fh i, le_inf (h_fg i) (h_fh i),
  Sup          := Sup,
  le_Sup       := Œª s f hf_mem i, le_Sup ‚ü®f, hf_mem, rfl‚ü©,
  Sup_le       := Œª s f h_forall i, Sup_le $ Œª m' hm',
  begin
    obtain ‚ü®g, hg_mem, hfm'‚ü© := hm',
    rw ‚Üê hfm',
    exact h_forall g hg_mem i,
  end,
  Inf          := Inf,
  Inf_le       := Œª s f hf_mem i,
  begin
    have hs : s.nonempty := ‚ü®f, hf_mem‚ü©,
    simp only [Inf_def, hs, if_true],
    exact Inf_le ‚ü®f, hf_mem, rfl‚ü©,
  end,
  le_Inf       := Œª s f h_forall i,
  begin
    by_cases hs : s.nonempty,
    swap, { simp only [Inf_def, hs, if_false], exact f.le i, },
    simp only [Inf_def, hs, if_true, le_Inf_iff, set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ],
    exact Œª g hg_mem, h_forall g hg_mem i,
  end,
  top          := ‚ä§,
  bot          := ‚ä•,
  le_top       := Œª f i, f.le' i,
  bot_le       := Œª f i, bot_le, }

end filtration

lemma measurable_set_of_filtration [preorder Œπ] {f : filtration Œπ m} {s : set Œ©} {i : Œπ}
  (hs : measurable_set[f i] s) : measurable_set[m] s :=
f.le i s hs

/-- A measure is œÉ-finite with respect to filtration if it is œÉ-finite with respect
to all the sub-œÉ-algebra of the filtration. -/
class sigma_finite_filtration [preorder Œπ] (Œº : measure Œ©) (f : filtration Œπ m) : Prop :=
(sigma_finite : ‚àÄ i : Œπ, sigma_finite (Œº.trim (f.le i)))

instance sigma_finite_of_sigma_finite_filtration [preorder Œπ] (Œº : measure Œ©) (f : filtration Œπ m)
  [hf : sigma_finite_filtration Œº f] (i : Œπ) :
  sigma_finite (Œº.trim (f.le i)) :=
by apply hf.sigma_finite -- can't exact here

@[priority 100]
instance is_finite_measure.sigma_finite_filtration [preorder Œπ] (Œº : measure Œ©) (f : filtration Œπ m)
  [is_finite_measure Œº] :
  sigma_finite_filtration Œº f :=
‚ü®Œª n, by apply_instance‚ü©

/-- Given a integrable function `g`, the conditional expectations of `g` with respect to a
filtration is uniformly integrable. -/
lemma integrable.uniform_integrable_condexp_filtration
  [preorder Œπ] {Œº : measure Œ©} [is_finite_measure Œº] {f : filtration Œπ m}
  {g : Œ© ‚Üí ‚Ñù} (hg : integrable g Œº) :
  uniform_integrable (Œª i, Œº[g | f i]) 1 Œº :=
hg.uniform_integrable_condexp f.le

section of_set

variables [preorder Œπ]

/-- Given a sequence of measurable sets `(s‚Çô)`, `filtration_of_set` is the smallest filtration
such that `s‚Çô` is measurable with respect to the `n`-the sub-œÉ-algebra in `filtration_of_set`. -/
def filtration_of_set {s : Œπ ‚Üí set Œ©} (hsm : ‚àÄ i, measurable_set (s i)) : filtration Œπ m :=
{ seq := Œª i, measurable_space.generate_from {t | ‚àÉ j ‚â§ i, s j = t},
  mono' := Œª n m hnm, measurable_space.generate_from_mono
    (Œª t ‚ü®k, hk‚ÇÅ, hk‚ÇÇ‚ü©, ‚ü®k, hk‚ÇÅ.trans hnm, hk‚ÇÇ‚ü©),
  le' := Œª n, measurable_space.generate_from_le (Œª t ‚ü®k, hk‚ÇÅ, hk‚ÇÇ‚ü©, hk‚ÇÇ ‚ñ∏ hsm k) }

lemma measurable_set_filtration_of_set {s : Œπ ‚Üí set Œ©}
  (hsm : ‚àÄ i, measurable_set[m] (s i)) (i : Œπ) {j : Œπ} (hj : j ‚â§ i) :
  measurable_set[filtration_of_set hsm i] (s j) :=
measurable_space.measurable_set_generate_from ‚ü®j, hj, rfl‚ü©

lemma measurable_set_filtration_of_set' {s : Œπ ‚Üí set Œ©}
  (hsm : ‚àÄ n, measurable_set[m] (s n)) (i : Œπ) :
  measurable_set[filtration_of_set hsm i] (s i) :=
measurable_set_filtration_of_set hsm i le_rfl

end of_set

namespace filtration
variables [topological_space Œ≤] [metrizable_space Œ≤] [mŒ≤ : measurable_space Œ≤] [borel_space Œ≤]
  [preorder Œπ]

include mŒ≤

/-- Given a sequence of functions, the natural filtration is the smallest sequence
of œÉ-algebras such that that sequence of functions is measurable with respect to
the filtration. -/
def natural (u : Œπ ‚Üí Œ© ‚Üí Œ≤) (hum : ‚àÄ i, strongly_measurable (u i)) : filtration Œπ m :=
{ seq   := Œª i, ‚®Ü j ‚â§ i, measurable_space.comap (u j) mŒ≤,
  mono' := Œª i j hij, bsupr_mono $ Œª k, ge_trans hij,
  le'   := Œª i,
  begin
    refine supr‚ÇÇ_le _,
    rintros j hj s ‚ü®t, ht, rfl‚ü©,
    exact (hum j).measurable ht,
  end }

section

open measurable_space

lemma filtration_of_set_eq_natural [mul_zero_one_class Œ≤] [nontrivial Œ≤]
  {s : Œπ ‚Üí set Œ©} (hsm : ‚àÄ i, measurable_set[m] (s i)) :
  filtration_of_set hsm = natural (Œª i, (s i).indicator (Œª œâ, 1 : Œ© ‚Üí Œ≤))
    (Œª i, strongly_measurable_one.indicator (hsm i)) :=
begin
  simp only [natural, filtration_of_set, measurable_space_supr_eq],
  ext1 i,
  refine le_antisymm (generate_from_le _) (generate_from_le _),
  { rintro _ ‚ü®j, hij, rfl‚ü©,
    refine measurable_set_generate_from ‚ü®j, measurable_set_generate_from ‚ü®hij, _‚ü©‚ü©,
    rw comap_eq_generate_from,
    refine measurable_set_generate_from ‚ü®{1}, measurable_set_singleton 1, _‚ü©,
    ext x,
    simp [set.indicator_const_preimage_eq_union] },
  { rintro t ‚ü®n, ht‚ü©,
    suffices : measurable_space.generate_from
      {t | ‚àÉ (H : n ‚â§ i), measurable_set[(measurable_space.comap
        ((s n).indicator (Œª œâ, 1 : Œ© ‚Üí Œ≤)) mŒ≤)] t}
      ‚â§ generate_from {t | ‚àÉ (j : Œπ) (H : j ‚â§ i), s j = t},
    { exact this _ ht },
    refine generate_from_le _,
    rintro t ‚ü®hn, u, hu, hu'‚ü©,
    obtain heq | heq | heq | heq := set.indicator_const_preimage (s n) u (1 : Œ≤),
    swap 4, rw set.mem_singleton_iff at heq,
    all_goals { rw heq at hu', rw ‚Üê hu' },
    exacts [measurable_set_empty _, measurable_set.univ, measurable_set_generate_from ‚ü®n, hn, rfl‚ü©,
      measurable_set.compl (measurable_set_generate_from ‚ü®n, hn, rfl‚ü©)] }
end

end

section limit

omit mŒ≤

variables {E : Type*} [has_zero E] [topological_space E]
  {‚Ñ± : filtration Œπ m} {f : Œπ ‚Üí Œ© ‚Üí E} {Œº : measure Œ©}

/-- Given a process `f` and a filtration `‚Ñ±`, if `f` converges to some `g` almost everywhere and
`g` is `‚®Ü n, ‚Ñ± n`-measurable, then `limit_process f ‚Ñ± Œº` chooses said `g`, else it returns 0.

This definition is used to phrase the a.e. martingale convergence theorem
`submartingale.ae_tendsto_limit_process` where an L¬π-bounded submartingale `f` adapted to `‚Ñ±`
converges to `limit_process f ‚Ñ± Œº` `Œº`-almost everywhere. -/
noncomputable
def limit_process (f : Œπ ‚Üí Œ© ‚Üí E) (‚Ñ± : filtration Œπ m) (Œº : measure Œ© . volume_tac) :=
if h : ‚àÉ g : Œ© ‚Üí E, strongly_measurable[‚®Ü n, ‚Ñ± n] g ‚àß
  ‚àÄ·µê œâ ‚àÇŒº, tendsto (Œª n, f n œâ) at_top (ùìù (g œâ)) then classical.some h else 0

lemma strongly_measurable_limit_process :
  strongly_measurable[‚®Ü n, ‚Ñ± n] (limit_process f ‚Ñ± Œº) :=
begin
  rw limit_process,
  split_ifs with h h,
  exacts [(classical.some_spec h).1, strongly_measurable_zero]
end

lemma strongly_measurable_limit_process' :
  strongly_measurable[m] (limit_process f ‚Ñ± Œº) :=
strongly_measurable_limit_process.mono (Sup_le (Œª m ‚ü®n, hn‚ü©, hn ‚ñ∏ ‚Ñ±.le _))

lemma mem_‚Ñíp_limit_process_of_snorm_bdd {R : ‚Ñù‚â•0} {p : ‚Ñù‚â•0‚àû}
  {F : Type*} [normed_add_comm_group F] {‚Ñ± : filtration ‚Ñï m} {f : ‚Ñï ‚Üí Œ© ‚Üí F}
  (hfm : ‚àÄ n, ae_strongly_measurable (f n) Œº) (hbdd : ‚àÄ n, snorm (f n) p Œº ‚â§ R) :
  mem_‚Ñíp (limit_process f ‚Ñ± Œº) p Œº :=
begin
  rw limit_process,
  split_ifs with h,
  { refine ‚ü®strongly_measurable.ae_strongly_measurable
      ((classical.some_spec h).1.mono (Sup_le (Œª m ‚ü®n, hn‚ü©, hn ‚ñ∏ ‚Ñ±.le _))),
      lt_of_le_of_lt (Lp.snorm_lim_le_liminf_snorm hfm _ (classical.some_spec h).2)
        (lt_of_le_of_lt _ (ennreal.coe_lt_top : ‚ÜëR < ‚àû))‚ü©,
    simp_rw [liminf_eq, eventually_at_top],
    exact Sup_le (Œª b ‚ü®a, ha‚ü©, (ha a le_rfl).trans (hbdd _)) },
  { exact zero_mem_‚Ñíp }
end

end limit

end filtration

end measure_theory
