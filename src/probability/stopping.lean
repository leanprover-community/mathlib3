/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import measure_theory.constructions.borel_space
import measure_theory.function.l1_space
import measure_theory.function.strongly_measurable
import topology.instances.discrete

/-!
# Filtration and stopping time

This file defines some standard definition from the theory of stochastic processes including
filtrations and stopping times. These definitions are used to model the amount of information
at a specific time and is the first step in formalizing stochastic processes.

## Main definitions

* `measure_theory.filtration`: a filtration on a measurable space
* `measure_theory.adapted`: a sequence of functions `u` is said to be adapted to a
  filtration `f` if at each point in time `i`, `u i` is `f i`-measurable
* `measure_theory.prog_measurable`: a sequence of functions `u` is said to be progressively
  measurable with respect to a filtration `f` if at each point in time `i`, `u` restricted to
  `set.Iic i √ó Œ±` is measurable with respect to the product `measurable_space` structure where the
  œÉ-algebra used for `Œ±` is `f i`.
* `measure_theory.filtration.natural`: the natural filtration with respect to a sequence of
  measurable functions is the smallest filtration to which it is adapted to
* `measure_theory.is_stopping_time`: a stopping time with respect to some filtration `f` is a
  function `œÑ` such that for all `i`, the preimage of `{j | j ‚â§ i}` along `œÑ` is
  `f i`-measurable
* `measure_theory.is_stopping_time.measurable_space`: the œÉ-algebra associated with a stopping time

## Main results

* `adapted.prog_measurable_of_continuous`: a continuous adapted process is progressively measurable.
* `prog_measurable.stopped_process`: the stopped process of a progressively measurable process is
  progressively measurable.
* `mem_‚Ñíp_stopped_process`: if a process belongs to `‚Ñíp` at every time in `‚Ñï`, then its stopped
  process belongs to `‚Ñíp` as well.

## Tags

filtration, stopping time, stochastic process

-/

open topological_space filter
open_locale classical measure_theory nnreal ennreal topological_space big_operators

namespace measure_theory

/-! ### Filtrations -/

/-- A `filtration` on measurable space `Œ±` with œÉ-algebra `m` is a monotone
sequence of sub-œÉ-algebras of `m`. -/
structure filtration {Œ± : Type*} (Œπ : Type*) [preorder Œπ] (m : measurable_space Œ±) :=
(seq   : Œπ ‚Üí measurable_space Œ±)
(mono' : monotone seq)
(le'   : ‚àÄ i : Œπ, seq i ‚â§ m)

variables {Œ± Œ≤ Œπ : Type*} {m : measurable_space Œ±}

instance [preorder Œπ] : has_coe_to_fun (filtration Œπ m) (Œª _, Œπ ‚Üí measurable_space Œ±) :=
‚ü®Œª f, f.seq‚ü©

namespace filtration
variables [preorder Œπ]

protected lemma mono {i j : Œπ} (f : filtration Œπ m) (hij : i ‚â§ j) : f i ‚â§ f j := f.mono' hij

protected lemma le (f : filtration Œπ m) (i : Œπ) : f i ‚â§ m := f.le' i

@[ext] protected lemma ext {f g : filtration Œπ m} (h : (f : Œπ ‚Üí measurable_space Œ±) = g) : f = g :=
by { cases f, cases g, simp only, exact h, }

variable (Œπ)
/-- The constant filtration which is equal to `m` for all `i : Œπ`. -/
def const (m' : measurable_space Œ±) (hm' : m' ‚â§ m) : filtration Œπ m :=
‚ü®Œª _, m', monotone_const, Œª _, hm'‚ü©
variable {Œπ}

@[simp]
lemma const_apply {m' : measurable_space Œ±} {hm' : m' ‚â§ m} (i : Œπ) : const Œπ m' hm' i = m' := rfl

instance : inhabited (filtration Œπ m) := ‚ü®const Œπ m le_rfl‚ü©

instance : has_le (filtration Œπ m) := ‚ü®Œª f g, ‚àÄ i, f i ‚â§ g i‚ü©

instance : has_bot (filtration Œπ m) := ‚ü®const Œπ ‚ä• bot_le‚ü©

instance : has_top (filtration Œπ m) := ‚ü®const Œπ m le_rfl‚ü©

instance : has_sup (filtration Œπ m) := ‚ü®Œª f g,
{ seq   := Œª i, f i ‚äî g i,
  mono' := Œª i j hij, sup_le ((f.mono hij).trans le_sup_left) ((g.mono hij).trans le_sup_right),
  le'   := Œª i, sup_le (f.le i) (g.le i) }‚ü©

@[norm_cast] lemma coe_fn_sup {f g : filtration Œπ m} : ‚áë(f ‚äî g) = f ‚äî g := rfl

instance : has_inf (filtration Œπ m) := ‚ü®Œª f g,
{ seq   := Œª i, f i ‚äì g i,
  mono' := Œª i j hij, le_inf (inf_le_left.trans (f.mono hij)) (inf_le_right.trans (g.mono hij)),
  le'   := Œª i, inf_le_left.trans (f.le i) }‚ü©

@[norm_cast] lemma coe_fn_inf {f g : filtration Œπ m} : ‚áë(f ‚äì g) = f ‚äì g := rfl

instance : has_Sup (filtration Œπ m) := ‚ü®Œª s,
{ seq   := Œª i, Sup ((Œª f : filtration Œπ m, f i) '' s),
  mono' := Œª i j hij,
  begin
    refine Sup_le (Œª m' hm', _),
    rw [set.mem_image] at hm',
    obtain ‚ü®f, hf_mem, hfm'‚ü© := hm',
    rw ‚Üê hfm',
    refine (f.mono hij).trans _,
    have hfj_mem : f j ‚àà ((Œª g : filtration Œπ m, g j) '' s), from ‚ü®f, hf_mem, rfl‚ü©,
    exact le_Sup hfj_mem,
  end,
  le'   := Œª i,
  begin
    refine Sup_le (Œª m' hm', _),
    rw [set.mem_image] at hm',
    obtain ‚ü®f, hf_mem, hfm'‚ü© := hm',
    rw ‚Üê hfm',
    exact f.le i,
  end, }‚ü©

lemma Sup_def (s : set (filtration Œπ m)) (i : Œπ) :
  Sup s i = Sup ((Œª f : filtration Œπ m, f i) '' s) :=
rfl

noncomputable
instance : has_Inf (filtration Œπ m) := ‚ü®Œª s,
{ seq   := Œª i, if set.nonempty s then Inf ((Œª f : filtration Œπ m, f i) '' s) else m,
  mono' := Œª i j hij,
  begin
    by_cases h_nonempty : set.nonempty s,
    swap, { simp only [h_nonempty, set.nonempty_image_iff, if_false, le_refl], },
    simp only [h_nonempty, if_true, le_Inf_iff, set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ],
    refine Œª f hf_mem, le_trans _ (f.mono hij),
    have hfi_mem : f i ‚àà ((Œª g : filtration Œπ m, g i) '' s), from ‚ü®f, hf_mem, rfl‚ü©,
    exact Inf_le hfi_mem,
  end,
  le'   := Œª i,
  begin
    by_cases h_nonempty : set.nonempty s,
    swap, { simp only [h_nonempty, if_false, le_refl], },
    simp only [h_nonempty, if_true],
    obtain ‚ü®f, hf_mem‚ü© := h_nonempty,
    exact le_trans (Inf_le ‚ü®f, hf_mem, rfl‚ü©) (f.le i),
  end, }‚ü©

lemma Inf_def (s : set (filtration Œπ m)) (i : Œπ) :
  Inf s i = if set.nonempty s then Inf ((Œª f : filtration Œπ m, f i) '' s) else m :=
rfl

noncomputable
instance : complete_lattice (filtration Œπ m) :=
{ le           := (‚â§),
  le_refl      := Œª f i, le_rfl,
  le_trans     := Œª f g h h_fg h_gh i, (h_fg i).trans (h_gh i),
  le_antisymm  := Œª f g h_fg h_gf, filtration.ext $ funext $ Œª i, (h_fg i).antisymm (h_gf i),
  sup          := (‚äî),
  le_sup_left  := Œª f g i, le_sup_left,
  le_sup_right := Œª f g i, le_sup_right,
  sup_le       := Œª f g h h_fh h_gh i, sup_le (h_fh i) (h_gh _),
  inf          := (‚äì),
  inf_le_left  := Œª f g i, inf_le_left,
  inf_le_right := Œª f g i, inf_le_right,
  le_inf       := Œª f g h h_fg h_fh i, le_inf (h_fg i) (h_fh i),
  Sup          := Sup,
  le_Sup       := Œª s f hf_mem i, le_Sup ‚ü®f, hf_mem, rfl‚ü©,
  Sup_le       := Œª s f h_forall i, Sup_le $ Œª m' hm',
  begin
    obtain ‚ü®g, hg_mem, hfm'‚ü© := hm',
    rw ‚Üê hfm',
    exact h_forall g hg_mem i,
  end,
  Inf          := Inf,
  Inf_le       := Œª s f hf_mem i,
  begin
    have hs : s.nonempty := ‚ü®f, hf_mem‚ü©,
    simp only [Inf_def, hs, if_true],
    exact Inf_le ‚ü®f, hf_mem, rfl‚ü©,
  end,
  le_Inf       := Œª s f h_forall i,
  begin
    by_cases hs : s.nonempty,
    swap, { simp only [Inf_def, hs, if_false], exact f.le i, },
    simp only [Inf_def, hs, if_true, le_Inf_iff, set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ],
    exact Œª g hg_mem, h_forall g hg_mem i,
  end,
  top          := ‚ä§,
  bot          := ‚ä•,
  le_top       := Œª f i, f.le' i,
  bot_le       := Œª f i, bot_le, }

end filtration

lemma measurable_set_of_filtration [preorder Œπ] {f : filtration Œπ m} {s : set Œ±} {i : Œπ}
  (hs : measurable_set[f i] s) : measurable_set[m] s :=
f.le i s hs

/-- A measure is œÉ-finite with respect to filtration if it is œÉ-finite with respect
to all the sub-œÉ-algebra of the filtration. -/
class sigma_finite_filtration [preorder Œπ] (Œº : measure Œ±) (f : filtration Œπ m) : Prop :=
(sigma_finite : ‚àÄ i : Œπ, sigma_finite (Œº.trim (f.le i)))

instance sigma_finite_of_sigma_finite_filtration [preorder Œπ] (Œº : measure Œ±) (f : filtration Œπ m)
  [hf : sigma_finite_filtration Œº f] (i : Œπ) :
  sigma_finite (Œº.trim (f.le i)) :=
by apply hf.sigma_finite -- can't exact here


section adapted_process

variables [measurable_space Œ≤] [preorder Œπ]
  {u v : Œπ ‚Üí Œ± ‚Üí Œ≤} {f : filtration Œπ m}

/-- A sequence of functions `u` is adapted to a filtration `f` if for all `i`,
`u i` is `f i`-measurable. -/
def adapted (f : filtration Œπ m) (u : Œπ ‚Üí Œ± ‚Üí Œ≤) : Prop :=
‚àÄ i : Œπ, measurable[f i] (u i)

namespace adapted

lemma add [has_add Œ≤] [has_measurable_add‚ÇÇ Œ≤] (hu : adapted f u) (hv : adapted f v) :
  adapted f (u + v) :=
Œª i, (hu i).add (hv i)

lemma neg [has_neg Œ≤] [has_measurable_neg Œ≤] (hu : adapted f u) : adapted f (-u) :=
Œª i, (hu i).neg

lemma smul [has_scalar ‚Ñù Œ≤] [has_measurable_smul ‚Ñù Œ≤] (c : ‚Ñù) (hu : adapted f u) :
  adapted f (c ‚Ä¢ u) :=
Œª i, (hu i).const_smul c

end adapted

variable (Œ≤)
lemma adapted_zero [has_zero Œ≤] (f : filtration Œπ m) : adapted f (0 : Œπ ‚Üí Œ± ‚Üí Œ≤) :=
Œª i, @measurable_zero Œ≤ Œ± _ (f i) _
variable {Œ≤}

/-- Progressively measurable process. A sequence of functions `u` is said to be progressively
measurable with respect to a filtration `f` if at each point in time `i`, `u` restricted to
`set.Iic i √ó Œ±` is measurable with respect to the product `measurable_space` structure where the
œÉ-algebra used for `Œ±` is `f i`.
The usual definition uses the interval `[0,i]`, which we replace by `set.Iic i`. We recover the
usual definition for index types `‚Ñù‚â•0` or `‚Ñï`. -/
def prog_measurable [measurable_space Œπ] (f : filtration Œπ m) (u : Œπ ‚Üí Œ± ‚Üí Œ≤) : Prop :=
‚àÄ i, measurable[subtype.measurable_space.prod (f i)] (Œª p : set.Iic i √ó Œ±, u p.1 p.2)

lemma prog_measurable_const [measurable_space Œπ] (f : filtration Œπ m) (b : Œ≤) :
  prog_measurable f ((Œª _ _, b) : Œπ ‚Üí Œ± ‚Üí Œ≤) :=
Œª i, @measurable_const _ _ _ (subtype.measurable_space.prod (f i)) _

namespace prog_measurable

variables [measurable_space Œπ]

protected lemma adapted (h : prog_measurable f u) : adapted f u :=
begin
  intro i,
  have : u i = (Œª p : set.Iic i √ó Œ±, u p.1 p.2) ‚àò (Œª x, (‚ü®i, set.mem_Iic.mpr le_rfl‚ü©, x)) := rfl,
  rw this,
  exact (h i).comp ((@measurable_const _ _ _ (f i) _).prod_mk (@measurable_id _ (f i))),
end

protected lemma comp {t : Œπ ‚Üí Œ± ‚Üí Œπ} (h : prog_measurable f u) (ht : prog_measurable f t)
  (ht_le : ‚àÄ i x, t i x ‚â§ i) :
  prog_measurable f (Œª i x, u (t i x) x) :=
begin
  intro i,
  have : (Œª p : ‚Ü•(set.Iic i) √ó Œ±, u (t (p.fst : Œπ) p.snd) p.snd)
    = (Œª p : ‚Ü•(set.Iic i) √ó Œ±, u (p.fst : Œπ) p.snd) ‚àò (Œª p : ‚Ü•(set.Iic i) √ó Œ±,
      (‚ü®t (p.fst : Œπ) p.snd, set.mem_Iic.mpr ((ht_le _ _).trans p.fst.prop)‚ü©, p.snd)) := rfl,
  rw this,
  exact (h i).comp ((ht i).subtype_mk.prod_mk (@measurable_snd _ _ _ (f i))),
end

section arithmetic

@[to_additive] protected lemma mul [has_mul Œ≤] [has_measurable_mul‚ÇÇ Œ≤]
  (hu : prog_measurable f u) (hv : prog_measurable f v) :
  prog_measurable f (Œª i x, u i x * v i x) :=
Œª i, (hu i).mul (hv i)

@[to_additive] protected lemma finset_prod' {Œ≥} [comm_monoid Œ≤] [has_measurable_mul‚ÇÇ Œ≤]
  {U : Œ≥ ‚Üí Œπ ‚Üí Œ± ‚Üí Œ≤} {s : finset Œ≥} (h : ‚àÄ c ‚àà s, prog_measurable f (U c)) :
  prog_measurable f (‚àè c in s, U c) :=
finset.prod_induction U (prog_measurable f) (Œª _ _, prog_measurable.mul)
  (prog_measurable_const _ 1) h

@[to_additive] protected lemma finset_prod {Œ≥} [comm_monoid Œ≤] [has_measurable_mul‚ÇÇ Œ≤]
  {U : Œ≥ ‚Üí Œπ ‚Üí Œ± ‚Üí Œ≤} {s : finset Œ≥} (h : ‚àÄ c ‚àà s, prog_measurable f (U c)) :
  prog_measurable f (Œª i a, ‚àè c in s, U c i a) :=
by { convert prog_measurable.finset_prod' h, ext i a, simp only [finset.prod_apply], }

@[to_additive] protected lemma inv [has_inv Œ≤] [has_measurable_inv Œ≤] (hu : prog_measurable f u) :
  prog_measurable f (Œª i x, (u i x)‚Åª¬π) :=
Œª i, (hu i).inv

@[to_additive] protected lemma div [has_div Œ≤] [has_measurable_div‚ÇÇ Œ≤]
  (hu : prog_measurable f u) (hv : prog_measurable f v) :
  prog_measurable f (Œª i x, u i x / v i x) :=
Œª i, (hu i).div (hv i)

end arithmetic

end prog_measurable

lemma prog_measurable_of_tendsto' {Œ≥} [measurable_space Œπ] [metric_space Œ≤] [borel_space Œ≤]
  (fltr : filter Œ≥) [fltr.ne_bot] [fltr.is_countably_generated] {U : Œ≥ ‚Üí Œπ ‚Üí Œ± ‚Üí Œ≤}
  (h : ‚àÄ l, prog_measurable f (U l)) (h_tendsto : tendsto U fltr (ùìù u)) :
  prog_measurable f u :=
begin
  refine Œª i, @measurable_of_tendsto_metric' (set.Iic i √ó Œ±) Œ≤ (measurable_space.prod _ (f i))
    _ _ _ Œ≥ _ _ fltr _ _ (Œª l, h l i) _,
  rw tendsto_pi_nhds at h_tendsto ‚ä¢,
  intro x,
  specialize h_tendsto x.fst,
  rw tendsto_nhds at h_tendsto ‚ä¢,
  exact Œª s hs h_mem, h_tendsto {g | g x.snd ‚àà s} (hs.preimage (continuous_apply x.snd)) h_mem,
end

lemma prog_measurable_of_tendsto [measurable_space Œπ] [metric_space Œ≤] [borel_space Œ≤]
  {U : ‚Ñï ‚Üí Œπ ‚Üí Œ± ‚Üí Œ≤}
  (h : ‚àÄ l, prog_measurable f (U l)) (h_tendsto : tendsto U at_top (ùìù u)) :
  prog_measurable f u :=
prog_measurable_of_tendsto' at_top h h_tendsto

/-- A continuous and adapted process is progressively measurable. -/
theorem adapted.prog_measurable_of_continuous [emetric_space Œπ] [measurable_space Œπ]
  [second_countable_topology Œπ] [opens_measurable_space Œπ] [metric_space Œ≤] [borel_space Œ≤]
  (h : adapted f u) (hu_cont : ‚àÄ x, continuous (Œª i, u i x)) :
  prog_measurable f u :=
Œª i, @measurable_uncurry_of_continuous_of_measurable _ _ (set.Iic i) _ _ _ _ _ _ _ _ _
  (Œª x, (hu_cont x).comp continuous_induced_dom) (Œª j, (h j).mono (f.mono j.prop) le_rfl)

end adapted_process

namespace filtration
variables {mŒ≤ : measurable_space Œ≤} [preorder Œπ]

include mŒ≤

/-- Given a sequence of functions, the natural filtration is the smallest sequence
of œÉ-algebras such that that sequence of functions is measurable with respect to
the filtration. -/
def natural (u : Œπ ‚Üí Œ± ‚Üí Œ≤) (hum : ‚àÄ i, measurable (u i)) : filtration Œπ m :=
{ seq   := Œª i, ‚®Ü j ‚â§ i, measurable_space.comap (u j) infer_instance,
  mono' := Œª i j hij, bsupr_le_bsupr' $ Œª k hk, le_trans hk hij,
  le'   := Œª i, bsupr_le (Œª j hj s hs, let ‚ü®t, ht, ht'‚ü© := hs in ht' ‚ñ∏ hum j ht) }

lemma adapted_natural {u : Œπ ‚Üí Œ± ‚Üí Œ≤} (hum : ‚àÄ i, measurable[m] (u i)) :
  adapted (natural u hum) u :=
Œª i, measurable.le (le_bsupr_of_le i (le_refl i) le_rfl) (Œª s hs, ‚ü®s, hs, rfl‚ü©)

end filtration

/-! ### Stopping times -/

/-- A stopping time with respect to some filtration `f` is a function
`œÑ` such that for all `i`, the preimage of `{j | j ‚â§ i}` along `œÑ` is measurable
with respect to `f i`.

Intuitively, the stopping time `œÑ` describes some stopping rule such that at time
`i`, we may determine it with the information we have at time `i`. -/
def is_stopping_time [preorder Œπ] (f : filtration Œπ m) (œÑ : Œ± ‚Üí Œπ) :=
‚àÄ i : Œπ, measurable_set[f i] $ {x | œÑ x ‚â§ i}

lemma is_stopping_time_const [preorder Œπ] {f : filtration Œπ m} (i : Œπ) :
  is_stopping_time f (Œª x, i) :=
Œª j, by simp only [measurable_set.const]

section measurable_set

section preorder
variables [preorder Œπ] {f : filtration Œπ m} {œÑ : Œ± ‚Üí Œπ}

lemma is_stopping_time.measurable_set_le (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable_set[f i] {x | œÑ x ‚â§ i} :=
hœÑ i

lemma is_stopping_time.measurable_set_lt_of_pred [pred_order Œπ]
  (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable_set[f i] {x | œÑ x < i} :=
begin
  by_cases hi_min : is_min i,
  { suffices : {x : Œ± | œÑ x < i} = ‚àÖ, by { rw this, exact @measurable_set.empty _ (f i), },
    ext1 x,
    simp only [set.mem_set_of_eq, set.mem_empty_eq, iff_false],
    rw is_min_iff_forall_not_lt at hi_min,
    exact hi_min (œÑ x), },
  have : {x : Œ± | œÑ x < i} = œÑ ‚Åª¬π' (set.Iio i),
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_preimage, set.mem_Iio], },
  rw [this, pred_order.Iio_eq_Iic_pred' hi_min],
  have : œÑ ‚Åª¬π' set.Iic (pred_order.pred i) = {x : Œ± | œÑ x ‚â§ pred_order.pred i},
  { ext1 x, simp only [set.mem_preimage, set.mem_Iic, set.mem_set_of_eq], },
  rw this,
  exact f.mono (pred_order.pred_le i) _ (hœÑ.measurable_set_le (pred_order.pred i)),
end

end preorder

section linear_order
variables [linear_order Œπ] {f : filtration Œπ m} {œÑ : Œ± ‚Üí Œπ}

lemma is_stopping_time.measurable_set_gt (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable_set[f i] {x | i < œÑ x} :=
begin
  have : {x | i < œÑ x} = {x | œÑ x ‚â§ i}·∂ú,
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_compl_eq, not_le], },
  rw this,
  exact (hœÑ.measurable_set_le i).compl,
end

variables [topological_space Œπ] [order_topology Œπ] [first_countable_topology Œπ]

/-- Auxiliary lemma for `is_stopping_time.measurable_set_lt`. -/
lemma is_stopping_time.measurable_set_lt_of_is_lub
  (hœÑ : is_stopping_time f œÑ) (i : Œπ) (h_lub : is_lub (set.Iio i) i) :
  measurable_set[f i] {x | œÑ x < i} :=
begin
  by_cases hi_min : is_min i,
  { suffices : {x : Œ± | œÑ x < i} = ‚àÖ, by { rw this, exact @measurable_set.empty _ (f i), },
    ext1 x,
    simp only [set.mem_set_of_eq, set.mem_empty_eq, iff_false],
    exact is_min_iff_forall_not_lt.mp hi_min (œÑ x), },
  obtain ‚ü®seq, -, -, h_tendsto, h_bound‚ü© : ‚àÉ seq : ‚Ñï ‚Üí Œπ,
      monotone seq ‚àß (‚àÄ j, seq j ‚â§ i) ‚àß tendsto seq at_top (ùìù i) ‚àß (‚àÄ j, seq j < i),
    from h_lub.exists_seq_monotone_tendsto (not_is_min_iff.mp hi_min),
  have h_Ioi_eq_Union : set.Iio i = ‚ãÉ j, { k | k ‚â§ seq j},
  { ext1 k,
    simp only [set.mem_Iio, set.mem_Union, set.mem_set_of_eq],
    refine ‚ü®Œª hk_lt_i, _, Œª h_exists_k_le_seq, _‚ü©,
    { rw tendsto_at_top' at h_tendsto,
      have h_nhds : set.Ici k ‚àà ùìù i,
        from mem_nhds_iff.mpr ‚ü®set.Ioi k, set.Ioi_subset_Ici le_rfl, is_open_Ioi, hk_lt_i‚ü©,
      obtain ‚ü®a, ha‚ü© : ‚àÉ (a : ‚Ñï), ‚àÄ (b : ‚Ñï), b ‚â• a ‚Üí k ‚â§ seq b := h_tendsto (set.Ici k) h_nhds,
      exact ‚ü®a, ha a le_rfl‚ü©, },
    { obtain ‚ü®j, hk_seq_j‚ü© := h_exists_k_le_seq,
      exact hk_seq_j.trans_lt (h_bound j), }, },
  have h_lt_eq_preimage : {x : Œ± | œÑ x < i} = œÑ ‚Åª¬π' (set.Iio i),
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_preimage, set.mem_Iio], },
  rw [h_lt_eq_preimage, h_Ioi_eq_Union],
  simp only [set.preimage_Union, set.preimage_set_of_eq],
  exact measurable_set.Union
    (Œª n, f.mono (h_bound n).le _ (hœÑ.measurable_set_le (seq n))),
end

lemma is_stopping_time.measurable_set_lt (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable_set[f i] {x | œÑ x < i} :=
begin
  obtain ‚ü®i', hi'_lub‚ü© : ‚àÉ i', is_lub (set.Iio i) i', from exists_lub_Iio i,
  cases lub_Iio_eq_self_or_Iio_eq_Iic i hi'_lub with hi'_eq_i h_Iio_eq_Iic,
  { rw ‚Üê hi'_eq_i at hi'_lub ‚ä¢,
    exact hœÑ.measurable_set_lt_of_is_lub i' hi'_lub, },
  { have h_lt_eq_preimage : {x : Œ± | œÑ x < i} = œÑ ‚Åª¬π' (set.Iio i) := rfl,
    rw [h_lt_eq_preimage, h_Iio_eq_Iic],
    exact f.mono (lub_Iio_le i hi'_lub) _ (hœÑ.measurable_set_le i'), },
end

lemma is_stopping_time.measurable_set_ge (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable_set[f i] {x | i ‚â§ œÑ x} :=
begin
  have : {x | i ‚â§ œÑ x} = {x | œÑ x < i}·∂ú,
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_compl_eq, not_lt], },
  rw this,
  exact (hœÑ.measurable_set_lt i).compl,
end

lemma is_stopping_time.measurable_set_eq (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable_set[f i] {x | œÑ x = i} :=
begin
  have : {x | œÑ x = i} = {x | œÑ x ‚â§ i} ‚à© {x | œÑ x ‚â• i},
  { ext1 x, simp only [set.mem_set_of_eq, ge_iff_le, set.mem_inter_eq, le_antisymm_iff], },
  rw this,
  exact (hœÑ.measurable_set_le i).inter (hœÑ.measurable_set_ge i),
end

lemma is_stopping_time.measurable_set_eq_le (hœÑ : is_stopping_time f œÑ) {i j : Œπ} (hle : i ‚â§ j) :
  measurable_set[f j] {x | œÑ x = i} :=
f.mono hle _ $ hœÑ.measurable_set_eq i

lemma is_stopping_time.measurable_set_lt_le (hœÑ : is_stopping_time f œÑ) {i j : Œπ} (hle : i ‚â§ j) :
  measurable_set[f j] {x | œÑ x < i} :=
f.mono hle _ $ hœÑ.measurable_set_lt i

end linear_order

section encodable

lemma is_stopping_time_of_measurable_set_eq [preorder Œπ] [encodable Œπ]
  {f : filtration Œπ m} {œÑ : Œ± ‚Üí Œπ} (hœÑ : ‚àÄ i, measurable_set[f i] {x | œÑ x = i}) :
  is_stopping_time f œÑ :=
begin
  intro i,
  rw show {x | œÑ x ‚â§ i} = ‚ãÉ k ‚â§ i, {x | œÑ x = k}, by { ext, simp },
  refine measurable_set.bUnion (set.countable_encodable _) (Œª k hk, _),
  exact f.mono hk _ (hœÑ k),
end

end encodable

end measurable_set

namespace is_stopping_time

protected lemma max [linear_order Œπ] {f : filtration Œπ m} {œÑ œÄ : Œ± ‚Üí Œπ}
  (hœÑ : is_stopping_time f œÑ) (hœÄ : is_stopping_time f œÄ) :
  is_stopping_time f (Œª x, max (œÑ x) (œÄ x)) :=
begin
  intro i,
  simp_rw [max_le_iff, set.set_of_and],
  exact (hœÑ i).inter (hœÄ i),
end

protected lemma min [linear_order Œπ] {f : filtration Œπ m} {œÑ œÄ : Œ± ‚Üí Œπ}
  (hœÑ : is_stopping_time f œÑ) (hœÄ : is_stopping_time f œÄ) :
  is_stopping_time f (Œª x, min (œÑ x) (œÄ x)) :=
begin
  intro i,
  simp_rw [min_le_iff, set.set_of_or],
  exact (hœÑ i).union (hœÄ i),
end

lemma add_const [add_group Œπ] [preorder Œπ] [covariant_class Œπ Œπ (function.swap (+)) (‚â§)]
  [covariant_class Œπ Œπ (+) (‚â§)]
  {f : filtration Œπ m} {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) {i : Œπ} (hi : 0 ‚â§ i) :
  is_stopping_time f (Œª x, œÑ x + i) :=
begin
  intro j,
  simp_rw [‚Üê le_sub_iff_add_le],
  exact f.mono (sub_le_self j hi) _ (hœÑ (j - i)),
end

section preorder

variables [preorder Œπ] {f : filtration Œπ m}

/-- The associated œÉ-algebra with a stopping time. -/
protected def measurable_space
  {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) : measurable_space Œ± :=
{ measurable_set' := Œª s, ‚àÄ i : Œπ, measurable_set[f i] (s ‚à© {x | œÑ x ‚â§ i}),
  measurable_set_empty :=
    Œª i, (set.empty_inter {x | œÑ x ‚â§ i}).symm ‚ñ∏ @measurable_set.empty _ (f i),
  measurable_set_compl := Œª s hs i,
    begin
      rw (_ : s·∂ú ‚à© {x | œÑ x ‚â§ i} = (s·∂ú ‚à™ {x | œÑ x ‚â§ i}·∂ú) ‚à© {x | œÑ x ‚â§ i}),
      { refine measurable_set.inter _ _,
        { rw ‚Üê set.compl_inter,
          exact (hs i).compl },
        { exact hœÑ i} },
      { rw set.union_inter_distrib_right,
        simp only [set.compl_inter_self, set.union_empty] }
    end,
  measurable_set_Union := Œª s hs i,
    begin
      rw forall_swap at hs,
      rw set.Union_inter,
      exact measurable_set.Union (hs i),
    end }

@[protected]
lemma measurable_set {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) (s : set Œ±) :
  measurable_set[hœÑ.measurable_space] s ‚Üî
  ‚àÄ i : Œπ, measurable_set[f i] (s ‚à© {x | œÑ x ‚â§ i}) :=
iff.rfl

lemma measurable_space_mono
  {œÑ œÄ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) (hœÄ : is_stopping_time f œÄ) (hle : œÑ ‚â§ œÄ) :
  hœÑ.measurable_space ‚â§ hœÄ.measurable_space :=
begin
  intros s hs i,
  rw (_ : s ‚à© {x | œÄ x ‚â§ i} = s ‚à© {x | œÑ x ‚â§ i} ‚à© {x | œÄ x ‚â§ i}),
  { exact (hs i).inter (hœÄ i) },
  { ext,
    simp only [set.mem_inter_eq, iff_self_and, and.congr_left_iff, set.mem_set_of_eq],
    intros hle' _,
    exact le_trans (hle _) hle' },
end

lemma measurable_space_le [encodable Œπ] {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) :
  hœÑ.measurable_space ‚â§ m :=
begin
  intros s hs,
  change ‚àÄ i, measurable_set[f i] (s ‚à© {x | œÑ x ‚â§ i}) at hs,
  rw (_ : s = ‚ãÉ i, s ‚à© {x | œÑ x ‚â§ i}),
  { exact measurable_set.Union (Œª i, f.le i _ (hs i)) },
  { ext x, split; rw set.mem_Union,
    { exact Œª hx, ‚ü®œÑ x, hx, le_rfl‚ü© },
    { rintro ‚ü®_, hx, _‚ü©,
      exact hx } }
end

section nat

lemma measurable_set_eq_const {f : filtration ‚Ñï m}
  {œÑ : Œ± ‚Üí ‚Ñï} (hœÑ : is_stopping_time f œÑ) (i : ‚Ñï) :
  measurable_set[hœÑ.measurable_space] {x | œÑ x = i} :=
begin
  rw hœÑ.measurable_set,
  intro j,
  by_cases i ‚â§ j,
  { rw (_ : {x | œÑ x = i} ‚à© {x | œÑ x ‚â§ j} = {x | œÑ x = i}),
    { exact hœÑ.measurable_set_eq_le h },
    { ext,
      simp only [set.mem_inter_eq, and_iff_left_iff_imp, set.mem_set_of_eq],
      rintro rfl,
      assumption } },
  { rw (_ : {x | œÑ x = i} ‚à© {x | œÑ x ‚â§ j} = ‚àÖ),
    { exact @measurable_set.empty _ (f j) },
    { ext,
      simp only [set.mem_empty_eq, set.mem_inter_eq, not_and, not_le, set.mem_set_of_eq, iff_false],
      rintro rfl,
      rwa not_le at h } }
end

end nat

end preorder

section linear_order

variable [linear_order Œπ]

lemma measurable [topological_space Œπ] [measurable_space Œπ]
  [borel_space Œπ] [order_topology Œπ] [second_countable_topology Œπ]
  {f : filtration Œπ m} {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) :
  measurable[hœÑ.measurable_space] œÑ :=
begin
  refine @measurable_of_Iic Œπ Œ± _ _ _ hœÑ.measurable_space _ _ _ _ _,
  simp_rw [hœÑ.measurable_set, set.preimage, set.mem_Iic],
  intros i j,
  rw (_ : {x | œÑ x ‚â§ i} ‚à© {x | œÑ x ‚â§ j} = {x | œÑ x ‚â§ min i j}),
  { exact f.mono (min_le_right i j) _ (hœÑ (min i j)) },
  { ext,
    simp only [set.mem_inter_eq, iff_self, le_min_iff, set.mem_set_of_eq] }
end

end linear_order

end is_stopping_time

section linear_order

/-! ## Stopped value and stopped process -/

/-- Given a map `u : Œπ ‚Üí Œ± ‚Üí E`, its stopped value with respect to the stopping
time `œÑ` is the map `x ‚Ü¶ u (œÑ x) x`. -/
def stopped_value (u : Œπ ‚Üí Œ± ‚Üí Œ≤) (œÑ : Œ± ‚Üí Œπ) : Œ± ‚Üí Œ≤ :=
Œª x, u (œÑ x) x

variable [linear_order Œπ]

/-- Given a map `u : Œπ ‚Üí Œ± ‚Üí E`, the stopped process with respect to `œÑ` is `u i x` if
`i ‚â§ œÑ x`, and `u (œÑ x) x` otherwise.

Intuitively, the stopped process stops evolving once the stopping time has occured. -/
def stopped_process (u : Œπ ‚Üí Œ± ‚Üí Œ≤) (œÑ : Œ± ‚Üí Œπ) : Œπ ‚Üí Œ± ‚Üí Œ≤ :=
Œª i x, u (min i (œÑ x)) x

lemma stopped_process_eq_of_le {u : Œπ ‚Üí Œ± ‚Üí Œ≤} {œÑ : Œ± ‚Üí Œπ}
  {i : Œπ} {x : Œ±} (h : i ‚â§ œÑ x) : stopped_process u œÑ i x = u i x :=
by simp [stopped_process, min_eq_left h]

lemma stopped_process_eq_of_ge {u : Œπ ‚Üí Œ± ‚Üí Œ≤} {œÑ : Œ± ‚Üí Œπ}
  {i : Œπ} {x : Œ±} (h : œÑ x ‚â§ i) : stopped_process u œÑ i x = u (œÑ x) x :=
by simp [stopped_process, min_eq_right h]

section prog_measurable

variables [measurable_space Œπ] [topological_space Œπ] [order_topology Œπ]
  [second_countable_topology Œπ] [borel_space Œπ]
  [measurable_space Œ≤]
  {u : Œπ ‚Üí Œ± ‚Üí Œ≤} {œÑ : Œ± ‚Üí Œπ} {f : filtration Œπ m}

lemma prog_measurable_min_stopping_time (hœÑ : is_stopping_time f œÑ) :
  prog_measurable f (Œª i x, min i (œÑ x)) :=
begin
  intro i,
  let m_prod : measurable_space (set.Iic i √ó Œ±) := measurable_space.prod _ (f i),
  let m_set : ‚àÄ t : set (set.Iic i √ó Œ±), measurable_space t :=
    Œª _, @subtype.measurable_space (set.Iic i √ó Œ±) _ m_prod,
  let s := {p : set.Iic i √ó Œ± | œÑ p.2 ‚â§ i},
  have hs : measurable_set[m_prod] s, from @measurable_snd (set.Iic i) Œ± _ (f i) _ (hœÑ i),
  have h_meas_fst : ‚àÄ t : set (set.Iic i √ó Œ±),
      measurable[m_set t] (Œª x : t, ((x : set.Iic i √ó Œ±).fst : Œπ)),
    from Œª t, (@measurable_subtype_coe (set.Iic i √ó Œ±) m_prod _).fst.subtype_coe,
  refine measurable_of_restrict_of_restrict_compl hs _ _,
  { refine @measurable.min _ _ _ _ _ (m_set s) _ _ _ _ _ (h_meas_fst s) _,
    refine @measurable_of_Iic Œπ s _ _ _ (m_set s) _ _ _ _ (Œª j, _),
    have h_set_eq : (Œª x : s, œÑ (x : set.Iic i √ó Œ±).snd) ‚Åª¬π' set.Iic j
      = (Œª x : s, (x : set.Iic i √ó Œ±).snd) ‚Åª¬π' {x | œÑ x ‚â§ min i j},
    { ext1 x,
      simp only [set.mem_preimage, set.mem_Iic, iff_and_self, le_min_iff, set.mem_set_of_eq],
      exact Œª _, x.prop, },
    rw h_set_eq,
    suffices h_meas : @measurable _ _ (m_set s) (f i) (Œª x : s, (x : set.Iic i √ó Œ±).snd),
      from h_meas (f.mono (min_le_left _ _) _ (hœÑ.measurable_set_le (min i j))),
    exact (@measurable_snd _ _ _ (f i)).comp (@measurable_subtype_coe _ m_prod _), },
  { suffices h_min_eq_left : (Œª x : s·∂ú, min ‚Üë((x : set.Iic i √ó Œ±).fst) (œÑ (x : set.Iic i √ó Œ±).snd))
      = Œª x : s·∂ú, ‚Üë((x : set.Iic i √ó Œ±).fst),
    { rw [set.restrict, h_min_eq_left],
      exact h_meas_fst _, },
    ext1 x,
    rw min_eq_left,
    have hx_fst_le : ‚Üë(x : set.Iic i √ó Œ±).fst ‚â§ i, from (x : set.Iic i √ó Œ±).fst.prop,
    refine hx_fst_le.trans (le_of_lt _),
    convert x.prop,
    simp only [not_le, set.mem_compl_eq, set.mem_set_of_eq], },
end

lemma prog_measurable.stopped_process (h : prog_measurable f u) (hœÑ : is_stopping_time f œÑ) :
  prog_measurable f (stopped_process u œÑ) :=
h.comp (prog_measurable_min_stopping_time hœÑ) (Œª i x, min_le_left _ _)

lemma prog_measurable.adapted_stopped_process
  (h : prog_measurable f u) (hœÑ : is_stopping_time f œÑ) :
  adapted f (stopped_process u œÑ) :=
(h.stopped_process hœÑ).adapted

lemma prog_measurable.measurable_stopped_process
  (hu : prog_measurable f u) (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable (stopped_process u œÑ i) :=
(hu.adapted_stopped_process hœÑ i).le (f.le _)

end prog_measurable

end linear_order

section nat
/-! ### Filtrations indexed by `‚Ñï` -/

open filtration

variables {f : filtration ‚Ñï m} {u : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} {œÑ œÄ : Œ± ‚Üí ‚Ñï}

lemma stopped_value_sub_eq_sum [add_comm_group Œ≤] (hle : œÑ ‚â§ œÄ) :
  stopped_value u œÄ - stopped_value u œÑ =
  Œª x, (‚àë i in finset.Ico (œÑ x) (œÄ x), (u (i + 1) - u i)) x :=
begin
  ext x,
  rw [finset.sum_Ico_eq_sub _ (hle x), finset.sum_range_sub, finset.sum_range_sub],
  simp [stopped_value],
end

lemma stopped_value_sub_eq_sum' [add_comm_group Œ≤] (hle : œÑ ‚â§ œÄ) {N : ‚Ñï} (hbdd : ‚àÄ x, œÄ x ‚â§ N) :
  stopped_value u œÄ - stopped_value u œÑ =
  Œª x, (‚àë i in finset.range (N + 1),
    set.indicator {x | œÑ x ‚â§ i ‚àß i < œÄ x} (u (i + 1) - u i)) x :=
begin
  rw stopped_value_sub_eq_sum hle,
  ext x,
  simp only [finset.sum_apply, finset.sum_indicator_eq_sum_filter],
  refine finset.sum_congr _ (Œª _ _, rfl),
  ext i,
  simp only [finset.mem_filter, set.mem_set_of_eq, finset.mem_range, finset.mem_Ico],
  exact ‚ü®Œª h, ‚ü®lt_trans h.2 (nat.lt_succ_iff.2 $ hbdd _), h‚ü©, Œª h, h.2‚ü©
end

section add_comm_monoid

variables [add_comm_monoid Œ≤]

/-- For filtrations indexed by `‚Ñï`, `adapted` and `prog_measurable` are equivalent. This lemma
provides `adapted f u ‚Üí prog_measurable f u`. See `prog_measurable.adapted` for the reverse
direction, which is true more generally. -/
lemma adapted.prog_measurable_of_nat [measurable_space Œ≤] [has_measurable_add‚ÇÇ Œ≤]
  (h : adapted f u) : prog_measurable f u :=
begin
  intro i,
  have : (Œª p : ‚Ü•(set.Iic i) √ó Œ±, u ‚Üë(p.fst) p.snd)
    = Œª p : ‚Ü•(set.Iic i) √ó Œ±, ‚àë j in finset.range (i + 1), if ‚Üëp.fst = j then u j p.snd else 0,
  { ext1 p,
    rw finset.sum_ite_eq,
    have hp_mem : (p.fst : ‚Ñï) ‚àà finset.range (i + 1) := finset.mem_range_succ_iff.mpr p.fst.prop,
    simp only [hp_mem, if_true], },
  rw this,
  refine finset.measurable_sum _ (Œª j hj, measurable.ite _ _ _),
  { suffices h_meas : measurable[measurable_space.prod _ (f i)]
        (Œª a : ‚Ü•(set.Iic i) √ó Œ±, (a.fst : ‚Ñï)),
      from h_meas (measurable_set_singleton j),
    exact (@measurable_fst _ Œ± _ (f i)).subtype_coe, },
  { have h_le : j ‚â§ i, from finset.mem_range_succ_iff.mp hj,
    exact (measurable.le (f.mono h_le) (h j)).comp (@measurable_snd _ Œ± _ (f i)), },
  { exact @measurable_const _ (set.Iic i √ó Œ±) _ (measurable_space.prod _ (f i)) _, },
end

/-- For filtrations indexed by `‚Ñï`, the stopped process obtained from an adapted process is
adapted. -/
lemma adapted.stopped_process_of_nat [measurable_space Œ≤] [has_measurable_add‚ÇÇ Œ≤]
  (hu : adapted f u) (hœÑ : is_stopping_time f œÑ) :
  adapted f (stopped_process u œÑ) :=
(hu.prog_measurable_of_nat.stopped_process hœÑ).adapted

lemma adapted.measurable_stopped_process_of_nat [measurable_space Œ≤] [has_measurable_add‚ÇÇ Œ≤]
  (hœÑ : is_stopping_time f œÑ) (hu : adapted f u) (n : ‚Ñï) :
  measurable (stopped_process u œÑ n) :=
hu.prog_measurable_of_nat.measurable_stopped_process hœÑ n

lemma stopped_value_eq {N : ‚Ñï} (hbdd : ‚àÄ x, œÑ x ‚â§ N) :
  stopped_value u œÑ =
  Œª x, (‚àë i in finset.range (N + 1), set.indicator {x | œÑ x = i} (u i)) x :=
begin
  ext y,
  rw [stopped_value, finset.sum_apply, finset.sum_eq_single (œÑ y)],
  { rw set.indicator_of_mem,
    exact rfl },
  { exact Œª i hi hneq, set.indicator_of_not_mem hneq.symm _ },
  { intro hy,
    rw set.indicator_of_not_mem,
    exact Œª _, hy (finset.mem_range.2 $ lt_of_le_of_lt (hbdd _) (nat.lt_succ_self _)) }
end

lemma stopped_process_eq (n : ‚Ñï) :
  stopped_process u œÑ n =
  set.indicator {a | n ‚â§ œÑ a} (u n) +
    ‚àë i in finset.range n, set.indicator {a | œÑ a = i} (u i) :=
begin
  ext x,
  rw [pi.add_apply, finset.sum_apply],
  cases le_or_lt n (œÑ x),
  { rw [stopped_process_eq_of_le h, set.indicator_of_mem, finset.sum_eq_zero, add_zero],
    { intros m hm,
      rw finset.mem_range at hm,
      exact set.indicator_of_not_mem ((lt_of_lt_of_le hm h).ne.symm) _ },
    { exact h } },
  { rw [stopped_process_eq_of_ge (le_of_lt h), finset.sum_eq_single_of_mem (œÑ x)],
    { rw [set.indicator_of_not_mem, zero_add, set.indicator_of_mem],
      { exact rfl }, -- refl does not work
      { exact not_le.2 h } },
    { rwa [finset.mem_range] },
    { intros b hb hneq,
      rw set.indicator_of_not_mem,
      exact hneq.symm } },
end

end add_comm_monoid

section normed_group

variables [measurable_space Œ≤] [normed_group Œ≤] [has_measurable_add‚ÇÇ Œ≤] [borel_space Œ≤] {p : ‚Ñù‚â•0‚àû}
  {Œº : measure Œ±}

lemma mem_‚Ñíp_stopped_process (hœÑ : is_stopping_time f œÑ) (hu : ‚àÄ n, mem_‚Ñíp (u n) p Œº) (n : ‚Ñï) :
  mem_‚Ñíp (stopped_process u œÑ n) p Œº :=
begin
  rw stopped_process_eq,
  refine mem_‚Ñíp.add _ _,
  { exact mem_‚Ñíp.indicator (f.le n {a : Œ± | n ‚â§ œÑ a} (hœÑ.measurable_set_ge n)) (hu n) },
  { suffices : mem_‚Ñíp (Œª x, ‚àë (i : ‚Ñï) in finset.range n, {a : Œ± | œÑ a = i}.indicator (u i) x) p Œº,
    { convert this, ext1 x, simp only [finset.sum_apply] },
    refine mem_‚Ñíp_finset_sum _ (Œª i hi, mem_‚Ñíp.indicator _ (hu i)),
    exact f.le i {a : Œ± | œÑ a = i} (hœÑ.measurable_set_eq i) },
end

lemma integrable_stopped_process (hœÑ : is_stopping_time f œÑ)
  (hu : ‚àÄ n, integrable (u n) Œº) (n : ‚Ñï) :
  integrable (stopped_process u œÑ n) Œº :=
by { simp_rw ‚Üê mem_‚Ñíp_one_iff_integrable at hu ‚ä¢, exact mem_‚Ñíp_stopped_process hœÑ hu n, }

lemma mem_‚Ñíp_stopped_value (hœÑ : is_stopping_time f œÑ)
  (hu : ‚àÄ n, mem_‚Ñíp (u n) p Œº) {N : ‚Ñï} (hbdd : ‚àÄ x, œÑ x ‚â§ N) :
  mem_‚Ñíp (stopped_value u œÑ) p Œº :=
begin
  rw stopped_value_eq hbdd,
  suffices : mem_‚Ñíp (Œª x, ‚àë (i : ‚Ñï) in finset.range (N + 1),
    {a : Œ± | œÑ a = i}.indicator (u i) x) p Œº,
  { convert this, ext1 x, simp only [finset.sum_apply] },
  refine mem_‚Ñíp_finset_sum _ (Œª i hi, mem_‚Ñíp.indicator _ (hu i)),
  exact f.le i {a : Œ± | œÑ a = i} (hœÑ.measurable_set_eq i)
end

lemma integrable_stopped_value (hœÑ : is_stopping_time f œÑ)
  (hu : ‚àÄ n, integrable (u n) Œº) {N : ‚Ñï} (hbdd : ‚àÄ x, œÑ x ‚â§ N) :
  integrable (stopped_value u œÑ) Œº :=
by { simp_rw ‚Üê mem_‚Ñíp_one_iff_integrable at hu ‚ä¢, exact mem_‚Ñíp_stopped_value hœÑ hu hbdd, }

end normed_group

end nat

end measure_theory
