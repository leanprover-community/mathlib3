/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/

import probability.ident_distrib
import measure_theory.function.l2_space
import measure_theory.integral.interval_integral
import analysis.specific_limits.floor_pow
import analysis.p_series
import analysis.asymptotics.specific_asymptotics

/-!
# The strong law of large numbers

We prove the strong law of large numbers, in `probability_theory.strong_law_ae`:
If `X n` is a sequence of independent identically distributed integrable real-valued random
variables, then `âˆ‘ i in range n, X i / n` converges almost surely to `ğ”¼[X 0]`.
We give here the strong version, due to Etemadi, that only requires pairwise independence.

## Implementation

We follow the proof by Etemadi, which goes as follows.

It suffices to prove the result for nonnegative `X`, as one can prove the general result by
splitting `X` into its positive part and negative part.
Consider `Xâ‚™` a sequence of integrable identically distributed pairwise independent random
variables. Let `Yâ‚™` be the truncation of `Xâ‚™` up to `n`. We claim that
* Almost surely, `Xâ‚™ = Yâ‚™` but for finitely many indices. Indeed, `âˆ‘ â„™ (Xâ‚™ â‰  Yâ‚™)` is bounded by
  `1 + ğ”¼[X]` (see `sum_prob_mem_Ioc_le` and `tsum_prob_mem_Ioi_lt_top`).
* Let `c > 1`. Along the sequence `n = c ^ k`, then `(âˆ‘_{i=0}^{n-1} Yáµ¢ - ğ”¼[Yáµ¢])/n` converges
  to `0`. This follows from a variance control, as
  âˆ‘_k â„™ (|âˆ‘_{i=0}^{c^k-1} Yáµ¢ - ğ”¼[Yáµ¢]| > c^k Îµ)
    â‰¤ âˆ‘_k (c^k Îµ)^{-2} âˆ‘_{i=0}^{c^k-1} Var[Yáµ¢]    (by Markov inequality)
    â‰¤ âˆ‘_i (C/i^2) Var[Yáµ¢]                         (as âˆ‘_{c^k > i} 1/(c^k)^2 â‰¤ C/i^2)
    â‰¤ âˆ‘_i (C/i^2) ğ”¼[Yáµ¢^2]
    â‰¤ 2C ğ”¼[X^2]                                   (see `sum_variance_truncation_le`)
* As `ğ”¼[Yáµ¢]` converges to `ğ”¼[X]`, it follows from the two previous items and Cesaro that, along
  the sequence `n = c^k`, one has `(âˆ‘_{i=0}^{n-1} Xáµ¢) / n â†’ ğ”¼[X]`.
* To generalize it to all sequences, we use the fact that `âˆ‘_{i=0}^{n-1} Xáµ¢` is nondecreasing and
  that, if `c` is close enough to `1`, the gap between `c^k` and `c^(k+1)` is small.
-/

noncomputable theory

open measure_theory filter finset asymptotics
open set (indicator)

open_locale topological_space big_operators measure_theory probability_theory ennreal nnreal

namespace probability_theory

/-! ### Prerequisites on truncations -/

section truncation

variables {Î± : Type*}

/-- Truncating a real-valued function to the interval `(-A, A]`. -/
def truncation {Î± : Type*} (f : Î± â†’ â„) (A : â„) :=
(indicator (set.Ioc (-A) A) id) âˆ˜ f

variables {m : measurable_space Î±} {Î¼ : measure Î±} {f : Î± â†’ â„}

lemma _root_.measure_theory.ae_strongly_measurable.truncation
  (hf : ae_strongly_measurable f Î¼) {A : â„} :
  ae_strongly_measurable (truncation f A) Î¼ :=
begin
  apply ae_strongly_measurable.comp_ae_measurable _ hf.ae_measurable,
  exact (strongly_measurable_id.indicator measurable_set_Ioc).ae_strongly_measurable,
end

lemma abs_truncation_le_bound (f : Î± â†’ â„) (A : â„) (x : Î±) :
  abs (truncation f A x) â‰¤ |A| :=
begin
  simp only [truncation, set.indicator, set.mem_Icc, id.def, function.comp_app],
  split_ifs,
  { simp only [real.norm_eq_abs, abs_le],
    split,
    { linarith [neg_abs_le_neg A, h.1] },
    { linarith [le_abs_self A, h.2] } },
  { simp [abs_nonneg] }
end

@[simp] lemma truncation_zero (f : Î± â†’ â„) :
  truncation f 0 = 0 :=
by simp [truncation]

lemma abs_truncation_le_abs_self (f : Î± â†’ â„) (A : â„) (x : Î±) :
  |truncation f A x| â‰¤ |f x| :=
begin
  simp only [truncation, indicator, set.mem_Icc, id.def, function.comp_app],
  split_ifs,
  { exact le_rfl },
  { simp [abs_nonneg] },
end

lemma truncation_eq_self {f : Î± â†’ â„} {A : â„} {x : Î±} (h : |f x| < A) :
  truncation f A x = f x :=
begin
  simp only [truncation, indicator, set.mem_Icc, id.def, function.comp_app, ite_eq_left_iff,
    not_le],
  assume H,
  apply H.elim,
  simp [(abs_lt.1 h).1, (abs_lt.1 h).2.le],
end

lemma truncation_eq_of_nonneg {f : Î± â†’ â„} {A : â„}  (h : âˆ€ x, 0 â‰¤ f x) :
  truncation f A = (indicator (set.Ioc 0 A) id) âˆ˜ f :=
begin
  ext x,
  rcases lt_trichotomy 0 (f x) with hx|hx|hx,
  { simp only [truncation, indicator, hx, set.mem_Ioc, id.def, function.comp_app, true_and],
    by_cases h'x : f x â‰¤ A,
    { have : - A < f x, by linarith [h x],
      simp only [this, true_and]},
    { simp only [h'x, and_false]} },
  { simp only [truncation, indicator, hx, id.def, function.comp_app, if_t_t]},
  { linarith [h x] },
end

lemma _root_.measure_theory.ae_strongly_measurable.mem_â„’p_truncation [is_finite_measure Î¼]
  (hf : ae_strongly_measurable f Î¼) {A : â„} {p : â„â‰¥0âˆ} :
  mem_â„’p (truncation f A) p Î¼ :=
begin
  refine mem_â„’p.mem_â„’p_of_exponent_le _ le_top,
  apply mem_â„’p_top_of_bound hf.truncation _
    (eventually_of_forall (Î» x, abs_truncation_le_bound _ _ _)),
end

lemma _root_.measure_theory.ae_strongly_measurable.integrable_truncation [is_finite_measure Î¼]
  (hf : ae_strongly_measurable f Î¼) {A : â„} :
  integrable (truncation f A) Î¼ :=
by { rw â† mem_â„’p_one_iff_integrable, exact hf.mem_â„’p_truncation }

lemma moment_truncation_eq_interval_integral (hf : ae_strongly_measurable f Î¼) {A : â„}
  (hA : 0 â‰¤ A) {n : â„•} (hn : n â‰  0) :
  âˆ« x, (truncation f A x) ^ n âˆ‚Î¼ = âˆ« y in (-A)..A, y ^ n âˆ‚(measure.map f Î¼) :=
begin
  have M : measurable_set (set.Ioc (-A) A) := measurable_set_Ioc,
  change âˆ« x, (Î» z, (indicator (set.Ioc (-A) A) id z) ^ n) (f x) âˆ‚Î¼ = _,
  rw [â† integral_map hf.ae_measurable, interval_integral.integral_of_le, â† integral_indicator M],
  { simp only [indicator, zero_pow' _ hn, id.def, ite_pow] },
  { linarith },
  { apply measurable.ae_strongly_measurable,
    convert (measurable_id.pow_const n).indicator M,
    simp only [indicator, zero_pow' _ hn, ite_pow] }
end

lemma moment_truncation_eq_interval_integral_of_nonneg (hf : ae_strongly_measurable f Î¼) {A : â„}
  {n : â„•} (hn : n â‰  0) (h'f : 0 â‰¤ f) :
  âˆ« x, (truncation f A x) ^ n âˆ‚Î¼ = âˆ« y in 0..A, y ^ n âˆ‚(measure.map f Î¼) :=
begin
  have M : measurable_set (set.Ioc 0 A) := measurable_set_Ioc,
  have M' : measurable_set (set.Ioc A 0) := measurable_set_Ioc,
  rw truncation_eq_of_nonneg h'f,
  change âˆ« x, (Î» z, (indicator (set.Ioc 0 A) id z) ^ n) (f x) âˆ‚Î¼ = _,
  rcases le_or_lt 0 A with hA | hA,
  { rw [â† integral_map hf.ae_measurable, interval_integral.integral_of_le hA,
        â† integral_indicator M],
    { simp only [indicator, zero_pow' _ hn, id.def, ite_pow] },
    { apply measurable.ae_strongly_measurable,
      convert (measurable_id.pow_const n).indicator M,
      simp only [indicator, zero_pow' _ hn, ite_pow] } },
  { rw [â† integral_map hf.ae_measurable, interval_integral.integral_of_ge hA.le,
        â† integral_indicator M'],
    { simp only [set.Ioc_eq_empty (not_lt.2 hA.le), zero_pow' _ hn, set.indicator_empty,
        integral_const, algebra.id.smul_eq_mul, mul_zero, zero_eq_neg],
      apply integral_eq_zero_of_ae,
      have : âˆ€áµ x âˆ‚(measure.map f Î¼), (0 : â„) â‰¤ x :=
        (ae_map_iff hf.ae_measurable measurable_set_Ici).2 (eventually_of_forall h'f),
      filter_upwards [this] with x hx,
      simp only [set.mem_Ioc, pi.zero_apply, ite_eq_right_iff, and_imp],
      assume h'x h''x,
      have : x = 0, by linarith,
      simp [this, zero_pow' _ hn] },
    { apply measurable.ae_strongly_measurable,
      convert (measurable_id.pow_const n).indicator M,
      simp only [indicator, zero_pow' _ hn, ite_pow] } }
end

lemma integral_truncation_eq_interval_integral (hf : ae_strongly_measurable f Î¼) {A : â„}
  (hA : 0 â‰¤ A) :
  âˆ« x, truncation f A x âˆ‚Î¼ = âˆ« y in (-A)..A, y âˆ‚(measure.map f Î¼) :=
by simpa using moment_truncation_eq_interval_integral hf hA one_ne_zero

lemma integral_truncation_eq_interval_integral_of_nonneg (hf : ae_strongly_measurable f Î¼) {A : â„}
  (h'f : 0 â‰¤ f) :
  âˆ« x, truncation f A x âˆ‚Î¼ = âˆ« y in 0..A, y âˆ‚(measure.map f Î¼) :=
by simpa using moment_truncation_eq_interval_integral_of_nonneg hf one_ne_zero h'f

lemma integral_truncation_le_integral_of_nonneg
  (hf : integrable f Î¼) (h'f : 0 â‰¤ f) {A : â„} :
  âˆ« x, truncation f A x âˆ‚Î¼ â‰¤ âˆ« x, f x âˆ‚Î¼ :=
begin
  apply integral_mono_of_nonneg (eventually_of_forall (Î» x, _)) hf (eventually_of_forall (Î» x, _)),
  { simp only [truncation, indicator, pi.zero_apply, set.mem_Ioc, id.def, function.comp_app],
    split_ifs,
    { exact h'f x },
    { exact le_rfl } },
  { simp only [truncation, indicator, set.mem_Ioc, id.def, function.comp_app],
    split_ifs,
    { exact le_rfl },
    { exact h'f x } }
end

/-- If a function is integrable, then the integral of its truncated versions converges to the
integral of the whole function. -/
lemma tendsto_integral_truncation {f : Î± â†’ â„} (hf : integrable f Î¼) :
  tendsto (Î» A, âˆ« x, truncation f A x âˆ‚Î¼) at_top (ğ“ (âˆ« x, f x âˆ‚Î¼)) :=
begin
  refine tendsto_integral_filter_of_dominated_convergence (Î» x, abs (f x)) _ _ _ _,
  { exact eventually_of_forall (Î» A, hf.ae_strongly_measurable.truncation) },
  { apply eventually_of_forall (Î» A, _),
    apply eventually_of_forall (Î» x, _),
    rw real.norm_eq_abs,
    exact abs_truncation_le_abs_self _ _ _ },
  { apply hf.abs },
  { apply eventually_of_forall (Î» x, _),
    apply tendsto_const_nhds.congr' _,
    filter_upwards [Ioi_mem_at_top (abs (f x))] with A hA,
    exact (truncation_eq_self hA).symm },
end

open probability_theory

lemma ident_distrib.truncation {Î² : Type*} [measurable_space Î²] {Î½ : measure Î²}
  {f : Î± â†’ â„} {g : Î² â†’ â„} (h : ident_distrib f g Î¼ Î½) {A : â„} :
  ident_distrib (truncation f A) (truncation g A) Î¼ Î½ :=
h.comp (strongly_measurable_id.indicator measurable_set_Ioc).measurable

end truncation

variables {Î© : Type*} [measure_space Î©] [is_probability_measure (â„™ : measure Î©)]

lemma sum_prob_mem_Ioc_le
  {X : Î© â†’ â„} (hint : integrable X) (hnonneg : 0 â‰¤ X) {K : â„•} {N : â„•} (hKN : K â‰¤ N) :
  âˆ‘ j in range K, â„™ {Ï‰ | X Ï‰ âˆˆ set.Ioc (j : â„) N} â‰¤ ennreal.of_real (ğ”¼[X] + 1) :=
begin
  let Ï : measure â„ := measure.map X â„™,
  haveI : is_probability_measure Ï := is_probability_measure_map hint.ae_measurable,
  have A : âˆ‘ j in range K, âˆ« x in j..N, (1 : â„) âˆ‚Ï â‰¤ ğ”¼[X] + 1, from calc
  âˆ‘ j in range K, âˆ« x in j..N, (1 : â„) âˆ‚Ï
      = âˆ‘ j in range K, âˆ‘ i in Ico j N, âˆ« x in i..(i+1 : â„•), (1 : â„) âˆ‚Ï :
    begin
      apply sum_congr rfl (Î» j hj, _),
      rw interval_integral.sum_integral_adjacent_intervals_Ico ((mem_range.1 hj).le.trans hKN),
      assume k hk,
      exact continuous_const.interval_integrable _ _,
    end
  ... = âˆ‘ i in range N, âˆ‘ j in range (min (i+1) K), âˆ« x in i..(i+1 : â„•), (1 : â„) âˆ‚Ï :
    begin
      simp_rw [sum_sigma'],
      refine sum_bij' (Î» (p : (Î£ (i : â„•), â„•)) hp, (âŸ¨p.2, p.1âŸ© : (Î£ (i : â„•), â„•))) _ (Î» a ha, rfl)
        (Î» (p : (Î£ (i : â„•), â„•)) hp, (âŸ¨p.2, p.1âŸ© : (Î£ (i : â„•), â„•))) _ _ _,
      { rintros âŸ¨i, jâŸ© hij,
        simp only [mem_sigma, mem_range, mem_Ico] at hij,
        simp only [hij, nat.lt_succ_iff.2 hij.2.1, mem_sigma, mem_range, lt_min_iff, and_self] },
      { rintros âŸ¨i, jâŸ© hij,
        simp only [mem_sigma, mem_range, lt_min_iff] at hij,
        simp only [hij, nat.lt_succ_iff.1 hij.2.1, mem_sigma, mem_range, mem_Ico, and_self] },
      { rintros âŸ¨i, jâŸ© hij, refl },
      { rintros âŸ¨i, jâŸ© hij, refl },
    end
  ... â‰¤ âˆ‘ i in range N, (i + 1) * âˆ« x in i..(i+1 : â„•), (1 : â„) âˆ‚Ï :
    begin
      apply sum_le_sum (Î» i hi, _),
      simp only [nat.cast_add, nat.cast_one, sum_const, card_range, nsmul_eq_mul, nat.cast_min],
      refine mul_le_mul_of_nonneg_right (min_le_left _ _) _,
      apply interval_integral.integral_nonneg,
      { simp only [le_add_iff_nonneg_right, zero_le_one] },
      { simp only [zero_le_one, implies_true_iff], }
    end
  ... â‰¤ âˆ‘ i in range N, âˆ« x in i..(i+1 : â„•), (x + 1) âˆ‚Ï :
    begin
      apply sum_le_sum (Î» i hi, _),
      have I : (i : â„) â‰¤ (i + 1 : â„•),
        by simp only [nat.cast_add, nat.cast_one, le_add_iff_nonneg_right, zero_le_one],
      simp_rw [interval_integral.integral_of_le I, â† integral_mul_left],
      apply set_integral_mono_on,
      { exact continuous_const.integrable_on_Ioc },
      { exact (continuous_id.add continuous_const).integrable_on_Ioc },
      { exact measurable_set_Ioc },
      { assume x hx,
        simp only [nat.cast_add, nat.cast_one, set.mem_Ioc] at hx,
        simp [hx.1.le] }
    end
  ... = âˆ« x in 0..N, x + 1 âˆ‚Ï :
    begin
      rw interval_integral.sum_integral_adjacent_intervals (Î» k hk, _),
      { refl },
      { exact (continuous_id.add continuous_const).interval_integrable _ _ }
    end
  ... = âˆ« x in 0..N, x âˆ‚Ï + âˆ« x in 0..N, 1 âˆ‚Ï :
    begin
      rw interval_integral.integral_add,
      { exact continuous_id.interval_integrable _ _ },
      { exact continuous_const.interval_integrable _ _ },
    end
  ... = ğ”¼[truncation X N] + âˆ« x in 0..N, 1 âˆ‚Ï :
    by rw integral_truncation_eq_interval_integral_of_nonneg hint.1 hnonneg
  ... â‰¤ ğ”¼[X] + âˆ« x in 0..N, 1 âˆ‚Ï :
    add_le_add_right (integral_truncation_le_integral_of_nonneg hint hnonneg) _
  ... â‰¤ ğ”¼[X] + 1 :
    begin
      refine add_le_add le_rfl _,
      rw interval_integral.integral_of_le (nat.cast_nonneg _),
      simp only [integral_const, measure.restrict_apply', measurable_set_Ioc, set.univ_inter,
        algebra.id.smul_eq_mul, mul_one],
      rw â† ennreal.one_to_real,
      exact ennreal.to_real_mono ennreal.one_ne_top prob_le_one,
    end,
  have B : âˆ€ a b, â„™ {Ï‰ | X Ï‰ âˆˆ set.Ioc a b} = ennreal.of_real (âˆ« x in set.Ioc a b, (1 : â„) âˆ‚Ï),
  { assume a b,
    rw of_real_integral_on_one Ï _,
    rw measure.map_apply_of_ae_measurable hint.ae_measurable measurable_set_Ioc,
    refl },
  calc âˆ‘ j in range K, â„™ {Ï‰ | X Ï‰ âˆˆ set.Ioc (j : â„) N}
      = âˆ‘ j in range K, ennreal.of_real (âˆ« x in set.Ioc (j : â„) N, (1 : â„) âˆ‚Ï) :
    by simp_rw B
  ... = ennreal.of_real (âˆ‘ j in range K, âˆ« x in set.Ioc (j : â„) N, (1 : â„) âˆ‚Ï) :
    begin
      rw ennreal.of_real_sum_of_nonneg,
      simp only [integral_const, algebra.id.smul_eq_mul, mul_one, ennreal.to_real_nonneg,
        implies_true_iff],
    end
  ... = ennreal.of_real (âˆ‘ j in range K, âˆ« x in (j : â„)..N, (1 : â„) âˆ‚Ï) :
    begin
      congr' 1,
      refine sum_congr rfl (Î» j hj, _),
      rw interval_integral.integral_of_le (nat.cast_le.2 ((mem_range.1 hj).le.trans hKN)),

    end
  ... â‰¤ ennreal.of_real (ğ”¼[X] + 1) : ennreal.of_real_le_of_real A
end

lemma tsum_prob_mem_Ioi_lt_top
  {X : Î© â†’ â„} (hint : integrable X) (hnonneg : 0 â‰¤ X) :
  âˆ‘' (j : â„•), â„™ {Ï‰ | X Ï‰ âˆˆ set.Ioi (j : â„)} < âˆ :=
begin
  suffices : âˆ€ (K : â„•), âˆ‘ j in range K, â„™ {Ï‰ | X Ï‰ âˆˆ set.Ioi (j : â„)} â‰¤ ennreal.of_real (ğ”¼[X] + 1),
  { apply (le_of_tendsto_of_tendsto (ennreal.tendsto_nat_tsum _) tendsto_const_nhds
      (eventually_of_forall this)).trans_lt ennreal.of_real_lt_top },
  assume K,
  have A : tendsto (Î» (N : â„•), âˆ‘ j in range K, â„™ {Ï‰ | X Ï‰ âˆˆ set.Ioc (j : â„) N})
    at_top (ğ“ (âˆ‘ j in range K, â„™ {Ï‰ | X Ï‰ âˆˆ set.Ioi (j : â„)})),
  { refine tendsto_finset_sum _ (Î» i hi, _),
    have : {Ï‰ | X Ï‰ âˆˆ set.Ioi (i : â„)} = â‹ƒ (N : â„•), {Ï‰ | X Ï‰ âˆˆ set.Ioc (i : â„) N},
    { apply set.subset.antisymm _ _,
      { assume Ï‰ hÏ‰,
        obtain âŸ¨N, hNâŸ© : âˆƒ (N : â„•), X Ï‰ â‰¤ N := exists_nat_ge (X Ï‰),
        exact set.mem_Union.2 âŸ¨N, hÏ‰, hNâŸ© },
      { simp only [set.mem_Ioc, set.mem_Ioi, set.Union_subset_iff, set.set_of_subset_set_of,
          implies_true_iff] {contextual := tt} } },
    rw this,
    apply tendsto_measure_Union,
    assume m n hmn x hx,
    exact âŸ¨hx.1, hx.2.trans (nat.cast_le.2 hmn)âŸ© },
  apply le_of_tendsto_of_tendsto A tendsto_const_nhds,
  filter_upwards [Ici_mem_at_top K] with N hN,
  exact sum_prob_mem_Ioc_le hint hnonneg hN
end

lemma sum_variance_truncation_le
  {X : Î© â†’ â„} (hint : integrable X) (hnonneg : 0 â‰¤ X) (K : â„•) :
  âˆ‘ j in range K, ((j : â„) ^ 2) â»Â¹ * ğ”¼[(truncation X j) ^ 2] â‰¤ 2 * ğ”¼[X] :=
begin
  set Y := Î» (n : â„•), truncation X n,
  let Ï : measure â„ := measure.map X â„™,
  have Y2 : âˆ€ n, ğ”¼[Y n ^ 2] = âˆ« x in 0..n, x ^ 2 âˆ‚Ï,
  { assume n,
    change ğ”¼[Î» x, (Y n x)^2] = _,
    rw [moment_truncation_eq_interval_integral_of_nonneg hint.1 two_ne_zero hnonneg] },
  calc âˆ‘ j in range K, ((j : â„) ^ 2) â»Â¹ * ğ”¼[Y j ^ 2]
      = âˆ‘ j in range K, ((j : â„) ^ 2) â»Â¹ * âˆ« x in 0..j, x ^ 2 âˆ‚Ï :
    by simp_rw [Y2]
  ... = âˆ‘ j in range K, ((j : â„) ^ 2) â»Â¹ * âˆ‘ k in range j, âˆ« x in k..(k+1 : â„•), x ^ 2 âˆ‚Ï :
    begin
      congr' 1 with j,
      congr' 1,
      rw interval_integral.sum_integral_adjacent_intervals,
      { refl },
      assume k hk,
      exact (continuous_id.pow _).interval_integrable _ _,
    end
  ... = âˆ‘ k in range K, (âˆ‘ j in Ioo k K, ((j : â„) ^ 2) â»Â¹) * âˆ« x in k..(k+1 : â„•), x ^ 2 âˆ‚Ï :
    begin
      simp_rw [mul_sum, sum_mul, sum_sigma'],
      refine sum_bij' (Î» (p : (Î£ (i : â„•), â„•)) hp, (âŸ¨p.2, p.1âŸ© : (Î£ (i : â„•), â„•))) _ (Î» a ha, rfl)
        (Î» (p : (Î£ (i : â„•), â„•)) hp, (âŸ¨p.2, p.1âŸ© : (Î£ (i : â„•), â„•))) _ _ _,
      { rintros âŸ¨i, jâŸ© hij,
        simp only [mem_sigma, mem_range, mem_filter] at hij,
        simp [hij, mem_sigma, mem_range, and_self, hij.2.trans hij.1], },
      { rintros âŸ¨i, jâŸ© hij,
        simp only [mem_sigma, mem_range, mem_Ioo] at hij,
        simp only [hij, mem_sigma, mem_range, and_self], },
      { rintros âŸ¨i, jâŸ© hij, refl },
      { rintros âŸ¨i, jâŸ© hij, refl },
    end
  ... â‰¤ âˆ‘ k in range K, (2/ (k+1)) * âˆ« x in k..(k+1 : â„•), x ^ 2 âˆ‚Ï :
    begin
      apply sum_le_sum (Î» k hk, _),
      simp_rw [â† one_div],
      refine mul_le_mul_of_nonneg_right (sum_Ioo_div_sq_le _ _) _,
      refine interval_integral.integral_nonneg_of_forall _ (Î» u, sq_nonneg _),
      simp only [nat.cast_add, nat.cast_one, le_add_iff_nonneg_right, zero_le_one],
    end
  ... â‰¤ âˆ‘ k in range K, âˆ« x in k..(k+1 : â„•), 2 * x âˆ‚Ï :
    begin
      apply sum_le_sum (Î» k hk, _),
      have Ik : (k : â„) â‰¤ (k + 1 : â„•), by simp,
      rw â† interval_integral.integral_const_mul,
      rw [interval_integral.integral_of_le Ik, interval_integral.integral_of_le Ik],
      apply set_integral_mono_on,
      { apply continuous.integrable_on_Ioc,
        exact continuous_const.mul (continuous_pow 2) },
      { apply continuous.integrable_on_Ioc,
        exact continuous_const.mul continuous_id' },
      { exact measurable_set_Ioc },
      { assume x hx,
        calc 2 / (â†‘k + 1) * x ^ 2 = (x / (k+1)) * (2 * x) : by ring_exp
        ... â‰¤ 1 * (2 * x) :
          begin
            apply mul_le_mul_of_nonneg_right _
              (mul_nonneg zero_le_two ((nat.cast_nonneg k).trans hx.1.le)),
            apply (div_le_one _).2 hx.2,
            simp only [nat.cast_add, nat.cast_one],
            linarith only [show (0 : â„) â‰¤ k, from  nat.cast_nonneg k],
          end
        ... = 2 * x : by rw one_mul }
    end
  ... = 2 * âˆ« x in (0 : â„)..K, x âˆ‚Ï :
    begin
      rw interval_integral.sum_integral_adjacent_intervals (Î» k hk, _),
      swap, { exact (continuous_const.mul continuous_id').interval_integrable _ _ },
      rw interval_integral.integral_const_mul,
      refl
    end
  ... â‰¤ 2 * ğ”¼[X] :
    begin
      apply mul_le_mul_of_nonneg_left _ zero_le_two,
      calc âˆ« x in 0..â†‘K, x âˆ‚Ï = âˆ« a, truncation X K a :
        by rw integral_truncation_eq_interval_integral_of_nonneg hint.1 hnonneg
      ... â‰¤ âˆ« (a : Î©), X a :
        begin
          apply integral_mono_of_nonneg (eventually_of_forall (Î» x, _)) hint
            (eventually_of_forall (Î» x, _)),
          { simp only [truncation, indicator, pi.zero_apply, set.mem_Ioc, id.def,
              function.comp_app],
            split_ifs,
            { exact hnonneg x },
            { exact le_rfl } },
          { simp only [truncation, indicator, set.mem_Ioc, id.def, function.comp_app],
            split_ifs,
            { exact le_rfl },
            { exact hnonneg x } }
        end
    end
end


section strong_law_aux

variables (X : â„• â†’ Î© â†’ â„) (hint : integrable (X 0))
  (hindep : pairwise (Î» i j, indep_fun (X i) (X j)))
  (hident : âˆ€ i, ident_distrib (X i) (X 0))
  (hnonneg : âˆ€ i Ï‰, 0 â‰¤ X i Ï‰)

include X hint hindep hident hnonneg

lemma strong_law_aux1 {c : â„} (c_one : 1 < c) {Îµ : â„} (Îµpos : 0 < Îµ) :
  âˆ€áµ Ï‰, âˆ€á¶  (n : â„•) in at_top,
    |âˆ‘ i in range âŒŠc^nâŒ‹â‚Š, truncation (X i) i Ï‰ - ğ”¼[âˆ‘ i in range âŒŠc^nâŒ‹â‚Š, truncation (X i) i]|
      < Îµ * âŒŠc^nâŒ‹â‚Š :=
begin
  have c_pos : 0 < c := zero_lt_one.trans c_one,
  let Ï : measure â„ := measure.map (X 0) â„™,
  have hX : âˆ€ i, ae_strongly_measurable (X i) â„™ :=
    Î» i, (hident i).symm.ae_strongly_measurable_snd hint.1,
  have A : âˆ€ i, strongly_measurable (indicator (set.Ioc (-i : â„) i) id) :=
    Î» i, strongly_measurable_id.indicator measurable_set_Ioc,
  set Y := Î» (n : â„•), truncation (X n) n with hY,
  set S := Î» n, âˆ‘ i in range n, Y i with hS,
  let u : â„• â†’ â„• := Î» n, âŒŠc ^ nâŒ‹â‚Š,
  have u_mono : monotone u :=
    Î» i j hij, nat.floor_mono (pow_le_pow c_one.le hij),
  have I1 : âˆ€ K, âˆ‘ j in range K, ((j : â„) ^ 2) â»Â¹ * Var[Y j] â‰¤ 2 * ğ”¼[X 0],
  { assume K,
    calc âˆ‘ j in range K, ((j : â„) ^ 2) â»Â¹ * Var[Y j] â‰¤
      âˆ‘ j in range K, ((j : â„) ^ 2) â»Â¹ * ğ”¼[(truncation (X 0) j)^2] :
      begin
        apply sum_le_sum (Î» j hj, _),
        refine mul_le_mul_of_nonneg_left _ (inv_nonneg.2 (sq_nonneg _)),
        rw (hident j).truncation.variance_eq,
        exact variance_le_expectation_sq,
      end
      ... â‰¤ 2 * ğ”¼[X 0] : sum_variance_truncation_le hint (hnonneg 0) K },
  let C := (c ^ 5 * (c - 1) â»Â¹ ^ 3) * (2 * ğ”¼[X 0]),
  have I2 : âˆ€ N, âˆ‘ i in range N, ((u i : â„) ^ 2) â»Â¹ * Var[S (u i)] â‰¤ C,
  { assume N,
    calc
    âˆ‘ i in range N, ((u i : â„) ^ 2) â»Â¹ * Var[S (u i)]
        = âˆ‘ i in range N, ((u i : â„) ^ 2) â»Â¹ * (âˆ‘ j in range (u i), Var[Y j]) :
      begin
        congr' 1 with i,
        congr' 1,
        rw [hS, indep_fun.Var_sum],
        { assume j hj,
          exact (hident j).ae_strongly_measurable.mem_â„’p_truncation },
        { assume k hk l hl hkl,
          exact (hindep k l hkl).comp (A k).measurable (A l).measurable }
      end
    ... = âˆ‘ j in range (u (N - 1)),
            (âˆ‘ i in (range N).filter (Î» i, j < u i), ((u i : â„) ^ 2) â»Â¹) * Var[Y j] :
      begin
        simp_rw [mul_sum, sum_mul, sum_sigma'],
        refine sum_bij' (Î» (p : (Î£ (i : â„•), â„•)) hp, (âŸ¨p.2, p.1âŸ© : (Î£ (i : â„•), â„•))) _ (Î» a ha, rfl)
          (Î» (p : (Î£ (i : â„•), â„•)) hp, (âŸ¨p.2, p.1âŸ© : (Î£ (i : â„•), â„•))) _ _ _,
        { rintros âŸ¨i, jâŸ© hij,
          simp only [mem_sigma, mem_range] at hij,
          simp only [hij.1, hij.2, mem_sigma, mem_range, mem_filter, and_true],
          exact hij.2.trans_le (u_mono (nat.le_pred_of_lt hij.1)) },
        { rintros âŸ¨i, jâŸ© hij,
          simp only [mem_sigma, mem_range, mem_filter] at hij,
          simp only [hij.2.1, hij.2.2, mem_sigma, mem_range, and_self] },
        { rintros âŸ¨i, jâŸ© hij, refl },
        { rintros âŸ¨i, jâŸ© hij, refl },
      end
    ... â‰¤ âˆ‘ j in range (u (N - 1)), (c ^ 5 * (c - 1) â»Â¹ ^ 3 / j ^ 2) * Var[Y j] :
      begin
        apply sum_le_sum (Î» j hj, _),
        rcases @eq_zero_or_pos _ _ j with rfl|hj,
        { simp only [Y, nat.cast_zero, zero_pow', ne.def, bit0_eq_zero, nat.one_ne_zero,
            not_false_iff, div_zero, zero_mul],
          simp only [nat.cast_zero, truncation_zero, variance_zero, mul_zero] },
        apply mul_le_mul_of_nonneg_right _ (variance_nonneg _ _),
        convert sum_div_nat_floor_pow_sq_le_div_sq N (nat.cast_pos.2 hj) c_one,
        { simp only [nat.cast_lt] },
        { simp only [one_div] }
      end
    ... = (c ^ 5 * (c - 1) â»Â¹ ^ 3) * âˆ‘ j in range (u (N - 1)), ((j : â„) ^ 2) â»Â¹ * Var[Y j] :
        by { simp_rw [mul_sum, div_eq_mul_inv], ring_nf }
    ... â‰¤ (c ^ 5 * (c - 1) â»Â¹ ^ 3) * (2 * ğ”¼[X 0]) :
      begin
        apply mul_le_mul_of_nonneg_left (I1 _),
        apply mul_nonneg (pow_nonneg c_pos.le _),
        exact pow_nonneg (inv_nonneg.2 (sub_nonneg.2 c_one.le)) _
      end },
  have I3 : âˆ€ N, âˆ‘ i in range N,
    â„™ {Ï‰ | (u i * Îµ : â„) â‰¤ |S (u i) Ï‰ - ğ”¼[S (u i)]|} â‰¤ ennreal.of_real (Îµ â»Â¹ ^ 2 * C),
  { assume N,
    calc âˆ‘ i in range N, â„™ {Ï‰ | (u i * Îµ : â„) â‰¤ |S (u i) Ï‰ - ğ”¼[S (u i)]|}
        â‰¤ âˆ‘ i in range N, ennreal.of_real (Var[S (u i)] / (u i * Îµ) ^ 2) :
      begin
        refine sum_le_sum (Î» i hi, _),
        apply meas_ge_le_mul_variance,
        { exact mem_â„’p_finset_sum' _
            (Î» j hj, (hident j).ae_strongly_measurable.mem_â„’p_truncation) },
        { apply mul_pos (nat.cast_pos.2 _) Îµpos,
          refine zero_lt_one.trans_le _,
          apply nat.le_floor,
          rw nat.cast_one,
          apply one_le_pow_of_one_le c_one.le }
      end
    ... = ennreal.of_real (âˆ‘ i in range N, Var[S (u i)] / (u i * Îµ) ^ 2) :
      begin
        rw ennreal.of_real_sum_of_nonneg (Î» i hi, _),
        exact div_nonneg (variance_nonneg _ _) (sq_nonneg _),
      end
    ... â‰¤ ennreal.of_real (Îµ â»Â¹ ^ 2 * C) :
      begin
        apply ennreal.of_real_le_of_real,
        simp_rw [div_eq_inv_mul, â† inv_powâ‚€, mul_invâ‚€, mul_comm _ (Îµâ»Â¹), mul_pow, mul_assoc,
          â† mul_sum],
        refine mul_le_mul_of_nonneg_left _ (sq_nonneg _),
        simp_rw [inv_powâ‚€],
        exact I2 N
      end },
  have I4 : âˆ‘' i, â„™ {Ï‰ | (u i * Îµ : â„) â‰¤ |S (u i) Ï‰ - ğ”¼[S (u i)]|} < âˆ :=
    (le_of_tendsto_of_tendsto' (ennreal.tendsto_nat_tsum _) tendsto_const_nhds I3).trans_lt
      ennreal.of_real_lt_top,
  filter_upwards [ae_eventually_not_mem I4.ne] with Ï‰ hÏ‰,
  simp_rw [not_le, mul_comm, S, sum_apply] at hÏ‰,
  exact hÏ‰,
end

lemma strong_law_aux2 {c : â„} (c_one : 1 < c) :
  âˆ€áµ Ï‰, asymptotics.is_o
  (Î» (n : â„•), âˆ‘ i in range âŒŠc^nâŒ‹â‚Š, truncation (X i) i Ï‰
              - ğ”¼[âˆ‘ i in range âŒŠc^nâŒ‹â‚Š, truncation (X i) i]) (Î» (n : â„•), (âŒŠc^nâŒ‹â‚Š : â„)) at_top :=
begin
  obtain âŸ¨v, -, v_pos, v_limâŸ© :
    âˆƒ (u : â„• â†’ â„), strict_anti u âˆ§ (âˆ€ (n : â„•), 0 < u n) âˆ§ tendsto u at_top (ğ“ 0) :=
    exists_seq_strict_anti_tendsto (0 : â„),
  have := Î» i, strong_law_aux1 X hint hindep hident hnonneg c_one (v_pos i),
  filter_upwards [ae_all_iff.2 this] with Ï‰ hÏ‰,
  apply asymptotics.is_o_iff.2 (Î» Îµ Îµpos, _),
  obtain âŸ¨i, hiâŸ© : âˆƒ i, v i < Îµ := ((tendsto_order.1 v_lim).2 Îµ Îµpos).exists,
  filter_upwards [hÏ‰ i] with n hn,
  simp only [real.norm_eq_abs, lattice_ordered_comm_group.abs_abs, nat.abs_cast],
  exact hn.le.trans (mul_le_mul_of_nonneg_right hi.le (nat.cast_nonneg _)),
end

omit hindep hnonneg
lemma strong_law_aux3 :
  asymptotics.is_o (Î» n, ğ”¼[âˆ‘ i in range n, truncation (X i) i] - n * ğ”¼[X 0])
    (Î» (n : â„•), (n : â„)) at_top :=
begin
  have A : âˆ€ i, strongly_measurable (indicator (set.Ioc (-i : â„) i) id) :=
    Î» i, strongly_measurable_id.indicator measurable_set_Ioc,
  have A : tendsto (Î» i, ğ”¼[truncation (X i) i]) at_top (ğ“ (ğ”¼[X 0])),
  { convert (tendsto_integral_truncation hint).comp tendsto_coe_nat_at_top_at_top,
    ext i,
    exact (hident i).truncation.integral_eq },
  convert asymptotics.is_o_sum_range_of_tendsto_zero (normed_group.tendsto_sub_nhds_zero_iff.2 A),
  ext1 n,
  simp only [sum_sub_distrib, sum_const, card_range, nsmul_eq_mul, sum_apply, sub_left_inj],
  rw integral_finset_sum _ (Î» i hi, _),
  exact ((hident i).symm.integrable_snd hint).1.integrable_truncation,
end
include hindep hnonneg

lemma strong_law_aux4 {c : â„} (c_one : 1 < c) :
  âˆ€áµ Ï‰, asymptotics.is_o
  (Î» (n : â„•), âˆ‘ i in range âŒŠc^nâŒ‹â‚Š, truncation (X i) i Ï‰ - âŒŠc^nâŒ‹â‚Š * ğ”¼[X 0])
    (Î» (n : â„•), (âŒŠc^nâŒ‹â‚Š : â„)) at_top :=
begin
  filter_upwards [strong_law_aux2 X hint hindep hident hnonneg c_one] with Ï‰ hÏ‰,
  have A : tendsto (Î» (n : â„•), âŒŠc ^ nâŒ‹â‚Š) at_top at_top :=
    tendsto_nat_floor_at_top.comp (tendsto_pow_at_top_at_top_of_one_lt c_one),
  convert hÏ‰.add ((strong_law_aux3 X hint hident).comp_tendsto A),
  ext1 n,
  simp,
end

omit hindep
lemma strong_law_aux5 :
  âˆ€áµ Ï‰, asymptotics.is_o
  (Î» (n : â„•), âˆ‘ i in range n, truncation (X i) i Ï‰ - âˆ‘ i in range n, X i Ï‰)
  (Î» (n : â„•), (n : â„)) at_top :=
begin
  have A : âˆ‘' (j : â„•), â„™ {Ï‰ | X j Ï‰ âˆˆ set.Ioi (j : â„)} < âˆ,
  { convert tsum_prob_mem_Ioi_lt_top hint (hnonneg 0),
    ext1 j,
    exact (hident j).measure_mem_eq measurable_set_Ioi },
  have B : âˆ€áµ Ï‰, tendsto (Î» (n : â„•), truncation (X n) n Ï‰ - X n Ï‰) at_top (ğ“ 0),
  { filter_upwards [ae_eventually_not_mem A.ne] with Ï‰ hÏ‰,
    apply tendsto_const_nhds.congr' _,
    filter_upwards [hÏ‰, Ioi_mem_at_top 0] with n hn npos,
    simp only [truncation, indicator, set.mem_Ioc, id.def, function.comp_app],
    split_ifs,
    { exact (sub_self _).symm },
    { have : - (n : â„) < X n Ï‰,
      { apply lt_of_lt_of_le _ (hnonneg n Ï‰),
        simpa only [right.neg_neg_iff, nat.cast_pos] using npos },
      simp only [this, true_and, not_le] at h,
      exact (hn h).elim } },
  filter_upwards [B] with Ï‰ hÏ‰,
  convert is_o_sum_range_of_tendsto_zero hÏ‰,
  ext n,
  rw sum_sub_distrib,
end
include hindep

lemma strong_law_aux6 {c : â„} (c_one : 1 < c) :
  âˆ€áµ Ï‰, tendsto (Î» (n : â„•), (âˆ‘ i in range âŒŠc^nâŒ‹â‚Š, X i Ï‰) / âŒŠc^nâŒ‹â‚Š) at_top (ğ“ (ğ”¼[X 0])) :=
begin
   have H : âˆ€ (n : â„•), (0 : â„) < âŒŠc ^ nâŒ‹â‚Š,
  { assume n,
    refine zero_lt_one.trans_le _,
    simp only [nat.one_le_cast, nat.one_le_floor_iff, one_le_pow_of_one_le c_one.le n] },
  filter_upwards [strong_law_aux4 X hint hindep hident hnonneg c_one,
    strong_law_aux5 X hint hident hnonneg] with Ï‰ hÏ‰ h'Ï‰,
  rw [â† normed_group.tendsto_sub_nhds_zero_iff, â† asymptotics.is_o_one_iff â„],
  have L : asymptotics.is_o (Î» (n : â„•), âˆ‘ i in range âŒŠc^nâŒ‹â‚Š, X i Ï‰ - âŒŠc^nâŒ‹â‚Š * ğ”¼[X 0])
    (Î» (n : â„•), (âŒŠc^nâŒ‹â‚Š : â„)) at_top,
  { have A : tendsto (Î» (n : â„•), âŒŠc ^ nâŒ‹â‚Š) at_top at_top :=
      tendsto_nat_floor_at_top.comp (tendsto_pow_at_top_at_top_of_one_lt c_one),
    convert hÏ‰.sub (h'Ï‰.comp_tendsto A),
    ext1 n,
    simp only [sub_sub_sub_cancel_left] },
  convert L.mul_is_O (is_O_refl (Î» (n : â„•), (âŒŠc ^ nâŒ‹â‚Š : â„) â»Â¹) at_top);
  { ext1 n,
    field_simp [(H n).ne'] },
end

lemma strong_law_aux7 :
  âˆ€áµ Ï‰, tendsto (Î» (n : â„•), (âˆ‘ i in range n, X i Ï‰) / n) at_top (ğ“ (ğ”¼[X 0])) :=
begin
  obtain âŸ¨c, -, cone, climâŸ© :
    âˆƒ (c : â„• â†’ â„), strict_anti c âˆ§ (âˆ€ (n : â„•), 1 < c n) âˆ§ tendsto c at_top (ğ“ 1) :=
      exists_seq_strict_anti_tendsto (1 : â„),
  have : âˆ€ k, âˆ€áµ Ï‰, tendsto (Î» (n : â„•), (âˆ‘ i in range âŒŠc k ^ nâŒ‹â‚Š, X i Ï‰) / âŒŠc k ^ nâŒ‹â‚Š)
    at_top (ğ“ (ğ”¼[X 0])) := Î» k, strong_law_aux6 X hint hindep hident hnonneg (cone k),
  filter_upwards [ae_all_iff.2 this] with Ï‰ hÏ‰,
  apply tendsto_div_of_monotone_of_tendsto_div_floor_pow _ _ _ c cone clim _,
  { assume m n hmn,
    exact sum_le_sum_of_subset_of_nonneg (range_mono hmn) (Î» i hi h'i, hnonneg i Ï‰) },
  { exact hÏ‰ }
end

end strong_law_aux

/-- *Strong law of large numbers*, almost sure version: if `X n` is a sequence of independent
identically distributed integrable real-valued random variables, then `âˆ‘ i in range n, X i / n`
converges almost surely to `ğ”¼[X 0]`. We give here the strong version, due to Etemadi, that only
requires pairwise independence. -/
theorem strong_law_ae
  (X : â„• â†’ Î© â†’ â„) (hint : integrable (X 0))
  (hindep : pairwise (Î» i j, indep_fun (X i) (X j)))
  (hident : âˆ€ i, ident_distrib (X i) (X 0)) :
  âˆ€áµ Ï‰, tendsto (Î» (n : â„•), (âˆ‘ i in range n, X i Ï‰) / n) at_top (ğ“ (ğ”¼[X 0])) :=
begin
  let pos : â„ â†’ â„ := (Î» x, max x 0),
  let neg : â„ â†’ â„ := (Î» x, max (-x) 0),
  have posm : measurable pos := measurable_id'.max measurable_const,
  have negm : measurable neg := measurable_id'.neg.max measurable_const,
  have A : âˆ€áµ Ï‰, tendsto (Î» (n : â„•), (âˆ‘ i in range n, (pos âˆ˜ (X i)) Ï‰) / n)
    at_top (ğ“ (ğ”¼[pos âˆ˜ (X 0)])) :=
      strong_law_aux7 _ hint.pos_part (Î» i j hij, (hindep i j hij).comp posm posm)
        (Î» i, (hident i).comp posm) (Î» i Ï‰, le_max_right _ _),
  have B : âˆ€áµ Ï‰, tendsto (Î» (n : â„•), (âˆ‘ i in range n, (neg âˆ˜ (X i)) Ï‰) / n)
    at_top (ğ“ (ğ”¼[neg âˆ˜ (X 0)])) :=
      strong_law_aux7 _ hint.neg_part (Î» i j hij, (hindep i j hij).comp negm negm)
        (Î» i, (hident i).comp negm) (Î» i Ï‰, le_max_right _ _),
  filter_upwards [A, B] with Ï‰ hÏ‰pos hÏ‰neg,
  convert hÏ‰pos.sub hÏ‰neg,
  { simp only [â† sub_div, â† sum_sub_distrib, max_zero_sub_max_neg_zero_eq_self] },
  { simp only [â†integral_sub hint.pos_part hint.neg_part, max_zero_sub_max_neg_zero_eq_self] }
end

end probability_theory
