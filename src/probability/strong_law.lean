/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/

import probability.ident_distrib
import measure_theory.integral.interval_integral
import analysis.specific_limits.floor_pow
import analysis.p_series
import analysis.asymptotics.specific_asymptotics

/-!
# The strong law of large numbers

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We prove the strong law of large numbers, in `probability_theory.strong_law_ae`:
If `X n` is a sequence of independent identically distributed integrable real-valued random
variables, then `‚àë i in range n, X i / n` converges almost surely to `ùîº[X 0]`.
We give here the strong version, due to Etemadi, that only requires pairwise independence.

This file also contains the L·µñ version of the strong law of large numbers provided by
`probability_theory.strong_law_Lp` which shows `‚àë i in range n, X i / n` converges in L·µñ to
`ùîº[X 0]` provided `X n` is independent identically distributed and is L·µñ.

## Implementation

We follow the proof by Etemadi
[Etemadi, *An elementary proof of the strong law of large numbers*][etemadi_strong_law],
which goes as follows.

It suffices to prove the result for nonnegative `X`, as one can prove the general result by
splitting a general `X` into its positive part and negative part.
Consider `X‚Çô` a sequence of nonnegative integrable identically distributed pairwise independent
random variables. Let `Y‚Çô` be the truncation of `X‚Çô` up to `n`. We claim that
* Almost surely, `X‚Çô = Y‚Çô` for all but finitely many indices. Indeed, `‚àë ‚Ñô (X‚Çô ‚â† Y‚Çô)` is bounded by
  `1 + ùîº[X]` (see `sum_prob_mem_Ioc_le` and `tsum_prob_mem_Ioi_lt_top`).
* Let `c > 1`. Along the sequence `n = c ^ k`, then `(‚àë_{i=0}^{n-1} Y·µ¢ - ùîº[Y·µ¢])/n` converges almost
  surely to `0`. This follows from a variance control, as
```
  ‚àë_k ‚Ñô (|‚àë_{i=0}^{c^k - 1} Y·µ¢ - ùîº[Y·µ¢]| > c^k Œµ)
    ‚â§ ‚àë_k (c^k Œµ)^{-2} ‚àë_{i=0}^{c^k - 1} Var[Y·µ¢]    (by Markov inequality)
    ‚â§ ‚àë_i (C/i^2) Var[Y·µ¢]                           (as ‚àë_{c^k > i} 1/(c^k)^2 ‚â§ C/i^2)
    ‚â§ ‚àë_i (C/i^2) ùîº[Y·µ¢^2]
    ‚â§ 2C ùîº[X^2]                                     (see `sum_variance_truncation_le`)
```
* As `ùîº[Y·µ¢]` converges to `ùîº[X]`, it follows from the two previous items and Cesaro that, along
  the sequence `n = c^k`, one has `(‚àë_{i=0}^{n-1} X·µ¢) / n ‚Üí ùîº[X]` almost surely.
* To generalize it to all indices, we use the fact that `‚àë_{i=0}^{n-1} X·µ¢` is nondecreasing and
  that, if `c` is close enough to `1`, the gap between `c^k` and `c^(k+1)` is small.
-/

noncomputable theory

open measure_theory filter finset asymptotics
open set (indicator)

open_locale topology big_operators measure_theory probability_theory ennreal nnreal

namespace probability_theory

/-! ### Prerequisites on truncations -/

section truncation

variables {Œ± : Type*}

/-- Truncating a real-valued function to the interval `(-A, A]`. -/
def truncation (f : Œ± ‚Üí ‚Ñù) (A : ‚Ñù) :=
(indicator (set.Ioc (-A) A) id) ‚àò f

variables {m : measurable_space Œ±} {Œº : measure Œ±} {f : Œ± ‚Üí ‚Ñù}

lemma _root_.measure_theory.ae_strongly_measurable.truncation
  (hf : ae_strongly_measurable f Œº) {A : ‚Ñù} :
  ae_strongly_measurable (truncation f A) Œº :=
begin
  apply ae_strongly_measurable.comp_ae_measurable _ hf.ae_measurable,
  exact (strongly_measurable_id.indicator measurable_set_Ioc).ae_strongly_measurable,
end

lemma abs_truncation_le_bound (f : Œ± ‚Üí ‚Ñù) (A : ‚Ñù) (x : Œ±) :
  |truncation f A x| ‚â§ |A| :=
begin
  simp only [truncation, set.indicator, set.mem_Icc, id.def, function.comp_app],
  split_ifs,
  { exact abs_le_abs h.2 (neg_le.2 h.1.le) },
  { simp [abs_nonneg] }
end

@[simp] lemma truncation_zero (f : Œ± ‚Üí ‚Ñù) :
  truncation f 0 = 0 :=
by simp [truncation]

lemma abs_truncation_le_abs_self (f : Œ± ‚Üí ‚Ñù) (A : ‚Ñù) (x : Œ±) :
  |truncation f A x| ‚â§ |f x| :=
begin
  simp only [truncation, indicator, set.mem_Icc, id.def, function.comp_app],
  split_ifs,
  { exact le_rfl },
  { simp [abs_nonneg] },
end

lemma truncation_eq_self {f : Œ± ‚Üí ‚Ñù} {A : ‚Ñù} {x : Œ±} (h : |f x| < A) :
  truncation f A x = f x :=
begin
  simp only [truncation, indicator, set.mem_Icc, id.def, function.comp_app, ite_eq_left_iff],
  assume H,
  apply H.elim,
  simp [(abs_lt.1 h).1, (abs_lt.1 h).2.le],
end

lemma truncation_eq_of_nonneg {f : Œ± ‚Üí ‚Ñù} {A : ‚Ñù} (h : ‚àÄ x, 0 ‚â§ f x) :
  truncation f A = (indicator (set.Ioc 0 A) id) ‚àò f :=
begin
  ext x,
  rcases (h x).lt_or_eq with hx|hx,
  { simp only [truncation, indicator, hx, set.mem_Ioc, id.def, function.comp_app, true_and],
    by_cases h'x : f x ‚â§ A,
    { have : - A < f x, by linarith [h x],
      simp only [this, true_and] },
    { simp only [h'x, and_false] } },
  { simp only [truncation, indicator, hx, id.def, function.comp_app, if_t_t]},
end

lemma truncation_nonneg {f : Œ± ‚Üí ‚Ñù} (A : ‚Ñù) {x : Œ±} (h : 0 ‚â§ f x) : 0 ‚â§ truncation f A x :=
set.indicator_apply_nonneg $ Œª _, h

lemma _root_.measure_theory.ae_strongly_measurable.mem_‚Ñíp_truncation [is_finite_measure Œº]
  (hf : ae_strongly_measurable f Œº) {A : ‚Ñù} {p : ‚Ñù‚â•0‚àû} :
  mem_‚Ñíp (truncation f A) p Œº :=
mem_‚Ñíp.of_bound hf.truncation (|A|) (eventually_of_forall (Œª x, abs_truncation_le_bound _ _ _))

lemma _root_.measure_theory.ae_strongly_measurable.integrable_truncation [is_finite_measure Œº]
  (hf : ae_strongly_measurable f Œº) {A : ‚Ñù} :
  integrable (truncation f A) Œº :=
by { rw ‚Üê mem_‚Ñíp_one_iff_integrable, exact hf.mem_‚Ñíp_truncation }

lemma moment_truncation_eq_interval_integral (hf : ae_strongly_measurable f Œº) {A : ‚Ñù}
  (hA : 0 ‚â§ A) {n : ‚Ñï} (hn : n ‚â† 0) :
  ‚à´ x, (truncation f A x) ^ n ‚àÇŒº = ‚à´ y in (-A)..A, y ^ n ‚àÇ(measure.map f Œº) :=
begin
  have M : measurable_set (set.Ioc (-A) A) := measurable_set_Ioc,
  change ‚à´ x, (Œª z, (indicator (set.Ioc (-A) A) id z) ^ n) (f x) ‚àÇŒº = _,
  rw [‚Üê integral_map hf.ae_measurable, interval_integral.integral_of_le, ‚Üê integral_indicator M],
  { simp only [indicator, zero_pow' _ hn, id.def, ite_pow] },
  { linarith },
  { exact ((measurable_id.indicator M).pow_const n).ae_strongly_measurable }
end

lemma moment_truncation_eq_interval_integral_of_nonneg (hf : ae_strongly_measurable f Œº) {A : ‚Ñù}
  {n : ‚Ñï} (hn : n ‚â† 0) (h'f : 0 ‚â§ f) :
  ‚à´ x, (truncation f A x) ^ n ‚àÇŒº = ‚à´ y in 0..A, y ^ n ‚àÇ(measure.map f Œº) :=
begin
  have M : measurable_set (set.Ioc 0 A) := measurable_set_Ioc,
  have M' : measurable_set (set.Ioc A 0) := measurable_set_Ioc,
  rw truncation_eq_of_nonneg h'f,
  change ‚à´ x, (Œª z, (indicator (set.Ioc 0 A) id z) ^ n) (f x) ‚àÇŒº = _,
  rcases le_or_lt 0 A with hA | hA,
  { rw [‚Üê integral_map hf.ae_measurable, interval_integral.integral_of_le hA,
        ‚Üê integral_indicator M],
    { simp only [indicator, zero_pow' _ hn, id.def, ite_pow] },
    { exact ((measurable_id.indicator M).pow_const n).ae_strongly_measurable } },
  { rw [‚Üê integral_map hf.ae_measurable, interval_integral.integral_of_ge hA.le,
        ‚Üê integral_indicator M'],
    { simp only [set.Ioc_eq_empty_of_le hA.le, zero_pow' _ hn, set.indicator_empty, integral_zero,
        zero_eq_neg],
      apply integral_eq_zero_of_ae,
      have : ‚àÄ·µê x ‚àÇ(measure.map f Œº), (0 : ‚Ñù) ‚â§ x :=
        (ae_map_iff hf.ae_measurable measurable_set_Ici).2 (eventually_of_forall h'f),
      filter_upwards [this] with x hx,
      simp only [indicator, set.mem_Ioc, pi.zero_apply, ite_eq_right_iff, and_imp],
      assume h'x h''x,
      have : x = 0, by linarith,
      simp [this, zero_pow' _ hn] },
    { exact ((measurable_id.indicator M).pow_const n).ae_strongly_measurable } }
end

lemma integral_truncation_eq_interval_integral (hf : ae_strongly_measurable f Œº) {A : ‚Ñù}
  (hA : 0 ‚â§ A) :
  ‚à´ x, truncation f A x ‚àÇŒº = ‚à´ y in (-A)..A, y ‚àÇ(measure.map f Œº) :=
by simpa using moment_truncation_eq_interval_integral hf hA one_ne_zero

lemma integral_truncation_eq_interval_integral_of_nonneg (hf : ae_strongly_measurable f Œº) {A : ‚Ñù}
  (h'f : 0 ‚â§ f) :
  ‚à´ x, truncation f A x ‚àÇŒº = ‚à´ y in 0..A, y ‚àÇ(measure.map f Œº) :=
by simpa using moment_truncation_eq_interval_integral_of_nonneg hf one_ne_zero h'f

lemma integral_truncation_le_integral_of_nonneg
  (hf : integrable f Œº) (h'f : 0 ‚â§ f) {A : ‚Ñù} :
  ‚à´ x, truncation f A x ‚àÇŒº ‚â§ ‚à´ x, f x ‚àÇŒº :=
begin
  apply integral_mono_of_nonneg (eventually_of_forall (Œª x, _)) hf (eventually_of_forall (Œª x, _)),
  { exact truncation_nonneg _ (h'f x) },
  { calc truncation f A x ‚â§ |truncation f A x| : le_abs_self _
                      ... ‚â§ |f x|              : abs_truncation_le_abs_self _ _ _
                      ... = f x                : abs_of_nonneg (h'f x) }
end

/-- If a function is integrable, then the integral of its truncated versions converges to the
integral of the whole function. -/
lemma tendsto_integral_truncation {f : Œ± ‚Üí ‚Ñù} (hf : integrable f Œº) :
  tendsto (Œª A, ‚à´ x, truncation f A x ‚àÇŒº) at_top (ùìù (‚à´ x, f x ‚àÇŒº)) :=
begin
  refine tendsto_integral_filter_of_dominated_convergence (Œª x, abs (f x)) _ _ _ _,
  { exact eventually_of_forall (Œª A, hf.ae_strongly_measurable.truncation) },
  { apply eventually_of_forall (Œª A, _),
    apply eventually_of_forall (Œª x, _),
    rw real.norm_eq_abs,
    exact abs_truncation_le_abs_self _ _ _ },
  { apply hf.abs },
  { apply eventually_of_forall (Œª x, _),
    apply tendsto_const_nhds.congr' _,
    filter_upwards [Ioi_mem_at_top (abs (f x))] with A hA,
    exact (truncation_eq_self hA).symm },
end

lemma ident_distrib.truncation {Œ≤ : Type*} [measurable_space Œ≤] {ŒΩ : measure Œ≤}
  {f : Œ± ‚Üí ‚Ñù} {g : Œ≤ ‚Üí ‚Ñù} (h : ident_distrib f g Œº ŒΩ) {A : ‚Ñù} :
  ident_distrib (truncation f A) (truncation g A) Œº ŒΩ :=
h.comp (measurable_id.indicator measurable_set_Ioc)

end truncation

section strong_law_ae

variables {Œ© : Type*} [measure_space Œ©] [is_probability_measure (‚Ñô : measure Œ©)]

section moment_estimates

lemma sum_prob_mem_Ioc_le
  {X : Œ© ‚Üí ‚Ñù} (hint : integrable X) (hnonneg : 0 ‚â§ X) {K : ‚Ñï} {N : ‚Ñï} (hKN : K ‚â§ N) :
  ‚àë j in range K, ‚Ñô {œâ | X œâ ‚àà set.Ioc (j : ‚Ñù) N} ‚â§ ennreal.of_real (ùîº[X] + 1) :=
begin
  let œÅ : measure ‚Ñù := measure.map X ‚Ñô,
  haveI : is_probability_measure œÅ := is_probability_measure_map hint.ae_measurable,
  have A : ‚àë j in range K, ‚à´ x in j..N, (1 : ‚Ñù) ‚àÇœÅ ‚â§ ùîº[X] + 1, from calc
  ‚àë j in range K, ‚à´ x in j..N, (1 : ‚Ñù) ‚àÇœÅ
      = ‚àë j in range K, ‚àë i in Ico j N, ‚à´ x in i..(i+1 : ‚Ñï), (1 : ‚Ñù) ‚àÇœÅ :
    begin
      apply sum_congr rfl (Œª j hj, _),
      rw interval_integral.sum_integral_adjacent_intervals_Ico ((mem_range.1 hj).le.trans hKN),
      assume k hk,
      exact continuous_const.interval_integrable _ _,
    end
  ... = ‚àë i in range N, ‚àë j in range (min (i+1) K), ‚à´ x in i..(i+1 : ‚Ñï), (1 : ‚Ñù) ‚àÇœÅ :
    begin
      simp_rw [sum_sigma'],
      refine sum_bij' (Œª (p : (Œ£ (i : ‚Ñï), ‚Ñï)) hp, (‚ü®p.2, p.1‚ü© : (Œ£ (i : ‚Ñï), ‚Ñï))) _ (Œª a ha, rfl)
        (Œª (p : (Œ£ (i : ‚Ñï), ‚Ñï)) hp, (‚ü®p.2, p.1‚ü© : (Œ£ (i : ‚Ñï), ‚Ñï))) _ _ _,
      { rintros ‚ü®i, j‚ü© hij,
        simp only [mem_sigma, mem_range, mem_Ico] at hij,
        simp only [hij, nat.lt_succ_iff.2 hij.2.1, mem_sigma, mem_range, lt_min_iff, and_self] },
      { rintros ‚ü®i, j‚ü© hij,
        simp only [mem_sigma, mem_range, lt_min_iff] at hij,
        simp only [hij, nat.lt_succ_iff.1 hij.2.1, mem_sigma, mem_range, mem_Ico, and_self] },
      { rintros ‚ü®i, j‚ü© hij, refl },
      { rintros ‚ü®i, j‚ü© hij, refl },
    end
  ... ‚â§ ‚àë i in range N, (i + 1) * ‚à´ x in i..(i+1 : ‚Ñï), (1 : ‚Ñù) ‚àÇœÅ :
    begin
      apply sum_le_sum (Œª i hi, _),
      simp only [nat.cast_add, nat.cast_one, sum_const, card_range, nsmul_eq_mul, nat.cast_min],
      refine mul_le_mul_of_nonneg_right (min_le_left _ _) _,
      apply interval_integral.integral_nonneg,
      { simp only [le_add_iff_nonneg_right, zero_le_one] },
      { simp only [zero_le_one, implies_true_iff], }
    end
  ... ‚â§ ‚àë i in range N, ‚à´ x in i..(i+1 : ‚Ñï), (x + 1) ‚àÇœÅ :
    begin
      apply sum_le_sum (Œª i hi, _),
      have I : (i : ‚Ñù) ‚â§ (i + 1 : ‚Ñï),
        by simp only [nat.cast_add, nat.cast_one, le_add_iff_nonneg_right, zero_le_one],
      simp_rw [interval_integral.integral_of_le I, ‚Üê integral_mul_left],
      apply set_integral_mono_on,
      { exact continuous_const.integrable_on_Ioc },
      { exact (continuous_id.add continuous_const).integrable_on_Ioc },
      { exact measurable_set_Ioc },
      { assume x hx,
        simp only [nat.cast_add, nat.cast_one, set.mem_Ioc] at hx,
        simp [hx.1.le] }
    end
  ... = ‚à´ x in 0..N, x + 1 ‚àÇœÅ :
    begin
      rw interval_integral.sum_integral_adjacent_intervals (Œª k hk, _),
      { norm_cast },
      { exact (continuous_id.add continuous_const).interval_integrable _ _ }
    end
  ... = ‚à´ x in 0..N, x ‚àÇœÅ + ‚à´ x in 0..N, 1 ‚àÇœÅ :
    begin
      rw interval_integral.integral_add,
      { exact continuous_id.interval_integrable _ _ },
      { exact continuous_const.interval_integrable _ _ },
    end
  ... = ùîº[truncation X N] + ‚à´ x in 0..N, 1 ‚àÇœÅ :
    by rw integral_truncation_eq_interval_integral_of_nonneg hint.1 hnonneg
  ... ‚â§ ùîº[X] + ‚à´ x in 0..N, 1 ‚àÇœÅ :
    add_le_add_right (integral_truncation_le_integral_of_nonneg hint hnonneg) _
  ... ‚â§ ùîº[X] + 1 :
    begin
      refine add_le_add le_rfl _,
      rw interval_integral.integral_of_le (nat.cast_nonneg _),
      simp only [integral_const, measure.restrict_apply', measurable_set_Ioc, set.univ_inter,
        algebra.id.smul_eq_mul, mul_one],
      rw ‚Üê ennreal.one_to_real,
      exact ennreal.to_real_mono ennreal.one_ne_top prob_le_one,
    end,
  have B : ‚àÄ a b, ‚Ñô {œâ | X œâ ‚àà set.Ioc a b} = ennreal.of_real (‚à´ x in set.Ioc a b, (1 : ‚Ñù) ‚àÇœÅ),
  { assume a b,
    rw [of_real_set_integral_one œÅ _,
        measure.map_apply_of_ae_measurable hint.ae_measurable measurable_set_Ioc],
    refl },
  calc ‚àë j in range K, ‚Ñô {œâ | X œâ ‚àà set.Ioc (j : ‚Ñù) N}
      = ‚àë j in range K, ennreal.of_real (‚à´ x in set.Ioc (j : ‚Ñù) N, (1 : ‚Ñù) ‚àÇœÅ) :
    by simp_rw B
  ... = ennreal.of_real (‚àë j in range K, ‚à´ x in set.Ioc (j : ‚Ñù) N, (1 : ‚Ñù) ‚àÇœÅ) :
    begin
      rw ennreal.of_real_sum_of_nonneg,
      simp only [integral_const, algebra.id.smul_eq_mul, mul_one, ennreal.to_real_nonneg,
        implies_true_iff],
    end
  ... = ennreal.of_real (‚àë j in range K, ‚à´ x in (j : ‚Ñù)..N, (1 : ‚Ñù) ‚àÇœÅ) :
    begin
      congr' 1,
      refine sum_congr rfl (Œª j hj, _),
      rw interval_integral.integral_of_le (nat.cast_le.2 ((mem_range.1 hj).le.trans hKN)),
    end
  ... ‚â§ ennreal.of_real (ùîº[X] + 1) : ennreal.of_real_le_of_real A
end

lemma tsum_prob_mem_Ioi_lt_top
  {X : Œ© ‚Üí ‚Ñù} (hint : integrable X) (hnonneg : 0 ‚â§ X) :
  ‚àë' (j : ‚Ñï), ‚Ñô {œâ | X œâ ‚àà set.Ioi (j : ‚Ñù)} < ‚àû :=
begin
  suffices : ‚àÄ (K : ‚Ñï), ‚àë j in range K, ‚Ñô {œâ | X œâ ‚àà set.Ioi (j : ‚Ñù)} ‚â§ ennreal.of_real (ùîº[X] + 1),
    from (le_of_tendsto_of_tendsto (ennreal.tendsto_nat_tsum _) tendsto_const_nhds
      (eventually_of_forall this)).trans_lt ennreal.of_real_lt_top,
  assume K,
  have A : tendsto (Œª (N : ‚Ñï), ‚àë j in range K, ‚Ñô {œâ | X œâ ‚àà set.Ioc (j : ‚Ñù) N})
    at_top (ùìù (‚àë j in range K, ‚Ñô {œâ | X œâ ‚àà set.Ioi (j : ‚Ñù)})),
  { refine tendsto_finset_sum _ (Œª i hi, _),
    have : {œâ | X œâ ‚àà set.Ioi (i : ‚Ñù)} = ‚ãÉ (N : ‚Ñï), {œâ | X œâ ‚àà set.Ioc (i : ‚Ñù) N},
    { apply set.subset.antisymm _ _,
      { assume œâ hœâ,
        obtain ‚ü®N, hN‚ü© : ‚àÉ (N : ‚Ñï), X œâ ‚â§ N := exists_nat_ge (X œâ),
        exact set.mem_Union.2 ‚ü®N, hœâ, hN‚ü© },
      { simp only [set.mem_Ioc, set.mem_Ioi, set.Union_subset_iff, set.set_of_subset_set_of,
          implies_true_iff] {contextual := tt} } },
    rw this,
    apply tendsto_measure_Union,
    assume m n hmn x hx,
    exact ‚ü®hx.1, hx.2.trans (nat.cast_le.2 hmn)‚ü© },
  apply le_of_tendsto_of_tendsto A tendsto_const_nhds,
  filter_upwards [Ici_mem_at_top K] with N hN,
  exact sum_prob_mem_Ioc_le hint hnonneg hN
end

lemma sum_variance_truncation_le
  {X : Œ© ‚Üí ‚Ñù} (hint : integrable X) (hnonneg : 0 ‚â§ X) (K : ‚Ñï) :
  ‚àë j in range K, ((j : ‚Ñù) ^ 2) ‚Åª¬π * ùîº[(truncation X j) ^ 2] ‚â§ 2 * ùîº[X] :=
begin
  set Y := Œª (n : ‚Ñï), truncation X n,
  let œÅ : measure ‚Ñù := measure.map X ‚Ñô,
  have Y2 : ‚àÄ n, ùîº[Y n ^ 2] = ‚à´ x in 0..n, x ^ 2 ‚àÇœÅ,
  { assume n,
    change ùîº[Œª x, (Y n x)^2] = _,
    rw [moment_truncation_eq_interval_integral_of_nonneg hint.1 two_ne_zero hnonneg] },
  calc ‚àë j in range K, ((j : ‚Ñù) ^ 2) ‚Åª¬π * ùîº[Y j ^ 2]
      = ‚àë j in range K, ((j : ‚Ñù) ^ 2) ‚Åª¬π * ‚à´ x in 0..j, x ^ 2 ‚àÇœÅ :
    by simp_rw [Y2]
  ... = ‚àë j in range K, ((j : ‚Ñù) ^ 2) ‚Åª¬π * ‚àë k in range j, ‚à´ x in k..(k+1 : ‚Ñï), x ^ 2 ‚àÇœÅ :
    begin
      congr' 1 with j,
      congr' 1,
      rw interval_integral.sum_integral_adjacent_intervals,
      { norm_cast },
      assume k hk,
      exact (continuous_id.pow _).interval_integrable _ _,
    end
  ... = ‚àë k in range K, (‚àë j in Ioo k K, ((j : ‚Ñù) ^ 2) ‚Åª¬π) * ‚à´ x in k..(k+1 : ‚Ñï), x ^ 2 ‚àÇœÅ :
    begin
      simp_rw [mul_sum, sum_mul, sum_sigma'],
      refine sum_bij' (Œª (p : (Œ£ (i : ‚Ñï), ‚Ñï)) hp, (‚ü®p.2, p.1‚ü© : (Œ£ (i : ‚Ñï), ‚Ñï))) _ (Œª a ha, rfl)
        (Œª (p : (Œ£ (i : ‚Ñï), ‚Ñï)) hp, (‚ü®p.2, p.1‚ü© : (Œ£ (i : ‚Ñï), ‚Ñï))) _ _ _,
      { rintros ‚ü®i, j‚ü© hij,
        simp only [mem_sigma, mem_range, mem_filter] at hij,
        simp [hij, mem_sigma, mem_range, and_self, hij.2.trans hij.1], },
      { rintros ‚ü®i, j‚ü© hij,
        simp only [mem_sigma, mem_range, mem_Ioo] at hij,
        simp only [hij, mem_sigma, mem_range, and_self], },
      { rintros ‚ü®i, j‚ü© hij, refl },
      { rintros ‚ü®i, j‚ü© hij, refl },
    end
  ... ‚â§ ‚àë k in range K, (2/ (k+1)) * ‚à´ x in k..(k+1 : ‚Ñï), x ^ 2 ‚àÇœÅ :
    begin
      apply sum_le_sum (Œª k hk, _),
      refine mul_le_mul_of_nonneg_right (sum_Ioo_inv_sq_le _ _) _,
      refine interval_integral.integral_nonneg_of_forall _ (Œª u, sq_nonneg _),
      simp only [nat.cast_add, nat.cast_one, le_add_iff_nonneg_right, zero_le_one],
    end
  ... ‚â§ ‚àë k in range K, ‚à´ x in k..(k+1 : ‚Ñï), 2 * x ‚àÇœÅ :
    begin
      apply sum_le_sum (Œª k hk, _),
      have Ik : (k : ‚Ñù) ‚â§ (k + 1 : ‚Ñï), by simp,
      rw [‚Üê interval_integral.integral_const_mul, interval_integral.integral_of_le Ik,
        interval_integral.integral_of_le Ik],
      refine set_integral_mono_on _ _ measurable_set_Ioc (Œª x hx, _),
      { apply continuous.integrable_on_Ioc,
        exact continuous_const.mul (continuous_pow 2) },
      { apply continuous.integrable_on_Ioc,
        exact continuous_const.mul continuous_id' },
      { calc 2 / (‚Üëk + 1) * x ^ 2 = (x / (k+1)) * (2 * x) : by ring_exp
        ... ‚â§ 1 * (2 * x) :
          mul_le_mul_of_nonneg_right begin
            apply_mod_cast (div_le_one _).2 hx.2,
            simp only [nat.cast_add, nat.cast_one],
            linarith only [show (0 : ‚Ñù) ‚â§ k, from  nat.cast_nonneg k],
          end (mul_nonneg zero_le_two ((nat.cast_nonneg k).trans hx.1.le))
        ... = 2 * x : by rw one_mul }
    end
  ... = 2 * ‚à´ x in (0 : ‚Ñù)..K, x ‚àÇœÅ :
    begin
      rw interval_integral.sum_integral_adjacent_intervals (Œª k hk, _),
      swap, { exact (continuous_const.mul continuous_id').interval_integrable _ _ },
      rw interval_integral.integral_const_mul,
      norm_cast
    end
  ... ‚â§ 2 * ùîº[X] :
    mul_le_mul_of_nonneg_left begin
      rw ‚Üê integral_truncation_eq_interval_integral_of_nonneg hint.1 hnonneg,
      exact integral_truncation_le_integral_of_nonneg hint hnonneg,
    end zero_le_two
end

end moment_estimates

section strong_law_nonneg

/- This paragraph proves the strong law of large numbers (almost sure version, assuming only
pairwise independence) for nonnegative random variables, following Etemadi's proof. -/

variables (X : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (hint : integrable (X 0))
  (hindep : pairwise (Œª i j, indep_fun (X i) (X j)))
  (hident : ‚àÄ i, ident_distrib (X i) (X 0))
  (hnonneg : ‚àÄ i œâ, 0 ‚â§ X i œâ)

include X hint hindep hident hnonneg

/- The truncation of `X·µ¢` up to `i` satisfies the strong law of large numbers (with respect to
the truncated expectation) along the sequence `c^n`, for any `c > 1`, up to a given `Œµ > 0`.
This follows from a variance control. -/
lemma strong_law_aux1 {c : ‚Ñù} (c_one : 1 < c) {Œµ : ‚Ñù} (Œµpos : 0 < Œµ) :
  ‚àÄ·µê œâ, ‚àÄ·∂† (n : ‚Ñï) in at_top,
    |‚àë i in range ‚åäc^n‚åã‚Çä, truncation (X i) i œâ - ùîº[‚àë i in range ‚åäc^n‚åã‚Çä, truncation (X i) i]|
      < Œµ * ‚åäc^n‚åã‚Çä :=
begin
  /- Let `S n = ‚àë i in range n, Y i` where `Y i = truncation (X i) i`. We should show that
  `|S k - ùîº[S k]| / k ‚â§ Œµ` along the sequence of powers of `c`. For this, we apply Borel-Cantelli:
  it suffices to show that the converse probabilites are summable. From Chebyshev inequality, this
  will follow from a variance control `‚àë' Var[S (c^i)] / (c^i)^2 < ‚àû`. This is checked in `I2` using
  pairwise independence to expand the variance of the sum as the sum of the variances, and then
  a straightforward but tedious computation (essentially boiling down to the fact that the sum of
  `1/(c ^ i)^2` beyong a threshold `j` is comparable to `1/j^2`).
  Note that we have written `c^i` in the above proof sketch, but rigorously one should put integer
  parts everywhere, making things more painful. We write `u i = ‚åäc^i‚åã‚Çä` for brevity. -/
  have c_pos : 0 < c := zero_lt_one.trans c_one,
  let œÅ : measure ‚Ñù := measure.map (X 0) ‚Ñô,
  have hX : ‚àÄ i, ae_strongly_measurable (X i) ‚Ñô :=
    Œª i, (hident i).symm.ae_strongly_measurable_snd hint.1,
  have A : ‚àÄ i, strongly_measurable (indicator (set.Ioc (-i : ‚Ñù) i) id) :=
    Œª i, strongly_measurable_id.indicator measurable_set_Ioc,
  set Y := Œª (n : ‚Ñï), truncation (X n) n with hY,
  set S := Œª n, ‚àë i in range n, Y i with hS,
  let u : ‚Ñï ‚Üí ‚Ñï := Œª n, ‚åäc ^ n‚åã‚Çä,
  have u_mono : monotone u := Œª i j hij, nat.floor_mono (pow_le_pow c_one.le hij),
  have I1 : ‚àÄ K, ‚àë j in range K, ((j : ‚Ñù) ^ 2) ‚Åª¬π * Var[Y j] ‚â§ 2 * ùîº[X 0],
  { assume K,
    calc ‚àë j in range K, ((j : ‚Ñù) ^ 2) ‚Åª¬π * Var[Y j] ‚â§
      ‚àë j in range K, ((j : ‚Ñù) ^ 2) ‚Åª¬π * ùîº[(truncation (X 0) j)^2] :
      begin
        apply sum_le_sum (Œª j hj, _),
        refine mul_le_mul_of_nonneg_left _ (inv_nonneg.2 (sq_nonneg _)),
        rw (hident j).truncation.variance_eq,
        exact variance_le_expectation_sq (hX 0).truncation,
      end
      ... ‚â§ 2 * ùîº[X 0] : sum_variance_truncation_le hint (hnonneg 0) K },
  let C := (c ^ 5 * (c - 1) ‚Åª¬π ^ 3) * (2 * ùîº[X 0]),
  have I2 : ‚àÄ N, ‚àë i in range N, ((u i : ‚Ñù) ^ 2) ‚Åª¬π * Var[S (u i)] ‚â§ C,
  { assume N,
    calc
    ‚àë i in range N, ((u i : ‚Ñù) ^ 2) ‚Åª¬π * Var[S (u i)]
        = ‚àë i in range N, ((u i : ‚Ñù) ^ 2) ‚Åª¬π * (‚àë j in range (u i), Var[Y j]) :
      begin
        congr' 1 with i,
        congr' 1,
        rw [hS, indep_fun.variance_sum],
        { assume j hj,
          exact (hident j).ae_strongly_measurable_fst.mem_‚Ñíp_truncation },
        { assume k hk l hl hkl,
          exact (hindep hkl).comp (A k).measurable (A l).measurable }
      end
    ... = ‚àë j in range (u (N - 1)),
            (‚àë i in (range N).filter (Œª i, j < u i), ((u i : ‚Ñù) ^ 2) ‚Åª¬π) * Var[Y j] :
      begin
        simp_rw [mul_sum, sum_mul, sum_sigma'],
        refine sum_bij' (Œª (p : (Œ£ (i : ‚Ñï), ‚Ñï)) hp, (‚ü®p.2, p.1‚ü© : (Œ£ (i : ‚Ñï), ‚Ñï))) _ (Œª a ha, rfl)
          (Œª (p : (Œ£ (i : ‚Ñï), ‚Ñï)) hp, (‚ü®p.2, p.1‚ü© : (Œ£ (i : ‚Ñï), ‚Ñï))) _ _ _,
        { rintros ‚ü®i, j‚ü© hij,
          simp only [mem_sigma, mem_range] at hij,
          simp only [hij.1, hij.2, mem_sigma, mem_range, mem_filter, and_true],
          exact hij.2.trans_le (u_mono (nat.le_pred_of_lt hij.1)) },
        { rintros ‚ü®i, j‚ü© hij,
          simp only [mem_sigma, mem_range, mem_filter] at hij,
          simp only [hij.2.1, hij.2.2, mem_sigma, mem_range, and_self] },
        { rintros ‚ü®i, j‚ü© hij, refl },
        { rintros ‚ü®i, j‚ü© hij, refl },
      end
    ... ‚â§ ‚àë j in range (u (N - 1)), (c ^ 5 * (c - 1) ‚Åª¬π ^ 3 / j ^ 2) * Var[Y j] :
      begin
        apply sum_le_sum (Œª j hj, _),
        rcases @eq_zero_or_pos _ _ j with rfl|hj,
        { simp only [Y, nat.cast_zero, zero_pow', ne.def, bit0_eq_zero, nat.one_ne_zero,
            not_false_iff, div_zero, zero_mul],
          simp only [nat.cast_zero, truncation_zero, variance_zero, mul_zero] },
        apply mul_le_mul_of_nonneg_right _ (variance_nonneg _ _),
        convert sum_div_nat_floor_pow_sq_le_div_sq N (nat.cast_pos.2 hj) c_one,
        { simp only [nat.cast_lt] },
        { simp only [one_div] }
      end
    ... = (c ^ 5 * (c - 1) ‚Åª¬π ^ 3) * ‚àë j in range (u (N - 1)), ((j : ‚Ñù) ^ 2) ‚Åª¬π * Var[Y j] :
      by { simp_rw [mul_sum, div_eq_mul_inv], ring_nf }
    ... ‚â§ (c ^ 5 * (c - 1) ‚Åª¬π ^ 3) * (2 * ùîº[X 0]) :
      begin
        apply mul_le_mul_of_nonneg_left (I1 _),
        apply mul_nonneg (pow_nonneg c_pos.le _),
        exact pow_nonneg (inv_nonneg.2 (sub_nonneg.2 c_one.le)) _
      end },
  have I3 : ‚àÄ N, ‚àë i in range N,
    ‚Ñô {œâ | (u i * Œµ : ‚Ñù) ‚â§ |S (u i) œâ - ùîº[S (u i)]|} ‚â§ ennreal.of_real (Œµ ‚Åª¬π ^ 2 * C),
  { assume N,
    calc ‚àë i in range N, ‚Ñô {œâ | (u i * Œµ : ‚Ñù) ‚â§ |S (u i) œâ - ùîº[S (u i)]|}
        ‚â§ ‚àë i in range N, ennreal.of_real (Var[S (u i)] / (u i * Œµ) ^ 2) :
      begin
        refine sum_le_sum (Œª i hi, _),
        apply meas_ge_le_variance_div_sq,
        { exact mem_‚Ñíp_finset_sum' _
            (Œª j hj, (hident j).ae_strongly_measurable_fst.mem_‚Ñíp_truncation) },
        { apply mul_pos (nat.cast_pos.2 _) Œµpos,
          refine zero_lt_one.trans_le _,
          apply nat.le_floor,
          rw nat.cast_one,
          apply one_le_pow_of_one_le c_one.le }
      end
    ... = ennreal.of_real (‚àë i in range N, Var[S (u i)] / (u i * Œµ) ^ 2) :
      begin
        rw ennreal.of_real_sum_of_nonneg (Œª i hi, _),
        exact div_nonneg (variance_nonneg _ _) (sq_nonneg _),
      end
    ... ‚â§ ennreal.of_real (Œµ ‚Åª¬π ^ 2 * C) :
      begin
        apply ennreal.of_real_le_of_real,
        simp_rw [div_eq_inv_mul, ‚Üê inv_pow, mul_inv, mul_comm _ (Œµ‚Åª¬π), mul_pow, mul_assoc,
          ‚Üê mul_sum],
        refine mul_le_mul_of_nonneg_left _ (sq_nonneg _),
        simp_rw [inv_pow],
        exact I2 N
      end },
  have I4 : ‚àë' i, ‚Ñô {œâ | (u i * Œµ : ‚Ñù) ‚â§ |S (u i) œâ - ùîº[S (u i)]|} < ‚àû :=
    (le_of_tendsto_of_tendsto' (ennreal.tendsto_nat_tsum _) tendsto_const_nhds I3).trans_lt
      ennreal.of_real_lt_top,
  filter_upwards [ae_eventually_not_mem I4.ne] with œâ hœâ,
  simp_rw [not_le, mul_comm, S, sum_apply] at hœâ,
  exact hœâ,
end

/- The truncation of `X·µ¢` up to `i` satisfies the strong law of large numbers
(with respect to the truncated expectation) along the sequence
`c^n`, for any `c > 1`. This follows from `strong_law_aux1` by varying `Œµ`. -/
lemma strong_law_aux2 {c : ‚Ñù} (c_one : 1 < c) :
  ‚àÄ·µê œâ, (Œª (n : ‚Ñï), ‚àë i in range ‚åäc^n‚åã‚Çä, truncation (X i) i œâ
    - ùîº[‚àë i in range ‚åäc^n‚åã‚Çä, truncation (X i) i]) =o[at_top] (Œª (n : ‚Ñï), (‚åäc^n‚åã‚Çä : ‚Ñù)) :=
begin
  obtain ‚ü®v, -, v_pos, v_lim‚ü© :
    ‚àÉ (v : ‚Ñï ‚Üí ‚Ñù), strict_anti v ‚àß (‚àÄ (n : ‚Ñï), 0 < v n) ‚àß tendsto v at_top (ùìù 0) :=
      exists_seq_strict_anti_tendsto (0 : ‚Ñù),
  have := Œª i, strong_law_aux1 X hint hindep hident hnonneg c_one (v_pos i),
  filter_upwards [ae_all_iff.2 this] with œâ hœâ,
  apply asymptotics.is_o_iff.2 (Œª Œµ Œµpos, _),
  obtain ‚ü®i, hi‚ü© : ‚àÉ i, v i < Œµ := ((tendsto_order.1 v_lim).2 Œµ Œµpos).exists,
  filter_upwards [hœâ i] with n hn,
  simp only [real.norm_eq_abs, lattice_ordered_comm_group.abs_abs, nat.abs_cast],
  exact hn.le.trans (mul_le_mul_of_nonneg_right hi.le (nat.cast_nonneg _)),
end

omit hindep hnonneg
/-- The expectation of the truncated version of `X·µ¢` behaves asymptotically like the whole
expectation. This follows from convergence and Cesaro averaging. -/
lemma strong_law_aux3 :
  (Œª n, ùîº[‚àë i in range n, truncation (X i) i] - n * ùîº[X 0]) =o[at_top] (coe : ‚Ñï ‚Üí ‚Ñù) :=
begin
  have A : tendsto (Œª i, ùîº[truncation (X i) i]) at_top (ùìù (ùîº[X 0])),
  { convert (tendsto_integral_truncation hint).comp tendsto_coe_nat_at_top_at_top,
    ext i,
    exact (hident i).truncation.integral_eq },
  convert asymptotics.is_o_sum_range_of_tendsto_zero (tendsto_sub_nhds_zero_iff.2 A),
  ext1 n,
  simp only [sum_sub_distrib, sum_const, card_range, nsmul_eq_mul, sum_apply, sub_left_inj],
  rw integral_finset_sum _ (Œª i hi, _),
  exact ((hident i).symm.integrable_snd hint).1.integrable_truncation,
end
include hindep hnonneg

/- The truncation of `X·µ¢` up to `i` satisfies the strong law of large numbers
(with respect to the original expectation) along the sequence
`c^n`, for any `c > 1`. This follows from the version from the truncated expectation, and the
fact that the truncated and the original expectations have the same asymptotic behavior. -/
lemma strong_law_aux4 {c : ‚Ñù} (c_one : 1 < c) :
  ‚àÄ·µê œâ, (Œª (n : ‚Ñï), ‚àë i in range ‚åäc^n‚åã‚Çä, truncation (X i) i œâ - ‚åäc^n‚åã‚Çä * ùîº[X 0]) =o[at_top]
    (Œª (n : ‚Ñï), (‚åäc^n‚åã‚Çä : ‚Ñù)) :=
begin
  filter_upwards [strong_law_aux2 X hint hindep hident hnonneg c_one] with œâ hœâ,
  have A : tendsto (Œª (n : ‚Ñï), ‚åäc ^ n‚åã‚Çä) at_top at_top :=
    tendsto_nat_floor_at_top.comp (tendsto_pow_at_top_at_top_of_one_lt c_one),
  convert hœâ.add ((strong_law_aux3 X hint hident).comp_tendsto A),
  ext1 n,
  simp,
end

omit hindep
/-- The truncated and non-truncated versions of `X·µ¢` have the same asymptotic behavior, as they
almost surely coincide at all but finitely many steps. This follows from a probability computation
and Borel-Cantelli. -/
lemma strong_law_aux5 :
  ‚àÄ·µê œâ, (Œª (n : ‚Ñï), ‚àë i in range n, truncation (X i) i œâ - ‚àë i in range n, X i œâ) =o[at_top]
    (Œª (n : ‚Ñï), (n : ‚Ñù)) :=
begin
  have A : ‚àë' (j : ‚Ñï), ‚Ñô {œâ | X j œâ ‚àà set.Ioi (j : ‚Ñù)} < ‚àû,
  { convert tsum_prob_mem_Ioi_lt_top hint (hnonneg 0),
    ext1 j,
    exact (hident j).measure_mem_eq measurable_set_Ioi },
  have B : ‚àÄ·µê œâ, tendsto (Œª (n : ‚Ñï), truncation (X n) n œâ - X n œâ) at_top (ùìù 0),
  { filter_upwards [ae_eventually_not_mem A.ne] with œâ hœâ,
    apply tendsto_const_nhds.congr' _,
    filter_upwards [hœâ, Ioi_mem_at_top 0] with n hn npos,
    simp only [truncation, indicator, set.mem_Ioc, id.def, function.comp_app],
    split_ifs,
    { exact (sub_self _).symm },
    { have : - (n : ‚Ñù) < X n œâ,
      { apply lt_of_lt_of_le _ (hnonneg n œâ),
        simpa only [right.neg_neg_iff, nat.cast_pos] using npos },
      simp only [this, true_and, not_le] at h,
      exact (hn h).elim } },
  filter_upwards [B] with œâ hœâ,
  convert is_o_sum_range_of_tendsto_zero hœâ,
  ext n,
  rw sum_sub_distrib,
end
include hindep

/- `X·µ¢` satisfies the strong law of large numbers along the sequence
`c^n`, for any `c > 1`. This follows from the version for the truncated `X·µ¢`, and the fact that
`X·µ¢` and its truncated version have the same asymptotic behavior. -/
lemma strong_law_aux6 {c : ‚Ñù} (c_one : 1 < c) :
  ‚àÄ·µê œâ, tendsto (Œª (n : ‚Ñï), (‚àë i in range ‚åäc^n‚åã‚Çä, X i œâ) / ‚åäc^n‚åã‚Çä) at_top (ùìù (ùîº[X 0])) :=
begin
   have H : ‚àÄ (n : ‚Ñï), (0 : ‚Ñù) < ‚åäc ^ n‚åã‚Çä,
  { assume n,
    refine zero_lt_one.trans_le _,
    simp only [nat.one_le_cast, nat.one_le_floor_iff, one_le_pow_of_one_le c_one.le n] },
  filter_upwards [strong_law_aux4 X hint hindep hident hnonneg c_one,
    strong_law_aux5 X hint hident hnonneg] with œâ hœâ h'œâ,
  rw [‚Üê tendsto_sub_nhds_zero_iff, ‚Üê asymptotics.is_o_one_iff ‚Ñù],
  have L : (Œª n : ‚Ñï, ‚àë i in range ‚åäc^n‚åã‚Çä, X i œâ - ‚åäc^n‚åã‚Çä * ùîº[X 0]) =o[at_top] (Œª n, (‚åäc^n‚åã‚Çä : ‚Ñù)),
  { have A : tendsto (Œª (n : ‚Ñï), ‚åäc ^ n‚åã‚Çä) at_top at_top :=
      tendsto_nat_floor_at_top.comp (tendsto_pow_at_top_at_top_of_one_lt c_one),
    convert hœâ.sub (h'œâ.comp_tendsto A),
    ext1 n,
    simp only [sub_sub_sub_cancel_left] },
  convert L.mul_is_O (is_O_refl (Œª (n : ‚Ñï), (‚åäc ^ n‚åã‚Çä : ‚Ñù) ‚Åª¬π) at_top);
  { ext1 n,
    field_simp [(H n).ne'] },
end

/-- `X·µ¢` satisfies the strong law of large numbers along all integers. This follows from the
corresponding fact along the sequences `c^n`, and the fact that any integer can be sandwiched
between `c^n` and `c^(n+1)` with comparably small error if `c` is close enough to `1`
(which is formalized in `tendsto_div_of_monotone_of_tendsto_div_floor_pow`). -/
lemma strong_law_aux7 :
  ‚àÄ·µê œâ, tendsto (Œª (n : ‚Ñï), (‚àë i in range n, X i œâ) / n) at_top (ùìù (ùîº[X 0])) :=
begin
  obtain ‚ü®c, -, cone, clim‚ü© :
    ‚àÉ (c : ‚Ñï ‚Üí ‚Ñù), strict_anti c ‚àß (‚àÄ (n : ‚Ñï), 1 < c n) ‚àß tendsto c at_top (ùìù 1) :=
      exists_seq_strict_anti_tendsto (1 : ‚Ñù),
  have : ‚àÄ k, ‚àÄ·µê œâ, tendsto (Œª (n : ‚Ñï), (‚àë i in range ‚åäc k ^ n‚åã‚Çä, X i œâ) / ‚åäc k ^ n‚åã‚Çä)
    at_top (ùìù (ùîº[X 0])) := Œª k, strong_law_aux6 X hint hindep hident hnonneg (cone k),
  filter_upwards [ae_all_iff.2 this] with œâ hœâ,
  apply tendsto_div_of_monotone_of_tendsto_div_floor_pow _ _ _ c cone clim _,
  { assume m n hmn,
    exact sum_le_sum_of_subset_of_nonneg (range_mono hmn) (Œª i hi h'i, hnonneg i œâ) },
  { exact hœâ }
end

end strong_law_nonneg

/-- *Strong law of large numbers*, almost sure version: if `X n` is a sequence of independent
identically distributed integrable real-valued random variables, then `‚àë i in range n, X i / n`
converges almost surely to `ùîº[X 0]`. We give here the strong version, due to Etemadi, that only
requires pairwise independence. -/
theorem strong_law_ae
  (X : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (hint : integrable (X 0))
  (hindep : pairwise (Œª i j, indep_fun (X i) (X j)))
  (hident : ‚àÄ i, ident_distrib (X i) (X 0)) :
  ‚àÄ·µê œâ, tendsto (Œª (n : ‚Ñï), (‚àë i in range n, X i œâ) / n) at_top (ùìù (ùîº[X 0])) :=
begin
  let pos : ‚Ñù ‚Üí ‚Ñù := (Œª x, max x 0),
  let neg : ‚Ñù ‚Üí ‚Ñù := (Œª x, max (-x) 0),
  have posm : measurable pos := measurable_id'.max measurable_const,
  have negm : measurable neg := measurable_id'.neg.max measurable_const,
  have A : ‚àÄ·µê œâ, tendsto (Œª (n : ‚Ñï), (‚àë i in range n, (pos ‚àò (X i)) œâ) / n)
    at_top (ùìù (ùîº[pos ‚àò (X 0)])) :=
      strong_law_aux7 _ hint.pos_part (Œª i j hij, (hindep hij).comp posm posm)
        (Œª i, (hident i).comp posm) (Œª i œâ, le_max_right _ _),
  have B : ‚àÄ·µê œâ, tendsto (Œª (n : ‚Ñï), (‚àë i in range n, (neg ‚àò (X i)) œâ) / n)
    at_top (ùìù (ùîº[neg ‚àò (X 0)])) :=
      strong_law_aux7 _ hint.neg_part (Œª i j hij, (hindep hij).comp negm negm)
        (Œª i, (hident i).comp negm) (Œª i œâ, le_max_right _ _),
  filter_upwards [A, B] with œâ hœâpos hœâneg,
  convert hœâpos.sub hœâneg,
  { simp only [‚Üê sub_div, ‚Üê sum_sub_distrib, max_zero_sub_max_neg_zero_eq_self] },
  { simp only [‚Üêintegral_sub hint.pos_part hint.neg_part, max_zero_sub_max_neg_zero_eq_self] }
end

end strong_law_ae

section strong_law_Lp

variables {Œ© : Type*} [measure_space Œ©] [is_probability_measure (‚Ñô : measure Œ©)]

/-- *Strong law of large numbers*, L·µñ version: if `X n` is a sequence of independent
identically distributed real-valued random variables in L·µñ, then `‚àë i in range n, X i / n`
converges in L·µñ to `ùîº[X 0]`. -/
theorem strong_law_Lp
  {p : ‚Ñù‚â•0‚àû} (hp : 1 ‚â§ p) (hp' : p ‚â† ‚àû)
  (X : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (h‚Ñíp : mem_‚Ñíp (X 0) p)
  (hindep : pairwise (Œª i j, indep_fun (X i) (X j)))
  (hident : ‚àÄ i, ident_distrib (X i) (X 0)) :
  tendsto (Œª n, snorm (Œª œâ, (‚àë i in range n, X i œâ) / n - ùîº[X 0]) p ‚Ñô) at_top (ùìù 0) :=
begin
  have hmeas : ‚àÄ i, ae_strongly_measurable (X i) ‚Ñô :=
    Œª i, (hident i).ae_strongly_measurable_iff.2 h‚Ñíp.1,
  have hint : integrable (X 0) ‚Ñô := h‚Ñíp.integrable hp,
  have havg : ‚àÄ n, ae_strongly_measurable (Œª œâ, (‚àë i in range n, X i œâ) / n) ‚Ñô,
  { intro n,
    simp_rw div_eq_mul_inv,
    exact ae_strongly_measurable.mul_const (ae_strongly_measurable_sum _  (Œª i _, hmeas i)) _ },
  refine tendsto_Lp_of_tendsto_in_measure _ hp hp' havg (mem_‚Ñíp_const _) _
    (tendsto_in_measure_of_tendsto_ae havg (strong_law_ae _ hint hindep hident)),
  rw (_ : (Œª n œâ, (‚àë i in range n, X i œâ) / ‚Üën) = Œª n, (‚àë i in range n, X i) / ‚Üën),
  { exact (uniform_integrable_average hp $
      mem_‚Ñíp.uniform_integrable_of_ident_distrib hp hp' h‚Ñíp hident).2.1 },
  { ext n œâ,
    simp only [pi.coe_nat, pi.div_apply, sum_apply] }
end

end strong_law_Lp

end probability_theory
