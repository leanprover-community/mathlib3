/-
Copyright (c) 2022 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/

import probability.moments

/-! # Sub-Gaussian random variables

TODO explain the many equivalent definitions.

* (i)
* (ii)
* (iii)
* (iv)

If `ùîº[X] = 0` then these four properties are equivalent to
* (v) for all `t : ‚Ñù`, `log ùîº[exp(t*X)] ‚â§ c t^2 / 2`.

## Main definitions

* `subgaussian_cgf X Œº c`: the random variable `X` has a sub-Gaussian cgf, with constant `c`. That
  is, for all `t ‚àà ‚Ñù` `exp(t*X)` is integrable (the cgf is well defined) and
  `cgf X Œº t ‚â§ c * t^2 / 2`.

## Main statements

* `Indep_fun.chernoff_sum_range`: For `X : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù` an independent family of real random
  variables, all with sub-Gaussian cdf with constant `c`, we have for all `Œµ ‚â• 0`,
  `‚Ñô(Œµ ‚â§ ‚àë i in finset.range n, X i) ‚â§ exp(- Œµ^2 / (2 * c * n))`.

## References

* [F. Bar, *Quuxes*][bibkey]

-/


open measure_theory filter finset

noncomputable theory

open_locale big_operators measure_theory probability_theory ennreal nnreal

namespace probability_theory

variables {Œ© : Type*} {m : measurable_space Œ©} {X : Œ© ‚Üí ‚Ñù} {Œº : measure Œ©} {t c Œµ : ‚Ñù}

include m

def subgaussian_cgf (X : Œ© ‚Üí ‚Ñù) (Œº : measure Œ©) (c : ‚Ñù) : Prop :=
‚àÄ t, integrable (Œª œâ, real.exp (t * X œâ)) Œº ‚àß cgf X Œº t ‚â§ c * t^2 / 2

lemma subgaussian_cgf.cgf_le (h : subgaussian_cgf X Œº c) (t : ‚Ñù) : cgf X Œº t ‚â§ c * t^2 / 2 :=
(h t).2

lemma subgaussian_cgf.integrable_exp_mul (h : subgaussian_cgf X Œº c) (t : ‚Ñù) :
  integrable (Œª œâ, real.exp (t * X œâ)) Œº := (h t).1

lemma subgaussian_cgf.mgf_le (h : subgaussian_cgf X Œº c) (t : ‚Ñù) :
  mgf X Œº t ‚â§ real.exp (c * t^2 / 2) :=
calc mgf X Œº t ‚â§ real.exp (cgf X Œº t) : real.le_exp_log _
... ‚â§ real.exp (c * t^2 / 2) : real.exp_monotone (h.cgf_le t)

lemma subgaussian_cgf_zero [is_probability_measure Œº] (hc : 0 ‚â§ c) : subgaussian_cgf 0 Œº c :=
begin
  refine Œª t, ‚ü®_, _‚ü©,
  { simp only [pi.zero_apply, mul_zero, real.exp_zero],
    exact integrable_const _, },
  { simp only [cgf_zero_fun, measure_univ, ennreal.one_to_real, real.log_one],
    exact div_nonneg (mul_nonneg hc (sq_nonneg _)) zero_le_two, },
end

lemma subgaussian_cgf.neg (h : subgaussian_cgf X Œº c) :
  subgaussian_cgf (-X) Œº c :=
begin
  refine Œª t, ‚ü®_, _‚ü©,
  { simp_rw [pi.neg_apply, mul_neg, ‚Üê neg_mul],
    exact (h (-t)).1, },
  { rw cgf_neg,
    refine (h.cgf_le (-t)).trans _,
    rw neg_pow_two, },
end

lemma subgaussian_cgf.add_indep_fun {Y : Œ© ‚Üí ‚Ñù} {cX cY : ‚Ñù} (hX : subgaussian_cgf X Œº cX)
  (hY : subgaussian_cgf Y Œº cY) (hindep : indep_fun X Y Œº) :
  subgaussian_cgf (X + Y) Œº (cX + cY) :=
begin
  intros t,
  refine ‚ü®hindep.integrable_exp_mul_add (hX.integrable_exp_mul t) (hY.integrable_exp_mul t), _‚ü©,
  rw hindep.cgf_add (hX.integrable_exp_mul t) (hY.integrable_exp_mul t),
  calc cgf X Œº t + cgf Y Œº t
      ‚â§ cX * t ^ 2 / 2 + cY * t ^ 2 / 2 : add_le_add (hX.cgf_le t) (hY.cgf_le t)
  ... = (cX + cY) * t ^ 2 / 2 : by ring,
end

lemma Indep_fun.subgaussian_cgf_sum {Œπ : Type*} [is_probability_measure Œº]
  {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (h_indep : Indep_fun (Œª i, infer_instance) X Œº) {c : Œπ ‚Üí ‚Ñù}
  (h_meas : ‚àÄ i, measurable (X i))
  {s : finset Œπ} (h_subg : ‚àÄ i ‚àà s, subgaussian_cgf (X i) Œº (c i)) :
  subgaussian_cgf (‚àë i in s, X i) Œº (‚àë i in s, c i) :=
begin
  refine Œª t,
    ‚ü®h_indep.integrable_exp_mul_sum h_meas (Œª i hi, (h_subg i hi).integrable_exp_mul t), _‚ü©,
  rw [h_indep.cgf_sum h_meas (Œª i hi, (h_subg i hi).integrable_exp_mul  t), sum_mul, sum_div],
  exact sum_le_sum (Œª i hi, (h_subg i hi).cgf_le t),
end

lemma measure_le_one [is_probability_measure Œº] (s : set Œ©) : Œº s ‚â§ 1 :=
(measure_mono (set.subset_univ _)).trans_eq measure_univ

lemma to_real_measure_le_one [is_probability_measure Œº] (s : set Œ©) : (Œº s).to_real ‚â§ 1 :=
begin
  rw [‚Üê ennreal.one_to_real, ennreal.to_real_le_to_real (measure_ne_top Œº _) ennreal.one_ne_top],
  exact measure_le_one _,
end

lemma subgaussian_cgf.chernoff_bound' [is_finite_measure Œº]
  (h : subgaussian_cgf X Œº c) (hc : 0 < c) (hŒµ : 0 ‚â§ Œµ) :
  (Œº {œâ | Œµ ‚â§ X œâ}).to_real ‚â§ real.exp(- Œµ^2 / (2*c)) :=
begin
  have h_le_t : ‚àÄ t : ‚Ñù, 0 ‚â§ t ‚Üí (Œº {œâ | Œµ ‚â§ X œâ}).to_real ‚â§ real.exp(- t * Œµ + c * t^2 / 2),
  { refine Œª t ht, (measure_ge_le_exp_cgf Œµ ht (h.integrable_exp_mul t)).trans _,
    exact real.exp_monotone (add_le_add le_rfl (h.cgf_le t)), },
  refine (h_le_t (Œµ / c) (div_nonneg hŒµ hc.le)).trans_eq _,
  congr,
  rw [div_pow, pow_two c, mul_div, mul_div_mul_comm, div_self hc.ne', one_mul, neg_mul,
    div_mul_eq_mul_div, ‚Üê pow_two, mul_comm, ‚Üê div_div],
  ring,
end

lemma subgaussian_cgf.chernoff_bound [is_probability_measure Œº]
  (h : subgaussian_cgf X Œº c) (hŒµ : 0 ‚â§ Œµ) :
  (Œº {œâ | Œµ ‚â§ X œâ}).to_real ‚â§ real.exp(- Œµ^2 / (2*c)) :=
begin
  cases lt_or_le 0 c with hc hc,
  { exact h.chernoff_bound' hc hŒµ, },
  suffices : 1 ‚â§ real.exp (-Œµ ^ 2 / (2 * c)), from (to_real_measure_le_one _).trans this,
  rw real.one_le_exp_iff,
  exact div_nonneg_of_nonpos (neg_nonpos_of_nonneg (sq_nonneg _))
    (mul_nonpos_of_nonneg_of_nonpos zero_le_two hc),
end

lemma Indep_fun.chernoff_sum {Œπ : Type*} [is_probability_measure Œº]
  {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (h_indep : Indep_fun (Œª i, infer_instance) X Œº) {c : Œπ ‚Üí ‚Ñù}
  (h_meas : ‚àÄ i, measurable (X i))
  {s : finset Œπ} (hs : s.nonempty) (h_subg : ‚àÄ i ‚àà s, subgaussian_cgf (X i) Œº (c i)) (hŒµ : 0 ‚â§ Œµ) :
  (Œº {œâ | Œµ ‚â§ ‚àë i in s, X i œâ}).to_real ‚â§ real.exp(- Œµ^2 / (2 * (‚àë i in s, c i))) :=
begin
  simp_rw ‚Üê finset.sum_apply,
  exact (h_indep.subgaussian_cgf_sum h_meas h_subg).chernoff_bound hŒµ,
end

lemma Indep_fun.chernoff_sum_same {Œπ : Type*} [is_probability_measure Œº] (hŒµ : 0 ‚â§ Œµ)
  {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (h_indep : Indep_fun (Œª i, infer_instance) X Œº)
  (h_meas : ‚àÄ i, measurable (X i))
  {s : finset Œπ} (hs : s.nonempty) (h_subg : ‚àÄ i ‚àà s, subgaussian_cgf (X i) Œº c) :
  (Œº {œâ | Œµ ‚â§ ‚àë i in s, X i œâ}).to_real ‚â§ real.exp(- Œµ^2 / (2 * c * (card s))) :=
calc (Œº {œâ | Œµ ‚â§ ‚àë i in s, X i œâ}).to_real
    ‚â§ real.exp(- Œµ^2 / (2 * (‚àë i in s, c))) : h_indep.chernoff_sum h_meas hs h_subg hŒµ
... = real.exp(- Œµ^2 / (2 * c * (card s))) :
    by { rw mul_assoc, congr, rw [sum_const, nsmul_eq_mul, mul_comm c], }

lemma Indep_fun.chernoff_sum_range [is_probability_measure Œº]
  {X : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} (h_indep : Indep_fun (Œª i, infer_instance) X Œº) (h_meas : ‚àÄ i, measurable (X i))
  (h_subg : ‚àÄ i, subgaussian_cgf (X i) Œº c) (hŒµ : 0 ‚â§ Œµ) (n : ‚Ñï) :
  (Œº {œâ | Œµ ‚â§ ‚àë i in finset.range n, X i œâ}).to_real ‚â§ real.exp(- Œµ^2 / (2 * c * n)) :=
begin
  cases n,
  { simp only [range_zero, sum_empty, nat.cast_zero, mul_zero, div_zero, real.exp_zero],
    exact to_real_measure_le_one _, },
  calc (Œº {œâ | Œµ ‚â§ ‚àë i in finset.range n.succ, X i œâ}).to_real
      ‚â§ real.exp(- Œµ^2 / (2 * c * (card (finset.range n.succ)))) :
        h_indep.chernoff_sum_same hŒµ h_meas ‚ü®0, finset.mem_range.mpr n.succ_pos‚ü© (Œª i _, h_subg i)
  ... = real.exp(- Œµ^2 / (2 * c * n.succ)) : by rw card_range
end

lemma Indep_fun.chernoff_mean_range [is_probability_measure Œº]
  {X : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} (h_indep : Indep_fun (Œª i, infer_instance) X Œº) (h_meas : ‚àÄ i, measurable (X i))
  (h_subg : ‚àÄ i, subgaussian_cgf (X i) Œº c) (hŒµ : 0 ‚â§ Œµ) (n : ‚Ñï) :
  (Œº {œâ | Œµ ‚â§ (‚àë i in finset.range n, X i œâ) / n}).to_real ‚â§ real.exp(- n * Œµ^2 / (2 * c)) :=
begin
  cases n,
  { simp only [range_zero, sum_empty, nat.cast_zero, neg_zero', zero_mul, zero_div, real.exp_zero],
    exact to_real_measure_le_one _, },
  have h_nŒµ : 0 ‚â§ ‚Üën.succ * Œµ := mul_nonneg (nat.cast_nonneg _) hŒµ,
  have h := h_indep.chernoff_sum_range h_meas h_subg h_nŒµ n.succ,
  refine (eq.trans_le _ (h.trans_eq _)),
  { congr' with œâ,
    rw [le_div_iff (nat.cast_pos.mpr n.succ_pos), mul_comm Œµ],
    apply_instance, },
  { congr' 1,
    by_cases hc : c = 0,
    { simp only [hc, mul_zero, zero_mul, div_zero], },
    field_simp [n.cast_add_one_ne_zero],
    ring, },
end

end probability_theory
