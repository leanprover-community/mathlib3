/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import probability.notation
import probability.integration

/-!
# Variance of random variables

We define the variance of a real-valued random variable as `Var[X] = ğ”¼[(X - ğ”¼[X])^2]` (in the
`probability_theory` locale).

We prove the basic properties of the variance:
* `variance_le_expectation_sq`: the inequality `Var[X] â‰¤ ğ”¼[X^2]`.
* `meas_ge_le_variance_div_sq`: Chebyshev's inequality, i.e.,
      `â„™ {Ï‰ | c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ ennreal.of_real (Var[X] / c ^ 2)`.
* `indep_fun.variance_add`: the variance of the sum of two independent random variables is the sum
  of the variances.
* `indep_fun.variance_sum`: the variance of a finite sum of pairwise independent random variables is
  the sum of the variances.
-/

open measure_theory filter finset

noncomputable theory

open_locale big_operators measure_theory probability_theory ennreal nnreal

namespace probability_theory

/-- The variance of a random variable is `ğ”¼[X^2] - ğ”¼[X]^2` or, equivalently, `ğ”¼[(X - ğ”¼[X])^2]`. We
use the latter as the definition, to ensure better behavior even in garbage situations. -/
def variance {Î© : Type*} {m : measurable_space Î©} (f : Î© â†’ â„) (Î¼ : measure Î©) : â„ :=
Î¼[(f - (Î» x, Î¼[f])) ^ 2]

@[simp] lemma variance_zero {Î© : Type*} {m : measurable_space Î©} (Î¼ : measure Î©) :
  variance 0 Î¼ = 0 :=
by simp [variance]

lemma variance_nonneg {Î© : Type*} {m : measurable_space Î©} (f : Î© â†’ â„) (Î¼ : measure Î©) :
  0 â‰¤ variance f Î¼ :=
integral_nonneg (Î» x, sq_nonneg _)

lemma variance_mul {Î© : Type*} {m : measurable_space Î©} (c : â„) (f : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (Î» x, c * f x) Î¼ = c^2 * variance f Î¼ :=
calc
variance (Î» x, c * f x) Î¼
    = âˆ« x, (c * f x - âˆ« y, c * f y âˆ‚Î¼) ^ 2 âˆ‚Î¼ : rfl
... = âˆ« x, (c * (f x - âˆ« y, f y âˆ‚Î¼)) ^ 2 âˆ‚Î¼ :
  by { congr' 1 with x, simp_rw [integral_mul_left, mul_sub] }
... = c^2 * variance f Î¼ :
  by { simp_rw [mul_pow, integral_mul_left], refl }

lemma variance_smul {Î© : Type*} {m : measurable_space Î©} (c : â„) (f : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (c â€¢ f) Î¼ = c^2 * variance f Î¼ :=
variance_mul c f Î¼

lemma variance_smul' {A : Type*} [comm_semiring A] [algebra A â„]
  {Î© : Type*} {m : measurable_space Î©} (c : A) (f : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (c â€¢ f) Î¼ = c^2 â€¢ variance f Î¼ :=
begin
  convert variance_smul (algebra_map A â„ c) f Î¼,
  { ext1 x, simp only [algebra_map_smul], },
  { simp only [algebra.smul_def, map_pow], }
end

localized
"notation `Var[` X `]` := probability_theory.variance X measure_theory.measure_space.volume"
in probability_theory

variables {Î© : Type*} [measure_space Î©] [is_probability_measure (volume : measure Î©)]

lemma variance_def' {X : Î© â†’ â„} (hX : mem_â„’p X 2) :
  Var[X] = ğ”¼[X^2] - ğ”¼[X]^2 :=
begin
  rw [variance, sub_sq', integral_sub', integral_add'], rotate,
  { exact hX.integrable_sq },
  { convert integrable_const (ğ”¼[X] ^ 2),
    apply_instance },
  { apply hX.integrable_sq.add,
    convert integrable_const (ğ”¼[X] ^ 2),
    apply_instance },
  { exact ((hX.integrable ennreal.one_le_two).const_mul 2).mul_const' _ },
  simp only [integral_mul_right, pi.pow_apply, pi.mul_apply, pi.bit0_apply, pi.one_apply,
    integral_const (integral â„™ X ^ 2), integral_mul_left (2 : â„), one_mul,
    variance, pi.pow_apply, measure_univ, ennreal.one_to_real, algebra.id.smul_eq_mul],
  ring,
end

lemma variance_le_expectation_sq {X : Î© â†’ â„} :
  Var[X] â‰¤ ğ”¼[X^2] :=
begin
  by_cases h_int : integrable X, swap,
  { simp only [variance, integral_undef h_int, pi.pow_apply, pi.sub_apply, sub_zero] },
  by_cases hX : mem_â„’p X 2,
  { rw variance_def' hX,
    simp only [sq_nonneg, sub_le_self_iff] },
  { rw [variance, integral_undef],
    { exact integral_nonneg (Î» a, sq_nonneg _) },
    { assume h,
      have A : mem_â„’p (X - Î» (x : Î©), ğ”¼[X]) 2 â„™ := (mem_â„’p_two_iff_integrable_sq
        (h_int.ae_strongly_measurable.sub ae_strongly_measurable_const)).2 h,
      have B : mem_â„’p (Î» (x : Î©), ğ”¼[X]) 2 â„™ := mem_â„’p_const _,
      apply hX,
      convert A.add B,
      simp } }
end

/-- *Chebyshev's inequality* : one can control the deviation probability of a real random variable
from its expectation in terms of the variance. -/
theorem meas_ge_le_variance_div_sq {X : Î© â†’ â„} (hX : mem_â„’p X 2) {c : â„} (hc : 0 < c) :
  â„™ {Ï‰ | c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ ennreal.of_real (Var[X] / c ^ 2) :=
begin
  have A : (ennreal.of_real c : â„â‰¥0âˆ) â‰  0,
    by simp only [hc, ne.def, ennreal.of_real_eq_zero, not_le],
  have B : ae_strongly_measurable (Î» (Ï‰ : Î©), ğ”¼[X]) â„™ := ae_strongly_measurable_const,
  convert meas_ge_le_mul_pow_snorm â„™ ennreal.two_ne_zero ennreal.two_ne_top
    (hX.ae_strongly_measurable.sub B) A,
  { ext Ï‰,
    set d : â„â‰¥0 := âŸ¨c, hc.leâŸ© with hd,
    have cd : c = d, by simp only [subtype.coe_mk],
    simp only [pi.sub_apply, ennreal.coe_le_coe, â† real.norm_eq_abs, â† coe_nnnorm,
      nnreal.coe_le_coe, cd, ennreal.of_real_coe_nnreal] },
  { rw (hX.sub (mem_â„’p_const _)).snorm_eq_integral_rpow_norm
      ennreal.two_ne_zero ennreal.two_ne_top,
    simp only [pi.sub_apply, ennreal.to_real_bit0, ennreal.one_to_real],
    rw ennreal.of_real_rpow_of_nonneg _ zero_le_two, rotate,
    { apply real.rpow_nonneg_of_nonneg,
      exact integral_nonneg (Î» x, real.rpow_nonneg_of_nonneg (norm_nonneg _) _) },
    rw [variance, â† real.rpow_mul, inv_mul_cancel], rotate,
    { exact two_ne_zero },
    { exact integral_nonneg (Î» x, real.rpow_nonneg_of_nonneg (norm_nonneg _) _) },
    simp only [pi.pow_apply, pi.sub_apply, real.rpow_two, real.rpow_one, real.norm_eq_abs,
      pow_bit0_abs, ennreal.of_real_inv_of_pos hc, ennreal.rpow_two],
    rw [â† ennreal.of_real_pow (inv_nonneg.2 hc.le), â† ennreal.of_real_mul (sq_nonneg _),
      div_eq_inv_mul, inv_pow] }
end

/-- The variance of the sum of two independent random variables is the sum of the variances. -/
theorem indep_fun.variance_add {X Y : Î© â†’ â„}
  (hX : mem_â„’p X 2) (hY : mem_â„’p Y 2) (h : indep_fun X Y) :
  Var[X + Y] = Var[X] + Var[Y] :=
calc
Var[X + Y] = ğ”¼[Î» a, (X a)^2 + (Y a)^2 + 2 * X a * Y a] - ğ”¼[X+Y]^2 :
  by simp [variance_def' (hX.add hY), add_sq']
... = (ğ”¼[X^2] + ğ”¼[Y^2] + 2 * ğ”¼[X * Y]) - (ğ”¼[X] + ğ”¼[Y])^2 :
begin
  simp only [pi.add_apply, pi.pow_apply, pi.mul_apply, mul_assoc],
  rw [integral_add, integral_add, integral_add, integral_mul_left],
  { exact hX.integrable ennreal.one_le_two },
  { exact hY.integrable ennreal.one_le_two },
  { exact hX.integrable_sq },
  { exact hY.integrable_sq },
  { exact hX.integrable_sq.add hY.integrable_sq },
  { apply integrable.const_mul,
    exact h.integrable_mul (hX.integrable ennreal.one_le_two) (hY.integrable ennreal.one_le_two) }
end
... = (ğ”¼[X^2] + ğ”¼[Y^2] + 2 * (ğ”¼[X] * ğ”¼[Y])) - (ğ”¼[X] + ğ”¼[Y])^2 :
begin
  congr,
  exact h.integral_mul_of_integrable
    (hX.integrable ennreal.one_le_two) (hY.integrable ennreal.one_le_two),
end
... = Var[X] + Var[Y] :
  by { simp only [variance_def', hX, hY, pi.pow_apply], ring }

/-- The variance of a finite sum of pairwise independent random variables is the sum of the
variances. -/
theorem indep_fun.variance_sum {Î¹ : Type*} {X : Î¹ â†’ Î© â†’ â„} {s : finset Î¹}
  (hs : âˆ€ i âˆˆ s, mem_â„’p (X i) 2) (h : set.pairwise â†‘s (Î» i j, indep_fun (X i) (X j))) :
  Var[âˆ‘ i in s, X i] = âˆ‘ i in s, Var[X i] :=
begin
  classical,
  induction s using finset.induction_on with k s ks IH,
  { simp only [finset.sum_empty, variance_zero] },
  rw [variance_def' (mem_â„’p_finset_sum' _ hs), sum_insert ks, sum_insert ks],
  simp only [add_sq'],
  calc ğ”¼[X k ^ 2 + (âˆ‘ i in s, X i) ^ 2 + 2 * X k * âˆ‘ i in s, X i] - ğ”¼[X k + âˆ‘ i in s, X i] ^ 2
  = (ğ”¼[X k ^ 2] + ğ”¼[(âˆ‘ i in s, X i) ^ 2] + ğ”¼[2 * X k * âˆ‘ i in s, X i])
    - (ğ”¼[X k] + ğ”¼[âˆ‘ i in s, X i]) ^ 2 :
  begin
    rw [integral_add', integral_add', integral_add'],
    { exact mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_self _ _)) },
    { apply integrable_finset_sum' _ (Î» i hi, _),
      exact mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_of_mem hi)) },
    { exact mem_â„’p.integrable_sq (hs _ (mem_insert_self _ _)) },
    { apply mem_â„’p.integrable_sq,
      exact mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))) },
    { apply integrable.add,
      { exact mem_â„’p.integrable_sq (hs _ (mem_insert_self _ _)) },
      { apply mem_â„’p.integrable_sq,
        exact mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))) } },
    { rw mul_assoc,
      apply integrable.const_mul _ 2,
      simp only [mul_sum, sum_apply, pi.mul_apply],
      apply integrable_finset_sum _ (Î» i hi, _),
      apply indep_fun.integrable_mul _
        (mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_self _ _)))
        (mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_of_mem hi))),
      apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) }
  end
  ... = Var[X k] + Var[âˆ‘ i in s, X i] +
    (ğ”¼[2 * X k * âˆ‘ i in s, X i] - 2 * ğ”¼[X k] * ğ”¼[âˆ‘ i in s, X i]) :
  begin
    rw [variance_def' (hs _ (mem_insert_self _ _)),
        variance_def' (mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))))],
    ring,
  end
  ... = Var[X k] + Var[âˆ‘ i in s, X i] :
  begin
    simp only [mul_assoc, integral_mul_left, pi.mul_apply, pi.bit0_apply, pi.one_apply, sum_apply,
      add_right_eq_self, mul_sum],
    rw integral_finset_sum s (Î» i hi, _), swap,
    { apply integrable.const_mul _ 2,
      apply indep_fun.integrable_mul _
        (mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_self _ _)))
        (mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_of_mem hi))),
      apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) },
    rw [integral_finset_sum s
      (Î» i hi, (mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_of_mem hi)))),
      mul_sum, mul_sum, â† sum_sub_distrib],
    apply finset.sum_eq_zero (Î» i hi, _),
    rw [integral_mul_left, indep_fun.integral_mul_of_integrable', sub_self],
    { apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) },
    { exact mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_self _ _)) },
    { exact mem_â„’p.integrable ennreal.one_le_two (hs _ (mem_insert_of_mem hi)) }
  end
  ... = Var[X k] + âˆ‘ i in s, Var[X i] :
    by rw IH (Î» i hi, hs i (mem_insert_of_mem hi))
      (h.mono (by simp only [coe_insert, set.subset_insert]))
end

end probability_theory
