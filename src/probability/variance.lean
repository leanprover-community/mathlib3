/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import probability.notation
import probability.integration

/-!
# Variance of random variables

We define the variance of a real-valued random variable as `Var[X] = ğ”¼[(X - ğ”¼[X])^2]` (in the
`probability_theory` locale).

We prove the basic properties of the variance:
* `variance_le_expectation_sq`: the inequality `Var[X] â‰¤ ğ”¼[X^2]`.
* `meas_ge_le_variance_div_sq`: Chebyshev's inequality, i.e.,
      `â„™ {Ï‰ | c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ ennreal.of_real (Var[X] / c ^ 2)`.
* `indep_fun.variance_add`: the variance of the sum of two independent random variables is the sum
  of the variances.
* `indep_fun.variance_sum`: the variance of a finite sum of pairwise independent random variables is
  the sum of the variances.
-/

open measure_theory filter finset

noncomputable theory

open_locale big_operators measure_theory probability_theory ennreal nnreal

namespace probability_theory

def evariance {Î© : Type*} {m : measurable_space Î©} (f : Î© â†’ â„) (Î¼ : measure Î©) : â„â‰¥0âˆ :=
âˆ«â» Ï‰, âˆ¥f Ï‰ - Î¼[f]âˆ¥â‚Š^2 âˆ‚Î¼

/-- The variance of a random variable is `ğ”¼[X^2] - ğ”¼[X]^2` or, equivalently, `ğ”¼[(X - ğ”¼[X])^2]`. We
use the latter as the definition, to ensure better behavior even in garbage situations. -/
def variance {Î© : Type*} {m : measurable_space Î©} (f : Î© â†’ â„) (Î¼ : measure Î©) : â„ :=
(evariance f Î¼).to_real

section move

lemma real.to_nnreal_of_nonneg {r : â„} (hr : 0 â‰¤ r) : r.to_nnreal = âŸ¨r, hrâŸ© :=
by simp_rw [real.to_nnreal, max_eq_left hr]

lemma real.to_nnreal_nonneg_eq_nnnorm {r : â„} (hr : 0 â‰¤ r) : r.to_nnreal = âˆ¥râˆ¥â‚Š :=
begin
  rw real.to_nnreal_of_nonneg hr,
  congr,
  rw [real.norm_eq_abs, abs_of_nonneg hr],
end

lemma real.to_nnreal_mul_nnnorm {r : â„} (s : â„) (hr : 0 â‰¤ r) : r.to_nnreal * âˆ¥sâˆ¥â‚Š = âˆ¥r * sâˆ¥â‚Š :=
begin
  rw real.to_nnreal_of_nonneg hr,
  simp only [nnnorm_mul, mul_eq_mul_right_iff],
  refine or.inl (nnreal.eq _),
  simp only [subtype.coe_mk, coe_nnnorm, real.norm_eq_abs, abs_of_nonneg hr]
end

end move

lemma evariance_eq_lintegral_of_real {Î© : Type*} {m : measurable_space Î©}
  (f : Î© â†’ â„) (Î¼ : measure Î©) :
  evariance f Î¼ = âˆ«â» Ï‰, ennreal.of_real ((f Ï‰ - Î¼[f])^2) âˆ‚Î¼ :=
begin
  rw evariance,
  congr,
  ext1 Ï‰,
  rw [pow_two, â† ennreal.coe_mul, â† nnnorm_mul, â† pow_two],
  congr,
  exact (real.to_nnreal_nonneg_eq_nnnorm $ sq_nonneg _).symm,
end

lemma variance_eq_of_integral_eq_zero {Î© : Type*} {m : measurable_space Î©}
  {f : Î© â†’ â„} {Î¼ : measure Î©} (hf : mem_â„’p f 2 Î¼) (hfint : Î¼[f] = 0) :
  variance f Î¼ = Î¼[f^2] :=
begin
  rw [variance, evariance_eq_lintegral_of_real, â† of_real_integral_eq_lintegral_of_real,
    ennreal.to_real_of_real];
  simp_rw [hfint, sub_zero],
  { refl },
  { exact integral_nonneg (Î» Ï‰, pow_two_nonneg _) },
  { convert hf.integrable_norm_rpow ennreal.two_ne_zero ennreal.two_ne_top,
    ext Ï‰,
    simp only [pi.sub_apply, real.norm_eq_abs, ennreal.to_real_bit0, ennreal.one_to_real,
      real.rpow_two, pow_bit0_abs] },
  { exact ae_of_all _ (Î» Ï‰, pow_two_nonneg _) }
end

lemma variance_eq {Î© : Type*} {m : measurable_space Î©} {f : Î© â†’ â„} {Î¼ : measure Î©}
  [is_finite_measure Î¼] (hf : mem_â„’p f 2 Î¼) :
  variance f Î¼ = Î¼[(f - (Î» Ï‰, Î¼[f]))^2] :=
begin
  rw [variance, evariance_eq_lintegral_of_real, â† of_real_integral_eq_lintegral_of_real,
    ennreal.to_real_of_real],
  { refl },
  { exact integral_nonneg (Î» Ï‰, pow_two_nonneg _) },
  { convert (hf.sub $ mem_â„’p_const (Î¼[f])).integrable_norm_rpow
      ennreal.two_ne_zero ennreal.two_ne_top,
    ext Ï‰,
    simp only [pi.sub_apply, real.norm_eq_abs, ennreal.to_real_bit0, ennreal.one_to_real,
      real.rpow_two, pow_bit0_abs] },
  { exact ae_of_all _ (Î» Ï‰, pow_two_nonneg _) }
end

@[simp] lemma evariance_zero {Î© : Type*} {m : measurable_space Î©} {Î¼ : measure Î©} :
  evariance 0 Î¼ = 0 :=
by simp [evariance]

lemma evariance_eq_zero_iff {Î© : Type*} {m : measurable_space Î©}
  {f : Î© â†’ â„} {Î¼ : measure Î©} (hf : ae_measurable f Î¼) :
  evariance f Î¼ = 0 â†” f =áµ[Î¼] Î» Ï‰, Î¼[f] :=
begin
  rw [evariance, lintegral_eq_zero_iff'],
  split; intro hf; filter_upwards [hf] with Ï‰ hÏ‰,
  { simp only [pi.zero_apply, pow_eq_zero_iff, nat.succ_pos', ennreal.coe_eq_zero,
      nnnorm_eq_zero, sub_eq_zero] at hÏ‰,
    exact hÏ‰ },
  { rw hÏ‰,
    simp },
  { measurability }
end

@[simp] lemma evariance_nonneg {Î© : Type*} {m : measurable_space Î©} (Î¼ : measure Î©) :
  0 â‰¤ evariance 0 Î¼ :=
zero_le _

lemma evariance_mul {Î© : Type*} {m : measurable_space Î©} (c : â„) (f : Î© â†’ â„) (Î¼ : measure Î©) :
  evariance (Î» Ï‰, c * f Ï‰) Î¼ = ennreal.of_real (c^2) * evariance f Î¼ :=
begin
  rw [evariance, evariance, â† lintegral_const_mul' _ _ ennreal.of_real_lt_top.ne],
  congr,
  ext1 Ï‰,
  rw [ennreal.of_real, â† ennreal.coe_pow, â† ennreal.coe_pow, â† ennreal.coe_mul],
  congr,
  rw [â† sq_abs, â† real.rpow_two, real.to_nnreal_rpow_of_nonneg (abs_nonneg _), nnreal.rpow_two,
    â† mul_pow, real.to_nnreal_mul_nnnorm _ (abs_nonneg _)],
  conv_rhs { rw [â† nnnorm_norm, norm_mul, norm_abs_eq_norm, â† norm_mul, nnnorm_norm, mul_sub] },
  congr,
  rw mul_comm,
  simp_rw [â† smul_eq_mul, â† integral_smul_const, smul_eq_mul, mul_comm],
end

localized "notation (name := probability_theory.variance) `eVar[` X `]` :=
  probability_theory.evariance X measure_theory.measure_space.volume" in probability_theory

variables {Î© : Type*} [measure_space Î©] [is_probability_measure (volume : measure Î©)]

theorem meas_ge_le_evariance_div_sq {X : Î© â†’ â„}
  (hX : ae_strongly_measurable X â„™) {c : â„â‰¥0} (hc : c â‰  0) :
  â„™ {Ï‰ | â†‘c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ eVar[X] / c ^ 2 :=
begin
  have A : (c : â„â‰¥0âˆ) â‰  0, { rwa [ne.def, ennreal.coe_eq_zero] },
  have B : ae_strongly_measurable (Î» (Ï‰ : Î©), ğ”¼[X]) â„™ := ae_strongly_measurable_const,
  convert meas_ge_le_mul_pow_snorm â„™ ennreal.two_ne_zero ennreal.two_ne_top (hX.sub B) A,
  { ext Ï‰,
    simp only [pi.sub_apply, ennreal.coe_le_coe, â† real.norm_eq_abs, â† coe_nnnorm,
      nnreal.coe_le_coe, ennreal.of_real_coe_nnreal] },
  { rw snorm_eq_lintegral_rpow_nnnorm ennreal.two_ne_zero ennreal.two_ne_top,
    simp only [ennreal.to_real_bit0, ennreal.one_to_real, pi.sub_apply, one_div],
    rw [div_eq_mul_inv, ennreal.inv_pow, mul_comm, ennreal.rpow_two],
    congr,
    simp_rw [â† ennreal.rpow_mul, inv_mul_cancel (two_ne_zero : (2 : â„) â‰  0), ennreal.rpow_two,
      ennreal.rpow_one, evariance] }
end


#exit
@[simp] lemma variance_zero {Î© : Type*} {m : measurable_space Î©} (Î¼ : measure Î©) :
  variance 0 Î¼ = 0 :=
by simp only [variance, evariance, pi.zero_apply, integral_const, algebra.id.smul_eq_mul, mul_zero,
  tsub_zero, zero_pow', ne.def, bit0_eq_zero, nat.one_ne_zero, not_false_iff, ennreal.of_real_zero,
  lintegral_const, zero_mul, ennreal.zero_to_real]

lemma variance_nonneg {Î© : Type*} {m : measurable_space Î©} (f : Î© â†’ â„) (Î¼ : measure Î©) :
  0 â‰¤ variance f Î¼ :=
integral_nonneg (Î» Ï‰, sq_nonneg _)

lemma variance_mul {Î© : Type*} {m : measurable_space Î©} (c : â„) (f : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (Î» Ï‰, c * f Ï‰) Î¼ = c^2 * variance f Î¼ :=
calc
variance (Î» Ï‰, c * f Ï‰) Î¼
    = âˆ« x, (c * f x - âˆ« y, c * f y âˆ‚Î¼) ^ 2 âˆ‚Î¼ : rfl
... = âˆ« x, (c * (f x - âˆ« y, f y âˆ‚Î¼)) ^ 2 âˆ‚Î¼ :
  by { congr' 1 with x, simp_rw [integral_mul_left, mul_sub] }
... = c^2 * variance f Î¼ :
  by { simp_rw [mul_pow, integral_mul_left], refl }

lemma variance_smul {Î© : Type*} {m : measurable_space Î©} (c : â„) (f : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (c â€¢ f) Î¼ = c^2 * variance f Î¼ :=
variance_mul c f Î¼

lemma variance_smul' {A : Type*} [comm_semiring A] [algebra A â„]
  {Î© : Type*} {m : measurable_space Î©} (c : A) (f : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (c â€¢ f) Î¼ = c^2 â€¢ variance f Î¼ :=
begin
  convert variance_smul (algebra_map A â„ c) f Î¼,
  { ext1 x, simp only [algebra_map_smul], },
  { simp only [algebra.smul_def, map_pow], }
end

localized "notation (name := probability_theory.variance) `Var[` X `]` :=
  probability_theory.variance X measure_theory.measure_space.volume" in probability_theory

variables {Î© : Type*} [measure_space Î©] [is_probability_measure (volume : measure Î©)]

lemma variance_def' {X : Î© â†’ â„} (hX : mem_â„’p X 2) :
  Var[X] = ğ”¼[X^2] - ğ”¼[X]^2 :=
begin
  rw [variance, sub_sq', integral_sub', integral_add'], rotate,
  { exact hX.integrable_sq },
  { convert integrable_const (ğ”¼[X] ^ 2),
    apply_instance },
  { apply hX.integrable_sq.add,
    convert integrable_const (ğ”¼[X] ^ 2),
    apply_instance },
  { exact ((hX.integrable one_le_two).const_mul 2).mul_const' _ },
  simp only [integral_mul_right, pi.pow_apply, pi.mul_apply, pi.bit0_apply, pi.one_apply,
    integral_const (integral â„™ X ^ 2), integral_mul_left (2 : â„), one_mul,
    variance, pi.pow_apply, measure_univ, ennreal.one_to_real, algebra.id.smul_eq_mul],
  ring,
end

lemma variance_le_expectation_sq {X : Î© â†’ â„} :
  Var[X] â‰¤ ğ”¼[X^2] :=
begin
  by_cases h_int : integrable X, swap,
  { simp only [variance, integral_undef h_int, pi.pow_apply, pi.sub_apply, sub_zero] },
  by_cases hX : mem_â„’p X 2,
  { rw variance_def' hX,
    simp only [sq_nonneg, sub_le_self_iff] },
  { rw [variance, integral_undef],
    { exact integral_nonneg (Î» a, sq_nonneg _) },
    { assume h,
      have A : mem_â„’p (X - Î» (Ï‰ : Î©), ğ”¼[X]) 2 â„™ := (mem_â„’p_two_iff_integrable_sq
        (h_int.ae_strongly_measurable.sub ae_strongly_measurable_const)).2 h,
      have B : mem_â„’p (Î» (Ï‰ : Î©), ğ”¼[X]) 2 â„™ := mem_â„’p_const _,
      apply hX,
      convert A.add B,
      simp } }
end

/-- *Chebyshev's inequality* : one can control the deviation probability of a real random variable
from its expectation in terms of the variance. -/
theorem meas_ge_le_variance_div_sq {X : Î© â†’ â„} (hX : mem_â„’p X 2) {c : â„} (hc : 0 < c) :
  â„™ {Ï‰ | c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ ennreal.of_real (Var[X] / c ^ 2) :=
begin
  have A : (ennreal.of_real c : â„â‰¥0âˆ) â‰  0,
    by simp only [hc, ne.def, ennreal.of_real_eq_zero, not_le],
  have B : ae_strongly_measurable (Î» (Ï‰ : Î©), ğ”¼[X]) â„™ := ae_strongly_measurable_const,
  convert meas_ge_le_mul_pow_snorm â„™ ennreal.two_ne_zero ennreal.two_ne_top
    (hX.ae_strongly_measurable.sub B) A,
  { ext Ï‰,
    set d : â„â‰¥0 := âŸ¨c, hc.leâŸ© with hd,
    have cd : c = d, by simp only [subtype.coe_mk],
    simp only [pi.sub_apply, ennreal.coe_le_coe, â† real.norm_eq_abs, â† coe_nnnorm,
      nnreal.coe_le_coe, cd, ennreal.of_real_coe_nnreal] },
  { rw (hX.sub (mem_â„’p_const _)).snorm_eq_integral_rpow_norm
      ennreal.two_ne_zero ennreal.two_ne_top,
    simp only [pi.sub_apply, ennreal.to_real_bit0, ennreal.one_to_real],
    rw ennreal.of_real_rpow_of_nonneg _ zero_le_two, rotate,
    { apply real.rpow_nonneg_of_nonneg,
      exact integral_nonneg (Î» x, real.rpow_nonneg_of_nonneg (norm_nonneg _) _) },
    rw [variance, â† real.rpow_mul, inv_mul_cancel], rotate,
    { exact two_ne_zero },
    { exact integral_nonneg (Î» x, real.rpow_nonneg_of_nonneg (norm_nonneg _) _) },
    simp only [pi.pow_apply, pi.sub_apply, real.rpow_two, real.rpow_one, real.norm_eq_abs,
      pow_bit0_abs, ennreal.of_real_inv_of_pos hc, ennreal.rpow_two],
    rw [â† ennreal.of_real_pow (inv_nonneg.2 hc.le), â† ennreal.of_real_mul (sq_nonneg _),
      div_eq_inv_mul, inv_pow] }
end

/-- The variance of the sum of two independent random variables is the sum of the variances. -/
theorem indep_fun.variance_add {X Y : Î© â†’ â„}
  (hX : mem_â„’p X 2) (hY : mem_â„’p Y 2) (h : indep_fun X Y) :
  Var[X + Y] = Var[X] + Var[Y] :=
calc
Var[X + Y] = ğ”¼[Î» a, (X a)^2 + (Y a)^2 + 2 * X a * Y a] - ğ”¼[X+Y]^2 :
  by simp [variance_def' (hX.add hY), add_sq']
... = (ğ”¼[X^2] + ğ”¼[Y^2] + 2 * ğ”¼[X * Y]) - (ğ”¼[X] + ğ”¼[Y])^2 :
begin
  simp only [pi.add_apply, pi.pow_apply, pi.mul_apply, mul_assoc],
  rw [integral_add, integral_add, integral_add, integral_mul_left],
  { exact hX.integrable one_le_two },
  { exact hY.integrable one_le_two },
  { exact hX.integrable_sq },
  { exact hY.integrable_sq },
  { exact hX.integrable_sq.add hY.integrable_sq },
  { apply integrable.const_mul,
    exact h.integrable_mul (hX.integrable one_le_two) (hY.integrable one_le_two) }
end
... = (ğ”¼[X^2] + ğ”¼[Y^2] + 2 * (ğ”¼[X] * ğ”¼[Y])) - (ğ”¼[X] + ğ”¼[Y])^2 :
begin
  congr,
  exact h.integral_mul_of_integrable
    (hX.integrable one_le_two) (hY.integrable one_le_two),
end
... = Var[X] + Var[Y] :
  by { simp only [variance_def', hX, hY, pi.pow_apply], ring }

/-- The variance of a finite sum of pairwise independent random variables is the sum of the
variances. -/
theorem indep_fun.variance_sum {Î¹ : Type*} {X : Î¹ â†’ Î© â†’ â„} {s : finset Î¹}
  (hs : âˆ€ i âˆˆ s, mem_â„’p (X i) 2) (h : set.pairwise â†‘s (Î» i j, indep_fun (X i) (X j))) :
  Var[âˆ‘ i in s, X i] = âˆ‘ i in s, Var[X i] :=
begin
  classical,
  induction s using finset.induction_on with k s ks IH,
  { simp only [finset.sum_empty, variance_zero] },
  rw [variance_def' (mem_â„’p_finset_sum' _ hs), sum_insert ks, sum_insert ks],
  simp only [add_sq'],
  calc ğ”¼[X k ^ 2 + (âˆ‘ i in s, X i) ^ 2 + 2 * X k * âˆ‘ i in s, X i] - ğ”¼[X k + âˆ‘ i in s, X i] ^ 2
  = (ğ”¼[X k ^ 2] + ğ”¼[(âˆ‘ i in s, X i) ^ 2] + ğ”¼[2 * X k * âˆ‘ i in s, X i])
    - (ğ”¼[X k] + ğ”¼[âˆ‘ i in s, X i]) ^ 2 :
  begin
    rw [integral_add', integral_add', integral_add'],
    { exact mem_â„’p.integrable one_le_two (hs _ (mem_insert_self _ _)) },
    { apply integrable_finset_sum' _ (Î» i hi, _),
      exact mem_â„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi)) },
    { exact mem_â„’p.integrable_sq (hs _ (mem_insert_self _ _)) },
    { apply mem_â„’p.integrable_sq,
      exact mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))) },
    { apply integrable.add,
      { exact mem_â„’p.integrable_sq (hs _ (mem_insert_self _ _)) },
      { apply mem_â„’p.integrable_sq,
        exact mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))) } },
    { rw mul_assoc,
      apply integrable.const_mul _ 2,
      simp only [mul_sum, sum_apply, pi.mul_apply],
      apply integrable_finset_sum _ (Î» i hi, _),
      apply indep_fun.integrable_mul _
        (mem_â„’p.integrable one_le_two (hs _ (mem_insert_self _ _)))
        (mem_â„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi))),
      apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) }
  end
  ... = Var[X k] + Var[âˆ‘ i in s, X i] +
    (ğ”¼[2 * X k * âˆ‘ i in s, X i] - 2 * ğ”¼[X k] * ğ”¼[âˆ‘ i in s, X i]) :
  begin
    rw [variance_def' (hs _ (mem_insert_self _ _)),
        variance_def' (mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))))],
    ring,
  end
  ... = Var[X k] + Var[âˆ‘ i in s, X i] :
  begin
    simp only [mul_assoc, integral_mul_left, pi.mul_apply, pi.bit0_apply, pi.one_apply, sum_apply,
      add_right_eq_self, mul_sum],
    rw integral_finset_sum s (Î» i hi, _), swap,
    { apply integrable.const_mul _ 2,
      apply indep_fun.integrable_mul _
        (mem_â„’p.integrable one_le_two (hs _ (mem_insert_self _ _)))
        (mem_â„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi))),
      apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) },
    rw [integral_finset_sum s
      (Î» i hi, (mem_â„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi)))),
      mul_sum, mul_sum, â† sum_sub_distrib],
    apply finset.sum_eq_zero (Î» i hi, _),
    rw [integral_mul_left, indep_fun.integral_mul', sub_self],
    { apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) },
    { exact mem_â„’p.ae_strongly_measurable (hs _ (mem_insert_self _ _)) },
    { exact mem_â„’p.ae_strongly_measurable (hs _ (mem_insert_of_mem hi)) }
  end
  ... = Var[X k] + âˆ‘ i in s, Var[X i] :
    by rw IH (Î» i hi, hs i (mem_insert_of_mem hi))
      (h.mono (by simp only [coe_insert, set.subset_insert]))
end

end probability_theory
