/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Kexing Ying
-/
import probability.notation
import probability.integration

/-!
# Variance of random variables

We define the variance of a real-valued random variable as `Var[X] = ğ”¼[(X - ğ”¼[X])^2]` (in the
`probability_theory` locale).

## Main definitions

* `probability_theory.evariance`: the variance of a real-valued random variable as a extended
  non-negative real.
* `probability_theory.variance`: the variance of a real-valued random variable as a real number.

## Main results

* `probability_theory.variance_le_expectation_sq`: the inequality `Var[X] â‰¤ ğ”¼[X^2]`.
* `probability_theory.meas_ge_le_variance_div_sq`: Chebyshev's inequality, i.e.,
      `â„™ {Ï‰ | c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ ennreal.of_real (Var[X] / c ^ 2)`.
* `probability_theory.meas_ge_le_evariance_div_sq`: Chebyshev's inequality formulated with
  `evariance` without requiring the random variables to be LÂ².
* `probability_theory.indep_fun.variance_add`: the variance of the sum of two independent
  random variables is the sum of the variances.
* `probability_theory.indep_fun.variance_sum`: the variance of a finite sum of pairwise
  independent random variables is the sum of the variances.
-/

open measure_theory filter finset

noncomputable theory

open_locale big_operators measure_theory probability_theory ennreal nnreal

namespace probability_theory

/-- The `â„â‰¥0âˆ`-valued variance of a real-valued random variable defined as the Lebesgue integral of
`(X - ğ”¼[X])^2`. -/
def evariance {Î© : Type*} {m : measurable_space Î©} (X : Î© â†’ â„) (Î¼ : measure Î©) : â„â‰¥0âˆ :=
âˆ«â» Ï‰, â€–X Ï‰ - Î¼[X]â€–â‚Š^2 âˆ‚Î¼

/-- The `â„`-valued variance of a real-valued random variable defined by applying `ennreal.to_real`
to `evariance`. -/
def variance {Î© : Type*} {m : measurable_space Î©} (X : Î© â†’ â„) (Î¼ : measure Î©) : â„ :=
(evariance X Î¼).to_real

variables {Î© : Type*} {m : measurable_space Î©} {X : Î© â†’ â„} {Î¼ : measure Î©}

lemma _root_.measure_theory.mem_â„’p.evariance_lt_top [is_finite_measure Î¼] (hX : mem_â„’p X 2 Î¼) :
  evariance X Î¼ < âˆ :=
begin
  have := ennreal.pow_lt_top (hX.sub $ mem_â„’p_const $ Î¼[X]).2 2,
  rw [snorm_eq_lintegral_rpow_nnnorm two_ne_zero ennreal.two_ne_top,
    â† ennreal.rpow_two] at this,
  simp only [pi.sub_apply, ennreal.to_real_bit0, ennreal.one_to_real, one_div] at this,
  rw [â† ennreal.rpow_mul, inv_mul_cancel (two_ne_zero : (2 : â„) â‰  0), ennreal.rpow_one] at this,
  simp_rw ennreal.rpow_two at this,
  exact this,
end

lemma evariance_eq_top [is_finite_measure Î¼]
  (hXm : ae_strongly_measurable X Î¼) (hX : Â¬ mem_â„’p X 2 Î¼) :
  evariance X Î¼ = âˆ :=
begin
  by_contra h,
  rw [â† ne.def, â† lt_top_iff_ne_top] at h,
  have : mem_â„’p (Î» Ï‰, X Ï‰ - Î¼[X]) 2 Î¼,
  { refine âŸ¨hXm.sub ae_strongly_measurable_const, _âŸ©,
    rw snorm_eq_lintegral_rpow_nnnorm two_ne_zero ennreal.two_ne_top,
    simp only [ennreal.to_real_bit0, ennreal.one_to_real, ennreal.rpow_two, ne.def],
    exact ennreal.rpow_lt_top_of_nonneg (by simp) h.ne },
  refine hX _,
  convert this.add (mem_â„’p_const $ Î¼[X]),
  ext Ï‰,
  rw [pi.add_apply, sub_add_cancel],
end

lemma evariance_lt_top_iff_mem_â„’p [is_finite_measure Î¼]
  (hX : ae_strongly_measurable X Î¼) :
  evariance X Î¼ < âˆ â†” mem_â„’p X 2 Î¼ :=
begin
  refine âŸ¨_, measure_theory.mem_â„’p.evariance_lt_topâŸ©,
  contrapose,
  rw [not_lt, top_le_iff],
  exact evariance_eq_top hX
end

lemma _root_.measure_theory.mem_â„’p.of_real_variance_eq [is_finite_measure Î¼]
  (hX : mem_â„’p X 2 Î¼) :
  ennreal.of_real (variance X Î¼) = evariance X Î¼ :=
by { rw [variance, ennreal.of_real_to_real], exact hX.evariance_lt_top.ne, }

include m

lemma evariance_eq_lintegral_of_real (X : Î© â†’ â„) (Î¼ : measure Î©) :
  evariance X Î¼ = âˆ«â» Ï‰, ennreal.of_real ((X Ï‰ - Î¼[X])^2) âˆ‚Î¼ :=
begin
  rw evariance,
  congr,
  ext1 Ï‰,
  rw [pow_two, â† ennreal.coe_mul, â† nnnorm_mul, â† pow_two],
  congr,
  exact (real.to_nnreal_eq_nnnorm_of_nonneg $ sq_nonneg _).symm,
end

lemma _root_.measure_theory.mem_â„’p.variance_eq_of_integral_eq_zero
  (hX : mem_â„’p X 2 Î¼) (hXint : Î¼[X] = 0) :
  variance X Î¼ = Î¼[X^2] :=
begin
  rw [variance, evariance_eq_lintegral_of_real, â† of_real_integral_eq_lintegral_of_real,
    ennreal.to_real_of_real];
  simp_rw [hXint, sub_zero],
  { refl },
  { exact integral_nonneg (Î» Ï‰, pow_two_nonneg _) },
  { convert hX.integrable_norm_rpow two_ne_zero ennreal.two_ne_top,
    ext Ï‰,
    simp only [pi.sub_apply, real.norm_eq_abs, ennreal.to_real_bit0, ennreal.one_to_real,
      real.rpow_two, pow_bit0_abs] },
  { exact ae_of_all _ (Î» Ï‰, pow_two_nonneg _) }
end

lemma _root_.measure_theory.mem_â„’p.variance_eq [is_finite_measure Î¼]
  (hX : mem_â„’p X 2 Î¼) :
  variance X Î¼ = Î¼[(X - (Î» Ï‰, Î¼[X]))^2] :=
begin
  rw [variance, evariance_eq_lintegral_of_real, â† of_real_integral_eq_lintegral_of_real,
    ennreal.to_real_of_real],
  { refl },
  { exact integral_nonneg (Î» Ï‰, pow_two_nonneg _) },
  { convert (hX.sub $ mem_â„’p_const (Î¼[X])).integrable_norm_rpow
      two_ne_zero ennreal.two_ne_top,
    ext Ï‰,
    simp only [pi.sub_apply, real.norm_eq_abs, ennreal.to_real_bit0, ennreal.one_to_real,
      real.rpow_two, pow_bit0_abs] },
  { exact ae_of_all _ (Î» Ï‰, pow_two_nonneg _) }
end

@[simp] lemma evariance_zero : evariance 0 Î¼ = 0 :=
by simp [evariance]

lemma evariance_eq_zero_iff (hX : ae_measurable X Î¼) :
  evariance X Î¼ = 0 â†” X =áµ[Î¼] Î» Ï‰, Î¼[X] :=
begin
  rw [evariance, lintegral_eq_zero_iff'],
  split; intro hX; filter_upwards [hX] with Ï‰ hÏ‰,
  { simp only [pi.zero_apply, pow_eq_zero_iff, nat.succ_pos', ennreal.coe_eq_zero,
      nnnorm_eq_zero, sub_eq_zero] at hÏ‰,
    exact hÏ‰ },
  { rw hÏ‰,
    simp },
  { measurability }
end

lemma evariance_mul (c : â„) (X : Î© â†’ â„) (Î¼ : measure Î©) :
  evariance (Î» Ï‰, c * X Ï‰) Î¼ = ennreal.of_real (c^2) * evariance X Î¼ :=
begin
  rw [evariance, evariance, â† lintegral_const_mul' _ _ ennreal.of_real_lt_top.ne],
  congr,
  ext1 Ï‰,
  rw [ennreal.of_real, â† ennreal.coe_pow, â† ennreal.coe_pow, â† ennreal.coe_mul],
  congr,
  rw [â† sq_abs, â† real.rpow_two, real.to_nnreal_rpow_of_nonneg (abs_nonneg _), nnreal.rpow_two,
    â† mul_pow, real.to_nnreal_mul_nnnorm _ (abs_nonneg _)],
  conv_rhs { rw [â† nnnorm_norm, norm_mul, norm_abs_eq_norm, â† norm_mul, nnnorm_norm, mul_sub] },
  congr,
  rw mul_comm,
  simp_rw [â† smul_eq_mul, â† integral_smul_const, smul_eq_mul, mul_comm],
end

localized "notation (name := probability_theory.evariance) `eVar[` X `]` :=
  probability_theory.evariance X measure_theory.measure_space.volume" in probability_theory

@[simp] lemma variance_zero (Î¼ : measure Î©) : variance 0 Î¼ = 0 :=
by simp only [variance, evariance_zero, ennreal.zero_to_real]

lemma variance_nonneg (X : Î© â†’ â„) (Î¼ : measure Î©) :
  0 â‰¤ variance X Î¼ :=
ennreal.to_real_nonneg

lemma variance_mul (c : â„) (X : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (Î» Ï‰, c * X Ï‰) Î¼ = c^2 * variance X Î¼ :=
begin
  rw [variance, evariance_mul, ennreal.to_real_mul, ennreal.to_real_of_real (sq_nonneg _)],
  refl,
end

lemma variance_smul (c : â„) (X : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (c â€¢ X) Î¼ = c^2 * variance X Î¼ :=
variance_mul c X Î¼

lemma variance_smul' {A : Type*} [comm_semiring A] [algebra A â„]
  (c : A) (X : Î© â†’ â„) (Î¼ : measure Î©) :
  variance (c â€¢ X) Î¼ = c^2 â€¢ variance X Î¼ :=
begin
  convert variance_smul (algebra_map A â„ c) X Î¼,
  { ext1 x, simp only [algebra_map_smul], },
  { simp only [algebra.smul_def, map_pow], }
end

localized "notation (name := probability_theory.variance) `Var[` X `]` :=
  probability_theory.variance X measure_theory.measure_space.volume" in probability_theory

omit m

variables [measure_space Î©]

lemma variance_def' [is_probability_measure (â„™ : measure Î©)]
  {X : Î© â†’ â„} (hX : mem_â„’p X 2) :
  Var[X] = ğ”¼[X^2] - ğ”¼[X]^2 :=
begin
  rw [hX.variance_eq, sub_sq', integral_sub', integral_add'], rotate,
  { exact hX.integrable_sq },
  { convert integrable_const (ğ”¼[X] ^ 2),
    apply_instance },
  { apply hX.integrable_sq.add,
    convert integrable_const (ğ”¼[X] ^ 2),
    apply_instance },
  { exact ((hX.integrable one_le_two).const_mul 2).mul_const' _ },
  simp only [integral_mul_right, pi.pow_apply, pi.mul_apply, pi.bit0_apply, pi.one_apply,
    integral_const (integral â„™ X ^ 2), integral_mul_left (2 : â„), one_mul,
    variance, pi.pow_apply, measure_univ, ennreal.one_to_real, algebra.id.smul_eq_mul],
  ring,
end

lemma variance_le_expectation_sq [is_probability_measure (â„™ : measure Î©)]
  {X : Î© â†’ â„} (hm : ae_strongly_measurable X â„™) :
  Var[X] â‰¤ ğ”¼[X^2] :=
begin
  by_cases hX : mem_â„’p X 2,
  { rw variance_def' hX,
    simp only [sq_nonneg, sub_le_self_iff] },
  rw [variance, evariance_eq_lintegral_of_real, â† integral_eq_lintegral_of_nonneg_ae],
  by_cases hint : integrable X, swap,
  { simp only [integral_undef hint, pi.pow_apply, pi.sub_apply, sub_zero] },
  { rw integral_undef,
    { exact integral_nonneg (Î» a, sq_nonneg _) },
    { intro h,
      have A : mem_â„’p (X - Î» (Ï‰ : Î©), ğ”¼[X]) 2 â„™ := (mem_â„’p_two_iff_integrable_sq
        (hint.ae_strongly_measurable.sub ae_strongly_measurable_const)).2 h,
      have B : mem_â„’p (Î» (Ï‰ : Î©), ğ”¼[X]) 2 â„™ := mem_â„’p_const _,
      apply hX,
      convert A.add B,
      simp } },
  { exact ae_of_all _ (Î» x, sq_nonneg _) },
  { exact (ae_measurable.pow_const (hm.ae_measurable.sub_const _) _).ae_strongly_measurable },
end

lemma evariance_def' [is_probability_measure (â„™ : measure Î©)]
  {X : Î© â†’ â„} (hX : ae_strongly_measurable X â„™) :
  eVar[X] = (âˆ«â» Ï‰, â€–X Ï‰â€–â‚Š^2) - ennreal.of_real (ğ”¼[X]^2) :=
begin
  by_cases hâ„’ : mem_â„’p X 2,
  { rw [â† hâ„’.of_real_variance_eq, variance_def' hâ„’, ennreal.of_real_sub _ (sq_nonneg _)],
    congr,
    simp_rw â† ennreal.coe_pow,
    rw lintegral_coe_eq_integral,
    { congr' 2 with Ï‰,
      simp only [pi.pow_apply, nnreal.coe_pow, coe_nnnorm, real.norm_eq_abs, pow_bit0_abs] },
    { exact hâ„’.abs.integrable_sq } },
  { symmetry,
    rw [evariance_eq_top hX hâ„’, ennreal.sub_eq_top_iff],
    refine âŸ¨_, ennreal.of_real_ne_topâŸ©,
    rw [mem_â„’p, not_and] at hâ„’,
    specialize hâ„’ hX,
    simp only [snorm_eq_lintegral_rpow_nnnorm two_ne_zero ennreal.two_ne_top, not_lt,
      top_le_iff, ennreal.to_real_bit0, ennreal.one_to_real, ennreal.rpow_two, one_div,
      ennreal.rpow_eq_top_iff, inv_lt_zero, inv_pos, zero_lt_bit0, zero_lt_one, and_true,
      or_iff_not_imp_left, not_and_distrib] at hâ„’,
    exact hâ„’ (Î» _, zero_le_two) }
end

/-- *Chebyshev's inequality* for `â„â‰¥0âˆ`-valued variance. -/
theorem meas_ge_le_evariance_div_sq {X : Î© â†’ â„}
  (hX : ae_strongly_measurable X â„™) {c : â„â‰¥0} (hc : c â‰  0) :
  â„™ {Ï‰ | â†‘c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ eVar[X] / c ^ 2 :=
begin
  have A : (c : â„â‰¥0âˆ) â‰  0, { rwa [ne.def, ennreal.coe_eq_zero] },
  have B : ae_strongly_measurable (Î» (Ï‰ : Î©), ğ”¼[X]) â„™ := ae_strongly_measurable_const,
  convert meas_ge_le_mul_pow_snorm â„™ two_ne_zero ennreal.two_ne_top (hX.sub B) A,
  { ext Ï‰,
    simp only [pi.sub_apply, ennreal.coe_le_coe, â† real.norm_eq_abs, â† coe_nnnorm,
      nnreal.coe_le_coe, ennreal.of_real_coe_nnreal] },
  { rw snorm_eq_lintegral_rpow_nnnorm two_ne_zero ennreal.two_ne_top,
    simp only [ennreal.to_real_bit0, ennreal.one_to_real, pi.sub_apply, one_div],
    rw [div_eq_mul_inv, ennreal.inv_pow, mul_comm, ennreal.rpow_two],
    congr,
    simp_rw [â† ennreal.rpow_mul, inv_mul_cancel (two_ne_zero : (2 : â„) â‰  0), ennreal.rpow_two,
      ennreal.rpow_one, evariance] }
end

/-- *Chebyshev's inequality* : one can control the deviation probability of a real random variable
from its expectation in terms of the variance. -/
theorem meas_ge_le_variance_div_sq [is_finite_measure (â„™ : measure Î©)]
  {X : Î© â†’ â„} (hX : mem_â„’p X 2) {c : â„} (hc : 0 < c) :
  â„™ {Ï‰ | c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ ennreal.of_real (Var[X] / c ^ 2) :=
begin
  rw [ennreal.of_real_div_of_pos (sq_pos_of_ne_zero _ hc.ne.symm), hX.of_real_variance_eq],
  convert @meas_ge_le_evariance_div_sq _ _ _ hX.1 (c.to_nnreal) (by simp [hc]),
  { simp only [real.coe_to_nnreal', max_le_iff, abs_nonneg, and_true] },
  { rw ennreal.of_real_pow hc.le,
    refl }
end

/-- The variance of the sum of two independent random variables is the sum of the variances. -/
theorem indep_fun.variance_add [is_probability_measure (â„™ : measure Î©)]
  {X Y : Î© â†’ â„} (hX : mem_â„’p X 2) (hY : mem_â„’p Y 2) (h : indep_fun X Y) :
  Var[X + Y] = Var[X] + Var[Y] :=
calc
Var[X + Y] = ğ”¼[Î» a, (X a)^2 + (Y a)^2 + 2 * X a * Y a] - ğ”¼[X+Y]^2 :
  by simp [variance_def' (hX.add hY), add_sq']
... = (ğ”¼[X^2] + ğ”¼[Y^2] + 2 * ğ”¼[X * Y]) - (ğ”¼[X] + ğ”¼[Y])^2 :
begin
  simp only [pi.add_apply, pi.pow_apply, pi.mul_apply, mul_assoc],
  rw [integral_add, integral_add, integral_add, integral_mul_left],
  { exact hX.integrable one_le_two },
  { exact hY.integrable one_le_two },
  { exact hX.integrable_sq },
  { exact hY.integrable_sq },
  { exact hX.integrable_sq.add hY.integrable_sq },
  { apply integrable.const_mul,
    exact h.integrable_mul (hX.integrable one_le_two) (hY.integrable one_le_two) }
end
... = (ğ”¼[X^2] + ğ”¼[Y^2] + 2 * (ğ”¼[X] * ğ”¼[Y])) - (ğ”¼[X] + ğ”¼[Y])^2 :
begin
  congr,
  exact h.integral_mul_of_integrable
    (hX.integrable one_le_two) (hY.integrable one_le_two),
end
... = Var[X] + Var[Y] :
  by { simp only [variance_def', hX, hY, pi.pow_apply], ring }

/-- The variance of a finite sum of pairwise independent random variables is the sum of the
variances. -/
theorem indep_fun.variance_sum [is_probability_measure (â„™ : measure Î©)]
  {Î¹ : Type*} {X : Î¹ â†’ Î© â†’ â„} {s : finset Î¹}
  (hs : âˆ€ i âˆˆ s, mem_â„’p (X i) 2) (h : set.pairwise â†‘s (Î» i j, indep_fun (X i) (X j))) :
  Var[âˆ‘ i in s, X i] = âˆ‘ i in s, Var[X i] :=
begin
  classical,
  induction s using finset.induction_on with k s ks IH,
  { simp only [finset.sum_empty, variance_zero] },
  rw [variance_def' (mem_â„’p_finset_sum' _ hs), sum_insert ks, sum_insert ks],
  simp only [add_sq'],
  calc ğ”¼[X k ^ 2 + (âˆ‘ i in s, X i) ^ 2 + 2 * X k * âˆ‘ i in s, X i] - ğ”¼[X k + âˆ‘ i in s, X i] ^ 2
  = (ğ”¼[X k ^ 2] + ğ”¼[(âˆ‘ i in s, X i) ^ 2] + ğ”¼[2 * X k * âˆ‘ i in s, X i])
    - (ğ”¼[X k] + ğ”¼[âˆ‘ i in s, X i]) ^ 2 :
  begin
    rw [integral_add', integral_add', integral_add'],
    { exact mem_â„’p.integrable one_le_two (hs _ (mem_insert_self _ _)) },
    { apply integrable_finset_sum' _ (Î» i hi, _),
      exact mem_â„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi)) },
    { exact mem_â„’p.integrable_sq (hs _ (mem_insert_self _ _)) },
    { apply mem_â„’p.integrable_sq,
      exact mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))) },
    { apply integrable.add,
      { exact mem_â„’p.integrable_sq (hs _ (mem_insert_self _ _)) },
      { apply mem_â„’p.integrable_sq,
        exact mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))) } },
    { rw mul_assoc,
      apply integrable.const_mul _ (2:â„),
      simp only [mul_sum, sum_apply, pi.mul_apply],
      apply integrable_finset_sum _ (Î» i hi, _),
      apply indep_fun.integrable_mul _
        (mem_â„’p.integrable one_le_two (hs _ (mem_insert_self _ _)))
        (mem_â„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi))),
      apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) }
  end
  ... = Var[X k] + Var[âˆ‘ i in s, X i] +
    (ğ”¼[2 * X k * âˆ‘ i in s, X i] - 2 * ğ”¼[X k] * ğ”¼[âˆ‘ i in s, X i]) :
  begin
    rw [variance_def' (hs _ (mem_insert_self _ _)),
        variance_def' (mem_â„’p_finset_sum' _ (Î» i hi, (hs _ (mem_insert_of_mem hi))))],
    ring,
  end
  ... = Var[X k] + Var[âˆ‘ i in s, X i] :
  begin
    simp only [mul_assoc, integral_mul_left, pi.mul_apply, pi.bit0_apply, pi.one_apply, sum_apply,
      add_right_eq_self, mul_sum],
    rw integral_finset_sum s (Î» i hi, _), swap,
    { apply integrable.const_mul _ (2:â„),
      apply indep_fun.integrable_mul _
        (mem_â„’p.integrable one_le_two (hs _ (mem_insert_self _ _)))
        (mem_â„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi))),
      apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) },
    rw [integral_finset_sum s
      (Î» i hi, (mem_â„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi)))),
      mul_sum, mul_sum, â† sum_sub_distrib],
    apply finset.sum_eq_zero (Î» i hi, _),
    rw [integral_mul_left, indep_fun.integral_mul', sub_self],
    { apply h (mem_insert_self _ _) (mem_insert_of_mem hi),
      exact (Î» hki, ks (hki.symm â–¸ hi)) },
    { exact mem_â„’p.ae_strongly_measurable (hs _ (mem_insert_self _ _)) },
    { exact mem_â„’p.ae_strongly_measurable (hs _ (mem_insert_of_mem hi)) }
  end
  ... = Var[X k] + âˆ‘ i in s, Var[X i] :
    by rw IH (Î» i hi, hs i (mem_insert_of_mem hi))
      (h.mono (by simp only [coe_insert, set.subset_insert]))
end

end probability_theory
