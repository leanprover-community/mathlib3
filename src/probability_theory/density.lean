/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import measure_theory.decomposition.radon_nikodym
import measure_theory.measure.lebesgue

/-!
# Probability density function

This file defines the probability density function of a random variable. In particular,
a measurable function `f` is said to the probability density function of a random variable `X`
if for all measurable sets `S`, `‚Ñô(X ‚àà S) = ‚à´ x in S, f x dx`. Probability density functions
are one way of describing the distribution of a random variable, and are useful for calculating
probabilities and finding moments (although the latter is better achieved with moment generating
functions).

This file also defines the continuous uniform distribution and proves some properties about
random variables with this distribution.

## Main definitions

* `measure_theory.measure.has_pdf` : A random variable `X : Œ± ‚Üí E` is said to `has_pdf` with
  respect to the measure `‚Ñô` on `Œ±` and `Œº` on `E` if there exists a measurable function `f`
  such that the push-forward measure of `‚Ñô` along `X` equals `Œº.with_density f`.
* `measure_theory.measure.pdf` : If `X` is a random variable that `has_pdf X ‚Ñô Œº`, then `pdf X`
  is the measurable function `f` such that the push-forward measure of `‚Ñô` along `X` equals
  `Œº.with_density f`.
* `measure_theory.measure.pdf.uniform` : A random variable `X` is said to follow the uniform
  distribution if it has a constant probability density function with a compact, non-null support.

## Main results

* `measure_theory.measure.pdf.integral_mul_eq_integral'` : Law of the unconscious statistician,
  i.e. if a random variable `X : Œ± ‚Üí E` has pdf `f`, then `ùîº(g(X)) = ‚à´ x, g x * f x dx` for
  all measurable `g : E ‚Üí ‚Ñù`.
* `measure_theory.measure.pdf.integral_mul_eq_integral` : A real-valued random variable `X` with
  pdf `f` has expectation `‚à´ x, x * f x dx`.
* `measure_theory.measure.pdf.uniform.integral_eq` : If `X` follows the uniform distribution with
  its pdf having support `s`, then `X` has expectation `(Œª s)‚Åª¬π * ‚à´ x in s, x dx` where `Œª`
  is the Lebesgue measure.

## TODOs

Ultimately, we would also like to define characteristic functions to describe distributions as
it exists for all random variables. However, to define this, we will need Fourier transforms
which we currently do not have.
-/

noncomputable theory
open_locale classical measure_theory nnreal ennreal

namespace measure_theory

open topological_space

variables {Œ± E : Type*} [normed_group E] [measurable_space E] [second_countable_topology E]
  [normed_space ‚Ñù E] [complete_space E] [borel_space E]

namespace measure

/-- A random variable `X : Œ± ‚Üí E` is said to `has_pdf` with respect to the measure `‚Ñô` on `Œ±` and
`Œº` on `E` if there exists a measurable function `f` such that the push-forward measure of `‚Ñô`
along `X` equals `Œº.with_density f`. -/
class has_pdf {m : measurable_space Œ±} (X : Œ± ‚Üí E)
  (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) : Prop :=
(pdf' : ‚àÉ (f : E ‚Üí ‚Ñù‚â•0‚àû), measurable f ‚àß map X ‚Ñô = Œº.with_density f)

/-- If `X` is a random variable that `has_pdf X ‚Ñô Œº`, then `pdf X` is the measurable function `f`
such that the push-forward measure of `‚Ñô` along `X` equals `Œº.with_density f`. -/
def pdf {m : measurable_space Œ±} (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) :=
if hX : has_pdf X ‚Ñô Œº then classical.some hX.pdf' else 0

@[measurability]
lemma measurable_pdf {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) :
  measurable (pdf X ‚Ñô Œº) :=
begin
  by_cases hX : has_pdf X ‚Ñô Œº,
  { rw [pdf, dif_pos hX],
    exact (classical.some_spec hX.pdf').1 },
  { rw [pdf, dif_neg hX],
    exact measurable_zero }
end

lemma pdf_spec {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : has_pdf X ‚Ñô Œº] :
  measure.map X ‚Ñô = Œº.with_density (pdf X ‚Ñô Œº) :=
begin
  rw [pdf, dif_pos hX],
  exact (classical.some_spec hX.pdf').2
end

lemma pdf_spec' {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : has_pdf X ‚Ñô Œº]
  {s : set E} (hs : measurable_set s) :
  measure.map X ‚Ñô s = ‚à´‚Åª x in s, pdf X ‚Ñô Œº x ‚àÇŒº :=
by rw [‚Üê with_density_apply _ hs, pdf_spec X ‚Ñô Œº]

namespace pdf

variables {m : measurable_space Œ±} {‚Ñô : measure Œ±} {Œº : measure E}

section

lemma lintegral_eq_measure_univ {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº] (hX : measurable X) :
  ‚à´‚Åª x, pdf X ‚Ñô Œº x ‚àÇŒº = ‚Ñô set.univ :=
begin
  rw [‚Üê set_lintegral_univ, ‚Üê pdf_spec' X ‚Ñô Œº measurable_set.univ,
      measure.map_apply hX measurable_set.univ, set.preimage_univ],
end

lemma ae_lt_top {m : measurable_space Œ±} (‚Ñô : measure Œ±) [is_finite_measure ‚Ñô] {Œº : measure E}
  {X : Œ± ‚Üí E} (hX : measurable X) : ‚àÄ·µê x ‚àÇŒº, pdf X ‚Ñô Œº x < ‚àû :=
begin
  by_cases hpdf : has_pdf X ‚Ñô Œº,
  { refine ae_lt_top (measurable_pdf X ‚Ñô Œº) _,
    rw lintegral_eq_measure_univ hX,
    { exact (measure_lt_top _ _).ne },
    { exact hpdf } },
  { rw [pdf, dif_neg hpdf],
    exact filter.eventually_of_forall (Œª x, with_top.zero_lt_top) }
end

lemma of_real_to_real_ae_eq [is_finite_measure ‚Ñô] {X : Œ± ‚Üí E} (hX : measurable X) :
  (Œª x, ennreal.of_real (pdf X ‚Ñô Œº x).to_real) =·µê[Œº] pdf X ‚Ñô Œº :=
begin
  by_cases hpdf : has_pdf X ‚Ñô Œº,
  { haveI := hpdf,
    change Œº {x : E | ennreal.of_real (pdf X ‚Ñô Œº x).to_real ‚â† pdf X ‚Ñô Œº x} = 0,
    have : ‚àÄ x, ennreal.of_real (pdf X ‚Ñô Œº x).to_real ‚â† pdf X ‚Ñô Œº x ‚Üî
      pdf X ‚Ñô Œº x = ‚àû,
    { intro x,
      split; intro h,
      { by_contra htop,
        rw [‚Üê ne.def, ‚Üê lt_top_iff_ne_top] at htop,
        exact h (ennreal.of_real_to_real htop.ne) },
      { rw h, simp } },
    { simp_rw this,
      suffices hne : ‚àÄ·µê x ‚àÇŒº, pdf X ‚Ñô Œº x ‚â† ‚àû,
      { simp_rw [ae_iff, not_not] at hne, exact hne },
      convert ae_lt_top ‚Ñô hX,
      simp_rw [lt_top_iff_ne_top] } },
  { convert ae_eq_refl _,
    ext1 x,
    rw [pdf, dif_neg hpdf, pi.zero_apply, ennreal.zero_to_real, ennreal.of_real_zero] }
end

/-- **The Law of the Unconscious Statistician**: Given a random variable `X` and a measurable
function `f`, `f ‚àò X` is a random variable with expectation `‚à´ x, f x * pdf X ‚àÇŒº`
where `Œº` is a measure on the codomain of `X`. -/
lemma integral_mul_eq_integral' [is_finite_measure ‚Ñô] {X : Œ± ‚Üí E} [has_pdf X ‚Ñô Œº]
  (hX : measurable X) (f : E ‚Üí ‚Ñù) (hf : measurable f)
  (hpdf : integrable (Œª x, f x * (pdf X ‚Ñô Œº x).to_real) Œº) :
  ‚à´ x, f x * (pdf X ‚Ñô Œº x).to_real ‚àÇŒº = ‚à´ x, f (X x) ‚àÇ‚Ñô :=
begin
  rw [‚Üê integral_map hX hf.ae_measurable, pdf_spec X ‚Ñô Œº,
      integral_eq_lintegral_pos_part_sub_lintegral_neg_part,
      integral_eq_lintegral_pos_part_sub_lintegral_neg_part,
      lintegral_with_density_eq_lintegral_mul, lintegral_with_density_eq_lintegral_mul],
  { congr' 2,
    { have : ‚àÄ x, ennreal.of_real (f x * (pdf X ‚Ñô Œº x).to_real) =
        ennreal.of_real (pdf X ‚Ñô Œº x).to_real * ennreal.of_real (f x),
      { intro x,
        rw [mul_comm, ennreal.of_real_mul ennreal.to_real_nonneg] },
      simp_rw [this],
      exact lintegral_congr_ae (filter.eventually_eq.mul
        (of_real_to_real_ae_eq hX) (ae_eq_refl _)) },
    { have : ‚àÄ x, ennreal.of_real (- (f x * (pdf X ‚Ñô Œº x).to_real)) =
        ennreal.of_real (pdf X ‚Ñô Œº x).to_real * ennreal.of_real (-f x),
      { intro x,
        rw [neg_mul_eq_neg_mul, mul_comm, ennreal.of_real_mul ennreal.to_real_nonneg] },
      simp_rw [this],
      exact lintegral_congr_ae (filter.eventually_eq.mul
        (of_real_to_real_ae_eq hX) (ae_eq_refl _)) } },
  { exact measurable_pdf X ‚Ñô Œº },
  { exact (measurable.neg hf).ennreal_of_real },
  { exact measurable_pdf X ‚Ñô Œº},
  { exact measurable.ennreal_of_real hf },
  { refine ‚ü®hf.ae_measurable, _‚ü©,
    rw [has_finite_integral, lintegral_with_density_eq_lintegral_mul _
          (measurable_pdf _ _ _) hf.nnnorm.coe_nnreal_ennreal],
    have : (Œª x, (pdf X ‚Ñô Œº * Œª x, ‚Üë‚à•f x‚à•‚Çä) x) =·µê[Œº] (Œª x, ‚à•f x * (pdf X ‚Ñô Œº x).to_real‚à•‚Çä),
    { simp_rw [‚Üê smul_eq_mul, nnnorm_smul, ennreal.coe_mul],
      rw [smul_eq_mul, mul_comm],
      refine filter.eventually_eq.mul (ae_eq_refl _)
        (ae_eq_trans (of_real_to_real_ae_eq hX).symm _),
      convert ae_eq_refl _,
      ext1 x,
      exact real.ennnorm_eq_of_real ennreal.to_real_nonneg },
    rw lintegral_congr_ae this,
    exact hpdf.2 },
  { assumption },
end

/-- A random variable that `has_pdf` is quasi-measure preserving. -/
lemma to_quasi_measure_preserving (X : Œ± ‚Üí E) (hX : measurable X) [has_pdf X ‚Ñô Œº] :
  quasi_measure_preserving X ‚Ñô Œº :=
{ measurable := hX,
  absolutely_continuous :=
  begin
    rw pdf_spec X ‚Ñô Œº,
    exact with_density_absolutely_continuous _ _,
    all_goals { apply_instance }
  end }

lemma map_absolutely_continuous {X : Œ± ‚Üí E} (hX : measurable X) [has_pdf X ‚Ñô Œº] :
  map X ‚Ñô ‚â™ Œº :=
(to_quasi_measure_preserving X hX).absolutely_continuous

end

section real

variables [is_finite_measure ‚Ñô] {X : Œ± ‚Üí ‚Ñù} (hX : measurable X)

include hX

/-- A real-valued random variable `X` `has_pdf X ‚Ñô Œª` (where `Œª` is the Lebesgue measure) if and
only if the push-forward measure of `‚Ñô` along `X` is absolutely continuous with respect to `Œª`. -/
lemma has_pdf_iff : has_pdf X ‚Ñô ‚Üî map X ‚Ñô ‚â™ volume :=
begin
  split,
  { intro hX',
    exactI map_absolutely_continuous hX },
  { intro h,
    refine ‚ü®‚ü®(measure.map X ‚Ñô).radon_nikodym_deriv volume, measurable_radon_nikodym_deriv _ _, _‚ü©‚ü©,
    haveI := ‚Ñô.is_finite_measure_map hX,
    rwa with_density_radon_nikodym_deriv_eq }
end

/-- A real-valued random variable that `has_pdf` transformed under a `quasi_measure_preserving`
map also `has_pdf`. -/
lemma quasi_measure_preserving_has_pdf [has_pdf X ‚Ñô]
  (g : ‚Ñù ‚Üí ‚Ñù) (hg : quasi_measure_preserving g) :
  has_pdf (g ‚àò X) ‚Ñô :=
begin
  rw [has_pdf_iff (hg.measurable.comp hX), ‚Üê map_map hg.measurable hX, pdf_spec X ‚Ñô],
  refine absolutely_continuous.mk (Œª s hsm hs, _),
  rw [map_apply hg.measurable hsm, with_density_apply _ (hg.measurable hsm)],
  have := hg.absolutely_continuous hs,
  rw map_apply hg.measurable hsm at this,
  exact set_lintegral_measure_zero _ _ this,
  apply_instance
end

/-- If `X` is a real-valued random variable that has pdf `f`, then the expectation of `X` equals
`‚à´ x, x * f x ‚àÇŒª` where `Œª` is the Lebesgue measure. -/
lemma integral_mul_eq_integral [has_pdf X ‚Ñô]
  (hpdf : integrable (Œª x, x * (pdf X ‚Ñô volume x).to_real) volume) :
  ‚à´ x, x * (pdf X ‚Ñô volume x).to_real ‚àÇ(volume) = ‚à´ x, X x ‚àÇ‚Ñô :=
integral_mul_eq_integral' hX _ measurable_id hpdf

lemma has_finite_integral_mul {f : ‚Ñù ‚Üí ‚Ñù} {g : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû}
  (hg : pdf X ‚Ñô =·µê[volume] g) (hgi : ‚à´‚Åª x, ‚à•f x‚à•‚Çä * g x ‚àÇ(volume) < ‚àû) :
  has_finite_integral (Œª x, f x * (pdf X ‚Ñô volume x).to_real) volume :=
begin
  rw [has_finite_integral],
  have : (Œª x, ‚Üë‚à•f x‚à•‚Çä * g x) =·µê[volume] (Œª x, ‚à•f x * (pdf X ‚Ñô volume x).to_real‚à•‚Çä),
  { refine ae_eq_trans (filter.eventually_eq.mul (ae_eq_refl (Œª x, ‚à•f x‚à•‚Çä))
      (ae_eq_trans hg.symm (of_real_to_real_ae_eq hX).symm)) _,
    simp_rw [‚Üê smul_eq_mul, nnnorm_smul, ennreal.coe_mul, smul_eq_mul],
    refine filter.eventually_eq.mul (ae_eq_refl _) _,
    convert ae_eq_refl _,
    ext1 x,
    exact real.ennnorm_eq_of_real ennreal.to_real_nonneg },
  rwa ‚Üê lintegral_congr_ae this,
end

end real

section

/-! **Uniform Distribution** -/

/-- A random variable `X` has uniform distribution if it has a probability density function `f`
with compact support `s` such that `f = (Œº s)‚Åª¬π 1‚Çõ` a.e. where `1‚Çõ` is the indicator function
for `s`.

We require compact support since otherwise the expectation might not be finite. -/
class uniform {m : measurable_space Œ±} (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac)
  extends has_pdf X ‚Ñô Œº :=
(support' : set E) (compact_support' : is_compact support')
(support_not_null' : 0 < Œº support')
(uniform' : pdf X ‚Ñô Œº =·µê[Œº] support'.indicator ((Œº support')‚Åª¬π ‚Ä¢ 1))

namespace uniform

/-- The support of the probability density function of a random variable of uniform distribution. -/
def support {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : uniform X ‚Ñô Œº] : set E :=
hX.support'

lemma is_compact_support {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : uniform X ‚Ñô Œº] :
  is_compact (support X ‚Ñô Œº) :=
hX.compact_support'

@[measurability]
lemma measurable_set_support {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : uniform X ‚Ñô Œº] :
  measurable_set (support X ‚Ñô Œº) :=
(is_compact_support X ‚Ñô Œº).is_closed.measurable_set

lemma support_not_null {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : uniform X ‚Ñô Œº] :
  0 < Œº (support X ‚Ñô Œº) :=
hX.support_not_null'

lemma pdf_ae_eq {m : measurable_space Œ±} (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac)
  [hX : uniform X ‚Ñô Œº] :
  pdf X ‚Ñô Œº =·µê[Œº] (support X ‚Ñô Œº).indicator ((Œº (support X ‚Ñô Œº))‚Åª¬π ‚Ä¢ 1) :=
hX.uniform'

lemma pdf_to_real_ae_eq {m : measurable_space Œ±}
  (X : Œ± ‚Üí E) (‚Ñô : measure Œ±) (Œº : measure E . volume_tac) [hX : uniform X ‚Ñô Œº] :
  (Œª x, (pdf X ‚Ñô Œº x).to_real) =·µê[Œº]
  (Œª x, ((support X ‚Ñô Œº).indicator ((Œº (support X ‚Ñô Œº))‚Åª¬π ‚Ä¢ (1 : E ‚Üí ‚Ñù‚â•0‚àû)) x).to_real) :=
filter.eventually_eq.fun_comp (pdf_ae_eq X ‚Ñô Œº) ennreal.to_real

variables [is_finite_measure ‚Ñô] {X : Œ± ‚Üí ‚Ñù} [uniform X ‚Ñô volume]

lemma mul_pdf_integrable (hX : measurable X) :
  integrable (Œª x : ‚Ñù, x * (pdf X ‚Ñô volume x).to_real) volume :=
begin
  by_cases hsupp : volume (support X ‚Ñô) = ‚àû,
  { have : pdf X ‚Ñô =·µê[volume] 0,
    { refine ae_eq_trans (pdf_ae_eq X ‚Ñô) _,
      simp [hsupp] },
    refine integrable.congr (integrable_zero _ _ _) _,
    rw [(by simp : (Œª x, 0 : ‚Ñù ‚Üí ‚Ñù) = (Œª x, x * (0 : ‚Ñù‚â•0‚àû).to_real))],
    refine filter.eventually_eq.mul (ae_eq_refl _)
      (filter.eventually_eq.fun_comp this.symm ennreal.to_real) },
  refine ‚ü®ae_measurable_id'.mul (measurable_pdf X ‚Ñô).ae_measurable.ennreal_to_real, _‚ü©,
  refine has_finite_integral_mul hX (pdf_ae_eq X ‚Ñô) _,
  set ind := (volume (support X ‚Ñô))‚Åª¬π ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû) with hind,
  have : ‚àÄ x, ‚Üë‚à•x‚à•‚Çä * (support X ‚Ñô).indicator ind x =
    (support X ‚Ñô).indicator (Œª x, ‚à•x‚à•‚Çä * ind x) x :=
      Œª x, ((support X ‚Ñô).indicator_mul_right (Œª x, ‚Üë‚à•x‚à•‚Çä) ind).symm,
  simp only [this, lintegral_indicator _ (measurable_set_support X ‚Ñô), hind, mul_one,
             algebra.id.smul_eq_mul, pi.one_apply, pi.smul_apply],
  rw lintegral_mul_const _ measurable_nnnorm.coe_nnreal_ennreal,
  { exact ennreal.mul_lt_top (set_lintegral_lt_top_of_is_compact
      (lt_top_iff_ne_top.2 hsupp) (is_compact_support X ‚Ñô) continuous_nnnorm).ne
      (ennreal.inv_lt_top.2 (support_not_null X ‚Ñô)).ne },
  { apply_instance }
end

/-- A real uniform random variable `X` with support `s` has expectation
`(Œª s)‚Åª¬π * ‚à´ x in s, x ‚àÇŒª` where `Œª` is the Lebesgue measure. -/
lemma integral_eq (hX : measurable X) :
  ‚à´ x, X x ‚àÇ‚Ñô =
  (volume (support X ‚Ñô))‚Åª¬π.to_real * ‚à´ x in support X ‚Ñô, x ‚àÇ(volume) :=
begin
  rw ‚Üê integral_mul_eq_integral hX (mul_pdf_integrable hX),
  all_goals { try { apply_instance } },
  rw integral_congr_ae (filter.eventually_eq.mul (ae_eq_refl _) (pdf_to_real_ae_eq X ‚Ñô)),
  have : ‚àÄ x, x * ((support X ‚Ñô).indicator
      ((volume (support X ‚Ñô))‚Åª¬π ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû)) x).to_real =
    x * ((support X ‚Ñô).indicator
      ((volume (support X ‚Ñô))‚Åª¬π.to_real ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù)) x),
  { refine Œª x, congr_arg ((*) x) _,
    by_cases hx : x ‚àà support X ‚Ñô,
    { simp [set.indicator_of_mem hx] },
    { simp [set.indicator_of_not_mem hx] }},
  simp_rw [this, ‚Üê (support X ‚Ñô).indicator_mul_right (Œª x, x),
           integral_indicator (measurable_set_support X ‚Ñô)],
  change ‚à´ x in support X ‚Ñô, x * ((volume (support X ‚Ñô))‚Åª¬π.to_real ‚Ä¢ 1) ‚àÇ(volume) = _,
  rw [integral_mul_right, mul_comm, algebra.id.smul_eq_mul, mul_one],
end .

end uniform

end

end pdf

end measure

end measure_theory
