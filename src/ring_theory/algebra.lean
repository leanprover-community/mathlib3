/-
Copyright (c) 2018 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau, Yury Kudryashov
-/
import data.matrix.basic
import linear_algebra.tensor_product
import ring_theory.subsemiring

/-!
# Algebra over Commutative Semiring (under category)

In this file we define algebra over commutative (semi)rings, algebra homomorphisms `alg_hom`,
algebra equivalences `alg_equiv`, and `subalgebra`s. We also define usual operations on `alg_hom`s
(`id`, `comp`) and subalgebras (`map`, `comap`).

If `S` is an `R`-algebra and `A` is an `S`-algebra then `algebra.comap.algebra R S A` can be used
to provide `A` with a structure of an `R`-algebra. Other than that, `algebra.comap` is now
deprecated and replcaed with `is_algebra_tower`.

## Notations

* `A ‚Üí‚Çê[R] B` : `R`-algebra homomorphism from `A` to `B`.
* `A ‚âÉ‚Çê[R] B` : `R`-algebra equivalence from `A` to `B`.
-/
noncomputable theory

universes u v w u‚ÇÅ v‚ÇÅ

open_locale tensor_product big_operators

section prio
-- We set this priority to 0 later in this file
set_option default_priority 200 -- see Note [default priority]
/-- The category of R-algebras where R is a commutative
ring is the under category R ‚Üì CRing. In the categorical
setting we have a forgetful functor R-Alg ‚•§ R-Mod.
However here it extends module in order to preserve
definitional equality in certain cases. -/
@[nolint has_inhabited_instance]
class algebra (R : Type u) (A : Type v) [comm_semiring R] [semiring A]
  extends has_scalar R A, R ‚Üí+* A :=
(commutes' : ‚àÄ r x, to_fun r * x = x * to_fun r)
(smul_def' : ‚àÄ r x, r ‚Ä¢ x = to_fun r * x)
end prio

/-- Embedding `R ‚Üí+* A` given by `algebra` structure. -/
def algebra_map (R : Type u) (A : Type v) [comm_semiring R] [semiring A] [algebra R A] : R ‚Üí+* A :=
algebra.to_ring_hom

/-- Creating an algebra from a morphism to the center of a semiring. -/
def ring_hom.to_algebra' {R S} [comm_semiring R] [semiring S] (i : R ‚Üí+* S)
  (h : ‚àÄ c x, i c * x = x * i c) :
  algebra R S :=
{ smul := Œª c x, i c * x,
  commutes' := h,
  smul_def' := Œª c x, rfl,
  .. i}

/-- Creating an algebra from a morphism to a commutative semiring. -/
def ring_hom.to_algebra {R S} [comm_semiring R] [comm_semiring S] (i : R ‚Üí+* S) :
  algebra R S :=
i.to_algebra' $ Œª _, mul_comm _

namespace algebra

variables {R : Type u} {S : Type v} {A : Type w}

/-- Let `R` be a commutative semiring, let `A` be a semiring with a `semimodule R` structure.
If `(r ‚Ä¢ 1) * x = x * (r ‚Ä¢ 1) = r ‚Ä¢ x` for all `r : R` and `x : A`, then `A` is an `algebra`
over `R`. -/
def of_semimodule' [comm_semiring R] [semiring A] [semimodule R A]
  (h‚ÇÅ : ‚àÄ (r : R) (x : A), (r ‚Ä¢ 1) * x = r ‚Ä¢ x)
  (h‚ÇÇ : ‚àÄ (r : R) (x : A), x * (r ‚Ä¢ 1) = r ‚Ä¢ x) : algebra R A :=
{ to_fun := Œª r, r ‚Ä¢ 1,
  map_one' := one_smul _ _,
  map_mul' := Œª r‚ÇÅ r‚ÇÇ, by rw [h‚ÇÅ, mul_smul],
  map_zero' := zero_smul _ _,
  map_add' := Œª r‚ÇÅ r‚ÇÇ, add_smul r‚ÇÅ r‚ÇÇ 1,
  commutes' := Œª r x, by simp only [h‚ÇÅ, h‚ÇÇ],
  smul_def' := Œª r x, by simp only [h‚ÇÅ] }

/-- Let `R` be a commutative semiring, let `A` be a semiring with a `semimodule R` structure.
If `(r ‚Ä¢ x) * y = x * (r ‚Ä¢ y) = r ‚Ä¢ (x * y)` for all `r : R` and `x y : A`, then `A`
is an `algebra` over `R`. -/
def of_semimodule [comm_semiring R] [semiring A] [semimodule R A]
  (h‚ÇÅ : ‚àÄ (r : R) (x y : A), (r ‚Ä¢ x) * y = r ‚Ä¢ (x * y))
  (h‚ÇÇ : ‚àÄ (r : R) (x y : A), x * (r ‚Ä¢ y) = r ‚Ä¢ (x * y)) : algebra R A :=
of_semimodule' (Œª r x, by rw [h‚ÇÅ, one_mul]) (Œª r x, by rw [h‚ÇÇ, mul_one])

section semiring

variables [comm_semiring R] [comm_semiring S] [semiring A] [algebra R A]

lemma smul_def'' (r : R) (x : A) : r ‚Ä¢ x = algebra_map R A r * x :=
algebra.smul_def' r x

/--
To prove two algebra structures on a fixed `[comm_semiring R] [semiring A]` agree,
it suffices to check the `algebra_map`s agree.
-/
-- We'll later use this to show `algebra ‚Ñ§ M` is a subsingleton.
@[ext]
lemma algebra_ext {R : Type*} [comm_semiring R] {A : Type*} [semiring A] (P Q : algebra R A)
  (w : ‚àÄ (r : R), by { haveI := P, exact algebra_map R A r } = by { haveI := Q, exact algebra_map R A r }) :
  P = Q :=
begin
  unfreezingI { rcases P with ‚ü®‚ü®P‚ü©‚ü©, rcases Q with ‚ü®‚ü®Q‚ü©‚ü© },
  congr,
  { funext r a,
    replace w := congr_arg (Œª s, s * a) (w r),
    simp only [‚Üêalgebra.smul_def''] at w,
    apply w, },
  { ext r,
    exact w r, },
  { apply proof_irrel_heq, },
  { apply proof_irrel_heq, },
end

@[priority 200] -- see Note [lower instance priority]
instance to_semimodule : semimodule R A :=
{ one_smul := by simp [smul_def''],
  mul_smul := by simp [smul_def'', mul_assoc],
  smul_add := by simp [smul_def'', mul_add],
  smul_zero := by simp [smul_def''],
  add_smul := by simp [smul_def'', add_mul],
  zero_smul := by simp [smul_def''] }

-- from now on, we don't want to use the following instance anymore
attribute [instance, priority 0] algebra.to_has_scalar

lemma smul_def (r : R) (x : A) : r ‚Ä¢ x = algebra_map R A r * x :=
algebra.smul_def' r x

lemma algebra_map_eq_smul_one (r : R) : algebra_map R A r = r ‚Ä¢ 1 :=
calc algebra_map R A r = algebra_map R A r * 1 : (mul_one _).symm
                   ... = r ‚Ä¢ 1                 : (algebra.smul_def r 1).symm

theorem commutes (r : R) (x : A) : algebra_map R A r * x = x * algebra_map R A r :=
algebra.commutes' r x

theorem left_comm (r : R) (x y : A) : x * (algebra_map R A r * y) = algebra_map R A r * (x * y) :=
by rw [‚Üê mul_assoc, ‚Üê commutes, mul_assoc]

@[simp] lemma mul_smul_comm (s : R) (x y : A) :
  x * (s ‚Ä¢ y) = s ‚Ä¢ (x * y) :=
by rw [smul_def, smul_def, left_comm]

@[simp] lemma smul_mul_assoc (r : R) (x y : A) :
  (r ‚Ä¢ x) * y = r ‚Ä¢ (x * y) :=
by rw [smul_def, smul_def, mul_assoc]

variables (R)
instance id : algebra R R := (ring_hom.id R).to_algebra
variables {R}

namespace id

@[simp] lemma map_eq_self (x : R) : algebra_map R R x = x := rfl

@[simp] lemma smul_eq_mul (x y : R) : x ‚Ä¢ y = x * y := rfl

end id

/-- Algebra over a subsemiring. -/
instance of_subsemiring (S : subsemiring R) : algebra S A :=
{ smul := Œª s x, (s : R) ‚Ä¢ x,
  commutes' := Œª r x, algebra.commutes r x,
  smul_def' := Œª r x, algebra.smul_def r x,
  .. (algebra_map R A).comp (subsemiring.subtype S) }

/-- Algebra over a subring. -/
instance of_subring {R A : Type*} [comm_ring R] [ring A] [algebra R A]
  (S : set R) [is_subring S] : algebra S A :=
{ smul := Œª s x, (s : R) ‚Ä¢ x,
  commutes' := Œª r x, algebra.commutes r x,
  smul_def' := Œª r x, algebra.smul_def r x,
  .. (algebra_map R A).comp (‚ü®coe, rfl, Œª _ _, rfl, rfl, Œª _ _, rfl‚ü© : S ‚Üí+* R) }

variables (R A)
/-- The multiplication in an algebra is a bilinear map. -/
def lmul : A ‚Üí‚Çó A ‚Üí‚Çó A :=
linear_map.mk‚ÇÇ R (*)
  (Œª x y z, add_mul x y z)
  (Œª c x y, by rw [smul_def, smul_def, mul_assoc _ x y])
  (Œª x y z, mul_add x y z)
  (Œª c x y, by rw [smul_def, smul_def, left_comm])

/-- The multiplication on the left in an algebra is a linear map. -/
def lmul_left (r : A) : A ‚Üí‚Çó A :=
lmul R A r

/-- The multiplication on the right in an algebra is a linear map. -/
def lmul_right (r : A) : A ‚Üí‚Çó A :=
(lmul R A).flip r

variables {R A}

@[simp] lemma lmul_apply (p q : A) : lmul R A p q = p * q := rfl
@[simp] lemma lmul_left_apply (p q : A) : lmul_left R A p q = p * q := rfl
@[simp] lemma lmul_right_apply (p q : A) : lmul_right R A p q = q * p := rfl

end semiring

end algebra

instance module.endomorphism_algebra (R : Type u) (M : Type v)
  [comm_ring R] [add_comm_group M] [module R M] : algebra R (M ‚Üí‚Çó[R] M) :=
{ to_fun    := Œª r, r ‚Ä¢ linear_map.id,
  map_one' := one_smul _ _,
  map_zero' := zero_smul _ _,
  map_add' := Œª r‚ÇÅ r‚ÇÇ, add_smul _ _ _,
  map_mul' := Œª r‚ÇÅ r‚ÇÇ, by { ext x, simp [mul_smul] },
  commutes' := by { intros, ext, simp },
  smul_def' := by { intros, ext, simp } }

instance matrix_algebra (n : Type u) (R : Type v)
  [fintype n] [decidable_eq n] [comm_semiring R] : algebra R (matrix n n R) :=
{ commutes' := by { intros, simp [matrix.scalar], },
  smul_def' := by { intros, simp [matrix.scalar], },
  ..(matrix.scalar n) }

set_option old_structure_cmd true
/-- Defining the homomorphism in the category R-Alg. -/
@[nolint has_inhabited_instance]
structure alg_hom (R : Type u) (A : Type v) (B : Type w)
  [comm_semiring R] [semiring A] [semiring B] [algebra R A] [algebra R B] extends ring_hom A B :=
(commutes' : ‚àÄ r : R, to_fun (algebra_map R A r) = algebra_map R B r)

run_cmd tactic.add_doc_string `alg_hom.to_ring_hom "Reinterpret an `alg_hom` as a `ring_hom`"

infixr ` ‚Üí‚Çê `:25 := alg_hom _
notation A ` ‚Üí‚Çê[`:25 R `] ` B := alg_hom R A B

namespace alg_hom

variables {R : Type u} {A : Type v} {B : Type w} {C : Type u‚ÇÅ} {D : Type v‚ÇÅ}

section semiring

variables [comm_semiring R] [semiring A] [semiring B] [semiring C] [semiring D]
variables [algebra R A] [algebra R B] [algebra R C] [algebra R D]

instance : has_coe_to_fun (A ‚Üí‚Çê[R] B) := ‚ü®_, Œª f, f.to_fun‚ü©

instance coe_ring_hom : has_coe (A ‚Üí‚Çê[R] B) (A ‚Üí+* B) := ‚ü®alg_hom.to_ring_hom‚ü©

instance coe_monoid_hom : has_coe (A ‚Üí‚Çê[R] B) (A ‚Üí* B) := ‚ü®Œª f, ‚Üë(f : A ‚Üí+* B)‚ü©

instance coe_add_monoid_hom : has_coe (A ‚Üí‚Çê[R] B) (A ‚Üí+ B) := ‚ü®Œª f, ‚Üë(f : A ‚Üí+* B)‚ü©

@[simp, norm_cast] lemma coe_mk {f : A ‚Üí B} (h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ h‚ÇÖ) :
  ‚áë(‚ü®f, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, h‚ÇÑ, h‚ÇÖ‚ü© : A ‚Üí‚Çê[R] B) = f := rfl

@[simp, norm_cast] lemma coe_to_ring_hom (f : A ‚Üí‚Çê[R] B) : ‚áë(f : A ‚Üí+* B) = f := rfl

-- as `simp` can already prove this lemma, it is not tagged with the `simp` attribute.
@[norm_cast] lemma coe_to_monoid_hom (f : A ‚Üí‚Çê[R] B) : ‚áë(f : A ‚Üí* B) = f := rfl

-- as `simp` can already prove this lemma, it is not tagged with the `simp` attribute.
@[norm_cast] lemma coe_to_add_monoid_hom (f : A ‚Üí‚Çê[R] B) : ‚áë(f : A ‚Üí+ B) = f := rfl

variables (œÜ : A ‚Üí‚Çê[R] B)

theorem coe_fn_inj ‚¶ÉœÜ‚ÇÅ œÜ‚ÇÇ : A ‚Üí‚Çê[R] B‚¶Ñ (H : ‚áëœÜ‚ÇÅ = œÜ‚ÇÇ) : œÜ‚ÇÅ = œÜ‚ÇÇ :=
by { cases œÜ‚ÇÅ, cases œÜ‚ÇÇ, congr, exact H }

theorem coe_ring_hom_injective : function.injective (coe : (A ‚Üí‚Çê[R] B) ‚Üí (A ‚Üí+* B)) :=
Œª œÜ‚ÇÅ œÜ‚ÇÇ H, coe_fn_inj $ show ((œÜ‚ÇÅ : (A ‚Üí+* B)) : A ‚Üí B) = ((œÜ‚ÇÇ : (A ‚Üí+* B)) : A ‚Üí B),
  from congr_arg _ H

theorem coe_monoid_hom_injective : function.injective (coe : (A ‚Üí‚Çê[R] B)  ‚Üí (A ‚Üí* B)) :=
ring_hom.coe_monoid_hom_injective.comp coe_ring_hom_injective

theorem coe_add_monoid_hom_injective : function.injective (coe : (A ‚Üí‚Çê[R] B)  ‚Üí (A ‚Üí+ B)) :=
ring_hom.coe_add_monoid_hom_injective.comp coe_ring_hom_injective

@[ext]
theorem ext {œÜ‚ÇÅ œÜ‚ÇÇ : A ‚Üí‚Çê[R] B} (H : ‚àÄ x, œÜ‚ÇÅ x = œÜ‚ÇÇ x) : œÜ‚ÇÅ = œÜ‚ÇÇ :=
coe_fn_inj $ funext H

theorem ext_iff {œÜ‚ÇÅ œÜ‚ÇÇ : A ‚Üí‚Çê[R] B} : œÜ‚ÇÅ = œÜ‚ÇÇ ‚Üî ‚àÄ x, œÜ‚ÇÅ x = œÜ‚ÇÇ x :=
‚ü®by { rintro rfl x, refl }, ext‚ü©

@[simp]
theorem commutes (r : R) : œÜ (algebra_map R A r) = algebra_map R B r := œÜ.commutes' r

theorem comp_algebra_map : œÜ.to_ring_hom.comp (algebra_map R A) = algebra_map R B :=
ring_hom.ext $ œÜ.commutes

@[simp] lemma map_add (r s : A) : œÜ (r + s) = œÜ r + œÜ s :=
œÜ.to_ring_hom.map_add r s

@[simp] lemma map_zero : œÜ 0 = 0 :=
œÜ.to_ring_hom.map_zero

@[simp] lemma map_mul (x y) : œÜ (x * y) = œÜ x * œÜ y :=
œÜ.to_ring_hom.map_mul x y

@[simp] lemma map_one : œÜ 1 = 1 :=
œÜ.to_ring_hom.map_one

@[simp] lemma map_smul (r : R) (x : A) : œÜ (r ‚Ä¢ x) = r ‚Ä¢ œÜ x :=
by simp only [algebra.smul_def, map_mul, commutes]

@[simp] lemma map_pow (x : A) (n : ‚Ñï) : œÜ (x ^ n) = (œÜ x) ^ n :=
œÜ.to_ring_hom.map_pow x n

lemma map_sum {Œπ : Type*} (f : Œπ ‚Üí A) (s : finset Œπ) :
  œÜ (‚àë x in s, f x) = ‚àë x in s, œÜ (f x) :=
œÜ.to_ring_hom.map_sum f s

@[simp] lemma map_nat_cast (n : ‚Ñï) : œÜ n = n :=
œÜ.to_ring_hom.map_nat_cast n

section

variables (R A)
/-- Identity map as an `alg_hom`. -/
protected def id : A ‚Üí‚Çê[R] A :=
{ commutes' := Œª _, rfl,
  ..ring_hom.id A  }

end

@[simp] lemma id_apply (p : A) : alg_hom.id R A p = p := rfl

/-- Composition of algebra homeomorphisms. -/
def comp (œÜ‚ÇÅ : B ‚Üí‚Çê[R] C) (œÜ‚ÇÇ : A ‚Üí‚Çê[R] B) : A ‚Üí‚Çê[R] C :=
{ commutes' := Œª r : R, by rw [‚Üê œÜ‚ÇÅ.commutes, ‚Üê œÜ‚ÇÇ.commutes]; refl,
  .. œÜ‚ÇÅ.to_ring_hom.comp ‚ÜëœÜ‚ÇÇ }

@[simp] lemma comp_apply (œÜ‚ÇÅ : B ‚Üí‚Çê[R] C) (œÜ‚ÇÇ : A ‚Üí‚Çê[R] B) (p : A) :
  œÜ‚ÇÅ.comp œÜ‚ÇÇ p = œÜ‚ÇÅ (œÜ‚ÇÇ p) := rfl

@[simp] theorem comp_id : œÜ.comp (alg_hom.id R A) = œÜ :=
ext $ Œª x, rfl

@[simp] theorem id_comp : (alg_hom.id R B).comp œÜ = œÜ :=
ext $ Œª x, rfl

theorem comp_assoc (œÜ‚ÇÅ : C ‚Üí‚Çê[R] D) (œÜ‚ÇÇ : B ‚Üí‚Çê[R] C) (œÜ‚ÇÉ : A ‚Üí‚Çê[R] B) :
  (œÜ‚ÇÅ.comp œÜ‚ÇÇ).comp œÜ‚ÇÉ = œÜ‚ÇÅ.comp (œÜ‚ÇÇ.comp œÜ‚ÇÉ) :=
ext $ Œª x, rfl

/-- R-Alg ‚•§ R-Mod -/
def to_linear_map : A ‚Üí‚Çó B :=
{ to_fun := œÜ,
  map_add' := œÜ.map_add,
  map_smul' := œÜ.map_smul }

@[simp] lemma to_linear_map_apply (p : A) : œÜ.to_linear_map p = œÜ p := rfl

theorem to_linear_map_inj {œÜ‚ÇÅ œÜ‚ÇÇ : A ‚Üí‚Çê[R] B} (H : œÜ‚ÇÅ.to_linear_map = œÜ‚ÇÇ.to_linear_map) : œÜ‚ÇÅ = œÜ‚ÇÇ :=
ext $ Œª x, show œÜ‚ÇÅ.to_linear_map x = œÜ‚ÇÇ.to_linear_map x, by rw H

@[simp] lemma comp_to_linear_map (f : A ‚Üí‚Çê[R] B) (g : B ‚Üí‚Çê[R] C) :
  (g.comp f).to_linear_map = g.to_linear_map.comp f.to_linear_map := rfl

end semiring

section comm_semiring

variables [comm_semiring R] [comm_semiring A] [comm_semiring B]
variables [algebra R A] [algebra R B]

variables (œÜ : A ‚Üí‚Çê[R] B)

lemma map_prod {Œπ : Type*} (f : Œπ ‚Üí A) (s : finset Œπ) :
  œÜ (‚àè x in s, f x) = ‚àè x in s, œÜ (f x) :=
œÜ.to_ring_hom.map_prod f s

end comm_semiring

section ring

variables [comm_ring R] [ring A] [ring B] [ring C]
variables [algebra R A] [algebra R B] [algebra R C] (œÜ : A ‚Üí‚Çê[R] B)

@[simp] lemma map_neg (x) : œÜ (-x) = -œÜ x :=
œÜ.to_ring_hom.map_neg x

@[simp] lemma map_sub (x y) : œÜ (x - y) = œÜ x - œÜ y :=
œÜ.to_ring_hom.map_sub x y

end ring

end alg_hom

set_option old_structure_cmd true
/-- An equivalence of algebras is an equivalence of rings commuting with the actions of scalars. -/
structure alg_equiv (R : Type u) (A : Type v) (B : Type w)
  [comm_semiring R] [semiring A] [semiring B] [algebra R A] [algebra R B]
  extends A ‚âÉ B, A ‚âÉ* B, A ‚âÉ+ B, A ‚âÉ+* B :=
(commutes' : ‚àÄ r : R, to_fun (algebra_map R A r) = algebra_map R B r)

attribute [nolint doc_blame] alg_equiv.to_ring_equiv
attribute [nolint doc_blame] alg_equiv.to_equiv
attribute [nolint doc_blame] alg_equiv.to_add_equiv
attribute [nolint doc_blame] alg_equiv.to_mul_equiv

notation A ` ‚âÉ‚Çê[`:50 R `] ` A' := alg_equiv R A A'

namespace alg_equiv

variables {R : Type u} {A‚ÇÅ : Type v} {A‚ÇÇ : Type w} {A‚ÇÉ : Type u‚ÇÅ}
variables [comm_semiring R] [semiring A‚ÇÅ] [semiring A‚ÇÇ] [semiring A‚ÇÉ]
variables [algebra R A‚ÇÅ] [algebra R A‚ÇÇ] [algebra R A‚ÇÉ]

instance : has_coe_to_fun (A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) := ‚ü®_, alg_equiv.to_fun‚ü©

@[ext]
lemma ext {f g : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ} (h : ‚àÄ a, f a = g a) : f = g :=
begin
  have h‚ÇÅ : f.to_equiv = g.to_equiv := equiv.ext h,
  cases f, cases g, congr,
  { exact (funext h) },
  { exact congr_arg equiv.inv_fun h‚ÇÅ }
end

lemma coe_fun_injective : @function.injective (A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) (A‚ÇÅ ‚Üí A‚ÇÇ) (Œª e, (e : A‚ÇÅ ‚Üí A‚ÇÇ)) :=
begin
  intros f g w,
  ext,
  exact congr_fun w a,
end

instance has_coe_to_ring_equiv : has_coe (A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) (A‚ÇÅ ‚âÉ+* A‚ÇÇ) := ‚ü®alg_equiv.to_ring_equiv‚ü©

@[simp] lemma mk_apply {to_fun inv_fun left_inv right_inv map_mul map_add commutes a} :
  (‚ü®to_fun, inv_fun, left_inv, right_inv, map_mul, map_add, commutes‚ü© : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) a = to_fun a :=
rfl

@[simp] lemma to_fun_apply {e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ} {a : A‚ÇÅ} : e.to_fun a = e a := rfl

@[simp, norm_cast] lemma coe_ring_equiv (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : ((e : A‚ÇÅ ‚âÉ+* A‚ÇÇ) : A‚ÇÅ ‚Üí A‚ÇÇ) = e := rfl

lemma coe_ring_equiv_injective : function.injective (Œª e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ, (e : A‚ÇÅ ‚âÉ+* A‚ÇÇ)) :=
begin
  intros f g w,
  ext,
  replace w : ((f : A‚ÇÅ ‚âÉ+* A‚ÇÇ) : A‚ÇÅ ‚Üí A‚ÇÇ) = ((g : A‚ÇÅ ‚âÉ+* A‚ÇÇ) : A‚ÇÅ ‚Üí A‚ÇÇ) :=
    congr_arg (Œª e : A‚ÇÅ ‚âÉ+* A‚ÇÇ, (e : A‚ÇÅ ‚Üí A‚ÇÇ)) w,
  exact congr_fun w a,
end

@[simp] lemma map_add (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : ‚àÄ x y, e (x + y) = e x + e y := e.to_add_equiv.map_add

@[simp] lemma map_zero (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : e 0 = 0 := e.to_add_equiv.map_zero

@[simp] lemma map_mul (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : ‚àÄ x y, e (x * y) = (e x) * (e y) := e.to_mul_equiv.map_mul

@[simp] lemma map_one (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : e 1 = 1 := e.to_mul_equiv.map_one

@[simp] lemma commutes (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : ‚àÄ (r : R), e (algebra_map R A‚ÇÅ r) = algebra_map R A‚ÇÇ r :=
  e.commutes'

@[simp] lemma map_neg {A‚ÇÅ : Type v} {A‚ÇÇ : Type w}
  [ring A‚ÇÅ] [ring A‚ÇÇ] [algebra R A‚ÇÅ] [algebra R A‚ÇÇ] (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) :
  ‚àÄ x, e (-x) = -(e x) := e.to_add_equiv.map_neg

@[simp] lemma map_sub {A‚ÇÅ : Type v} {A‚ÇÇ : Type w}
  [ring A‚ÇÅ] [ring A‚ÇÇ] [algebra R A‚ÇÅ] [algebra R A‚ÇÇ] (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) :
  ‚àÄ x y, e (x - y) = e x - e y := e.to_add_equiv.map_sub

lemma map_sum (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) {Œπ : Type*} (f : Œπ ‚Üí A‚ÇÅ) (s : finset Œπ) :
  e (‚àë x in s, f x) = ‚àë x in s, e (f x) :=
e.to_add_equiv.map_sum f s

instance has_coe_to_alg_hom : has_coe (A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) (A‚ÇÅ ‚Üí‚Çê[R] A‚ÇÇ) :=
  ‚ü®Œª e, { map_one' := e.map_one, map_zero' := e.map_zero, ..e }‚ü©

@[simp, norm_cast] lemma coe_alg_hom (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : ((e : A‚ÇÅ ‚Üí‚Çê[R] A‚ÇÇ) : A‚ÇÅ ‚Üí A‚ÇÇ) = e :=
  rfl

lemma injective (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : function.injective e := e.to_equiv.injective

lemma surjective (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : function.surjective e := e.to_equiv.surjective

lemma bijective (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : function.bijective e := e.to_equiv.bijective

instance : has_one (A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÅ) := ‚ü®{commutes' := Œª r, rfl, ..(1 : A‚ÇÅ ‚âÉ+* A‚ÇÅ)}‚ü©

instance : inhabited (A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÅ) := ‚ü®1‚ü©

/-- Algebra equivalences are reflexive. -/
@[refl]
def refl : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÅ := 1

@[simp] lemma coe_refl : (@refl R A‚ÇÅ _ _ _ : A‚ÇÅ ‚Üí‚Çê[R] A‚ÇÅ) = alg_hom.id R A‚ÇÅ :=
alg_hom.ext (Œª x, rfl)

/-- Algebra equivalences are symmetric. -/
@[symm]
def symm (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : A‚ÇÇ ‚âÉ‚Çê[R] A‚ÇÅ :=
{ commutes' := Œª r, by { rw ‚Üêe.to_ring_equiv.symm_apply_apply (algebra_map R A‚ÇÅ r), congr,
                         change _ = e _, rw e.commutes, },
  ..e.to_ring_equiv.symm, }

@[simp] lemma inv_fun_apply {e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ} {a : A‚ÇÇ} : e.inv_fun a = e.symm a := rfl

@[simp] lemma symm_symm {e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ} : e.symm.symm = e :=
by { ext, refl, }

/-- Algebra equivalences are transitive. -/
@[trans]
def trans (e‚ÇÅ : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) (e‚ÇÇ : A‚ÇÇ ‚âÉ‚Çê[R] A‚ÇÉ) : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÉ :=
{ commutes' := Œª r, show e‚ÇÇ.to_fun (e‚ÇÅ.to_fun _) = _, by rw [e‚ÇÅ.commutes', e‚ÇÇ.commutes'],
  ..(e‚ÇÅ.to_ring_equiv.trans e‚ÇÇ.to_ring_equiv), }

@[simp] lemma apply_symm_apply (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : ‚àÄ x, e (e.symm x) = x :=
  e.to_equiv.apply_symm_apply

@[simp] lemma symm_apply_apply (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) : ‚àÄ x, e.symm (e x) = x :=
  e.to_equiv.symm_apply_apply

@[simp] lemma trans_apply (e‚ÇÅ : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) (e‚ÇÇ : A‚ÇÇ ‚âÉ‚Çê[R] A‚ÇÉ) (x : A‚ÇÅ) :
  (e‚ÇÅ.trans e‚ÇÇ) x = e‚ÇÇ (e‚ÇÅ x) := rfl

@[simp] lemma comp_symm (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) :
  alg_hom.comp (e : A‚ÇÅ ‚Üí‚Çê[R] A‚ÇÇ) ‚Üëe.symm = alg_hom.id R A‚ÇÇ :=
by { ext, simp }

@[simp] lemma symm_comp (e : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ) :
  alg_hom.comp ‚Üëe.symm (e : A‚ÇÅ ‚Üí‚Çê[R] A‚ÇÇ) = alg_hom.id R A‚ÇÅ :=
by { ext, simp }

/-- If an algebra morphism has an inverse, it is a algebra isomorphism. -/
def of_alg_hom (f : A‚ÇÅ ‚Üí‚Çê[R] A‚ÇÇ) (g : A‚ÇÇ ‚Üí‚Çê[R] A‚ÇÅ) (h‚ÇÅ : f.comp g = alg_hom.id R A‚ÇÇ) (h‚ÇÇ : g.comp f = alg_hom.id R A‚ÇÅ) : A‚ÇÅ ‚âÉ‚Çê[R] A‚ÇÇ :=
{ inv_fun   := g,
  left_inv  := alg_hom.ext_iff.1 h‚ÇÇ,
  right_inv := alg_hom.ext_iff.1 h‚ÇÅ,
  ..f }

end alg_equiv

namespace algebra

variables (R : Type u) (S : Type v) (A : Type w)
include R S A

/-- `comap R S A` is a type alias for `A`, and has an R-algebra structure defined on it
  when `algebra R S` and `algebra S A`. If `S` is an `R`-algebra and `A` is an `S`-algebra then
  `algebra.comap.algebra R S A` can be used to provide `A` with a structure of an `R`-algebra.
  Other than that, `algebra.comap` is now deprecated and replcaed with `is_algebra_tower`. -/
/- This is done to avoid a type class search with meta-variables `algebra R ?m_1` and
    `algebra ?m_1 A -/
/- The `nolint` attribute is added because it has unused arguments `R` and `S`, but these are necessary for synthesizing the
     appropriate type classes -/
@[nolint unused_arguments]
def comap : Type w := A

instance comap.inhabited [h : inhabited A] : inhabited (comap R S A) := h
instance comap.semiring [h : semiring A] : semiring (comap R S A) := h
instance comap.ring [h : ring A] : ring (comap R S A) := h
instance comap.comm_semiring [h : comm_semiring A] : comm_semiring (comap R S A) := h
instance comap.comm_ring [h : comm_ring A] : comm_ring (comap R S A) := h

instance comap.algebra' [comm_semiring S] [semiring A] [h : algebra S A] :
  algebra S (comap R S A) := h

/-- Identity homomorphism `A ‚Üí‚Çê[S] comap R S A`. -/
def comap.to_comap [comm_semiring S] [semiring A] [algebra S A] :
  A ‚Üí‚Çê[S] comap R S A := alg_hom.id S A
/-- Identity homomorphism `comap R S A ‚Üí‚Çê[S] A`. -/
def comap.of_comap [comm_semiring S] [semiring A] [algebra S A] :
  comap R S A ‚Üí‚Çê[S] A := alg_hom.id S A

variables [comm_semiring R] [comm_semiring S] [semiring A] [algebra R S] [algebra S A]

/-- `R ‚ü∂ S` induces `S-Alg ‚•§ R-Alg` -/
instance comap.algebra : algebra R (comap R S A) :=
{ smul := Œª r x, (algebra_map R S r ‚Ä¢ x : A),
  commutes' := Œª r x, algebra.commutes _ _,
  smul_def' := Œª _ _, algebra.smul_def _ _,
  .. (algebra_map S A).comp (algebra_map R S) }

/-- Embedding of `S` into `comap R S A`. -/
def to_comap : S ‚Üí‚Çê[R] comap R S A :=
{ commutes' := Œª r, rfl,
  .. algebra_map S A }

theorem to_comap_apply (x) : to_comap R S A x = algebra_map S A x := rfl

end algebra

namespace alg_hom

variables {R : Type u} {S : Type v} {A : Type w} {B : Type u‚ÇÅ}
variables [comm_semiring R] [comm_semiring S] [semiring A] [semiring B]
variables [algebra R S] [algebra S A] [algebra S B] (œÜ : A ‚Üí‚Çê[S] B)
include R

/-- R ‚ü∂ S induces S-Alg ‚•§ R-Alg -/
def comap : algebra.comap R S A ‚Üí‚Çê[R] algebra.comap R S B :=
{ commutes' := Œª r, œÜ.commutes (algebra_map R S r)
  ..œÜ }

end alg_hom

namespace rat

instance algebra_rat {Œ±} [division_ring Œ±] [char_zero Œ±] : algebra ‚Ñö Œ± :=
(rat.cast_hom Œ±).to_algebra' $ Œª r x, r.cast_commute x

end rat

/-- A subalgebra is a subring that includes the range of `algebra_map`. -/
structure subalgebra (R : Type u) (A : Type v)
  [comm_semiring R] [semiring A] [algebra R A] : Type v :=
(carrier : subsemiring A)
(algebra_map_mem' : ‚àÄ r, algebra_map R A r ‚àà carrier)

namespace subalgebra

variables {R : Type u} {A : Type v} {B : Type w}
variables [comm_semiring R] [semiring A] [algebra R A] [semiring B] [algebra R B]
include R

instance : has_coe (subalgebra R A) (subsemiring A) :=
‚ü®Œª S, S.carrier‚ü©

instance : has_mem A (subalgebra R A) :=
‚ü®Œª x S, x ‚àà (S : set A)‚ü©

variables {A}
theorem mem_coe {x : A} {s : subalgebra R A} : x ‚àà (s : set A) ‚Üî x ‚àà s :=
iff.rfl

@[ext] theorem ext {S T : subalgebra R A}
  (h : ‚àÄ x : A, x ‚àà S ‚Üî x ‚àà T) : S = T :=
by cases S; cases T; congr; ext x; exact h x

theorem ext_iff {S T : subalgebra R A} : S = T ‚Üî ‚àÄ x : A, x ‚àà S ‚Üî x ‚àà T :=
‚ü®Œª h x, by rw h, ext‚ü©

variables (S : subalgebra R A)

theorem algebra_map_mem (r : R) : algebra_map R A r ‚àà S :=
S.algebra_map_mem' r

theorem srange_le : (algebra_map R A).srange ‚â§ S :=
Œª x ‚ü®r, _, hr‚ü©, hr ‚ñ∏ S.algebra_map_mem r

theorem range_subset : set.range (algebra_map R A) ‚äÜ S :=
Œª x ‚ü®r, hr‚ü©, hr ‚ñ∏ S.algebra_map_mem r

theorem range_le : set.range (algebra_map R A) ‚â§ S :=
S.range_subset

theorem one_mem : (1 : A) ‚àà S :=
subsemiring.one_mem S

theorem mul_mem {x y : A} (hx : x ‚àà S) (hy : y ‚àà S) : x * y ‚àà S :=
subsemiring.mul_mem S hx hy

theorem smul_mem {x : A} (hx : x ‚àà S) (r : R) : r ‚Ä¢ x ‚àà S :=
(algebra.smul_def r x).symm ‚ñ∏ S.mul_mem (S.algebra_map_mem r) hx

theorem pow_mem {x : A} (hx : x ‚àà S) (n : ‚Ñï) : x ^ n ‚àà S :=
subsemiring.pow_mem S hx n

theorem zero_mem : (0 : A) ‚àà S :=
subsemiring.zero_mem S

theorem add_mem {x y : A} (hx : x ‚àà S) (hy : y ‚àà S) : x + y ‚àà S :=
subsemiring.add_mem S hx hy

theorem neg_mem {R : Type u} {A : Type v} [comm_ring R] [ring A]
  [algebra R A] (S : subalgebra R A) {x : A} (hx : x ‚àà S) : -x ‚àà S :=
neg_one_smul R x ‚ñ∏ S.smul_mem hx _

theorem sub_mem {R : Type u} {A : Type v} [comm_ring R] [ring A]
  [algebra R A] (S : subalgebra R A) {x y : A} (hx : x ‚àà S) (hy : y ‚àà S) : x - y ‚àà S :=
S.add_mem hx $ S.neg_mem hy

theorem nsmul_mem {x : A} (hx : x ‚àà S) (n : ‚Ñï) : n ‚Ä¢‚Ñï x ‚àà S :=
subsemiring.nsmul_mem S hx n

theorem gsmul_mem {R : Type u} {A : Type v} [comm_ring R] [ring A]
  [algebra R A] (S : subalgebra R A) {x : A} (hx : x ‚àà S) (n : ‚Ñ§) : n ‚Ä¢‚Ñ§ x ‚àà S :=
int.cases_on n (Œª i, S.nsmul_mem hx i) (Œª i, S.neg_mem $ S.nsmul_mem hx _)

theorem coe_nat_mem (n : ‚Ñï) : (n : A) ‚àà S :=
subsemiring.coe_nat_mem S n

theorem coe_int_mem {R : Type u} {A : Type v} [comm_ring R] [ring A]
  [algebra R A] (S : subalgebra R A) (n : ‚Ñ§) : (n : A) ‚àà S :=
int.cases_on n (Œª i, S.coe_nat_mem i) (Œª i, S.neg_mem $ S.coe_nat_mem $ i + 1)

theorem list_prod_mem {L : list A} (h : ‚àÄ x ‚àà L, x ‚àà S) : L.prod ‚àà S :=
subsemiring.list_prod_mem S h

theorem list_sum_mem {L : list A} (h : ‚àÄ x ‚àà L, x ‚àà S) : L.sum ‚àà S :=
subsemiring.list_sum_mem S h

theorem multiset_prod_mem {R : Type u} {A : Type v} [comm_semiring R] [comm_semiring A]
  [algebra R A] (S : subalgebra R A) {m : multiset A} (h : ‚àÄ x ‚àà m, x ‚àà S) : m.prod ‚àà S :=
subsemiring.multiset_prod_mem S m h

theorem multiset_sum_mem {m : multiset A} (h : ‚àÄ x ‚àà m, x ‚àà S) : m.sum ‚àà S :=
subsemiring.multiset_sum_mem S m h

theorem prod_mem {R : Type u} {A : Type v} [comm_semiring R] [comm_semiring A]
  [algebra R A] (S : subalgebra R A) {Œπ : Type w} {t : finset Œπ} {f : Œπ ‚Üí A}
  (h : ‚àÄ x ‚àà t, f x ‚àà S) : ‚àè x in t, f x ‚àà S :=
subsemiring.prod_mem S h

theorem sum_mem {Œπ : Type w} {t : finset Œπ} {f : Œπ ‚Üí A}
  (h : ‚àÄ x ‚àà t, f x ‚àà S) : ‚àë x in t, f x ‚àà S :=
subsemiring.sum_mem S h

instance {R : Type u} {A : Type v} [comm_semiring R] [semiring A] [algebra R A]
  (S : subalgebra R A) : is_add_submonoid (S : set A) :=
{ zero_mem := S.zero_mem,
  add_mem := Œª _ _, S.add_mem }

instance {R : Type u} {A : Type v} [comm_semiring R] [semiring A] [algebra R A]
  (S : subalgebra R A) : is_submonoid (S : set A) :=
{ one_mem := S.one_mem,
  mul_mem := Œª _ _, S.mul_mem }

instance {R : Type u} {A : Type v} [comm_ring R] [ring A] [algebra R A] (S : subalgebra R A) :
  is_subring (S : set A) :=
{ neg_mem := Œª _, S.neg_mem }

instance : inhabited S := ‚ü®0‚ü©
instance (R : Type u) (A : Type v) [comm_semiring R] [semiring A]
  [algebra R A] (S : subalgebra R A) : semiring S := subsemiring.to_semiring S
instance (R : Type u) (A : Type v) [comm_semiring R] [comm_semiring A]
  [algebra R A] (S : subalgebra R A) : comm_semiring S := subsemiring.to_comm_semiring S
instance (R : Type u) (A : Type v) [comm_ring R] [ring A]
  [algebra R A] (S : subalgebra R A) : ring S := @@subtype.ring _ S.is_subring
instance (R : Type u) (A : Type v) [comm_ring R] [comm_ring A]
  [algebra R A] (S : subalgebra R A) : comm_ring S := @@subtype.comm_ring _ S.is_subring

instance algebra : algebra R S :=
{ smul := Œª (c:R) x, ‚ü®c ‚Ä¢ x.1, S.smul_mem x.2 c‚ü©,
  commutes' := Œª c x, subtype.eq $ algebra.commutes _ _,
  smul_def' := Œª c x, subtype.eq $ algebra.smul_def _ _,
  .. (algebra_map R A).cod_srestrict S $ Œª x, S.range_le ‚ü®x, rfl‚ü© }

instance to_algebra {R : Type u} {A : Type v} [comm_semiring R] [comm_semiring A]
  [algebra R A] (S : subalgebra R A) : algebra S A :=
algebra.of_subsemiring _

-- todo: standardize on the names these morphisms
-- compare with submodule.subtype

/-- Embedding of a subalgebra into the algebra. -/
def val : S ‚Üí‚Çê[R] A :=
by refine_struct { to_fun := (coe : S ‚Üí A) }; intros; refl

@[simp]
lemma val_apply (x : S) : S.val x = (x : A) := rfl

/-- Convert a `subalgebra` to `submodule` -/
def to_submodule : submodule R A :=
{ carrier := S,
  zero_mem' := (0:S).2,
  add_mem' := Œª x y hx hy, (‚ü®x, hx‚ü© + ‚ü®y, hy‚ü© : S).2,
  smul_mem' := Œª c x hx, (algebra.smul_def c x).symm ‚ñ∏
    (‚ü®algebra_map R A c, S.range_le ‚ü®c, rfl‚ü©‚ü© * ‚ü®x, hx‚ü©:S).2 }

instance coe_to_submodule : has_coe (subalgebra R A) (submodule R A) :=
‚ü®to_submodule‚ü©

instance to_submodule.is_subring {R : Type u} {A : Type v} [comm_ring R] [ring A] [algebra R A]
  (S : subalgebra R A) : is_subring ((S : submodule R A) : set A) := S.is_subring

@[simp] lemma mem_to_submodule {x} : x ‚àà (S : submodule R A) ‚Üî x ‚àà S := iff.rfl

theorem to_submodule_injective {S U : subalgebra R A} (h : (S : submodule R A) = U) : S = U :=
ext $ Œª x, by rw [‚Üê mem_to_submodule, ‚Üê mem_to_submodule, h]

theorem to_submodule_inj {S U : subalgebra R A} : (S : submodule R A) = U ‚Üî S = U :=
‚ü®to_submodule_injective, congr_arg _‚ü©

instance : partial_order (subalgebra R A) :=
{ le := Œª S T, (S : set A) ‚äÜ (T : set A),
  le_refl := Œª S, set.subset.refl S,
  le_trans := Œª _ _ _, set.subset.trans,
  le_antisymm := Œª S T hst hts, ext $ Œª x, ‚ü®@hst x, @hts x‚ü© }

/-- Reinterpret an `S`-subalgebra as an `R`-subalgebra in `comap R S A`. -/
def comap {R : Type u} {S : Type v} {A : Type w}
  [comm_semiring R] [comm_semiring S] [semiring A] [algebra R S] [algebra S A]
  (iSB : subalgebra S A) : subalgebra R (algebra.comap R S A) :=
{ carrier := (iSB : subsemiring A),
  algebra_map_mem' := Œª r, iSB.algebra_map_mem (algebra_map R S r) }

/-- If `S` is an `R`-subalgebra of `A` and `T` is an `S`-subalgebra of `A`,
then `T` is an `R`-subalgebra of `A`. -/
def under {R : Type u} {A : Type v} [comm_semiring R] [comm_semiring A]
  {i : algebra R A} (S : subalgebra R A)
  (T : subalgebra S A) : subalgebra R A :=
{ carrier := T,
  algebra_map_mem' := Œª r, T.algebra_map_mem ‚ü®algebra_map R A r, S.algebra_map_mem r‚ü© }

/-- Transport a subalgebra via an algebra homomorphism. -/
def map (S : subalgebra R A) (f : A ‚Üí‚Çê[R] B) : subalgebra R B :=
{ carrier := subsemiring.map (f : A ‚Üí+* B) S,
  algebra_map_mem' := Œª r, f.commutes r ‚ñ∏ set.mem_image_of_mem _ (S.algebra_map_mem r) }

/-- Preimage of a subalgebra under an algebra homomorphism. -/
def comap' (S : subalgebra R B) (f : A ‚Üí‚Çê[R] B) : subalgebra R A :=
{ carrier := subsemiring.comap (f : A ‚Üí+* B) S,
  algebra_map_mem' := Œª r, show f (algebra_map R A r) ‚àà S,
    from (f.commutes r).symm ‚ñ∏ S.algebra_map_mem r }

theorem map_le {S : subalgebra R A} {f : A ‚Üí‚Çê[R] B} {U : subalgebra R B} :
  map S f ‚â§ U ‚Üî S ‚â§ comap' U f :=
set.image_subset_iff

end subalgebra

namespace alg_hom

variables {R : Type u} {A : Type v} {B : Type w}
variables [comm_semiring R] [semiring A] [semiring B] [algebra R A] [algebra R B]
variables (œÜ : A ‚Üí‚Çê[R] B)

/-- Range of an `alg_hom` as a subalgebra. -/
protected def range (œÜ : A ‚Üí‚Çê[R] B) : subalgebra R B :=
{ carrier :=
  { carrier := set.range œÜ,
    one_mem' := ‚ü®1, œÜ.map_one‚ü©,
    mul_mem' := Œª _ _ ‚ü®x, hx‚ü© ‚ü®y, hy‚ü©, ‚ü®x * y, by rw [œÜ.map_mul, hx, hy]‚ü©,
    zero_mem' := ‚ü®0, œÜ.map_zero‚ü©,
    add_mem' := Œª _ _ ‚ü®x, hx‚ü© ‚ü®y, hy‚ü©, ‚ü®x + y, by rw [œÜ.map_add, hx, hy]‚ü© },
  algebra_map_mem' := Œª r, ‚ü®algebra_map R A r, œÜ.commutes r‚ü© }

end alg_hom

namespace algebra

variables (R : Type u) (A : Type v)

variables [comm_semiring R] [semiring A] [algebra R A]

/-- `algebra_map` as an `alg_hom`. -/
def of_id : R ‚Üí‚Çê[R] A :=
{ commutes' := Œª _, rfl, .. algebra_map R A }
variables {R}

theorem of_id_apply (r) : of_id R A r = algebra_map R A r := rfl

end algebra

namespace algebra

variables (R : Type u) {A : Type v} {B : Type w}
variables [comm_semiring R] [semiring A] [algebra R A] [semiring B] [algebra R B]

/-- The minimal subalgebra that includes `s`. -/
def adjoin (s : set A) : subalgebra R A :=
{ carrier := subsemiring.closure (set.range (algebra_map R A) ‚à™ s),
  algebra_map_mem' := Œª r, subsemiring.subset_closure $ or.inl ‚ü®r, rfl‚ü© }
variables {R}

protected lemma gc : galois_connection (adjoin R : set A ‚Üí subalgebra R A) coe :=
Œª s S, ‚ü®Œª H, le_trans (le_trans (set.subset_union_right _ _) subsemiring.subset_closure) H,
Œª H, subsemiring.closure_le.2 $ set.union_subset S.range_subset H‚ü©

/-- Galois insertion between `adjoin` and `coe`. -/
protected def gi : galois_insertion (adjoin R : set A ‚Üí subalgebra R A) coe :=
{ choice := Œª s hs, adjoin R s,
  gc := algebra.gc,
  le_l_u := Œª S, (algebra.gc (S : set A) (adjoin R S)).1 $ le_refl _,
  choice_eq := Œª _ _, rfl }

instance : complete_lattice (subalgebra R A) :=
galois_insertion.lift_complete_lattice algebra.gi

instance : inhabited (subalgebra R A) := ‚ü®‚ä•‚ü©

theorem mem_bot {x : A} : x ‚àà (‚ä• : subalgebra R A) ‚Üî x ‚àà set.range (algebra_map R A) :=
suffices (‚ä• : subalgebra R A) = (of_id R A).range, by rw this; refl,
le_antisymm bot_le $ subalgebra.range_le _

theorem mem_top {x : A} : x ‚àà (‚ä§ : subalgebra R A) :=
subsemiring.subset_closure $ or.inr trivial

theorem eq_top_iff {S : subalgebra R A} :
  S = ‚ä§ ‚Üî ‚àÄ x : A, x ‚àà S :=
‚ü®Œª h x, by rw h; exact mem_top, Œª h, by ext x; exact ‚ü®Œª _, mem_top, Œª _, h x‚ü©‚ü©

@[simp] theorem map_top (f : A ‚Üí‚Çê[R] B) : subalgebra.map (‚ä§ : subalgebra R A) f = f.range :=
subalgebra.ext $ Œª x, ‚ü®Œª ‚ü®y, _, hy‚ü©, ‚ü®y, hy‚ü©, Œª ‚ü®y, hy‚ü©, ‚ü®y, algebra.mem_top, hy‚ü©‚ü©

@[simp] theorem map_bot (f : A ‚Üí‚Çê[R] B) : subalgebra.map (‚ä• : subalgebra R A) f = ‚ä• :=
eq_bot_iff.2 $ Œª x ‚ü®y, hy, hfy‚ü©, let ‚ü®r, hr‚ü© := mem_bot.1 hy in subalgebra.range_le _
‚ü®r, by rwa [‚Üê f.commutes, hr]‚ü©

@[simp] theorem comap_top (f : A ‚Üí‚Çê[R] B) : subalgebra.comap' (‚ä§ : subalgebra R B) f = ‚ä§ :=
eq_top_iff.2 $ Œª x, mem_top

/-- `alg_hom` to `‚ä§ : subalgebra R A`. -/
def to_top : A ‚Üí‚Çê[R] (‚ä§ : subalgebra R A) :=
by refine_struct { to_fun := Œª x, (‚ü®x, mem_top‚ü© : (‚ä§ : subalgebra R A)) }; intros; refl

end algebra

section nat

variables (R : Type*) [semiring R]

/-- Reinterpret a `ring_hom` as an `‚Ñï`-algebra homomorphism. -/
def alg_hom_nat
  {R : Type u} [semiring R] [algebra ‚Ñï R]
  {S : Type v} [semiring S] [algebra ‚Ñï S]
  (f : R ‚Üí+* S) : R ‚Üí‚Çê[‚Ñï] S :=
{ commutes' := Œª i, show f _ = _, by simp, .. f }

/-- Semiring ‚•§ ‚Ñï-Alg -/
instance algebra_nat : algebra ‚Ñï R :=
{ commutes' := nat.cast_commute,
  smul_def' := Œª _ _, nsmul_eq_mul _ _,
  .. nat.cast_ring_hom R }

variables {R}
/-- A subsemiring is a `‚Ñï`-subalgebra. -/
def subalgebra_of_subsemiring (S : subsemiring R) : subalgebra ‚Ñï R :=
{ carrier := S,
  algebra_map_mem' := Œª i, S.coe_nat_mem i }

@[simp] lemma mem_subalgebra_of_subsemiring {x : R} {S : subsemiring R} :
  x ‚àà subalgebra_of_subsemiring S ‚Üî x ‚àà S :=
iff.rfl

section span_nat
open submodule

lemma span_nat_eq_add_group_closure (s : set R) :
  (span ‚Ñï s).to_add_submonoid = add_submonoid.closure s :=
eq.symm $ add_submonoid.closure_eq_of_le subset_span $ Œª x hx, span_induction hx
  (Œª x hx, add_submonoid.subset_closure hx) (add_submonoid.zero_mem _)
  (Œª _ _, add_submonoid.add_mem _) (Œª _ _ _, add_submonoid.nsmul_mem _ ‚Äπ_‚Ä∫ _)

@[simp] lemma span_nat_eq (s : add_submonoid R) : (span ‚Ñï (s : set R)).to_add_submonoid = s :=
by rw [span_nat_eq_add_group_closure, s.closure_eq]

end span_nat

end nat

section int

variables (R : Type*) [ring R]

/-- Reinterpret a `ring_hom` as a `‚Ñ§`-algebra homomorphism. -/
def alg_hom_int
  {R : Type u} [comm_ring R] [algebra ‚Ñ§ R]
  {S : Type v} [comm_ring S] [algebra ‚Ñ§ S]
  (f : R ‚Üí+* S) : R ‚Üí‚Çê[‚Ñ§] S :=
{ commutes' := Œª i, show f _ = _, by simp, .. f }

/-- Ring ‚•§ ‚Ñ§-Alg -/
instance algebra_int : algebra ‚Ñ§ R :=
{ commutes' := int.cast_commute,
  smul_def' := Œª _ _, gsmul_eq_mul _ _,
  .. int.cast_ring_hom R }

/--
Promote a ring homomorphisms to a `‚Ñ§`-algebra homomorphism.
-/
def ring_hom.to_int_alg_hom {R S : Type*} [ring R] [ring S] (f : R ‚Üí+* S) : R ‚Üí‚Çê[‚Ñ§] S :=
{ commutes' := Œª n, by simp,
  .. f }

variables {R}
/-- A subring is a `‚Ñ§`-subalgebra. -/
def subalgebra_of_subring (S : set R) [is_subring S] : subalgebra ‚Ñ§ R :=
{ carrier :=
  { carrier := S,
    one_mem' := is_submonoid.one_mem,
    mul_mem' := Œª _ _, is_submonoid.mul_mem,
    zero_mem' := is_add_submonoid.zero_mem,
    add_mem' := Œª _ _, is_add_submonoid.add_mem, },
  algebra_map_mem' := Œª i, int.induction_on i (show (0 : R) ‚àà S, from is_add_submonoid.zero_mem)
    (Œª i ih, show (i + 1 : R) ‚àà S, from is_add_submonoid.add_mem ih is_submonoid.one_mem)
    (Œª i ih, show ((-i - 1 : ‚Ñ§) : R) ‚àà S, by { rw [int.cast_sub, int.cast_one],
      exact is_add_subgroup.sub_mem S _ _ ih is_submonoid.one_mem }) }


section
variables {S : Type*} [ring S]

instance int_algebra_subsingleton : subsingleton (algebra ‚Ñ§ S) :=
‚ü®Œª P Q, by { ext, simp, }‚ü©
end

section
variables {S : Type*} [semiring S]

instance nat_algebra_subsingleton : subsingleton (algebra ‚Ñï S) :=
‚ü®Œª P Q, by { ext, simp, }‚ü©
end

@[simp] lemma mem_subalgebra_of_subring {x : R} {S : set R} [is_subring S] :
  x ‚àà subalgebra_of_subring S ‚Üî x ‚àà S :=
iff.rfl

section span_int
open submodule

lemma span_int_eq_add_group_closure (s : set R) :
  (span ‚Ñ§ s).to_add_subgroup = add_subgroup.closure s :=
eq.symm $ add_subgroup.closure_eq_of_le _ subset_span $ Œª x hx, span_induction hx
  (Œª x hx, add_subgroup.subset_closure hx) (add_subgroup.zero_mem _)
  (Œª _ _, add_subgroup.add_mem _) (Œª _ _ _, add_subgroup.gsmul_mem _ ‚Äπ_‚Ä∫ _)

@[simp] lemma span_int_eq (s : add_subgroup R) : (span ‚Ñ§ (s : set R)).to_add_subgroup = s :=
by rw [span_int_eq_add_group_closure, s.closure_eq]

end span_int

end int

section restrict_scalars
/- In this section, we describe restriction of scalars: if `S` is an algebra over `R`, then
`S`-modules are also `R`-modules. -/

variables (R : Type*) [comm_ring R] (S : Type*) [ring S] [algebra R S]
variables (E : Type*) [add_comm_group E] [module S E] {F : Type*} [add_comm_group F] [module S F]

/--
When `E` is a module over a ring `S`, and `S` is an algebra over `R`, then `E` inherits a
module structure over `R`, called `module.restrict_scalars' R S E`.
We do not register this as an instance as `S` can not be inferred.
-/
def module.restrict_scalars' : module R E :=
{ smul      := Œª c x, (algebra_map R S c) ‚Ä¢ x,
  one_smul  := by simp,
  mul_smul  := by simp [mul_smul],
  smul_add  := by simp [smul_add],
  smul_zero := by simp [smul_zero],
  add_smul  := by simp [add_smul],
  zero_smul := by simp [zero_smul] }

/--
When `E` is a module over a ring `S`, and `S` is an algebra over `R`, then `E` inherits a
module structure over `R`, provided as a type synonym `module.restrict_scalars R S E := E`.
-/
@[nolint unused_arguments]
def module.restrict_scalars (R : Type*) (S : Type*) (E : Type*) : Type* := E

instance (R : Type*) (S : Type*) (E : Type*) [I : inhabited E] :
  inhabited (module.restrict_scalars R S E) := I

instance (R : Type*) (S : Type*) (E : Type*) [I : add_comm_group E] :
  add_comm_group (module.restrict_scalars R S E) := I

instance : module R (module.restrict_scalars R S E) :=
(module.restrict_scalars' R S E : module R E)

lemma module.restrict_scalars_smul_def (c : R) (x : module.restrict_scalars R S E) :
  c ‚Ä¢ x = ((algebra_map R S c) ‚Ä¢ x : E) := rfl

/--
`module.restrict_scalars R S S` is `R`-linearly equivalent to the original algebra `S`.

Unfortunately these structures are not generally definitionally equal:
the `R`-module structure on `S` is part of the data of `S`,
while the `R`-module structure on `module.restrict_scalars R S S`
comes from the ring homomorphism `R ‚Üí+* S`, which is a separate part of the data of `S`.
The field `algebra.smul_def'` gives the equation we need here.
-/
def algebra.restrict_scalars_equiv :
  (module.restrict_scalars R S S) ‚âÉ‚Çó[R] S :=
{ to_fun := Œª s, s,
  inv_fun := Œª s, s,
  left_inv := Œª s, rfl,
  right_inv := Œª s, rfl,
  map_add' := Œª x y, rfl,
  map_smul' := Œª c x, (algebra.smul_def' _ _).symm, }

@[simp]
lemma algebra.restrict_scalars_equiv_apply (s : S) :
  algebra.restrict_scalars_equiv R S s = s := rfl
@[simp]
lemma algebra.restrict_scalars_equiv_symm_apply (s : S) :
  (algebra.restrict_scalars_equiv R S).symm s = s := rfl

variables {S E}

open module

/--
`V.restrict_scalars R` is the `R`-submodule of the `R`-module given by restriction of scalars,
corresponding to `V`, an `S`-submodule of the original `S`-module.
-/
@[simps]
def submodule.restrict_scalars (V : submodule S E) : submodule R (restrict_scalars R S E) :=
{ carrier := V.carrier,
  zero_mem' := V.zero_mem,
  smul_mem' := Œª c e h, V.smul_mem _ h,
  add_mem' := Œª x y hx hy, V.add_mem hx hy, }

@[simp]
lemma submodule.restrict_scalars_mem (V : submodule S E) (e : E) :
  e ‚àà V.restrict_scalars R ‚Üî e ‚àà V :=
iff.refl _

@[simp]
lemma submodule.restrict_scalars_bot :
  submodule.restrict_scalars R (‚ä• : submodule S E) = ‚ä• :=
rfl

@[simp]
lemma submodule.restrict_scalars_top :
  submodule.restrict_scalars R (‚ä§ : submodule S E) = ‚ä§ :=
rfl

/-- The `R`-linear map induced by an `S`-linear map when `S` is an algebra over `R`. -/
def linear_map.restrict_scalars (f : E ‚Üí‚Çó[S] F) :
  (restrict_scalars R S E) ‚Üí‚Çó[R] (restrict_scalars R S F) :=
{ to_fun := f.to_fun,
  map_add' := Œªx y, f.map_add x y,
  map_smul' := Œªc x, f.map_smul (algebra_map R S c) x }

@[simp, norm_cast squash] lemma linear_map.coe_restrict_scalars_eq_coe (f : E ‚Üí‚Çó[S] F) :
  (f.restrict_scalars R : E ‚Üí F) = f := rfl

@[simp]
lemma restrict_scalars_ker (f : E ‚Üí‚Çó[S] F) :
  (f.restrict_scalars R).ker = submodule.restrict_scalars R f.ker :=
rfl

variables (ùïú : Type*) [field ùïú] (ùïú' : Type*) [field ùïú'] [algebra ùïú ùïú']
variables (W : Type*) [add_comm_group W] [vector_space ùïú' W]

/--
`V.restrict_scalars ùïú` is the `ùïú`-subspace of the `ùïú`-vector space given by restriction of scalars,
corresponding to `V`, a `ùïú'`-subspace of the original `ùïú'`-vector space.
-/
def subspace.restrict_scalars (V : subspace ùïú' W) : subspace ùïú (restrict_scalars ùïú ùïú' W) :=
{ ..submodule.restrict_scalars ùïú (V : submodule ùïú' W) }

end restrict_scalars


/-!
When `V` and `W` are `S`-modules, for some `R`-algebra `S`,
the collection of `S`-linear maps from `V` to `W` forms an `R`-module.
(But not generally an `S`-module, because `S` may be non-commutative.)
-/
section module_of_linear_maps

variables (R : Type*) [comm_ring R] (S : Type*) [ring S] [algebra R S]
  (V : Type*) [add_comm_group V] [module S V]
  (W : Type*) [add_comm_group W] [module S W]

/--
For `r : R`, and `f : V ‚Üí‚Çó[S] W` (where `S` is an `R`-algebra) we define
`(r ‚Ä¢ f) v = f (r ‚Ä¢ v)`.
-/
def linear_map_algebra_has_scalar : has_scalar R (V ‚Üí‚Çó[S] W) :=
{ smul := Œª r f,
  { to_fun := Œª v, f ((algebra_map R S r) ‚Ä¢ v),
    map_add' := Œª x y, by simp [smul_add],
    map_smul' := Œª s v, by simp [smul_smul, algebra.commutes], } }

local attribute [instance] linear_map_algebra_has_scalar

/-- The `R`-module structure on `S`-linear maps, for `S` an `R`-algebra. -/
def linear_map_algebra_module : module R (V ‚Üí‚Çó[S] W) :=
{ one_smul := Œª f, begin ext v, dsimp [(‚Ä¢)], simp, end,
  mul_smul := Œª r r' f,
  begin
    ext v, dsimp [(‚Ä¢)],
    rw [linear_map.map_smul, linear_map.map_smul, linear_map.map_smul, ring_hom.map_mul,
        smul_smul, algebra.commutes],
  end,
  smul_zero := Œª r, by { ext v, dsimp [(‚Ä¢)], refl, },
  smul_add := Œª r f g, by { ext v, dsimp [(‚Ä¢)], simp [linear_map.map_add], },
  zero_smul := Œª f, by { ext v, dsimp [(‚Ä¢)], simp, },
  add_smul := Œª r r' f, by { ext v, dsimp [(‚Ä¢)], simp [add_smul], }, }

local attribute [instance] linear_map_algebra_module

variables {R S V W}
@[simp]
lemma linear_map_algebra_module.smul_apply (c : R) (f : V ‚Üí‚Çó[S] W) (v : V) :
  (c ‚Ä¢ f) v = (c ‚Ä¢ (f v) : module.restrict_scalars R S W) :=
begin
  erw [linear_map.map_smul],
  refl,
end

end module_of_linear_maps
