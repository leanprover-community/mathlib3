/-
Copyright ¬© 2020 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri, Andrew Yang
-/

import ring_theory.adjoin.basic
import algebra.lie.of_associative
import ring_theory.ideal.cotangent
import ring_theory.is_tensor_product

/-!
# Derivations

This file defines derivation. A derivation `D` from the `R`-algebra `A` to the `A`-module `M` is an
`R`-linear map that satisfy the Leibniz rule `D (a * b) = a * D b + D a * b`.

## Main results

- `derivation`: The type of `R`-derivations from `A` to `M`. This has an `A`-module structure.
- `derivation.llcomp`: We may compose linear maps and derivations to obtain a derivation,
  and the composition is bilinear.
- `derivation.lie_algebra`: The `R`-derivations from `A` to `A` form an lie algebra over `R`.
- `derivation_to_square_zero_equiv_lift`: The `R`-derivations from `A` into a square-zero ideal `I`
  of `B` corresponds to the lifts `A ‚Üí‚Çê[R] B` of the map `A ‚Üí‚Çê[R] B ‚ß∏ I`.
- `kaehler_differential`: The module of kaehler differentials. For an `R`-algebra `S`, we provide
  the notation `Œ©[S‚ÅÑR]` for `kaehler_differential R S`.
  Note that the slash is `\textfractionsolidus`.
- `kaehler_differential.D`: The derivation into the module of kaehler differentials.
- `kaehler_differential.span_range_derivation`: The image of `D` spans `Œ©[S‚ÅÑR]` as an `S`-module.
- `kaehler_differential.linear_map_equiv_derivation`:
  The isomorphism `Hom_R(Œ©[S‚ÅÑR], M) ‚âÉ‚Çó[S] Der_R(S, M)`.
- `kaehler_differential.quot_ker_total_equiv`: An alternative description of `Œ©[S‚ÅÑR]` as `S` copies
  of `S` with kernel (`kaehler_differential.ker_total`) generated by the relations:
  1. `dx + dy = d(x + y)`
  2. `x dy + y dx = d(x * y)`
  3. `dr = 0` for `r ‚àà R`
- `kaehler_differential.map`: Given a map between the arrows `R ‚Üí A` and `S ‚Üí B`, we have an
  `A`-linear map `Œ©[A‚ÅÑR] ‚Üí Œ©[B‚ÅÑS]`.

## Future project

- Generalize derivations into bimodules.
- Define a `is_kaehler_differential` predicate.

-/

open algebra
open_locale big_operators

/-- `D : derivation R A M` is an `R`-linear map from `A` to `M` that satisfies the `leibniz`
equality. We also require that `D 1 = 0`. See `derivation.mk'` for a constructor that deduces this
assumption from the Leibniz rule when `M` is cancellative.

TODO: update this when bimodules are defined. -/
@[protect_proj]
structure derivation (R : Type*) (A : Type*) [comm_semiring R] [comm_semiring A]
  [algebra R A] (M : Type*) [add_comm_monoid M] [module A M] [module R M]
  extends A ‚Üí‚Çó[R] M :=
(map_one_eq_zero' : to_linear_map 1 = 0)
(leibniz' (a b : A) : to_linear_map (a * b) = a ‚Ä¢ to_linear_map b + b ‚Ä¢ to_linear_map a)

/-- The `linear_map` underlying a `derivation`. -/
add_decl_doc derivation.to_linear_map

namespace derivation

section

variables {R : Type*} [comm_semiring R]
variables {A : Type*} [comm_semiring A] [algebra R A]
variables {M : Type*} [add_comm_monoid M] [module A M] [module R M]
variables (D : derivation R A M) {D1 D2 : derivation R A M} (r : R) (a b : A)

instance : add_monoid_hom_class (derivation R A M) A M :=
{ coe := Œª D, D.to_fun,
  coe_injective' := Œª D1 D2 h, by { cases D1, cases D2, congr, exact fun_like.coe_injective h },
  map_add := Œª D, D.to_linear_map.map_add',
  map_zero := Œª D, D.to_linear_map.map_zero }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`
directly. -/
instance : has_coe_to_fun (derivation R A M) (Œª _, A ‚Üí M) := ‚ü®Œª D, D.to_linear_map.to_fun‚ü©

-- Not a simp lemma because it can be proved via `coe_fn_coe` + `to_linear_map_eq_coe`
lemma to_fun_eq_coe : D.to_fun = ‚áëD := rfl

instance has_coe_to_linear_map : has_coe (derivation R A M) (A ‚Üí‚Çó[R] M) :=
‚ü®Œª D, D.to_linear_map‚ü©

@[simp] lemma to_linear_map_eq_coe : D.to_linear_map = D := rfl

@[simp] lemma mk_coe (f : A ‚Üí‚Çó[R] M) (h‚ÇÅ h‚ÇÇ) :
  ((‚ü®f, h‚ÇÅ, h‚ÇÇ‚ü© : derivation R A M) : A ‚Üí M) = f := rfl

@[simp, norm_cast]
lemma coe_fn_coe (f : derivation R A M) : ‚áë(f : A ‚Üí‚Çó[R] M) = f := rfl

lemma coe_injective : @function.injective (derivation R A M) (A ‚Üí M) coe_fn :=
fun_like.coe_injective

@[ext] theorem ext (H : ‚àÄ a, D1 a = D2 a) : D1 = D2 :=
fun_like.ext _ _ H

lemma congr_fun (h : D1 = D2) (a : A) : D1 a = D2 a := fun_like.congr_fun h a

protected lemma map_add : D (a + b) = D a + D b := map_add D a b
protected lemma map_zero : D 0 = 0 := map_zero D
@[simp] lemma map_smul : D (r ‚Ä¢ a) = r ‚Ä¢ D a := D.to_linear_map.map_smul r a
@[simp] lemma leibniz : D (a * b) = a ‚Ä¢ D b + b ‚Ä¢ D a := D.leibniz' _ _

lemma map_sum {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí A) : D (‚àë i in s, f i) = ‚àë i in s, D (f i) :=
D.to_linear_map.map_sum

@[simp, priority 900] lemma map_smul_of_tower {S : Type*} [has_smul S A] [has_smul S M]
  [linear_map.compatible_smul A M S R] (D : derivation R A M) (r : S) (a : A) :
  D (r ‚Ä¢ a) = r ‚Ä¢ D a :=
D.to_linear_map.map_smul_of_tower r a

@[simp] lemma map_one_eq_zero : D 1 = 0 := D.map_one_eq_zero'

@[simp] lemma map_algebra_map : D (algebra_map R A r) = 0 :=
by rw [‚Üêmul_one r, ring_hom.map_mul, ring_hom.map_one, ‚Üêsmul_def, map_smul, map_one_eq_zero,
  smul_zero]

@[simp] lemma map_coe_nat (n : ‚Ñï) : D (n : A) = 0 :=
by rw [‚Üê nsmul_one, D.map_smul_of_tower n, map_one_eq_zero, smul_zero]

@[simp] lemma leibniz_pow (n : ‚Ñï) : D (a ^ n) = n ‚Ä¢ a ^ (n - 1) ‚Ä¢ D a :=
begin
  induction n with n ihn,
  { rw [pow_zero, map_one_eq_zero, zero_smul] },
  { rcases (zero_le n).eq_or_lt with (rfl|hpos),
    { rw [pow_one, one_smul, pow_zero, one_smul] },
    { have : a * a ^ (n - 1) = a ^ n, by rw [‚Üê pow_succ, nat.sub_add_cancel hpos],
      simp only [pow_succ, leibniz, ihn, smul_comm a n, smul_smul a, add_smul, this,
        nat.succ_eq_add_one, nat.add_succ_sub_one, add_zero, one_nsmul] } }
end

lemma eq_on_adjoin {s : set A} (h : set.eq_on D1 D2 s) : set.eq_on D1 D2 (adjoin R s) :=
Œª x hx, algebra.adjoin_induction hx h
  (Œª r, (D1.map_algebra_map r).trans (D2.map_algebra_map r).symm)
  (Œª x y hx hy, by simp only [map_add, *])
  (Œª x y hx hy, by simp only [leibniz, *])

/-- If adjoin of a set is the whole algebra, then any two derivations equal on this set are equal
on the whole algebra. -/
lemma ext_of_adjoin_eq_top (s : set A) (hs : adjoin R s = ‚ä§) (h : set.eq_on D1 D2 s) : D1 = D2 :=
ext $ Œª a, eq_on_adjoin h $ hs.symm ‚ñ∏ trivial

/- Data typeclasses -/

instance : has_zero (derivation R A M) :=
‚ü®{ to_linear_map := 0,
   map_one_eq_zero' := rfl,
   leibniz' := Œª a b, by simp only [add_zero, linear_map.zero_apply, smul_zero] }‚ü©

@[simp] lemma coe_zero : ‚áë(0 : derivation R A M) = 0 := rfl
@[simp] lemma coe_zero_linear_map : ‚Üë(0 : derivation R A M) = (0 : A ‚Üí‚Çó[R] M) := rfl
lemma zero_apply (a : A) : (0 : derivation R A M) a = 0 := rfl

instance : has_add (derivation R A M) :=
‚ü®Œª D1 D2,
  { to_linear_map := D1 + D2,
    map_one_eq_zero' := by simp,
    leibniz' := Œª a b, by simp only [leibniz, linear_map.add_apply,
      coe_fn_coe, smul_add, add_add_add_comm] }‚ü©

@[simp] lemma coe_add (D1 D2 : derivation R A M) : ‚áë(D1 + D2) = D1 + D2 := rfl
@[simp] lemma coe_add_linear_map (D1 D2 : derivation R A M) : ‚Üë(D1 + D2) = (D1 + D2 : A ‚Üí‚Çó[R] M) :=
rfl
lemma add_apply : (D1 + D2) a = D1 a + D2 a := rfl

instance : inhabited (derivation R A M) := ‚ü®0‚ü©

section scalar

variables {S : Type*} [monoid S] [distrib_mul_action S M] [smul_comm_class R S M]
  [smul_comm_class S A M]

@[priority 100]
instance : has_smul S (derivation R A M) :=
‚ü®Œª r D,
  { to_linear_map := r ‚Ä¢ D,
    map_one_eq_zero' := by rw [linear_map.smul_apply, coe_fn_coe, D.map_one_eq_zero, smul_zero],
    leibniz' := Œª a b, by simp only [linear_map.smul_apply, coe_fn_coe, leibniz, smul_add,
      smul_comm r] }‚ü©

@[simp] lemma coe_smul (r : S) (D : derivation R A M) : ‚áë(r ‚Ä¢ D) = r ‚Ä¢ D := rfl
@[simp] lemma coe_smul_linear_map (r : S) (D : derivation R A M) :
  ‚Üë(r ‚Ä¢ D) = (r ‚Ä¢ D : A ‚Üí‚Çó[R] M) := rfl
lemma smul_apply (r : S) (D : derivation R A M) : (r ‚Ä¢ D) a = r ‚Ä¢ D a := rfl

instance : add_comm_monoid (derivation R A M) :=
coe_injective.add_comm_monoid _ coe_zero coe_add (Œª _ _, rfl)

/-- `coe_fn` as an `add_monoid_hom`. -/
def coe_fn_add_monoid_hom : derivation R A M ‚Üí+ (A ‚Üí M) :=
{ to_fun := coe_fn, map_zero' := coe_zero, map_add' := coe_add }

@[priority 100]
instance : distrib_mul_action S (derivation R A M) :=
function.injective.distrib_mul_action coe_fn_add_monoid_hom coe_injective coe_smul

instance [distrib_mul_action S·µê·µí·µñ M] [is_central_scalar S M] :
  is_central_scalar S (derivation R A M) :=
{ op_smul_eq_smul := Œª _ _, ext $ Œª _, op_smul_eq_smul _ _}

end scalar

@[priority 100]
instance {S : Type*} [semiring S] [module S M] [smul_comm_class R S M] [smul_comm_class S A M] :
  module S (derivation R A M) :=
function.injective.module S coe_fn_add_monoid_hom coe_injective coe_smul

instance [is_scalar_tower R A M] : is_scalar_tower R A (derivation R A M) :=
‚ü®Œª x y z, ext (Œª a, smul_assoc _ _ _)‚ü©

section push_forward

variables {N : Type*} [add_comm_monoid N] [module A N] [module R N] [is_scalar_tower R A M]
  [is_scalar_tower R A N]
variables (f : M ‚Üí‚Çó[A] N) (e : M ‚âÉ‚Çó[A] N)

/-- We can push forward derivations using linear maps, i.e., the composition of a derivation with a
linear map is a derivation. Furthermore, this operation is linear on the spaces of derivations. -/
def _root_.linear_map.comp_der : derivation R A M ‚Üí‚Çó[R] derivation R A N :=
{ to_fun    := Œª D,
  { to_linear_map := (f : M ‚Üí‚Çó[R] N).comp (D : A ‚Üí‚Çó[R] M),
    map_one_eq_zero' := by simp only [linear_map.comp_apply, coe_fn_coe, map_one_eq_zero, map_zero],
    leibniz'  := Œª a b, by simp only [coe_fn_coe, linear_map.comp_apply, linear_map.map_add,
      leibniz, linear_map.coe_coe_is_scalar_tower, linear_map.map_smul] },
  map_add'  := Œª D‚ÇÅ D‚ÇÇ, by { ext, exact linear_map.map_add _ _ _, },
  map_smul' := Œª r D, by { ext, exact linear_map.map_smul _ _ _, }, }

@[simp] lemma coe_to_linear_map_comp :
  (f.comp_der D : A ‚Üí‚Çó[R] N) = (f : M ‚Üí‚Çó[R] N).comp (D : A ‚Üí‚Çó[R] M) :=
rfl

@[simp] lemma coe_comp :
  (f.comp_der D : A ‚Üí N) = (f : M ‚Üí‚Çó[R] N).comp (D : A ‚Üí‚Çó[R] M) :=
rfl

/-- The composition of a derivation with a linear map as a bilinear map -/
@[simps]
def llcomp : (M ‚Üí‚Çó[A] N) ‚Üí‚Çó[A] derivation R A M ‚Üí‚Çó[R] derivation R A N :=
{ to_fun := Œª f, f.comp_der,
  map_add' := Œª f‚ÇÅ f‚ÇÇ, by { ext, refl },
  map_smul' := Œª r D, by { ext, refl } }

/-- Pushing a derivation foward through a linear equivalence is an equivalence. -/
def _root_.linear_equiv.comp_der : derivation R A M ‚âÉ‚Çó[R] derivation R A N :=
{ inv_fun := e.symm.to_linear_map.comp_der,
  left_inv := Œª D, by { ext a, exact e.symm_apply_apply (D a) },
  right_inv := Œª D, by { ext a, exact e.apply_symm_apply (D a) },
  ..e.to_linear_map.comp_der }


end push_forward

section restrict_scalars

variables {S : Type*} [comm_semiring S]
variables [algebra S A] [module S M] [linear_map.compatible_smul A M R S]

variables (R)

/-- If `A` is both an `R`-algebra and an `S`-algebra; `M` is both an `R`-module and an `S`-module,
then an `S`-derivation `A ‚Üí M` is also an `R`-derivation if it is also `R`-linear. -/
protected
def restrict_scalars (d : derivation S A M) : derivation R A M :=
{ map_one_eq_zero' := d.map_one_eq_zero,
  leibniz' := d.leibniz,
  to_linear_map := d.to_linear_map.restrict_scalars R }

end restrict_scalars

end

section cancel

variables {R : Type*} [comm_semiring R] {A : Type*} [comm_semiring A] [algebra R A]
  {M : Type*} [add_cancel_comm_monoid M] [module R M] [module A M]

/-- Define `derivation R A M` from a linear map when `M` is cancellative by verifying the Leibniz
rule. -/
def mk' (D : A ‚Üí‚Çó[R] M) (h : ‚àÄ a b, D (a * b) = a ‚Ä¢ D b + b ‚Ä¢ D a) : derivation R A M :=
{ to_linear_map := D,
  map_one_eq_zero' := add_right_eq_self.1 $ by simpa only [one_smul, one_mul] using (h 1 1).symm,
  leibniz' := h }

@[simp] lemma coe_mk' (D : A ‚Üí‚Çó[R] M) (h) : ‚áë(mk' D h) = D := rfl
@[simp] lemma coe_mk'_linear_map (D : A ‚Üí‚Çó[R] M) (h) : (mk' D h : A ‚Üí‚Çó[R] M) = D := rfl

end cancel

section

variables {R : Type*} [comm_ring R]
variables {A : Type*} [comm_ring A] [algebra R A]

section

variables {M : Type*} [add_comm_group M] [module A M] [module R M]
variables (D : derivation R A M) {D1 D2 : derivation R A M} (r : R) (a b : A)

protected lemma map_neg : D (-a) = -D a := map_neg D a
protected lemma map_sub : D (a - b) = D a - D b := map_sub D a b

@[simp] lemma map_coe_int (n : ‚Ñ§) : D (n : A) = 0 :=
by rw [‚Üê zsmul_one, D.map_smul_of_tower n, map_one_eq_zero, smul_zero]

lemma leibniz_of_mul_eq_one {a b : A} (h : a * b = 1) : D a = -a^2 ‚Ä¢ D b :=
begin
  rw neg_smul,
  refine eq_neg_of_add_eq_zero_left _,
  calc D a + a ^ 2 ‚Ä¢ D b = a ‚Ä¢ b ‚Ä¢ D a + a ‚Ä¢ a ‚Ä¢ D b : by simp only [smul_smul, h, one_smul, sq]
                     ... = a ‚Ä¢ D (a * b)             : by rw [leibniz, smul_add, add_comm]
                     ... = 0                         : by rw [h, map_one_eq_zero, smul_zero]
end

lemma leibniz_inv_of [invertible a] : D (‚Öüa) = -‚Öüa^2 ‚Ä¢ D a :=
D.leibniz_of_mul_eq_one $ inv_of_mul_self a

lemma leibniz_inv {K : Type*} [field K] [module K M] [algebra R K] (D : derivation R K M) (a : K) :
  D (a‚Åª¬π) = -a‚Åª¬π ^ 2 ‚Ä¢ D a :=
begin
  rcases eq_or_ne a 0 with (rfl|ha),
  { simp },
  { exact D.leibniz_of_mul_eq_one (inv_mul_cancel ha) }
end

instance : has_neg (derivation R A M) :=
‚ü®Œª D, mk' (-D) $  Œª a b,
  by simp only [linear_map.neg_apply, smul_neg, neg_add_rev, leibniz, coe_fn_coe, add_comm]‚ü©

@[simp] lemma coe_neg (D : derivation R A M) : ‚áë(-D) = -D := rfl
@[simp] lemma coe_neg_linear_map (D : derivation R A M) : ‚Üë(-D) = (-D : A ‚Üí‚Çó[R] M) :=
rfl
lemma neg_apply : (-D) a = -D a := rfl

instance : has_sub (derivation R A M) :=
‚ü®Œª D1 D2, mk' (D1 - D2 : A ‚Üí‚Çó[R] M) $ Œª a b,
  by simp only [linear_map.sub_apply, leibniz, coe_fn_coe, smul_sub, add_sub_add_comm]‚ü©

@[simp] lemma coe_sub (D1 D2 : derivation R A M) : ‚áë(D1 - D2) = D1 - D2 := rfl
@[simp] lemma coe_sub_linear_map (D1 D2 : derivation R A M) : ‚Üë(D1 - D2) = (D1 - D2 : A ‚Üí‚Çó[R] M) :=
rfl
lemma sub_apply : (D1 - D2) a = D1 a - D2 a := rfl

instance : add_comm_group (derivation R A M) :=
coe_injective.add_comm_group _ coe_zero coe_add coe_neg coe_sub (Œª _ _, rfl) (Œª _ _, rfl)

end

section lie_structures

/-! # Lie structures -/

variables (D : derivation R A A) {D1 D2 : derivation R A A} (r : R) (a b : A)

/-- The commutator of derivations is again a derivation. -/
instance : has_bracket (derivation R A A) (derivation R A A) :=
‚ü®Œª D1 D2, mk' (‚ÅÖ(D1 : module.End R A), (D2 : module.End R A)‚ÅÜ) $ Œª a b,
  by { simp only [ring.lie_def, map_add, id.smul_eq_mul, linear_map.mul_apply, leibniz, coe_fn_coe,
    linear_map.sub_apply], ring, }‚ü©

@[simp] lemma commutator_coe_linear_map :
  ‚Üë‚ÅÖD1, D2‚ÅÜ = ‚ÅÖ(D1 : module.End R A), (D2 : module.End R A)‚ÅÜ := rfl

lemma commutator_apply : ‚ÅÖD1, D2‚ÅÜ a = D1 (D2 a) - D2 (D1 a) := rfl

instance : lie_ring (derivation R A A) :=
{ add_lie     := Œª d e f, by { ext a, simp only [commutator_apply, add_apply, map_add], ring, },
  lie_add     := Œª d e f, by { ext a, simp only [commutator_apply, add_apply, map_add], ring, },
  lie_self    := Œª d, by { ext a, simp only [commutator_apply, add_apply, map_add], ring_nf, },
  leibniz_lie := Œª d e f,
    by { ext a, simp only [commutator_apply, add_apply, sub_apply, map_sub], ring, } }

instance : lie_algebra R (derivation R A A) :=
{ lie_smul := Œª r d e, by { ext a, simp only [commutator_apply, map_smul, smul_sub, smul_apply]},
  ..derivation.module }

end lie_structures

end

end derivation

section to_square_zero

universes u v w

variables {R : Type u} {A : Type v} {B : Type w} [comm_semiring R] [comm_semiring A] [comm_ring B]
variables [algebra R A] [algebra R B] (I : ideal B) (hI : I ^ 2 = ‚ä•)

/-- If `f‚ÇÅ f‚ÇÇ : A ‚Üí‚Çê[R] B` are two lifts of the same `A ‚Üí‚Çê[R] B ‚ß∏ I`,
  we may define a map `f‚ÇÅ - f‚ÇÇ : A ‚Üí‚Çó[R] I`. -/
def diff_to_ideal_of_quotient_comp_eq (f‚ÇÅ f‚ÇÇ : A ‚Üí‚Çê[R] B)
  (e : (ideal.quotient.mk‚Çê R I).comp f‚ÇÅ = (ideal.quotient.mk‚Çê R I).comp f‚ÇÇ) :
  A ‚Üí‚Çó[R] I :=
linear_map.cod_restrict (I.restrict_scalars _) (f‚ÇÅ.to_linear_map - f‚ÇÇ.to_linear_map)
begin
  intro x,
  change f‚ÇÅ x - f‚ÇÇ x ‚àà I,
  rw [‚Üê ideal.quotient.eq, ‚Üê ideal.quotient.mk‚Çê_eq_mk R, ‚Üê alg_hom.comp_apply, e],
  refl,
end

@[simp]
lemma diff_to_ideal_of_quotient_comp_eq_apply (f‚ÇÅ f‚ÇÇ : A ‚Üí‚Çê[R] B)
  (e : (ideal.quotient.mk‚Çê R I).comp f‚ÇÅ = (ideal.quotient.mk‚Çê R I).comp f‚ÇÇ) (x : A) :
  ((diff_to_ideal_of_quotient_comp_eq I f‚ÇÅ f‚ÇÇ e) x : B) = f‚ÇÅ x - f‚ÇÇ x :=
rfl

variables [algebra A B] [is_scalar_tower R A B]

include hI

/-- Given a tower of algebras `R ‚Üí A ‚Üí B`, and a square-zero `I : ideal B`, each lift `A ‚Üí‚Çê[R] B`
of the canonical map `A ‚Üí‚Çê[R] B ‚ß∏ I` corresponds to a `R`-derivation from `A` to `I`. -/
def derivation_to_square_zero_of_lift
  (f : A ‚Üí‚Çê[R] B) (e : (ideal.quotient.mk‚Çê R I).comp f = is_scalar_tower.to_alg_hom R A (B ‚ß∏ I)) :
  derivation R A I :=
begin
  refine
  { map_one_eq_zero' := _,
    leibniz' := _,
    ..(diff_to_ideal_of_quotient_comp_eq I f (is_scalar_tower.to_alg_hom R A B) _) },
  { rw e, ext, refl },
  { ext, change f 1 - algebra_map A B 1 = 0, rw [map_one, map_one, sub_self] },
  { intros x y,
    let F := diff_to_ideal_of_quotient_comp_eq I f (is_scalar_tower.to_alg_hom R A B)
      (by { rw e, ext, refl }),
    have : (f x - algebra_map A B x) * (f y - algebra_map A B y) = 0,
    { rw [‚Üê ideal.mem_bot, ‚Üê hI, pow_two],
      convert (ideal.mul_mem_mul (F x).2 (F y).2) using 1 },
    ext,
    dsimp only [submodule.coe_add, submodule.coe_mk, linear_map.coe_mk,
      diff_to_ideal_of_quotient_comp_eq_apply, submodule.coe_smul_of_tower,
      is_scalar_tower.coe_to_alg_hom', linear_map.to_fun_eq_coe],
    simp only [map_mul, sub_mul, mul_sub, algebra.smul_def] at ‚ä¢ this,
    rw [sub_eq_iff_eq_add, sub_eq_iff_eq_add] at this,
    rw this,
    ring }
end

lemma derivation_to_square_zero_of_lift_apply (f : A ‚Üí‚Çê[R] B)
  (e : (ideal.quotient.mk‚Çê R I).comp f = is_scalar_tower.to_alg_hom R A (B ‚ß∏ I))
  (x : A) : (derivation_to_square_zero_of_lift I hI f e x : B) = f x - algebra_map A B x := rfl

/-- Given a tower of algebras `R ‚Üí A ‚Üí B`, and a square-zero `I : ideal B`, each `R`-derivation
from `A` to `I` corresponds to a lift `A ‚Üí‚Çê[R] B` of the canonical map `A ‚Üí‚Çê[R] B ‚ß∏ I`. -/
@[simps {attrs := []}]
def lift_of_derivation_to_square_zero (f : derivation R A I) :
  A ‚Üí‚Çê[R] B :=
{ to_fun := Œª x, f x + algebra_map A B x,
  map_one' := by rw [map_one, f.map_one_eq_zero, submodule.coe_zero, zero_add],
  map_mul' := Œª x y, begin
    have : (f x : B) * (f y) = 0,
    { rw [‚Üê ideal.mem_bot, ‚Üê hI, pow_two], convert (ideal.mul_mem_mul (f x).2 (f y).2) using 1 },
    simp only [map_mul, f.leibniz, add_mul, mul_add, submodule.coe_add,
      submodule.coe_smul_of_tower, algebra.smul_def, this],
    ring
  end,
  commutes' := Œª r,
    by simp only [derivation.map_algebra_map, eq_self_iff_true, zero_add, submodule.coe_zero,
      ‚Üêis_scalar_tower.algebra_map_apply R A B r],
  map_zero' := ((I.restrict_scalars R).subtype.comp f.to_linear_map +
    (is_scalar_tower.to_alg_hom R A B).to_linear_map).map_zero,
  ..((I.restrict_scalars R).subtype.comp f.to_linear_map +
    (is_scalar_tower.to_alg_hom R A B).to_linear_map : A ‚Üí‚Çó[R] B) }

@[simp] lemma lift_of_derivation_to_square_zero_mk_apply (d : derivation R A I) (x : A) :
  ideal.quotient.mk I (lift_of_derivation_to_square_zero I hI d x) = algebra_map A (B ‚ß∏ I) x :=
by { rw [lift_of_derivation_to_square_zero_apply, map_add,
  ideal.quotient.eq_zero_iff_mem.mpr (d x).prop, zero_add], refl }

/-- Given a tower of algebras `R ‚Üí A ‚Üí B`, and a square-zero `I : ideal B`,
there is a 1-1 correspondance between `R`-derivations from `A` to `I` and
lifts `A ‚Üí‚Çê[R] B` of the canonical map `A ‚Üí‚Çê[R] B ‚ß∏ I`. -/
@[simps]
def derivation_to_square_zero_equiv_lift :
  derivation R A I ‚âÉ
    { f : A ‚Üí‚Çê[R] B // (ideal.quotient.mk‚Çê R I).comp f = is_scalar_tower.to_alg_hom R A (B ‚ß∏ I) } :=
begin
  refine ‚ü®Œª d, ‚ü®lift_of_derivation_to_square_zero I hI d, _‚ü©, Œª f,
    (derivation_to_square_zero_of_lift I hI f.1 f.2 : _), _, _‚ü©,
  { ext x, exact lift_of_derivation_to_square_zero_mk_apply I hI d x },
  { intro d, ext x, exact add_sub_cancel (d x : B) (algebra_map A B x) },
  { rintro ‚ü®f, hf‚ü©, ext x,  exact sub_add_cancel (f x) (algebra_map A B x) }
end

end to_square_zero

section kaehler_differential

open_locale tensor_product

variables (R S : Type*) [comm_ring R] [comm_ring S] [algebra R S]

/-- The kernel of the multiplication map `S ‚äó[R] S ‚Üí‚Çê[R] S`. -/
abbreviation kaehler_differential.ideal : ideal (S ‚äó[R] S) :=
ring_hom.ker (tensor_product.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S)

variable {S}

lemma kaehler_differential.one_smul_sub_smul_one_mem_ideal (a : S) :
  (1 : S) ‚äó‚Çú[R] a - a ‚äó‚Çú[R] (1 : S) ‚àà kaehler_differential.ideal R S :=
by simp [ring_hom.mem_ker]

variables {R}

variables {M : Type*} [add_comm_group M] [module R M] [module S M] [is_scalar_tower R S M]

/-- For a `R`-derivation `S ‚Üí M`, this is the map `S ‚äó[R] S ‚Üí‚Çó[S] M` sending `s ‚äó‚Çú t ‚Ü¶ s ‚Ä¢ D t`. -/
def derivation.tensor_product_to (D : derivation R S M) : S ‚äó[R] S ‚Üí‚Çó[S] M :=
tensor_product.algebra_tensor_module.lift ((linear_map.lsmul S (S ‚Üí‚Çó[R] M)).flip D.to_linear_map)

lemma derivation.tensor_product_to_tmul (D : derivation R S M) (s t : S) :
  D.tensor_product_to (s ‚äó‚Çú t) = s ‚Ä¢ D t :=
rfl

lemma derivation.tensor_product_to_mul (D : derivation R S M) (x y : S ‚äó[R] S) :
  D.tensor_product_to (x * y) = tensor_product.lmul' R x ‚Ä¢ D.tensor_product_to y +
    tensor_product.lmul' R y ‚Ä¢ D.tensor_product_to x :=
begin
  apply tensor_product.induction_on x,
  { rw [zero_mul, map_zero, map_zero, zero_smul, smul_zero, add_zero] },
  swap, { rintros, simp only [add_mul, map_add, add_smul, *, smul_add], rw add_add_add_comm },
  intros x‚ÇÅ x‚ÇÇ,
  apply tensor_product.induction_on y,
  { rw [mul_zero, map_zero, map_zero, zero_smul, smul_zero, add_zero] },
  swap, { rintros, simp only [mul_add, map_add, add_smul, *, smul_add], rw add_add_add_comm },
  intros x y,
  simp only [tensor_product.tmul_mul_tmul, derivation.tensor_product_to,
    tensor_product.algebra_tensor_module.lift_apply, tensor_product.lift.tmul',
    tensor_product.lmul'_apply_tmul],
  dsimp,
  rw D.leibniz,
  simp only [smul_smul, smul_add, mul_comm (x * y) x‚ÇÅ, mul_right_comm x‚ÇÅ x‚ÇÇ, ‚Üê mul_assoc],
end

variables (R S)

/-- The kernel of `S ‚äó[R] S ‚Üí‚Çê[R] S` is generated by `1 ‚äó s - s ‚äó 1` as a `S`-module. -/
lemma kaehler_differential.submodule_span_range_eq_ideal :
  submodule.span S (set.range $ Œª s : S, (1 : S) ‚äó‚Çú[R] s - s ‚äó‚Çú[R] (1 : S)) =
    (kaehler_differential.ideal R S).restrict_scalars S :=
begin
  apply le_antisymm,
  { rw submodule.span_le,
    rintros _ ‚ü®s, rfl‚ü©,
    exact kaehler_differential.one_smul_sub_smul_one_mem_ideal _ _ },
  { rintros x (hx : _ = _),
    have : x - (tensor_product.lmul' R x) ‚äó‚Çú[R] (1 : S)   = x,
    { rw [hx, tensor_product.zero_tmul, sub_zero] },
    rw ‚Üê this,
    clear this hx,
    apply tensor_product.induction_on x; clear x,
    { rw [map_zero, tensor_product.zero_tmul, sub_zero], exact zero_mem _ },
    { intros x y,
      convert_to x ‚Ä¢ (1 ‚äó‚Çú y - y ‚äó‚Çú 1) ‚àà _,
      { rw [tensor_product.lmul'_apply_tmul, smul_sub, tensor_product.smul_tmul',
          tensor_product.smul_tmul', smul_eq_mul, smul_eq_mul, mul_one] },
      { refine submodule.smul_mem _ x _,
        apply submodule.subset_span,
        exact set.mem_range_self y } },
    { intros x y hx hy,
      rw [map_add, tensor_product.add_tmul, ‚Üê sub_add_sub_comm],
      exact add_mem hx hy } }
end

lemma kaehler_differential.span_range_eq_ideal :
  ideal.span (set.range $ Œª s : S, (1 : S) ‚äó‚Çú[R] s - s ‚äó‚Çú[R] (1 : S)) =
    kaehler_differential.ideal R S :=
begin
  apply le_antisymm,
  { rw ideal.span_le,
    rintros _ ‚ü®s, rfl‚ü©,
    exact kaehler_differential.one_smul_sub_smul_one_mem_ideal _ _ },
  { change (kaehler_differential.ideal R S).restrict_scalars S ‚â§ (ideal.span _).restrict_scalars S,
    rw [‚Üê kaehler_differential.submodule_span_range_eq_ideal, ideal.span],
    conv_rhs { rw ‚Üê submodule.span_span_of_tower S },
    exact submodule.subset_span }
end

/--
The module of K√§hler differentials (Kahler differentials, Kaehler differentials).
This is implemented as `I / I ^ 2` with `I` the kernel of the multiplication map `S ‚äó[R] S ‚Üí‚Çê[R] S`.
To view elements as a linear combination of the form `s ‚Ä¢ D s'`, use
`kaehler_differential.tensor_product_to_surjective` and `derivation.tensor_product_to_tmul`.

We also provide the notation `Œ©[S‚ÅÑR]` for `kaehler_differential R S`.
Note that the slash is `\textfractionsolidus`.
-/
@[derive [add_comm_group, module (S ‚äó[R] S)]]
def kaehler_differential : Type* := (kaehler_differential.ideal R S).cotangent

notation `Œ©[`:100 S `‚ÅÑ`:0 R `]`:0 := kaehler_differential R S

instance : nonempty Œ©[S‚ÅÑR] := ‚ü®0‚ü©

instance kaehler_differential.module' {R' : Type*} [comm_ring R'] [algebra R' S] :
  module R' Œ©[S‚ÅÑR] :=
(module.comp_hom (kaehler_differential.ideal R S).cotangent (algebra_map R' S) : _)

instance : is_scalar_tower S (S ‚äó[R] S) Œ©[S‚ÅÑR] :=
ideal.cotangent.is_scalar_tower _

instance kaehler_differential.is_scalar_tower_of_tower {R‚ÇÅ R‚ÇÇ : Type*} [comm_ring R‚ÇÅ] [comm_ring R‚ÇÇ]
  [algebra R‚ÇÅ S] [algebra R‚ÇÇ S] [algebra R‚ÇÅ R‚ÇÇ] [is_scalar_tower R‚ÇÅ R‚ÇÇ S] :
  is_scalar_tower R‚ÇÅ R‚ÇÇ Œ©[S‚ÅÑR] :=
begin
  convert restrict_scalars.is_scalar_tower R‚ÇÅ R‚ÇÇ Œ©[S‚ÅÑR] using 1,
  ext x m,
  show algebra_map R‚ÇÅ S x ‚Ä¢ m = algebra_map R‚ÇÇ S (algebra_map R‚ÇÅ R‚ÇÇ x) ‚Ä¢ m,
  rw ‚Üê is_scalar_tower.algebra_map_apply,
end

instance kaehler_differential.is_scalar_tower' :
  is_scalar_tower R (S ‚äó[R] S) Œ©[S‚ÅÑR] :=
begin
  convert restrict_scalars.is_scalar_tower R (S ‚äó[R] S) Œ©[S‚ÅÑR] using 1,
  ext x m,
  show algebra_map R S x ‚Ä¢ m = algebra_map R (S ‚äó[R] S) x ‚Ä¢ m,
  simp_rw [is_scalar_tower.algebra_map_apply R S (S ‚äó[R] S), is_scalar_tower.algebra_map_smul]
end

/-- The quotient map `I ‚Üí Œ©[S‚ÅÑR]` with `I` being the kernel of `S ‚äó[R] S ‚Üí S`. -/
def kaehler_differential.from_ideal : kaehler_differential.ideal R S ‚Üí‚Çó[S ‚äó[R] S] Œ©[S‚ÅÑR] :=
(kaehler_differential.ideal R S).to_cotangent

/-- (Implementation) The underlying linear map of the derivation into `Œ©[S‚ÅÑR]`. -/
def kaehler_differential.D_linear_map : S ‚Üí‚Çó[R] Œ©[S‚ÅÑR] :=
((kaehler_differential.from_ideal R S).restrict_scalars R).comp
  ((tensor_product.include_right.to_linear_map - tensor_product.include_left.to_linear_map :
    S ‚Üí‚Çó[R] S ‚äó[R] S).cod_restrict ((kaehler_differential.ideal R S).restrict_scalars R)
      (kaehler_differential.one_smul_sub_smul_one_mem_ideal R) : _ ‚Üí‚Çó[R] _)

lemma kaehler_differential.D_linear_map_apply (s : S) :
  kaehler_differential.D_linear_map R S s = (kaehler_differential.ideal R S).to_cotangent
    ‚ü®1 ‚äó‚Çú s - s ‚äó‚Çú 1, kaehler_differential.one_smul_sub_smul_one_mem_ideal R s‚ü© :=
rfl

/-- The universal derivation into `Œ©[S‚ÅÑR]`. -/
def kaehler_differential.D : derivation R S Œ©[S‚ÅÑR] :=
{ map_one_eq_zero' := begin
    dsimp [kaehler_differential.D_linear_map_apply],
    rw [ideal.to_cotangent_eq_zero, subtype.coe_mk, sub_self],
    exact zero_mem _
  end,
  leibniz' := Œª a b, begin
    dsimp [kaehler_differential.D_linear_map_apply],
    rw [‚Üê linear_map.map_smul_of_tower, ‚Üê linear_map.map_smul_of_tower, ‚Üê map_add,
      ideal.to_cotangent_eq, pow_two],
    convert submodule.mul_mem_mul (kaehler_differential.one_smul_sub_smul_one_mem_ideal R a : _)
      (kaehler_differential.one_smul_sub_smul_one_mem_ideal R b : _) using 1,
    simp only [add_subgroup_class.coe_sub, submodule.coe_add, submodule.coe_mk,
      tensor_product.tmul_mul_tmul, mul_sub, sub_mul, mul_comm b,
      submodule.coe_smul_of_tower, smul_sub, tensor_product.smul_tmul', smul_eq_mul, mul_one],
    ring_nf,
  end,
  ..(kaehler_differential.D_linear_map R S) }

lemma kaehler_differential.D_apply (s : S) :
  kaehler_differential.D R S s = (kaehler_differential.ideal R S).to_cotangent
    ‚ü®1 ‚äó‚Çú s - s ‚äó‚Çú 1, kaehler_differential.one_smul_sub_smul_one_mem_ideal R s‚ü© :=
rfl

lemma kaehler_differential.span_range_derivation :
  submodule.span S (set.range $ kaehler_differential.D R S) = ‚ä§ :=
begin
  rw _root_.eq_top_iff,
  rintros x -,
  obtain ‚ü®‚ü®x, hx‚ü©, rfl‚ü© := ideal.to_cotangent_surjective _ x,
  have : x ‚àà (kaehler_differential.ideal R S).restrict_scalars S := hx,
  rw ‚Üê kaehler_differential.submodule_span_range_eq_ideal at this,
  suffices : ‚àÉ hx, (kaehler_differential.ideal R S).to_cotangent ‚ü®x, hx‚ü© ‚àà
    submodule.span S (set.range $ kaehler_differential.D R S),
  { exact this.some_spec },
  apply submodule.span_induction this,
  { rintros _ ‚ü®x, rfl‚ü©,
    refine ‚ü®kaehler_differential.one_smul_sub_smul_one_mem_ideal R x, _‚ü©,
    apply submodule.subset_span,
    exact ‚ü®x, kaehler_differential.D_linear_map_apply R S x‚ü© },
  { exact ‚ü®zero_mem _, submodule.zero_mem _‚ü© },
  { rintros x y ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©, exact ‚ü®add_mem hx‚ÇÅ hy‚ÇÅ, submodule.add_mem _ hx‚ÇÇ hy‚ÇÇ‚ü© },
  { rintros r x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©, exact ‚ü®((kaehler_differential.ideal R S).restrict_scalars S).smul_mem
      r hx‚ÇÅ, submodule.smul_mem _ r hx‚ÇÇ‚ü© }
end

variables {R S}

/-- The linear map from `Œ©[S‚ÅÑR]`, associated with a derivation. -/
def derivation.lift_kaehler_differential (D : derivation R S M) : Œ©[S‚ÅÑR] ‚Üí‚Çó[S] M :=
begin
  refine ((kaehler_differential.ideal R S ‚Ä¢ ‚ä§ :
    submodule (S ‚äó[R] S) (kaehler_differential.ideal R S)).restrict_scalars S).liftq _ _,
  { exact D.tensor_product_to.comp ((kaehler_differential.ideal R S).subtype.restrict_scalars S) },
  { intros x hx,
    change _ = _,
    apply submodule.smul_induction_on hx; clear hx x,
    { rintros x (hx : _ = _) ‚ü®y, hy : _ = _‚ü© -,
      dsimp,
      rw [derivation.tensor_product_to_mul, hx, hy, zero_smul, zero_smul, zero_add] },
    { intros x y ex ey, rw [map_add, ex, ey, zero_add] } }
end

lemma derivation.lift_kaehler_differential_apply (D : derivation R S M) (x) :
  D.lift_kaehler_differential
    ((kaehler_differential.ideal R S).to_cotangent x) = D.tensor_product_to x :=
rfl

lemma derivation.lift_kaehler_differential_comp (D : derivation R S M) :
  D.lift_kaehler_differential.comp_der (kaehler_differential.D R S) = D :=
begin
  ext a,
  dsimp [kaehler_differential.D_apply],
  refine (D.lift_kaehler_differential_apply _).trans _,
  rw [subtype.coe_mk, map_sub, derivation.tensor_product_to_tmul,
    derivation.tensor_product_to_tmul, one_smul, D.map_one_eq_zero, smul_zero, sub_zero],
end

@[simp] lemma derivation.lift_kaehler_differential_comp_D (D' : derivation R S M) (x : S) :
  D'.lift_kaehler_differential (kaehler_differential.D R S x) = D' x :=
derivation.congr_fun D'.lift_kaehler_differential_comp x

@[ext]
lemma derivation.lift_kaehler_differential_unique
  (f f' : Œ©[S‚ÅÑR] ‚Üí‚Çó[S] M)
  (hf : f.comp_der (kaehler_differential.D R S) =
    f'.comp_der (kaehler_differential.D R S)) :
  f = f' :=
begin
  apply linear_map.ext,
  intro x,
  have : x ‚àà submodule.span S (set.range $ kaehler_differential.D R S),
  { rw kaehler_differential.span_range_derivation, trivial },
  apply submodule.span_induction this,
  { rintros _ ‚ü®x, rfl‚ü©, exact congr_arg (Œª D : derivation R S M, D x) hf },
  { rw [map_zero, map_zero] },
  { intros x y hx hy, rw [map_add, map_add, hx, hy] },
  { intros a x e, rw [map_smul, map_smul, e] }
end

variables (R S)

lemma derivation.lift_kaehler_differential_D :
  (kaehler_differential.D R S).lift_kaehler_differential = linear_map.id :=
derivation.lift_kaehler_differential_unique _ _
  (kaehler_differential.D R S).lift_kaehler_differential_comp

variables {R S}

lemma kaehler_differential.D_tensor_product_to (x : kaehler_differential.ideal R S) :
  (kaehler_differential.D R S).tensor_product_to x =
    (kaehler_differential.ideal R S).to_cotangent x :=
begin
  rw [‚Üê derivation.lift_kaehler_differential_apply, derivation.lift_kaehler_differential_D],
  refl,
end

variables (R S)

lemma kaehler_differential.tensor_product_to_surjective :
  function.surjective (kaehler_differential.D R S).tensor_product_to :=
begin
  intro x, obtain ‚ü®x, rfl‚ü© := (kaehler_differential.ideal R S).to_cotangent_surjective x,
  exact ‚ü®x, kaehler_differential.D_tensor_product_to x‚ü©
end

/-- The `S`-linear maps from `Œ©[S‚ÅÑR]` to `M` are (`S`-linearly) equivalent to `R`-derivations
from `S` to `M`.  -/
def kaehler_differential.linear_map_equiv_derivation : (Œ©[S‚ÅÑR] ‚Üí‚Çó[S] M) ‚âÉ‚Çó[S] derivation R S M :=
{ inv_fun := derivation.lift_kaehler_differential,
  left_inv := Œª f, derivation.lift_kaehler_differential_unique _ _
    (derivation.lift_kaehler_differential_comp _),
  right_inv := derivation.lift_kaehler_differential_comp,
  ..(derivation.llcomp.flip $ kaehler_differential.D R S) }

/-- The quotient ring of `S ‚äó S ‚ß∏ J ^ 2` by `Œ©[S‚ÅÑR]` is isomorphic to `S`. -/
def kaehler_differential.quotient_cotangent_ideal_ring_equiv :
  (S ‚äó S ‚ß∏ kaehler_differential.ideal R S ^ 2) ‚ß∏
    (kaehler_differential.ideal R S).cotangent_ideal ‚âÉ+* S :=
begin
  have : function.right_inverse tensor_product.include_left
    (‚Üë(tensor_product.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S) : S ‚äó[R] S ‚Üí+* S),
  { intro x, rw [alg_hom.coe_to_ring_hom, ‚Üê alg_hom.comp_apply,
      tensor_product.lmul'_comp_include_left], refl },
  refine (ideal.quot_cotangent _).trans _,
  refine (ideal.quot_equiv_of_eq _).trans (ring_hom.quotient_ker_equiv_of_right_inverse this),
  ext, refl,
end

/-- The quotient ring of `S ‚äó S ‚ß∏ J ^ 2` by `Œ©[S‚ÅÑR]` is isomorphic to `S` as an `S`-algebra. -/
def kaehler_differential.quotient_cotangent_ideal :
  ((S ‚äó S ‚ß∏ kaehler_differential.ideal R S ^ 2) ‚ß∏
    (kaehler_differential.ideal R S).cotangent_ideal) ‚âÉ‚Çê[S] S :=
{ commutes' := (kaehler_differential.quotient_cotangent_ideal_ring_equiv R S).apply_symm_apply,
  ..kaehler_differential.quotient_cotangent_ideal_ring_equiv R S }

lemma kaehler_differential.End_equiv_aux (f : S ‚Üí‚Çê[R] S ‚äó S ‚ß∏ kaehler_differential.ideal R S ^ 2) :
  (ideal.quotient.mk‚Çê R (kaehler_differential.ideal R S).cotangent_ideal).comp f =
    is_scalar_tower.to_alg_hom R S _ ‚Üî
  (tensor_product.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S).ker_square_lift.comp f = alg_hom.id R S :=
begin
  rw [alg_hom.ext_iff, alg_hom.ext_iff],
  apply forall_congr,
  intro x,
  have e‚ÇÅ : (tensor_product.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S).ker_square_lift (f x) =
    kaehler_differential.quotient_cotangent_ideal_ring_equiv R S
      (ideal.quotient.mk (kaehler_differential.ideal R S).cotangent_ideal $ f x),
  { generalize : f x = y, obtain ‚ü®y, rfl‚ü© := ideal.quotient.mk_surjective y, refl },
  have e‚ÇÇ : x = kaehler_differential.quotient_cotangent_ideal_ring_equiv
    R S (is_scalar_tower.to_alg_hom R S _ x),
  { exact (mul_one x).symm },
  split,
  { intro e,
    exact (e‚ÇÅ.trans (@ring_equiv.congr_arg _ _ _ _ _ _
      (kaehler_differential.quotient_cotangent_ideal_ring_equiv R S) _ _ e)).trans e‚ÇÇ.symm },
  { intro e, apply (kaehler_differential.quotient_cotangent_ideal_ring_equiv R S).injective,
    exact e‚ÇÅ.symm.trans (e.trans e‚ÇÇ) }
end

/-- Derivations into `Œ©[S‚ÅÑR]` is equivalent to derivations
into `(kaehler_differential.ideal R S).cotangent_ideal` -/
-- This has type
-- `derivation R S Œ©[ S / R ] ‚âÉ‚Çó[R] derivation R S (kaehler_differential.ideal R S).cotangent_ideal`
-- But lean times-out if this is given explicitly.
noncomputable
def kaehler_differential.End_equiv_derivation' :=
@linear_equiv.comp_der R _ _ _ _ Œ©[S‚ÅÑR] _ _ _ _ _ _ _ _ _
  ((kaehler_differential.ideal R S).cotangent_equiv_ideal.restrict_scalars S)

/-- (Implementation) An `equiv` version of `kaehler_differential.End_equiv_aux`.
Used in `kaehler_differential.End_equiv`. -/
def kaehler_differential.End_equiv_aux_equiv :
  {f // (ideal.quotient.mk‚Çê R (kaehler_differential.ideal R S).cotangent_ideal).comp f =
    is_scalar_tower.to_alg_hom R S _ } ‚âÉ
  { f // (tensor_product.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S).ker_square_lift.comp f = alg_hom.id R S } :=
(equiv.refl _).subtype_equiv (kaehler_differential.End_equiv_aux R S)

/--
The endomorphisms of `Œ©[S‚ÅÑR]` corresponds to sections of the surjection `S ‚äó[R] S ‚ß∏ J ^ 2 ‚Üí‚Çê[R] S`,
with `J` being the kernel of the multiplication map `S ‚äó[R] S ‚Üí‚Çê[R] S`.
-/
noncomputable
def kaehler_differential.End_equiv :
  module.End S Œ©[S‚ÅÑR] ‚âÉ
    { f // (tensor_product.lmul' R : S ‚äó[R] S ‚Üí‚Çê[R] S).ker_square_lift.comp f = alg_hom.id R S } :=
(kaehler_differential.linear_map_equiv_derivation R S).to_equiv.trans $
  (kaehler_differential.End_equiv_derivation' R S).to_equiv.trans $
  (derivation_to_square_zero_equiv_lift
  (kaehler_differential.ideal R S).cotangent_ideal
  (kaehler_differential.ideal R S).cotangent_ideal_square).trans $
  kaehler_differential.End_equiv_aux_equiv R S

section presentation

open kaehler_differential (D)
open finsupp (single)

/-- The `S`-submodule of `S ‚Üí‚ÇÄ S` (the direct sum of copies of `S` indexed by `S`) generated by
the relations:
1. `dx + dy = d(x + y)`
2. `x dy + y dx = d(x * y)`
3. `dr = 0` for `r ‚àà R`
where `db` is the unit in the copy of `S` with index `b`.

This is the kernel of the surjection `finsupp.total S Œ©[S‚ÅÑR] S (kaehler_differential.D R S)`.
See `kaehler_differential.ker_total_eq` and `kaehler_differential.total_surjective`.
-/
noncomputable
def kaehler_differential.ker_total : submodule S (S ‚Üí‚ÇÄ S) :=
submodule.span S
  ((set.range (Œª (x : S √ó S), single x.1 1 + single x.2 1 - single (x.1 + x.2) 1)) ‚à™
  (set.range (Œª (x : S √ó S), single x.2 x.1 + single x.1 x.2 - single (x.1 * x.2) 1)) ‚à™
  (set.range (Œª x : R, single (algebra_map R S x) 1)))

local notation x `ùñ£` y := (kaehler_differential.ker_total R S).mkq (single y x)

lemma kaehler_differential.ker_total_mkq_single_add (x y z) :
  (z ùñ£ (x + y)) = (z ùñ£ x) + (z ùñ£ y) :=
begin
  rw [‚Üê map_add, eq_comm, ‚Üê sub_eq_zero, ‚Üê map_sub, submodule.mkq_apply,
    submodule.quotient.mk_eq_zero],
  simp_rw [‚Üê finsupp.smul_single_one _ z, ‚Üê smul_add, ‚Üê smul_sub],
  exact submodule.smul_mem _ _ (submodule.subset_span (or.inl $ or.inl $ ‚ü®‚ü®_, _‚ü©, rfl‚ü©)),
end

lemma kaehler_differential.ker_total_mkq_single_mul (x y z) :
  (z ùñ£ (x * y)) = ((z * x) ùñ£ y) + ((z * y) ùñ£ x) :=
begin
  rw [‚Üê map_add, eq_comm, ‚Üê sub_eq_zero, ‚Üê map_sub, submodule.mkq_apply,
    submodule.quotient.mk_eq_zero],
  simp_rw [‚Üê finsupp.smul_single_one _ z, ‚Üê @smul_eq_mul _ _ z,
    ‚Üê finsupp.smul_single, ‚Üê smul_add, ‚Üê smul_sub],
  exact submodule.smul_mem _ _ (submodule.subset_span (or.inl $ or.inr $ ‚ü®‚ü®_, _‚ü©, rfl‚ü©)),
end

lemma kaehler_differential.ker_total_mkq_single_algebra_map (x y) :
  (y ùñ£ (algebra_map R S x)) = 0 :=
begin
  rw [submodule.mkq_apply, submodule.quotient.mk_eq_zero, ‚Üê finsupp.smul_single_one _ y],
  exact submodule.smul_mem _ _ (submodule.subset_span (or.inr $ ‚ü®_, rfl‚ü©)),
end

lemma kaehler_differential.ker_total_mkq_single_algebra_map_one (x) :
  (x ùñ£ 1) = 0 :=
begin
  rw [‚Üê (algebra_map R S).map_one, kaehler_differential.ker_total_mkq_single_algebra_map],
end

lemma kaehler_differential.ker_total_mkq_single_smul (r : R) (x y) :
  (y ùñ£ (r ‚Ä¢ x)) = r ‚Ä¢ (y ùñ£ x) :=
begin
  rw [algebra.smul_def, kaehler_differential.ker_total_mkq_single_mul,
    kaehler_differential.ker_total_mkq_single_algebra_map, add_zero,
    ‚Üê linear_map.map_smul_of_tower, finsupp.smul_single, mul_comm, algebra.smul_def],
end

/-- The (universal) derivation into `(S ‚Üí‚ÇÄ S) ‚ß∏ kaehler_differential.ker_total R S`. -/
noncomputable
def kaehler_differential.derivation_quot_ker_total :
  derivation R S ((S ‚Üí‚ÇÄ S) ‚ß∏ kaehler_differential.ker_total R S) :=
{ to_fun := Œª x, 1 ùñ£ x,
  map_add' := Œª x y, kaehler_differential.ker_total_mkq_single_add _ _ _ _ _,
  map_smul' := Œª r s, kaehler_differential.ker_total_mkq_single_smul _ _ _ _ _,
  map_one_eq_zero' := kaehler_differential.ker_total_mkq_single_algebra_map_one _ _ _,
  leibniz' := Œª a b, (kaehler_differential.ker_total_mkq_single_mul _ _ _ _ _).trans
    (by { simp_rw [‚Üê finsupp.smul_single_one _ (1 * _ : S)], dsimp, simp }) }

lemma kaehler_differential.derivation_quot_ker_total_apply (x) :
  kaehler_differential.derivation_quot_ker_total R S x = (1 ùñ£ x) := rfl

lemma kaehler_differential.derivation_quot_ker_total_lift_comp_total :
  (kaehler_differential.derivation_quot_ker_total R S).lift_kaehler_differential.comp
    (finsupp.total S Œ©[S‚ÅÑR] S (kaehler_differential.D R S)) = submodule.mkq _ :=
begin
  apply finsupp.lhom_ext,
  intros a b,
  conv_rhs { rw [‚Üê finsupp.smul_single_one a b, linear_map.map_smul] },
  simp [kaehler_differential.derivation_quot_ker_total_apply],
end

lemma kaehler_differential.ker_total_eq :
  (finsupp.total S Œ©[S‚ÅÑR] S (kaehler_differential.D R S)).ker =
    kaehler_differential.ker_total R S :=
begin
  apply le_antisymm,
  { conv_rhs { rw ‚Üê (kaehler_differential.ker_total R S).ker_mkq },
    rw ‚Üê kaehler_differential.derivation_quot_ker_total_lift_comp_total,
    exact linear_map.ker_le_ker_comp _ _ },
  { rw [kaehler_differential.ker_total, submodule.span_le],
    rintros _ ((‚ü®‚ü®x, y‚ü©, rfl‚ü©|‚ü®‚ü®x, y‚ü©, rfl‚ü©)|‚ü®x, rfl‚ü©); dsimp; simp [linear_map.mem_ker] },
end

lemma kaehler_differential.total_surjective :
  function.surjective (finsupp.total S Œ©[S‚ÅÑR] S (kaehler_differential.D R S)) :=
begin
  rw [‚Üê linear_map.range_eq_top, finsupp.range_total, kaehler_differential.span_range_derivation],
end

/-- `Œ©[S‚ÅÑR]` is isomorphic to `S` copies of `S` with kernel `kaehler_differential.ker_total`. -/
@[simps] noncomputable
def kaehler_differential.quot_ker_total_equiv :
  ((S ‚Üí‚ÇÄ S) ‚ß∏ kaehler_differential.ker_total R S) ‚âÉ‚Çó[S] Œ©[S‚ÅÑR] :=
{ inv_fun := (kaehler_differential.derivation_quot_ker_total R S).lift_kaehler_differential,
  left_inv := begin
    intro x,
    obtain ‚ü®x, rfl‚ü© := submodule.mkq_surjective _ x,
    exact linear_map.congr_fun
      (kaehler_differential.derivation_quot_ker_total_lift_comp_total R S : _) x,
  end,
  right_inv := begin
    intro x,
    obtain ‚ü®x, rfl‚ü© := kaehler_differential.total_surjective R S x,
    erw linear_map.congr_fun
      (kaehler_differential.derivation_quot_ker_total_lift_comp_total R S : _) x,
    refl
  end,
  ..(kaehler_differential.ker_total R S).liftq
    (finsupp.total S Œ©[S‚ÅÑR] S (kaehler_differential.D R S))
    (kaehler_differential.ker_total_eq R S).ge }

lemma kaehler_differential.quot_ker_total_equiv_symm_comp_D :
  (kaehler_differential.quot_ker_total_equiv R S).symm.to_linear_map.comp_der
    (kaehler_differential.D R S) = kaehler_differential.derivation_quot_ker_total R S :=
by convert
  (kaehler_differential.derivation_quot_ker_total R S).lift_kaehler_differential_comp using 0

variables (A B : Type*) [comm_ring A] [comm_ring B] [algebra R A] [algebra S B] [algebra R B]
variables [algebra A B] [is_scalar_tower R S B] [is_scalar_tower R A B]

-- The map `(A ‚Üí‚ÇÄ A) ‚Üí‚Çó[A] (B ‚Üí‚ÇÄ B)`
local notation `finsupp_map` := ((finsupp.map_range.linear_map (algebra.of_id A B).to_linear_map)
  .comp (finsupp.lmap_domain A A (algebra_map A B)))

lemma kaehler_differential.ker_total_map (h : function.surjective (algebra_map A B)) :
  (kaehler_differential.ker_total R A).map finsupp_map ‚äî
    submodule.span A (set.range (Œª x : S, single (algebra_map S B x) (1 : B))) =
    (kaehler_differential.ker_total S B).restrict_scalars _  :=
begin
  rw [kaehler_differential.ker_total, submodule.map_span, kaehler_differential.ker_total,
    submodule.restrict_scalars_span _ _ h],
  simp_rw [set.image_union, submodule.span_union, ‚Üê set.image_univ, set.image_image,
    set.image_univ, map_sub, map_add],
  simp only [linear_map.comp_apply, finsupp.map_range.linear_map_apply, finsupp.map_range_single,
    finsupp.lmap_domain_apply, finsupp.map_domain_single, alg_hom.to_linear_map_apply,
    algebra.of_id_apply, ‚Üê is_scalar_tower.algebra_map_apply, map_one, map_add, map_mul],
  simp_rw [sup_assoc, ‚Üê (h.prod_map h).range_comp],
  congr' 3,
  rw [sup_eq_right],
  apply submodule.span_mono,
  simp_rw is_scalar_tower.algebra_map_apply R S B,
  exact set.range_comp_subset_range (algebra_map R S) (Œª x, single (algebra_map S B x) (1 : B))
end

end presentation

section exact_sequence

local attribute [irreducible] kaehler_differential

/- We have the commutative diagram
A --‚Üí B
‚Üë     ‚Üë
|     |
R --‚Üí S -/
variables (A B : Type*) [comm_ring A] [comm_ring B] [algebra R A] [algebra R B]
variables [algebra A B] [algebra S B] [is_scalar_tower R A B] [is_scalar_tower R S B]

variables {R B}

/-- For a tower `R ‚Üí A ‚Üí B` and an `R`-derivation `B ‚Üí M`, we may compose with `A ‚Üí B` to obtain an
`R`-derivation `A ‚Üí M`. -/
def derivation.comp_algebra_map [module A M] [module B M] [is_scalar_tower A B M]
  (d : derivation R B M) : derivation R A M :=
{ map_one_eq_zero' := by simp,
  leibniz' := Œª a b, by simp,
  to_linear_map := d.to_linear_map.comp (is_scalar_tower.to_alg_hom R A B).to_linear_map }

variables (R B)

/-- The map `Œ©[A‚ÅÑR] ‚Üí‚Çó[A] Œ©[B‚ÅÑR]` given a square
A --‚Üí B
‚Üë     ‚Üë
|     |
R --‚Üí S -/
def kaehler_differential.map : Œ©[A‚ÅÑR] ‚Üí‚Çó[A] Œ©[B‚ÅÑS] :=
derivation.lift_kaehler_differential
  (((kaehler_differential.D S B).restrict_scalars R).comp_algebra_map A)

lemma kaehler_differential.map_comp_der :
  (kaehler_differential.map R S A B).comp_der (kaehler_differential.D R A) =
    (((kaehler_differential.D S B).restrict_scalars R).comp_algebra_map A) :=
derivation.lift_kaehler_differential_comp _

lemma kaehler_differential.map_D (x : A) :
  kaehler_differential.map R S A B (kaehler_differential.D R A x) =
    kaehler_differential.D S B (algebra_map A B x) :=
derivation.congr_fun (kaehler_differential.map_comp_der R S A B) x

open is_scalar_tower (to_alg_hom)

lemma kaehler_differential.map_surjective_of_surjective
  (h : function.surjective (algebra_map A B)) :
  function.surjective (kaehler_differential.map R S A B) :=
begin
  rw [‚Üê linear_map.range_eq_top, _root_.eq_top_iff, ‚Üê @submodule.restrict_scalars_top B A,
    ‚Üê kaehler_differential.span_range_derivation, submodule.restrict_scalars_span _ _ h,
    submodule.span_le],
  rintros _ ‚ü®x, rfl‚ü©,
  obtain ‚ü®y, rfl‚ü© := h x,
  rw ‚Üê kaehler_differential.map_D R S A B,
  exact ‚ü®_, rfl‚ü©,
end

/-- The lift of the map `Œ©[A‚ÅÑR] ‚Üí‚Çó[A] Œ©[B‚ÅÑR]` to the base change along `A ‚Üí B`.
This is the first map in the exact sequence `B ‚äó[A] Œ©[A‚ÅÑR] ‚Üí Œ©[B‚ÅÑR] ‚Üí Œ©[B‚ÅÑA] ‚Üí 0`. -/
noncomputable
def kaehler_differential.map_base_change : B ‚äó[A] Œ©[A‚ÅÑR] ‚Üí‚Çó[B] Œ©[B‚ÅÑR] :=
(tensor_product.is_base_change A Œ©[A‚ÅÑR] B).lift (kaehler_differential.map R R A B)

@[simp]
lemma kaehler_differential.map_base_change_tmul (x : B) (y : Œ©[A‚ÅÑR]) :
  kaehler_differential.map_base_change R A B (x ‚äó‚Çú y) =
    x ‚Ä¢ kaehler_differential.map R R A B y :=
begin
  conv_lhs { rw [‚Üê mul_one x, ‚Üê smul_eq_mul, ‚Üê tensor_product.smul_tmul', linear_map.map_smul] },
  congr' 1,
  exact is_base_change.lift_eq _ _ _
end

end exact_sequence

end kaehler_differential
