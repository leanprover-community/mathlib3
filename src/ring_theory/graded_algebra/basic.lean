/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser, Kevin Buzzard, Jujian Zhang
-/
import algebra.direct_sum.algebra
import algebra.direct_sum.decomposition
import algebra.direct_sum.internal
import algebra.direct_sum.ring

/-!
# Internally-graded rings and algebras

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the typeclass `graded_algebra ğ’œ`, for working with an algebra `A` that is
internally graded by a collection of submodules `ğ’œ : Î¹ â†’ submodule R A`.
See the docstring of that typeclass for more information.

## Main definitions

* `graded_ring ğ’œ`: the typeclass, which is a combination of `set_like.graded_monoid`, and
  `direct_sum.decomposition ğ’œ`.
* `graded_algebra ğ’œ`: A convenience alias for `graded_ring` when `ğ’œ` is a family of submodules.
* `direct_sum.decompose_ring_equiv ğ’œ : A â‰ƒâ‚[R] â¨ i, ğ’œ i`, a more bundled version of
  `direct_sum.decompose ğ’œ`.
* `direct_sum.decompose_alg_equiv ğ’œ : A â‰ƒâ‚[R] â¨ i, ğ’œ i`, a more bundled version of
  `direct_sum.decompose ğ’œ`.
* `graded_algebra.proj ğ’œ i` is the linear map from `A` to its degree `i : Î¹` component, such that
  `proj ğ’œ i x = decompose ğ’œ x i`.

## Implementation notes

For now, we do not have internally-graded semirings and internally-graded rings; these can be
represented with `ğ’œ : Î¹ â†’ submodule â„• A` and `ğ’œ : Î¹ â†’ submodule â„¤ A` respectively, since all
`semiring`s are â„•-algebras via `algebra_nat`, and all `ring`s are `â„¤`-algebras via `algebra_int`.

## Tags

graded algebra, graded ring, graded semiring, decomposition
-/

open_locale direct_sum big_operators

variables {Î¹ R A Ïƒ : Type*}
section graded_ring
variables [decidable_eq Î¹] [add_monoid Î¹] [comm_semiring R] [semiring A] [algebra R A]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)

include A

open direct_sum

/-- An internally-graded `R`-algebra `A` is one that can be decomposed into a collection
of `submodule R A`s indexed by `Î¹` such that the canonical map `A â†’ â¨ i, ğ’œ i` is bijective and
respects multiplication, i.e. the product of an element of degree `i` and an element of degree `j`
is an element of degree `i + j`.

Note that the fact that `A` is internally-graded, `graded_algebra ğ’œ`, implies an externally-graded
algebra structure `direct_sum.galgebra R (Î» i, â†¥(ğ’œ i))`, which in turn makes available an
`algebra R (â¨ i, ğ’œ i)` instance.
-/
class graded_ring (ğ’œ : Î¹ â†’ Ïƒ) extends set_like.graded_monoid ğ’œ, direct_sum.decomposition ğ’œ.

variables [graded_ring ğ’œ]
namespace direct_sum

/-- If `A` is graded by `Î¹` with degree `i` component `ğ’œ i`, then it is isomorphic as
a ring to a direct sum of components. -/
def decompose_ring_equiv : A â‰ƒ+* â¨ i, ğ’œ i := ring_equiv.symm
{ map_mul' := (coe_ring_hom ğ’œ).map_mul,
  map_add' := (coe_ring_hom ğ’œ).map_add,
  ..(decompose_add_equiv ğ’œ).symm }

@[simp] lemma decompose_one : decompose ğ’œ (1 : A) = 1 := map_one (decompose_ring_equiv ğ’œ)
@[simp] lemma decompose_symm_one : (decompose ğ’œ).symm 1 = (1 : A) :=
map_one (decompose_ring_equiv ğ’œ).symm

@[simp] lemma decompose_mul (x y : A) : decompose ğ’œ (x * y) = decompose ğ’œ x * decompose ğ’œ y :=
map_mul (decompose_ring_equiv ğ’œ) x y
@[simp] lemma decompose_symm_mul (x y : â¨ i, ğ’œ i) :
  (decompose ğ’œ).symm (x * y) = (decompose ğ’œ).symm x * (decompose ğ’œ).symm y :=
map_mul (decompose_ring_equiv ğ’œ).symm x y

end direct_sum

/-- The projection maps of a graded ring -/
def graded_ring.proj (i : Î¹) : A â†’+ A :=
(add_submonoid_class.subtype (ğ’œ i)).comp $
  (dfinsupp.eval_add_monoid_hom i).comp $
  ring_hom.to_add_monoid_hom $ ring_equiv.to_ring_hom $ direct_sum.decompose_ring_equiv ğ’œ

@[simp] lemma graded_ring.proj_apply (i : Î¹) (r : A) :
  graded_ring.proj ğ’œ i r = (decompose ğ’œ r : â¨ i, ğ’œ i) i := rfl

lemma graded_ring.proj_recompose (a : â¨ i, ğ’œ i) (i : Î¹) :
  graded_ring.proj ğ’œ i ((decompose ğ’œ).symm a) =
  (decompose ğ’œ).symm (direct_sum.of _ i (a i)) :=
by rw [graded_ring.proj_apply, decompose_symm_of, equiv.apply_symm_apply]

lemma graded_ring.mem_support_iff [Î  i (x : ğ’œ i), decidable (x â‰  0)] (r : A) (i : Î¹) :
  i âˆˆ (decompose ğ’œ r).support â†” graded_ring.proj ğ’œ i r â‰  0 :=
dfinsupp.mem_support_iff.trans zero_mem_class.coe_eq_zero.not.symm

end graded_ring

section add_cancel_monoid

open direct_sum

variables [decidable_eq Î¹] [semiring A] [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variables {i j : Î¹}

namespace direct_sum

lemma coe_decompose_mul_add_of_left_mem
  [add_left_cancel_monoid Î¹] [graded_ring ğ’œ] {a b : A} (a_mem : a âˆˆ ğ’œ i) :
  (decompose ğ’œ (a * b) (i + j) : A) = a * decompose ğ’œ b j :=
by { lift a to ğ’œ i using a_mem, rw [decompose_mul, decompose_coe, coe_of_mul_apply_add] }

lemma coe_decompose_mul_add_of_right_mem
  [add_right_cancel_monoid Î¹] [graded_ring ğ’œ] {a b : A} (b_mem : b âˆˆ ğ’œ j) :
  (decompose ğ’œ (a * b) (i + j) : A) = decompose ğ’œ a i * b :=
by { lift b to ğ’œ j using b_mem, rw [decompose_mul, decompose_coe, coe_mul_of_apply_add] }

lemma decompose_mul_add_left
  [add_left_cancel_monoid Î¹] [graded_ring ğ’œ] (a : ğ’œ i) {b : A} :
  decompose ğ’œ (â†‘a * b) (i + j) =
    @graded_monoid.ghas_mul.mul Î¹ (Î» i, ğ’œ i) _ _ _ _ a (decompose ğ’œ b j) :=
subtype.ext $ coe_decompose_mul_add_of_left_mem ğ’œ a.2

lemma decompose_mul_add_right
  [add_right_cancel_monoid Î¹] [graded_ring ğ’œ] {a : A} (b : ğ’œ j) :
  decompose ğ’œ (a * â†‘b) (i + j) =
    @graded_monoid.ghas_mul.mul Î¹ (Î» i, ğ’œ i) _ _ _ _ (decompose ğ’œ a i) b :=
subtype.ext $ coe_decompose_mul_add_of_right_mem ğ’œ b.2

end direct_sum

end add_cancel_monoid

section graded_algebra
variables [decidable_eq Î¹] [add_monoid Î¹] [comm_semiring R] [semiring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A)

/-- A special case of `graded_ring` with `Ïƒ = submodule R A`. This is useful both because it
can avoid typeclass search, and because it provides a more concise name. -/
@[reducible]
def graded_algebra := graded_ring ğ’œ

/-- A helper to construct a `graded_algebra` when the `set_like.graded_monoid` structure is already
available. This makes the `left_inv` condition easier to prove, and phrases the `right_inv`
condition in a way that allows custom `@[ext]` lemmas to apply.

See note [reducible non-instances]. -/
@[reducible]
def graded_algebra.of_alg_hom [set_like.graded_monoid ğ’œ] (decompose : A â†’â‚[R] â¨ i, ğ’œ i)
  (right_inv : (direct_sum.coe_alg_hom ğ’œ).comp decompose = alg_hom.id R A)
  (left_inv : âˆ€ i (x : ğ’œ i), decompose (x : A) = direct_sum.of (Î» i, â†¥(ğ’œ i)) i x) :
  graded_algebra ğ’œ :=
{ decompose' := decompose,
  left_inv := alg_hom.congr_fun right_inv,
  right_inv := begin
    suffices : decompose.comp (direct_sum.coe_alg_hom ğ’œ) = alg_hom.id _ _,
    from alg_hom.congr_fun this,
    ext i x : 2,
    exact (decompose.congr_arg $ direct_sum.coe_alg_hom_of _ _ _).trans (left_inv i x),
  end}

variable [graded_algebra ğ’œ]

namespace direct_sum

/-- If `A` is graded by `Î¹` with degree `i` component `ğ’œ i`, then it is isomorphic as
an algebra to a direct sum of components. -/
@[simps]
def decompose_alg_equiv : A â‰ƒâ‚[R] â¨ i, ğ’œ i := alg_equiv.symm
{ map_mul' := (coe_alg_hom ğ’œ).map_mul,
  map_add' := (coe_alg_hom ğ’œ).map_add,
  commutes' := (coe_alg_hom ğ’œ).commutes,
  ..(decompose_add_equiv ğ’œ).symm }

end direct_sum

open direct_sum

/-- The projection maps of graded algebra-/
def graded_algebra.proj (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ] (i : Î¹) : A â†’â‚—[R] A :=
(ğ’œ i).subtype.comp $
  (dfinsupp.lapply i).comp $
  (decompose_alg_equiv ğ’œ).to_alg_hom.to_linear_map

@[simp] lemma graded_algebra.proj_apply (i : Î¹) (r : A) :
  graded_algebra.proj ğ’œ i r = (decompose ğ’œ r : â¨ i, ğ’œ i) i := rfl

lemma graded_algebra.proj_recompose (a : â¨ i, ğ’œ i) (i : Î¹) :
  graded_algebra.proj ğ’œ i ((decompose ğ’œ).symm a) =
  (decompose ğ’œ).symm (of _ i (a i)) :=
by rw [graded_algebra.proj_apply, decompose_symm_of, equiv.apply_symm_apply]

lemma graded_algebra.mem_support_iff [decidable_eq A] (r : A) (i : Î¹) :
  i âˆˆ (decompose ğ’œ r).support â†” graded_algebra.proj ğ’œ i r â‰  0 :=
dfinsupp.mem_support_iff.trans submodule.coe_eq_zero.not.symm

end graded_algebra

section canonical_order

open set_like.graded_monoid direct_sum

variables [semiring A] [decidable_eq Î¹]
variables [canonically_ordered_add_monoid Î¹]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [graded_ring ğ’œ]

/--
If `A` is graded by a canonically ordered add monoid, then the projection map `x â†¦ xâ‚€` is a ring
homomorphism.
-/
@[simps]
def graded_ring.proj_zero_ring_hom : A â†’+* A :=
{ to_fun := Î» a, decompose ğ’œ a 0,
  map_one' := decompose_of_mem_same ğ’œ one_mem,
  map_zero' := by { rw decompose_zero, refl },
  map_add' := Î» _ _, by { rw decompose_add, refl },
  map_mul' := begin
    refine direct_sum.decomposition.induction_on ğ’œ (Î» x, _) _ _,
    { simp only [zero_mul, decompose_zero, zero_apply, zero_mem_class.coe_zero] },
    { rintros i âŸ¨c, hcâŸ©,
      refine direct_sum.decomposition.induction_on ğ’œ _ _ _,
      { simp only [mul_zero, decompose_zero, zero_apply, zero_mem_class.coe_zero] },
      { rintros j âŸ¨c', hc'âŸ©,
        { simp only [subtype.coe_mk],
          by_cases h : i + j = 0,
          { rw [decompose_of_mem_same ğ’œ (show c * c' âˆˆ ğ’œ 0, from h â–¸ mul_mem hc hc'),
              decompose_of_mem_same ğ’œ (show c âˆˆ ğ’œ 0, from (add_eq_zero_iff.mp h).1 â–¸ hc),
              decompose_of_mem_same ğ’œ (show c' âˆˆ ğ’œ 0, from (add_eq_zero_iff.mp h).2 â–¸ hc')] },
          { rw [decompose_of_mem_ne ğ’œ (mul_mem hc hc') h],
            cases (show i â‰  0 âˆ¨ j â‰  0, by rwa [add_eq_zero_iff, not_and_distrib] at h) with h' h',
            { simp only [decompose_of_mem_ne ğ’œ hc h', zero_mul] },
            { simp only [decompose_of_mem_ne ğ’œ hc' h', mul_zero] } } }, },
      { intros _ _ hd he,
        simp only [mul_add, decompose_add, add_apply, add_mem_class.coe_add, hd, he] }, },
    { rintros _ _ ha hb _,
      simp only [add_mul, decompose_add, add_apply, add_mem_class.coe_add, ha, hb], },
  end }

variables {a b : A} {n i : Î¹}

namespace direct_sum

lemma coe_decompose_mul_of_left_mem_of_not_le
  (a_mem : a âˆˆ ğ’œ i) (h : Â¬ i â‰¤ n) : (decompose ğ’œ (a * b) n : A) = 0 :=
by { lift a to ğ’œ i using a_mem, rwa [decompose_mul, decompose_coe, coe_of_mul_apply_of_not_le] }

lemma coe_decompose_mul_of_right_mem_of_not_le
  (b_mem : b âˆˆ ğ’œ i) (h : Â¬ i â‰¤ n) : (decompose ğ’œ (a * b) n : A) = 0 :=
by { lift b to ğ’œ i using b_mem, rwa [decompose_mul, decompose_coe, coe_mul_of_apply_of_not_le] }

variables [has_sub Î¹] [has_ordered_sub Î¹] [contravariant_class Î¹ Î¹ (+) (â‰¤)]

lemma coe_decompose_mul_of_left_mem_of_le
  (a_mem : a âˆˆ ğ’œ i) (h : i â‰¤ n) : (decompose ğ’œ (a * b) n : A) = a * decompose ğ’œ b (n - i) :=
by { lift a to ğ’œ i using a_mem, rwa [decompose_mul, decompose_coe, coe_of_mul_apply_of_le] }

lemma coe_decompose_mul_of_right_mem_of_le
  (b_mem : b âˆˆ ğ’œ i) (h : i â‰¤ n) : (decompose ğ’œ (a * b) n : A) = decompose ğ’œ a (n - i) * b :=
by { lift b to ğ’œ i using b_mem, rwa [decompose_mul, decompose_coe, coe_mul_of_apply_of_le] }

lemma coe_decompose_mul_of_left_mem (n) [decidable (i â‰¤ n)] (a_mem : a âˆˆ ğ’œ i) :
  (decompose ğ’œ (a * b) n : A) = if i â‰¤ n then a * decompose ğ’œ b (n - i) else 0 :=
by { lift a to ğ’œ i using a_mem, rwa [decompose_mul, decompose_coe, coe_of_mul_apply] }

lemma coe_decompose_mul_of_right_mem (n) [decidable (i â‰¤ n)] (b_mem : b âˆˆ ğ’œ i) :
  (decompose ğ’œ (a * b) n : A) = if i â‰¤ n then decompose ğ’œ a (n - i) * b else 0 :=
by { lift b to ğ’œ i using b_mem, rwa [decompose_mul, decompose_coe, coe_mul_of_apply] }

end direct_sum

end canonical_order
