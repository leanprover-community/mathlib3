/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser, Kevin Buzzard, Jujian Zhang
-/
import algebra.direct_sum.algebra
import algebra.direct_sum.internal

/-!
# Internally-graded algebras

This file defines the typeclass `graded_algebra ğ’œ`, for working with an algebra `A` that is
internally graded by a collection of submodules `ğ’œ : Î¹ â†’ submodule R A`.
See the docstring of that typeclass for more information.

## Main definitions

* `graded_algebra ğ’œ`: the typeclass, which is a combination of `set_like.graded_monoid`, and
  a constructive version of `direct_sum.submodule_is_internal ğ’œ`.
* `graded_algebra.decompose : A â‰ƒâ‚[R] â¨ i, ğ’œ i`, which breaks apart an element of the algebra into
  its constituent pieces.

## Implementation notes

For now, we do not have internally-graded semirings and internally-graded rings; these can be
represented with `ğ’œ : Î¹ â†’ submodule â„• A` and `ğ’œ : Î¹ â†’ submodule â„¤ A` respectively, since all
`semiring`s are â„•-algebras via `algebra_nat`, and all `ring`s are `â„¤`-algebras via `algebra_int`.

## Tags

graded algebra, graded ring, graded semiring, decomposition
-/

open_locale direct_sum big_operators

section graded_algebra

variables {Î¹ R A : Type*}
variables [decidable_eq Î¹] [add_comm_monoid Î¹] [comm_semiring R] [ring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A)

/-- An internally-graded `R`-algebra `A` is one that can be decomposed into a collection
of `submodule R A`s indexed by `Î¹` such that the canonical map `A â†’ â¨ i, ğ’œ i` is bijective and
respects multiplication, i.e. the product of an element of degree `i` and an element of degree `j`
is an element of degree `i + j`.

Note that the fact that `A` is internally-graded, `graded_algebra ğ’œ`, implies an externally-graded
algebra structure `direct_sum.galgebra R (Î» i, â†¥(ğ’œ i))`, which in turn makes available an
`algebra R (â¨ i, ğ’œ i)` instance.
-/
class graded_algebra extends set_like.graded_monoid ğ’œ :=
(decompose' : A â†’ â¨ i, ğ’œ i)
(left_inv : function.left_inverse decompose' (direct_sum.submodule_coe ğ’œ))
(right_inv : function.right_inverse decompose' (direct_sum.submodule_coe ğ’œ))

lemma graded_ring.is_internal [graded_algebra ğ’œ] :
  direct_sum.submodule_is_internal ğ’œ :=
âŸ¨graded_algebra.left_inv.injective, graded_algebra.right_inv.surjectiveâŸ©

variable [graded_algebra ğ’œ]

/-- If `A` is graded by `Î¹` with degree `i` component `ğ’œ i`, then it is isomorphic as
an algebra to a direct sum of components. -/
def graded_algebra.decompose : A â‰ƒâ‚[R] â¨ i, ğ’œ i := alg_equiv.symm
{ to_fun := direct_sum.submodule_coe_alg_hom ğ’œ,
  inv_fun := graded_algebra.decompose',
  left_inv := graded_algebra.left_inv,
  right_inv := graded_algebra.right_inv,
  map_mul' := alg_hom.map_mul _,
  map_add' := alg_hom.map_add _,
  commutes' := alg_hom.commutes _ }

@[simp] lemma graded_algebra.decompose'_def :
  graded_algebra.decompose' = graded_algebra.decompose ğ’œ := rfl

@[simp] lemma graded_algebra.decompose_symm_of {i : Î¹} (x : ğ’œ i) :
  (graded_algebra.decompose ğ’œ).symm (direct_sum.of _ i x) = x :=
direct_sum.submodule_coe_alg_hom_of ğ’œ _ _

/-- The projection maps of graded algebra-/
def graded_algebra.proj (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ] (i : Î¹) : A â†’â‚—[R] A :=
(ğ’œ i).subtype.comp $
  (dfinsupp.lapply i).comp $
  (graded_algebra.decompose ğ’œ).to_alg_hom.to_linear_map

@[simp] lemma graded_algebra.proj_apply (i : Î¹) (r : A) :
  graded_algebra.proj ğ’œ i r = (graded_algebra.decompose ğ’œ r : â¨ i, ğ’œ i) i := rfl

lemma graded_algebra.proj_mem (i : Î¹) (r : A) :
  graded_algebra.proj ğ’œ i r âˆˆ ğ’œ i := (graded_algebra.decompose ğ’œ r i).2

/-- The support of `r` is the `finset` where `proj R A i r â‰  0 â†” i âˆˆ r.support`-/
def graded_algebra.support [Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0)]
  (r : A) : finset Î¹ :=
(graded_algebra.decompose ğ’œ r).support

lemma graded_algebra.proj_recompose (a : â¨ i, ğ’œ i) (i : Î¹) :
  graded_algebra.proj ğ’œ i ((graded_algebra.decompose ğ’œ).symm a) =
  (graded_algebra.decompose ğ’œ).symm (direct_sum.of _ i (a i)) :=
by rw [graded_algebra.proj_apply, graded_algebra.decompose_symm_of, alg_equiv.apply_symm_apply]

variable [Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0)]

lemma graded_ring.mem_support_iff
  (r : A) (i : Î¹) :
i âˆˆ graded_algebra.support ğ’œ r â†” (graded_algebra.proj ğ’œ i r â‰  0) :=
âŸ¨Î» hi, begin
  contrapose! hi,
  unfold graded_algebra.support,
  unfold graded_algebra.proj at hi,
  rw [dfinsupp.mem_support_iff, not_not],
  simp only [submodule.subtype_apply, dfinsupp.lapply_apply, alg_equiv.to_alg_hom_eq_coe,
    function.comp_app, linear_map.coe_comp, alg_equiv.to_alg_hom_to_linear_map,
    alg_equiv.to_linear_map_apply, submodule.coe_eq_zero] at hi,
  exact hi,
end, Î» hi, begin
  unfold graded_algebra.proj at hi,
  unfold graded_algebra.support,
  simp only [ne.def, dfinsupp.mem_support_to_fun],
  intro rid,
  simp only [submodule.subtype_apply, dfinsupp.lapply_apply, alg_equiv.to_alg_hom_eq_coe,
    function.comp_app, linear_map.coe_comp, alg_equiv.to_alg_hom_to_linear_map,
    alg_equiv.to_linear_map_apply, submodule.coe_eq_zero] at hi,
  rw rid at hi,
  simp only [eq_self_iff_true, not_true, ne.def, submodule.coe_zero] at hi,
  exact hi,
endâŸ©

lemma graded_algebra.as_sum (r : A) :
  r = âˆ‘ i in graded_algebra.support ğ’œ r, graded_algebra.proj ğ’œ i r :=
begin
  conv_lhs { rw [â†@graded_algebra.right_inv Î¹ R A _ _ _ _ _ ğ’œ _ r,
    graded_algebra.decompose'_def,
    â†direct_sum.sum_support_of _ (graded_algebra.decompose ğ’œ r), linear_map.map_sum] },
  unfold graded_algebra.support,
  apply finset.sum_congr rfl,
  intros i hi,
  rw [direct_sum.submodule_coe_of],
  refl,
end

lemma graded_ring.mul_proj (r r' : A) (i : Î¹) :
  graded_algebra.proj ğ’œ i (r * r') =
  âˆ‘ ij in finset.filter (Î» ij : Î¹ Ã— Î¹, ij.1 + ij.2 = i)
    ((graded_algebra.support ğ’œ r).product (graded_algebra.support ğ’œ r')),
    (graded_algebra.proj ğ’œ ij.1 r) * (graded_algebra.proj ğ’œ ij.2 r') :=
begin
  have set_eq : (graded_algebra.support ğ’œ r).product (graded_algebra.support ğ’œ r') =
  finset.filter (Î» ij : Î¹ Ã— Î¹, ij.1 + ij.2 = i) _ âˆª
  finset.filter (Î» ij : Î¹ Ã— Î¹, ij.1 + ij.2 â‰  i) _ := (finset.filter_union_filter_neg_eq _ _).symm,
  conv_lhs { rw [graded_algebra.as_sum ğ’œ r, graded_algebra.as_sum ğ’œ r', finset.sum_mul_sum,
    linear_map.map_sum, set_eq] },
  rw finset.sum_union,
  suffices : âˆ‘ (x : Î¹ Ã— Î¹) in finset.filter (Î» (ij : Î¹ Ã— Î¹), ij.fst + ij.snd â‰  i)
    ((graded_algebra.support ğ’œ r).product (graded_algebra.support ğ’œ r')),
  (graded_algebra.proj ğ’œ i) (graded_algebra.proj ğ’œ x.fst r * graded_algebra.proj ğ’œ x.snd r') = 0,
  rw [this, add_zero], apply finset.sum_congr rfl,
  rintros âŸ¨j, kâŸ© h, simp only [finset.mem_filter, finset.mem_product] at h âŠ¢,
  obtain âŸ¨âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚ƒâŸ© := h,
  rw â†hâ‚ƒ,
  obtain âŸ¨a, rflâŸ© := (graded_algebra.decompose ğ’œ).symm.bijective.surjective r,
  obtain âŸ¨b, rflâŸ© := (graded_algebra.decompose ğ’œ).symm.bijective.surjective r',
  rw [graded_algebra.proj_recompose, graded_algebra.proj_recompose, â†alg_equiv.map_mul,
    direct_sum.of_mul_of, graded_algebra.proj_recompose],
  congr, rw [direct_sum.of_eq_same],

  apply finset.sum_eq_zero, rintros âŸ¨j, kâŸ© h,
  simp only [ne.def, finset.mem_filter, finset.mem_product] at h âŠ¢,
  obtain âŸ¨âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚ƒâŸ© := h,
  obtain âŸ¨a, rflâŸ© := (graded_algebra.decompose ğ’œ).symm.bijective.surjective r,
  obtain âŸ¨b, rflâŸ© := (graded_algebra.decompose ğ’œ).symm.bijective.surjective r',
  rw [graded_algebra.proj_recompose, graded_algebra.proj_recompose, â†alg_equiv.map_mul,
    direct_sum.of_mul_of, graded_algebra.proj_recompose, direct_sum.of_eq_of_ne],
  simp only [ring_equiv.map_zero, add_monoid_hom.map_zero],
  rw [alg_equiv.map_zero],
  intro rid, exact hâ‚ƒ rid,

  rw [finset.disjoint_iff_inter_eq_empty, finset.eq_empty_iff_forall_not_mem],
  rintros âŸ¨j, kâŸ© rid,
  simp only [ne.def, finset.mem_filter, finset.mem_inter, finset.mem_product] at rid,
  rcases rid with âŸ¨âŸ¨_, hâ‚âŸ©, âŸ¨_, hâ‚‚âŸ©âŸ©, exact hâ‚‚ hâ‚,
end

lemma graded_algebra.proj_homogeneous_element {x : A} {i : Î¹} (hx : x âˆˆ ğ’œ i) :
  graded_algebra.proj ğ’œ i x = x :=
begin
  rw [graded_algebra.proj_apply, â†subtype.coe_mk x hx, subtype.coe_injective.eq_iff,
    â†graded_algebra.decompose_symm_of, alg_equiv.apply_symm_apply, direct_sum.of_eq_same],
end


lemma graded_ring.proj_homogeneous_element_of_ne {x : A} {i j : Î¹} (hx : x âˆˆ ğ’œ i) (hij : i â‰  j):
  graded_algebra.proj ğ’œ j x = 0 :=
begin
  rw â†graded_algebra.proj_homogeneous_element ğ’œ hx,
  obtain âŸ¨a, rflâŸ© := (graded_algebra.decompose ğ’œ).symm.bijective.surjective x,
  rw [graded_algebra.proj_recompose, graded_algebra.proj_recompose, direct_sum.of_eq_of_ne,
    add_monoid_hom.map_zero, alg_equiv.map_zero], exact hij,
end

end graded_algebra
