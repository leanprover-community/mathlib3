/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser, Kevin Buzzard, Jujian Zhang
-/
import algebra.direct_sum.algebra
import algebra.direct_sum.internal
import algebra.direct_sum.ring
import group_theory.subgroup.basic

/-!
# Internally-graded algebras

This file defines the typeclass `graded_algebra ğ’œ`, for working with an algebra `A` that is
internally graded by a collection of submodules `ğ’œ : Î¹ â†’ submodule R A`.
See the docstring of that typeclass for more information.

## Main definitions

* `graded_algebra ğ’œ`: the typeclass, which is a combination of `set_like.graded_monoid`, and
  a constructive version of `direct_sum.submodule_is_internal ğ’œ`.
* `graded_algebra.decompose : A â‰ƒâ‚[R] â¨ i, ğ’œ i`, which breaks apart an element of the algebra into
  its constituent pieces.
* `graded_algebra.proj ğ’œ i` is the linear map from `A` to its degree `i : Î¹` component, such that
  `proj ğ’œ i x = decompose ğ’œ x i`.
* `graded_algebra.support ğ’œ r` is the `finset Î¹` containing the `i : Î¹` such that the degree `i`
  component of `r` is not zero.

## Implementation notes

For now, we do not have internally-graded semirings and internally-graded rings; these can be
represented with `ğ’œ : Î¹ â†’ submodule â„• A` and `ğ’œ : Î¹ â†’ submodule â„¤ A` respectively, since all
`semiring`s are â„•-algebras via `algebra_nat`, and all `ring`s are `â„¤`-algebras via `algebra_int`.

## Tags

graded algebra, graded ring, graded semiring, decomposition
-/

open_locale direct_sum big_operators

section graded_algebra

variables {Î¹ R A : Type*}
variables [decidable_eq Î¹] [add_monoid Î¹] [comm_semiring R] [semiring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A)

/-- An internally-graded `R`-algebra `A` is one that can be decomposed into a collection
of `submodule R A`s indexed by `Î¹` such that the canonical map `A â†’ â¨ i, ğ’œ i` is bijective and
respects multiplication, i.e. the product of an element of degree `i` and an element of degree `j`
is an element of degree `i + j`.

Note that the fact that `A` is internally-graded, `graded_algebra ğ’œ`, implies an externally-graded
algebra structure `direct_sum.galgebra R (Î» i, â†¥(ğ’œ i))`, which in turn makes available an
`algebra R (â¨ i, ğ’œ i)` instance.
-/

class graded_algebra extends set_like.graded_monoid ğ’œ :=
(decompose' : A â†’ â¨ i, ğ’œ i)
(left_inv : function.left_inverse decompose' (direct_sum.submodule_coe ğ’œ))
(right_inv : function.right_inverse decompose' (direct_sum.submodule_coe ğ’œ))

lemma graded_algebra.is_internal [graded_algebra ğ’œ] :
  direct_sum.submodule_is_internal ğ’œ :=
âŸ¨graded_algebra.left_inv.injective, graded_algebra.right_inv.surjectiveâŸ©

/-- A helper to construct a `graded_algebra` when the `set_like.graded_monoid` structure is already
available. This makes the `left_inv` condition easier to prove, and phrases the `right_inv`
condition in a way that allows custom `@[ext]` lemmas to apply.

See note [reducible non-instances]. -/
@[reducible]
def graded_algebra.of_alg_hom [set_like.graded_monoid ğ’œ] (decompose : A â†’â‚[R] â¨ i, ğ’œ i)
  (right_inv : (direct_sum.submodule_coe_alg_hom ğ’œ).comp decompose = alg_hom.id R A)
  (left_inv : âˆ€ i (x : ğ’œ i), decompose (x : A) = direct_sum.of (Î» i, â†¥(ğ’œ i)) i x) :
  graded_algebra ğ’œ :=
{ decompose' := decompose,
  right_inv := alg_hom.congr_fun right_inv,
  left_inv := begin
    suffices : decompose.comp (direct_sum.submodule_coe_alg_hom ğ’œ) = alg_hom.id _ _,
    from alg_hom.congr_fun this,
    ext i x : 2,
    exact (decompose.congr_arg $ direct_sum.submodule_coe_alg_hom_of _ _ _).trans (left_inv i x),
  end}

variable [graded_algebra ğ’œ]

/-- If `A` is graded by `Î¹` with degree `i` component `ğ’œ i`, then it is isomorphic as
an algebra to a direct sum of components. -/
def graded_algebra.decompose : A â‰ƒâ‚[R] â¨ i, ğ’œ i := alg_equiv.symm
{ to_fun := direct_sum.submodule_coe_alg_hom ğ’œ,
  inv_fun := graded_algebra.decompose',
  left_inv := graded_algebra.left_inv,
  right_inv := graded_algebra.right_inv,
  map_mul' := alg_hom.map_mul _,
  map_add' := alg_hom.map_add _,
  commutes' := alg_hom.commutes _ }

@[simp] lemma graded_algebra.decompose'_def :
  graded_algebra.decompose' = graded_algebra.decompose ğ’œ := rfl

@[simp] lemma graded_algebra.decompose_symm_of {i : Î¹} (x : ğ’œ i) :
  (graded_algebra.decompose ğ’œ).symm (direct_sum.of _ i x) = x :=
direct_sum.submodule_coe_alg_hom_of ğ’œ _ _

/-- The projection maps of graded algebra-/
def graded_algebra.proj (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ] (i : Î¹) : A â†’â‚—[R] A :=
(ğ’œ i).subtype.comp $
  (dfinsupp.lapply i).comp $
  (graded_algebra.decompose ğ’œ).to_alg_hom.to_linear_map

@[simp] lemma graded_algebra.proj_apply (i : Î¹) (r : A) :
  graded_algebra.proj ğ’œ i r = (graded_algebra.decompose ğ’œ r : â¨ i, ğ’œ i) i := rfl

/-- The support of `r` is the `finset` where `proj R A i r â‰  0 â†” i âˆˆ r.support`-/
def graded_algebra.support [Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0)]
  (r : A) : finset Î¹ :=
(graded_algebra.decompose ğ’œ r).support

lemma graded_algebra.proj_recompose (a : â¨ i, ğ’œ i) (i : Î¹) :
  graded_algebra.proj ğ’œ i ((graded_algebra.decompose ğ’œ).symm a) =
  (graded_algebra.decompose ğ’œ).symm (direct_sum.of _ i (a i)) :=
by rw [graded_algebra.proj_apply, graded_algebra.decompose_symm_of, alg_equiv.apply_symm_apply]

@[simp] lemma graded_algebra.decompose_coe {i : Î¹} (x : ğ’œ i) :
  graded_algebra.decompose ğ’œ x = direct_sum.of _ i x :=
by rw [â†graded_algebra.decompose_symm_of, alg_equiv.apply_symm_apply]

lemma graded_algebra.decompose_of_mem {x : A} {i : Î¹} (hx : x âˆˆ ğ’œ i) :
  graded_algebra.decompose ğ’œ x = direct_sum.of _ i (âŸ¨x, hxâŸ© : ğ’œ i) :=
graded_algebra.decompose_coe _ âŸ¨x, hxâŸ©

lemma graded_algebra.decompose_of_mem_same {x : A} {i : Î¹} (hx : x âˆˆ ğ’œ i) :
  (graded_algebra.decompose ğ’œ x i : A) = x :=
by rw [graded_algebra.decompose_of_mem _ hx, direct_sum.of_eq_same, subtype.coe_mk]

lemma graded_algebra.decompose_of_mem_ne {x : A} {i j : Î¹} (hx : x âˆˆ ğ’œ i) (hij : i â‰  j):
  (graded_algebra.decompose ğ’œ x j : A) = 0 :=
by rw [graded_algebra.decompose_of_mem _ hx, direct_sum.of_eq_of_ne _ _ _ _ hij, submodule.coe_zero]


variable [Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0)]

lemma graded_algebra.mem_support_iff (r : A) (i : Î¹) :
  i âˆˆ graded_algebra.support ğ’œ r â†” graded_algebra.proj ğ’œ i r â‰  0 :=
begin
  rw [graded_algebra.support, dfinsupp.mem_support_iff, graded_algebra.proj_apply],
  simp only [ne.def, submodule.coe_eq_zero],
end

lemma graded_algebra.sum_support_decompose (r : A) :
  âˆ‘ i in graded_algebra.support ğ’œ r, (graded_algebra.decompose ğ’œ r i : A) = r :=
begin
  conv_rhs { rw [â†(graded_algebra.decompose ğ’œ).symm_apply_apply r,
    â†direct_sum.sum_support_of _ (graded_algebra.decompose ğ’œ r)] },
  rw [alg_equiv.map_sum, graded_algebra.support],
  simp_rw graded_algebra.decompose_symm_of,
end

end graded_algebra
