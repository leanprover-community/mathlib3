/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/

import ring_theory.ideal.basic
import ring_theory.ideal.operations
import linear_algebra.finsupp
import ring_theory.graded_algebra.basic

/-!

# Homogeneous ideal of a graded algebra

This file defines homogeneous ideals of `graded_algebra A` where `A : Î¹ â†’ ideal R`and operations on
them:
* `mul`, `inf`, `Inf` of homogeneous ideals are homogeneous;
* `âŠ¤`, `âŠ¥`, i.e. the trivial ring and `R` are homogeneous;
* `radical` of a homogeneous ideal is homogeneous.
-/

open set_like direct_sum set
open_locale big_operators pointwise direct_sum

section homogeneous_core

variables {Î¹ R A : Type*} [comm_ring R] [comm_ring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A)
variable (I : ideal A)

/-- For any `I : ideal R`, not necessarily homogeneous, there is a homogeneous ideal associated with
`I` spanned by all homogeneous elements in `I`. This construction is used when proving that the
radical of a homogeneous ideal is homogeneous. -/
def ideal.homogeneous_core : ideal A :=
ideal.span (coe '' ((coe : subtype (is_homogeneous ğ’œ) â†’ A) â»Â¹' I))

lemma ideal.homogeneous_core_is_mono : monotone (ideal.homogeneous_core ğ’œ) :=
Î» I J I_le_J, ideal.span_mono $ set.image_subset _ $ Î» x, @I_le_J _

lemma ideal.homogeneous_core_le_ideal : I.homogeneous_core ğ’œ â‰¤ I :=
ideal.span_le.2 $ image_preimage_subset _ _

end homogeneous_core

section is_homogeneous_ideal_defs

variables {Î¹ R A : Type*} [comm_ring R] [comm_ring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A)
variables [decidable_eq Î¹] [add_comm_monoid Î¹]  [graded_algebra ğ’œ]
variable (I : ideal A)

/--An `I : ideal R` is homogeneous if for every `r âˆˆ I`, all homogeneous components
  of `r` are in `I`.-/
def ideal.is_homogeneous : Prop :=
âˆ€ (i : Î¹) â¦ƒr : Aâ¦„, r âˆˆ I â†’ (graded_algebra.decompose ğ’œ r i : A) âˆˆ I

lemma ideal.is_homogeneous_iff_forall_subset :
  I.is_homogeneous ğ’œ â†” âˆ€ i, (I : set A) âŠ† graded_algebra.proj ğ’œ i â»Â¹' I :=
iff.rfl

lemma ideal.is_homogeneous_iff_subset_Inter :
  I.is_homogeneous ğ’œ â†” (I : set A) âŠ† â‹‚ i, graded_algebra.proj ğ’œ i â»Â¹' â†‘I :=
subset_Inter_iff.symm

lemma ideal.is_homogeneous.exists_iff_eq_span :
  (âˆƒ (S : set (homogeneous_submonoid ğ’œ)), I = ideal.span (coe '' S)) â†”
    I = I.homogeneous_core ğ’œ :=
(set.image_preimage.compose (submodule.gi _ _).gc).exists_eq_l _

lemma mul_homogeneous_element_mem_of_mem
  {I : ideal A} (r x : A) (hxâ‚ : is_homogeneous ğ’œ x) (hxâ‚‚ : x âˆˆ I) (j : Î¹) :
  graded_algebra.proj ğ’œ j (r * x) âˆˆ I :=
begin
  letI : Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0) := Î» _ _, classical.dec _,
  rw [â†graded_algebra.sum_support_decompose ğ’œ r, finset.sum_mul, linear_map.map_sum],
  apply ideal.sum_mem,
  intros k hk,
  obtain âŸ¨i, hiâŸ© := hxâ‚,
  have memâ‚ : (graded_algebra.decompose ğ’œ r k : A) * x âˆˆ ğ’œ (k + i) := graded_monoid.mul_mem
    (submodule.coe_mem _) hi,
  rw [graded_algebra.proj_apply, graded_algebra.decompose_of_mem ğ’œ memâ‚,
    coe_of_submodule_apply ğ’œ, submodule.coe_mk],
  split_ifs,
  { exact I.mul_mem_left _ hxâ‚‚ },
  { exact I.zero_mem },
end

lemma ideal.is_homogeneous_homogeneous_core : (I.homogeneous_core ğ’œ).is_homogeneous ğ’œ :=
begin
  rintros i r hr,
  rw [ideal.homogeneous_core, ideal.span, finsupp.span_eq_range_total] at hr,
  rw linear_map.mem_range at hr,
  obtain âŸ¨s, rflâŸ© := hr,
  rw [â†graded_algebra.proj_apply, finsupp.total_apply, finsupp.sum, linear_map.map_sum],
  refine ideal.sum_mem _ _,
  rintros z hz1,
  rw [smul_eq_mul],
  refine mul_homogeneous_element_mem_of_mem ğ’œ (s z) z _ _ i,
  { rcases z with âŸ¨z, hz2âŸ©,
    rw subtype.image_preimage_coe at hz2,
    exact hz2.2, },
  { exact ideal.subset_span z.2 },
end

variables {ğ’œ I}

lemma ideal.is_homogeneous.homogeneous_core_eq_self (h : I.is_homogeneous ğ’œ) :
  I = I.homogeneous_core ğ’œ :=
begin
  apply le_antisymm _ (I.homogeneous_core_le_ideal ğ’œ),
  intros x hx,
  letI : Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0) := Î» _ _, classical.dec _,
  rw â†graded_algebra.sum_support_decompose ğ’œ x,
  refine ideal.sum_mem _ _,
  intros j hj,
  apply ideal.subset_span,
  exact âŸ¨âŸ¨_, is_homogeneous_coe _âŸ©, h _ hx, rflâŸ©,
end

variables (ğ’œ I)

lemma ideal.is_homogeneous.iff_eq :
  I.is_homogeneous ğ’œ â†” I = I.homogeneous_core ğ’œ :=
âŸ¨ Î» hI, hI.homogeneous_core_eq_self,
  Î» hI, hI.symm â–¸ I.is_homogeneous_homogeneous_core ğ’œ âŸ©

lemma ideal.is_homogeneous.iff_exists :
  I.is_homogeneous ğ’œ â†” âˆƒ (S : set (homogeneous_submonoid ğ’œ)), I = ideal.span (coe '' S) :=
by rw [ideal.is_homogeneous.exists_iff_eq_span, ideal.is_homogeneous.iff_eq]

end is_homogeneous_ideal_defs

section operations

variables {Î¹ R A : Type*} [comm_ring R] [comm_ring A] [algebra R A]
variables [decidable_eq Î¹] [add_comm_monoid Î¹]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]
variable (I : ideal A)

/--For any `comm_ring R`, we collect the homogeneous ideals of `R` into a type.-/
abbreviation homogeneous_ideal : Type* := { I : ideal A // I.is_homogeneous ğ’œ }

lemma ideal.is_homogeneous.bot : ideal.is_homogeneous ğ’œ âŠ¥ := Î» i r hr,
begin
  simp only [ideal.mem_bot] at hr,
  rw [hr, alg_equiv.map_zero, zero_apply],
  apply ideal.zero_mem
end

instance homogeneous_ideal.inhabited : inhabited (homogeneous_ideal ğ’œ) :=
{ default := âŸ¨âŠ¥, ideal.is_homogeneous.bot _âŸ©}

instance homogeneous_ideal.has_top :
  has_top (homogeneous_ideal ğ’œ) :=
âŸ¨âŸ¨âŠ¤, Î» _ _ _, by simp only [submodule.mem_top]âŸ©âŸ©

@[simp] lemma homogeneous_ideal.eq_top_iff (I : homogeneous_ideal ğ’œ) : I = âŠ¤ â†” I.1 = âŠ¤ :=
subtype.ext_iff

instance homogeneous_ideal.order : partial_order (homogeneous_ideal ğ’œ) :=
partial_order.lift _ subtype.coe_injective

instance homogeneous_ideal.has_mem : has_mem A (homogeneous_ideal ğ’œ) :=
{ mem := Î» r I, r âˆˆ I.1 }

variables {ğ’œ}

lemma ideal.is_homogeneous.inf {I J : ideal A}
  (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) : (I âŠ“ J).is_homogeneous ğ’œ :=
Î» i r hr, âŸ¨HI _ hr.1, HJ _ hr.2âŸ©

lemma ideal.is_homogeneous.Inf {â„ : set (ideal A)} (h : âˆ€ I âˆˆ â„, ideal.is_homogeneous ğ’œ I) :
  (Inf â„).is_homogeneous ğ’œ :=
begin
  intros i x Hx, simp only [ideal.mem_Inf] at Hx âŠ¢,
  intros J HJ,
  exact h _ HJ _ (Hx HJ),
end

lemma ideal.is_homogeneous.mul {I J : ideal A}
  (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) : (I * J).is_homogeneous ğ’œ :=
begin
  rw ideal.is_homogeneous.iff_exists at HI HJ âŠ¢,
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := âŸ¨HI, HJâŸ©,
  rw ideal.span_mul_span',
  refine âŸ¨sâ‚ * sâ‚‚, congr_arg _ _âŸ©,
  exact (set.image_mul (submonoid.subtype _).to_mul_hom).symm,
end

lemma ideal.is_homogeneous.sup {I J : ideal A}
  (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) : (I âŠ” J).is_homogeneous ğ’œ :=
begin
  rw ideal.is_homogeneous.iff_exists at HI HJ âŠ¢,
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := âŸ¨HI, HJâŸ©,
  refine âŸ¨sâ‚ âˆª sâ‚‚, _âŸ©,
  rw [set.image_union],
  exact (submodule.span_union _ _).symm,
end

lemma ideal.is_homogeneous.Sup
  {â„ : set (ideal A)} (Hâ„ : âˆ€ (I âˆˆ â„), ideal.is_homogeneous ğ’œ I) :
  (Sup â„).is_homogeneous ğ’œ :=
begin
  simp_rw ideal.is_homogeneous.iff_exists at Hâ„ âŠ¢,
  choose ğ“ˆ hğ“ˆ using Hâ„,
  refine âŸ¨â‹ƒ I hI, ğ“ˆ I hI, _âŸ©,
  simp_rw [set.image_Union, ideal.span_Union, Sup_eq_supr],
  conv in (ideal.span _) { rw â†hğ“ˆ i x },
end

variables (ğ’œ)

instance : has_inf (homogeneous_ideal ğ’œ) :=
{ inf := Î» I J, âŸ¨I âŠ“ J, I.prop.inf J.propâŸ© }

instance : has_Inf (homogeneous_ideal ğ’œ) :=
{ Inf := Î» â„, âŸ¨Inf (coe '' â„), ideal.is_homogeneous.Inf $ Î» _ âŸ¨I, _, hIâŸ©, hI â–¸ I.propâŸ© }

instance : has_sup (homogeneous_ideal ğ’œ) :=
{ sup := Î» I J, âŸ¨I âŠ” J, I.prop.sup J.propâŸ© }

instance : has_Sup (homogeneous_ideal ğ’œ) :=
{ Sup := Î» â„, âŸ¨Sup (coe '' â„), ideal.is_homogeneous.Sup $ Î» _ âŸ¨I, _, hIâŸ©, hI â–¸ I.propâŸ© }

instance : has_mul (homogeneous_ideal ğ’œ) :=
{ mul := Î» I J, âŸ¨I * J, I.prop.mul J.propâŸ© }

instance : has_add (homogeneous_ideal ğ’œ) := âŸ¨(âŠ”)âŸ©

end operations

section homogeneous_core

variables {Î¹ R A : Type*} [comm_ring R] [comm_ring A]
variables [algebra R A] [decidable_eq Î¹] [add_comm_monoid Î¹]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]
variable (I : ideal A)

lemma ideal.homogeneous_core.gc :
  galois_connection
    (subtype.val : homogeneous_ideal ğ’œ â†’ ideal A)
    (Î» I, âŸ¨I.homogeneous_core ğ’œ, I.is_homogeneous_homogeneous_core ğ’œâŸ© :
      ideal A â†’ homogeneous_ideal ğ’œ) :=
Î» I J, âŸ¨
  Î» H, show I.1 â‰¤ ideal.homogeneous_core ğ’œ J, begin
    rw I.2.homogeneous_core_eq_self,
    exact ideal.homogeneous_core_is_mono ğ’œ H,
  end,
  Î» H, le_trans H (ideal.homogeneous_core_le_ideal _ _)âŸ©

/--There is a galois coinsertion between homogeneous ideals and ideals via
`(Î» I, I.1)` and `ideal.homogeneous_core`-/
def ideal.homogeneous_core.gi :
  galois_coinsertion
    (subtype.val : homogeneous_ideal ğ’œ â†’ ideal A)
    (Î» I, âŸ¨I.homogeneous_core ğ’œ, I.is_homogeneous_homogeneous_core ğ’œâŸ© :
      ideal A â†’ homogeneous_ideal ğ’œ) :=
{ choice := Î» I HI, âŸ¨I, begin
    have eq : I = I.homogeneous_core ğ’œ,
    refine le_antisymm HI _,
    apply (ideal.homogeneous_core_le_ideal ğ’œ I),
    rw eq, apply ideal.is_homogeneous_homogeneous_core,
  endâŸ©,
  gc := ideal.homogeneous_core.gc ğ’œ,
  u_l_le := Î» I, by apply ideal.homogeneous_core_le_ideal,
  choice_eq := Î» I H, le_antisymm H (I.homogeneous_core_le_ideal _) }

lemma ideal.homogeneous_core_eq_Sup :
  I.homogeneous_core ğ’œ = Sup {J : ideal A | J.is_homogeneous ğ’œ âˆ§ J â‰¤ I} :=
begin
  refine (is_lub.Sup_eq _).symm,
  apply is_greatest.is_lub,
  have coe_mono : monotone (coe : {I : ideal A // I.is_homogeneous ğ’œ} â†’ ideal A) := Î» _ _, id,
  convert coe_mono.map_is_greatest (ideal.homogeneous_core.gc ğ’œ).is_greatest_u using 1,
  simp only [subtype.coe_image, exists_prop, mem_set_of_eq, subtype.coe_mk],
end

end homogeneous_core

section homogeneous_hull

variables {Î¹ R A : Type*} [comm_ring R] [comm_ring A]
variables [algebra R A] [decidable_eq Î¹] [add_comm_monoid Î¹]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]
variable (I : ideal A)

/--For any `I : ideal R`, not necessarily homogeneous, there is a homogeneous ideal associated with
`I` spanned by all homogeneous components of elements in `I`. -/
def ideal.homogeneous_hull : ideal A :=
ideal.span {r : A | âˆƒ (i : Î¹) (x : I), (graded_algebra.decompose ğ’œ x i : A) = r}

lemma ideal.is_homogeneous.homogeneous_hull : (I.homogeneous_hull ğ’œ).is_homogeneous ğ’œ :=
begin
  rw ideal.is_homogeneous.iff_exists,
  use {x : homogeneous_submonoid ğ’œ | âˆƒ (i : Î¹) (r : I), (graded_algebra.decompose ğ’œ r i : A) = x},
  rw [ideal.homogeneous_hull], congr, ext r, split; intros h,
  { obtain âŸ¨i, âŸ¨x, hx1âŸ©, hx2âŸ© := h,
    exact âŸ¨âŸ¨_, is_homogeneous_coe _âŸ©, âŸ¨âŸ¨i, âŸ¨âŸ¨x, hx1âŸ©, rflâŸ©âŸ©, hx2âŸ©âŸ©,},
  { obtain âŸ¨_, âŸ¨âŸ¨i, âŸ¨âŸ¨r, hrâŸ©, hâŸ©âŸ©, rflâŸ©âŸ© := h,
    use i, use âŸ¨r, hrâŸ©, exact h }
end

lemma ideal.ideal_le_homogeneous_hull :
  I â‰¤ ideal.homogeneous_hull ğ’œ I :=
begin
  intros r hr,
  letI : Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0) := Î» _ _, classical.dec _,
  rw [â†graded_algebra.sum_support_decompose ğ’œ r, ideal.homogeneous_hull],
  refine ideal.sum_mem _ _, intros j hj,
  apply ideal.subset_span, use j, use âŸ¨r, hrâŸ©, refl,
end

lemma ideal.homogeneous_hull_is_mono : monotone (ideal.homogeneous_hull ğ’œ) := Î» I J I_le_J,
begin
  apply ideal.span_mono, rintros r âŸ¨hr1, âŸ¨x, hxâŸ©, rflâŸ©,
  refine âŸ¨hr1, âŸ¨âŸ¨x, I_le_J hxâŸ©, rflâŸ©âŸ©,
end

lemma ideal.homogeneous_hull.eq_Inf :
  ideal.homogeneous_hull ğ’œ I = Inf { J : ideal A | J.is_homogeneous ğ’œ âˆ§ I â‰¤ J } :=
begin
  ext, split; intros hx,
  { rw ideal.mem_Inf, rintros K âŸ¨HK1, HK2âŸ©,
    rw [ideal.homogeneous_hull, ideal.mem_span] at hx,
    apply hx K, rintros r âŸ¨i, âŸ¨âŸ¨y, hyâŸ©, rflâŸ©âŸ©,
    apply HK1, apply HK2, exact hy, },
  { rw ideal.mem_Inf at hx,
    refine @hx (ideal.homogeneous_hull ğ’œ I) _,
    exact âŸ¨ideal.is_homogeneous.homogeneous_hull _ _, ideal.ideal_le_homogeneous_hull _ _âŸ©, }
end

lemma homogeneous_hull_eq_supr :
  I.homogeneous_hull ğ’œ = â¨† i, ideal.span (graded_algebra.proj ğ’œ i '' I) :=
begin
  rw â†ideal.span_Union,
  apply congr_arg ideal.span _,
  ext1, simp only [set.mem_Union, set.mem_image, mem_set_of_eq, graded_algebra.proj_apply,
    set_like.exists, exists_prop, subtype.coe_mk, set_like.mem_coe],
end

variables {ğ’œ I}

lemma ideal.is_homogeneous.homogeneous_hull_eq_self (h : I.is_homogeneous ğ’œ) :
  ideal.homogeneous_hull ğ’œ I = I :=
begin
  rw ideal.homogeneous_hull.eq_Inf, ext x, split; intros hx,
  rw ideal.mem_Inf at hx, apply hx, refine âŸ¨h, le_refl IâŸ©,
  rw ideal.mem_Inf, rintros J âŸ¨HJ1, HJ2âŸ©, apply HJ2, exact hx,
end

variables (ğ’œ I)

end homogeneous_hull

section galois_connection

variables {Î¹ R A : Type*} [comm_ring R] [comm_ring A]
variables [algebra R A] [decidable_eq Î¹] [add_comm_monoid Î¹]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]

lemma ideal.homgeneous_hull.gc :
  galois_connection
    (Î» I, âŸ¨ideal.homogeneous_hull ğ’œ I, ideal.is_homogeneous.homogeneous_hull ğ’œ IâŸ© :
      ideal A â†’ homogeneous_ideal ğ’œ)
    (Î» I, I.1 : homogeneous_ideal ğ’œ â†’ ideal A) :=
Î» I J,
âŸ¨ Î» H, begin
    refine le_trans _ H,
    apply ideal.ideal_le_homogeneous_hull,
  end,
  Î» H, begin
    show ideal.homogeneous_hull ğ’œ I â‰¤ J.val,
    rw â†J.2.homogeneous_hull_eq_self,
    exact ideal.homogeneous_hull_is_mono ğ’œ H,
  end âŸ©


/--There is a galois insertion between homogeneous ideals and ideals via
`ideal.homgeneous_hull A` and `(Î» I, I.1)`-/
def ideal.homogeneous_hull.gi :
  galois_insertion
    (Î» I, âŸ¨ideal.homogeneous_hull ğ’œ I, ideal.is_homogeneous.homogeneous_hull ğ’œ IâŸ© :
      ideal A â†’ homogeneous_ideal ğ’œ)
    (subtype.val : homogeneous_ideal ğ’œ â†’ ideal A) :=
{ choice := Î» I H, âŸ¨I, begin
    have eq : I = ideal.homogeneous_hull ğ’œ I,
    have ineq1 : I â‰¤ ideal.homogeneous_hull ğ’œ I := ideal.ideal_le_homogeneous_hull ğ’œ I,
    exact le_antisymm ineq1 H,
    rw eq, apply ideal.is_homogeneous.homogeneous_hull,
  endâŸ©,
  gc := ideal.homgeneous_hull.gc ğ’œ,
  le_l_u := Î» âŸ¨I, HIâŸ©, by { apply ideal.ideal_le_homogeneous_hull },
  choice_eq := Î» I H, begin
    refine le_antisymm _ H, apply ideal.ideal_le_homogeneous_hull,
  end }

end galois_connection
