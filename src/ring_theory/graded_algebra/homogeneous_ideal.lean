/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/

import ring_theory.ideal.basic
import ring_theory.ideal.operations
import linear_algebra.finsupp
import ring_theory.graded_algebra.basic

/-!
# Homogeneous ideals of a graded algebra

This file defines homogeneous ideals of `graded_algebra ğ’œ` where `ğ’œ : Î¹ â†’ submodule R A` and
operations on them.

## Main definitions

For any `I : ideal A`:
* `ideal.is_homogeneous ğ’œ I`: The property that an ideal is closed under `graded_algebra.proj`.
* `homogeneous_ideal ğ’œ`: The subtype of ideals which satisfy `ideal.is_homogeneous`
* `ideal.homogeneous_core I ğ’œ`: The largest homogeneous ideal smaller than `I`.
* `ideal.homogeneous_hull I ğ’œ`: The smallest homogeneous ideal larger than `I`.

## Main statements

* `homogeneous_ideal.complete_lattice`: `ideal.is_homogeneous` is preserved by `âŠ¥`, `âŠ¤`, `âŠ”`, `âŠ“`,
  `â¨†`, `â¨…`, and so the subtype of homogeneous ideals inherits a complete lattice structure.
* `ideal.homogeneous_core.gi`: `ideal.homogeneous_core` forms a galois insertion with coercion.
* `ideal.homogeneous_hull.gi`: `ideal.homogeneous_hull` forms a galois insertion with coercion.

## Implementation notes

We introduce `ideal.homogeneous_core'` earlier than might be expected so that we can get access
to `ideal.is_homogeneous.iff_exists` as quickly as possible.

## Tags

graded algebra, homogeneous
-/

open set_like direct_sum set
open_locale big_operators pointwise direct_sum

variables {Î¹ R A : Type*}

section homogeneous_def

variables [comm_semiring R] [semiring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A)
variables [decidable_eq Î¹] [add_monoid Î¹] [graded_algebra ğ’œ]
variable (I : ideal A)

/--An `I : ideal A` is homogeneous if for every `r âˆˆ I`, all homogeneous components
  of `r` are in `I`.-/
def ideal.is_homogeneous : Prop :=
âˆ€ (i : Î¹) â¦ƒr : Aâ¦„, r âˆˆ I â†’ (graded_algebra.decompose ğ’œ r i : A) âˆˆ I

/-- For any `semiring A`, we collect the homogeneous ideals of `A` into a type. -/
abbreviation homogeneous_ideal : Type* := { I : ideal A // I.is_homogeneous ğ’œ }

instance : has_mem A (homogeneous_ideal ğ’œ) :=
{ mem := Î» r I, r âˆˆ (I : ideal A) }

end homogeneous_def

section homogeneous_core

variables [comm_semiring R] [semiring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A)
variable (I : ideal A)

/-- For any `I : ideal A`, not necessarily homogeneous, `I.homogeneous_core' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`, as an ideal. -/
def ideal.homogeneous_core' : ideal A :=
ideal.span (coe '' ((coe : subtype (is_homogeneous ğ’œ) â†’ A) â»Â¹' I))

lemma ideal.homogeneous_core'_mono : monotone (ideal.homogeneous_core' ğ’œ) :=
Î» I J I_le_J, ideal.span_mono $ set.image_subset _ $ Î» x, @I_le_J _

lemma ideal.homogeneous_core'_le : I.homogeneous_core' ğ’œ â‰¤ I :=
ideal.span_le.2 $ image_preimage_subset _ _

end homogeneous_core

section is_homogeneous_ideal_defs

variables [comm_semiring R] [semiring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A)
variables [decidable_eq Î¹] [add_monoid Î¹] [graded_algebra ğ’œ]
variable (I : ideal A)

lemma ideal.is_homogeneous_iff_forall_subset :
  I.is_homogeneous ğ’œ â†” âˆ€ i, (I : set A) âŠ† graded_algebra.proj ğ’œ i â»Â¹' I :=
iff.rfl

lemma ideal.is_homogeneous_iff_subset_Inter :
  I.is_homogeneous ğ’œ â†” (I : set A) âŠ† â‹‚ i, graded_algebra.proj ğ’œ i â»Â¹' â†‘I :=
subset_Inter_iff.symm

lemma ideal.mul_homogeneous_element_mem_of_mem
  {I : ideal A} (r x : A) (hxâ‚ : is_homogeneous ğ’œ x) (hxâ‚‚ : x âˆˆ I) (j : Î¹) :
  graded_algebra.proj ğ’œ j (r * x) âˆˆ I :=
begin
  letI : Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0) := Î» _ _, classical.dec _,
  rw [â†graded_algebra.sum_support_decompose ğ’œ r, finset.sum_mul, linear_map.map_sum],
  apply ideal.sum_mem,
  intros k hk,
  obtain âŸ¨i, hiâŸ© := hxâ‚,
  have memâ‚ : (graded_algebra.decompose ğ’œ r k : A) * x âˆˆ ğ’œ (k + i) := graded_monoid.mul_mem
    (submodule.coe_mem _) hi,
  erw [graded_algebra.proj_apply, graded_algebra.decompose_of_mem ğ’œ memâ‚,
    coe_of_submodule_apply ğ’œ, submodule.coe_mk],
  split_ifs,
  { exact I.mul_mem_left _ hxâ‚‚ },
  { exact I.zero_mem },
end

lemma ideal.is_homogeneous_span (s : set A) (h : âˆ€ x âˆˆ s, is_homogeneous ğ’œ x) :
  (ideal.span s).is_homogeneous ğ’œ :=
begin
  rintros i r hr,
  rw [ideal.span, finsupp.span_eq_range_total] at hr,
  rw linear_map.mem_range at hr,
  obtain âŸ¨s, rflâŸ© := hr,
  rw [â†graded_algebra.proj_apply, finsupp.total_apply, finsupp.sum, linear_map.map_sum],
  refine ideal.sum_mem _ _,
  rintros z hz1,
  rw [smul_eq_mul],
  refine ideal.mul_homogeneous_element_mem_of_mem ğ’œ (s z) z _ _ i,
  { rcases z with âŸ¨z, hz2âŸ©,
    apply h _ hz2, },
  { exact ideal.subset_span z.2 },
end

/--For any `I : ideal A`, not necessarily homogeneous, `I.homogeneous_core' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`.-/
def ideal.homogeneous_core : homogeneous_ideal ğ’œ :=
âŸ¨ideal.homogeneous_core' ğ’œ I,
  ideal.is_homogeneous_span _ _ (Î» x h, by { rw [subtype.image_preimage_coe] at h, exact h.2 })âŸ©

lemma ideal.homogeneous_core_mono : monotone (ideal.homogeneous_core ğ’œ) :=
ideal.homogeneous_core'_mono ğ’œ

lemma ideal.coe_homogeneous_core_le : â†‘(I.homogeneous_core ğ’œ) â‰¤ I :=
ideal.homogeneous_core'_le ğ’œ I

variables {ğ’œ I}

lemma ideal.mem_homogeneous_core_of_is_homogeneous_of_mem {x : A}
  (h : set_like.is_homogeneous ğ’œ x) (hmem : x âˆˆ I) : x âˆˆ I.homogeneous_core ğ’œ :=
ideal.subset_span âŸ¨âŸ¨x, hâŸ©, hmem, rflâŸ©

lemma ideal.is_homogeneous.coe_homogeneous_core_eq_self (h : I.is_homogeneous ğ’œ) :
  â†‘(I.homogeneous_core ğ’œ) = I :=
begin
  apply le_antisymm (I.homogeneous_core'_le ğ’œ) _,
  intros x hx,
  letI : Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0) := Î» _ _, classical.dec _,
  rw â†graded_algebra.sum_support_decompose ğ’œ x,
  exact ideal.sum_mem _ (Î» j hj, ideal.subset_span âŸ¨âŸ¨_, is_homogeneous_coe _âŸ©, h _ hx, rflâŸ©)
end

@[simp] lemma homogeneous_ideal.homogeneous_core_coe_eq_self (I : homogeneous_ideal ğ’œ) :
  (I : ideal A).homogeneous_core ğ’œ = I :=
subtype.coe_injective $ ideal.is_homogeneous.coe_homogeneous_core_eq_self I.prop

variables (ğ’œ I)

lemma ideal.is_homogeneous.iff_eq : I.is_homogeneous ğ’œ â†” â†‘(I.homogeneous_core ğ’œ) = I :=
âŸ¨ Î» hI, hI.coe_homogeneous_core_eq_self,
  Î» hI, hI â–¸ (ideal.homogeneous_core ğ’œ I).2 âŸ©

lemma ideal.is_homogeneous.iff_exists :
  I.is_homogeneous ğ’œ â†” âˆƒ (S : set (homogeneous_submonoid ğ’œ)), I = ideal.span (coe '' S) :=
begin
  rw [ideal.is_homogeneous.iff_eq, eq_comm],
  exact ((set.image_preimage.compose (submodule.gi _ _).gc).exists_eq_l _).symm,
end

end is_homogeneous_ideal_defs

/-! ### Operations

In this section, we show that `ideal.is_homogeneous` is preserved by various notations, then use
these results to provide these notation typeclasses for `homogeneous_ideal`. -/

section operations

section semiring

variables [comm_semiring R] [semiring A] [algebra R A]
variables [decidable_eq Î¹] [add_monoid Î¹]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]

namespace ideal.is_homogeneous

lemma bot : ideal.is_homogeneous ğ’œ âŠ¥ := Î» i r hr,
begin
  simp only [ideal.mem_bot] at hr,
  rw [hr, alg_equiv.map_zero, zero_apply],
  apply ideal.zero_mem
end

lemma top : ideal.is_homogeneous ğ’œ âŠ¤ :=
Î» i r hr, by simp only [submodule.mem_top]

variables {ğ’œ}

lemma inf {I J : ideal A} (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) :
  (I âŠ“ J).is_homogeneous ğ’œ :=
Î» i r hr, âŸ¨HI _ hr.1, HJ _ hr.2âŸ©

lemma Inf {â„ : set (ideal A)} (h : âˆ€ I âˆˆ â„, ideal.is_homogeneous ğ’œ I) :
  (Inf â„).is_homogeneous ğ’œ :=
begin
  intros i x Hx,
  simp only [ideal.mem_Inf] at Hx âŠ¢,
  intros J HJ,
  exact h _ HJ _ (Hx HJ),
end

lemma sup {I J : ideal A} (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) :
  (I âŠ” J).is_homogeneous ğ’œ :=
begin
  rw iff_exists at HI HJ âŠ¢,
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := âŸ¨HI, HJâŸ©,
  refine âŸ¨sâ‚ âˆª sâ‚‚, _âŸ©,
  rw [set.image_union],
  exact (submodule.span_union _ _).symm,
end

lemma Sup {â„ : set (ideal A)} (Hâ„ : âˆ€ (I âˆˆ â„), ideal.is_homogeneous ğ’œ I) :
  (Sup â„).is_homogeneous ğ’œ :=
begin
  simp_rw iff_exists at Hâ„ âŠ¢,
  choose ğ“ˆ hğ“ˆ using Hâ„,
  refine âŸ¨â‹ƒ I hI, ğ“ˆ I hI, _âŸ©,
  simp_rw [set.image_Union, ideal.span_Union, Sup_eq_supr],
  conv in (ideal.span _) { rw â†hğ“ˆ i x },
end

end ideal.is_homogeneous

variables {ğ’œ}

namespace homogeneous_ideal

instance : partial_order (homogeneous_ideal ğ’œ) :=
partial_order.lift _ subtype.coe_injective

instance : has_bot (homogeneous_ideal ğ’œ) :=
âŸ¨âŸ¨âŠ¥, ideal.is_homogeneous.bot ğ’œâŸ©âŸ©

@[simp] lemma coe_bot : â†‘(âŠ¥ : homogeneous_ideal ğ’œ) = (âŠ¥ : ideal A) := rfl

@[simp] lemma eq_bot_iff (I : homogeneous_ideal ğ’œ) : I = âŠ¥ â†” (I : ideal A) = âŠ¥ :=
subtype.ext_iff

instance : has_top (homogeneous_ideal ğ’œ) :=
âŸ¨âŸ¨âŠ¤, ideal.is_homogeneous.top ğ’œâŸ©âŸ©

@[simp] lemma coe_top : â†‘(âŠ¤ : homogeneous_ideal ğ’œ) = (âŠ¤ : ideal A) := rfl

@[simp] lemma eq_top_iff (I : homogeneous_ideal ğ’œ) : I = âŠ¤ â†” (I : ideal A) = âŠ¤ :=
subtype.ext_iff

instance : has_inf (homogeneous_ideal ğ’œ) :=
{ inf := Î» I J, âŸ¨I âŠ“ J, I.prop.inf J.propâŸ© }

@[simp] lemma coe_inf (I J : homogeneous_ideal ğ’œ) : â†‘(I âŠ“ J) = (I âŠ“ J : ideal A) := rfl

instance : has_Inf (homogeneous_ideal ğ’œ) :=
{ Inf := Î» â„, âŸ¨Inf (coe '' â„), ideal.is_homogeneous.Inf $ Î» _ âŸ¨I, _, hIâŸ©, hI â–¸ I.propâŸ© }

@[simp] lemma coe_Inf (â„ : set (homogeneous_ideal ğ’œ)) : â†‘(Inf â„) = (Inf (coe '' â„) : ideal A) :=
rfl

@[simp] lemma coe_infi {Î¹' : Sort*} (s : Î¹' â†’ homogeneous_ideal ğ’œ) :
  â†‘(â¨… i, s i) = â¨… i, (s i : ideal A) :=
by rw [infi, infi, coe_Inf, â†set.range_comp]

instance : has_sup (homogeneous_ideal ğ’œ) :=
{ sup := Î» I J, âŸ¨I âŠ” J, I.prop.sup J.propâŸ© }

@[simp] lemma coe_sup (I J : homogeneous_ideal ğ’œ) : â†‘(I âŠ” J) = (I âŠ” J : ideal A) := rfl

instance : has_Sup (homogeneous_ideal ğ’œ) :=
{ Sup := Î» â„, âŸ¨Sup (coe '' â„), ideal.is_homogeneous.Sup $ Î» _ âŸ¨I, _, hIâŸ©, hI â–¸ I.propâŸ© }

@[simp] lemma coe_Sup (â„ : set (homogeneous_ideal ğ’œ)) : â†‘(Sup â„) = (Sup (coe '' â„) : ideal A) :=
rfl

@[simp] lemma coe_supr {Î¹' : Sort*} (s : Î¹' â†’ homogeneous_ideal ğ’œ) :
  â†‘(â¨† i, s i) = â¨† i, (s i : ideal A) :=
by rw [supr, supr, coe_Sup, â†set.range_comp]

instance : complete_lattice (homogeneous_ideal ğ’œ) :=
subtype.coe_injective.complete_lattice _ coe_sup coe_inf coe_Sup coe_Inf coe_top coe_bot

instance : has_add (homogeneous_ideal ğ’œ) := âŸ¨(âŠ”)âŸ©

@[simp] lemma coe_add (I J : homogeneous_ideal ğ’œ) : â†‘(I + J) = (I + J : ideal A) := rfl

instance : inhabited (homogeneous_ideal ğ’œ) := { default := âŠ¥ }

end homogeneous_ideal

end semiring

section comm_semiring
variables [comm_semiring R] [comm_semiring A] [algebra R A]
variables [decidable_eq Î¹] [add_monoid Î¹]
variables {ğ’œ : Î¹ â†’ submodule R A} [graded_algebra ğ’œ]
variable (I : ideal A)

lemma ideal.is_homogeneous.mul {I J : ideal A}
  (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) : (I * J).is_homogeneous ğ’œ :=
begin
  rw ideal.is_homogeneous.iff_exists at HI HJ âŠ¢,
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := âŸ¨HI, HJâŸ©,
  rw ideal.span_mul_span',
  refine âŸ¨sâ‚ * sâ‚‚, congr_arg _ _âŸ©,
  exact (set.image_mul (submonoid.subtype _).to_mul_hom).symm,
end

variables {ğ’œ}

instance : has_mul (homogeneous_ideal ğ’œ) :=
{ mul := Î» I J, âŸ¨I * J, I.prop.mul J.propâŸ© }

@[simp] lemma homogeneous_ideal.coe_mul (I J : homogeneous_ideal ğ’œ) :
  â†‘(I * J) = (I * J : ideal A) := rfl

end comm_semiring

end operations

/-! ### Homogeneous core

Note that many results about the homogeneous core came earlier in this file, as they are helpful
for building the lattice structure. -/

section homogeneous_core

variables [comm_semiring R] [semiring A]
variables [algebra R A] [decidable_eq Î¹] [add_monoid Î¹]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]
variable (I : ideal A)

lemma ideal.homogeneous_core.gc : galois_connection coe (ideal.homogeneous_core ğ’œ) :=
Î» I J, âŸ¨
  Î» H, I.homogeneous_core_coe_eq_self â–¸ ideal.homogeneous_core_mono ğ’œ H,
  Î» H, le_trans H (ideal.homogeneous_core'_le _ _)âŸ©

/--`coe : homogeneous_ideal ğ’œ â†’ ideal A` and `ideal.homogeneous_core ğ’œ` forms a galois
coinsertion-/
def ideal.homogeneous_core.gi : galois_coinsertion coe (ideal.homogeneous_core ğ’œ) :=
{ choice := Î» I HI, âŸ¨I, le_antisymm (I.coe_homogeneous_core_le ğ’œ) HI â–¸ subtype.prop _âŸ©,
  gc := ideal.homogeneous_core.gc ğ’œ,
  u_l_le := Î» I, ideal.homogeneous_core'_le _ _,
  choice_eq := Î» I H, le_antisymm H (I.coe_homogeneous_core_le _) }

lemma ideal.homogeneous_core_eq_Sup :
  I.homogeneous_core ğ’œ = Sup {J : homogeneous_ideal ğ’œ | â†‘J â‰¤ I} :=
eq.symm $ is_lub.Sup_eq $ (ideal.homogeneous_core.gc ğ’œ).is_greatest_u.is_lub

lemma ideal.homogeneous_core'_eq_Sup :
  I.homogeneous_core' ğ’œ = Sup {J : ideal A | J.is_homogeneous ğ’œ âˆ§ J â‰¤ I} :=
begin
  refine (is_lub.Sup_eq _).symm,
  apply is_greatest.is_lub,
  have coe_mono : monotone (coe : {I : ideal A // I.is_homogeneous ğ’œ} â†’ ideal A) := Î» _ _, id,
  convert coe_mono.map_is_greatest (ideal.homogeneous_core.gc ğ’œ).is_greatest_u using 1,
  simp only [subtype.coe_image, exists_prop, mem_set_of_eq, subtype.coe_mk],
end

end homogeneous_core

/-! ### Homogeneous hulls -/

section homogeneous_hull

variables [comm_semiring R] [semiring A]
variables [algebra R A] [decidable_eq Î¹] [add_monoid Î¹]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]
variable (I : ideal A)

/--For any `I : ideal A`, not necessarily homogeneous, `I.homogeneous_hull ğ’œ` is
the smallest homogeneous ideal containing `I`. -/
def ideal.homogeneous_hull : homogeneous_ideal ğ’œ :=
âŸ¨ideal.span {r : A | âˆƒ (i : Î¹) (x : I), (graded_algebra.decompose ğ’œ x i : A) = r}, begin
  refine ideal.is_homogeneous_span _ _ (Î» x hx, _),
  obtain âŸ¨i, x, rflâŸ© := hx,
  apply set_like.is_homogeneous_coe
endâŸ©

lemma ideal.le_coe_homogeneous_hull :
  I â‰¤ ideal.homogeneous_hull ğ’œ I :=
begin
  intros r hr,
  letI : Î  (i : Î¹) (x : ğ’œ i), decidable (x â‰  0) := Î» _ _, classical.dec _,
  rw [â†graded_algebra.sum_support_decompose ğ’œ r],
  refine ideal.sum_mem _ _, intros j hj,
  apply ideal.subset_span, use j, use âŸ¨r, hrâŸ©, refl,
end

lemma ideal.homogeneous_hull_mono : monotone (ideal.homogeneous_hull ğ’œ) := Î» I J I_le_J,
begin
  apply ideal.span_mono,
  rintros r âŸ¨hr1, âŸ¨x, hxâŸ©, rflâŸ©,
  refine âŸ¨hr1, âŸ¨âŸ¨x, I_le_J hxâŸ©, rflâŸ©âŸ©,
end

variables {I ğ’œ}

lemma ideal.is_homogeneous.homogeneous_hull_eq_self (h : I.is_homogeneous ğ’œ) :
  â†‘(ideal.homogeneous_hull ğ’œ I) = I :=
begin
  apply le_antisymm _ (ideal.le_coe_homogeneous_hull _ _),
  apply (ideal.span_le).2,
  rintros _ âŸ¨i, x, rflâŸ©,
  exact h _ x.prop,
end

@[simp] lemma homogeneous_ideal.homogeneous_hull_coe_eq_self (I : homogeneous_ideal ğ’œ) :
  (I : ideal A).homogeneous_hull ğ’œ = I :=
subtype.coe_injective $ ideal.is_homogeneous.homogeneous_hull_eq_self I.prop

variables (I ğ’œ)

lemma ideal.coe_homogeneous_hull_eq_supr :
  â†‘(I.homogeneous_hull ğ’œ) = â¨† i, ideal.span (graded_algebra.proj ğ’œ i '' I) :=
begin
  rw â†ideal.span_Union,
  apply congr_arg ideal.span _,
  ext1,
  simp only [set.mem_Union, set.mem_image, mem_set_of_eq, graded_algebra.proj_apply,
    set_like.exists, exists_prop, subtype.coe_mk, set_like.mem_coe],
end

lemma ideal.homogeneous_hull_eq_supr :
  (I.homogeneous_hull ğ’œ) =
  â¨† i, âŸ¨ideal.span (graded_algebra.proj ğ’œ i '' I), ideal.is_homogeneous_span ğ’œ _
    (by {rintros _ âŸ¨x, -, rflâŸ©, apply set_like.is_homogeneous_coe})âŸ© :=
by { ext1, rw [ideal.coe_homogeneous_hull_eq_supr, homogeneous_ideal.coe_supr], refl, }

end homogeneous_hull

section galois_connection

variables [comm_semiring R] [semiring A]
variables [algebra R A] [decidable_eq Î¹] [add_monoid Î¹]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]

lemma ideal.homogeneous_hull.gc : galois_connection (ideal.homogeneous_hull ğ’œ) coe :=
Î» I J, âŸ¨
  le_trans (ideal.le_coe_homogeneous_hull _ _),
  Î» H, J.homogeneous_hull_coe_eq_self â–¸ ideal.homogeneous_hull_mono ğ’œ HâŸ©

/-- `ideal.homogeneous_hull ğ’œ` and `coe : homogeneous_ideal ğ’œ â†’ ideal A` forms a galois insertion-/
def ideal.homogeneous_hull.gi : galois_insertion (ideal.homogeneous_hull ğ’œ) coe :=
{ choice := Î» I H, âŸ¨I, le_antisymm H (I.le_coe_homogeneous_hull ğ’œ) â–¸ subtype.prop _âŸ©,
  gc := ideal.homogeneous_hull.gc ğ’œ,
  le_l_u := Î» I, ideal.le_coe_homogeneous_hull _ _,
  choice_eq := Î» I H, le_antisymm (I.le_coe_homogeneous_hull ğ’œ) H}

lemma ideal.homogeneous_hull_eq_Inf (I : ideal A) :
  ideal.homogeneous_hull ğ’œ I = Inf { J : homogeneous_ideal ğ’œ | I â‰¤ J } :=
eq.symm $ is_glb.Inf_eq $ (ideal.homogeneous_hull.gc ğ’œ).is_least_l.is_glb

end galois_connection
