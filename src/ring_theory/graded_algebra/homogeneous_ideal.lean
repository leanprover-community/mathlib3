/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/

import ring_theory.ideal.basic
import ring_theory.ideal.operations
import linear_algebra.finsupp
import ring_theory.graded_algebra.basic

/-!

# Homogeneous ideal of a graded algebra

This file defines homogeneous ideals of `graded_algebra A` where `A : Œπ ‚Üí submodule R R`and
operations on them:
* `mul`, `inf`, `Inf` of homogeneous ideals are homogeneous;
* `‚ä§`, `‚ä•`, i.e. the trivial ring and `R` are homogeneous;
* `radical` of a homogeneous ideal is homogeneous.
-/

noncomputable theory

section is_homogeneous_ideal_defs

open set_like direct_sum set
open_locale big_operators

variables {Œπ R : Type*} [comm_ring R] [decidable_eq Œπ] [add_comm_monoid Œπ]
variables (A : Œπ ‚Üí submodule R R) [graded_algebra A]
variable (I : ideal R)

/--An `I : ideal R` is called homogeneous if for every `r ‚àà I`, every homogeneous component of `r`
  is in `I`.-/
@[reducible] def is_homogeneous_ideal : Prop :=
  ‚àÄ {i : Œπ} {r : R}, r ‚àà I ‚Üí (graded_algebra.decompose A r i : R) ‚àà I

lemma is_homogeneous_ideal_iff_forall_subset (I : ideal R) :
  is_homogeneous_ideal A I ‚Üî ‚àÄ i, (I : set R) ‚äÜ graded_algebra.proj A i ‚Åª¬π' I :=
iff.rfl

lemma is_homogeneous_ideal_iff_subset_Inter (I : ideal R) :
  is_homogeneous_ideal A I ‚Üî (I : set R) ‚äÜ ‚ãÇ i, graded_algebra.proj A i ‚Åª¬π' ‚ÜëI :=
subset_Inter_iff.symm

lemma is_homogeneous_ideal.exists_iff_eq_span :
  (‚àÉ (S : set (homogeneous_submonoid A)), I = ideal.span (coe '' S)) ‚Üî
  I = ideal.span {x | x ‚àà I ‚àß is_homogeneous A x} :=
‚ü®Œª ‚ü®S, hI‚ü©, begin
    ext r, split; intro hr,
  { rw hI at hr,
    suffices : coe '' S ‚äÜ {x | x ‚àà I ‚àß is_homogeneous A x},
    exact (ideal.span_mono this) hr,
    intros s hs, split, rw hI,
    refine ideal.subset_span hs,
    obtain ‚ü®‚ü®s', homs'‚ü©, hs‚ÇÅ, hs‚ÇÇ‚ü© := hs,
    convert homs', rw ‚Üêhs‚ÇÇ, refl },
  { obtain ‚ü®l, hl‚ü© := (finsupp.mem_span_iff_total R _ _).mp hr,
    rw ‚Üêhl, apply ideal.sum_mem, rintros ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© hx‚ÇÉ,
    simp only [linear_map.id_coe, id.def, finsupp.mem_support_iff, linear_map.coe_smul_right,
      ne.def, smul_eq_mul, subtype.coe_mk] at hx‚ÇÅ hx‚ÇÇ hx‚ÇÉ ‚ä¢,
    exact ideal.mul_mem_left _ _ hx‚ÇÅ, }
  end, Œª hI, ‚ü®{x : homogeneous_submonoid A | ‚Üëx ‚àà I}, begin
    rw hI, congr, ext r, split; intros hr,
    { rcases hr with ‚ü®r_mem, ‚ü®i, r_eq‚ü©‚ü©,
      use r, exact ‚ü®i, r_eq‚ü©, refine ‚ü®_, rfl‚ü©,
      simp only [mem_set_of_eq, subtype.coe_mk], convert ‚Üêr_mem, },
    { rcases hr with ‚ü®‚ü®r', hr'‚ü©, hr‚ÇÅ, hr‚ÇÇ‚ü©,
      simp only [mem_set_of_eq, subtype.coe_mk] at hr‚ÇÅ,
      rw ‚Üêhr‚ÇÇ, rw ‚ÜêhI at hr‚ÇÅ, refine ‚ü®hr‚ÇÅ, hr'‚ü©, }
  end‚ü©‚ü©

variable [Œ† (i : Œπ) (x : A i), decidable (x ‚â† 0)]

lemma is_homogeneous_ideal.mul_homogeneous_element
  {I : ideal R} (r x : R) (hx‚ÇÅ : is_homogeneous A x) (hx‚ÇÇ : x ‚àà I) (j : Œπ) :
  graded_algebra.proj A j (r * x) ‚àà I :=
begin
  rw [‚Üêgraded_algebra.sum_support_decompose A r, finset.sum_mul, linear_map.map_sum],
  apply ideal.sum_mem,
  intros k hk,
  obtain ‚ü®i, hi‚ü© := hx‚ÇÅ,
  have mem‚ÇÅ : (graded_algebra.proj A k) r * x ‚àà A (k + i) := graded_monoid.mul_mem
   (by { rw [graded_algebra.proj_apply], exact submodule.coe_mem _, }) hi,
  by_cases k + i = j,
  { rw ‚Üêh, rw graded_algebra.proj_apply at ‚ä¢ mem‚ÇÅ,
    rw graded_algebra.decompose_of_mem A mem‚ÇÅ, simp only [of_eq_same, submodule.coe_mk],
    apply I.mul_mem_left _ hx‚ÇÇ, },
  { rw [graded_algebra.proj_apply, graded_algebra.decompose_of_mem_ne],
    exact I.zero_mem, exact mem‚ÇÅ, intro rid, apply h rid, }
end

lemma is_homogeneous_ideal.homogeneous_component
  {I : ideal R} (HI : ‚àÉ (S : set (homogeneous_submonoid A)), I = ideal.span (coe '' S))
  (x : R) (hx : x ‚àà I) (i : Œπ) :
  graded_algebra.proj A i x ‚àà I :=
begin
  have HI' := HI,
  rw [is_homogeneous_ideal.exists_iff_eq_span, ideal.span,
      finsupp.span_eq_range_total] at HI',
  rw HI' at hx,
  obtain ‚ü®s, rfl‚ü© := hx,
  rw [finsupp.total_apply, finsupp.sum, linear_map.map_sum],
  refine ideal.sum_mem I _,
  rintros ‚ü®j, ‚ü®hj‚ÇÅ, hj‚ÇÇ‚ü©‚ü© hj‚ÇÉ,
  simp only [algebra.id.smul_eq_mul, subtype.coe_mk, smul_eq_mul],
  apply is_homogeneous_ideal.mul_homogeneous_element, exact hj‚ÇÇ, exact hj‚ÇÅ,
end

lemma is_homogeneous_ideal_iff_eq :
  is_homogeneous_ideal A I ‚Üî I = ideal.span {x | x ‚àà I ‚àß is_homogeneous A x} :=
‚ü® Œª hI, begin
  ext, split; intro hx,
  { rw ‚Üêgraded_algebra.sum_support_decompose A x,
    refine ideal.sum_mem _ _,
    intros j hj,
    replace hI := @hI j x hx,
    rw ideal.mem_span, intros J HJ,
    refine HJ _,
    simp only [mem_set_of_eq],
    refine ‚ü®hI, _‚ü©, refine ‚ü®j, _‚ü©, exact submodule.coe_mem _, },
  { rw [ideal.mem_span] at hx,
    apply hx,
    intros y hy,
    exact hy.1,  },
  end,
  Œª hI, begin
    intros i r hr, rw ‚Üêis_homogeneous_ideal.exists_iff_eq_span at hI,
    apply is_homogeneous_ideal.homogeneous_component A hI r hr,
  end ‚ü©

lemma is_homogeneous_ideal_iff_exists :
  is_homogeneous_ideal A I ‚Üî ‚àÉ (S : set (homogeneous_submonoid A)), I = ideal.span (coe '' S) :=
by rw [is_homogeneous_ideal.exists_iff_eq_span, is_homogeneous_ideal_iff_eq]

lemma is_homogeneous_ideal.tfae : tfae
  [ is_homogeneous_ideal A I
  , ‚àÄ i, (I : set R) ‚äÜ graded_algebra.proj A i ‚Åª¬π' I
  , (I : set R) ‚äÜ ‚ãÇ i, graded_algebra.proj A i ‚Åª¬π' ‚ÜëI
  , ‚àÉ (S : set (homogeneous_submonoid A)), I = ideal.span (coe '' S)
  , I = ideal.span {x | x ‚àà I ‚àß is_homogeneous A x}] :=
begin
  tfae_have : 1‚Üî2, exact is_homogeneous_ideal_iff_forall_subset A I,
  tfae_have : 1‚Üî3, exact is_homogeneous_ideal_iff_subset_Inter A I,
  tfae_have : 4‚Üî5, exact is_homogeneous_ideal.exists_iff_eq_span A I,
  tfae_have : 1‚Üî4, exact is_homogeneous_ideal_iff_exists A I,
  tfae_finish
end

end is_homogeneous_ideal_defs

section operations

open set_like direct_sum set
open_locale big_operators pointwise

variables {Œπ R : Type*} [comm_ring R] [decidable_eq Œπ] [add_comm_monoid Œπ]
variables (A : Œπ ‚Üí submodule R R) [graded_algebra A]
variable (I : ideal R)

/--We collect all homogeneous ideal into a type.-/
def homogeneous_ideal : Type* := { I : ideal R // is_homogeneous_ideal A I }

instance homogeneous_ideal.inhabited : inhabited (homogeneous_ideal A) :=
‚ü®‚ü®‚ä•, begin
    intros i r hr, simp only [ideal.mem_bot] at hr,
    rw [hr, alg_equiv.map_zero, zero_apply],
    apply ideal.zero_mem
  end‚ü©‚ü©

instance homogeneous_ideal.has_bot : has_bot (homogeneous_ideal A) :=
‚ü®‚ü®‚ä•, begin
    intros i r hr, simp only [ideal.mem_bot] at hr,
    rw [hr, alg_equiv.map_zero, zero_apply],
    apply ideal.zero_mem
  end‚ü©‚ü©

instance homogeneous_ideal.has_top :
  has_top (homogeneous_ideal A) :=
‚ü®‚ü®‚ä§, Œª _ _ _, by simp only [submodule.mem_top]‚ü©‚ü©

@[simp] lemma homogeneous_ideal.eq_top_iff
  (I : homogeneous_ideal A) : I = ‚ä§ ‚Üî I.1 = ‚ä§ :=
‚ü® Œª h, by { rw h, refl },
  Œª h, begin
    have h' : I.val = (‚ä§ : homogeneous_ideal A).val,
    rw h, refl,
    apply subtype.val_injective h',
  end ‚ü©

instance homogeneous_ideal.has_le : has_le (homogeneous_ideal A) :=
{ le := Œª I J, I.1 ‚â§ J.1 }

instance homogeneous_ideal.order : preorder (homogeneous_ideal A) :=
{ le_refl := Œª I, by {unfold has_le.le, exact le_refl I.1, },
  le_trans := Œª I J K HIJ HJK,
    by {unfold has_le.le at HIJ HJK ‚ä¢, exact @le_trans _ _ I.1 J.1 K.1 HIJ HJK, },
  ..(homogeneous_ideal.has_le A)}

instance homogeneous_ideal.has_mem : has_mem R (homogeneous_ideal A) :=
{ mem := Œª r I, r ‚àà I.1 }


instance homogeneous_ideal.has_inf : has_inf (homogeneous_ideal A) :=
{ inf := Œª I J, begin
  refine ‚ü®I.1 ‚äì J.1, _‚ü©,
  intros i r hr, split, apply I.2 hr.1, apply J.2 hr.2,
end }

instance homogeneous_ideal.has_Inf  :
  has_Inf (homogeneous_ideal A) :=
{ Inf := Œª ‚Ñê, ‚ü®Inf (set.image (Œª x : homogeneous_ideal A, x.val) ‚Ñê), begin
    intros i x Hx, simp only [submodule.mem_Inf] at Hx ‚ä¢,
    intros J HJ, simp only [set.mem_image, subtype.val_eq_coe] at HJ,
    obtain ‚ü®K, HK‚ÇÅ, HK‚ÇÇ‚ü© := HJ, rw ‚ÜêHK‚ÇÇ,
    have HK‚ÇÉ := K.2,
    apply HK‚ÇÉ, apply Hx, simp only [set.mem_image, subtype.val_eq_coe], use K, exact ‚ü®HK‚ÇÅ, rfl‚ü©,
end‚ü© }


variable [Œ† (i : Œπ) (x : A i), decidable (x ‚â† 0)]

instance homogeneous_ideal.has_mul :
  has_mul (homogeneous_ideal A) :=
{ mul := Œª I J, ‚ü®I.1 * J.1, begin
    have HI := I.2, have HJ := J.2,
    rw is_homogeneous_ideal_iff_exists at HI HJ,
    choose s‚ÇÅ hI using HI,
    choose s‚ÇÇ hJ using HJ,
    rw is_homogeneous_ideal_iff_exists,
    rw [hI, hJ, ideal.span_mul_span'],
    refine ‚ü®s‚ÇÅ * s‚ÇÇ, _‚ü©,
    apply congr_arg,
    ext, split; intro hx,
    { rw set.mem_mul at hx,
      obtain ‚ü®y1, y2, h1, h2, h3‚ü© := hx,
      rw set.mem_image at h1, obtain ‚ü®z1, h1‚ü© := h1,
      have hy1 : y1 ‚àà set_like.homogeneous_submonoid A,
      rw ‚Üêh1.2, exact z1.2,
      rw set.mem_image at h2, obtain ‚ü®z2, h2‚ü© := h2,
      have hy2 : y2 ‚àà set_like.homogeneous_submonoid A,
      rw ‚Üêh2.2, exact z2.2,

      use y1 * y2, apply submonoid.mul_mem,
      exact hy1, exact hy2,
      refine ‚ü®_, h3‚ü©, rw set.mem_mul, use y1, assumption,
      use y2, assumption, tidy, },
    { rw set.mem_image at hx,
      obtain ‚ü®y, hy1, hy‚ü© := hx,
      rw set.mem_mul at hy1 ‚ä¢,
      obtain ‚ü®z1, z2, hz1, hz2, hz3‚ü© := hy1,
      use z1, use z2, split, rw set.mem_image, use z1, refine ‚ü®hz1, rfl‚ü©,
      split, rw set.mem_image, use z2, refine ‚ü®hz2, rfl‚ü©, tidy, }
  end‚ü© }

instance homogeneous_ideal.has_sup : has_sup (homogeneous_ideal A) :=
{ sup := Œª ‚ü®I, HI‚ü© ‚ü®J, HJ‚ü©, ‚ü®I ‚äî J, begin
    rw is_homogeneous_ideal_iff_exists at HI HJ,
    choose s‚ÇÅ hI using HI,
    choose s‚ÇÇ hJ using HJ,
    rw is_homogeneous_ideal_iff_exists,
    refine ‚ü®s‚ÇÅ ‚à™ s‚ÇÇ, _‚ü©,
    rw [set.image_union, ideal.span, hI, hJ],
    exact (submodule.span_union _ _).symm,
end‚ü© }

instance homogeneous_ideal.has_Sup : has_Sup (homogeneous_ideal A) :=
{ Sup := Œª ‚Ñê, ‚ü®Sup (set.image (Œª x : homogeneous_ideal A, x.val) ‚Ñê), begin
    have H‚Ñê : ‚àÄ I : ‚Ñê, is_homogeneous_ideal A I.1.1 := Œª I, I.1.2,
    simp_rw [is_homogeneous_ideal_iff_exists] at H‚Ñê,
    set ùìà : ‚Ñê ‚Üí set (homogeneous_submonoid A) := Œª I : ‚Ñê, Exists.some (H‚Ñê I) with ùìà_eq,
    have hùìà : ‚àÄ I : ‚Ñê, I.1.1 = ideal.span (coe '' ùìà I) := Œª I : ‚Ñê, Exists.some_spec (H‚Ñê I),
    rw is_homogeneous_ideal_iff_exists,
    use sUnion (set.range ùìà),
    rw [sUnion_range, image_Union, ideal.span, submodule.span_Union],
    ext r, split,
    { suffices : Sup ((Œª (x : homogeneous_ideal A), x.val) '' ‚Ñê) ‚â§ _, revert r, exact this,
      rw Sup_le_iff, intros I HI,
      have ineq1 : I ‚â§ ideal.span I := ideal.subset_span, refine le_trans ineq1 _,
      rw ideal.span_le, simp only [coe_subset_coe, ideal.submodule_span_eq], intros x hx,
      simp only [mem_image, subtype.val_eq_coe] at HI,
      obtain ‚ü®I', HI1, HI2‚ü© := HI, rw submodule.mem_supr, intros J HJ,
      apply HJ ‚ü®I', HI1‚ü©, rw ‚Üêhùìà ‚ü®I', HI1‚ü©,simp only [subtype.val_eq_coe], rw HI2, assumption },
    { suffices : _ ‚â§  Sup ((Œª (x : homogeneous_ideal A), x.val) '' ‚Ñê), revert r, exact this,
      rw supr_le_iff, intros I, rw submodule.span_le, intros x hx,
      simp only [mem_image] at hx, obtain ‚ü®x', hx1, hx2‚ü© := hx,
      simp only [mem_coe, subtype.val_eq_coe], dsimp only at hx1,
      apply ideal.mem_Sup_of_mem, simp only [mem_image], use I.1, refine ‚ü®I.2, rfl‚ü©,
      simp only [subtype.val_eq_coe] at hùìà ‚ä¢, rw hùìà,
      refine ideal.subset_span _, rw [mem_image], use x', refine ‚ü®hx1, hx2‚ü©, }
  end‚ü© }

instance homogeneous_ideal.has_add : has_add (homogeneous_ideal A) := ‚ü®(‚äî)‚ü©

end operations

section of_ideal

open set_like direct_sum set

variables {Œπ R : Type*} [comm_ring R]
variables (A : Œπ ‚Üí submodule R R)
variable (I : ideal R)

/--For any `I : ideal R`, not necessarily homogeneous, there is a homogeneous ideal asscoiated with
`I` spaned by all homogeneous elements in `I`. This construction will be used when proving radical
of homogeneous ideal is homogeneous-/
@[reducible] def homogeneous_ideal_of_ideal : ideal R := ideal.span (set_of (is_homogeneous A) ‚à© I)

lemma homogeneous_ideal_of_ideal_le_ideal (I : ideal R) :
  homogeneous_ideal_of_ideal A I ‚â§ I :=
begin
  rw homogeneous_ideal_of_ideal,
  conv_rhs { rw ‚Üêideal.span_eq I },
  apply ideal.span_mono, exact (set_of (is_homogeneous A)).inter_subset_right ‚ÜëI,
end

variables [add_comm_monoid Œπ] [decidable_eq Œπ]
variables [graded_algebra A] [Œ† (i : Œπ) (x : A i), decidable (x ‚â† 0)]

lemma is_homogeneous_ideal.homogeneous_ideal_of_ideal :
  is_homogeneous_ideal A (homogeneous_ideal_of_ideal A I) :=
begin
  rw is_homogeneous_ideal_iff_exists,
  use ({x | ‚Üëx ‚àà I}),
  rw homogeneous_ideal_of_ideal, congr, ext, split; intro hx;
  simp only [mem_inter_eq, mem_set_of_eq, set_like.mem_coe] at hx ‚ä¢,
  use x, exact hx.1, refine ‚ü®hx.2, rfl‚ü©,
  obtain ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü© := hx, simp only [mem_set_of_eq] at hy‚ÇÅ, split, rw ‚Üêhy‚ÇÇ,
  rcases y with ‚ü®y, ‚ü®i, hy‚ÇÉ‚ü©‚ü©, use i, refine hy‚ÇÉ,
  rw ‚Üêhy‚ÇÇ, refine hy‚ÇÅ,
end

end of_ideal
