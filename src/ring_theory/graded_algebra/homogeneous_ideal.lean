/-
Copyright (c) 2021 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import ring_theory.ideal.basic
import ring_theory.ideal.operations
import linear_algebra.finsupp
import ring_theory.graded_algebra.basic
/-!
# Homogeneous ideals of a graded algebra

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines homogeneous ideals of `graded_ring ğ’œ` where `ğ’œ : Î¹ â†’ submodule R A` and
operations on them.

## Main definitions

For any `I : ideal A`:
* `ideal.is_homogeneous ğ’œ I`: The property that an ideal is closed under `graded_ring.proj`.
* `homogeneous_ideal ğ’œ`: The structure extending ideals which satisfy `ideal.is_homogeneous`
* `ideal.homogeneous_core I ğ’œ`: The largest homogeneous ideal smaller than `I`.
* `ideal.homogeneous_hull I ğ’œ`: The smallest homogeneous ideal larger than `I`.

## Main statements

* `homogeneous_ideal.complete_lattice`: `ideal.is_homogeneous` is preserved by `âŠ¥`, `âŠ¤`, `âŠ”`, `âŠ“`,
  `â¨†`, `â¨…`, and so the subtype of homogeneous ideals inherits a complete lattice structure.
* `ideal.homogeneous_core.gi`: `ideal.homogeneous_core` forms a galois insertion with coercion.
* `ideal.homogeneous_hull.gi`: `ideal.homogeneous_hull` forms a galois insertion with coercion.

## Implementation notes

We introduce `ideal.homogeneous_core'` earlier than might be expected so that we can get access
to `ideal.is_homogeneous.iff_exists` as quickly as possible.

## Tags

graded algebra, homogeneous
-/

open set_like direct_sum set
open_locale big_operators pointwise direct_sum

variables {Î¹ Ïƒ R A : Type*}

section homogeneous_def

variables [semiring A]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variables [decidable_eq Î¹] [add_monoid Î¹] [graded_ring ğ’œ]
variable (I : ideal A)
include A

/--An `I : ideal A` is homogeneous if for every `r âˆˆ I`, all homogeneous components
  of `r` are in `I`.-/
def ideal.is_homogeneous : Prop :=
âˆ€ (i : Î¹) â¦ƒr : Aâ¦„, r âˆˆ I â†’ (direct_sum.decompose ğ’œ r i : A) âˆˆ I

/-- For any `semiring A`, we collect the homogeneous ideals of `A` into a type. -/
structure homogeneous_ideal extends submodule A A :=
(is_homogeneous' : ideal.is_homogeneous ğ’œ to_submodule)

variable {ğ’œ}
/--Converting a homogeneous ideal to an ideal-/
def homogeneous_ideal.to_ideal (I : homogeneous_ideal ğ’œ) : ideal A := I.to_submodule

lemma homogeneous_ideal.is_homogeneous (I : homogeneous_ideal ğ’œ) :
  I.to_ideal.is_homogeneous ğ’œ := I.is_homogeneous'

lemma homogeneous_ideal.to_ideal_injective :
  function.injective (homogeneous_ideal.to_ideal : homogeneous_ideal ğ’œ â†’ ideal A) :=
Î» âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© (h : x = y), by simp [h]

instance homogeneous_ideal.set_like : set_like (homogeneous_ideal ğ’œ) A :=
{ coe := Î» I, I.to_ideal,
  coe_injective' := Î» I J h, homogeneous_ideal.to_ideal_injective $ set_like.coe_injective h }

@[ext] lemma homogeneous_ideal.ext {I J : homogeneous_ideal ğ’œ}
  (h : I.to_ideal = J.to_ideal) : I = J := homogeneous_ideal.to_ideal_injective h

@[simp] lemma homogeneous_ideal.mem_iff {I : homogeneous_ideal ğ’œ} {x : A} :
  x âˆˆ I.to_ideal â†” x âˆˆ I := iff.rfl

end homogeneous_def

section homogeneous_core

variables [semiring A]
variables [set_like Ïƒ A]  (ğ’œ : Î¹ â†’ Ïƒ)
variable (I : ideal A)
include A

/-- For any `I : ideal A`, not necessarily homogeneous, `I.homogeneous_core' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`, as an ideal. -/
def ideal.homogeneous_core' (I : ideal A) : ideal A :=
ideal.span (coe '' ((coe : subtype (is_homogeneous ğ’œ) â†’ A) â»Â¹' I))

lemma ideal.homogeneous_core'_mono : monotone (ideal.homogeneous_core' ğ’œ) :=
Î» I J I_le_J, ideal.span_mono $ set.image_subset _ $ Î» x, @I_le_J _

lemma ideal.homogeneous_core'_le : I.homogeneous_core' ğ’œ â‰¤ I :=
ideal.span_le.2 $ image_preimage_subset _ _

end homogeneous_core

section is_homogeneous_ideal_defs

variables [semiring A]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ)
variables [decidable_eq Î¹] [add_monoid Î¹] [graded_ring ğ’œ]
variable (I : ideal A)
include A

lemma ideal.is_homogeneous_iff_forall_subset :
  I.is_homogeneous ğ’œ â†” âˆ€ i, (I : set A) âŠ† graded_ring.proj ğ’œ i â»Â¹' I :=
iff.rfl

lemma ideal.is_homogeneous_iff_subset_Inter :
  I.is_homogeneous ğ’œ â†” (I : set A) âŠ† â‹‚ i, graded_ring.proj ğ’œ i â»Â¹' â†‘I :=
subset_Inter_iff.symm

lemma ideal.mul_homogeneous_element_mem_of_mem
  {I : ideal A} (r x : A) (hxâ‚ : is_homogeneous ğ’œ x) (hxâ‚‚ : x âˆˆ I) (j : Î¹) :
  graded_ring.proj ğ’œ j (r * x) âˆˆ I :=
begin
  classical,
  rw [â†direct_sum.sum_support_decompose ğ’œ r, finset.sum_mul, map_sum],
  apply ideal.sum_mem,
  intros k hk,
  obtain âŸ¨i, hiâŸ© := hxâ‚,
  have memâ‚ : (direct_sum.decompose ğ’œ r k : A) * x âˆˆ ğ’œ (k + i) := graded_monoid.mul_mem
    (set_like.coe_mem _) hi,
  erw [graded_ring.proj_apply, direct_sum.decompose_of_mem ğ’œ memâ‚,
    coe_of_apply, set_like.coe_mk],
  split_ifs,
  { exact I.mul_mem_left _ hxâ‚‚ },
  { exact I.zero_mem },
end

lemma ideal.is_homogeneous_span (s : set A) (h : âˆ€ x âˆˆ s, is_homogeneous ğ’œ x) :
  (ideal.span s).is_homogeneous ğ’œ :=
begin
  rintros i r hr,
  rw [ideal.span, finsupp.span_eq_range_total] at hr,
  rw linear_map.mem_range at hr,
  obtain âŸ¨s, rflâŸ© := hr,
  rw [finsupp.total_apply, finsupp.sum, decompose_sum, dfinsupp.finset_sum_apply,
    add_submonoid_class.coe_finset_sum],
  refine ideal.sum_mem _ _,
  rintros z hz1,
  rw [smul_eq_mul],
  refine ideal.mul_homogeneous_element_mem_of_mem ğ’œ (s z) z _ _ i,
  { rcases z with âŸ¨z, hz2âŸ©,
    apply h _ hz2, },
  { exact ideal.subset_span z.2 },
end

/--For any `I : ideal A`, not necessarily homogeneous, `I.homogeneous_core' ğ’œ`
is the largest homogeneous ideal of `A` contained in `I`.-/
def ideal.homogeneous_core : homogeneous_ideal ğ’œ :=
âŸ¨ideal.homogeneous_core' ğ’œ I,
  ideal.is_homogeneous_span _ _ (Î» x h, by { rw [subtype.image_preimage_coe] at h, exact h.2 })âŸ©

lemma ideal.homogeneous_core_mono : monotone (ideal.homogeneous_core ğ’œ) :=
ideal.homogeneous_core'_mono ğ’œ

lemma ideal.to_ideal_homogeneous_core_le : (I.homogeneous_core ğ’œ).to_ideal â‰¤ I :=
ideal.homogeneous_core'_le ğ’œ I

variables {ğ’œ I}

lemma ideal.mem_homogeneous_core_of_is_homogeneous_of_mem {x : A}
  (h : set_like.is_homogeneous ğ’œ x) (hmem : x âˆˆ I) : x âˆˆ I.homogeneous_core ğ’œ :=
ideal.subset_span âŸ¨âŸ¨x, hâŸ©, hmem, rflâŸ©

lemma ideal.is_homogeneous.to_ideal_homogeneous_core_eq_self (h : I.is_homogeneous ğ’œ) :
  (I.homogeneous_core ğ’œ).to_ideal = I :=
begin
  apply le_antisymm (I.homogeneous_core'_le ğ’œ) _,
  intros x hx,
  classical,
  rw â†direct_sum.sum_support_decompose ğ’œ x,
  exact ideal.sum_mem _ (Î» j hj, ideal.subset_span âŸ¨âŸ¨_, is_homogeneous_coe _âŸ©, h _ hx, rflâŸ©)
end

@[simp] lemma homogeneous_ideal.to_ideal_homogeneous_core_eq_self (I : homogeneous_ideal ğ’œ) :
  I.to_ideal.homogeneous_core ğ’œ = I :=
by ext1; convert ideal.is_homogeneous.to_ideal_homogeneous_core_eq_self I.is_homogeneous

variables (ğ’œ I)

lemma ideal.is_homogeneous.iff_eq : I.is_homogeneous ğ’œ â†” (I.homogeneous_core ğ’œ).to_ideal = I :=
âŸ¨ Î» hI, hI.to_ideal_homogeneous_core_eq_self,
  Î» hI, hI â–¸ (ideal.homogeneous_core ğ’œ I).2 âŸ©

lemma ideal.is_homogeneous.iff_exists :
  I.is_homogeneous ğ’œ â†” âˆƒ (S : set (homogeneous_submonoid ğ’œ)), I = ideal.span (coe '' S) :=
begin
  rw [ideal.is_homogeneous.iff_eq, eq_comm],
  exact ((set.image_preimage.compose (submodule.gi _ _).gc).exists_eq_l _).symm,
end

end is_homogeneous_ideal_defs

/-! ### Operations

In this section, we show that `ideal.is_homogeneous` is preserved by various notations, then use
these results to provide these notation typeclasses for `homogeneous_ideal`. -/

section operations

section semiring

variables [semiring A] [decidable_eq Î¹] [add_monoid Î¹]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [graded_ring ğ’œ]
include A

namespace ideal.is_homogeneous

lemma bot : ideal.is_homogeneous ğ’œ âŠ¥ := Î» i r hr,
begin
  simp only [ideal.mem_bot] at hr,
  rw [hr, decompose_zero, zero_apply],
  apply ideal.zero_mem
end

lemma top : ideal.is_homogeneous ğ’œ âŠ¤ :=
Î» i r hr, by simp only [submodule.mem_top]

variables {ğ’œ}

lemma inf {I J : ideal A} (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) :
  (I âŠ“ J).is_homogeneous ğ’œ :=
Î» i r hr, âŸ¨HI _ hr.1, HJ _ hr.2âŸ©

lemma sup {I J : ideal A} (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) :
  (I âŠ” J).is_homogeneous ğ’œ :=
begin
  rw iff_exists at HI HJ âŠ¢,
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := âŸ¨HI, HJâŸ©,
  refine âŸ¨sâ‚ âˆª sâ‚‚, _âŸ©,
  rw [set.image_union],
  exact (submodule.span_union _ _).symm,
end

protected lemma supr {Îº : Sort*} {f : Îº â†’ ideal A} (h : âˆ€ i, (f i).is_homogeneous ğ’œ) :
  (â¨† i, f i).is_homogeneous ğ’œ :=
begin
  simp_rw iff_exists at h âŠ¢,
  choose s hs using h,
  refine âŸ¨â‹ƒ i, s i, _âŸ©,
  simp_rw [set.image_Union, ideal.span_Union],
  congr',
  exact funext hs,
end

protected lemma infi {Îº : Sort*} {f : Îº â†’ ideal A} (h : âˆ€ i, (f i).is_homogeneous ğ’œ) :
  (â¨… i, f i).is_homogeneous ğ’œ :=
begin
  intros i x hx,
  simp only [ideal.mem_infi] at âŠ¢ hx,
  exact Î» j, h _ _ (hx j),
end

lemma suprâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : Î  i, Îº' i â†’ ideal A}
  (h : âˆ€ i j, (f i j).is_homogeneous ğ’œ) :
  (â¨† i j, f i j).is_homogeneous ğ’œ :=
is_homogeneous.supr $ Î» i, is_homogeneous.supr $ h i

lemma infiâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} {f : Î  i, Îº' i â†’ ideal A}
  (h : âˆ€ i j, (f i j).is_homogeneous ğ’œ) :
  (â¨… i j, f i j).is_homogeneous ğ’œ :=
is_homogeneous.infi $ Î» i, is_homogeneous.infi $ h i

lemma Sup {â„ : set (ideal A)} (h : âˆ€ I âˆˆ â„, ideal.is_homogeneous ğ’œ I) :
  (Sup â„).is_homogeneous ğ’œ :=
by { rw Sup_eq_supr, exact suprâ‚‚ h }

lemma Inf {â„ : set (ideal A)} (h : âˆ€ I âˆˆ â„, ideal.is_homogeneous ğ’œ I) :
  (Inf â„).is_homogeneous ğ’œ :=
by { rw Inf_eq_infi, exact infiâ‚‚ h }

end ideal.is_homogeneous

variables {ğ’œ}

namespace homogeneous_ideal

instance : partial_order (homogeneous_ideal ğ’œ) := set_like.partial_order

instance : has_top (homogeneous_ideal ğ’œ) := âŸ¨âŸ¨âŠ¤, ideal.is_homogeneous.top ğ’œâŸ©âŸ©
instance : has_bot (homogeneous_ideal ğ’œ) := âŸ¨âŸ¨âŠ¥, ideal.is_homogeneous.bot ğ’œâŸ©âŸ©
instance : has_sup (homogeneous_ideal ğ’œ) := âŸ¨Î» I J, âŸ¨_, I.is_homogeneous.sup J.is_homogeneousâŸ©âŸ©
instance : has_inf (homogeneous_ideal ğ’œ) := âŸ¨Î» I J, âŸ¨_, I.is_homogeneous.inf J.is_homogeneousâŸ©âŸ©
instance : has_Sup (homogeneous_ideal ğ’œ) :=
âŸ¨Î» S, âŸ¨â¨† s âˆˆ S, to_ideal s, ideal.is_homogeneous.suprâ‚‚ $ Î» s _, s.is_homogeneousâŸ©âŸ©
instance : has_Inf (homogeneous_ideal ğ’œ) :=
âŸ¨Î» S, âŸ¨â¨… s âˆˆ S, to_ideal s, ideal.is_homogeneous.infiâ‚‚ $ Î» s _, s.is_homogeneousâŸ©âŸ©

@[simp] lemma coe_top : ((âŠ¤ : homogeneous_ideal ğ’œ) : set A) = univ := rfl
@[simp] lemma coe_bot : ((âŠ¥ : homogeneous_ideal ğ’œ) : set A) = 0 := rfl
@[simp] lemma coe_sup (I J : homogeneous_ideal ğ’œ) : â†‘(I âŠ” J) = (I + J : set A) :=
submodule.coe_sup _ _
@[simp] lemma coe_inf (I J : homogeneous_ideal ğ’œ) : (â†‘(I âŠ“ J) : set A) = I âˆ© J := rfl

@[simp] lemma to_ideal_top : (âŠ¤ : homogeneous_ideal ğ’œ).to_ideal = (âŠ¤ : ideal A) := rfl
@[simp] lemma to_ideal_bot : (âŠ¥ : homogeneous_ideal ğ’œ).to_ideal = (âŠ¥ : ideal A) := rfl

@[simp] lemma to_ideal_sup (I J : homogeneous_ideal ğ’œ) :
  (I âŠ” J).to_ideal = I.to_ideal âŠ” J.to_ideal := rfl

@[simp] lemma to_ideal_inf (I J : homogeneous_ideal ğ’œ) :
  (I âŠ“ J).to_ideal = I.to_ideal âŠ“ J.to_ideal := rfl

@[simp] lemma to_ideal_Sup (â„ : set (homogeneous_ideal ğ’œ)) :
  (Sup â„).to_ideal = â¨† s âˆˆ â„, to_ideal s := rfl

@[simp] lemma to_ideal_Inf (â„ : set (homogeneous_ideal ğ’œ)) :
  (Inf â„).to_ideal = â¨… s âˆˆ â„, to_ideal s := rfl

@[simp] lemma to_ideal_supr {Îº : Sort*} (s : Îº â†’ homogeneous_ideal ğ’œ) :
  (â¨† i, s i).to_ideal = â¨† i, (s i).to_ideal :=
by rw [supr, to_ideal_Sup, supr_range]

@[simp] lemma to_ideal_infi {Îº : Sort*} (s : Îº â†’ homogeneous_ideal ğ’œ) :
  (â¨… i, s i).to_ideal = â¨… i, (s i).to_ideal :=
by rw [infi, to_ideal_Inf, infi_range]

@[simp] lemma to_ideal_suprâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} (s : Î  i, Îº' i â†’ homogeneous_ideal ğ’œ) :
  (â¨† i j, s i j).to_ideal = â¨† i j, (s i j).to_ideal :=
by simp_rw to_ideal_supr

@[simp] lemma to_ideal_infiâ‚‚ {Îº : Sort*} {Îº' : Îº â†’ Sort*} (s : Î  i, Îº' i â†’ homogeneous_ideal ğ’œ) :
  (â¨… i j, s i j).to_ideal = â¨… i j, (s i j).to_ideal :=
by simp_rw to_ideal_infi

@[simp] lemma eq_top_iff (I : homogeneous_ideal ğ’œ) : I = âŠ¤ â†” I.to_ideal = âŠ¤ :=
to_ideal_injective.eq_iff.symm

@[simp] lemma eq_bot_iff (I : homogeneous_ideal ğ’œ) : I = âŠ¥ â†” I.to_ideal = âŠ¥ :=
to_ideal_injective.eq_iff.symm

instance : complete_lattice (homogeneous_ideal ğ’œ) :=
to_ideal_injective.complete_lattice _ to_ideal_sup to_ideal_inf to_ideal_Sup to_ideal_Inf
  to_ideal_top to_ideal_bot

instance : has_add (homogeneous_ideal ğ’œ) := âŸ¨(âŠ”)âŸ©

@[simp] lemma to_ideal_add (I J : homogeneous_ideal ğ’œ) :
  (I + J).to_ideal = I.to_ideal + J.to_ideal := rfl

instance : inhabited (homogeneous_ideal ğ’œ) := { default := âŠ¥ }

end homogeneous_ideal

end semiring

section comm_semiring
variables [comm_semiring A]
variables [decidable_eq Î¹] [add_monoid Î¹]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] {ğ’œ : Î¹ â†’ Ïƒ} [graded_ring ğ’œ]
variable (I : ideal A)
include A

lemma ideal.is_homogeneous.mul {I J : ideal A}
  (HI : I.is_homogeneous ğ’œ) (HJ : J.is_homogeneous ğ’œ) : (I * J).is_homogeneous ğ’œ :=
begin
  rw ideal.is_homogeneous.iff_exists at HI HJ âŠ¢,
  obtain âŸ¨âŸ¨sâ‚, rflâŸ©, âŸ¨sâ‚‚, rflâŸ©âŸ© := âŸ¨HI, HJâŸ©,
  rw ideal.span_mul_span',
  exact âŸ¨sâ‚ * sâ‚‚, congr_arg _ $ (set.image_mul (homogeneous_submonoid ğ’œ).subtype).symmâŸ©,
end

variables {ğ’œ}

instance : has_mul (homogeneous_ideal ğ’œ) :=
{ mul := Î» I J, âŸ¨I.to_ideal * J.to_ideal, I.is_homogeneous.mul J.is_homogeneousâŸ© }

@[simp] lemma homogeneous_ideal.to_ideal_mul (I J : homogeneous_ideal ğ’œ) :
  (I * J).to_ideal = I.to_ideal * J.to_ideal := rfl

end comm_semiring

end operations

/-! ### Homogeneous core

Note that many results about the homogeneous core came earlier in this file, as they are helpful
for building the lattice structure. -/

section homogeneous_core

open homogeneous_ideal

variables [semiring A] [decidable_eq Î¹] [add_monoid Î¹]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [graded_ring ğ’œ]
variable (I : ideal A)
include A

lemma ideal.homogeneous_core.gc : galois_connection to_ideal (ideal.homogeneous_core ğ’œ) :=
Î» I J, âŸ¨
  Î» H, I.to_ideal_homogeneous_core_eq_self â–¸ ideal.homogeneous_core_mono ğ’œ H,
  Î» H, le_trans H (ideal.homogeneous_core'_le _ _)âŸ©

/--`to_ideal : homogeneous_ideal ğ’œ â†’ ideal A` and `ideal.homogeneous_core ğ’œ` forms a galois
coinsertion-/
def ideal.homogeneous_core.gi : galois_coinsertion to_ideal (ideal.homogeneous_core ğ’œ) :=
{ choice := Î» I HI,
    âŸ¨I, le_antisymm (I.to_ideal_homogeneous_core_le ğ’œ) HI â–¸ homogeneous_ideal.is_homogeneous _âŸ©,
  gc := ideal.homogeneous_core.gc ğ’œ,
  u_l_le := Î» I, ideal.homogeneous_core'_le _ _,
  choice_eq := Î» I H, le_antisymm H (I.to_ideal_homogeneous_core_le _) }

lemma ideal.homogeneous_core_eq_Sup :
  I.homogeneous_core ğ’œ = Sup {J : homogeneous_ideal ğ’œ | J.to_ideal â‰¤ I} :=
eq.symm $ is_lub.Sup_eq $ (ideal.homogeneous_core.gc ğ’œ).is_greatest_u.is_lub

lemma ideal.homogeneous_core'_eq_Sup :
  I.homogeneous_core' ğ’œ = Sup {J : ideal A | J.is_homogeneous ğ’œ âˆ§ J â‰¤ I} :=
begin
  refine (is_lub.Sup_eq _).symm,
  apply is_greatest.is_lub,
  have coe_mono : monotone (to_ideal : homogeneous_ideal ğ’œ â†’ ideal A) := Î» x y, id,
  convert coe_mono.map_is_greatest (ideal.homogeneous_core.gc ğ’œ).is_greatest_u using 1,
  ext,
  rw [mem_image, mem_set_of_eq],
  refine âŸ¨Î» hI, âŸ¨âŸ¨x, hI.1âŸ©, âŸ¨hI.2, rflâŸ©âŸ©, by rintro âŸ¨x, âŸ¨hx, rflâŸ©âŸ©; exact âŸ¨x.is_homogeneous, hxâŸ©âŸ©,
end

end homogeneous_core

/-! ### Homogeneous hulls -/

section homogeneous_hull

open homogeneous_ideal

variables [semiring A] [decidable_eq Î¹] [add_monoid Î¹]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [graded_ring ğ’œ]
variable (I : ideal A)
include A

/--For any `I : ideal A`, not necessarily homogeneous, `I.homogeneous_hull ğ’œ` is
the smallest homogeneous ideal containing `I`. -/
def ideal.homogeneous_hull : homogeneous_ideal ğ’œ :=
âŸ¨ideal.span {r : A | âˆƒ (i : Î¹) (x : I), (direct_sum.decompose ğ’œ (x : A) i : A) = r}, begin
  refine ideal.is_homogeneous_span _ _ (Î» x hx, _),
  obtain âŸ¨i, x, rflâŸ© := hx,
  apply set_like.is_homogeneous_coe
endâŸ©

lemma ideal.le_to_ideal_homogeneous_hull :
  I â‰¤ (ideal.homogeneous_hull ğ’œ I).to_ideal :=
begin
  intros r hr,
  classical,
  rw [â†direct_sum.sum_support_decompose ğ’œ r],
  refine ideal.sum_mem _ _, intros j hj,
  apply ideal.subset_span, use j, use âŸ¨r, hrâŸ©, refl,
end

lemma ideal.homogeneous_hull_mono : monotone (ideal.homogeneous_hull ğ’œ) := Î» I J I_le_J,
begin
  apply ideal.span_mono,
  rintros r âŸ¨hr1, âŸ¨x, hxâŸ©, rflâŸ©,
  refine âŸ¨hr1, âŸ¨âŸ¨x, I_le_J hxâŸ©, rflâŸ©âŸ©,
end

variables {I ğ’œ}

lemma ideal.is_homogeneous.to_ideal_homogeneous_hull_eq_self (h : I.is_homogeneous ğ’œ) :
  (ideal.homogeneous_hull ğ’œ I).to_ideal = I :=
begin
  apply le_antisymm _ (ideal.le_to_ideal_homogeneous_hull _ _),
  apply (ideal.span_le).2,
  rintros _ âŸ¨i, x, rflâŸ©,
  exact h _ x.prop,
end

@[simp] lemma homogeneous_ideal.homogeneous_hull_to_ideal_eq_self (I : homogeneous_ideal ğ’œ) :
  I.to_ideal.homogeneous_hull ğ’œ = I :=
homogeneous_ideal.to_ideal_injective $ I.is_homogeneous.to_ideal_homogeneous_hull_eq_self

variables (I ğ’œ)

lemma ideal.to_ideal_homogeneous_hull_eq_supr :
  (I.homogeneous_hull ğ’œ).to_ideal = â¨† i, ideal.span (graded_ring.proj ğ’œ i '' I) :=
begin
  rw â†ideal.span_Union,
  apply congr_arg ideal.span _,
  ext1,
  simp only [set.mem_Union, set.mem_image, mem_set_of_eq, graded_ring.proj_apply,
    set_like.exists, exists_prop, subtype.coe_mk, set_like.mem_coe],
end

lemma ideal.homogeneous_hull_eq_supr :
  (I.homogeneous_hull ğ’œ) =
  â¨† i, âŸ¨ideal.span (graded_ring.proj ğ’œ i '' I), ideal.is_homogeneous_span ğ’œ _
    (by {rintros _ âŸ¨x, -, rflâŸ©, apply set_like.is_homogeneous_coe})âŸ© :=
by { ext1, rw [ideal.to_ideal_homogeneous_hull_eq_supr, to_ideal_supr], refl }

end homogeneous_hull

section galois_connection

open homogeneous_ideal

variables [semiring A] [decidable_eq Î¹] [add_monoid Î¹]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [graded_ring ğ’œ]
include A

lemma ideal.homogeneous_hull.gc : galois_connection (ideal.homogeneous_hull ğ’œ) to_ideal :=
Î» I J, âŸ¨
  le_trans (ideal.le_to_ideal_homogeneous_hull _ _),
  Î» H, J.homogeneous_hull_to_ideal_eq_self â–¸ ideal.homogeneous_hull_mono ğ’œ HâŸ©

/-- `ideal.homogeneous_hull ğ’œ` and `to_ideal : homogeneous_ideal ğ’œ â†’ ideal A` form a galois
insertion-/
def ideal.homogeneous_hull.gi : galois_insertion (ideal.homogeneous_hull ğ’œ) to_ideal :=
{ choice := Î» I H, âŸ¨I, le_antisymm H (I.le_to_ideal_homogeneous_hull ğ’œ) â–¸ is_homogeneous _âŸ©,
  gc := ideal.homogeneous_hull.gc ğ’œ,
  le_l_u := Î» I, ideal.le_to_ideal_homogeneous_hull _ _,
  choice_eq := Î» I H, le_antisymm (I.le_to_ideal_homogeneous_hull ğ’œ) H}

lemma ideal.homogeneous_hull_eq_Inf (I : ideal A) :
  ideal.homogeneous_hull ğ’œ I = Inf { J : homogeneous_ideal ğ’œ | I â‰¤ J.to_ideal } :=
eq.symm $ is_glb.Inf_eq $ (ideal.homogeneous_hull.gc ğ’œ).is_least_l.is_glb

end galois_connection

section irrelevant_ideal

variables [semiring A]
variables [decidable_eq Î¹]
variables [canonically_ordered_add_monoid Î¹]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [graded_ring ğ’œ]
include A

open graded_ring set_like.graded_monoid direct_sum

/--
For a graded ring `â¨áµ¢ ğ’œáµ¢` graded by a `canonically_ordered_add_monoid Î¹`, the irrelevant ideal
refers to `â¨_{i>0} ğ’œáµ¢`, or equivalently `{a | aâ‚€ = 0}`. This definition is used in `Proj`
construction where `Î¹` is always `â„•` so the irrelevant ideal is simply elements with `0` as
0-th coordinate.

# Future work
Here in the definition, `Î¹` is assumed to be `canonically_ordered_add_monoid`. However, the notion
of irrelevant ideal makes sense in a more general setting by defining it as the ideal of elements
with `0` as i-th coordinate for all `i â‰¤ 0`, i.e. `{a | âˆ€ (i : Î¹), i â‰¤ 0 â†’ aáµ¢ = 0}`.
-/
def homogeneous_ideal.irrelevant : homogeneous_ideal ğ’œ :=
âŸ¨(graded_ring.proj_zero_ring_hom ğ’œ).ker, Î» i r (hr : (decompose ğ’œ r 0 : A) = 0), begin
  change (decompose ğ’œ (decompose ğ’œ r _ : A) 0 : A) = 0,
  by_cases h : i = 0,
  { rw [h, hr, decompose_zero, zero_apply, zero_mem_class.coe_zero] },
  { rw [decompose_of_mem_ne ğ’œ (set_like.coe_mem _) h] }
endâŸ©

@[simp] lemma homogeneous_ideal.mem_irrelevant_iff (a : A) :
  a âˆˆ homogeneous_ideal.irrelevant ğ’œ â†” proj ğ’œ 0 a = 0 := iff.rfl

@[simp] lemma homogeneous_ideal.to_ideal_irrelevant :
  (homogeneous_ideal.irrelevant ğ’œ).to_ideal = (graded_ring.proj_zero_ring_hom ğ’œ).ker := rfl

end irrelevant_ideal
