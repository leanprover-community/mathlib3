/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/

import ring_theory.graded_algebra.homogeneous_ideal

/-!

This file contains a proof that the radical of any homogeneous ideal is a homogeneous ideal

## Main statements

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

* `ideal.is_homogeneous.is_prime_iff`: for any `I : ideal A`, if `I` is homogeneous, then
  `I` is prime if and only if `I` is homogeneously prime, i.e. `I â‰  âŠ¤` and if `x, y` are
  homogeneous elements such that `x * y âˆˆ I`, then at least one of `x,y` is in `I`.
* `ideal.is_prime.homogeneous_core`: for any `I : ideal A`, if `I` is prime, then
  `I.homogeneous_core ğ’œ` (i.e. the largest homogeneous ideal contained in `I`) is also prime.
* `ideal.is_homogeneous.radical`: for any `I : ideal A`, if `I` is homogeneous, then the
  radical of `I` is homogeneous as well.
* `homogeneous_ideal.radical`: for any `I : homogeneous_ideal ğ’œ`, `I.radical` is the the
  radical of `I` as a `homogeneous_ideal ğ’œ`

## Implementation details

Throughout this file, the indexing type `Î¹` of grading is assumed to be a
`linear_ordered_cancel_add_comm_monoid`. This might be stronger than necessary but cancelling
property is strictly necessary; for a counterexample of how `ideal.is_homogeneous.is_prime_iff`
fails for a non-cancellative set see `counterexample/homogeneous_prime_not_prime.lean`.

## Tags

homogeneous, radical
-/

open graded_ring direct_sum set_like finset
open_locale big_operators

variables {Î¹ Ïƒ A : Type*}
variables [comm_ring A]
variables [linear_ordered_cancel_add_comm_monoid Î¹]
variables [set_like Ïƒ A] [add_submonoid_class Ïƒ A] {ğ’œ : Î¹ â†’ Ïƒ} [graded_ring ğ’œ]
include A

lemma ideal.is_homogeneous.is_prime_of_homogeneous_mem_or_mem
  {I : ideal A} (hI : I.is_homogeneous ğ’œ) (I_ne_top : I â‰  âŠ¤)
  (homogeneous_mem_or_mem : âˆ€ {x y : A},
    is_homogeneous ğ’œ x â†’ is_homogeneous ğ’œ y â†’ (x * y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I)) :
  ideal.is_prime I :=
âŸ¨I_ne_top, begin
  intros x y hxy,
  by_contradiction rid,
  obtain âŸ¨ridâ‚, ridâ‚‚âŸ© := not_or_distrib.mp rid,
  /-
  The idea of the proof is the following :
  since `x * y âˆˆ I` and `I` homogeneous, then `proj i (x * y) âˆˆ I` for any `i : Î¹`.
  Then consider two sets `{i âˆˆ x.support | xáµ¢ âˆ‰ I}` and `{j âˆˆ y.support | yâ±¼ âˆ‰ J}`;
  let `maxâ‚, maxâ‚‚` be the maximum of the two sets, then `proj (maxâ‚ + maxâ‚‚) (x * y) âˆˆ I`.
  Then, `proj maxâ‚ x âˆ‰ I` and `proj maxâ‚‚ j âˆ‰ I`
  but `proj i x âˆˆ I` for all `maxâ‚ < i` and `proj j y âˆˆ I` for all `maxâ‚‚ < j`.
  `  proj (maxâ‚ + maxâ‚‚) (x * y)`
  `= âˆ‘ {(i, j) âˆˆ supports | i + j = maxâ‚ + maxâ‚‚}, xáµ¢ * yâ±¼`
  `= proj maxâ‚ x * proj maxâ‚‚ y`
  `  + âˆ‘ {(i, j) âˆˆ supports \ {(maxâ‚, maxâ‚‚)} | i + j = maxâ‚ + maxâ‚‚}, xáµ¢ * yâ±¼`.
  This is a contradiction, because both `proj (maxâ‚ + maxâ‚‚) (x * y) âˆˆ I` and the sum on the
  right hand side is in `I` however `proj maxâ‚ x * proj maxâ‚‚ y` is not in `I`.
  -/
  classical,
  set setâ‚ := (decompose ğ’œ x).support.filter (Î» i, proj ğ’œ i x âˆ‰ I) with setâ‚_eq,
  set setâ‚‚ := (decompose ğ’œ y).support.filter (Î» i, proj ğ’œ i y âˆ‰ I) with setâ‚‚_eq,
  have nonempty :
    âˆ€ (x : A), (x âˆ‰ I) â†’ ((decompose ğ’œ x).support.filter (Î» i, proj ğ’œ i x âˆ‰ I)).nonempty,
  { intros x hx,
    rw filter_nonempty_iff,
    contrapose! hx,
    simp_rw proj_apply at hx,
    rw â† sum_support_decompose ğ’œ x,
    exact ideal.sum_mem _ hx, },
  set maxâ‚ := setâ‚.max' (nonempty x ridâ‚) with maxâ‚_eq,
  set maxâ‚‚ := setâ‚‚.max' (nonempty y ridâ‚‚) with maxâ‚‚_eq,
  have mem_maxâ‚ : maxâ‚ âˆˆ setâ‚ := max'_mem setâ‚ (nonempty x ridâ‚),
  have mem_maxâ‚‚ : maxâ‚‚ âˆˆ setâ‚‚ := max'_mem setâ‚‚ (nonempty y ridâ‚‚),
  replace hxy : proj ğ’œ (maxâ‚ + maxâ‚‚) (x * y) âˆˆ I := hI _ hxy,

  have mem_I : proj ğ’œ maxâ‚ x * proj ğ’œ maxâ‚‚ y âˆˆ I,
  { set antidiag :=
      ((decompose ğ’œ x).support Ã—Ë¢ (decompose ğ’œ y).support)
        .filter (Î» z : Î¹ Ã— Î¹, z.1 + z.2 = maxâ‚ + maxâ‚‚) with ha,
    have mem_antidiag : (maxâ‚, maxâ‚‚) âˆˆ antidiag,
    { simp only [add_sum_erase, mem_filter, mem_product],
      exact âŸ¨âŸ¨mem_of_mem_filter _ mem_maxâ‚, mem_of_mem_filter _ mem_maxâ‚‚âŸ©, rflâŸ© },
    have eq_add_sum :=
      calc  proj ğ’œ (maxâ‚ + maxâ‚‚) (x * y)
          = âˆ‘ ij in antidiag, proj ğ’œ ij.1 x * proj ğ’œ ij.2 y
          : by simp_rw [ha, proj_apply, direct_sum.decompose_mul,
                        direct_sum.coe_mul_apply ğ’œ]
      ... = proj ğ’œ maxâ‚ x * proj ğ’œ maxâ‚‚ y + âˆ‘ ij in antidiag.erase (maxâ‚, maxâ‚‚),
                                              proj ğ’œ ij.1 x * proj ğ’œ ij.2 y
          : (add_sum_erase _ _ mem_antidiag).symm,
    rw eq_sub_of_add_eq eq_add_sum.symm,
    refine ideal.sub_mem _ hxy (ideal.sum_mem _ (Î» z H, _)),
    rcases z with âŸ¨i, jâŸ©,
    simp only [mem_erase, prod.mk.inj_iff, ne.def, mem_filter, mem_product] at H,
    rcases H with âŸ¨Hâ‚, âŸ¨Hâ‚‚, Hâ‚ƒâŸ©, Hâ‚„âŸ©,
    have max_lt : maxâ‚ < i âˆ¨ maxâ‚‚ < j,
    { rcases lt_trichotomy maxâ‚ i with h | rfl | h,
      { exact or.inl h },
      { refine false.elim (Hâ‚ âŸ¨rfl, add_left_cancel Hâ‚„âŸ©), },
      { apply or.inr,
        have := add_lt_add_right h j,
        rw Hâ‚„ at this,
        exact lt_of_add_lt_add_left this, }, },
    cases max_lt,
    { -- in this case `maxâ‚ < i`, then `xáµ¢ âˆˆ I`; for otherwise `i âˆˆ setâ‚` then `i â‰¤ maxâ‚`.
      have not_mem : i âˆ‰ setâ‚ := Î» h, lt_irrefl _
        ((max'_lt_iff setâ‚ (nonempty x ridâ‚)).mp max_lt i h),
      rw setâ‚_eq at not_mem,
      simp only [not_and, not_not, ne.def, mem_filter] at not_mem,
      exact ideal.mul_mem_right _ I (not_mem Hâ‚‚), },
    { -- in this case  `maxâ‚‚ < j`, then `yâ±¼ âˆˆ I`; for otherwise `j âˆˆ setâ‚‚`, then `j â‰¤ maxâ‚‚`.
      have not_mem : j âˆ‰ setâ‚‚ := Î» h, lt_irrefl _
        ((max'_lt_iff setâ‚‚ (nonempty y ridâ‚‚)).mp max_lt j h),
      rw setâ‚‚_eq at not_mem,
      simp only [not_and, not_not, ne.def, mem_filter] at not_mem,
      exact ideal.mul_mem_left I _ (not_mem Hâ‚ƒ), }, },

  have not_mem_I : proj ğ’œ maxâ‚ x * proj ğ’œ maxâ‚‚ y âˆ‰ I,
  { have neither_mem : proj ğ’œ maxâ‚ x âˆ‰ I âˆ§ proj ğ’œ maxâ‚‚ y âˆ‰ I,
    { rw mem_filter at mem_maxâ‚ mem_maxâ‚‚,
      exact âŸ¨mem_maxâ‚.2, mem_maxâ‚‚.2âŸ©, },
    intro rid,
    cases homogeneous_mem_or_mem âŸ¨maxâ‚, set_like.coe_mem _âŸ© âŸ¨maxâ‚‚, set_like.coe_mem _âŸ© mem_I,
    { apply neither_mem.1 h },
    { apply neither_mem.2 h }, },

  exact not_mem_I mem_I,
endâŸ©

lemma ideal.is_homogeneous.is_prime_iff {I : ideal A} (h : I.is_homogeneous ğ’œ) :
  I.is_prime â†”
  (I â‰  âŠ¤) âˆ§
    âˆ€ {x y : A}, set_like.is_homogeneous ğ’œ x â†’ set_like.is_homogeneous ğ’œ y
      â†’ (x * y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I) :=
âŸ¨Î» HI,
  âŸ¨ne_of_apply_ne _ HI.ne_top, Î» x y hx hy hxy, ideal.is_prime.mem_or_mem HI hxyâŸ©,
  Î» âŸ¨I_ne_top, homogeneous_mem_or_memâŸ©,
    h.is_prime_of_homogeneous_mem_or_mem I_ne_top @homogeneous_mem_or_memâŸ©

lemma ideal.is_prime.homogeneous_core {I : ideal A} (h : I.is_prime) :
  (I.homogeneous_core ğ’œ).to_ideal.is_prime :=
begin
  apply (ideal.homogeneous_core ğ’œ I).is_homogeneous.is_prime_of_homogeneous_mem_or_mem,
  { exact ne_top_of_le_ne_top h.ne_top (ideal.to_ideal_homogeneous_core_le ğ’œ I) },
  rintros x y hx hy hxy,
  have H := h.mem_or_mem (ideal.to_ideal_homogeneous_core_le ğ’œ I hxy),
  refine H.imp _ _,
  { exact ideal.mem_homogeneous_core_of_is_homogeneous_of_mem hx, },
  { exact ideal.mem_homogeneous_core_of_is_homogeneous_of_mem hy, },
end

lemma ideal.is_homogeneous.radical_eq {I : ideal A} (hI : I.is_homogeneous ğ’œ) :
  I.radical = Inf { J | J.is_homogeneous ğ’œ âˆ§ I â‰¤ J âˆ§ J.is_prime } :=
begin
  rw ideal.radical_eq_Inf,
  apply le_antisymm,
  { exact Inf_le_Inf (Î» J, and.right), },
  { refine Inf_le_Inf_of_forall_exists_le _,
    rintros J âŸ¨HJâ‚, HJâ‚‚âŸ©,
    refine âŸ¨(J.homogeneous_core ğ’œ).to_ideal, _, J.to_ideal_homogeneous_core_le _âŸ©,
    refine âŸ¨homogeneous_ideal.is_homogeneous _, _, HJâ‚‚.homogeneous_coreâŸ©,
    refine hI.to_ideal_homogeneous_core_eq_self.symm.trans_le (ideal.homogeneous_core_mono _ HJâ‚), }
end

lemma ideal.is_homogeneous.radical {I : ideal A} (h : I.is_homogeneous ğ’œ)  :
  I.radical.is_homogeneous ğ’œ :=
by { rw h.radical_eq, exact ideal.is_homogeneous.Inf (Î» _, and.left) }

/-- The radical of a homogenous ideal, as another homogenous ideal. -/
def homogeneous_ideal.radical (I : homogeneous_ideal ğ’œ) : homogeneous_ideal ğ’œ :=
âŸ¨I.to_ideal.radical, I.is_homogeneous.radicalâŸ©

@[simp]
lemma homogeneous_ideal.coe_radical (I : homogeneous_ideal ğ’œ) :
  I.radical.to_ideal = I.to_ideal.radical := rfl
