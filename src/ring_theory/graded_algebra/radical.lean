/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/

import ring_theory.graded_algebra.homogeneous_ideal

/-!

This file contains a proof that the radical of any homogeneous ideal is a homogeneous ideal

## Main statements

* `homogeneous_ideal.is_prime_iff`: a homogeneous ideal `I` is prime if and only if `I` is
  homogeneously prime, i.e. if `x, y` are homogeneous elements such that `x * y âˆˆ I`, then
  at least one of `x,y` is in `I`.
* `homogeneous_ideal.rad`: radical of homogeneous ideal is a homogeneous ideal.

## Implementation details

Through out this file, the indexing type `Î¹` of grading is assumed to be a
`linear_ordered_cancel_add_comm_monoid`. This might be stronger than necessary and `linearith`
does not work on `linear_ordered_cancel_add_comm_monoid`.

## Tags

homogeneous, radical
-/


open_locale big_operators

section linear_ordered_cancel_add_comm_monoid

variables {Î¹ R A : Type*}
variables [comm_semiring R] [comm_ring A] [algebra R A]
variables [linear_ordered_cancel_add_comm_monoid Î¹] [decidable_eq Î¹]
variables {ğ’œ : Î¹ â†’ submodule R A} [graded_algebra ğ’œ]

lemma ideal.is_homogeneous.is_prime_of_homogeneous_mem_or_mem
  {I : ideal A} (hI : I.is_homogeneous ğ’œ) (I_ne_top : I â‰  âŠ¤)
  (homogeneous_mem_or_mem : âˆ€ {x y : A},
    set_like.is_homogeneous ğ’œ x â†’ set_like.is_homogeneous ğ’œ y
    â†’ (x * y âˆˆ I â†’ x âˆˆ I âˆ¨ y âˆˆ I)) : ideal.is_prime I :=
âŸ¨I_ne_top, begin
  intros x y hxy, by_contradiction rid,
  obtain âŸ¨ridâ‚, ridâ‚‚âŸ© := not_or_distrib.mp rid,
  letI : Î  (x : A),
    decidable_pred (Î» (i : Î¹), graded_algebra.proj ğ’œ i x âˆ‰ I) := Î» x, classical.dec_pred _,
  letI : Î  i (x : ğ’œ i), decidable (x â‰  0) := Î» i x, classical.dec _,
  set setâ‚ := (graded_algebra.support ğ’œ x).filter (Î» i, graded_algebra.proj ğ’œ i x âˆ‰ I) with setâ‚_eq,
  set setâ‚‚ := (graded_algebra.support ğ’œ y).filter (Î» i, graded_algebra.proj ğ’œ i y âˆ‰ I) with setâ‚‚_eq,
  have setâ‚_nonempty : setâ‚.nonempty,
  { rw finset.filter_nonempty_iff,
    contrapose! ridâ‚,
    rw â† graded_algebra.sum_support_decompose ğ’œ x,
    apply ideal.sum_mem _ ridâ‚,},
  have setâ‚‚_nonempty : setâ‚‚.nonempty,
  { rw finset.filter_nonempty_iff,
    contrapose! ridâ‚‚,
    rw â† graded_algebra.sum_support_decompose ğ’œ y,
    apply ideal.sum_mem _ ridâ‚‚, },
  set maxâ‚ := setâ‚.max' setâ‚_nonempty with maxâ‚_eq,
  set maxâ‚‚ := setâ‚‚.max' setâ‚‚_nonempty with maxâ‚‚_eq,
  have mem_maxâ‚ := finset.max'_mem setâ‚ setâ‚_nonempty,
  have mem_maxâ‚‚ := finset.max'_mem setâ‚‚ setâ‚‚_nonempty,
  replace hxy : âˆ€ (i : Î¹), (graded_algebra.decompose ğ’œ (x * y) i : A) âˆˆ I := Î» i, hI i hxy,
  specialize hxy (maxâ‚ + maxâ‚‚),
  have eq :=
    calc  graded_algebra.proj ğ’œ (maxâ‚ + maxâ‚‚) (x * y)
        = âˆ‘ ij in ((graded_algebra.support ğ’œ x).product (graded_algebra.support ğ’œ y)).filter
            (Î» z, z.1 + z.2 = maxâ‚ + maxâ‚‚),
            (graded_algebra.proj ğ’œ ij.1 x) * (graded_algebra.proj ğ’œ ij.2 y)
        : begin
          rw [graded_algebra.proj_apply, alg_equiv.map_mul, graded_algebra.support,
            graded_algebra.support, direct_sum.coe_mul_apply_submodule],
          refl,
        end
    ... = âˆ‘ ij in (((graded_algebra.support ğ’œ x).product (graded_algebra.support ğ’œ y)).filter
            (Î» (z : Î¹ Ã— Î¹), z.1 + z.2 = maxâ‚ + maxâ‚‚)).erase (maxâ‚, maxâ‚‚),
            (graded_algebra.proj ğ’œ ij.1 x) * (graded_algebra.proj ğ’œ ij.2 y) +
          (graded_algebra.proj ğ’œ maxâ‚ x) * (graded_algebra.proj ğ’œ maxâ‚‚ y)
        : begin
          rw finset.sum_erase_add,
          simp only [finset.mem_filter, finset.mem_product, eq_self_iff_true, and_true],
          exact âŸ¨(finset.filter_subset _ _) mem_maxâ‚, (finset.filter_subset _ _) mem_maxâ‚‚âŸ©,
        end,

  have eqâ‚‚ : (graded_algebra.proj ğ’œ maxâ‚) x * (graded_algebra.proj ğ’œ maxâ‚‚) y
          = graded_algebra.proj ğ’œ (maxâ‚ + maxâ‚‚) (x * y)
          - âˆ‘ (ij : Î¹ Ã— Î¹) in (((graded_algebra.support ğ’œ x).product
              (graded_algebra.support ğ’œ y)).filter
              (Î» (z : Î¹ Ã— Î¹), z.fst + z.snd = maxâ‚ + maxâ‚‚)).erase (maxâ‚, maxâ‚‚),
              (graded_algebra.proj ğ’œ ij.fst) x * (graded_algebra.proj ğ’œ ij.snd) y,
  { rw [eq, eq_sub_iff_add_eq, add_comm], },

  have mem_I :
    (graded_algebra.proj ğ’œ (maxâ‚, maxâ‚‚).fst) x * (graded_algebra.proj ğ’œ (maxâ‚, maxâ‚‚).snd) y âˆˆ I,
  { rw eqâ‚‚,
    refine ideal.sub_mem _ hxy (ideal.sum_mem _ (Î» z H, _)),
    rcases z with âŸ¨i, jâŸ©,
    simp only [finset.mem_erase, prod.mk.inj_iff, ne.def, finset.mem_filter,
      finset.mem_product] at H,
    rcases H with âŸ¨Hâ‚, âŸ¨Hâ‚‚, Hâ‚ƒâŸ©, Hâ‚„âŸ©,
    have max_lt : maxâ‚ < i âˆ¨ maxâ‚‚ < j,
    { rcases lt_trichotomy maxâ‚ i with h | rfl | h,
      { exact or.inl h },
      { refine false.elim (Hâ‚ âŸ¨rfl, add_left_cancel Hâ‚„âŸ©), },
      { apply or.inr,
        have := add_lt_add_right h j,
        rw Hâ‚„ at this,
        apply lt_of_add_lt_add_left this, }, },
    cases max_lt,
    { -- in this case `i < maxâ‚`, so `of A i (x i) âˆˆ I`
      have not_mem : i âˆ‰ setâ‚ := Î» h,
        lt_irrefl _ ((finset.max'_lt_iff setâ‚ setâ‚_nonempty).mp max_lt i h),
      rw setâ‚_eq at not_mem,
      simp only [not_and, not_not, ne.def, dfinsupp.mem_support_to_fun,
        finset.mem_filter] at not_mem,
      exact ideal.mul_mem_right _ I (not_mem Hâ‚‚), },
    { -- in this case  `maxâ‚‚ < j`, so `of A j (y j) âˆˆ I`
      have not_mem : j âˆ‰ setâ‚‚ := Î» h,
        lt_irrefl _ ((finset.max'_lt_iff setâ‚‚ setâ‚‚_nonempty).mp max_lt j h),
      rw setâ‚‚_eq at not_mem,
      simp only [not_and, not_not, ne.def, dfinsupp.mem_support_to_fun,
        finset.mem_filter] at not_mem,
      exact ideal.mul_mem_left I _ (not_mem Hâ‚ƒ), }, },
  specialize homogeneous_mem_or_mem âŸ¨maxâ‚, submodule.coe_mem _âŸ© âŸ¨maxâ‚‚, submodule.coe_mem _âŸ© mem_I,
  cases homogeneous_mem_or_mem;
  simp only [ne.def, dfinsupp.mem_support_to_fun, finset.mem_filter] at mem_maxâ‚ mem_maxâ‚‚,
  exact mem_maxâ‚.2 homogeneous_mem_or_mem,
  exact mem_maxâ‚‚.2 homogeneous_mem_or_mem,
endâŸ©

lemma homogeneous_ideal.is_prime_iff (I : homogeneous_ideal ğ’œ) :
  I.1.is_prime â†”
  (I â‰  âŠ¤) âˆ§
    âˆ€ {x y : A}, set_like.is_homogeneous ğ’œ x â†’ set_like.is_homogeneous ğ’œ y
      â†’ (x * y âˆˆ I.1 â†’ x âˆˆ I.1 âˆ¨ y âˆˆ I.1) :=
âŸ¨Î» HI,
  âŸ¨ne_of_apply_ne _ HI.ne_top, Î» x y hx hy hxy, ideal.is_prime.mem_or_mem HI hxyâŸ©,
  Î» âŸ¨I_ne_top, homogeneous_mem_or_memâŸ©,
    I.prop.is_prime_of_homogeneous_mem_or_mem (subtype.coe_injective.ne I_ne_top)
      @homogeneous_mem_or_mem âŸ©

lemma ideal.is_prime.homogeneous_core {I : ideal A} (h : I.is_prime) :
  (I.homogeneous_core ğ’œ : ideal A).is_prime :=
begin
  apply (ideal.homogeneous_core ğ’œ I).prop.is_prime_of_homogeneous_mem_or_mem,
  { exact ne_top_of_le_ne_top h.ne_top (ideal.coe_homogeneous_core_le ğ’œ I) },
  rintros x y hx hy hxy,
  have H := h.mem_or_mem (ideal.coe_homogeneous_core_le ğ’œ I hxy),
  refine H.imp _ _,
  { exact ideal.mem_homogeneous_core_of_is_homogeneous_of_mem hx, },
  { exact ideal.mem_homogeneous_core_of_is_homogeneous_of_mem hy, },
end

lemma homogeneous_ideal.rad_eq (I : homogeneous_ideal ğ’œ) :
  (I : ideal A).radical = Inf {J | â†‘I â‰¤ J âˆ§ J.is_homogeneous ğ’œ âˆ§ J.is_prime} :=
begin
  letI : Î  i (x : ğ’œ i), decidable (x â‰  0) := Î» i x, classical.dec _,
  rw ideal.radical_eq_Inf,
  apply le_antisymm,
  { refine Inf_le_Inf _,
    rintros J âŸ¨HJâ‚, _, HJâ‚‚âŸ©,
    exact âŸ¨HJâ‚, HJâ‚‚âŸ©, },
  { intros x hx,
    rw [submodule.mem_Inf] at hx âŠ¢,
    rintros J âŸ¨HJâ‚, HJâ‚‚âŸ©,
    specialize hx (ideal.homogeneous_core ğ’œ J) _,
    refine âŸ¨_, (ideal.homogeneous_core ğ’œ _).prop, HJâ‚‚.homogeneous_coreâŸ©,
    { refine eq.trans_le _ (ideal.homogeneous_core_mono _ HJâ‚),
      have HI := I.prop,
      rw [ideal.is_homogeneous.iff_eq] at HI,
      rw HI },
    refine (ideal.coe_homogeneous_core_le ğ’œ J) hx, },
end

lemma ideal.is_homogeneous_ideal.radical {I : ideal A} (h : I.is_homogeneous ğ’œ)  :
  I.radical.is_homogeneous ğ’œ :=
begin
  have radI_eq : I.radical = _ := homogeneous_ideal.rad_eq âŸ¨I, hâŸ©,
  rw radI_eq,
  convert (Inf {J : homogeneous_ideal ğ’œ | I â‰¤ J.val âˆ§ J.val.is_prime}).2,
  ext J,
  simp only [subtype.coe_mk, set.mem_set_of_eq, subtype.exists, exists_prop],
  split;
  intro H,
  { exact âŸ¨_, H.2.1, âŸ¨H.1, H.2.2âŸ©, rflâŸ©, },
  { obtain âŸ¨J', HJ1, âŸ¨HJ2, HJ3âŸ©, rflâŸ© := H,
    exact âŸ¨HJ2, HJ1, HJ3âŸ©, },
end

end linear_ordered_cancel_add_comm_monoid
