/-
2020. No rights reserved. https://unlicense.org/
Authors: Johan Commelin
-/

import ring_theory.witt_vector_preps

noncomputable theory
open mv_polynomial


structure witt_package :=
(enum : Type)
(witt_polynomial : enum â†’ mv_polynomial enum â„¤)
(witt_structure  : Î  {idx : Type} (Î¦ : mv_polynomial idx â„¤), enum â†’ mv_polynomial (idx Ã— enum) â„¤)
(structure_prop  : âˆ€ {idx : Type} (Î¦ : mv_polynomial idx â„¤) (n : enum),
                    aeval (Î» k, (witt_structure Î¦ k)) (witt_polynomial n) =
                    aeval (Î» i, (rename_hom (Î» k, (i,k)) (witt_polynomial n))) Î¦)

namespace witt_package

variables (W : witt_package) (R : Type*) (S : Type*)
variables [comm_ring R] [comm_ring S]

/-- The ring of Witt vectors (depending on a â€œWitt packageâ€ `W`). -/
def witt_vector (R : Type*) := W.enum â†’ R

local notation `ğ•` := W.witt_vector -- type as `\bbW`

section ring_data
/-!
## Data for the ring structure

We will use the Witt package to define the data of a ring structure on the Witt vectors.
To show that this data satisfies the axioms of a ring, we will need more work,
and this will be done below.
-/

/-- An auxiliary inductive type to talk about the two sides of addition/multiplication.

`side.l` and `side.r` refer to the left and right hand sides respectively,
of expressions such as `x + y` and `x * y`.
We use this as indexing type for the Witt structure polynomials for addition and multiplication.
See `witt_add` and `witt_mul`. -/
inductive side | l | r

def side.cond {Î± : Type*} : side â†’ Î± â†’ Î± â†’ Î±
| side.l x _ := x
| side.r _ y := y

open side

/-- The polynomial used for defining the element `0` of the ring of Witt vectors. -/
noncomputable def witt_zero :=
W.witt_structure (0 : mv_polynomial empty â„¤)

/-- The polynomial used for defining the element `1` of the ring of Witt vectors. -/
noncomputable def witt_one :=
W.witt_structure (1 : mv_polynomial empty â„¤)

/-- The polynomial used for defining the addition of the ring of Witt vectors. -/
noncomputable def witt_add :=
W.witt_structure (X l + X r)

/-- The polynomial used for defining the multiplication of the ring of Witt vectors. -/
noncomputable def witt_mul :=
W.witt_structure (X l * X r)

/-- The polynomial used for defining the negation of the ring of Witt vectors. -/
noncomputable def witt_neg :=
W.witt_structure (-X ())

noncomputable instance : has_zero (ğ• R) :=
âŸ¨Î» n, aeval (Î» p : empty Ã— W.enum, p.1.elim) (W.witt_zero n)âŸ©

noncomputable instance : has_one (ğ• R) :=
âŸ¨Î» n, aeval (Î» p : empty Ã— W.enum, p.1.elim) (W.witt_one n)âŸ©

noncomputable instance : has_add (ğ• R) :=
âŸ¨Î» x y n, aeval (Î» sn : side Ã— W.enum, cond sn.1 (x sn.2) (y sn.2)) (W.witt_add n)âŸ©

noncomputable instance : has_mul (ğ• R) :=
âŸ¨Î» x y n, aeval (Î» sn : side Ã— W.enum, cond sn.1 (x sn.2) (y sn.2)) (W.witt_mul n)âŸ©

noncomputable instance : has_neg (ğ• R) :=
âŸ¨Î» x n, aeval (Î» k : unit Ã— W.enum, x k.2) (W.witt_neg n)âŸ©

end ring_data

section map
/-!
## Functoriality of the Witt vector construction

We define `witt_package.map`, the map between rings of Witt vectors
induced by a map between the coefficient rings.
-/

open function
variables {Î± : Type*} {Î² : Type*}

/-- The map between Witt vectors induced by a map between the coefficients. -/
def map (f : Î± â†’ Î²) : ğ• Î± â†’ ğ• Î² := Î» w, f âˆ˜ w

lemma map_injective (f : Î± â†’ Î²) (hf : injective f) :
  injective (W.map f : ğ• Î± â†’ ğ• Î²) :=
Î» x y h, funext $ Î» n, hf $ by exact congr_fun h n

lemma map_surjective (f : Î± â†’ Î²) (hf : surjective f) :
  surjective (W.map f : ğ• Î± â†’ ğ• Î²) :=
Î» x, âŸ¨Î» n, classical.some $ hf $ x n,
by { funext n, dsimp [map], rw classical.some_spec (hf (x n)) }âŸ©

variables (f : R â†’+* S)

/-- Auxiliary tactic for showing that `witt_package.map` respects ring data. -/
meta def witt_map : tactic unit :=
`[funext n,
  show f (aeval _ _) = aeval _ _,
  rw map_aeval,
  apply evalâ‚‚_hom_congr (ring_hom.ext_int _ _) _ rfl,
  funext p,
  rcases p with âŸ¨âŸ¨âŸ©, iâŸ©; refl]

@[simp] lemma map_zero : W.map f (0 : ğ• R) = 0 :=
by witt_map

@[simp] lemma map_one : W.map f (1 : ğ• R) = 1 :=
by witt_map

@[simp] lemma map_add (x y : ğ• R) : W.map f (x + y) = W.map f x + W.map f y :=
by witt_map

@[simp] lemma map_mul (x y : ğ• R) : W.map f (x * y) = W.map f x * W.map f y :=
by witt_map

@[simp] lemma map_neg (x : ğ• R) : W.map f (-x) = - W.map f x :=
by witt_map

end map

section ghost_map
/-!
## Ghost map/components
-/

noncomputable def ghost_component (n : W.enum) (x : ğ• R) : R :=
aeval x (W.witt_polynomial n)

end ghost_map

end witt_package
