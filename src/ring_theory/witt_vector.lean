/-
2020. No rights reserved. https://unlicense.org/
Authors: Johan Commelin
-/

-- import algebra.inj_surj
import data.nat.choose
import data.int.gcd
import data.mv_polynomial
import data.zmod.basic
import data.fintype.card
import ring_theory.multiplicity
import algebra.invertible
import number_theory.quadratic_reciprocity
import ring_theory.witt_vector_preps
import tactic
import tactic.nth_rewrite

/-!
# Witt vectors

## Main definitions
TODO

## Notation
TODO

## Implementation details
TODO
-/

/-- `witt_vectors p R` is the ring of `p`-typical Witt vectors over the commutative ring `R`,
where `p` is a prime number.

If `p` is invertible in `R`, this ring is isomorphic to `‚Ñï ‚Üí R` (the product of `‚Ñï` copies of `R`).
If `R` is a ring of characteristic `p`, then `witt_vectors p R` is a ring of characteristic `0`.
The canonical example is `witt_vectors p (zmod p)`,
which is isomorphic to the `p`-adic integers `‚Ñ§_[p]`. -/
def witt_vectors (p : ‚Ñï) (R : Type*) := ‚Ñï ‚Üí R

universes u v w u‚ÇÅ
open mv_polynomial (hiding aeval) -- this hiding is a hack, because we setup notation below
open set
open finset (range)

open_locale big_operators

-- TODO: This should be fixed in mathlib
local notation `aeval` := mv_polynomial.aeval _ _

local attribute [-simp] coe_eval‚ÇÇ_hom

variables (p : ‚Ñï)
variables (R : Type u) [comm_ring R]

/-!
## Witt polynomials

To endow `witt_vectors p R` with a ring structure,
we need to study the so-called Witt polynomials.
-/

/-- `witt_polynomial p R n` is the `n`-th Witt polynomial
with respect to a prime `p` with coefficients in a commutative ring `R`.
It is defined as:

`‚àë_{i ‚â§ n} p^i X_i^{p^{n-i}} ‚àà R[X_0, X_1, X_2, ‚Ä¶]`. -/
noncomputable def witt_polynomial (n : ‚Ñï) : mv_polynomial ‚Ñï R :=
‚àë i in range (n+1), C (p^i : R) * X i ^ (p^(n-i))

/-! We set up notation locally to this file, to keep statements short and comprehensible.
This allows us to simply write `W n` or `W_ ‚Ñ§ n`. -/

-- Notation with ring of coefficients explicit
localized "notation `W_` := witt_polynomial p"   in witt
-- Notation with ring of coefficients implicit
localized "notation `W`  := witt_polynomial p _" in witt

open_locale witt

/- The first observation is that the Witt polynomial doesn't really depend on the coefficient ring.
If we map the coefficients through a ring homomorphism, we obtain the corresponding Witt polynomial
over the target ring. -/
section
variables {R} {S : Type*} [comm_ring S]

@[simp] lemma map_hom_witt_polynomial (f : R ‚Üí+* S) (n : ‚Ñï) :
  map_hom f (W n) = W n :=
begin
  rw [witt_polynomial, ring_hom.map_sum],
  apply finset.sum_congr rfl,
  intros i hi,
  rw [ring_hom.map_mul, map_hom_C f, ring_hom.map_pow,
      ring_hom.map_nat_cast, ring_hom.map_pow, ring_hom.map_pow, map_hom_X],
end

-- no longer used...
lemma map_witt_polynomial (f : R ‚Üí+* S) (n : ‚Ñï) :
  map f (W n) = W n :=
map_hom_witt_polynomial p f n

variables (R)

lemma aeval_witt_polynomial {A : Type*} [comm_ring A] [algebra R A] (f : ‚Ñï ‚Üí A) (n : ‚Ñï) :
  aeval f (W_ R n) = ‚àë i in range (n+1), p^i * (f i) ^ (p ^ (n-i)) :=
by { -- clean this up
  simp only [witt_polynomial, alg_hom.map_sum, aeval_C, ring_hom.map_nat_cast, alg_hom.map_pow,
    C_pow, aeval_X, alg_hom.map_mul,
 ring_hom.map_nat_cast, alg_hom.map_pow, C_pow, aeval_X, alg_hom.map_mul],
 simp only [alg_hom.map_nat_cast], }

end

/-- View a polynomial written in terms of the basis of Witt polynomials
as a polynomial written in terms of the standard basis.

In particular, this sends `X n` to `witt_polynomial p n`.
This fact is recorded in `from_W_to_X_basis_X`. -/
noncomputable def from_W_to_X_basis : mv_polynomial ‚Ñï R ‚Üí‚Çê[R] mv_polynomial ‚Ñï R :=
aeval W

@[simp] lemma from_W_to_X_basis_X (n) : from_W_to_X_basis p R (X n) = W n :=
by simp [from_W_to_X_basis]

-- We need p to be invertible for the following definitions

/-- The `X_in_terms_of_W p R n` is the polynomial on the basis of Witt polynomials
that corresponds to the ordinary `X n`.
This means that `from_W_to_X_basis` sends `X_in_terms_of_W p R n` to `X n`.
This fact is recorded in `from_W_to_X_basis_X_in_terms_of_W`. -/
noncomputable def X_in_terms_of_W [invertible (p : R)] :
  ‚Ñï ‚Üí mv_polynomial ‚Ñï R
| n := (X n - (‚àë i : fin n,
  have _ := i.2, (C (p^(i : ‚Ñï) : R) * (X_in_terms_of_W i)^(p^(n-i))))) * C (‚Öüp ^ n : R)

lemma X_in_terms_of_W_eq [invertible (p : R)] {n : ‚Ñï} :
  X_in_terms_of_W p R n =
  (X n - (‚àë i in range n, C (p^i : R) * X_in_terms_of_W p R i ^ p ^ (n - i))) * C (‚Öüp ^ n : R) :=
by { rw [X_in_terms_of_W, ‚Üê fin.sum_univ_eq_sum_range], refl }

/-- View a polynomial written in terms of the standard basis
as a polynomial written in terms of the Witt basis.

This sends `W n` to `X n`, and `X n` to `X_in_terms_of_W p R n`. -/
noncomputable def from_X_to_W_basis [invertible (p : R)] :
  mv_polynomial ‚Ñï R ‚Üí‚Çê[R] mv_polynomial ‚Ñï R :=
aeval (X_in_terms_of_W p R)

@[simp] lemma from_X_to_W_basis_X [invertible (p : R)] (n : ‚Ñï) :
  (from_X_to_W_basis p R) (X n) = X_in_terms_of_W p R n :=
by rw [from_X_to_W_basis, aeval_X]

@[simp] lemma from_W_to_X_basis_X_in_terms_of_W [invertible (p : R)] (n : ‚Ñï) :
  from_W_to_X_basis p R (X_in_terms_of_W p R n) = X n :=
begin
  apply nat.strong_induction_on n,
  clear n, intros n H,
  rw [X_in_terms_of_W_eq],
  simp only [alg_hom.map_mul, alg_hom.map_sub, alg_hom_C, from_W_to_X_basis_X p R n, alg_hom.map_sum],
  rw [finset.sum_congr rfl, (_ : W_ R n - ‚àë i in range n, C (p^i : R) * (X i)^p^(n-i) = C (p^n : R) * X n)],
  { rw [mul_right_comm, ‚Üê C_mul, ‚Üê mul_pow, mul_inv_of_self, one_pow, C_1, one_mul] },
  { simp [witt_polynomial, nat.sub_self, finset.sum_range_succ] },
  { intros i h,
    rw finset.mem_range at h,
    simp only [alg_hom.map_mul, alg_hom.map_pow, alg_hom_C, function.comp_app, H i h] },
end

lemma from_W_to_X_basis_comp_from_X_to_W_basis [invertible (p : R)] :
  (from_W_to_X_basis p R).comp (from_X_to_W_basis p _) = alg_hom.id _ _ :=
begin
  apply mv_polynomial.alg_hom_ext R (mv_polynomial ‚Ñï R),
  intro n,
  rw [from_X_to_W_basis, alg_hom.comp_apply, aeval_X],
  exact from_W_to_X_basis_X_in_terms_of_W p R n
end

lemma X_in_terms_of_W_aux [invertible (p : R)] (n : ‚Ñï) :
  X_in_terms_of_W p R n * C (p^n : R) =
  X n - ‚àë i in range n, C (p^i : R) * (X_in_terms_of_W p R i)^p^(n-i) :=
by rw [X_in_terms_of_W_eq, mul_assoc, ‚Üê C_mul, ‚Üê mul_pow, inv_of_mul_self, one_pow, C_1, mul_one]

lemma from_X_to_W_basis_witt_polynomial [invertible (p : R)] (n : ‚Ñï) :
  (from_X_to_W_basis p R) (W n) = X n :=
begin
  rw [witt_polynomial],
  rw [alg_hom.map_sum],
  simp only [alg_hom.map_pow, C_pow, alg_hom.map_mul],
  simp only [from_X_to_W_basis_X, alg_hom_C],
  rw [finset.sum_range_succ, nat.sub_self, nat.pow_zero, pow_one],
  rw [mul_comm, ‚Üê C_pow],
  rw X_in_terms_of_W_aux,
  simp only [C_pow, sub_add_cancel],
end

lemma from_X_to_W_basis_comp_from_W_to_X_basis [invertible (p : R)] :
  (from_X_to_W_basis p R).comp (from_W_to_X_basis p _) = alg_hom.id _ _ :=
begin
  apply mv_polynomial.alg_hom_ext R (mv_polynomial ‚Ñï R),
  intro n,
  rw [alg_hom.comp_apply, from_W_to_X_basis_X],
  exact from_X_to_W_basis_witt_polynomial p R n,
end

@[simp] lemma from_X_to_W_basis_comp_from_W_to_X_basis_apply [invertible (p : R)] (œÜ : mv_polynomial ‚Ñï R) :
  (from_X_to_W_basis p R) (from_W_to_X_basis p R œÜ) = œÜ :=
begin
  rw [‚Üê alg_hom.comp_apply, from_X_to_W_basis_comp_from_W_to_X_basis, alg_hom.id_apply],
end

@[simp] lemma from_W_to_X_basis_comp_from_X_to_W_basis_apply [invertible (p : R)] (œÜ : mv_polynomial ‚Ñï R) :
  (from_W_to_X_basis p R) (from_X_to_W_basis p R œÜ) = œÜ :=
begin
  rw [‚Üê alg_hom.comp_apply, from_W_to_X_basis_comp_from_X_to_W_basis, alg_hom.id_apply],
end

@[simp] lemma X_in_terms_of_W_prop‚ÇÇ [invertible (p : R)] (k : ‚Ñï) :
  aeval (X_in_terms_of_W p R) (W_ R k) = X k :=
begin
  rw ‚Üê from_X_to_W_basis_comp_from_W_to_X_basis_apply p R (X k),
  rw from_W_to_X_basis_X,
  refl,
end

@[simp] lemma X_in_terms_of_W_prop [invertible (p : R)] (n : ‚Ñï) :
  aeval (W_ R) (X_in_terms_of_W p R n) = X n :=
begin
  rw ‚Üê from_W_to_X_basis_comp_from_X_to_W_basis_apply p R (X n),
  rw from_X_to_W_basis_X,
  refl,
end

noncomputable def witt.alg_equiv [invertible (p : R)] : mv_polynomial ‚Ñï R ‚âÉ‚Çê[R] mv_polynomial ‚Ñï R :=
equiv_of_family (W_ R) (X_in_terms_of_W p R)
(X_in_terms_of_W_prop‚ÇÇ p R)
(X_in_terms_of_W_prop p R)

variables {idx : Type*} [fact p.prime]

noncomputable def witt_structure_rat (Œ¶ : mv_polynomial idx ‚Ñö) (n : ‚Ñï) :
  mv_polynomial (idx √ó ‚Ñï) ‚Ñö :=
aeval (Œª k : ‚Ñï, (aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñö k)))) Œ¶) (X_in_terms_of_W p ‚Ñö n)

theorem witt_structure_rat_prop (Œ¶ : mv_polynomial idx ‚Ñö) (n : ‚Ñï) :
  aeval (witt_structure_rat p Œ¶) (W_ ‚Ñö n) = aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñö n))) Œ¶ :=
calc aeval (witt_structure_rat p Œ¶) (W_ ‚Ñö n) =
      aeval (Œª k, aeval (Œª b, (rename_hom (prod.mk b)) (W_ ‚Ñö k)) Œ¶)
        (aeval (X_in_terms_of_W p ‚Ñö) (W_ ‚Ñö n)) :
      by { conv_rhs { rw [aeval_eq_eval‚ÇÇ_hom', map_aeval] },
           apply eval‚ÇÇ_hom_congr (ring_hom.ext_rat _ _) rfl rfl }
... = aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñö n))) Œ¶ :
      by rw [X_in_terms_of_W_prop‚ÇÇ p _ n, aeval_X]

theorem witt_structure_prop_exists_unique (Œ¶ : mv_polynomial idx ‚Ñö) :
  ‚àÉ! (œÜ : ‚Ñï ‚Üí mv_polynomial (idx √ó ‚Ñï) ‚Ñö),
    ‚àÄ (n : ‚Ñï), aeval œÜ (W_ ‚Ñö n) = aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñö n))) Œ¶ :=
begin
  refine ‚ü®witt_structure_rat p Œ¶, _, _‚ü©,
  { intro n, apply witt_structure_rat_prop },
  { intros œÜ H,
    funext n,
    rw show œÜ n = aeval œÜ (aeval (W_ ‚Ñö) (X_in_terms_of_W p ‚Ñö n)),
    { rw [X_in_terms_of_W_prop p, aeval_X] },
    rw [aeval_eq_eval‚ÇÇ_hom', map_aeval],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_rat _ _) _ rfl,
    funext k, exact H k },
end

lemma witt_structure_rat_rec_aux (Œ¶ : mv_polynomial idx ‚Ñö) (n) :
  (witt_structure_rat p Œ¶ n) * C (p^n : ‚Ñö) =
  ((aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñö n))) Œ¶)) -
  ‚àë i in range n, C (p^i : ‚Ñö) * (witt_structure_rat p Œ¶ i)^p^(n-i) :=
begin
  have := X_in_terms_of_W_aux p ‚Ñö n,
  replace := congr_arg (aeval (Œª k : ‚Ñï, (aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñö k)))) Œ¶)) this,
  rw [alg_hom.map_mul, aeval_C] at this,
  convert this, clear this,
  conv_rhs { simp only [alg_hom.map_sub, aeval_X] },
  rw sub_right_inj,
  rw [alg_hom.map_sum, finset.sum_congr rfl],
  intros i hi,
  rw [alg_hom.map_mul, aeval_C, alg_hom.map_pow],
  refl
end

lemma witt_structure_rat_rec (Œ¶ : mv_polynomial idx ‚Ñö) (n) :
  (witt_structure_rat p Œ¶ n) = C (1/p^n : ‚Ñö) *
  (aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñö n))) Œ¶ -
  ‚àë i in range n, C (p^i : ‚Ñö) * (witt_structure_rat p Œ¶ i)^p^(n-i)) :=
begin
  rw [‚Üê witt_structure_rat_rec_aux p Œ¶ n, mul_comm, mul_assoc,
      ‚Üê C_mul, mul_one_div_cancel, C_1, mul_one],
  exact pow_ne_zero _ (nat.cast_ne_zero.2 $ ne_of_gt (nat.prime.pos ‚Äπ_‚Ä∫)),
end

noncomputable def witt_structure_int (Œ¶ : mv_polynomial idx ‚Ñ§) (n : ‚Ñï) : mv_polynomial (idx √ó ‚Ñï) ‚Ñ§ :=
finsupp.map_range rat.num (rat.coe_int_num 0)
  (witt_structure_rat p (map_hom (int.cast_ring_hom ‚Ñö) Œ¶) n)
.

lemma mv_polynomial.coe_int_rat_map_injective (I : Type*) :
  function.injective (map_hom (int.cast_ring_hom ‚Ñö) : mv_polynomial I ‚Ñ§ ‚Üí mv_polynomial I ‚Ñö) :=
begin
  apply map_injective,
  intros m n,
  exact int.cast_inj.mp
end
.

lemma sub_congr (a b c d : R) (h1 : a = c) (h2 : b = d) : a - b = c - d :=
by rw [h1, h2]
.

variables {Œπ : Type*} {œÉ : Type*}
variables {S : Type*} [comm_ring S]
variables {T : Type*} [comm_ring T]

lemma foo' (Œ¶ : mv_polynomial idx ‚Ñ§) (n : ‚Ñï)
  (IH : ‚àÄ m : ‚Ñï, m < n ‚Üí
    map_hom (int.cast_ring_hom ‚Ñö) (witt_structure_int p Œ¶ m) =
    witt_structure_rat p (map_hom (int.cast_ring_hom ‚Ñö) Œ¶) m) :
  map_hom (int.cast_ring_hom ‚Ñö)
    (((aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñ§ n)))) Œ¶) -
      (‚àë i in range n, C (p^i : ‚Ñ§) * (witt_structure_int p Œ¶ i)^p^(n-i))) =
  aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñö n)))
   (map_hom (int.cast_ring_hom ‚Ñö) Œ¶) -
  (‚àë i in range n, C (p^i : ‚Ñö) * (witt_structure_rat p (map_hom (int.cast_ring_hom ‚Ñö) Œ¶) i)^p^(n-i)) :=
begin
  rw [ring_hom.map_sub, ring_hom.map_sum],
  apply sub_congr,
  { clear IH,
    rw [map_aeval, aeval_eq_eval‚ÇÇ_hom', eval‚ÇÇ_hom_map_hom],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext k, rw [map_hom_rename_hom, map_hom_witt_polynomial] },
  { apply finset.sum_congr rfl,
    intros i hi,
    rw finset.mem_range at hi,
    specialize IH i hi,
    simp only [IH, int.cast_coe_nat, ring_hom.eq_int_cast, C_pow,
      ring_hom.map_pow, map_hom_C, ring_hom.map_mul],
    simp only [ring_hom.map_nat_cast], }
end

@[simp] lemma witt_polynomial_zmod_self (n : ‚Ñï) :
  W_ (zmod (p^(n+1))) (n + 1) = aeval (Œª i, ((X i)^p)) (W_ (zmod (p^(n+1))) n) :=
begin
  delta witt_polynomial,
  rw [finset.sum_range_succ, ‚Üê nat.cast_pow,
      char_p.cast_eq_zero (zmod (p^(n+1))) (p^(n+1)),
      C_0, zero_mul, zero_add],
  rw [alg_hom.map_sum, finset.sum_congr rfl],
  intros k hk,
  rw [alg_hom.map_mul, alg_hom.map_pow, aeval_X, alg_hom_C],
  rw [‚Üê pow_mul, mul_comm p, ‚Üê nat.pow_succ, nat.succ_eq_add_one],
  congr,
  rw finset.mem_range at hk,
  omega
end

@[simp] lemma frobenius_zmod (p : ‚Ñï) [hp : fact p.prime] (a : zmod p) :
  frobenius _ p a = a :=
begin
  have ppos : p > 0 := nat.prime.pos ‚Äπ_‚Ä∫,
  by_cases h : a = 0,
  { subst a, apply zero_pow ppos },
  { have := zmod.fermat_little p h,
    replace := congr_arg (Œª x, a * x) this,
    simp at this,
    convert this,
    rw [‚Üê pow_succ, frobenius_def], congr, clear this h a hp,
    revert ppos p, omega manual nat }
end

lemma fermat_little' (p : ‚Ñï) [hp : fact p.prime] (a : zmod p) : a^p = a :=
frobenius_zmod p a

lemma mv_polynomial.frobenius_zmod (œÜ : mv_polynomial œÉ (zmod p)) :
  frobenius _ p œÜ = aeval (Œª i, X i ^ p) œÜ :=
begin
  apply induction_on œÜ,
  { intro a, rw [aeval_C, frobenius_def, ‚Üê C_pow, fermat_little'], refl },
  { simp only [alg_hom.map_add, ring_hom.map_add], intros _ _ hf hg, rw [hf, hg] },
  { simp only [aeval_X, ring_hom.map_mul, alg_hom.map_mul],
    intros _ _ hf, rw [hf, frobenius_def], },
end

lemma mv_polynomial.zmod_pow_char (œÜ : mv_polynomial Œπ (zmod p)) :
  (aeval (Œª i, (X i)^p)) œÜ = œÜ^p :=
begin
  symmetry,
  apply mv_polynomial.frobenius_zmod
end

lemma rat_mv_poly_is_integral_iff (p : mv_polynomial Œπ ‚Ñö) :
  map (int.cast_ring_hom ‚Ñö) (finsupp.map_range rat.num (rat.coe_int_num 0) p) = p ‚Üî
  ‚àÄ m, (coeff m p).denom = 1 :=
begin
  rw mv_polynomial.ext_iff,
  apply forall_congr, intro m,
  rw coeff_map,
  split; intro h,
  { rw [‚Üê h], apply rat.coe_int_denom },
  { show (rat.num (coeff m p) : ‚Ñö) = coeff m p,
    lift (coeff m p) to ‚Ñ§ using h with n hn,
    rw rat.coe_int_num n }
end

lemma mv_polynomial.algebra_map_eq_C (r : R) :
  algebra_map R (mv_polynomial œÉ R) r = C r :=
rfl

lemma blur' (Œ¶ : mv_polynomial idx ‚Ñ§) (n : ‚Ñï)
  (IH : ‚àÄ m : ‚Ñï, m < (n + 1) ‚Üí
    map_hom (int.cast_ring_hom ‚Ñö) (witt_structure_int p Œ¶ m) =
      witt_structure_rat p (map_hom (int.cast_ring_hom ‚Ñö) Œ¶) m) :
  aeval (Œª b, rename_hom (Œª i, (b, i)) (aeval (Œª i, ((X i ^ p : mv_polynomial ‚Ñï ‚Ñ§))) (W_ ‚Ñ§ n))) Œ¶ =
  aeval (Œª i, aeval (Œª bi, (X bi)^p) (witt_structure_int p Œ¶ i)) (W_ ‚Ñ§ n) :=
begin
  have aux := Œª x, aeval_X ‚Ñ§ _ (witt_structure_int p Œ¶) x,
  have aux‚ÇÇ : ‚àÄ n : ‚Ñï, (algebra_map ‚Ñö (mv_polynomial (idx √ó ‚Ñï) ‚Ñö)) (p ^ n) =
    map_hom (int.cast_ring_hom ‚Ñö) (aeval (witt_structure_int p Œ¶) (C (p ^ n : ‚Ñ§))),
  { intro n, rw [map_aeval, eval‚ÇÇ_hom_C, ring_hom.eq_int_cast, mv_polynomial.algebra_map_eq_C],
    rw [C_pow, C_eq_coe_nat], norm_cast, },
  have key := (witt_structure_rat_prop p (map_hom (int.cast_ring_hom ‚Ñö) Œ¶) n).symm,
  conv_rhs at key
  { rw [witt_polynomial, alg_hom.map_sum],
    conv {
      apply_congr, skip,
      rw [alg_hom.map_mul, alg_hom.map_pow, aeval_C, aeval_X],
      rw [‚Üê IH x (finset.mem_range.mp H)],
      rw [‚Üê aux, aux‚ÇÇ],
      rw [‚Üê ring_hom.map_pow, ‚Üê alg_hom.map_pow, ‚Üê ring_hom.map_mul, ‚Üê alg_hom.map_mul], },
    rw [‚Üê ring_hom.map_sum, ‚Üê alg_hom.map_sum], },
  replace key := congr_arg (aeval (Œª bi, (X bi ^ p : mv_polynomial (idx √ó ‚Ñï) ‚Ñö))) key,

  apply mv_polynomial.coe_int_rat_map_injective,

  calc _ = _ : _
     ... = _ : key
     ... = _ : _,

  { clear IH aux aux‚ÇÇ key,
    simp only [map_aeval, map_eval‚ÇÇ_hom, aeval_eq_eval‚ÇÇ_hom', ring_hom.map_pow, eval‚ÇÇ_hom_map_hom,
      eval‚ÇÇ_hom_rename_hom, ‚Üê map_hom_witt_polynomial p (int.cast_ring_hom ‚Ñö)],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext i,
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext k,
    rw [map_hom_rename_hom, map_hom_X, rename_hom_X] },
  { simp only [map_aeval, map_eval‚ÇÇ_hom, aeval_eq_eval‚ÇÇ_hom', ring_hom.map_pow,
      eval‚ÇÇ_hom_map_hom, witt_polynomial, int.nat_cast_eq_coe_nat],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext i,
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext bi, rw map_hom_X }
end
.

lemma map_hom_witt_structure_int (Œ¶ : mv_polynomial idx ‚Ñ§) (n : ‚Ñï) :
  map_hom (int.cast_ring_hom ‚Ñö) (witt_structure_int p Œ¶ n) =
    witt_structure_rat p (map_hom (int.cast_ring_hom ‚Ñö) Œ¶) n :=
begin
  apply nat.strong_induction_on n, clear n,
  intros n IH,
  erw rat_mv_poly_is_integral_iff,
  intro c,
  rw witt_structure_rat_rec p _ n,
  rw coeff_C_mul,
  rw [mul_comm, mul_div_assoc', mul_one],
  rw ‚Üê foo' p Œ¶ n IH,
  erw coeff_map,
  rw show (p : ‚Ñö)^n = ((p^n : ‚Ñï) : ‚Ñ§), by norm_cast,
  erw rat.denom_div_cast_eq_one_iff,
  swap,
  { rw int.coe_nat_pow, apply pow_ne_zero, exact_mod_cast ne_of_gt (nat.prime.pos ‚Äπ_‚Ä∫) },
  induction n with n ih, {simp}, clear ih, revert c,
  rw [‚Üê C_dvd_iff_dvd_coeff, nat.succ_eq_add_one],
  rw C_dvd_iff_zmod,
  -- rw ‚Üê eq_mod_iff_dvd_sub',
  rw [ring_hom.map_sub, sub_eq_zero, map_aeval],
  simp only [witt_polynomial_zmod_self, map_aeval, map_hom_witt_polynomial,
    map_hom_rename_hom, ring_hom.map_pow, rename_hom_X],

  have key := congr_arg (map_hom (int.cast_ring_hom (zmod (p^(n+1))))) (blur' p Œ¶ n IH),

  calc _ = _ : _
     ... = _ : key
     ... = _ : _,

  { clear key IH,
    rw map_aeval,
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext i,
    rw ‚Üê map_hom_witt_polynomial p (int.cast_ring_hom (zmod _)) n,
    simp only [map_hom_X, map_aeval, map_eval‚ÇÇ_hom, ring_hom.map_pow,
      rename_hom_X, eval‚ÇÇ_hom_map_hom],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) rfl rfl, },

  { clear key IH,
    rw [aeval_witt_polynomial, ring_hom.map_sum, ring_hom.map_sum],
    apply finset.sum_congr rfl,
    intros k hk, rw finset.mem_range at hk,
    rw [‚Üê sub_eq_zero, ‚Üê ring_hom.map_sub, ‚Üê C_dvd_iff_zmod],
    rw [‚Üê int.nat_cast_eq_coe_nat, C_eq_coe_nat],
    rw [‚Üê int.nat_cast_eq_coe_nat, C_eq_coe_nat, ‚Üê mul_sub],
    rw show p^(n+1) = p^k * p^(n-k+1),
    { rw ‚Üê nat.pow_add, congr' 1, omega },
    rw [nat.cast_mul, nat.cast_pow, nat.cast_pow],
    apply mul_dvd_mul_left,
    rw show p^(n+1-k) = p * p^(n-k),
    { rw [mul_comm, ‚Üê nat.pow_succ], congr' 1, omega },
    rw [pow_mul],
    -- the machine!
    apply dvd_sub_pow_of_dvd_sub,

    rw [‚Üê C_eq_coe_nat, int.nat_cast_eq_coe_nat, C_dvd_iff_zmod],
    rw [ring_hom.map_sub, sub_eq_zero, ring_hom.map_pow, ‚Üê mv_polynomial.zmod_pow_char],
    rw [map_aeval, aeval_eq_eval‚ÇÇ_hom', eval‚ÇÇ_hom_map_hom],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    { funext i, rw [ring_hom.map_pow, map_hom_X] } }
end
.

theorem witt_structure_int_prop (Œ¶ : mv_polynomial idx ‚Ñ§) (n) :
  aeval (witt_structure_int p Œ¶) (W_ ‚Ñ§ n) = aeval (Œª b, (rename_hom (Œª i, (b,i)) (W_ ‚Ñ§ n))) Œ¶ :=
begin
  apply mv_polynomial.coe_int_rat_map_injective,
  convert witt_structure_rat_prop p (map_hom (int.cast_ring_hom ‚Ñö) Œ¶) n,
  { rw [map_aeval, aeval_eq_eval‚ÇÇ_hom',
        ‚Üê map_hom_witt_polynomial p (int.cast_ring_hom ‚Ñö), eval‚ÇÇ_hom_map_hom],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext i, apply map_hom_witt_structure_int },
  { rw [map_aeval, aeval_eq_eval‚ÇÇ_hom', eval‚ÇÇ_hom_map_hom],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext b, rw [map_hom_rename_hom, map_hom_witt_polynomial] }
end

theorem witt_structure_int_exists_unique (Œ¶ : mv_polynomial idx ‚Ñ§) :
  ‚àÉ! (œÜ : ‚Ñï ‚Üí mv_polynomial (idx √ó ‚Ñï) ‚Ñ§),
  ‚àÄ (n : ‚Ñï), aeval œÜ (W_ ‚Ñ§ n) = aeval (Œª b : idx, (rename_hom (Œª i, (b,i)) (W_ ‚Ñ§ n))) Œ¶ :=
begin
  refine ‚ü®witt_structure_int p Œ¶, _, _‚ü©,
  { apply witt_structure_int_prop },
  { intros œÜ H,
    funext k,
    apply mv_polynomial.coe_int_rat_map_injective,
    rw map_hom_witt_structure_int,
    refine congr_fun _ k,
    have := (witt_structure_prop_exists_unique p (map_hom (int.cast_ring_hom ‚Ñö) Œ¶)),
    apply unique_of_exists_unique this,
    { clear this, intro n,
      specialize H n,
      convert congr_arg (map_hom (int.cast_ring_hom ‚Ñö)) H using 1,
      { rw [map_aeval, ‚Üê map_hom_witt_polynomial p (int.cast_ring_hom ‚Ñö),
        aeval_eq_eval‚ÇÇ_hom', eval‚ÇÇ_hom_map_hom],
        exact eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) rfl rfl },
      { rw [map_aeval, aeval_eq_eval‚ÇÇ_hom', eval‚ÇÇ_hom_map_hom],
        apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
        funext i, rw [map_hom_rename_hom, map_hom_witt_polynomial] } },
    { intro n, apply witt_structure_rat_prop } },
end
.

theorem witt_structure_prop (Œ¶ : mv_polynomial idx ‚Ñ§) (n) :
  aeval (Œª i, map_hom (int.cast_ring_hom R) (witt_structure_int p Œ¶ i)) (W_ ‚Ñ§ n) =
  aeval (Œª b, (rename_hom (Œª i, (b,i)) (W n))) Œ¶ :=
begin
  convert congr_arg (map_hom (int.cast_ring_hom R)) (witt_structure_int_prop p Œ¶ n),
  { rw [aeval_eq_eval‚ÇÇ_hom', map_aeval],
    exact eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) rfl rfl },
  { rw [aeval_eq_eval‚ÇÇ_hom', map_aeval],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    simp only [map_hom_rename_hom, map_hom_witt_polynomial] }
end

namespace witt_vectors

local notation `ùïé` := witt_vectors -- type as `\bbW`

instance : functor (ùïé p) :=
{ map := Œª Œ± Œ≤ f v, f ‚àò v,
  map_const := Œª Œ± Œ≤ a v, Œª _, a }

instance : is_lawful_functor (ùïé p) :=
{ map_const_eq := Œª Œ± Œ≤, rfl,
  id_map := Œª Œ± v, rfl,
  comp_map := Œª Œ± Œ≤ Œ≥ f g v, rfl }

variable (R)

instance : has_zero (ùïé p R) :=
‚ü®Œª _, 0‚ü©

variable {R}

def Teichmuller (r : R) : ùïé p R
| 0 := r
| (n+1) := 0

@[simp] lemma Teichmuller_zero : Teichmuller p (0:R) = 0 :=
funext $ Œª n, match n with | 0 := rfl | (n+1) := rfl end

variable (R)

instance : has_one (ùïé p R) :=
‚ü®Teichmuller p 1‚ü©

noncomputable def witt_add : ‚Ñï ‚Üí mv_polynomial (bool √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (X tt + X ff)

noncomputable def witt_mul : ‚Ñï ‚Üí mv_polynomial (bool √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (X tt * X ff)

noncomputable def witt_neg : ‚Ñï ‚Üí mv_polynomial (unit √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (-X unit.star)

noncomputable instance : has_add (ùïé p R) :=
‚ü®Œª x y n, aeval (Œª bn : bool √ó ‚Ñï, cond bn.1 (x bn.2) (y bn.2)) (witt_add p n)‚ü©

noncomputable instance : has_mul (ùïé p R) :=
‚ü®Œª x y n, aeval (Œª bn : bool √ó ‚Ñï, cond bn.1 (x bn.2) (y bn.2)) (witt_mul p n)‚ü©

noncomputable instance : has_neg (ùïé p R) :=
‚ü®Œª x n, aeval (Œª n : unit √ó ‚Ñï, x n.2) (witt_neg p n)‚ü©

variable {R}

@[simp] lemma Teichmuller_one : Teichmuller p (1:R) = 1 := rfl

variable {p}

noncomputable def ghost_component (n : ‚Ñï) (w : ùïé p R) : R :=
aeval w (W_ R n)

section map
open function
variables {Œ± : Type*} {Œ≤ : Type*}

def map (f : Œ± ‚Üí Œ≤) : ùïé p Œ± ‚Üí ùïé p Œ≤ := Œª w, f ‚àò w

lemma map_injective (f : Œ± ‚Üí Œ≤) (hf : injective f) :
  injective (map f : ùïé p Œ± ‚Üí ùïé p Œ≤) :=
Œª x y h, funext $ Œª n, hf $ by exact congr_fun h n

lemma map_surjective (f : Œ± ‚Üí Œ≤) (hf : surjective f) :
  surjective (map f : ùïé p Œ± ‚Üí ùïé p Œ≤) :=
Œª x, ‚ü®Œª n, classical.some $ hf $ x n,
by { funext n, dsimp [map], rw classical.some_spec (hf (x n)) }‚ü©

variables (f : R ‚Üí+* S)

@[simp] lemma map_zero : map f (0 : ùïé p R) = 0 :=
funext $ Œª n, f.map_zero

@[simp] lemma map_one : map f (1 : ùïé p R) = 1 :=
funext $ Œª n,
match n with
| 0     := f.map_one
| (n+1) := f.map_zero
end

@[simp] lemma map_add (x y : ùïé p R) :
  map f (x + y) = map f x + map f y :=
begin
  funext n,
  show f (aeval _ _) = aeval _ _,
  rw map_aeval,
  apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
  { funext bn, rcases bn with ‚ü®‚ü®‚ü©, i‚ü©; refl, }
end

@[simp] lemma map_mul (x y : ùïé p R) :
  map f (x * y) = map f x * map f y :=
begin
  funext n,
  show f (aeval _ _) = aeval _ _,
  rw map_aeval,
  apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
  { funext bn, rcases bn with ‚ü®‚ü®‚ü©, i‚ü©; refl, }
end

@[simp] lemma map_neg (x : ùïé p R) :
  map f (-x) = -map f x :=
begin
  funext n,
  show f (aeval _ _) = aeval _ _,
  rw map_aeval,
  apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
  { funext bn, rcases bn with ‚ü®‚ü®‚ü©, i‚ü©; refl, }
end

end map

noncomputable def ghost_map : ùïé p R ‚Üí (‚Ñï ‚Üí R) := Œª w n, ghost_component n w

@[simp] lemma ghost_map.zero : ghost_map (0 : ùïé p R) = 0 :=
funext $ Œª n,
begin
  delta ghost_map ghost_component,
  rw [aeval_witt_polynomial, finset.sum_eq_zero],
  { refl },
  intros i hi,
  convert mul_zero _,
  apply zero_pow _,
  apply nat.pow_pos,
  apply nat.prime.pos, assumption,
end

@[simp] lemma ghost_map.one : ghost_map (1 : ùïé p R) = 1 :=
funext $ Œª n,
begin
  delta ghost_map ghost_component,
  rw [aeval_witt_polynomial],
  have : 0 ‚àà range (n+1),
  { rw finset.mem_range, exact nat.succ_pos n },
  rw ‚Üê finset.insert_erase this,
  rw finset.sum_insert (finset.not_mem_erase 0 (range (n + 1))),
  convert add_zero _,
  { apply finset.sum_eq_zero, intros i hi,
    rw finset.mem_erase at hi,
    suffices H : (1 : ùïé p R) i = 0,
    { rw [H, zero_pow, mul_zero], apply nat.pow_pos, exact nat.prime.pos ‚Äπ_‚Ä∫ },
    rw ‚Üê Teichmuller_one, cases hi with hi bla, revert hi,
    exact match i with
    | 0 := Œª H, false.elim (H rfl)
    | (n+1) := Œª H, rfl
    end },
  { dsimp, rw one_mul, symmetry,
    apply one_pow }
end

variable {R}

@[simp] lemma ghost_map.add (x y : ùïé p R) :
  ghost_map (x + y) = ghost_map x + ghost_map y :=
funext $ Œª n,
begin
  delta ghost_map ghost_component,
  have := congr_arg (Œª (œà : mv_polynomial (bool √ó ‚Ñï) R), aeval (Œª (bn : bool √ó ‚Ñï), cond bn.1 (x bn.2) (y bn.2)) œà) (witt_structure_prop p _ (X tt + X ff) n),
  convert this using 1; clear this,
  { delta witt_vectors.has_add witt_add,
    rw ‚Üê map_hom_witt_polynomial p (int.cast_ring_hom R),
    simp only [aeval_eq_eval‚ÇÇ_hom', eval‚ÇÇ_hom_map_hom, map_eval‚ÇÇ_hom],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext k,
    exact eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) rfl rfl },
  { simp only [aeval_eq_eval‚ÇÇ_hom', ring_hom.map_add, eval‚ÇÇ_hom_X', eval‚ÇÇ_hom_rename_hom],
    refl }
end

@[simp] lemma ghost_map.mul (x y : ùïé p R) :
  ghost_map (x * y) = ghost_map x * ghost_map y :=
funext $ Œª n,
begin
  delta ghost_map ghost_component,
  have := congr_arg (Œª (œà : mv_polynomial (bool √ó ‚Ñï) R), aeval (Œª (bn : bool √ó ‚Ñï), cond bn.1 (x bn.2) (y bn.2)) œà) (witt_structure_prop p _ (X tt * X ff) n),
  convert this using 1; clear this,
  { delta witt_vectors.has_mul witt_mul,
    rw ‚Üê map_hom_witt_polynomial p (int.cast_ring_hom R),
    simp only [aeval_eq_eval‚ÇÇ_hom', eval‚ÇÇ_hom_map_hom, map_eval‚ÇÇ_hom],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext k,
    exact eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) rfl rfl },
  { simp only [aeval_eq_eval‚ÇÇ_hom', ring_hom.map_mul, eval‚ÇÇ_hom_X', eval‚ÇÇ_hom_rename_hom],
    refl },
end

@[simp] lemma ghost_map.neg (x : ùïé p R) :
  ghost_map (-x) = - ghost_map x :=
funext $ Œª n,
begin
  delta ghost_map ghost_component,
  have := congr_arg (Œª (œà : mv_polynomial (unit √ó ‚Ñï) R), aeval (Œª (n : unit √ó ‚Ñï), (x n.2)) œà) (witt_structure_prop p _ (-X unit.star) n),
  convert this using 1; clear this,
  { delta witt_vectors.has_neg witt_neg,
    rw ‚Üê map_hom_witt_polynomial p (int.cast_ring_hom R),
    simp only [aeval_eq_eval‚ÇÇ_hom', eval‚ÇÇ_hom_map_hom, map_eval‚ÇÇ_hom],
    apply eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) _ rfl,
    funext k,
    exact eval‚ÇÇ_hom_congr (ring_hom.ext_int _ _) rfl rfl },
  { simp only [aeval_eq_eval‚ÇÇ_hom', ring_hom.map_neg, eval‚ÇÇ_hom_X', eval‚ÇÇ_hom_rename_hom],
    refl },
end
.

variables (p) (R)

noncomputable def ghost_map.equiv_of_invertible [invertible (p : R)] :
  ùïé p R ‚âÉ (‚Ñï ‚Üí R) :=
mv_polynomial.comap_equiv (witt.alg_equiv p R)

lemma ghost_map_eq [invertible (p : R)] :
  (ghost_map : ùïé p R ‚Üí ‚Ñï ‚Üí R) = ghost_map.equiv_of_invertible p R :=
begin
  ext w n,
  dsimp [ghost_map.equiv_of_invertible, witt.alg_equiv],
  rw [aeval_X], refl,
end

lemma ghost_map.bijective_of_invertible [invertible (p : R)] :
  function.bijective (ghost_map : ùïé p R ‚Üí ‚Ñï ‚Üí R) :=
by { rw ghost_map_eq, exact (ghost_map.equiv_of_invertible p R).bijective }

section
open function

variable (R)

noncomputable def mv_polynomial.counit : mv_polynomial R ‚Ñ§ ‚Üí+* R :=
eval‚ÇÇ_hom (int.cast_ring_hom R) id

lemma counit_surjective : surjective (mv_polynomial.counit R) :=
Œª r, ‚ü®X r, eval‚ÇÇ_hom_X' _ _ _‚ü©

end

local attribute [instance] mv_polynomial.invertible_rat_coe_nat

variable (R)

noncomputable def aux‚ÇÅ : comm_ring (ùïé p (mv_polynomial R ‚Ñö)) :=
function.injective.comm_ring (ghost_map)
  (ghost_map.bijective_of_invertible p (mv_polynomial R ‚Ñö)).1
  (ghost_map.zero) (ghost_map.one) (ghost_map.add) (ghost_map.mul) (ghost_map.neg)

local attribute [instance] aux‚ÇÅ

noncomputable def aux‚ÇÇ : comm_ring (ùïé p (mv_polynomial R ‚Ñ§)) :=
function.injective.comm_ring (map $ mv_polynomial.map_hom (int.cast_ring_hom ‚Ñö))
  (map_injective _ $ mv_polynomial.coe_int_rat_map_injective _)
  (map_zero _) (map_one _) (map_add _) (map_mul _) (map_neg _)

local attribute [instance] aux‚ÇÇ

noncomputable instance : comm_ring (ùïé p R) :=
function.surjective.comm_ring
  (map $ mv_polynomial.counit _) (map_surjective _ $ counit_surjective _)
  (map_zero _) (map_one _) (map_add _) (map_mul _) (map_neg _)

/-- Teichmuller is a natural transformation -/
@[simp] lemma map_Teichmuller (f : R ‚Üí+* S) (r : R) :
  map f (Teichmuller p r) = Teichmuller p (f r) :=
by { ext n, cases n, { refl }, { exact f.map_zero } }

@[simp] lemma aeval_Teichmuller_witt_polynomial (r : R) (n : ‚Ñï) :
  aeval (Teichmuller p r) (W_ R n) = r ^ p ^ n :=
begin
  rw aeval_witt_polynomial,
  rw [finset.sum_eq_single 0, pow_zero, one_mul, nat.sub_zero],
  { refl },
  { intros i hi h0,
    convert mul_zero _, convert zero_pow _,
    { cases i, { contradiction }, { refl } },
    { apply nat.pow_pos, apply nat.prime.pos, assumption } },
  { contrapose!, intro, rw finset.mem_range, exact nat.succ_pos n }
end

lemma Teichmuller_mul_aux‚ÇÅ (x y : mv_polynomial R ‚Ñö) :
  Teichmuller p (x * y) = Teichmuller p x * Teichmuller p y :=
begin
  apply (ghost_map.bijective_of_invertible p (mv_polynomial R ‚Ñö)).1,
  rw ghost_map.mul,
  ext1 n,
  dsimp [ghost_map, ghost_component],
  simp [mul_pow],
end

lemma Teichmuller_mul_aux‚ÇÇ (x y : mv_polynomial R ‚Ñ§) :
  Teichmuller p (x * y) = Teichmuller p x * Teichmuller p y :=
begin
  apply map_injective (map_hom (int.cast_ring_hom ‚Ñö)) (mv_polynomial.coe_int_rat_map_injective _),
  { simp [Teichmuller_mul_aux‚ÇÅ], },
  { assumption } -- map_injective shouldn't have the p.prime assumption
end

@[simp] lemma Teichmuller_mul (x y : R) :
  Teichmuller p (x * y) = Teichmuller p x * Teichmuller p y :=
begin
  rcases counit_surjective R x with ‚ü®x, rfl‚ü©,
  rcases counit_surjective R y with ‚ü®y, rfl‚ü©,
  simp only [‚Üê map_Teichmuller, ‚Üê ring_hom.map_mul, Teichmuller_mul_aux‚ÇÇ, map_mul],
end

end witt_vectors

attribute [irreducible] witt_vectors
