/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Robert Y. Lewis
-/

import ring_theory.witt_vector.witt_vector_preps
import ring_theory.witt_vector.structure_polynomial
import data.mv_polynomial.comap

/-!
# Witt vectors

In this file we define the type of `p`-typical Witt vectors and their ring structure.

For a fixed commutative ring `R` and prime `p`,
a Witt vector `x : ğ• R` is an infinite sequence `â„• â†’ R` of elements of `R`.
However, the ring operations `+` and `*` are not defined in the obvious component-wise way.
Instead, these operations are defined via certain polynomials
using the machinery in `structure_polynomial.lean`.
The `n`th value of the sum of two Witt vectors can depend on the 0th through `n`th values
of the summands. This effectively simulates a â€œcarryingâ€ operation.

## Main definitions

* `witt_vector.coeff x n`: projects the `n`th value of the Witt vector `x`
* `ghost_component n x`: evaluates the `n`th Witt polynomial using the first `n` coefficients of `x`,
  producing a value in `R`. This is effectively a truncating operation.
  If `p` is invertible in `R`, then the ghost components produce an equivalence with `â„• â†’ R`,
  which we use to define the ring operations on `ğ• R`.
* `witt_vector.comm_ring`: the ring structure induced by the ghost components.

## Notation

We use notation `ğ• R`, entered `\bbW`, for the Witt vectors over `R`.

## Implementation details

As we prove that the ghost components respect the ring operations, we face a number of repetitive
proofs. To avoid duplicating code we factor these proofs into a custom tactic, only slightly more
powerful than a tactic macro. This tactic is not particularly useful outside of its applications
in this file.

-/

/-- `witt_vector p R` is the ring of `p`-typical Witt vectors over the commutative ring `R`,
where `p` is a prime number.

If `p` is invertible in `R`, this ring is isomorphic to `â„• â†’ R` (the product of `â„•` copies of `R`).
If `R` is a ring of characteristic `p`, then `witt_vector p R` is a ring of characteristic `0`.
The canonical example is `witt_vector p (zmod p)`,
which is isomorphic to the `p`-adic integers `â„¤_[p]`. -/
def witt_vector (p : â„•) (R : Type*) := â„• â†’ R

variables {p : â„•}
-- TODO: make this localized notation??
local notation `ğ•` := witt_vector p -- type as `\bbW`

namespace witt_vector

variables (p) {R : Type*}

def mk (x : â„• â†’ R) : witt_vector p R := x

/-
`x.coeff n` is the `n`th value of the Witt vector `n`.

This concept does not have a standard name in the literature.
-/

def coeff (x : ğ• R) (n : â„•) : R := x n

@[ext]
lemma ext {x y : ğ• R} (h : âˆ€ n, x.coeff n = y.coeff n) : x = y :=
funext $ Î» n, h n

lemma ext_iff {x y : ğ• R} : x = y â†” âˆ€ n, x.coeff n = y.coeff n :=
âŸ¨Î» h n, by rw h, extâŸ©

@[simp] lemma coeff_mk (x : â„• â†’ R) (i : â„•) :
  (mk p x).coeff i = x i := rfl


/-
These instances are not needed for the rest of the development, but it is interesting to establish
early on that `witt_vector p` is a lawful functor.
-/
instance : functor (witt_vector p) :=
{ map := Î» Î± Î² f v, f âˆ˜ v,
  map_const := Î» Î± Î² a v, Î» _, a }

instance : is_lawful_functor (witt_vector p) :=
{ map_const_eq := Î» Î± Î², rfl,
  id_map := Î» Î± v, rfl,
  comp_map := Î» Î± Î² Î³ f g v, rfl }

end witt_vector

universes u v w uâ‚
open mv_polynomial
open set
open finset (range)
open finsupp (single)

open_locale big_operators

local attribute [-simp] coe_evalâ‚‚_hom

variables (p) {R S T : Type*} [comm_ring R] [comm_ring S] [comm_ring T]

open_locale witt

namespace witt_vector

section ring_data

/--
An auxiliary definition used in `witt_vector.eval`. Evaluates a polynomial whose variables come from
the disjoint union of `k` copies of `â„•`, with a curried evaluation `x`.
This can be defined more generally but we use only a specific instance here.
-/
noncomputable def peval {k : â„•} (Ï† : mv_polynomial (fin k Ã— â„•) â„¤) (x : fin k â†’ â„• â†’ R) : R :=
aeval (function.uncurry x) Ï†

/--
Let `Ï†` be a family of polynomials, indexed by natural numbers, whose variables come from the
disjoint union of `k` copies of `â„•`, and let `xáµ¢` be a Witt vector for `0 â‰¤ i < k`.

`eval Ï† x` evaluates `phi` mapping the variable `X_(i, n)` to the `n`th coefficient of `xáµ¢`.

Instantiating `Ï†` with certain polynomials defined in `structure_polynomial.lean` establishes the
ring operations on `ğ• R`. For example, `witt_vector.witt_add` is such a `Ï†` with `k = 2`;
evaluating this at `(xâ‚€, xâ‚)` gives us the sum of two Witt vectors `xâ‚€ + xâ‚`.
-/
noncomputable def eval {k : â„•} (Ï† : â„• â†’ mv_polynomial (fin k Ã— â„•) â„¤) (x : fin k â†’ ğ• R) : ğ• R :=
mk p $ Î» n, peval (Ï† n) $ Î» i, (x i).coeff

variables (R) [fact p.prime]

noncomputable instance : has_zero (ğ• R) :=
âŸ¨eval (witt_zero p) ![]âŸ©

noncomputable instance : has_one (ğ• R) :=
âŸ¨eval (witt_one p) ![]âŸ©

noncomputable instance : has_add (ğ• R) :=
âŸ¨Î» x y, eval (witt_add p) ![x.coeff, y.coeff]âŸ©

noncomputable instance : has_mul (ğ• R) :=
âŸ¨Î» x y, eval (witt_mul p) ![x.coeff, y.coeff]âŸ©

noncomputable instance : has_neg (ğ• R) :=
âŸ¨Î» x, eval (witt_neg p) ![x.coeff]âŸ©

end ring_data

section coeff

variables (p R) [fact p.prime]

@[simp] lemma zero_coeff (n : â„•) : (0 : ğ• R).coeff n = 0 :=
show (aeval _ (witt_zero p n) : R) = 0,
by simp only [witt_zero_eq_zero, alg_hom.map_zero]

@[simp] lemma one_coeff_zero : (1 : ğ• R).coeff 0 = 1 :=
show (aeval _ (witt_one p 0) : R) = 1,
by simp only [witt_one_zero_eq_one, alg_hom.map_one]

@[simp] lemma one_coeff_pos (n : â„•) (hn : 0 < n) : coeff (1 : ğ• R) n = 0 :=
show (aeval _ (witt_one p n) : R) = 0,
by simp only [hn, witt_one_pos_eq_zero, alg_hom.map_zero]

lemma add_coeff (x y : ğ• R) (n : â„•) :
  (x + y).coeff n = peval (witt_add p n) ![x.coeff, y.coeff] :=
rfl

lemma mul_coeff (x y : ğ• R) (n : â„•) :
  (x * y).coeff n = peval (witt_mul p n) ![x.coeff, y.coeff] :=
rfl

lemma neg_coeff (x : ğ• R) (n : â„•) :
  (-x).coeff n = peval (witt_neg p n) ![x.coeff] := rfl

end coeff

variables {p} {R}

section map
open function
variables {Î± : Type*} {Î² : Type*}

/-- `f : Î± â†’ Î²` induces a map from `ğ• Î±` to `ğ• Î²` in the obvious way. -/
def map_fun (f : Î± â†’ Î²) : ğ• Î± â†’ ğ• Î² := Î» x, f âˆ˜ x

lemma map_fun_injective (f : Î± â†’ Î²) (hf : injective f) :
  injective (map_fun f : ğ• Î± â†’ ğ• Î²) :=
Î» x y h, funext $ Î» n, hf $ by exact congr_fun h n

lemma map_fun_surjective (f : Î± â†’ Î²) (hf : surjective f) :
  surjective (map_fun f : ğ• Î± â†’ ğ• Î²) :=
Î» x, âŸ¨Î» n, classical.some $ hf $ x n,
by { funext n, dsimp [map_fun], rw classical.some_spec (hf (x n)) }âŸ©

variables (f : R â†’+* S)

/-- Auxiliary tactic for showing that `witt_package.map` respects ring data. -/
meta def witt_map : tactic unit :=
`[funext n,
  show f (aeval _ _) = aeval _ _,
  rw map_aeval,
  apply evalâ‚‚_hom_congr (ring_hom.ext_int _ _) _ rfl,
  ext âŸ¨i, kâŸ©,
  fin_cases i; refl]

variable [fact p.prime]

@[simp] lemma map_fun_zero : map_fun f (0 : ğ• R) = 0 :=
by witt_map

@[simp] lemma map_fun_one : map_fun f (1 : ğ• R) = 1 :=
by witt_map

@[simp] lemma map_fun_add (x y : ğ• R) :
  map_fun f (x + y) = map_fun f x + map_fun f y :=
by witt_map

@[simp] lemma map_fun_mul (x y : ğ• R) :
  map_fun f (x * y) = map_fun f x * map_fun f y :=
by witt_map

@[simp] lemma map_fun_neg (x : ğ• R) :
  map_fun f (-x) = -map_fun f x :=
by witt_map

end map

section

noncomputable def ghost_component (n : â„•) (x : ğ• R) : R :=
aeval x.coeff (W_ â„¤ n)

lemma ghost_component_apply (n : â„•) (x : ğ• R) :
  ghost_component n x = aeval x.coeff (W_ â„¤ n) := rfl

lemma ghost_component_apply' (n : â„•) (x : ğ• R) :
  ghost_component n x = aeval x.coeff (W_ R n) :=
begin
  simp only [ghost_component_apply, aeval_eq_evalâ‚‚_hom,
    â† map_witt_polynomial p (int.cast_ring_hom R), evalâ‚‚_hom_map_hom],
  exact evalâ‚‚_hom_congr (ring_hom.ext_int _ _) rfl rfl,
end

noncomputable def ghost_map_fun : ğ• R â†’ (â„• â†’ R) := Î» w n, ghost_component n w

end

end witt_vector

section tactic
setup_tactic_parser
open tactic
meta def tactic.interactive.ghost_boo (poly fn: parse parser.pexpr) : tactic unit :=
do fn â† to_expr ```(%%fn : fin _ â†’ â„• â†’ R),
  `(fin %%k â†’ _ â†’ _) â† infer_type fn,
  to_expr ```(witt_structure_int_prop p (%%poly : mv_polynomial (fin %%k) â„¤) n) >>= note `aux none >>=
     apply_fun_to_hyp ```(aeval (function.uncurry %%fn)) none,
`[simp only [aeval_bindâ‚] at aux,
  simp only [ghost_component_apply],
  convert aux using 1; clear aux;
  simp only [alg_hom.map_zero, alg_hom.map_one, alg_hom.map_add, alg_hom.map_mul, alg_hom.map_neg,
    aeval_X];
  simp only [aeval_eq_evalâ‚‚_hom, evalâ‚‚_hom_rename]; refl]
end tactic

namespace witt_vector


section p_prime
open finset mv_polynomial function set

variable {p}
variables [comm_ring R] [comm_ring S] [comm_ring T]

@[simp] lemma ghost_map_fun_apply (x : ğ• R) (n : â„•) :
  ghost_map_fun x n = ghost_component n x := rfl

variable [hp : fact p.prime]
include hp

@[simp] lemma ghost_component_zero (n : â„•) :
  ghost_component n (0 : ğ• R) = 0 :=
by ghost_boo 0 ![]

@[simp] lemma ghost_component_one (n : â„•) :
  ghost_component n (1 : ğ• R) = 1 :=
by ghost_boo 1 ![]

variable {R}

@[simp] lemma ghost_component_add (n : â„•) (x y : ğ• R) :
  ghost_component n (x + y) = ghost_component n x + ghost_component n y :=
by ghost_boo (X 0 + X 1) ![x.coeff, y.coeff]

@[simp] lemma ghost_component_mul (n : â„•) (x y : ğ• R) :
  ghost_component n (x * y) = ghost_component n x * ghost_component n y :=
by ghost_boo (X 0 * X 1) ![x.coeff, y.coeff]

@[simp] lemma ghost_component_neg (n : â„•) (x : ğ• R) :
  ghost_component n (-x) = - ghost_component n x :=
by ghost_boo (-X 0) ![x.coeff]

variables (R)

@[simp] lemma ghost_map_fun.zero : ghost_map_fun (0 : ğ• R) = 0 :=
by { ext n, simp only [pi.zero_apply, ghost_map_fun_apply, ghost_component_zero], }

@[simp] lemma ghost_map_fun.one : ghost_map_fun (1 : ğ• R) = 1 :=
by { ext n, simp only [pi.one_apply, ghost_map_fun_apply, ghost_component_one], }

variable {R}

@[simp] lemma ghost_map_fun.add (x y : ğ• R) :
  ghost_map_fun (x + y) = ghost_map_fun x + ghost_map_fun y :=
by { ext n, simp only [ghost_component_add, pi.add_apply, ghost_map_fun_apply], }

@[simp] lemma ghost_map_fun.mul (x y : ğ• R) :
  ghost_map_fun (x * y) = ghost_map_fun x * ghost_map_fun y :=
by { ext n, simp only [ghost_component_mul, pi.mul_apply, ghost_map_fun_apply], }

@[simp] lemma ghost_map_fun.neg (x : ğ• R) :
  ghost_map_fun (-x) = - ghost_map_fun x :=
by { ext n, simp only [ghost_component_neg, pi.neg_apply, ghost_map_fun_apply], }

end p_prime

variables (p) (R)

noncomputable def ghost_map_fun.equiv_of_invertible [invertible (p : R)] :
  ğ• R â‰ƒ (â„• â†’ R) :=
mv_polynomial.comap_equiv (witt.alg_equiv p R)

lemma ghost_map_fun_eq [invertible (p : R)] :
  (ghost_map_fun : ğ• R â†’ â„• â†’ R) = ghost_map_fun.equiv_of_invertible p R :=
begin
  ext w n,
  rw [ghost_map_fun_apply, ghost_component_apply'],
  dsimp [ghost_map_fun.equiv_of_invertible, witt.alg_equiv],
  rw bindâ‚_X_right, refl
end

lemma ghost_map_fun.bijective_of_invertible [invertible (p : R)] :
  function.bijective (ghost_map_fun : ğ• R â†’ â„• â†’ R) :=
by { rw ghost_map_fun_eq, exact (ghost_map_fun.equiv_of_invertible p R).bijective }

section
open function

variable (R)

noncomputable def mv_polynomial.counit : mv_polynomial R â„¤ â†’+* R :=
evalâ‚‚_hom (int.cast_ring_hom R) id

lemma counit_surjective : surjective (mv_polynomial.counit R) :=
Î» r, âŸ¨X r, evalâ‚‚_hom_X' _ _ _âŸ©

end

local attribute [instance] mv_polynomial.invertible_rat_coe_nat

variable (R)

variable [hp : fact p.prime]
include hp

private noncomputable def comm_ring_auxâ‚ : comm_ring (ğ• (mv_polynomial R â„š)) :=
function.injective.comm_ring (ghost_map_fun)
  (ghost_map_fun.bijective_of_invertible p (mv_polynomial R â„š)).1
  (ghost_map_fun.zero _) (ghost_map_fun.one _) (ghost_map_fun.add) (ghost_map_fun.mul) (ghost_map_fun.neg)

local attribute [instance] comm_ring_auxâ‚

private noncomputable def comm_ring_auxâ‚‚ : comm_ring (ğ• (mv_polynomial R â„¤)) :=
function.injective.comm_ring (map_fun $ mv_polynomial.map (int.cast_ring_hom â„š))
  (map_fun_injective _ $ mv_polynomial.map_injective _ int.cast_injective)
  (map_fun_zero _) (map_fun_one _) (map_fun_add _) (map_fun_mul _) (map_fun_neg _)

local attribute [instance] comm_ring_auxâ‚‚

noncomputable instance : comm_ring (ğ• R) :=
function.surjective.comm_ring
  (map_fun $ mv_polynomial.counit _) (map_fun_surjective _ $ counit_surjective _)
  (map_fun_zero _) (map_fun_one _) (map_fun_add _) (map_fun_mul _) (map_fun_neg _)

variables {p R}

section map
open function

noncomputable def map (f : R â†’+* S) : ğ• R â†’+* ğ• S :=
{ to_fun := map_fun f,
  map_zero' := map_fun_zero f,
  map_one' := map_fun_one f,
  map_add' := map_fun_add f,
  map_mul' := map_fun_mul f }

lemma map_injective (f : R â†’+* S) (hf : injective f) :
  injective (map f : ğ• R â†’ ğ• S) :=
map_fun_injective f hf

lemma map_surjective (f : R â†’+* S) (hf : surjective f) :
  surjective (map f : ğ• R â†’ ğ• S) :=
map_fun_surjective f hf

lemma map_coeff (f : R â†’+* S) (x : ğ• R) (n : â„•) :
  (map f x).coeff n = f (x.coeff n) := rfl

end map

noncomputable def ghost_map : ğ• R â†’+* â„• â†’ R :=
{ to_fun := ghost_map_fun,
  map_zero' := ghost_map_fun.zero R,
  map_one' := ghost_map_fun.one R,
  map_add' := ghost_map_fun.add,
  map_mul' := ghost_map_fun.mul }

@[simp] lemma ghost_map_apply (x : ğ• R) (n : â„•) :
  ghost_map x n = ghost_component n x := rfl

variables (p R)

noncomputable def ghost_equiv [invertible (p : R)] : ğ• R â‰ƒ+* (â„• â†’ R) :=
{ inv_fun := (ghost_map_fun.equiv_of_invertible p R).inv_fun,
  left_inv :=
  begin
    dsimp [ghost_map], rw [ghost_map_fun_eq],
    exact (ghost_map_fun.equiv_of_invertible p R).left_inv
  end,
  right_inv :=
  begin
    dsimp [ghost_map], rw [ghost_map_fun_eq],
    exact (ghost_map_fun.equiv_of_invertible p R).right_inv
  end,
  .. (ghost_map : ğ• R â†’+* (â„• â†’ R)) }

lemma ghost_map.bijective_of_invertible [invertible (p : R)] :
  function.bijective (ghost_map : ğ• R â†’ â„• â†’ R) :=
ghost_map_fun.bijective_of_invertible p R

end witt_vector

attribute [irreducible] witt_vector
