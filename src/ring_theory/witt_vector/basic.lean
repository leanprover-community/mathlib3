/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Robert Y. Lewis
-/

import ring_theory.witt_vector.structure_polynomial
import data.mv_polynomial.counit
import data.mv_polynomial.invertible

/-!
# Witt vectors

In this file we define the type of `p`-typical Witt vectors and their ring structure.

For a fixed commutative ring `R` and prime `p`,
a Witt vector `x : ğ• R` is an infinite sequence `â„• â†’ R` of elements of `R`.
However, the ring operations `+` and `*` are not defined in the obvious component-wise way.
Instead, these operations are defined via certain polynomials
using the machinery in `structure_polynomial.lean`.
The `n`th value of the sum of two Witt vectors can depend on the 0th through `n`th values
of the summands. This effectively simulates a â€œcarryingâ€ operation.

## Main definitions

* `witt_vector p R`: the type of `p`-typical Witt vectors with coefficients in `R`.
* `witt_vector.coeff x n`: projects the `n`th value of the Witt vector `x`.
* `witt_vector.map`: lifts a ring homomorphism `R â†’+* S` to a ring homomorphism `ğ• R â†’+* ğ• S`.
* `witt_vector.ghost_component n x`: evaluates the `n`th Witt polynomial
  on the first `n` coefficients of `x`, producing a value in `R`.
  This is a ring homomorphism.
* `witt_vector.ghost_map`: a ring homomorphism `ğ• R â†’+* (â„• â†’ R)`, obtained by packaging
  all the ghost components together.
  If `p` is invertible in `R`, then the ghost map is an equivalence,
  which we use to define the ring operations on `ğ• R`.
* `witt_vector.comm_ring`: the ring structure induced by the ghost components.

## Notation

We use notation `ğ• R`, entered `\bbW`, for the Witt vectors over `R`.

## Implementation details

As we prove that the ghost components respect the ring operations, we face a number of repetitive
proofs. To avoid duplicating code we factor these proofs into a custom tactic, only slightly more
powerful than a tactic macro. This tactic is not particularly useful outside of its applications
in this file.

-/

noncomputable theory

/-- `witt_vector p R` is the ring of `p`-typical Witt vectors over the commutative ring `R`,
where `p` is a prime number.

If `p` is invertible in `R`, this ring is isomorphic to `â„• â†’ R` (the product of `â„•` copies of `R`).
If `R` is a ring of characteristic `p`, then `witt_vector p R` is a ring of characteristic `0`.
The canonical example is `witt_vector p (zmod p)`,
which is isomorphic to the `p`-adic integers `â„¤_[p]`. -/
@[nolint unused_arguments]
def witt_vector (p : â„•) (R : Type*) := â„• â†’ R

variables {p : â„•}

/- We cannot make this `localized` notation, because the `p` on the RHS doesn't occur on the left
Hiding the `p` in the notation is very convenient, so we opt for repeating the `local notation`
in other files that use Witt vectors. -/
local notation `ğ•` := witt_vector p -- type as `\bbW`

namespace witt_vector

variables (p) {R : Type*}

/-- Construct a Witt vector `mk p x : ğ• R` from a sequence `x` of elements of `R`. -/
def mk (x : â„• â†’ R) : witt_vector p R := x

instance [inhabited R] : inhabited (ğ• R) := âŸ¨mk p $ Î» _, default RâŸ©

/--
`x.coeff n` is the `n`th coefficient of the Witt vector `n`.

This concept does not have a standard name in the literature.
-/
def coeff (x : ğ• R) (n : â„•) : R := x n

@[ext] lemma ext {x y : ğ• R} (h : âˆ€ n, x.coeff n = y.coeff n) : x = y :=
funext $ Î» n, h n

lemma ext_iff {x y : ğ• R} : x = y â†” âˆ€ n, x.coeff n = y.coeff n :=
âŸ¨Î» h n, by rw h, extâŸ©

@[simp] lemma coeff_mk (x : â„• â†’ R) :
  (mk p x).coeff = x := rfl

/- These instances are not needed for the rest of the development,
but it is interesting to establish early on that `witt_vector p` is a lawful functor. -/
instance : functor (witt_vector p) :=
{ map := Î» Î± Î² f v, f âˆ˜ v,
  map_const := Î» Î± Î² a v, Î» _, a }

instance : is_lawful_functor (witt_vector p) :=
{ map_const_eq := Î» Î± Î², rfl,
  id_map := Î» Î± v, rfl,
  comp_map := Î» Î± Î² Î³ f g v, rfl }

end witt_vector

universes u v w uâ‚
open mv_polynomial
open set
open finset (range)
open finsupp (single)

open_locale big_operators

local attribute [-simp] coe_evalâ‚‚_hom

variables (p) {R S T : Type*} [comm_ring R] [comm_ring S] [comm_ring T]

open_locale witt

namespace witt_vector

section ring_data

/-- An auxiliary definition used in `witt_vector.eval`.
Evaluates a polynomial whose variables come from the disjoint union of `k` copies of `â„•`,
with a curried evaluation `x`.
This can be defined more generally but we use only a specific instance here. -/
def peval {k : â„•} (Ï† : mv_polynomial (fin k Ã— â„•) â„¤) (x : fin k â†’ â„• â†’ R) : R :=
aeval (function.uncurry x) Ï†

/--
Let `Ï†` be a family of polynomials, indexed by natural numbers, whose variables come from the
disjoint union of `k` copies of `â„•`, and let `xáµ¢` be a Witt vector for `0 â‰¤ i < k`.

`eval Ï† x` evaluates `Ï†` mapping the variable `X_(i, n)` to the `n`th coefficient of `xáµ¢`.

Instantiating `Ï†` with certain polynomials defined in `structure_polynomial.lean` establishes the
ring operations on `ğ• R`. For example, `witt_vector.witt_add` is such a `Ï†` with `k = 2`;
evaluating this at `(xâ‚€, xâ‚)` gives us the sum of two Witt vectors `xâ‚€ + xâ‚`.
-/
def eval {k : â„•} (Ï† : â„• â†’ mv_polynomial (fin k Ã— â„•) â„¤) (x : fin k â†’ ğ• R) : ğ• R :=
mk p $ Î» n, peval (Ï† n) $ Î» i, (x i).coeff

variables (R) [fact p.prime]

instance : has_zero (ğ• R) :=
âŸ¨eval (witt_zero p) ![]âŸ©

instance : has_one (ğ• R) :=
âŸ¨eval (witt_one p) ![]âŸ©

instance : has_add (ğ• R) :=
âŸ¨Î» x y, eval (witt_add p) ![x, y]âŸ©

instance : has_mul (ğ• R) :=
âŸ¨Î» x y, eval (witt_mul p) ![x, y]âŸ©

instance : has_neg (ğ• R) :=
âŸ¨Î» x, eval (witt_neg p) ![x]âŸ©

end ring_data

section coeff

variables (p R) [fact p.prime]

@[simp] lemma zero_coeff (n : â„•) : (0 : ğ• R).coeff n = 0 :=
show (aeval _ (witt_zero p n) : R) = 0,
by simp only [witt_zero_eq_zero, alg_hom.map_zero]

@[simp] lemma one_coeff_zero : (1 : ğ• R).coeff 0 = 1 :=
show (aeval _ (witt_one p 0) : R) = 1,
by simp only [witt_one_zero_eq_one, alg_hom.map_one]

@[simp] lemma one_coeff_eq_of_pos (n : â„•) (hn : 0 < n) : coeff (1 : ğ• R) n = 0 :=
show (aeval _ (witt_one p n) : R) = 0,
by simp only [hn, witt_one_pos_eq_zero, alg_hom.map_zero]

lemma add_coeff (x y : ğ• R) (n : â„•) :
  (x + y).coeff n = peval (witt_add p n) ![x.coeff, y.coeff] := rfl

lemma mul_coeff (x y : ğ• R) (n : â„•) :
  (x * y).coeff n = peval (witt_mul p n) ![x.coeff, y.coeff] := rfl

lemma neg_coeff (x : ğ• R) (n : â„•) :
  (-x).coeff n = peval (witt_neg p n) ![x.coeff] := rfl

end coeff

variables {p} {R}

section map
open function
variables {Î± : Type*} {Î² : Type*}

/-- `f : Î± â†’ Î²` induces a map from `ğ• Î±` to `ğ• Î²` in the obvious way. -/
def map_fun (f : Î± â†’ Î²) : ğ• Î± â†’ ğ• Î² := Î» x, f âˆ˜ x

lemma map_fun_injective (f : Î± â†’ Î²) (hf : injective f) :
  injective (map_fun f : ğ• Î± â†’ ğ• Î²) :=
Î» x y h, funext $ Î» n, hf $ by exact congr_fun h n

lemma map_fun_surjective (f : Î± â†’ Î²) (hf : surjective f) :
  surjective (map_fun f : ğ• Î± â†’ ğ• Î²) :=
Î» x, âŸ¨Î» n, classical.some $ hf $ x n,
by { funext n, dsimp [map_fun], rw classical.some_spec (hf (x n)) }âŸ©

variables (f : R â†’+* S)

/-- Auxiliary tactic for showing that `map_fun` respects the ring operations. -/
meta def witt_map : tactic unit :=
`[funext n,
  show f (aeval _ _) = aeval _ _,
  rw map_aeval,
  apply evalâ‚‚_hom_congr (ring_hom.ext_int _ _) _ rfl,
  ext âŸ¨i, kâŸ©,
  fin_cases i; refl]

variable [fact p.prime]

/- We do not tag these lemmas as `@[simp]` because the will be bundled in `map` later on. -/
lemma map_fun_zero : map_fun f (0 : ğ• R) = 0 := by witt_map

lemma map_fun_one : map_fun f (1 : ğ• R) = 1 := by witt_map

lemma map_fun_add (x y : ğ• R) : map_fun f (x + y) = map_fun f x + map_fun f y := by witt_map

lemma map_fun_mul (x y : ğ• R) : map_fun f (x * y) = map_fun f x * map_fun f y := by witt_map

lemma map_fun_neg (x : ğ• R) : map_fun f (-x) = -map_fun f x := by witt_map

end map

end witt_vector

section tactic
setup_tactic_parser
open tactic

/-- An auxiliary tactic for proving that `ghost_component_fun` respects the ring operations. -/
meta def tactic.interactive.ghost_component (poly fn : parse parser.pexpr) : tactic unit :=
do fn â† to_expr ```(%%fn : fin _ â†’ â„• â†’ R),
  `(fin %%k â†’ _ â†’ _) â† infer_type fn,
  to_expr ```(witt_structure_int_prop p (%%poly : mv_polynomial (fin %%k) â„¤) n) >>= note `aux none >>=
     apply_fun_to_hyp ```(aeval (function.uncurry %%fn)) none,
`[simp only [aeval_bindâ‚] at aux,
  simp only [ghost_component_fun_apply],
  convert aux using 1; clear aux;
  simp only [alg_hom.map_zero, alg_hom.map_one, alg_hom.map_add, alg_hom.map_mul, alg_hom.map_neg,
    aeval_X];
  simp only [aeval_eq_evalâ‚‚_hom, evalâ‚‚_hom_rename]; refl]
end tactic

namespace witt_vector

/-- Evaluates the `n`th Witt polynomial on the first `n` coefficients of `x`,
producing a value in `R`. This is bundled as a ring hom in `ghost_component`
once the ring structure is available. -/
private def ghost_component_fun (n : â„•) (x : ğ• R) : R :=
aeval x.coeff (W_ â„¤ n)

lemma ghost_component_fun_apply (n : â„•) (x : ğ• R) :
  ghost_component_fun n x = aeval x.coeff (W_ â„¤ n) := rfl

lemma ghost_component_fun_apply' (n : â„•) (x : ğ• R) :
  ghost_component_fun n x = aeval x.coeff (W_ R n) :=
begin
  simp only [ghost_component_fun_apply, aeval_eq_evalâ‚‚_hom,
    â† map_witt_polynomial p (int.cast_ring_hom R), evalâ‚‚_hom_map_hom],
  exact evalâ‚‚_hom_congr (ring_hom.ext_int _ _) rfl rfl,
end

/-- Reorders the arguments of `ghost_component_fun`.
This function will be bundled as the ring homomorphism `witt_vector.ghost_map`
once the ring structure is available,
but we rely on it to set up the ring structure in the first place. -/
private def ghost_map_fun : ğ• R â†’ (â„• â†’ R) := Î» w n, ghost_component_fun n w

section p_prime
open finset mv_polynomial function set

variable {p}

/- The following lemmas are not `@[simp]` because we will bundle these functions later on. -/

lemma ghost_map_fun_apply (x : ğ• R) (n : â„•) :
  ghost_map_fun x n = ghost_component_fun n x := rfl

variable [hp : fact p.prime]
include hp

lemma ghost_component_fun_zero (n : â„•) :
  ghost_component_fun n (0 : ğ• R) = 0 :=
by ghost_component 0 ![]

lemma ghost_component_fun_one (n : â„•) :
  ghost_component_fun n (1 : ğ• R) = 1 :=
by ghost_component 1 ![]

variable {R}

lemma ghost_component_fun_add (n : â„•) (x y : ğ• R) :
  ghost_component_fun n (x + y) = ghost_component_fun n x + ghost_component_fun n y :=
by ghost_component (X 0 + X 1) ![x.coeff, y.coeff]

lemma ghost_component_fun_mul (n : â„•) (x y : ğ• R) :
  ghost_component_fun n (x * y) = ghost_component_fun n x * ghost_component_fun n y :=
by ghost_component (X 0 * X 1) ![x.coeff, y.coeff]

lemma ghost_component_fun_neg (n : â„•) (x : ğ• R) :
  ghost_component_fun n (-x) = - ghost_component_fun n x :=
by ghost_component (-X 0) ![x.coeff]

variables (R)

lemma ghost_map_fun.zero : ghost_map_fun (0 : ğ• R) = 0 :=
by { ext n, simp only [pi.zero_apply, ghost_map_fun_apply, ghost_component_fun_zero], }

lemma ghost_map_fun.one : ghost_map_fun (1 : ğ• R) = 1 :=
by { ext n, simp only [pi.one_apply, ghost_map_fun_apply, ghost_component_fun_one], }

variable {R}

lemma ghost_map_fun.add (x y : ğ• R) :
  ghost_map_fun (x + y) = ghost_map_fun x + ghost_map_fun y :=
by { ext n, simp only [ghost_component_fun_add, pi.add_apply, ghost_map_fun_apply], }

lemma ghost_map_fun.mul (x y : ğ• R) :
  ghost_map_fun (x * y) = ghost_map_fun x * ghost_map_fun y :=
by { ext n, simp only [ghost_component_fun_mul, pi.mul_apply, ghost_map_fun_apply], }

lemma ghost_map_fun.neg (x : ğ• R) :
  ghost_map_fun (-x) = - ghost_map_fun x :=
by { ext n, simp only [ghost_component_fun_neg, pi.neg_apply, ghost_map_fun_apply], }

end p_prime

variables (p) (R)

/-- The bijection between `ğ• R` and `â„• â†’ R`,
under the assumption that `p` is invertible in `R`. -/
def ghost_map_fun.equiv_of_invertible [invertible (p : R)] :
  ğ• R â‰ƒ (â„• â†’ R) :=
{ to_fun := ghost_map_fun,
  inv_fun := Î» x, mk p $ Î» n, aeval x (X_in_terms_of_W p R n),
  left_inv :=
  begin
    intro x,
    ext n,
    have := bindâ‚_witt_polynomial_X_in_terms_of_W p R n,
    apply_fun (aeval x.coeff) at this,
    simpa only [aeval_bindâ‚, aeval_X, ghost_map_fun, ghost_component_fun, aeval_witt_polynomial]
  end,
  right_inv :=
  begin
    intro x,
    ext n,
    have := bindâ‚_X_in_terms_of_W_witt_polynomial p R n,
    apply_fun (aeval x) at this,
    simpa only [aeval_bindâ‚, aeval_X, ghost_map_fun, ghost_component_fun, aeval_witt_polynomial]
  end }

lemma ghost_map_fun_eq [invertible (p : R)] :
  (ghost_map_fun : ğ• R â†’ â„• â†’ R) = ghost_map_fun.equiv_of_invertible p R :=
rfl

lemma ghost_map_fun.bijective_of_invertible [invertible (p : R)] :
  function.bijective (ghost_map_fun : ğ• R â†’ â„• â†’ R) :=
by { rw ghost_map_fun_eq, exact (ghost_map_fun.equiv_of_invertible p R).bijective }

local attribute [instance] mv_polynomial.invertible_coe_nat

variable (R)

variable [hp : fact p.prime]
include hp

private def comm_ring_auxâ‚ : comm_ring (ğ• (mv_polynomial R â„š)) :=
function.injective.comm_ring (ghost_map_fun)
  (ghost_map_fun.bijective_of_invertible p (mv_polynomial R â„š)).1
  (ghost_map_fun.zero _) (ghost_map_fun.one _) (ghost_map_fun.add) (ghost_map_fun.mul) (ghost_map_fun.neg)

local attribute [instance] comm_ring_auxâ‚

private def comm_ring_auxâ‚‚ : comm_ring (ğ• (mv_polynomial R â„¤)) :=
function.injective.comm_ring (map_fun $ mv_polynomial.map (int.cast_ring_hom â„š))
  (map_fun_injective _ $ mv_polynomial.map_injective _ int.cast_injective)
  (map_fun_zero _) (map_fun_one _) (map_fun_add _) (map_fun_mul _) (map_fun_neg _)

local attribute [instance] comm_ring_auxâ‚‚

/-- The commutative ring structure on `ğ• R`. -/
instance : comm_ring (ğ• R) :=
function.surjective.comm_ring
  (map_fun $ mv_polynomial.counit _) (map_fun_surjective _ $ counit_surjective _)
  (map_fun_zero _) (map_fun_one _) (map_fun_add _) (map_fun_mul _) (map_fun_neg _)

variables {p R}

section map
open function

/-- `witt_vector.map f` is the ring homomorphism `ğ• R â†’+* ğ• S` naturally induced
by a ring homomorphism `f : R â†’+* S`. It acts coefficientwise. -/
def map (f : R â†’+* S) : ğ• R â†’+* ğ• S :=
{ to_fun := map_fun f,
  map_zero' := map_fun_zero f,
  map_one' := map_fun_one f,
  map_add' := map_fun_add f,
  map_mul' := map_fun_mul f }

lemma map_injective (f : R â†’+* S) (hf : injective f) :
  injective (map f : ğ• R â†’ ğ• S) :=
map_fun_injective f hf

lemma map_surjective (f : R â†’+* S) (hf : surjective f) :
  surjective (map f : ğ• R â†’ ğ• S) :=
map_fun_surjective f hf

@[simp] lemma map_coeff (f : R â†’+* S) (x : ğ• R) (n : â„•) :
  (map f x).coeff n = f (x.coeff n) := rfl

end map

/-- Evaluates the `n`th Witt polynomial on the first `n` coefficients of `x`,
producing a value in `R`. -/
def ghost_component (n : â„•) : ğ• R â†’+* R :=
{ to_fun := ghost_component_fun n,
  map_zero' := ghost_component_fun_zero n,
  map_one' := ghost_component_fun_one n,
  map_add' := ghost_component_fun_add n,
  map_mul' := ghost_component_fun_mul n }

lemma ghost_component_apply (n : â„•) (x : ğ• R) :
  ghost_component n x = aeval x.coeff (W_ â„¤ n) := rfl

/-- `witt_vector.ghost_map` is a ring homomorphism that maps each Witt vector
to the sequence of its ghost components. -/
def ghost_map : ğ• R â†’+* â„• â†’ R :=
{ to_fun := ghost_map_fun,
  map_zero' := ghost_map_fun.zero R,
  map_one' := ghost_map_fun.one R,
  map_add' := ghost_map_fun.add,
  map_mul' := ghost_map_fun.mul }

@[simp] lemma ghost_map_apply (x : ğ• R) (n : â„•) :
  ghost_map x n = ghost_component n x := rfl

variables (p R)

/-- `witt_vector.ghost_map` is a ring isomorphism when `p` is invertible in `R`. -/
def ghost_equiv [invertible (p : R)] : ğ• R â‰ƒ+* (â„• â†’ R) :=
{ inv_fun := (ghost_map_fun.equiv_of_invertible p R).inv_fun,
  left_inv :=
  begin
    dsimp [ghost_map], rw [ghost_map_fun_eq],
    exact (ghost_map_fun.equiv_of_invertible p R).left_inv
  end,
  right_inv :=
  begin
    dsimp [ghost_map], rw [ghost_map_fun_eq],
    exact (ghost_map_fun.equiv_of_invertible p R).right_inv
  end,
  .. (ghost_map : ğ• R â†’+* (â„• â†’ R)) }

lemma ghost_map.bijective_of_invertible [invertible (p : R)] :
  function.bijective (ghost_map : ğ• R â†’ â„• â†’ R) :=
ghost_map_fun.bijective_of_invertible p R

end witt_vector

attribute [irreducible] witt_vector
