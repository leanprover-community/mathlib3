/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Robert Y. Lewis
-/

import ring_theory.witt_vector.structure_polynomial

/-!
# Witt vectors

In this file we define the type of `p`-typical Witt vectors and ring operations on it.
The ring axioms are verified in `ring_theory/witt_vector/basic.lean`.

For a fixed commutative ring `R` and prime `p`,
a Witt vector `x : ùïé R` is an infinite sequence `‚Ñï ‚Üí R` of elements of `R`.
However, the ring operations `+` and `*` are not defined in the obvious component-wise way.
Instead, these operations are defined via certain polynomials
using the machinery in `structure_polynomial.lean`.
The `n`th value of the sum of two Witt vectors can depend on the `0`-th through `n`th values
of the summands. This effectively simulates a ‚Äúcarrying‚Äù operation.

## Main definitions

* `witt_vector p R`: the type of `p`-typical Witt vectors with coefficients in `R`.
* `witt_vector.coeff x n`: projects the `n`th value of the Witt vector `x`.

## Notation

We use notation `ùïé R`, entered `\bbW`, for the Witt vectors over `R`.

## References

* [Hazewinkel, *Witt Vectors*][Haze09]

* [Commelin and Lewis, *Formalizing the Ring of Witt Vectors*][CL21]
-/

noncomputable theory

/-- `witt_vector p R` is the ring of `p`-typical Witt vectors over the commutative ring `R`,
where `p` is a prime number.

If `p` is invertible in `R`, this ring is isomorphic to `‚Ñï ‚Üí R` (the product of `‚Ñï` copies of `R`).
If `R` is a ring of characteristic `p`, then `witt_vector p R` is a ring of characteristic `0`.
The canonical example is `witt_vector p (zmod p)`,
which is isomorphic to the `p`-adic integers `‚Ñ§_[p]`. -/
structure witt_vector (p : ‚Ñï) (R : Type*) :=
mk [] :: (coeff : ‚Ñï ‚Üí R)

variables {p : ‚Ñï}

/- We cannot make this `localized` notation, because the `p` on the RHS doesn't occur on the left
Hiding the `p` in the notation is very convenient, so we opt for repeating the `local notation`
in other files that use Witt vectors. -/
local notation `ùïé` := witt_vector p -- type as `\bbW`

namespace witt_vector

variables (p) {R : Type*}

/-- Construct a Witt vector `mk p x : ùïé R` from a sequence `x` of elements of `R`. -/
add_decl_doc witt_vector.mk

/--
`x.coeff n` is the `n`th coefficient of the Witt vector `x`.

This concept does not have a standard name in the literature.
-/
add_decl_doc witt_vector.coeff

@[ext] lemma ext {x y : ùïé R} (h : ‚àÄ n, x.coeff n = y.coeff n) : x = y :=
begin
  cases x,
  cases y,
  simp only at h,
  simp [function.funext_iff, h]
end

lemma ext_iff {x y : ùïé R} : x = y ‚Üî ‚àÄ n, x.coeff n = y.coeff n :=
‚ü®Œª h n, by rw h, ext‚ü©

lemma coeff_mk (x : ‚Ñï ‚Üí R) :
  (mk p x).coeff = x := rfl

/- These instances are not needed for the rest of the development,
but it is interesting to establish early on that `witt_vector p` is a lawful functor. -/
instance : functor (witt_vector p) :=
{ map := Œª Œ± Œ≤ f v, mk p (f ‚àò v.coeff),
  map_const := Œª Œ± Œ≤ a v, mk p (Œª _, a) }

instance : is_lawful_functor (witt_vector p) :=
{ map_const_eq := Œª Œ± Œ≤, rfl,
  id_map := Œª Œ± ‚ü®v, _‚ü©, rfl,
  comp_map := Œª Œ± Œ≤ Œ≥ f g v, rfl }

variables (p) [hp : fact p.prime] [comm_ring R]
include hp
open mv_polynomial

section ring_operations

/-- The polynomials used for defining the element `0` of the ring of Witt vectors. -/
def witt_zero : ‚Ñï ‚Üí mv_polynomial (fin 0 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p 0

/-- The polynomials used for defining the element `1` of the ring of Witt vectors. -/
def witt_one : ‚Ñï ‚Üí mv_polynomial (fin 0 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p 1

/-- The polynomials used for defining the addition of the ring of Witt vectors. -/
def witt_add : ‚Ñï ‚Üí mv_polynomial (fin 2 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (X 0 + X 1)

/-- The polynomials used for defining repeated addition of the ring of Witt vectors. -/
def witt_nsmul (n : ‚Ñï) : ‚Ñï ‚Üí mv_polynomial (fin 1 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (n ‚Ä¢ X 0)

/-- The polynomials used for defining repeated addition of the ring of Witt vectors. -/
def witt_zsmul (n : ‚Ñ§) : ‚Ñï ‚Üí mv_polynomial (fin 1 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (n ‚Ä¢ X 0)

/-- The polynomials used for describing the subtraction of the ring of Witt vectors. -/
def witt_sub : ‚Ñï ‚Üí mv_polynomial (fin 2 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (X 0 - X 1)

/-- The polynomials used for defining the multiplication of the ring of Witt vectors. -/
def witt_mul : ‚Ñï ‚Üí mv_polynomial (fin 2 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (X 0 * X 1)

/-- The polynomials used for defining the negation of the ring of Witt vectors. -/
def witt_neg : ‚Ñï ‚Üí mv_polynomial (fin 1 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (-X 0)

/-- The polynomials used for defining repeated addition of the ring of Witt vectors. -/
def witt_pow (n : ‚Ñï) : ‚Ñï ‚Üí mv_polynomial (fin 1 √ó ‚Ñï) ‚Ñ§ :=
witt_structure_int p (X 0 ^ n)

variable {p}
omit hp

/-- An auxiliary definition used in `witt_vector.eval`.
Evaluates a polynomial whose variables come from the disjoint union of `k` copies of `‚Ñï`,
with a curried evaluation `x`.
This can be defined more generally but we use only a specific instance here. -/
def peval {k : ‚Ñï} (œÜ : mv_polynomial (fin k √ó ‚Ñï) ‚Ñ§) (x : fin k ‚Üí ‚Ñï ‚Üí R) : R :=
aeval (function.uncurry x) œÜ

/--
Let `œÜ` be a family of polynomials, indexed by natural numbers, whose variables come from the
disjoint union of `k` copies of `‚Ñï`, and let `x·µ¢` be a Witt vector for `0 ‚â§ i < k`.

`eval œÜ x` evaluates `œÜ` mapping the variable `X_(i, n)` to the `n`th coefficient of `x·µ¢`.

Instantiating `œÜ` with certain polynomials defined in `structure_polynomial.lean` establishes the
ring operations on `ùïé R`. For example, `witt_vector.witt_add` is such a `œÜ` with `k = 2`;
evaluating this at `(x‚ÇÄ, x‚ÇÅ)` gives us the sum of two Witt vectors `x‚ÇÄ + x‚ÇÅ`.
-/
def eval {k : ‚Ñï} (œÜ : ‚Ñï ‚Üí mv_polynomial (fin k √ó ‚Ñï) ‚Ñ§) (x : fin k ‚Üí ùïé R) : ùïé R :=
mk p $ Œª n, peval (œÜ n) $ Œª i, (x i).coeff

variables (R) [fact p.prime]

instance : has_zero (ùïé R) :=
‚ü®eval (witt_zero p) ![]‚ü©

instance : inhabited (ùïé R) := ‚ü®0‚ü©

instance : has_one (ùïé R) :=
‚ü®eval (witt_one p) ![]‚ü©

instance : has_add (ùïé R) :=
‚ü®Œª x y, eval (witt_add p) ![x, y]‚ü©

instance : has_sub (ùïé R) :=
‚ü®Œª x y, eval (witt_sub p) ![x, y]‚ü©

instance has_nat_scalar : has_scalar ‚Ñï (ùïé R) :=
‚ü®Œª n x, eval (witt_nsmul p n) ![x]‚ü©

instance has_int_scalar : has_scalar ‚Ñ§ (ùïé R) :=
‚ü®Œª n x, eval (witt_zsmul p n) ![x]‚ü©

instance : has_mul (ùïé R) :=
‚ü®Œª x y, eval (witt_mul p) ![x, y]‚ü©

instance : has_neg (ùïé R) :=
‚ü®Œª x, eval (witt_neg p) ![x]‚ü©

instance has_nat_pow : has_pow (ùïé R) ‚Ñï :=
‚ü®Œª x n, eval (witt_pow p n) ![x]‚ü©

end ring_operations

section witt_structure_simplifications

@[simp] lemma witt_zero_eq_zero (n : ‚Ñï) : witt_zero p n = 0 :=
begin
  apply mv_polynomial.map_injective (int.cast_ring_hom ‚Ñö) int.cast_injective,
  simp only [witt_zero, witt_structure_rat, bind‚ÇÅ, aeval_zero',
    constant_coeff_X_in_terms_of_W, ring_hom.map_zero,
    alg_hom.map_zero, map_witt_structure_int],
end

@[simp] lemma witt_one_zero_eq_one : witt_one p 0 = 1 :=
begin
  apply mv_polynomial.map_injective (int.cast_ring_hom ‚Ñö) int.cast_injective,
  simp only [witt_one, witt_structure_rat, X_in_terms_of_W_zero, alg_hom.map_one,
    ring_hom.map_one, bind‚ÇÅ_X_right, map_witt_structure_int]
end

@[simp] lemma witt_one_pos_eq_zero (n : ‚Ñï) (hn : 0 < n) : witt_one p n = 0 :=
begin
  apply mv_polynomial.map_injective (int.cast_ring_hom ‚Ñö) int.cast_injective,
  simp only [witt_one, witt_structure_rat, ring_hom.map_zero, alg_hom.map_one,
    ring_hom.map_one, map_witt_structure_int],
  revert hn, apply nat.strong_induction_on n, clear n,
  intros n IH hn,
  rw X_in_terms_of_W_eq,
  simp only [alg_hom.map_mul, alg_hom.map_sub, alg_hom.map_sum, alg_hom.map_pow,
    bind‚ÇÅ_X_right, bind‚ÇÅ_C_right],
  rw [sub_mul, one_mul],
  rw [finset.sum_eq_single 0],
  { simp only [inv_of_eq_inv, one_mul, inv_pow, tsub_zero, ring_hom.map_one, pow_zero],
    simp only [one_pow, one_mul, X_in_terms_of_W_zero, sub_self, bind‚ÇÅ_X_right] },
  { intros i hin hi0,
    rw [finset.mem_range] at hin,
    rw [IH _ hin (nat.pos_of_ne_zero hi0), zero_pow (pow_pos hp.1.pos _), mul_zero], },
  { rw finset.mem_range, intro, contradiction }
end

@[simp] lemma witt_add_zero : witt_add p 0 = X (0,0) + X (1,0) :=
begin
  apply mv_polynomial.map_injective (int.cast_ring_hom ‚Ñö) int.cast_injective,
  simp only [witt_add, witt_structure_rat, alg_hom.map_add, ring_hom.map_add,
    rename_X, X_in_terms_of_W_zero, map_X,
     witt_polynomial_zero, bind‚ÇÅ_X_right, map_witt_structure_int],
end

@[simp] lemma witt_sub_zero : witt_sub p 0 = X (0,0) - X (1,0) :=
begin
  apply mv_polynomial.map_injective (int.cast_ring_hom ‚Ñö) int.cast_injective,
  simp only [witt_sub, witt_structure_rat, alg_hom.map_sub, ring_hom.map_sub,
    rename_X, X_in_terms_of_W_zero, map_X,
     witt_polynomial_zero, bind‚ÇÅ_X_right, map_witt_structure_int],
end

@[simp] lemma witt_mul_zero : witt_mul p 0 = X (0,0) * X (1,0) :=
begin
  apply mv_polynomial.map_injective (int.cast_ring_hom ‚Ñö) int.cast_injective,
  simp only [witt_mul, witt_structure_rat, rename_X, X_in_terms_of_W_zero, map_X,
    witt_polynomial_zero, ring_hom.map_mul,
    bind‚ÇÅ_X_right, alg_hom.map_mul, map_witt_structure_int]
end

@[simp] lemma witt_neg_zero : witt_neg p 0 = - X (0,0) :=
begin
  apply mv_polynomial.map_injective (int.cast_ring_hom ‚Ñö) int.cast_injective,
  simp only [witt_neg, witt_structure_rat, rename_X, X_in_terms_of_W_zero, map_X,
    witt_polynomial_zero, ring_hom.map_neg,
   alg_hom.map_neg, bind‚ÇÅ_X_right, map_witt_structure_int]
end

@[simp] lemma constant_coeff_witt_add (n : ‚Ñï) :
  constant_coeff (witt_add p n) = 0 :=
begin
  apply constant_coeff_witt_structure_int p _ _ n,
  simp only [add_zero, ring_hom.map_add, constant_coeff_X],
end

@[simp] lemma constant_coeff_witt_sub (n : ‚Ñï) :
  constant_coeff (witt_sub p n) = 0 :=
begin
  apply constant_coeff_witt_structure_int p _ _ n,
  simp only [sub_zero, ring_hom.map_sub, constant_coeff_X],
end

@[simp] lemma constant_coeff_witt_mul (n : ‚Ñï) :
  constant_coeff (witt_mul p n) = 0 :=
begin
  apply constant_coeff_witt_structure_int p _ _ n,
  simp only [mul_zero, ring_hom.map_mul, constant_coeff_X],
end

@[simp] lemma constant_coeff_witt_neg (n : ‚Ñï) :
  constant_coeff (witt_neg p n) = 0 :=
begin
  apply constant_coeff_witt_structure_int p _ _ n,
  simp only [neg_zero, ring_hom.map_neg, constant_coeff_X],
end

@[simp] lemma constant_coeff_witt_nsmul (m : ‚Ñï) (n : ‚Ñï):
  constant_coeff (witt_nsmul p m n) = 0 :=
begin
  apply constant_coeff_witt_structure_int p _ _ n,
  simp only [smul_zero, map_nsmul, constant_coeff_X],
end

@[simp] lemma constant_coeff_witt_zsmul (z : ‚Ñ§) (n : ‚Ñï):
  constant_coeff (witt_zsmul p z n) = 0 :=
begin
  apply constant_coeff_witt_structure_int p _ _ n,
  simp only [smul_zero, map_zsmul, constant_coeff_X],
end

end witt_structure_simplifications

section coeff

variables (p R)

@[simp] lemma zero_coeff (n : ‚Ñï) : (0 : ùïé R).coeff n = 0 :=
show (aeval _ (witt_zero p n) : R) = 0,
by simp only [witt_zero_eq_zero, alg_hom.map_zero]

@[simp] lemma one_coeff_zero : (1 : ùïé R).coeff 0 = 1 :=
show (aeval _ (witt_one p 0) : R) = 1,
by simp only [witt_one_zero_eq_one, alg_hom.map_one]

@[simp] lemma one_coeff_eq_of_pos (n : ‚Ñï) (hn : 0 < n) : coeff (1 : ùïé R) n = 0 :=
show (aeval _ (witt_one p n) : R) = 0,
by simp only [hn, witt_one_pos_eq_zero, alg_hom.map_zero]

variables {p R}

omit hp
@[simp]
lemma v2_coeff {p' R'} (x y : witt_vector p' R') (i : fin 2) :
  (![x, y] i).coeff = ![x.coeff, y.coeff] i :=
by fin_cases i; simp
include hp

lemma add_coeff (x y : ùïé R) (n : ‚Ñï) :
  (x + y).coeff n = peval (witt_add p n) ![x.coeff, y.coeff] :=
by simp [(+), eval]

lemma sub_coeff (x y : ùïé R) (n : ‚Ñï) :
  (x - y).coeff n = peval (witt_sub p n) ![x.coeff, y.coeff] :=
by simp [has_sub.sub, eval]

lemma mul_coeff (x y : ùïé R) (n : ‚Ñï) :
  (x * y).coeff n = peval (witt_mul p n) ![x.coeff, y.coeff] :=
by simp [(*), eval]

lemma neg_coeff (x : ùïé R) (n : ‚Ñï) :
  (-x).coeff n = peval (witt_neg p n) ![x.coeff] :=
by simp [has_neg.neg, eval, matrix.cons_fin_one]

lemma nsmul_coeff (m : ‚Ñï) (x : ùïé R) (n : ‚Ñï) :
  (m ‚Ä¢ x).coeff n = peval (witt_nsmul p m n) ![x.coeff] :=
by simp [has_scalar.smul, eval, matrix.cons_fin_one]

lemma zsmul_coeff (m : ‚Ñ§) (x : ùïé R) (n : ‚Ñï) :
  (m ‚Ä¢ x).coeff n = peval (witt_zsmul p m n) ![x.coeff] :=
by simp [has_scalar.smul, eval, matrix.cons_fin_one]

lemma pow_coeff (m : ‚Ñï) (x : ùïé R) (n : ‚Ñï) :
  (x ^ m).coeff n = peval (witt_pow p m n) ![x.coeff] :=
by simp [has_pow.pow, eval, matrix.cons_fin_one]

lemma add_coeff_zero (x y : ùïé R) : (x + y).coeff 0 = x.coeff 0 + y.coeff 0 :=
by simp [add_coeff, peval]

lemma mul_coeff_zero (x y : ùïé R) : (x * y).coeff 0 = x.coeff 0 * y.coeff 0 :=
by simp [mul_coeff, peval]

end coeff

lemma witt_add_vars (n : ‚Ñï) :
  (witt_add p n).vars ‚äÜ finset.univ.product (finset.range (n + 1)) :=
witt_structure_int_vars _ _ _

lemma witt_sub_vars (n : ‚Ñï) :
  (witt_sub p n).vars ‚äÜ finset.univ.product (finset.range (n + 1)) :=
witt_structure_int_vars _ _ _

lemma witt_mul_vars (n : ‚Ñï) :
  (witt_mul p n).vars ‚äÜ finset.univ.product (finset.range (n + 1)) :=
witt_structure_int_vars _ _ _

lemma witt_neg_vars (n : ‚Ñï) :
  (witt_neg p n).vars ‚äÜ finset.univ.product (finset.range (n + 1)) :=
witt_structure_int_vars _ _ _

lemma witt_nsmul_vars (m : ‚Ñï) (n : ‚Ñï) :
  (witt_nsmul p m n).vars ‚äÜ finset.univ.product (finset.range (n + 1)) :=
witt_structure_int_vars _ _ _

lemma witt_zsmul_vars (m : ‚Ñ§) (n : ‚Ñï) :
  (witt_zsmul p m n).vars ‚äÜ finset.univ.product (finset.range (n + 1)) :=
witt_structure_int_vars _ _ _

lemma witt_pow_vars (m : ‚Ñï) (n : ‚Ñï) :
  (witt_pow p m n).vars ‚äÜ finset.univ.product (finset.range (n + 1)) :=
witt_structure_int_vars _ _ _

end witt_vector
