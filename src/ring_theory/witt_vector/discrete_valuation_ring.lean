/-
Copyright (c) 2022 Robert Y. Lewis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert Y. Lewis, Heather Macbeth, Johan Commelin
-/

import ring_theory.witt_vector.domain
import ring_theory.witt_vector.mul_coeff
import ring_theory.discrete_valuation_ring
import tactic.linear_combination

/-!

# Witt vectors over a perfect ring

This file establishes that Witt vectors over a perfect field are a discrete valuation ring.
When `k` is a perfect ring, a nonzero `a : ğ• k` can be written as `p^m * b` for some `m : â„•` and
`b : ğ• k` with nonzero 0th coefficient.
When `k` is also a field, this `b` can be chosen to be a unit of `ğ• k`.

## Main declarations

* `witt_vector.exists_eq_pow_p_mul`: the existence of this element `b` over a perfect ring
* `witt_vector.exists_eq_pow_p_mul'`: the existence of this unit `b` over a perfect field
* `witt_vector.discrete_valuation_ring`: `ğ• k` is a discrete valuation ring if `k` is a perfect
    field

-/

noncomputable theory

namespace witt_vector

variables {p : â„•} [hp : fact p.prime]
include hp
local notation `ğ•` := witt_vector p

section comm_ring
variables {k : Type*} [comm_ring k] [char_p k p]

/-- This is the `n+1`st coefficient of our inverse. -/
def succ_nth_val_units (n : â„•) (a : units k) (A : ğ• k) (bs : fin (n+1) â†’ k) : k :=
- â†‘(aâ»Â¹ ^ (p^(n+1)))
* (A.coeff (n + 1) * â†‘(aâ»Â¹ ^ (p^(n+1))) + nth_remainder p n (truncate_fun (n+1) A) bs)

/--
Recursively defines the sequence of coefficients for the inverse to a Witt vector whose first entry
is a unit.
-/
noncomputable def inverse_coeff (a : units k) (A : ğ• k) : â„• â†’ k
| 0       := â†‘aâ»Â¹
| (n + 1) := succ_nth_val_units n a A (Î» i, inverse_coeff i.val)
              using_well_founded { dec_tac := `[apply fin.is_lt] }

/--
Upgrade a Witt vector `A` whose first entry `A.coeff 0` is a unit to be, itself, a unit in `ğ• k`.
-/
def mk_unit {a : units k} {A : ğ• k} (hA : A.coeff 0 = a) : units (ğ• k) :=
units.mk_of_mul_eq_one A (witt_vector.mk p (inverse_coeff a A))
  begin
    ext n,
    induction n with n ih,
    { simp [witt_vector.mul_coeff_zero, inverse_coeff, hA] },
    let H_coeff := A.coeff (n + 1) * â†‘(aâ»Â¹ ^ p ^ (n + 1))
      + nth_remainder p n (truncate_fun (n + 1) A) (Î» (i : fin (n + 1)), inverse_coeff a A i),
    have H := units.mul_inv (a ^ p ^ (n + 1)),
    linear_combination (H, -H_coeff) { normalize := ff },
    have ha : (a:k) ^ (p ^ (n + 1)) = â†‘(a ^ (p ^ (n + 1))) := by norm_cast,
    have ha_inv : (â†‘(aâ»Â¹):k) ^ (p ^ (n + 1)) = â†‘(a ^ (p ^ (n + 1)))â»Â¹ :=
      by exact_mod_cast inv_pow _ _,
    simp only [nth_remainder_spec, inverse_coeff, succ_nth_val_units, hA, fin.val_eq_coe,
      one_coeff_eq_of_pos, nat.succ_pos', H_coeff, ha_inv, ha, inv_pow],
    ring!,
  end

@[simp] lemma coe_mk_unit {a : units k} {A : ğ• k} (hA : A.coeff 0 = a) : (mk_unit hA : ğ• k) = A :=
rfl

end comm_ring

section field
variables {k : Type*} [field k] [char_p k p]

lemma is_unit_of_coeff_zero_ne_zero (x : ğ• k) (hx : x.coeff 0 â‰  0) : is_unit x :=
begin
  let y : kË£ := units.mk0 (x.coeff 0) hx,
  have hy : x.coeff 0 = y := rfl,
  exact (mk_unit hy).is_unit
end

variables (p)
lemma irreducible : irreducible (p : ğ• k) :=
begin
  have hp : Â¬ is_unit (p : ğ• k),
  { intro hp,
    simpa only [constant_coeff_apply, coeff_p_zero, not_is_unit_zero]
      using constant_coeff.is_unit_map hp, },
  refine âŸ¨hp, Î» a b hab, _âŸ©,
  obtain âŸ¨ha0, hb0âŸ© : a â‰  0 âˆ§ b â‰  0,
  { rw â† mul_ne_zero_iff, intro h, rw h at hab, exact p_nonzero p k hab },
  obtain âŸ¨m, a, ha, rflâŸ© := verschiebung_nonzero ha0,
  obtain âŸ¨n, b, hb, rflâŸ© := verschiebung_nonzero hb0,
  cases m, { exact or.inl (is_unit_of_coeff_zero_ne_zero a ha) },
  cases n, { exact or.inr (is_unit_of_coeff_zero_ne_zero b hb) },
  rw iterate_verschiebung_mul at hab,
  apply_fun (Î» x, coeff x 1) at hab,
  simp only [coeff_p_one, nat.add_succ, add_comm _ n, function.iterate_succ', function.comp_app,
    verschiebung_coeff_add_one, verschiebung_coeff_zero] at hab,
  exact (one_ne_zero hab).elim
end

end field

section perfect_ring
variables {k : Type*} [comm_ring k] [char_p k p] [perfect_ring k p]

lemma exists_eq_pow_p_mul (a : ğ• k) (ha : a â‰  0) :
  âˆƒ (m : â„•) (b : ğ• k), b.coeff 0 â‰  0 âˆ§ a = p ^ m * b :=
begin
  obtain âŸ¨m, c, hc, hcmâŸ© := witt_vector.verschiebung_nonzero ha,
  obtain âŸ¨b, rflâŸ© := (frobenius_bijective p k).surjective.iterate m c,
  rw witt_vector.iterate_frobenius_coeff at hc,
  have := congr_fun (witt_vector.verschiebung_frobenius_comm.comp_iterate m) b,
  simp only [function.comp_app] at this,
  rw â† this at hcm,
  refine âŸ¨m, b, _, _âŸ©,
  { contrapose! hc,
    have : 0 < p ^ m := pow_pos (nat.prime.pos (fact.out _)) _,
    simp [hc, zero_pow this] },
  { rw â† mul_left_iterate (p : ğ• k) m,
    convert hcm,
    ext1 x,
    rw [mul_comm, â† witt_vector.verschiebung_frobenius x] },
end

end perfect_ring

section perfect_field
variables {k : Type*} [field k] [char_p k p] [perfect_ring k p]

lemma exists_eq_pow_p_mul' (a : ğ• k) (ha : a â‰  0) :
  âˆƒ (m : â„•) (b : units (ğ• k)), a = p ^ m * b :=
begin
  obtain âŸ¨m, b, hâ‚, hâ‚‚âŸ© := exists_eq_pow_p_mul a ha,
  let bâ‚€ := units.mk0 (b.coeff 0) hâ‚,
  have hbâ‚€ : b.coeff 0 = bâ‚€ := rfl,
  exact âŸ¨m, mk_unit hbâ‚€, hâ‚‚âŸ©,
end

instance : discrete_valuation_ring (ğ• k) :=
discrete_valuation_ring.of_has_unit_mul_pow_irreducible_factorization
begin
  refine âŸ¨p, irreducible p, Î» x hx, _âŸ©,
  obtain âŸ¨n, b, hbâŸ© := exists_eq_pow_p_mul' x hx,
  exact âŸ¨n, b, hb.symmâŸ©,
end

end perfect_field
end witt_vector
