import ring_theory.witt_vector.basic
import ring_theory.witt_vector.witt_vector_preps

/-!

# The `is_poly` predicate

`witt_vector.is_poly` is a (type-valued) predicate on functions `f : Î  R, ğ• R â†’ ğ• R`.
It asserts that there is a family of polynomials `Ï† : â„• â†’ mv_polynomial â„• â„¤`,
such that the `n`th coefficient of `f x` is equal to `Ï† n` evaluated on the coefficients of `x`.
Many operations on Witt vectors satisfy this predicate (or an analogue for higher arity functions).
We say that such a function `f` is a *polynomial function*.

The power of satisfying this predicate comes from `is_poly.ext'`.
It shows that if `Ï†` and `Ïˆ` witness that `f` and `g` are polynomial functions,
then `f = g` not merely when `Ï† = Ïˆ`, but in fact it suffices to prove
```
âˆ€ n, bindâ‚ Ï† (witt_polynomial p _ n) = bindâ‚ Ïˆ (witt_polynomial p _ n)
```
(in other words, when evaluating the Witt polynomials on `Ï†` and `Ïˆ`, we get the same values)
which will then imply `Ï† = Ïˆ` and hence `f = g`.

Even though this sufficient condition looks somewhat intimidating,
it is rather pleasant to check in practice;
more so than direct checking of `Ï† = Ïˆ`.

In practice, we apply this technique to show that the composition of `witt_vector.frobenius`
and `witt_vector.verschiebung` is equal to multiplication by `p`.

## Main declarations

* `witt_vector.is_poly`, `witt_vector.is_polyâ‚‚`:
  two predicates that assert that a unary/binary function on Witt vectors
  is polynomial in the coefficients of the input values.
* `witt_vector.is_poly.ext'`, `witt_vector.is_polyâ‚‚.ext'`:
  two polynomial functions are equal if their families of polynomials are equal
  after evaluating the Witt polynmials on them.
* `witt_vector.is_poly.comp` (+ many variants) show that unary/binary compositions
  of polynomial functions are polynomial.
* `witt_vector.id_is_poly`, `witt_vector.neg_is_poly`,
  `witt_vector.add_is_polyâ‚‚`, `witt_vector.mul_is_polyâ‚‚`:
  several well-known operations are polynomial functions
  (for Verschiebung, Frobenius, and multiplication by `p`, see their respective files).

## On higher arity analogues

Ideally, there should be a predicate `is_polyâ‚™` for functions of higher arity,
together with `is_polyâ‚™.comp` that shows how such functions compose.
Since mathlib does not have a library on composition of higher arity functions,
we have only implemented the unary and binary variants so far.
Nullary functions (a.k.a. constants) are treated as constant functions and fall under the unary case.
-/

/-
This tactic is used later in the development for certain simplifications.
We define it here so it is a shared import.
-/

mk_simp_attribute ghost_simps
"Simplification rules for ghost equations"

namespace tactic
namespace interactive
setup_tactic_parser
/-- A macro for a common simplification when rewriting with ghost component equations. -/
meta def witt_simp (lems : parse simp_arg_list) : tactic unit :=
do tactic.try tactic.intro1,
   --lems â† simp_lemmas.add_simp lems `rename_bindâ‚ tt,
   simp none tt
     (lems ++ [simp_arg_type.symm_expr ``(mv_polynomial.rename_bindâ‚),
               simp_arg_type.symm_expr ``(mv_polynomial.bindâ‚_bindâ‚)])
     [`ghost_simps] (loc.ns [none])
-- `[try {intro}, simp only [â† rename_bindâ‚, â† bindâ‚_bindâ‚] with ghost_simps]

end interactive
end tactic

namespace witt_vector

variables {p : â„•} {R S Ïƒ idx : Type*} [hp : fact p.prime] [comm_ring R] [comm_ring S]

local notation `ğ•` := witt_vector p -- type as `\bbW`

local attribute [semireducible] witt_vector
local attribute [instance] mv_polynomial.invertible_rat_coe_nat

open mv_polynomial
open function (uncurry)
local attribute [-simp] coe_evalâ‚‚_hom

include hp
variables (p)

noncomputable theory

lemma poly_eq_of_witt_polynomial_bind_eq' (f g : â„• â†’ mv_polynomial (idx Ã— â„•) â„¤)
  (h : âˆ€ n, bindâ‚ f (witt_polynomial p _ n) = bindâ‚ g (witt_polynomial p _ n)) :
  f = g :=
begin
  ext1 n,
  apply mv_polynomial.map_injective (int.cast_ring_hom â„š) int.cast_injective,
  rw â† function.funext_iff at h,
  replace h := congr_arg
    (Î» fam, bindâ‚ (mv_polynomial.map (int.cast_ring_hom â„š) âˆ˜ fam)
    (X_in_terms_of_W p â„š n)) h,
  simpa only [function.comp, map_bindâ‚, map_witt_polynomial,
    â† bindâ‚_bindâ‚, bindâ‚_witt_polynomial_X_in_terms_of_W, bindâ‚_X_right] using h
end

lemma poly_eq_of_witt_polynomial_bind_eq (f g : â„• â†’ mv_polynomial â„• â„¤)
  (h : âˆ€ n, bindâ‚ f (witt_polynomial p _ n) = bindâ‚ g (witt_polynomial p _ n)) :
  f = g :=
begin
  ext1 n,
  apply mv_polynomial.map_injective (int.cast_ring_hom â„š) int.cast_injective,
  rw â† function.funext_iff at h,
  replace h := congr_arg
    (Î» fam, bindâ‚ (mv_polynomial.map (int.cast_ring_hom â„š) âˆ˜ fam)
    (X_in_terms_of_W p â„š n)) h,
  simpa only [function.comp, map_bindâ‚, map_witt_polynomial,
    â† bindâ‚_bindâ‚, bindâ‚_witt_polynomial_X_in_terms_of_W, bindâ‚_X_right] using h
end

-- Ideally, we would generalise this to n-ary functions
-- But we don't have a good theory of n-ary compositions in mathlib
omit hp

/--
A function `f : Î  R, ğ• R â†’ ğ• R` that maps Witt vectors to Witt vectors over arbitrary base rings
is said to be *polynomial* if there is a family of polynomials `Ï†â‚™` over `â„¤` such that the `n`th
coefficient of `f x` is given by evaluating `Ï†â‚™` at the coefficients of `x`.

See also `witt_vector.is_polyâ‚‚` for the binary variant.
-/
structure is_poly (f : Î  â¦ƒRâ¦„ [comm_ring R], witt_vector p R â†’ ğ• R) (Ï† : â„• â†’ mv_polynomial â„• â„¤) :=
(coeff : âˆ€ â¦ƒRâ¦„ [comm_ring R] (x : ğ• R), (f x).coeff = Î» n, aeval x.coeff (Ï† n))

/-- The identity function on Witt vectors is a polynomial function. -/
def id_is_poly : is_poly p (Î» _ _, id) X :=
âŸ¨by { introsI, simp only [aeval_X, id] }âŸ©

include hp

/-- The additive negation is a polynomial function on Witt vectors. -/
def neg_is_poly : is_poly p (Î» R _, by exactI @has_neg.neg (ğ• R) _)
  (Î» n, rename prod.snd (witt_neg p n)) :=
{ coeff :=
  begin
    introsI, funext n,
    rw [neg_coeff, aeval_eq_evalâ‚‚_hom, evalâ‚‚_hom_rename],
    apply evalâ‚‚_hom_congr rfl _ rfl,
    ext âŸ¨i, kâŸ©, fin_cases i, refl,
  end }


section zero_one
/- To avoid a theory of 0-ary functions (a.k.a. constants)
we model them as constant unary functions. -/

/-- The function that is constantly zero on Witt vectors is a polynomial function. -/
def zero_is_poly : is_poly p (Î» _ _ _, by exactI 0) 0 :=
âŸ¨by { introsI, funext n, simp only [pi.zero_apply, alg_hom.map_zero, zero_coeff] }âŸ©

@[simp] lemma bindâ‚_zero_witt_polynomial (n : â„•) :
  bindâ‚ (0 : â„• â†’ mv_polynomial â„• R) (witt_polynomial p R n) = 0 :=
by rw [â† aeval_eq_bindâ‚, aeval_zero, constant_coeff_witt_polynomial, ring_hom.map_zero]

omit hp

/-- The coefficients of `1 : ğ• R` as polynomials. -/
def one_poly (n : â„•) : mv_polynomial â„• â„¤ := if n = 0 then 1 else 0

include hp

@[simp] lemma bindâ‚_one_poly_witt_polynomial (n : â„•) :
  bindâ‚ one_poly (witt_polynomial p â„¤ n) = 1 :=
begin
  rw [witt_polynomial_eq_sum_C_mul_X_pow, alg_hom.map_sum, finset.sum_eq_single 0],
  { simp only [one_poly, one_pow, one_mul, alg_hom.map_pow, C_1, pow_zero, bindâ‚_X_right,
      if_true, eq_self_iff_true], },
  { intros i hi hi0,
    simp only [one_poly, if_neg hi0, zero_pow (pow_pos (nat.prime.pos hp) _), mul_zero,
      alg_hom.map_pow, bindâ‚_X_right, alg_hom.map_mul], },
  { rw finset.mem_range, dec_trivial }
end

/-- The function that is constantly one on Witt vectors is a polynomial function. -/
def one_is_poly : is_poly p (Î» _ _ _, by exactI 1) one_poly :=
{ coeff :=
  begin
    introsI, funext n, cases n,
    { simp only [one_poly, if_true, eq_self_iff_true, one_coeff_zero, alg_hom.map_one], },
    { simp only [one_poly, nat.succ_pos', one_coeff_pos, if_neg n.succ_ne_zero, alg_hom.map_zero] }
  end }

end zero_one

omit hp

/--
A binary function `f : Î  R, ğ• R â†’ ğ• R â†’ ğ• R` on Witt vectors
is said to be *polynomial* if there is a family of polynomials `Ï†â‚™` over `â„¤` such that the `n`th
coefficient of `f x y` is given by evaluating `Ï†â‚™` at the coefficients of `x` and `y`.

See also `witt_vector.is_poly` for the unary variant.
-/
structure is_polyâ‚‚ (f : Î  â¦ƒRâ¦„ [comm_ring R], witt_vector p R â†’ ğ• R â†’ ğ• R)
  (Ï† : â„• â†’ mv_polynomial (fin 2 Ã— â„•) â„¤) :=
(coeff : âˆ€ â¦ƒRâ¦„ [comm_ring R] (x y : ğ• R), (f x y).coeff = Î» n, peval (Ï† n) ![x.coeff, y.coeff])

/-- Addition of Witt vectors is a polynomial function. -/
def add_is_polyâ‚‚ [fact p.prime] : is_polyâ‚‚ p (Î» _ _, by exactI (+)) (witt_add p) :=
âŸ¨by { introsI, refl }âŸ©

/-- Multiplication of Witt vectors is a polynomial function. -/
def mul_is_polyâ‚‚ [fact p.prime] : is_polyâ‚‚ p (Î» _ _, by exactI (*)) (witt_mul p) :=
âŸ¨by { introsI, refl }âŸ©

namespace is_poly

instance : inhabited (is_poly p (Î» _ _, id) _) :=
âŸ¨id_is_poly pâŸ©

variables {p}

lemma ext {f Ï† g Ïˆ} (hf : is_poly p f Ï†) (hg : is_poly p g Ïˆ) (h : Ï† = Ïˆ) : f = g :=
by { ext R _Rcr x n, rw [hf.coeff, hg.coeff, h] }

/-- The composition of polynomial functions is polynomial. -/
def comp {g Ïˆ f Ï†} (hg : is_poly p g Ïˆ) (hf : is_poly p f Ï†) :
  is_poly p (Î» R _Rcr, @g R _Rcr âˆ˜ @f R _Rcr) (Î» n, bindâ‚ Ï† (Ïˆ n)) :=
âŸ¨by { intros, simp only [aeval_bindâ‚, function.comp, hg.coeff, hf.coeff] }âŸ©

/-- The composition of a polynomial function with a binary polynomial function is polynomial. -/
def compâ‚‚ {g Ïˆ f Ï†} (hg : is_poly p g Ïˆ) (hf : is_polyâ‚‚ p f Ï†) :
  is_polyâ‚‚ p (Î» R _Rcr x y, by exactI g (f x y)) (Î» n, bindâ‚ Ï† (Ïˆ n)) :=
âŸ¨by { intros, simp only [peval, aeval_bindâ‚, function.comp, hg.coeff, hf.coeff] }âŸ©

include hp

lemma ext' {f Ï† g Ïˆ} (hf : is_poly p f Ï†) (hg : is_poly p g Ïˆ)
  (h : âˆ€ n, bindâ‚ Ï† (witt_polynomial p _ n) = bindâ‚ Ïˆ (witt_polynomial p _ n)) :
  âˆ€ (R) [_Rcr : comm_ring R] (x : ğ• R), by exactI f x = g x :=
by { rw (is_poly.ext hf hg $ poly_eq_of_witt_polynomial_bind_eq p _ _ h), intros, refl }

end is_poly

namespace is_polyâ‚‚

instance [fact p.prime] : inhabited (is_polyâ‚‚ p _ _) :=
âŸ¨add_is_polyâ‚‚ pâŸ©

variables {p}

lemma ext {f Ï† g Ïˆ} (hf : is_polyâ‚‚ p f Ï†) (hg : is_polyâ‚‚ p g Ïˆ) (h : Ï† = Ïˆ) : f = g :=
by { ext R _Rcr x n, rw [hf.coeff, hg.coeff, h] }

/-- The composition of a binary polynomial function
with two unary polynomial functions is polynomial. -/
def comp {h Ï‡ f Ï† g Ïˆ} (hh : is_polyâ‚‚ p h Ï‡) (hf : is_poly p f Ï†) (hg : is_poly p g Ïˆ) :
  is_polyâ‚‚ p (Î» R _Rcr x y, by exactI h (f x) (g y))
    (Î» n, bindâ‚ (uncurry $
            ![Î» k, rename (prod.mk (0 : fin 2)) (Ï† k),
              Î» k, rename (prod.mk (1 : fin 2)) (Ïˆ k)]) (Ï‡ n)) :=
{ coeff :=
  begin
    intros,
    funext n,
    simp only [peval, aeval_bindâ‚, function.comp, hh.coeff, hf.coeff, hg.coeff, uncurry],
    apply evalâ‚‚_hom_congr rfl _ rfl,
    ext âŸ¨i, nâŸ©,
    fin_cases i;
    simp only [aeval_eq_evalâ‚‚_hom, evalâ‚‚_hom_rename, function.comp, matrix.cons_val_zero,
      matrix.head_cons, matrix.cons_val_one],
  end }

/-- The composition of a binary polynomial function
 with a unary polynomial function in the first argument is polynomial. -/
def comp_left {g Ïˆ f Ï†} (hg : is_polyâ‚‚ p g Ïˆ) (hf : is_poly p f Ï†) :
  is_polyâ‚‚ p (Î» R _Rcr x y, by exactI g (f x) y) _ :=
hg.comp hf (id_is_poly p)

/-- The composition of a binary polynomial function
 with a unary polynomial function in the second argument is polynomial. -/
def comp_right {g Ïˆ f Ï†} (hg : is_polyâ‚‚ p g Ïˆ) (hf : is_poly p f Ï†) :
  is_polyâ‚‚ p (Î» R _Rcr x y, by exactI g x (f y)) _ :=
hg.comp (id_is_poly p) hf

def diag {f Ï†} (hf : is_polyâ‚‚ p f Ï†) :
  is_poly p (Î» R _Rcr x, by exactI f x x) (Î» n, bindâ‚ (uncurry ![X, X]) (Ï† n)) :=
{ coeff :=
  begin
    intros, funext n,
    simp only [hf.coeff, peval, uncurry, aeval_bindâ‚],
    apply evalâ‚‚_hom_congr rfl _ rfl,
    ext âŸ¨i, kâŸ©, fin_cases i;
    simp only [matrix.head_cons, aeval_X, matrix.cons_val_zero, matrix.cons_val_one],
  end }

include hp

lemma ext' {f Ï† g Ïˆ} (hf : is_polyâ‚‚ p f Ï†) (hg : is_polyâ‚‚ p g Ïˆ)
  (h : âˆ€ n, bindâ‚ Ï† (witt_polynomial p _ n) = bindâ‚ Ïˆ (witt_polynomial p _ n)) :
  âˆ€ (R) [_Rcr : comm_ring R] (x y : ğ• R), by exactI f x y = g x y :=
by { rw (is_polyâ‚‚.ext hf hg $ poly_eq_of_witt_polynomial_bind_eq' p _ _ h), intros, refl }

end is_polyâ‚‚

attribute [ghost_simps]
      witt_structure_int_prop witt_add witt_mul witt_neg
      alg_hom.map_zero alg_hom.map_one bindâ‚_zero_witt_polynomial bindâ‚_one_poly_witt_polynomial
      alg_hom.map_add alg_hom.map_mul bindâ‚_X_right function.uncurry bindâ‚_rename function.comp
      matrix.head_cons matrix.cons_val_one matrix.cons_val_zero
      if_true eq_self_iff_true if_false add_zero ring_hom.map_zero
      ring_hom.map_mul alg_hom.map_mul mul_add
      ring_hom.map_nat_cast alg_hom.map_nat_cast

end witt_vector
