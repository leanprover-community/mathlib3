/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/

import ring_theory.witt_vector.frobenius_fraction_field

/-!

## F-isocrystals over a perfect field

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

When `k` is an integral domain, so is `ùïé k`, and we can consider its field of fractions `K(p, k)`.
The endomorphism `witt_vector.frobenius` lifts to `œÜ : K(p, k) ‚Üí K(p, k)`; if `k` is perfect, `œÜ` is
an automorphism.

Let `k` be a perfect integral domain. Let `V` be a vector space over `K(p,k)`.
An *isocrystal* is a bijective map `V ‚Üí V` that is `œÜ`-semilinear.
A theorem of Dieudonn√© and Manin classifies the finite-dimensional isocrystals over algebraically
closed fields. In the one-dimensional case, this classification states that the isocrystal
structures are parametrized by their "slope" `m : ‚Ñ§`.
Any one-dimensional isocrystal is isomorphic to `œÜ(p^m ‚Ä¢ x) : K(p,k) ‚Üí K(p,k)` for some `m`.

This file proves this one-dimensional case of the classification theorem.
The construction is described in Dupuis, Lewis, and Macbeth,
[Formalized functional analysis via semilinear maps][dupuis-lewis-macbeth2022].

## Main declarations

* `witt_vector.isocrystal`: a vector space over the field `K(p, k)` additionally equipped with a
  Frobenius-linear automorphism.
* `witt_vector.isocrystal_classification`: a one-dimensional isocrystal admits an isomorphism to one
  of the standard one-dimensional isocrystals.

## Notation

This file introduces notation in the locale `isocrystal`.
* `K(p, k)`: `fraction_ring (witt_vector p k)`
* `œÜ(p, k)`: `witt_vector.fraction_ring.frobenius_ring_hom p k`
* `M ‚Üí·∂†À°[p, k] M‚ÇÇ`: `linear_map (witt_vector.fraction_ring.frobenius_ring_hom p k) M M‚ÇÇ`
* `M ‚âÉ·∂†À°[p, k] M‚ÇÇ`: `linear_equiv (witt_vector.fraction_ring.frobenius_ring_hom p k) M M‚ÇÇ`
* `Œ¶(p, k)`: `witt_vector.isocrystal.frobenius p k`
* `M ‚Üí·∂†‚Å±[p, k] M‚ÇÇ`: `witt_vector.isocrystal_hom p k M M‚ÇÇ`
* `M ‚âÉ·∂†‚Å±[p, k] M‚ÇÇ`: `witt_vector.isocrystal_equiv p k M M‚ÇÇ`

## References

* [Formalized functional analysis via semilinear maps][dupuis-lewis-macbeth2022]
* [Theory of commutative formal groups over fields of finite characteristic][manin1963]
* <https://www.math.ias.edu/~lurie/205notes/Lecture26-Isocrystals.pdf>

-/

noncomputable theory
open finite_dimensional

namespace witt_vector

variables (p : ‚Ñï) [fact p.prime]
variables (k : Type*) [comm_ring k]
localized "notation (name := witt_vector.fraction_ring)
  `K(`p`, `k`)` := fraction_ring (witt_vector p k)" in isocrystal

section perfect_ring
variables [is_domain k] [char_p k p] [perfect_ring k p]

/-! ### Frobenius-linear maps -/

/-- The Frobenius automorphism of `k` induces an automorphism of `K`. -/
def fraction_ring.frobenius : K(p, k) ‚âÉ+* K(p, k) :=
is_fraction_ring.field_equiv_of_ring_equiv (frobenius_equiv p k)

/-- The Frobenius automorphism of `k` induces an endomorphism of `K`. For notation purposes. -/
def fraction_ring.frobenius_ring_hom : K(p, k) ‚Üí+* K(p, k) := fraction_ring.frobenius p k

localized "notation (name := witt_vector.frobenius_ring_hom)
  `œÜ(`p`, `k`)` := witt_vector.fraction_ring.frobenius_ring_hom p k" in isocrystal

instance inv_pair‚ÇÅ : ring_hom_inv_pair (œÜ(p, k)) _ :=
ring_hom_inv_pair.of_ring_equiv (fraction_ring.frobenius p k)

instance inv_pair‚ÇÇ :
  ring_hom_inv_pair ((fraction_ring.frobenius p k).symm : K(p, k) ‚Üí+* K(p, k)) _ :=
ring_hom_inv_pair.of_ring_equiv (fraction_ring.frobenius p k).symm

localized "notation (name := frobenius_ring_hom.linear_map) M ` ‚Üí·∂†À°[`:50 p `, ` k `] ` M‚ÇÇ :=
  linear_map (witt_vector.fraction_ring.frobenius_ring_hom p k) M M‚ÇÇ" in isocrystal
localized "notation (name := frobenius_ring_hom.linear_equiv) M ` ‚âÉ·∂†À°[`:50 p `, ` k `] ` M‚ÇÇ :=
  linear_equiv (witt_vector.fraction_ring.frobenius_ring_hom p k) M M‚ÇÇ" in isocrystal

/-! ### Isocrystals -/

/--
An isocrystal is a vector space over the field `K(p, k)` additionally equipped with a
Frobenius-linear automorphism.
-/
class isocrystal (V : Type*) [add_comm_group V] extends module K(p, k) V :=
( frob : V ‚âÉ·∂†À°[p, k] V )

variables (V : Type*) [add_comm_group V] [isocrystal p k V]
variables (V‚ÇÇ : Type*) [add_comm_group V‚ÇÇ] [isocrystal p k V‚ÇÇ]

variables {V}

/--
Project the Frobenius automorphism from an isocrystal. Denoted by `Œ¶(p, k)` when V can be inferred.
-/
def isocrystal.frobenius : V ‚âÉ·∂†À°[p, k] V := @isocrystal.frob p _ k _ _ _ _ _ _ _
variables (V)

localized "notation `Œ¶(`p`, `k`)` := witt_vector.isocrystal.frobenius p k" in isocrystal

/-- A homomorphism between isocrystals respects the Frobenius map. -/
@[nolint has_nonempty_instance]
structure isocrystal_hom extends V ‚Üí‚Çó[K(p, k)] V‚ÇÇ :=
( frob_equivariant : ‚àÄ x : V, Œ¶(p, k) (to_linear_map x) = to_linear_map (Œ¶(p, k) x) )

/-- An isomorphism between isocrystals respects the Frobenius map. -/
@[nolint has_nonempty_instance]
structure isocrystal_equiv extends V ‚âÉ‚Çó[K(p, k)] V‚ÇÇ :=
( frob_equivariant : ‚àÄ x : V, Œ¶(p, k) (to_linear_equiv x) = to_linear_equiv (Œ¶(p, k) x) )

localized "notation (name := isocrystal_hom)
  M ` ‚Üí·∂†‚Å±[`:50 p `, ` k `] ` M‚ÇÇ := witt_vector.isocrystal_hom p k M M‚ÇÇ" in isocrystal
localized "notation (name := isocrystal_equiv)
  M ` ‚âÉ·∂†‚Å±[`:50 p `, ` k `] ` M‚ÇÇ := witt_vector.isocrystal_equiv p k M M‚ÇÇ" in isocrystal


end perfect_ring

open_locale isocrystal

/-! ### Classification of isocrystals in dimension 1 -/

/-- A helper instance for type class inference. -/
local attribute [instance]
def fraction_ring.module : module K(p, k) K(p, k) := semiring.to_module

/--
Type synonym for `K(p, k)` to carry the standard 1-dimensional isocrystal structure
of slope `m : ‚Ñ§`.
-/
@[nolint unused_arguments has_nonempty_instance, derive [add_comm_group, module K(p, k)]]
def standard_one_dim_isocrystal (m : ‚Ñ§) : Type* :=
K(p, k)

section perfect_ring
variables [is_domain k] [char_p k p] [perfect_ring k p]

/-- The standard one-dimensional isocrystal of slope `m : ‚Ñ§` is an isocrystal. -/
instance (m : ‚Ñ§) : isocrystal p k (standard_one_dim_isocrystal p k m) :=
{ frob := (fraction_ring.frobenius p k).to_semilinear_equiv.trans
   (linear_equiv.smul_of_ne_zero _ _ _ (zpow_ne_zero m (witt_vector.fraction_ring.p_nonzero p k))) }

@[simp] lemma standard_one_dim_isocrystal.frobenius_apply (m : ‚Ñ§)
  (x : standard_one_dim_isocrystal p k m) :
  Œ¶(p, k) x = (p:K(p, k)) ^ m ‚Ä¢ œÜ(p, k) x :=
rfl

end perfect_ring

/-- A one-dimensional isocrystal over an algebraically closed field
admits an isomorphism to one of the standard (indexed by `m : ‚Ñ§`) one-dimensional isocrystals. -/
theorem isocrystal_classification
  (k : Type*) [field k] [is_alg_closed k] [char_p k p]
  (V : Type*) [add_comm_group V] [isocrystal p k V]
  (h_dim : finrank K(p, k) V = 1) :
  ‚àÉ (m : ‚Ñ§), nonempty (standard_one_dim_isocrystal p k m ‚âÉ·∂†‚Å±[p, k] V) :=
begin
  haveI : nontrivial V := finite_dimensional.nontrivial_of_finrank_eq_succ h_dim,
  obtain ‚ü®x, hx‚ü© : ‚àÉ x : V, x ‚â† 0 := exists_ne 0,
  have : Œ¶(p, k) x ‚â† 0 := by simpa only [map_zero] using Œ¶(p,k).injective.ne hx,
  obtain ‚ü®a, ha, hax‚ü© : ‚àÉ a : K(p, k), a ‚â† 0 ‚àß Œ¶(p, k) x = a ‚Ä¢ x,
  { rw finrank_eq_one_iff_of_nonzero' x hx at h_dim,
    obtain ‚ü®a, ha‚ü© := h_dim (Œ¶(p, k) x),
    refine ‚ü®a, _, ha.symm‚ü©,
    intros ha',
    apply this,
    simp only [‚Üêha, ha', zero_smul] },
  obtain ‚ü®b, hb, m, hmb‚ü© := witt_vector.exists_frobenius_solution_fraction_ring p ha,
  replace hmb : œÜ(p, k) b * a = p ^ m * b := by convert hmb,
  use m,
  let F‚ÇÄ : standard_one_dim_isocrystal p k m ‚Üí‚Çó[K(p,k)] V :=
    linear_map.to_span_singleton K(p, k) V x,
  let F : standard_one_dim_isocrystal p k m ‚âÉ‚Çó[K(p,k)] V,
  { refine linear_equiv.of_bijective F‚ÇÄ ‚ü®_, _‚ü©,
    { rw ‚Üê linear_map.ker_eq_bot,
      exact linear_map.ker_to_span_singleton K(p, k) V hx },
    { rw ‚Üê linear_map.range_eq_top,
      rw ‚Üê (finrank_eq_one_iff_of_nonzero x hx).mp h_dim,
      rw linear_map.span_singleton_eq_range } },
  refine ‚ü®‚ü®(linear_equiv.smul_of_ne_zero K(p, k) _ _ hb).trans F, _‚ü©‚ü©,
  intros c,
  rw [linear_equiv.trans_apply, linear_equiv.trans_apply,
      linear_equiv.smul_of_ne_zero_apply, linear_equiv.smul_of_ne_zero_apply,
      linear_equiv.map_smul, linear_equiv.map_smul],
  simp only [hax, linear_equiv.of_bijective_apply, linear_map.to_span_singleton_apply,
    linear_equiv.map_smul‚Çõ‚Çó, standard_one_dim_isocrystal.frobenius_apply, algebra.id.smul_eq_mul],
  simp only [‚Üêmul_smul],
  congr' 1,
  linear_combination œÜ(p,k) c * hmb,
end

end witt_vector
