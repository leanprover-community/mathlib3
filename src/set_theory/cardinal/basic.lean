/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Floris van Doorn
-/
import data.nat.enat
import data.set.countable
import logic.small
import order.conditionally_complete_lattice
import set_theory.schroeder_bernstein

/-!
# Cardinal Numbers

We define cardinal numbers as a quotient of types under the equivalence relation of equinumerity.

## Main definitions

* `cardinal` the type of cardinal numbers (in a given universe).
* `cardinal.mk Œ±` or `#Œ±` is the cardinality of `Œ±`. The notation `#` lives in the locale
  `cardinal`.
* There is an instance that `cardinal` forms a `canonically_ordered_comm_semiring`.
* Addition `c‚ÇÅ + c‚ÇÇ` is defined by `cardinal.add_def Œ± Œ≤ : #Œ± + #Œ≤ = #(Œ± ‚äï Œ≤)`.
* Multiplication `c‚ÇÅ * c‚ÇÇ` is defined by `cardinal.mul_def : #Œ± * #Œ≤ = #(Œ± √ó Œ≤)`.
* The order `c‚ÇÅ ‚â§ c‚ÇÇ` is defined by `cardinal.le_def Œ± Œ≤ : #Œ± ‚â§ #Œ≤ ‚Üî nonempty (Œ± ‚Ü™ Œ≤)`.
* Exponentiation `c‚ÇÅ ^ c‚ÇÇ` is defined by `cardinal.power_def Œ± Œ≤ : #Œ± ^ #Œ≤ = #(Œ≤ ‚Üí Œ±)`.
* `cardinal.omega` or `œâ` the cardinality of `‚Ñï`. This definition is universe polymorphic:
  `cardinal.omega.{u} : cardinal.{u}`
  (contrast with `‚Ñï : Type`, which lives in a specific universe).
  In some cases the universe level has to be given explicitly.
* `cardinal.min (I : nonempty Œπ) (c : Œπ ‚Üí cardinal)` is the minimal cardinal in the range of `c`.
* `cardinal.succ c` is the successor cardinal, the smallest cardinal larger than `c`.
* `cardinal.sum` is the sum of a collection of cardinals.
* `cardinal.sup` is the supremum of a collection of cardinals.
* `cardinal.powerlt c‚ÇÅ c‚ÇÇ` or `c‚ÇÅ ^< c‚ÇÇ` is defined as `sup_{Œ≥ < Œ≤} Œ±^Œ≥`.

## Main Statements

* Cantor's theorem: `cardinal.cantor c : c < 2 ^ c`.
* K√∂nig's theorem: `cardinal.sum_lt_prod`

## Implementation notes

* There is a type of cardinal numbers in every universe level:
  `cardinal.{u} : Type (u + 1)` is the quotient of types in `Type u`.
  The operation `cardinal.lift` lifts cardinal numbers to a higher level.
* Cardinal arithmetic specifically for infinite cardinals (like `Œ∫ * Œ∫ = Œ∫`) is in the file
  `set_theory/cardinal_ordinal.lean`.
* There is an instance `has_pow cardinal`, but this will only fire if Lean already knows that both
  the base and the exponent live in the same universe. As a workaround, you can add
  ```
    local infixr ^ := @has_pow.pow cardinal cardinal cardinal.has_pow
  ```
  to a file. This notation will work even if Lean doesn't know yet that the base and the exponent
  live in the same universe (but no exponents in other types can be used).

## References

* <https://en.wikipedia.org/wiki/Cardinal_number>

## Tags

cardinal number, cardinal arithmetic, cardinal exponentiation, omega,
Cantor's theorem, K√∂nig's theorem, Konig's theorem
-/

open function set
open_locale classical

noncomputable theory

universes u v w x
variables {Œ± Œ≤ : Type u}

/-- The equivalence relation on types given by equivalence (bijective correspondence) of types.
  Quotienting by this equivalence relation gives the cardinal numbers.
-/
instance cardinal.is_equivalent : setoid (Type u) :=
{ r := Œª Œ± Œ≤, nonempty (Œ± ‚âÉ Œ≤),
  iseqv := ‚ü®Œª Œ±,
    ‚ü®equiv.refl Œ±‚ü©,
    Œª Œ± Œ≤ ‚ü®e‚ü©, ‚ü®e.symm‚ü©,
    Œª Œ± Œ≤ Œ≥ ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü©, ‚ü®e‚ÇÅ.trans e‚ÇÇ‚ü©‚ü© }

/-- `cardinal.{u}` is the type of cardinal numbers in `Type u`,
  defined as the quotient of `Type u` by existence of an equivalence
  (a bijection with explicit inverse). -/
def cardinal : Type (u + 1) := quotient cardinal.is_equivalent

namespace cardinal

/-- The cardinal number of a type -/
def mk : Type u ‚Üí cardinal := quotient.mk

localized "notation `#` := cardinal.mk" in cardinal

instance can_lift_cardinal_Type : can_lift cardinal.{u} (Type u) :=
‚ü®mk, Œª c, true, Œª c _, quot.induction_on c $ Œª Œ±, ‚ü®Œ±, rfl‚ü©‚ü©

@[elab_as_eliminator]
lemma induction_on {p : cardinal ‚Üí Prop} (c : cardinal) (h : ‚àÄ Œ±, p (#Œ±)) : p c :=
quotient.induction_on c h

@[elab_as_eliminator]
lemma induction_on‚ÇÇ {p : cardinal ‚Üí cardinal ‚Üí Prop} (c‚ÇÅ : cardinal) (c‚ÇÇ : cardinal)
  (h : ‚àÄ Œ± Œ≤, p (#Œ±) (#Œ≤)) : p c‚ÇÅ c‚ÇÇ :=
quotient.induction_on‚ÇÇ c‚ÇÅ c‚ÇÇ h

@[elab_as_eliminator]
lemma induction_on‚ÇÉ {p : cardinal ‚Üí cardinal ‚Üí cardinal ‚Üí Prop} (c‚ÇÅ : cardinal) (c‚ÇÇ : cardinal)
  (c‚ÇÉ : cardinal) (h : ‚àÄ Œ± Œ≤ Œ≥, p (#Œ±) (#Œ≤) (#Œ≥)) : p c‚ÇÅ c‚ÇÇ c‚ÇÉ :=
quotient.induction_on‚ÇÉ c‚ÇÅ c‚ÇÇ c‚ÇÉ h

protected lemma eq : #Œ± = #Œ≤ ‚Üî nonempty (Œ± ‚âÉ Œ≤) := quotient.eq

@[simp] theorem mk_def (Œ± : Type u) : @eq cardinal ‚ü¶Œ±‚üß (#Œ±) := rfl

@[simp] theorem mk_out (c : cardinal) : #(c.out) = c := quotient.out_eq _

/-- The representative of the cardinal of a type is equivalent ot the original type. -/
def out_mk_equiv {Œ± : Type v} : (#Œ±).out ‚âÉ Œ± :=
nonempty.some $ cardinal.eq.mp (by simp)

lemma mk_congr (e : Œ± ‚âÉ Œ≤) : # Œ± = # Œ≤ := quot.sound ‚ü®e‚ü©

alias mk_congr ‚Üê equiv.cardinal_eq

/-- Lift a function between `Type*`s to a function between `cardinal`s. -/
def map (f : Type u ‚Üí Type v) (hf : ‚àÄ Œ± Œ≤, Œ± ‚âÉ Œ≤ ‚Üí f Œ± ‚âÉ f Œ≤) :
  cardinal.{u} ‚Üí cardinal.{v} :=
quotient.map f (Œª Œ± Œ≤ ‚ü®e‚ü©, ‚ü®hf Œ± Œ≤ e‚ü©)

@[simp] lemma map_mk (f : Type u ‚Üí Type v) (hf : ‚àÄ Œ± Œ≤, Œ± ‚âÉ Œ≤ ‚Üí f Œ± ‚âÉ f Œ≤) (Œ± : Type u) :
  map f hf (#Œ±) = #(f Œ±) := rfl

/-- Lift a binary operation `Type* ‚Üí Type* ‚Üí Type*` to a binary operation on `cardinal`s. -/
def map‚ÇÇ (f : Type u ‚Üí Type v ‚Üí Type w) (hf : ‚àÄ Œ± Œ≤ Œ≥ Œ¥, Œ± ‚âÉ Œ≤ ‚Üí Œ≥ ‚âÉ Œ¥ ‚Üí f Œ± Œ≥ ‚âÉ f Œ≤ Œ¥) :
  cardinal.{u} ‚Üí cardinal.{v} ‚Üí cardinal.{w} :=
quotient.map‚ÇÇ f $ Œª Œ± Œ≤ ‚ü®e‚ÇÅ‚ü© Œ≥ Œ¥ ‚ü®e‚ÇÇ‚ü©, ‚ü®hf Œ± Œ≤ Œ≥ Œ¥ e‚ÇÅ e‚ÇÇ‚ü©

/-- The universe lift operation on cardinals. You can specify the universes explicitly with
  `lift.{u v} : cardinal.{v} ‚Üí cardinal.{max v u}` -/
def lift (c : cardinal.{v}) : cardinal.{max v u} :=
map ulift (Œª Œ± Œ≤ e, equiv.ulift.trans $ e.trans equiv.ulift.symm) c

@[simp] theorem mk_ulift (Œ±) : #(ulift.{v u} Œ±) = lift.{v} (#Œ±) := rfl

theorem lift_umax : lift.{(max u v) u} = lift.{v u} :=
funext $ Œª a, induction_on a $ Œª Œ±, (equiv.ulift.trans equiv.ulift.symm).cardinal_eq

theorem lift_umax' : lift.{(max v u) u} = lift.{v u} := lift_umax

theorem lift_id' (a : cardinal.{max u v}) : lift.{u} a = a :=
induction_on a $ Œª Œ±, mk_congr equiv.ulift

@[simp] theorem lift_id (a : cardinal) : lift.{u u} a = a := lift_id'.{u u} a
@[simp] theorem lift_uzero (a : cardinal.{u}) : lift.{0} a = a := lift_id'.{0 u} a

@[simp] theorem lift_lift (a : cardinal) :
  lift.{w} (lift.{v} a) = lift.{(max v w)} a :=
induction_on a $ Œª Œ±,
(equiv.ulift.trans $ equiv.ulift.trans equiv.ulift.symm).cardinal_eq

/-- We define the order on cardinal numbers by `#Œ± ‚â§ #Œ≤` if and only if
  there exists an embedding (injective function) from Œ± to Œ≤. -/
instance : has_le cardinal.{u} :=
‚ü®Œªq‚ÇÅ q‚ÇÇ, quotient.lift_on‚ÇÇ q‚ÇÅ q‚ÇÇ (ŒªŒ± Œ≤, nonempty $ Œ± ‚Ü™ Œ≤) $
  assume Œ± Œ≤ Œ≥ Œ¥ ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü©,
    propext ‚ü®assume ‚ü®e‚ü©, ‚ü®e.congr e‚ÇÅ e‚ÇÇ‚ü©, assume ‚ü®e‚ü©, ‚ü®e.congr e‚ÇÅ.symm e‚ÇÇ.symm‚ü©‚ü©‚ü©

theorem le_def (Œ± Œ≤ : Type u) : #Œ± ‚â§ #Œ≤ ‚Üî nonempty (Œ± ‚Ü™ Œ≤) :=
iff.rfl

theorem mk_le_of_injective {Œ± Œ≤ : Type u} {f : Œ± ‚Üí Œ≤} (hf : injective f) : #Œ± ‚â§ #Œ≤ :=
‚ü®‚ü®f, hf‚ü©‚ü©

theorem _root_.function.embedding.cardinal_le {Œ± Œ≤ : Type u} (f : Œ± ‚Ü™ Œ≤) : #Œ± ‚â§ #Œ≤ := ‚ü®f‚ü©

theorem mk_le_of_surjective {Œ± Œ≤ : Type u} {f : Œ± ‚Üí Œ≤} (hf : surjective f) : #Œ≤ ‚â§ #Œ± :=
‚ü®embedding.of_surjective f hf‚ü©

theorem le_mk_iff_exists_set {c : cardinal} {Œ± : Type u} :
  c ‚â§ #Œ± ‚Üî ‚àÉ p : set Œ±, #p = c :=
‚ü®induction_on c $ Œª Œ≤ ‚ü®‚ü®f, hf‚ü©‚ü©,
  ‚ü®set.range f, (equiv.of_injective f hf).cardinal_eq.symm‚ü©,
Œª ‚ü®p, e‚ü©, e ‚ñ∏ ‚ü®‚ü®subtype.val, Œª a b, subtype.eq‚ü©‚ü©‚ü©

theorem mk_subtype_le {Œ± : Type u} (p : Œ± ‚Üí Prop) : #(subtype p) ‚â§ #Œ± :=
‚ü®embedding.subtype p‚ü©

theorem mk_set_le (s : set Œ±) : #s ‚â§ #Œ± :=
mk_subtype_le s

theorem out_embedding {c c' : cardinal} : c ‚â§ c' ‚Üî nonempty (c.out ‚Ü™ c'.out) :=
by { transitivity _, rw [‚Üêquotient.out_eq c, ‚Üêquotient.out_eq c'], refl }

instance : preorder cardinal.{u} :=
{ le          := (‚â§),
  le_refl     := by rintros ‚ü®Œ±‚ü©; exact ‚ü®embedding.refl _‚ü©,
  le_trans    := by rintros ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü© ‚ü®Œ≥‚ü© ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü©; exact ‚ü®e‚ÇÅ.trans e‚ÇÇ‚ü© }

instance : partial_order cardinal.{u} :=
{ le_antisymm := by { rintros ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü© ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü©, exact quotient.sound (e‚ÇÅ.antisymm e‚ÇÇ) },
  .. cardinal.preorder }

theorem lift_mk_le {Œ± : Type u} {Œ≤ : Type v} :
  lift.{(max v w)} (#Œ±) ‚â§ lift.{(max u w)} (#Œ≤) ‚Üî nonempty (Œ± ‚Ü™ Œ≤) :=
‚ü®Œª ‚ü®f‚ü©, ‚ü®embedding.congr equiv.ulift equiv.ulift f‚ü©,
 Œª ‚ü®f‚ü©, ‚ü®embedding.congr equiv.ulift.symm equiv.ulift.symm f‚ü©‚ü©

/-- A variant of `cardinal.lift_mk_le` with specialized universes.
Because Lean often can not realize it should use this specialization itself,
we provide this statement separately so you don't have to solve the specialization problem either.
-/
theorem lift_mk_le' {Œ± : Type u} {Œ≤ : Type v} :
  lift.{v} (#Œ±) ‚â§ lift.{u} (#Œ≤) ‚Üî nonempty (Œ± ‚Ü™ Œ≤) :=
lift_mk_le.{u v 0}

theorem lift_mk_eq {Œ± : Type u} {Œ≤ : Type v} :
  lift.{(max v w)} (#Œ±) = lift.{(max u w)} (#Œ≤) ‚Üî nonempty (Œ± ‚âÉ Œ≤) :=
quotient.eq.trans
‚ü®Œª ‚ü®f‚ü©, ‚ü®equiv.ulift.symm.trans $ f.trans equiv.ulift‚ü©,
 Œª ‚ü®f‚ü©, ‚ü®equiv.ulift.trans $ f.trans equiv.ulift.symm‚ü©‚ü©

/-- A variant of `cardinal.lift_mk_eq` with specialized universes.
Because Lean often can not realize it should use this specialization itself,
we provide this statement separately so you don't have to solve the specialization problem either.
-/
theorem lift_mk_eq' {Œ± : Type u} {Œ≤ : Type v} :
  lift.{v} (#Œ±) = lift.{u} (#Œ≤) ‚Üî nonempty (Œ± ‚âÉ Œ≤) :=
lift_mk_eq.{u v 0}

@[simp] theorem lift_le {a b : cardinal} : lift a ‚â§ lift b ‚Üî a ‚â§ b :=
induction_on‚ÇÇ a b $ Œª Œ± Œ≤, by { rw ‚Üê lift_umax, exact lift_mk_le }

/-- `cardinal.lift` as an `order_embedding`. -/
@[simps { fully_applied := ff }] def lift_order_embedding : cardinal.{v} ‚Ü™o cardinal.{max v u} :=
order_embedding.of_map_le_iff lift (Œª _ _, lift_le)

theorem lift_injective : injective lift.{u v} := lift_order_embedding.injective

@[simp] theorem lift_inj {a b : cardinal} : lift a = lift b ‚Üî a = b :=
lift_injective.eq_iff

@[simp] theorem lift_lt {a b : cardinal} : lift a < lift b ‚Üî a < b :=
lift_order_embedding.lt_iff_lt

instance : has_zero cardinal.{u} := ‚ü®#pempty‚ü©

instance : inhabited cardinal.{u} := ‚ü®0‚ü©

lemma mk_eq_zero (Œ± : Type u) [is_empty Œ±] : #Œ± = 0 :=
(equiv.equiv_pempty Œ±).cardinal_eq

@[simp] theorem lift_zero : lift 0 = 0 := mk_congr (equiv.equiv_pempty _)

@[simp] theorem lift_eq_zero {a : cardinal.{v}} : lift.{u} a = 0 ‚Üî a = 0 :=
lift_injective.eq_iff' lift_zero

lemma mk_eq_zero_iff {Œ± : Type u} : #Œ± = 0 ‚Üî is_empty Œ± :=
‚ü®Œª e, let ‚ü®h‚ü© := quotient.exact e in h.is_empty, @mk_eq_zero Œ±‚ü©

theorem mk_ne_zero_iff {Œ± : Type u} : #Œ± ‚â† 0 ‚Üî nonempty Œ± :=
(not_iff_not.2 mk_eq_zero_iff).trans not_is_empty_iff

@[simp] lemma mk_ne_zero (Œ± : Type u) [nonempty Œ±] : #Œ± ‚â† 0 := mk_ne_zero_iff.2 ‚Äπ_‚Ä∫

instance : has_one cardinal.{u} := ‚ü®‚ü¶punit‚üß‚ü©

instance : nontrivial cardinal.{u} := ‚ü®‚ü®1, 0, mk_ne_zero _‚ü©‚ü©

lemma mk_eq_one (Œ± : Type u) [unique Œ±] : #Œ± = 1 :=
mk_congr equiv_punit_of_unique

theorem le_one_iff_subsingleton {Œ± : Type u} : #Œ± ‚â§ 1 ‚Üî subsingleton Œ± :=
‚ü®Œª ‚ü®f‚ü©, ‚ü®Œª a b, f.injective (subsingleton.elim _ _)‚ü©,
 Œª ‚ü®h‚ü©, ‚ü®‚ü®Œª a, punit.star, Œª a b _, h _ _‚ü©‚ü©‚ü©

instance : has_add cardinal.{u} := ‚ü®map‚ÇÇ sum $ Œª Œ± Œ≤ Œ≥ Œ¥, equiv.sum_congr‚ü©

theorem add_def (Œ± Œ≤ : Type u) : #Œ± + #Œ≤ = #(Œ± ‚äï Œ≤) := rfl

@[simp] lemma mk_sum (Œ± : Type u) (Œ≤ : Type v) :
  #(Œ± ‚äï Œ≤) = lift.{v u} (#Œ±) + lift.{u v} (#Œ≤) :=
mk_congr ((equiv.ulift).symm.sum_congr (equiv.ulift).symm)

@[simp] theorem mk_option {Œ± : Type u} : #(option Œ±) = #Œ± + 1 :=
(equiv.option_equiv_sum_punit Œ±).cardinal_eq

@[simp] lemma mk_psum (Œ± : Type u) (Œ≤ : Type v) : #(psum Œ± Œ≤) = lift.{v} (#Œ±) + lift.{u} (#Œ≤) :=
(mk_congr (equiv.psum_equiv_sum Œ± Œ≤)).trans (mk_sum Œ± Œ≤)

@[simp] lemma mk_fintype (Œ± : Type u) [fintype Œ±] : #Œ± = fintype.card Œ± :=
begin
  refine fintype.induction_empty_option' _ _ _ Œ±,
  { introsI Œ± Œ≤ h e hŒ±, letI := fintype.of_equiv Œ≤ e.symm,
    rwa [mk_congr e, fintype.card_congr e] at hŒ± },
  { refl },
  { introsI Œ± h hŒ±, simp [hŒ±] }
end

instance : has_mul cardinal.{u} := ‚ü®map‚ÇÇ prod $ Œª Œ± Œ≤ Œ≥ Œ¥, equiv.prod_congr‚ü©

theorem mul_def (Œ± Œ≤ : Type u) : #Œ± * #Œ≤ = #(Œ± √ó Œ≤) := rfl

@[simp] lemma mk_prod (Œ± : Type u) (Œ≤ : Type v) :
  #(Œ± √ó Œ≤) = lift.{v u} (#Œ±) * lift.{u v} (#Œ≤) :=
mk_congr (equiv.ulift.symm.prod_congr (equiv.ulift).symm)

protected theorem add_comm (a b : cardinal.{u}) : a + b = b + a :=
induction_on‚ÇÇ a b $ Œª Œ± Œ≤, mk_congr (equiv.sum_comm Œ± Œ≤)

protected theorem mul_comm (a b : cardinal.{u}) : a * b = b * a :=
induction_on‚ÇÇ a b $ Œª Œ± Œ≤, mk_congr (equiv.prod_comm Œ± Œ≤)

protected theorem zero_add (a : cardinal.{u}) : 0 + a = a :=
induction_on a $ Œª Œ±, mk_congr (equiv.empty_sum pempty Œ±)

protected theorem zero_mul (a : cardinal.{u}) : 0 * a = 0 :=
induction_on a $ Œª Œ±, mk_congr (equiv.pempty_prod Œ±)

protected theorem one_mul (a : cardinal.{u}) : 1 * a = a :=
induction_on a $ Œª Œ±, mk_congr (equiv.punit_prod Œ±)

protected theorem left_distrib (a b c : cardinal.{u}) : a * (b + c) = a * b + a * c :=
induction_on‚ÇÉ a b c $ Œª Œ± Œ≤ Œ≥, mk_congr (equiv.prod_sum_distrib Œ± Œ≤ Œ≥)

protected theorem eq_zero_or_eq_zero_of_mul_eq_zero {a b : cardinal.{u}} :
  a * b = 0 ‚Üí a = 0 ‚à® b = 0 :=
begin
  induction a using cardinal.induction_on with Œ±,
  induction b using cardinal.induction_on with Œ≤,
  simp only [mul_def, mk_eq_zero_iff, is_empty_prod],
  exact id
end

/-- The cardinal exponential. `#Œ± ^ #Œ≤` is the cardinal of `Œ≤ ‚Üí Œ±`. -/
protected def power (a b : cardinal.{u}) : cardinal.{u} :=
map‚ÇÇ (Œª Œ± Œ≤ : Type u, Œ≤ ‚Üí Œ±) (Œª Œ± Œ≤ Œ≥ Œ¥ e‚ÇÅ e‚ÇÇ, e‚ÇÇ.arrow_congr e‚ÇÅ) a b

instance : has_pow cardinal cardinal := ‚ü®cardinal.power‚ü©
local infixr ^ := @has_pow.pow cardinal cardinal cardinal.has_pow
local infixr ` ^‚Ñï `:80 := @has_pow.pow cardinal ‚Ñï monoid.has_pow

theorem power_def (Œ± Œ≤) : #Œ± ^ #Œ≤ = #(Œ≤ ‚Üí Œ±) := rfl

theorem mk_arrow (Œ± : Type u) (Œ≤ : Type v) : #(Œ± ‚Üí Œ≤) = lift.{u} (#Œ≤) ^ lift.{v} (#Œ±) :=
mk_congr (equiv.ulift.symm.arrow_congr equiv.ulift.symm)

@[simp] theorem lift_power (a b) : lift (a ^ b) = lift a ^ lift b :=
induction_on‚ÇÇ a b $ Œª Œ± Œ≤,
mk_congr (equiv.ulift.trans (equiv.ulift.arrow_congr equiv.ulift).symm)

@[simp] theorem power_zero {a : cardinal} : a ^ 0 = 1 :=
induction_on a $ assume Œ±, (equiv.pempty_arrow_equiv_punit Œ±).cardinal_eq

@[simp] theorem power_one {a : cardinal} : a ^ 1 = a :=
induction_on a $ assume Œ±, (equiv.punit_arrow_equiv Œ±).cardinal_eq

theorem power_add {a b c : cardinal} : a ^ (b + c) = a ^ b * a ^ c :=
induction_on‚ÇÉ a b c $ assume Œ± Œ≤ Œ≥, (equiv.sum_arrow_equiv_prod_arrow Œ≤ Œ≥ Œ±).cardinal_eq

instance : comm_semiring cardinal.{u} :=
{ zero          := 0,
  one           := 1,
  add           := (+),
  mul           := (*),
  zero_add      := cardinal.zero_add,
  add_zero      := assume a, by rw [cardinal.add_comm a 0, cardinal.zero_add a],
  add_assoc     := Œªa b c, induction_on‚ÇÉ a b c $ assume Œ± Œ≤ Œ≥, mk_congr (equiv.sum_assoc Œ± Œ≤ Œ≥),
  add_comm      := cardinal.add_comm,
  zero_mul      := cardinal.zero_mul,
  mul_zero      := assume a, by rw [cardinal.mul_comm a 0, cardinal.zero_mul a],
  one_mul       := cardinal.one_mul,
  mul_one       := assume a, by rw [cardinal.mul_comm a 1, cardinal.one_mul a],
  mul_assoc     := Œªa b c, induction_on‚ÇÉ a b c $ assume Œ± Œ≤ Œ≥, mk_congr (equiv.prod_assoc Œ± Œ≤ Œ≥),
  mul_comm      := cardinal.mul_comm,
  left_distrib  := cardinal.left_distrib,
  right_distrib := assume a b c, by rw [cardinal.mul_comm (a + b) c, cardinal.left_distrib c a b,
    cardinal.mul_comm c a, cardinal.mul_comm c b],
  npow          := Œª n c, c ^ n,
  npow_zero'    := @power_zero,
  npow_succ'    := Œª n c, by rw [nat.cast_succ, power_add, power_one, cardinal.mul_comm] }

theorem power_bit0 (a b : cardinal) : a ^ (bit0 b) = a ^ b * a ^ b :=
power_add

theorem power_bit1 (a b : cardinal) : a ^ (bit1 b) = a ^ b * a ^ b * a :=
by rw [bit1, ‚Üêpower_bit0, power_add, power_one]

@[simp] theorem one_power {a : cardinal} : 1 ^ a = 1 :=
induction_on a $ assume Œ±, (equiv.arrow_punit_equiv_punit Œ±).cardinal_eq

@[simp] theorem mk_bool : #bool = 2 := by simp

@[simp] theorem mk_Prop : #(Prop) = 2 := by simp

@[simp] theorem zero_power {a : cardinal} : a ‚â† 0 ‚Üí 0 ^ a = 0 :=
induction_on a $ assume Œ± heq, mk_eq_zero_iff.2 $ is_empty_pi.2 $
let ‚ü®a‚ü© := mk_ne_zero_iff.1 heq in ‚ü®a, pempty.is_empty‚ü©

theorem power_ne_zero {a : cardinal} (b) : a ‚â† 0 ‚Üí a ^ b ‚â† 0 :=
induction_on‚ÇÇ a b $ Œª Œ± Œ≤ h,
let ‚ü®a‚ü© := mk_ne_zero_iff.1 h in mk_ne_zero_iff.2 ‚ü®Œª _, a‚ü©

theorem mul_power {a b c : cardinal} : (a * b) ^ c = a ^ c * b ^ c :=
induction_on‚ÇÉ a b c $ assume Œ± Œ≤ Œ≥, (equiv.arrow_prod_equiv_prod_arrow Œ± Œ≤ Œ≥).cardinal_eq

theorem power_mul {a b c : cardinal} : a ^ (b * c) = (a ^ b) ^ c :=
by rw [mul_comm b c];
from (induction_on‚ÇÉ a b c $ assume Œ± Œ≤ Œ≥, mk_congr (equiv.curry Œ≥ Œ≤ Œ±))

@[simp] lemma pow_cast_right (Œ∫ : cardinal.{u}) (n : ‚Ñï) :
  (Œ∫ ^ (‚Üën : cardinal.{u})) = Œ∫ ^‚Ñï n :=
rfl

@[simp] theorem lift_one : lift 1 = 1 :=
mk_congr (equiv.ulift.trans equiv.punit_equiv_punit)

@[simp] theorem lift_add (a b) : lift (a + b) = lift a + lift b :=
induction_on‚ÇÇ a b $ Œª Œ± Œ≤,
mk_congr (equiv.ulift.trans (equiv.sum_congr equiv.ulift equiv.ulift).symm)

@[simp] theorem lift_mul (a b) : lift (a * b) = lift a * lift b :=
induction_on‚ÇÇ a b $ Œª Œ± Œ≤,
mk_congr (equiv.ulift.trans (equiv.prod_congr equiv.ulift equiv.ulift).symm)

@[simp] theorem lift_bit0 (a : cardinal) : lift (bit0 a) = bit0 (lift a) :=
lift_add a a

@[simp] theorem lift_bit1 (a : cardinal) : lift (bit1 a) = bit1 (lift a) :=
by simp [bit1]

theorem lift_two : lift.{u v} 2 = 2 := by simp

@[simp] theorem mk_set {Œ± : Type u} : #(set Œ±) = 2 ^ #Œ± := by simp [set, mk_arrow]

/-- A variant of `cardinal.mk_set` expressed in terms of a `set` instead of a `Type`. -/
@[simp] theorem mk_powerset {Œ± : Type u} (s : set Œ±) : #‚Ü•(ùí´ s) = 2 ^ #‚Ü•s :=
(mk_congr (equiv.set.powerset s)).trans mk_set

theorem lift_two_power (a) : lift (2 ^ a) = 2 ^ lift a := by simp

section order_properties
open sum

protected theorem zero_le : ‚àÄ(a : cardinal), 0 ‚â§ a :=
by rintro ‚ü®Œ±‚ü©; exact ‚ü®embedding.of_is_empty‚ü©

protected theorem add_le_add : ‚àÄ{a b c d : cardinal}, a ‚â§ b ‚Üí c ‚â§ d ‚Üí a + c ‚â§ b + d :=
by rintros ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü© ‚ü®Œ≥‚ü© ‚ü®Œ¥‚ü© ‚ü®e‚ÇÅ‚ü© ‚ü®e‚ÇÇ‚ü©; exact ‚ü®e‚ÇÅ.sum_map e‚ÇÇ‚ü©

protected theorem add_le_add_left (a) {b c : cardinal} : b ‚â§ c ‚Üí a + b ‚â§ a + c :=
cardinal.add_le_add le_rfl

protected theorem le_iff_exists_add {a b : cardinal} : a ‚â§ b ‚Üî ‚àÉ c, b = a + c :=
‚ü®induction_on‚ÇÇ a b $ Œª Œ± Œ≤ ‚ü®‚ü®f, hf‚ü©‚ü©,
  have (Œ± ‚äï ((range f)·∂ú : set Œ≤)) ‚âÉ Œ≤, from
    (equiv.sum_congr (equiv.of_injective f hf) (equiv.refl _)).trans $
    (equiv.set.sum_compl (range f)),
  ‚ü®#‚Ü•(range f)·∂ú, mk_congr this.symm‚ü©,
 Œª ‚ü®c, e‚ü©, add_zero a ‚ñ∏ e.symm ‚ñ∏ cardinal.add_le_add_left _ (cardinal.zero_le _)‚ü©

instance : order_bot cardinal.{u} :=
{ bot := 0, bot_le := cardinal.zero_le }

instance : canonically_ordered_comm_semiring cardinal.{u} :=
{ add_le_add_left       := Œª a b h c, cardinal.add_le_add_left _ h,
  le_iff_exists_add     := @cardinal.le_iff_exists_add,
  eq_zero_or_eq_zero_of_mul_eq_zero := @cardinal.eq_zero_or_eq_zero_of_mul_eq_zero,
  ..cardinal.order_bot,
  ..cardinal.comm_semiring, ..cardinal.partial_order }

@[simp] theorem zero_lt_one : (0 : cardinal) < 1 :=
lt_of_le_of_ne (zero_le _) zero_ne_one

lemma zero_power_le (c : cardinal.{u}) : (0 : cardinal.{u}) ^ c ‚â§ 1 :=
by { by_cases h : c = 0, rw [h, power_zero], rw [zero_power h], apply zero_le }

theorem power_le_power_left : ‚àÄ {a b c : cardinal}, a ‚â† 0 ‚Üí b ‚â§ c ‚Üí a ^ b ‚â§ a ^ c :=
by rintros ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü© ‚ü®Œ≥‚ü© hŒ± ‚ü®e‚ü©; exact
  let ‚ü®a‚ü© := mk_ne_zero_iff.1 hŒ± in
  ‚ü®@embedding.arrow_congr_left _ _ _ ‚ü®a‚ü© e‚ü©

theorem self_le_power (a : cardinal) {b : cardinal} (hb : 1 ‚â§ b) : a ‚â§ a ^ b :=
begin
  rcases eq_or_ne a 0 with rfl|ha,
  { exact zero_le _ },
  { convert power_le_power_left ha hb, exact power_one.symm }
end

/-- **Cantor's theorem** -/
theorem cantor (a : cardinal.{u}) : a < 2 ^ a :=
begin
  induction a using cardinal.induction_on with Œ±,
  rw [‚Üê mk_set],
  refine ‚ü®‚ü®‚ü®singleton, Œª a b, singleton_eq_singleton_iff.1‚ü©‚ü©, _‚ü©,
  rintro ‚ü®‚ü®f, hf‚ü©‚ü©,
  exact cantor_injective f hf
end

instance : no_max_order cardinal.{u} :=
{ exists_gt := Œª a, ‚ü®_, cantor a‚ü©, ..cardinal.partial_order }

instance : linear_order cardinal.{u} :=
{ le_total    := by rintros ‚ü®Œ±‚ü© ‚ü®Œ≤‚ü©; exact embedding.total,
  decidable_le := classical.dec_rel _,
  .. cardinal.partial_order }

instance : canonically_linear_ordered_add_monoid cardinal.{u} :=
{ .. (infer_instance : canonically_ordered_add_monoid cardinal.{u}),
  .. cardinal.linear_order }

-- short-circuit type class inference
instance : distrib_lattice cardinal.{u} := by apply_instance

theorem one_lt_iff_nontrivial {Œ± : Type u} : 1 < #Œ± ‚Üî nontrivial Œ± :=
by rw [‚Üê not_le, le_one_iff_subsingleton, ‚Üê not_nontrivial_iff_subsingleton, not_not]

theorem power_le_max_power_one {a b c : cardinal} (h : b ‚â§ c) : a ^ b ‚â§ max (a ^ c) 1 :=
begin
  by_cases ha : a = 0,
  simp [ha, zero_power_le],
  exact le_trans (power_le_power_left ha h) (le_max_left _ _)
end

theorem power_le_power_right {a b c : cardinal} : a ‚â§ b ‚Üí a ^ c ‚â§ b ^ c :=
induction_on‚ÇÉ a b c $ assume Œ± Œ≤ Œ≥ ‚ü®e‚ü©, ‚ü®embedding.arrow_congr_right e‚ü©

end order_properties

/-- The minimum cardinal in a family of cardinals (the existence
  of which is provided by `min_injective`). -/
protected def min {Œπ} (I : nonempty Œπ) (f : Œπ ‚Üí cardinal) : cardinal :=
f $ classical.some $ @embedding.min_injective _ (Œª i, (f i).out) I

theorem min_eq {Œπ} (I) (f : Œπ ‚Üí cardinal) : ‚àÉ i, cardinal.min I f = f i :=
‚ü®_, rfl‚ü©

theorem min_le {Œπ I} (f : Œπ ‚Üí cardinal) (i) : cardinal.min I f ‚â§ f i :=
by rw [‚Üê mk_out (cardinal.min I f), ‚Üê mk_out (f i)]; exact
let ‚ü®g‚ü© := classical.some_spec
  (@embedding.min_injective _ (Œª i, (f i).out) I) in
‚ü®g i‚ü©

theorem le_min {Œπ I} {f : Œπ ‚Üí cardinal} {a} : a ‚â§ cardinal.min I f ‚Üî ‚àÄ i, a ‚â§ f i :=
‚ü®Œª h i, le_trans h (min_le _ _),
 Œª h, let ‚ü®i, e‚ü© := min_eq I f in e.symm ‚ñ∏ h i‚ü©

protected theorem wf : @well_founded cardinal.{u} (<) :=
‚ü®Œª a, classical.by_contradiction $ Œª h,
  let Œπ := {c :cardinal // ¬¨ acc (<) c},
      f : Œπ ‚Üí cardinal := subtype.val,
      ‚ü®‚ü®c, hc‚ü©, hi‚ü© := @min_eq Œπ ‚ü®‚ü®_, h‚ü©‚ü© f in
    hc (acc.intro _ (Œª j ‚ü®_, h'‚ü©,
      classical.by_contradiction $ Œª hj, h' $
      by have := min_le f ‚ü®j, hj‚ü©; rwa hi at this))‚ü©

instance has_wf : @has_well_founded cardinal.{u} := ‚ü®(<), cardinal.wf‚ü©

instance : conditionally_complete_linear_order_bot cardinal :=
cardinal.wf.conditionally_complete_linear_order_with_bot 0 $ le_antisymm (cardinal.zero_le _) $
  not_lt.1 (cardinal.wf.not_lt_min set.univ ‚ü®0, mem_univ _‚ü© (mem_univ 0))

instance wo : @is_well_order cardinal.{u} (<) := ‚ü®cardinal.wf‚ü©

/-- The successor cardinal - the smallest cardinal greater than
  `c`. This is not the same as `c + 1` except in the case of finite `c`. -/
def succ (c : cardinal) : cardinal :=
Inf {c' | c < c'}

theorem succ_nonempty (c : cardinal) : {c' : cardinal | c < c'}.nonempty :=
‚ü®_, cantor _‚ü©

theorem lt_succ_self (c : cardinal) : c < succ c :=
Inf_mem (succ_nonempty c)

theorem succ_le {a b : cardinal} : succ a ‚â§ b ‚Üî a < b :=
‚ü®lt_of_lt_of_le (lt_succ_self _), Œª h, cInf_le' h‚ü©

@[simp] theorem lt_succ {a b : cardinal} : a < succ b ‚Üî a ‚â§ b :=
by rw [‚Üê not_le, succ_le, not_lt]

theorem add_one_le_succ (c : cardinal.{u}) : c + 1 ‚â§ succ c :=
begin
  refine (le_cInf_iff'' (succ_nonempty c)).2 (Œª b hlt, _),
  rcases ‚ü®b, c‚ü© with ‚ü®‚ü®Œ≤‚ü©, ‚ü®Œ≥‚ü©‚ü©,
  cases le_of_lt hlt with f,
  have : ¬¨ surjective f := Œª hn, (not_le_of_lt hlt) (mk_le_of_surjective hn),
  simp only [surjective, not_forall] at this,
  rcases this with ‚ü®b, hb‚ü©,
  calc #Œ≥ + 1 = #(option Œ≥) : mk_option.symm
          ... ‚â§ #Œ≤          : (f.option_elim b hb).cardinal_le
end

lemma succ_pos (c : cardinal) : 0 < succ c := by simp

lemma succ_ne_zero (c : cardinal) : succ c ‚â† 0 := (succ_pos _).ne'

/-- The indexed sum of cardinals is the cardinality of the
  indexed disjoint union, i.e. sigma type. -/
def sum {Œπ} (f : Œπ ‚Üí cardinal) : cardinal := mk Œ£ i, (f i).out

theorem le_sum {Œπ} (f : Œπ ‚Üí cardinal) (i) : f i ‚â§ sum f :=
by rw ‚Üê quotient.out_eq (f i); exact
‚ü®‚ü®Œª a, ‚ü®i, a‚ü©, Œª a b h, eq_of_heq $ by injection h‚ü©‚ü©

@[simp] theorem mk_sigma {Œπ} (f : Œπ ‚Üí Type*) : #(Œ£ i, f i) = sum (Œª i, #(f i)) :=
mk_congr $ equiv.sigma_congr_right $ Œª i, out_mk_equiv.symm

@[simp] theorem sum_const (Œπ : Type u) (a : cardinal.{v}) :
  sum (Œª i : Œπ, a) = lift.{v} (#Œπ) * lift.{u} a :=
induction_on a $ Œª Œ±, mk_congr $
  calc (Œ£ i : Œπ, quotient.out (#Œ±)) ‚âÉ Œπ √ó quotient.out (#Œ±) : equiv.sigma_equiv_prod _ _
  ... ‚âÉ ulift Œπ √ó ulift Œ± : equiv.ulift.symm.prod_congr (out_mk_equiv.trans equiv.ulift.symm)

theorem sum_const' (Œπ : Type u) (a : cardinal.{u}) : sum (Œª _:Œπ, a) = #Œπ * a := by simp

@[simp] theorem sum_add_distrib {Œπ} (f g : Œπ ‚Üí cardinal) :
  sum (f + g) = sum f + sum g :=
by simpa only [mk_sigma, mk_sum, mk_out, lift_id] using
  mk_congr (equiv.sigma_sum_distrib (quotient.out ‚àò f) (quotient.out ‚àò g))

@[simp] theorem sum_add_distrib' {Œπ} (f g : Œπ ‚Üí cardinal) :
  cardinal.sum (Œª i, f i + g i) = sum f + sum g :=
sum_add_distrib f g

@[simp] theorem lift_sum {Œπ : Type u} (f : Œπ ‚Üí cardinal.{v}) :
  cardinal.lift.{w} (cardinal.sum f) = cardinal.sum (Œª i, cardinal.lift.{w} (f i)) :=
equiv.cardinal_eq $ equiv.ulift.trans $ equiv.sigma_congr_right $ Œª a, nonempty.some $
  by rw [‚Üêlift_mk_eq, mk_out, mk_out, lift_lift]

theorem sum_le_sum {Œπ} (f g : Œπ ‚Üí cardinal) (H : ‚àÄ i, f i ‚â§ g i) : sum f ‚â§ sum g :=
‚ü®(embedding.refl _).sigma_map $ Œª i, classical.choice $
  by have := H i; rwa [‚Üê quot.out_eq (f i), ‚Üê quot.out_eq (g i)] at this‚ü©

lemma mk_le_mk_mul_of_mk_preimage_le {c : cardinal} (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ b : Œ≤, #(f ‚Åª¬π' {b}) ‚â§ c) :
  #Œ± ‚â§ #Œ≤ * c :=
by simpa only [‚Üêmk_congr (@equiv.sigma_fiber_equiv Œ± Œ≤ f), mk_sigma, ‚Üêsum_const']
  using sum_le_sum _ _ hf

/-- The range of an indexed cardinal function, whose outputs live in a higher universe than the
    inputs, is always bounded above. -/
theorem bdd_above_range {Œπ : Type u} (f : Œπ ‚Üí cardinal.{max u v}) : bdd_above (set.range f) :=
‚ü®_, by { rintros a ‚ü®i, rfl‚ü©, exact le_sum f i }‚ü©

instance (a : cardinal.{u}) : small.{u} (set.Iic a) :=
begin
  rw ‚Üêmk_out a,
  apply @small_of_surjective (set a.out) (Iic (#a.out)) _ (Œª x, ‚ü®#x, mk_set_le x‚ü©),
  rintro ‚ü®x, hx‚ü©,
  simpa using le_mk_iff_exists_set.1 hx
end

/-- A set of cardinals is bounded above iff it's small, i.e. it corresponds to an usual ZFC set. -/
theorem bdd_above_iff_small (s : set cardinal.{u}) : bdd_above s ‚Üî small.{u} s :=
‚ü®Œª ‚ü®a, ha‚ü©, @small_subset _ (Iic a) s (Œª x h, ha h) _, begin
  rintro ‚ü®Œπ, ‚ü®e‚ü©‚ü©,
  suffices : range (Œª x : Œπ, (e.symm x).1) = s,
  { rw ‚Üêthis,
    apply bdd_above_range.{u u} },
  ext x,
  refine ‚ü®_, Œª hx, ‚ü®e ‚ü®x, hx‚ü©, _‚ü©‚ü©,
  { rintro ‚ü®a, rfl‚ü©,
    exact (e.symm a).prop },
  { simp_rw [subtype.val_eq_coe, equiv.symm_apply_apply], refl }
end‚ü©

/-- The indexed supremum of cardinals is the smallest cardinal above
  everything in the family. -/
def sup {Œπ : Type u} (f : Œπ ‚Üí cardinal.{max u v}) : cardinal :=
Sup (set.range f)

theorem le_sup {Œπ} (f : Œπ ‚Üí cardinal.{max u v}) (i) : f i ‚â§ sup f :=
le_cSup (bdd_above_range f) (mem_range_self i)

theorem sup_le_iff {Œπ} {f : Œπ ‚Üí cardinal} {a} : sup f ‚â§ a ‚Üî ‚àÄ i, f i ‚â§ a :=
(cSup_le_iff' (bdd_above_range f)).trans (by simp)

theorem sup_le {Œπ} {f : Œπ ‚Üí cardinal} {a} : (‚àÄ i, f i ‚â§ a) ‚Üí sup f ‚â§ a :=
sup_le_iff.2

theorem sup_le_sup {Œπ} (f g : Œπ ‚Üí cardinal) (H : ‚àÄ i, f i ‚â§ g i) : sup f ‚â§ sup g :=
sup_le $ Œª i, le_trans (H i) (le_sup _ _)

theorem sup_le_sum {Œπ} (f : Œπ ‚Üí cardinal) : sup f ‚â§ sum f :=
sup_le $ le_sum _

theorem sum_le_sup {Œπ : Type u} (f : Œπ ‚Üí cardinal.{u}) : sum f ‚â§ #Œπ * sup.{u u} f :=
by rw ‚Üê sum_const'; exact sum_le_sum _ _ (le_sup _)

theorem sum_le_sup_lift {Œπ : Type u} (f : Œπ ‚Üí cardinal.{max u v}) :
  sum f ‚â§ (#Œπ).lift * sup.{u v} f :=
begin
  rw [‚Üê(sup f).lift_id, ‚Üêlift_umax, lift_umax.{(max u v) u}, ‚Üêsum_const],
  exact sum_le_sum _ _ (le_sup _)
end

theorem sum_nat_eq_add_sum_succ (f : ‚Ñï ‚Üí cardinal.{u}) :
  cardinal.sum f = f 0 + cardinal.sum (Œª i, f (i + 1)) :=
begin
  refine (equiv.sigma_nat_succ (Œª i, quotient.out (f i))).cardinal_eq.trans _,
  simp only [mk_sum, mk_out, lift_id, mk_sigma],
end

theorem sup_eq_zero {Œπ} {f : Œπ ‚Üí cardinal} [is_empty Œπ] : sup f = 0 :=
by { rw ‚Üênonpos_iff_eq_zero, exact sup_le is_empty_elim }

/-- The indexed product of cardinals is the cardinality of the Pi type
  (dependent product). -/
def prod {Œπ : Type u} (f : Œπ ‚Üí cardinal) : cardinal := #(Œ† i, (f i).out)

@[simp] theorem mk_pi {Œπ : Type u} (Œ± : Œπ ‚Üí Type v) : #(Œ† i, Œ± i) = prod (Œª i, #(Œ± i)) :=
mk_congr $ equiv.Pi_congr_right $ Œª i, out_mk_equiv.symm

@[simp] theorem prod_const (Œπ : Type u) (a : cardinal.{v}) :
  prod (Œª i : Œπ, a) = lift.{u} a ^ lift.{v} (#Œπ) :=
induction_on a $ Œª Œ±, mk_congr $ equiv.Pi_congr equiv.ulift.symm $
  Œª i, out_mk_equiv.trans equiv.ulift.symm

theorem prod_const' (Œπ : Type u) (a : cardinal.{u}) : prod (Œª _:Œπ, a) = a ^ #Œπ :=
induction_on a $ Œª Œ±, (mk_pi _).symm

theorem prod_le_prod {Œπ} (f g : Œπ ‚Üí cardinal) (H : ‚àÄ i, f i ‚â§ g i) : prod f ‚â§ prod g :=
‚ü®embedding.Pi_congr_right $ Œª i, classical.choice $
  by have := H i; rwa [‚Üê mk_out (f i), ‚Üê mk_out (g i)] at this‚ü©

@[simp] theorem prod_eq_zero {Œπ} (f : Œπ ‚Üí cardinal.{u}) : prod f = 0 ‚Üî ‚àÉ i, f i = 0 :=
by { lift f to Œπ ‚Üí Type u using Œª _, trivial, simp only [mk_eq_zero_iff, ‚Üê mk_pi, is_empty_pi] }

theorem prod_ne_zero {Œπ} (f : Œπ ‚Üí cardinal) : prod f ‚â† 0 ‚Üî ‚àÄ i, f i ‚â† 0 :=
by simp [prod_eq_zero]

@[simp] theorem lift_prod {Œπ : Type u} (c : Œπ ‚Üí cardinal.{v}) :
  lift.{w} (prod c) = prod (Œª i, lift.{w} (c i)) :=
begin
  lift c to Œπ ‚Üí Type v using Œª _, trivial,
  simp only [‚Üê mk_pi, ‚Üê mk_ulift],
  exact mk_congr (equiv.ulift.trans $ equiv.Pi_congr_right $ Œª i, equiv.ulift.symm)
end

@[simp] theorem lift_min {Œπ I} (f : Œπ ‚Üí cardinal) :
  lift (cardinal.min I f) = cardinal.min I (lift ‚àò f) :=
le_antisymm (le_min.2 $ Œª a, lift_le.2 $ min_le _ a) $
let ‚ü®i, e‚ü© := min_eq I (lift ‚àò f) in
by rw e; exact lift_le.2 (le_min.2 $ Œª j, lift_le.1 $
by have := min_le (lift ‚àò f) j; rwa e at this)

theorem lift_down {a : cardinal.{u}} {b : cardinal.{max u v}} :
  b ‚â§ lift a ‚Üí ‚àÉ a', lift a' = b :=
induction_on‚ÇÇ a b $ Œª Œ± Œ≤,
by rw [‚Üê lift_id (#Œ≤), ‚Üê lift_umax, ‚Üê lift_umax.{u v}, lift_mk_le]; exact
Œª ‚ü®f‚ü©, ‚ü®#(set.range f), eq.symm $ lift_mk_eq.2
  ‚ü®embedding.equiv_of_surjective
    (embedding.cod_restrict _ f set.mem_range_self)
    $ Œª ‚ü®a, ‚ü®b, e‚ü©‚ü©, ‚ü®b, subtype.eq e‚ü©‚ü©‚ü©

theorem le_lift_iff {a : cardinal.{u}} {b : cardinal.{max u v}} :
  b ‚â§ lift a ‚Üî ‚àÉ a', lift a' = b ‚àß a' ‚â§ a :=
‚ü®Œª h, let ‚ü®a', e‚ü© := lift_down h in ‚ü®a', e, lift_le.1 $ e.symm ‚ñ∏ h‚ü©,
 Œª ‚ü®a', e, h‚ü©, e ‚ñ∏ lift_le.2 h‚ü©

theorem lt_lift_iff {a : cardinal.{u}} {b : cardinal.{max u v}} :
  b < lift a ‚Üî ‚àÉ a', lift a' = b ‚àß a' < a :=
‚ü®Œª h, let ‚ü®a', e‚ü© := lift_down (le_of_lt h) in
      ‚ü®a', e, lift_lt.1 $ e.symm ‚ñ∏ h‚ü©,
 Œª ‚ü®a', e, h‚ü©, e ‚ñ∏ lift_lt.2 h‚ü©

@[simp] theorem lift_succ (a) : lift (succ a) = succ (lift a) :=
le_antisymm
  (le_of_not_gt $ Œª h, begin
    rcases lt_lift_iff.1 h with ‚ü®b, e, h‚ü©,
    rw [lt_succ, ‚Üê lift_le, e] at h,
    exact not_lt_of_le h (lt_succ_self _)
  end)
  (succ_le.2 $ lift_lt.2 $ lt_succ_self _)

@[simp] theorem lift_max {a : cardinal.{u}} {b : cardinal.{v}} :
  lift.{(max v w)} a = lift.{(max u w)} b ‚Üî lift.{v} a = lift.{u} b :=
calc lift.{(max v w)} a = lift.{(max u w)} b
  ‚Üî lift.{w} (lift.{v} a) = lift.{w} (lift.{u} b) : by simp
  ... ‚Üî lift.{v} a = lift.{u} b : lift_inj

@[simp] theorem lift_min' {a b : cardinal} : lift (min a b) = min (lift a) (lift b) :=
begin
  cases le_total a b,
  { rw [min_eq_left h, min_eq_left (lift_le.2 h)] },
  { rw [min_eq_right h, min_eq_right (lift_le.2 h)] }
end

@[simp] theorem lift_max' {a b : cardinal} : lift (max a b) = max (lift a) (lift b) :=
begin
  cases le_total a b,
  { rw [max_eq_right h, max_eq_right (lift_le.2 h)] },
  { rw [max_eq_left h, max_eq_left (lift_le.2 h)] }
end

protected lemma le_sup_iff {Œπ : Type v} {f : Œπ ‚Üí cardinal.{max v w}} {c : cardinal} :
  (c ‚â§ sup f) ‚Üî (‚àÄ b, (‚àÄ i, f i ‚â§ b) ‚Üí c ‚â§ b) :=
‚ü®Œª h b hb, le_trans h (sup_le hb), Œª h, h _ $ le_sup f‚ü©

/-- The lift of a supremum is the supremum of the lifts. -/
lemma lift_sup {Œπ : Type v} (f : Œπ ‚Üí cardinal.{max v w}) :
  lift.{u} (sup.{v w} f) = sup.{v (max u w)} (Œª i : Œπ, lift.{u} (f i)) :=
begin
  apply le_antisymm,
  { rw [cardinal.le_sup_iff], intros c hc, by_contra h,
    obtain ‚ü®d, rfl‚ü© := cardinal.lift_down (not_le.mp h).le,
    simp only [lift_le, sup_le_iff] at h hc,
    exact h hc },
  { simp only [cardinal.sup_le, lift_le, le_sup, implies_true_iff] }
end

/-- To prove that the lift of a supremum is bounded by some cardinal `t`,
it suffices to show that the lift of each cardinal is bounded by `t`. -/
lemma lift_sup_le {Œπ : Type v} (f : Œπ ‚Üí cardinal.{max v w})
  (t : cardinal.{max u v w}) (w : ‚àÄ i, lift.{u} (f i) ‚â§ t) :
  lift.{u} (sup f) ‚â§ t :=
by { rw lift_sup, exact sup_le w }

@[simp] lemma lift_sup_le_iff {Œπ : Type v} (f : Œπ ‚Üí cardinal.{max v w}) (t : cardinal.{max u v w}) :
  lift.{u} (sup f) ‚â§ t ‚Üî ‚àÄ i, lift.{u} (f i) ‚â§ t :=
‚ü®Œª h i, (lift_le.mpr (le_sup f i)).trans h,
 Œª h, lift_sup_le f t h‚ü©

universes v' w'

/--
To prove an inequality between the lifts to a common universe of two different supremums,
it suffices to show that the lift of each cardinal from the smaller supremum
if bounded by the lift of some cardinal from the larger supremum.
-/
lemma lift_sup_le_lift_sup
  {Œπ : Type v} {Œπ' : Type v'} (f : Œπ ‚Üí cardinal.{max v w}) (f' : Œπ' ‚Üí cardinal.{max v' w'})
  (g : Œπ ‚Üí Œπ') (h : ‚àÄ i, lift.{(max v' w')} (f i) ‚â§ lift.{(max v w)} (f' (g i))) :
  lift.{(max v' w')} (sup f) ‚â§ lift.{(max v w)} (sup f') :=
begin
  apply lift_sup_le.{(max v' w')} f,
  intro i,
  apply le_trans (h i),
  simp only [lift_le],
  apply le_sup,
end

/-- A variant of `lift_sup_le_lift_sup` with universes specialized via `w = v` and `w' = v'`.
This is sometimes necessary to avoid universe unification issues. -/
lemma lift_sup_le_lift_sup'
  {Œπ : Type v} {Œπ' : Type v'} (f : Œπ ‚Üí cardinal.{v}) (f' : Œπ' ‚Üí cardinal.{v'})
  (g : Œπ ‚Üí Œπ') (h : ‚àÄ i, lift.{v'} (f i) ‚â§ lift.{v} (f' (g i))) :
  lift.{v'} (sup.{v v} f) ‚â§ lift.{v} (sup.{v' v'} f') :=
lift_sup_le_lift_sup f f' g h

/-- `œâ` is the smallest infinite cardinal, also known as ‚Ñµ‚ÇÄ. -/
def omega : cardinal.{u} := lift (#‚Ñï)

localized "notation `œâ` := cardinal.omega" in cardinal

lemma mk_nat : #‚Ñï = œâ := (lift_id _).symm

theorem omega_ne_zero : œâ ‚â† 0 := mk_ne_zero _

theorem omega_pos : 0 < œâ :=
pos_iff_ne_zero.2 omega_ne_zero

@[simp] theorem lift_omega : lift œâ = œâ := lift_lift _

@[simp] theorem omega_le_lift {c : cardinal.{u}} : œâ ‚â§ lift.{v} c ‚Üî œâ ‚â§ c :=
by rw [‚Üê lift_omega, lift_le]

@[simp] theorem lift_le_omega {c : cardinal.{u}} : lift.{v} c ‚â§ œâ ‚Üî c ‚â§ œâ :=
by rw [‚Üê lift_omega, lift_le]

/-! ### Properties about the cast from `‚Ñï` -/

@[simp] theorem mk_fin (n : ‚Ñï) : #(fin n) = n := by simp

@[simp] theorem lift_nat_cast (n : ‚Ñï) : lift.{u} (n : cardinal.{v}) = n :=
by induction n; simp *

@[simp] lemma lift_eq_nat_iff {a : cardinal.{u}} {n : ‚Ñï} : lift.{v} a = n ‚Üî a = n :=
lift_injective.eq_iff' (lift_nat_cast n)

@[simp] lemma nat_eq_lift_iff {n : ‚Ñï} {a : cardinal.{u}} :
  (n : cardinal) = lift.{v} a ‚Üî (n : cardinal) = a :=
by rw [‚Üê lift_nat_cast.{v} n, lift_inj]

theorem lift_mk_fin (n : ‚Ñï) : lift (#(fin n)) = n := by simp

lemma mk_finset {Œ± : Type u} {s : finset Œ±} : #s = ‚Üë(finset.card s) := by simp

theorem card_le_of_finset {Œ±} (s : finset Œ±) :
  (s.card : cardinal) ‚â§ #Œ± :=
begin
  rw (_ : (s.card : cardinal) = #s),
  { exact ‚ü®function.embedding.subtype _‚ü© },
  rw [cardinal.mk_fintype, fintype.card_coe]
end

@[simp, norm_cast] theorem nat_cast_pow {m n : ‚Ñï} : (‚Üë(pow m n) : cardinal) = m ^ n :=
by induction n; simp [pow_succ', power_add, *]

@[simp, norm_cast] theorem nat_cast_le {m n : ‚Ñï} : (m : cardinal) ‚â§ n ‚Üî m ‚â§ n :=
by rw [‚Üê lift_mk_fin, ‚Üê lift_mk_fin, lift_le]; exact
‚ü®Œª ‚ü®‚ü®f, hf‚ü©‚ü©, by simpa only [fintype.card_fin] using fintype.card_le_of_injective f hf,
  Œª h, ‚ü®(fin.cast_le h).to_embedding‚ü©‚ü©

@[simp, norm_cast] theorem nat_cast_lt {m n : ‚Ñï} : (m : cardinal) < n ‚Üî m < n :=
by simp [lt_iff_le_not_le, -not_le]

instance : char_zero cardinal := ‚ü®strict_mono.injective $ Œª m n, nat_cast_lt.2‚ü©

theorem nat_cast_inj {m n : ‚Ñï} : (m : cardinal) = n ‚Üî m = n := nat.cast_inj

lemma nat_cast_injective : injective (coe : ‚Ñï ‚Üí cardinal) :=
nat.cast_injective

@[simp, norm_cast, priority 900] theorem nat_succ (n : ‚Ñï) : (n.succ : cardinal) = succ n :=
le_antisymm (add_one_le_succ _) (succ_le.2 $ nat_cast_lt.2 $ nat.lt_succ_self _)

@[simp] theorem succ_zero : succ 0 = 1 :=
by norm_cast

theorem card_le_of {Œ± : Type u} {n : ‚Ñï} (H : ‚àÄ s : finset Œ±, s.card ‚â§ n) :
  # Œ± ‚â§ n :=
begin
  refine lt_succ.1 (lt_of_not_ge $ Œª hn, _),
  rw [‚Üê cardinal.nat_succ, ‚Üê cardinal.lift_mk_fin n.succ] at hn,
  cases hn with f,
  refine not_lt_of_le (H $ finset.univ.map f) _,
  rw [finset.card_map, ‚Üê fintype.card, fintype.card_ulift, fintype.card_fin],
  exact n.lt_succ_self
end

theorem cantor' (a) {b : cardinal} (hb : 1 < b) : a < b ^ a :=
by rw [‚Üê succ_le, (by norm_cast : succ 1 = 2)] at hb;
   exact lt_of_lt_of_le (cantor _) (power_le_power_right hb)

theorem one_le_iff_pos {c : cardinal} : 1 ‚â§ c ‚Üî 0 < c :=
by rw [‚Üê succ_zero, succ_le]

theorem one_le_iff_ne_zero {c : cardinal} : 1 ‚â§ c ‚Üî c ‚â† 0 :=
by rw [one_le_iff_pos, pos_iff_ne_zero]

theorem nat_lt_omega (n : ‚Ñï) : (n : cardinal.{u}) < œâ :=
succ_le.1 $ by rw [‚Üê nat_succ, ‚Üê lift_mk_fin, omega, lift_mk_le.{0 0 u}]; exact
‚ü®‚ü®coe, Œª a b, fin.ext‚ü©‚ü©

@[simp] theorem one_lt_omega : 1 < œâ :=
by simpa using nat_lt_omega 1

theorem lt_omega {c : cardinal.{u}} : c < œâ ‚Üî ‚àÉ n : ‚Ñï, c = n :=
‚ü®Œª h, begin
  rcases lt_lift_iff.1 h with ‚ü®c, rfl, h'‚ü©,
  rcases le_mk_iff_exists_set.1 h'.1 with ‚ü®S, rfl‚ü©,
  suffices : finite S,
  { lift S to finset ‚Ñï using this,
    simp },
  contrapose! h',
  haveI := infinite.to_subtype h',
  exact ‚ü®infinite.nat_embedding S‚ü©
end, Œª ‚ü®n, e‚ü©, e.symm ‚ñ∏ nat_lt_omega _‚ü©

theorem omega_le {c : cardinal.{u}} : œâ ‚â§ c ‚Üî ‚àÄ n : ‚Ñï, (n:cardinal) ‚â§ c :=
‚ü®Œª h n, le_trans (le_of_lt (nat_lt_omega _)) h,
 Œª h, le_of_not_lt $ Œª hn, begin
  rcases lt_omega.1 hn with ‚ü®n, rfl‚ü©,
  exact not_le_of_lt (nat.lt_succ_self _) (nat_cast_le.1 (h (n+1)))
end‚ü©

theorem lt_omega_iff_fintype {Œ± : Type u} : #Œ± < œâ ‚Üî nonempty (fintype Œ±) :=
lt_omega.trans ‚ü®Œª ‚ü®n, e‚ü©, begin
  rw [‚Üê lift_mk_fin n] at e,
  cases quotient.exact e with f,
  exact ‚ü®fintype.of_equiv _ f.symm‚ü©
end, Œª ‚ü®_‚ü©, by exactI ‚ü®_, mk_fintype _‚ü©‚ü©

theorem lt_omega_of_fintype (Œ± : Type u) [fintype Œ±] : #Œ± < œâ :=
lt_omega_iff_fintype.2 ‚ü®infer_instance‚ü©

theorem lt_omega_iff_finite {Œ±} {S : set Œ±} : #S < œâ ‚Üî finite S :=
lt_omega_iff_fintype.trans finite_def.symm

instance can_lift_cardinal_nat : can_lift cardinal ‚Ñï :=
‚ü® coe, Œª x, x < œâ, Œª x hx, let ‚ü®n, hn‚ü© := lt_omega.mp hx in ‚ü®n, hn.symm‚ü©‚ü©

theorem add_lt_omega {a b : cardinal} (ha : a < œâ) (hb : b < œâ) : a + b < œâ :=
match a, b, lt_omega.1 ha, lt_omega.1 hb with
| _, _, ‚ü®m, rfl‚ü©, ‚ü®n, rfl‚ü© := by rw [‚Üê nat.cast_add]; apply nat_lt_omega
end

lemma add_lt_omega_iff {a b : cardinal} : a + b < œâ ‚Üî a < œâ ‚àß b < œâ :=
‚ü®Œª h, ‚ü®lt_of_le_of_lt (self_le_add_right _ _) h, lt_of_le_of_lt (self_le_add_left _ _) h‚ü©,
  Œª‚ü®h1, h2‚ü©, add_lt_omega h1 h2‚ü©

lemma omega_le_add_iff {a b : cardinal} : œâ ‚â§ a + b ‚Üî œâ ‚â§ a ‚à® œâ ‚â§ b :=
by simp only [‚Üê not_lt, add_lt_omega_iff, not_and_distrib]

/-- See also `cardinal.nsmul_lt_omega_iff_of_ne_zero` if you already have `n ‚â† 0`. -/
lemma nsmul_lt_omega_iff {n : ‚Ñï} {a : cardinal} : n ‚Ä¢ a < œâ ‚Üî n = 0 ‚à® a < œâ :=
begin
  cases n,
  { simpa using nat_lt_omega 0 },
  simp only [nat.succ_ne_zero, false_or],
  induction n with n ih,
  { simp },
  rw [succ_nsmul, add_lt_omega_iff, ih, and_self]
end

/-- See also `cardinal.nsmul_lt_omega_iff` for a hypothesis-free version. -/
lemma nsmul_lt_omega_iff_of_ne_zero {n : ‚Ñï} {a : cardinal} (h : n ‚â† 0) : n ‚Ä¢ a < œâ ‚Üî a < œâ :=
nsmul_lt_omega_iff.trans $ or_iff_right h

theorem mul_lt_omega {a b : cardinal} (ha : a < œâ) (hb : b < œâ) : a * b < œâ :=
match a, b, lt_omega.1 ha, lt_omega.1 hb with
| _, _, ‚ü®m, rfl‚ü©, ‚ü®n, rfl‚ü© := by rw [‚Üê nat.cast_mul]; apply nat_lt_omega
end

lemma mul_lt_omega_iff {a b : cardinal} : a * b < œâ ‚Üî a = 0 ‚à® b = 0 ‚à® a < œâ ‚àß b < œâ :=
begin
  split,
  { intro h, by_cases ha : a = 0, { left, exact ha },
    right, by_cases hb : b = 0, { left, exact hb },
    right, rw [‚Üê ne, ‚Üê one_le_iff_ne_zero] at ha hb, split,
    { rw [‚Üê mul_one a],
      refine lt_of_le_of_lt (mul_le_mul' (le_refl a) hb) h },
    { rw [‚Üê one_mul b],
      refine lt_of_le_of_lt (mul_le_mul' ha (le_refl b)) h }},
  rintro (rfl|rfl|‚ü®ha,hb‚ü©); simp only [*, mul_lt_omega, omega_pos, zero_mul, mul_zero]
end

lemma omega_le_mul_iff {a b : cardinal} : œâ ‚â§ a * b ‚Üî a ‚â† 0 ‚àß b ‚â† 0 ‚àß (œâ ‚â§ a ‚à® œâ ‚â§ b) :=
let h := (@mul_lt_omega_iff a b).not in
by rwa [not_lt, not_or_distrib, not_or_distrib, not_and_distrib, not_lt, not_lt] at h

lemma mul_lt_omega_iff_of_ne_zero {a b : cardinal} (ha : a ‚â† 0) (hb : b ‚â† 0) :
  a * b < œâ ‚Üî a < œâ ‚àß b < œâ :=
by simp [mul_lt_omega_iff, ha, hb]

theorem power_lt_omega {a b : cardinal} (ha : a < œâ) (hb : b < œâ) : a ^ b < œâ :=
match a, b, lt_omega.1 ha, lt_omega.1 hb with
| _, _, ‚ü®m, rfl‚ü©, ‚ü®n, rfl‚ü© := by rw [‚Üê nat_cast_pow]; apply nat_lt_omega
end

lemma eq_one_iff_unique {Œ± : Type*} :
  #Œ± = 1 ‚Üî subsingleton Œ± ‚àß nonempty Œ± :=
calc #Œ± = 1 ‚Üî #Œ± ‚â§ 1 ‚àß ¬¨#Œ± < 1 : eq_iff_le_not_lt
        ... ‚Üî subsingleton Œ± ‚àß nonempty Œ± :
begin
  apply and_congr le_one_iff_subsingleton,
  push_neg,
  rw [one_le_iff_ne_zero, mk_ne_zero_iff]
end

theorem infinite_iff {Œ± : Type u} : infinite Œ± ‚Üî œâ ‚â§ #Œ± :=
by rw [‚Üênot_lt, lt_omega_iff_fintype, not_nonempty_iff, is_empty_fintype]

@[simp] lemma omega_le_mk (Œ± : Type u) [infinite Œ±] : œâ ‚â§ #Œ± := infinite_iff.1 ‚Äπ_‚Ä∫

lemma encodable_iff {Œ± : Type u} : nonempty (encodable Œ±) ‚Üî #Œ± ‚â§ œâ :=
‚ü®Œª ‚ü®h‚ü©, ‚ü®(@encodable.encode' Œ± h).trans equiv.ulift.symm.to_embedding‚ü©,
  Œª ‚ü®h‚ü©, ‚ü®encodable.of_inj _ (h.trans equiv.ulift.to_embedding).injective‚ü©‚ü©

@[simp] lemma mk_le_omega [encodable Œ±] : #Œ± ‚â§ œâ := encodable_iff.1 ‚ü®‚Äπ_‚Ä∫‚ü©

lemma denumerable_iff {Œ± : Type u} : nonempty (denumerable Œ±) ‚Üî #Œ± = œâ :=
‚ü®Œª ‚ü®h‚ü©, mk_congr ((@denumerable.eqv Œ± h).trans equiv.ulift.symm),
 Œª h, by { cases quotient.exact h with f, exact ‚ü®denumerable.mk' $ f.trans equiv.ulift‚ü© }‚ü©

@[simp] lemma mk_denumerable (Œ± : Type u) [denumerable Œ±] : #Œ± = œâ :=
denumerable_iff.1 ‚ü®‚Äπ_‚Ä∫‚ü©

@[simp] lemma mk_set_le_omega (s : set Œ±) : #s ‚â§ œâ ‚Üî countable s :=
begin
  rw [countable_iff_exists_injective], split,
  { rintro ‚ü®f'‚ü©, cases embedding.trans f' equiv.ulift.to_embedding with f hf, exact ‚ü®f, hf‚ü© },
  { rintro ‚ü®f, hf‚ü©, exact ‚ü®embedding.trans ‚ü®f, hf‚ü© equiv.ulift.symm.to_embedding‚ü© }
end

@[simp] lemma omega_add_omega : œâ + œâ = œâ := mk_denumerable _

lemma omega_mul_omega : œâ * œâ = œâ := mk_denumerable _

@[simp] lemma add_le_omega {c‚ÇÅ c‚ÇÇ : cardinal} : c‚ÇÅ + c‚ÇÇ ‚â§ œâ ‚Üî c‚ÇÅ ‚â§ œâ ‚àß c‚ÇÇ ‚â§ œâ :=
‚ü®Œª h, ‚ü®le_self_add.trans h, le_add_self.trans h‚ü©, Œª h, omega_add_omega ‚ñ∏ add_le_add h.1 h.2‚ü©

/-- This function sends finite cardinals to the corresponding natural, and infinite cardinals
  to 0. -/
def to_nat : zero_hom cardinal ‚Ñï :=
‚ü®Œª c, if h : c < omega.{v} then classical.some (lt_omega.1 h) else 0,
  begin
    have h : 0 < œâ := nat_lt_omega 0,
    rw [dif_pos h, ‚Üê cardinal.nat_cast_inj, ‚Üê classical.some_spec (lt_omega.1 h), nat.cast_zero],
  end‚ü©

lemma to_nat_apply_of_lt_omega {c : cardinal} (h : c < œâ) :
  c.to_nat = classical.some (lt_omega.1 h) :=
dif_pos h

@[simp]
lemma to_nat_apply_of_omega_le {c : cardinal} (h : œâ ‚â§ c) :
  c.to_nat = 0 :=
dif_neg (not_lt_of_le h)

@[simp]
lemma cast_to_nat_of_lt_omega {c : cardinal} (h : c < œâ) :
  ‚Üëc.to_nat = c :=
by rw [to_nat_apply_of_lt_omega h, ‚Üê classical.some_spec (lt_omega.1 h)]

@[simp]
lemma cast_to_nat_of_omega_le {c : cardinal} (h : œâ ‚â§ c) :
  ‚Üëc.to_nat = (0 : cardinal) :=
by rw [to_nat_apply_of_omega_le h, nat.cast_zero]

lemma to_nat_le_iff_le_of_lt_omega {c d : cardinal} (hc : c < œâ) (hd : d < œâ) :
  c.to_nat ‚â§ d.to_nat ‚Üî c ‚â§ d :=
by rw [‚Üênat_cast_le, cast_to_nat_of_lt_omega hc, cast_to_nat_of_lt_omega hd]

lemma to_nat_lt_iff_lt_of_lt_omega {c d : cardinal} (hc : c < œâ) (hd : d < œâ) :
  c.to_nat < d.to_nat ‚Üî c < d :=
by rw [‚Üênat_cast_lt, cast_to_nat_of_lt_omega hc, cast_to_nat_of_lt_omega hd]

lemma to_nat_le_of_le_of_lt_omega {c d : cardinal} (hd : d < œâ) (hcd : c ‚â§ d) :
  c.to_nat ‚â§ d.to_nat :=
(to_nat_le_iff_le_of_lt_omega (lt_of_le_of_lt hcd hd) hd).mpr hcd

lemma to_nat_lt_of_lt_of_lt_omega {c d : cardinal} (hd : d < œâ) (hcd : c < d) :
  c.to_nat < d.to_nat :=
(to_nat_lt_iff_lt_of_lt_omega (hcd.trans hd) hd).mpr hcd

@[simp]
lemma to_nat_cast (n : ‚Ñï) : cardinal.to_nat n = n :=
begin
  rw [to_nat_apply_of_lt_omega (nat_lt_omega n), ‚Üê nat_cast_inj],
  exact (classical.some_spec (lt_omega.1 (nat_lt_omega n))).symm,
end

/-- `to_nat` has a right-inverse: coercion. -/
lemma to_nat_right_inverse : function.right_inverse (coe : ‚Ñï ‚Üí cardinal) to_nat := to_nat_cast

lemma to_nat_surjective : surjective to_nat := to_nat_right_inverse.surjective

@[simp]
lemma mk_to_nat_of_infinite [h : infinite Œ±] : (#Œ±).to_nat = 0 :=
dif_neg (not_lt_of_le (infinite_iff.1 h))

lemma mk_to_nat_eq_card [fintype Œ±] : (#Œ±).to_nat = fintype.card Œ± := by simp

@[simp]
lemma zero_to_nat : to_nat 0 = 0 :=
by rw [‚Üê to_nat_cast 0, nat.cast_zero]

@[simp]
lemma one_to_nat : to_nat 1 = 1 :=
by rw [‚Üê to_nat_cast 1, nat.cast_one]

@[simp] lemma to_nat_eq_one {c : cardinal} : to_nat c = 1 ‚Üî c = 1 :=
‚ü®Œª h, (cast_to_nat_of_lt_omega (lt_of_not_ge (one_ne_zero ‚àò h.symm.trans ‚àò
  to_nat_apply_of_omega_le))).symm.trans ((congr_arg coe h).trans nat.cast_one),
  Œª h, (congr_arg to_nat h).trans one_to_nat‚ü©

lemma to_nat_eq_one_iff_unique {Œ± : Type*} : (#Œ±).to_nat = 1 ‚Üî subsingleton Œ± ‚àß nonempty Œ± :=
to_nat_eq_one.trans eq_one_iff_unique

@[simp] lemma to_nat_lift (c : cardinal.{v}) : (lift.{u v} c).to_nat = c.to_nat :=
begin
  apply nat_cast_injective,
  cases lt_or_ge c œâ with hc hc,
  { rw [cast_to_nat_of_lt_omega, ‚Üêlift_nat_cast, cast_to_nat_of_lt_omega hc],
    rwa [‚Üêlift_omega, lift_lt] },
  { rw [cast_to_nat_of_omega_le, ‚Üêlift_nat_cast, cast_to_nat_of_omega_le hc, lift_zero],
    rwa [‚Üêlift_omega, lift_le] },
end

lemma to_nat_congr {Œ≤ : Type v} (e : Œ± ‚âÉ Œ≤) : (#Œ±).to_nat = (#Œ≤).to_nat :=
by rw [‚Üêto_nat_lift, lift_mk_eq.mpr ‚ü®e‚ü©, to_nat_lift]

@[simp] lemma to_nat_mul (x y : cardinal) : (x * y).to_nat = x.to_nat * y.to_nat :=
begin
  by_cases hx1 : x = 0,
  { rw [comm_semiring.mul_comm, hx1, mul_zero, zero_to_nat, nat.zero_mul] },
  by_cases hy1 : y = 0,
  { rw [hy1, zero_to_nat, mul_zero, mul_zero, zero_to_nat] },
  refine nat_cast_injective (eq.trans _ (nat.cast_mul _ _).symm),
  cases lt_or_ge x œâ with hx2 hx2,
  { cases lt_or_ge y œâ with hy2 hy2,
    { rw [cast_to_nat_of_lt_omega, cast_to_nat_of_lt_omega hx2, cast_to_nat_of_lt_omega hy2],
      exact mul_lt_omega hx2 hy2 },
    { rw [cast_to_nat_of_omega_le hy2, mul_zero, cast_to_nat_of_omega_le],
      exact not_lt.mp (mt (mul_lt_omega_iff_of_ne_zero hx1 hy1).mp (Œª h, not_lt.mpr hy2 h.2)) } },
  { rw [cast_to_nat_of_omega_le hx2, zero_mul, cast_to_nat_of_omega_le],
    exact not_lt.mp (mt (mul_lt_omega_iff_of_ne_zero hx1 hy1).mp (Œª h, not_lt.mpr hx2 h.1)) },
end

@[simp] lemma to_nat_add_of_lt_omega {a : cardinal.{u}} {b : cardinal.{v}}
  (ha : a < œâ) (hb : b < œâ) : ((lift.{v u} a) + (lift.{u v} b)).to_nat = a.to_nat + b.to_nat :=
begin
  apply cardinal.nat_cast_injective,
  replace ha : (lift.{v u} a) < œâ := by { rw [‚Üê lift_omega], exact lift_lt.2 ha },
  replace hb : (lift.{u v} b) < œâ := by { rw [‚Üê lift_omega], exact lift_lt.2 hb },
  rw [nat.cast_add, ‚Üê to_nat_lift.{v u} a, ‚Üê to_nat_lift.{u v} b, cast_to_nat_of_lt_omega ha,
    cast_to_nat_of_lt_omega hb, cast_to_nat_of_lt_omega (add_lt_omega ha hb)]
end

/-- This function sends finite cardinals to the corresponding natural, and infinite cardinals
  to `‚ä§`. -/
def to_enat : cardinal ‚Üí+ enat :=
{ to_fun := Œª c, if c < omega.{v} then c.to_nat else ‚ä§,
  map_zero' := by simp [if_pos (lt_trans zero_lt_one one_lt_omega)],
  map_add' := Œª x y, begin
    by_cases hx : x < œâ,
    { obtain ‚ü®x0, rfl‚ü© := lt_omega.1 hx,
      by_cases hy : y < œâ,
      { obtain ‚ü®y0, rfl‚ü© := lt_omega.1 hy,
        simp only [add_lt_omega hx hy, hx, hy, to_nat_cast, if_true],
        rw [‚Üê nat.cast_add, to_nat_cast, nat.cast_add] },
      { rw [if_neg hy, if_neg, enat.add_top],
        contrapose! hy,
        apply lt_of_le_of_lt le_add_self hy } },
    { rw [if_neg hx, if_neg, enat.top_add],
      contrapose! hx,
      apply lt_of_le_of_lt le_self_add hx },
  end }

@[simp]
lemma to_enat_apply_of_lt_omega {c : cardinal} (h : c < œâ) :
  c.to_enat = c.to_nat :=
if_pos h

@[simp]
lemma to_enat_apply_of_omega_le {c : cardinal} (h : œâ ‚â§ c) :
  c.to_enat = ‚ä§ :=
if_neg (not_lt_of_le h)

@[simp]
lemma to_enat_cast (n : ‚Ñï) : cardinal.to_enat n = n :=
by rw [to_enat_apply_of_lt_omega (nat_lt_omega n), to_nat_cast]

@[simp]
lemma mk_to_enat_of_infinite [h : infinite Œ±] : (#Œ±).to_enat = ‚ä§ :=
to_enat_apply_of_omega_le (infinite_iff.1 h)

lemma to_enat_surjective : surjective to_enat :=
begin
  intro x,
  exact enat.cases_on x ‚ü®œâ, to_enat_apply_of_omega_le (le_refl œâ)‚ü©
    (Œª n, ‚ü®n, to_enat_cast n‚ü©),
end

lemma mk_to_enat_eq_coe_card [fintype Œ±] : (#Œ±).to_enat = fintype.card Œ± :=
by simp

lemma mk_int : #‚Ñ§ = œâ := mk_denumerable ‚Ñ§

lemma mk_pnat : #‚Ñï+ = œâ := mk_denumerable ‚Ñï+

/-- **K√∂nig's theorem** -/
theorem sum_lt_prod {Œπ} (f g : Œπ ‚Üí cardinal) (H : ‚àÄ i, f i < g i) : sum f < prod g :=
lt_of_not_ge $ Œª ‚ü®F‚ü©, begin
  haveI : inhabited (Œ† (i : Œπ), (g i).out),
  { refine ‚ü®Œª i, classical.choice $ mk_ne_zero_iff.1 _‚ü©,
    rw mk_out,
    exact (H i).ne_bot },
  let G := inv_fun F,
  have sG : surjective G := inv_fun_surjective F.2,
  choose C hc using show ‚àÄ i, ‚àÉ b, ‚àÄ a, G ‚ü®i, a‚ü© i ‚â† b,
  { assume i,
    simp only [- not_exists, not_exists.symm, not_forall.symm],
    refine Œª h, not_le_of_lt (H i) _,
    rw [‚Üê mk_out (f i), ‚Üê mk_out (g i)],
    exact ‚ü®embedding.of_surjective _ h‚ü© },
  exact (let ‚ü®‚ü®i, a‚ü©, h‚ü© := sG C in hc i a (congr_fun h _))
end

@[simp] theorem mk_empty : #empty = 0 := mk_eq_zero _

@[simp] theorem mk_pempty : #pempty = 0 := mk_eq_zero _

@[simp] theorem mk_punit : #punit = 1 := mk_eq_one punit

theorem mk_unit : #unit = 1 := mk_punit

@[simp] theorem mk_singleton {Œ± : Type u} (x : Œ±) : #({x} : set Œ±) = 1 :=
mk_eq_one _

@[simp] theorem mk_plift_true : #(plift true) = 1 := mk_eq_one _
@[simp] theorem mk_plift_false : #(plift false) = 0 := mk_eq_zero _

@[simp] theorem mk_vector (Œ± : Type u) (n : ‚Ñï) : #(vector Œ± n) = (#Œ±) ^‚Ñï n :=
(mk_congr (equiv.vector_equiv_fin Œ± n)).trans $ by simp

theorem mk_list_eq_sum_pow (Œ± : Type u) : #(list Œ±) = sum (Œª n : ‚Ñï, (#Œ±) ^‚Ñï n) :=
calc #(list Œ±) = #(Œ£ n, vector Œ± n) : mk_congr (equiv.sigma_fiber_equiv list.length).symm
... = sum (Œª n : ‚Ñï, (#Œ±) ^‚Ñï n) : by simp

theorem mk_quot_le {Œ± : Type u} {r : Œ± ‚Üí Œ± ‚Üí Prop} : #(quot r) ‚â§ #Œ± :=
mk_le_of_surjective quot.exists_rep

theorem mk_quotient_le {Œ± : Type u} {s : setoid Œ±} : #(quotient s) ‚â§ #Œ± :=
mk_quot_le

theorem mk_subtype_le_of_subset {Œ± : Type u} {p q : Œ± ‚Üí Prop} (h : ‚àÄ ‚¶Éx‚¶Ñ, p x ‚Üí q x) :
  #(subtype p) ‚â§ #(subtype q) :=
‚ü®embedding.subtype_map (embedding.refl Œ±) h‚ü©

@[simp] theorem mk_emptyc (Œ± : Type u) : #(‚àÖ : set Œ±) = 0 := mk_eq_zero _

lemma mk_emptyc_iff {Œ± : Type u} {s : set Œ±} : #s = 0 ‚Üî s = ‚àÖ :=
begin
  split,
  { intro h,
    rw mk_eq_zero_iff at h,
    exact eq_empty_iff_forall_not_mem.2 (Œª x hx, h.elim' ‚ü®x, hx‚ü©) },
  { rintro rfl, exact mk_emptyc _ }
end

@[simp] theorem mk_univ {Œ± : Type u} : #(@univ Œ±) = #Œ± :=
mk_congr (equiv.set.univ Œ±)

theorem mk_image_le {Œ± Œ≤ : Type u} {f : Œ± ‚Üí Œ≤} {s : set Œ±} : #(f '' s) ‚â§ #s :=
mk_le_of_surjective surjective_onto_image

theorem mk_image_le_lift {Œ± : Type u} {Œ≤ : Type v} {f : Œ± ‚Üí Œ≤} {s : set Œ±} :
  lift.{u} (#(f '' s)) ‚â§ lift.{v} (#s) :=
lift_mk_le.{v u 0}.mpr ‚ü®embedding.of_surjective _ surjective_onto_image‚ü©

theorem mk_range_le {Œ± Œ≤ : Type u} {f : Œ± ‚Üí Œ≤} : #(range f) ‚â§ #Œ± :=
mk_le_of_surjective surjective_onto_range

theorem mk_range_le_lift {Œ± : Type u} {Œ≤ : Type v} {f : Œ± ‚Üí Œ≤} :
  lift.{u} (#(range f)) ‚â§ lift.{v} (#Œ±) :=
lift_mk_le.{v u 0}.mpr ‚ü®embedding.of_surjective _ surjective_onto_range‚ü©

lemma mk_range_eq (f : Œ± ‚Üí Œ≤) (h : injective f) : #(range f) = #Œ± :=
mk_congr ((equiv.of_injective f h).symm)

lemma mk_range_eq_of_injective {Œ± : Type u} {Œ≤ : Type v} {f : Œ± ‚Üí Œ≤} (hf : injective f) :
  lift.{u} (#(range f)) = lift.{v} (#Œ±) :=
lift_mk_eq'.mpr ‚ü®(equiv.of_injective f hf).symm‚ü©

lemma mk_range_eq_lift {Œ± : Type u} {Œ≤ : Type v} {f : Œ± ‚Üí Œ≤} (hf : injective f) :
  lift.{(max u w)} (# (range f)) = lift.{(max v w)} (# Œ±) :=
lift_mk_eq.mpr ‚ü®(equiv.of_injective f hf).symm‚ü©

theorem mk_image_eq {Œ± Œ≤ : Type u} {f : Œ± ‚Üí Œ≤} {s : set Œ±} (hf : injective f) :
  #(f '' s) = #s :=
mk_congr ((equiv.set.image f s hf).symm)

theorem mk_Union_le_sum_mk {Œ± Œπ : Type u} {f : Œπ ‚Üí set Œ±} : #(‚ãÉ i, f i) ‚â§ sum (Œª i, #(f i)) :=
calc #(‚ãÉ i, f i) ‚â§ #(Œ£ i, f i)        : mk_le_of_surjective (set.sigma_to_Union_surjective f)
              ... = sum (Œª i, #(f i)) : mk_sigma _

theorem mk_Union_eq_sum_mk {Œ± Œπ : Type u} {f : Œπ ‚Üí set Œ±} (h : ‚àÄi j, i ‚â† j ‚Üí disjoint (f i) (f j)) :
  #(‚ãÉ i, f i) = sum (Œª i, #(f i)) :=
calc #(‚ãÉ i, f i) = #(Œ£ i, f i)       : mk_congr (set.Union_eq_sigma_of_disjoint h)
              ... = sum (Œªi, #(f i)) : mk_sigma _

lemma mk_Union_le {Œ± Œπ : Type u} (f : Œπ ‚Üí set Œ±) :
  #(‚ãÉ i, f i) ‚â§ #Œπ * cardinal.sup.{u u} (Œª i, #(f i)) :=
le_trans mk_Union_le_sum_mk (sum_le_sup _)

lemma mk_sUnion_le {Œ± : Type u} (A : set (set Œ±)) :
  #(‚ãÉ‚ÇÄ A) ‚â§ #A * cardinal.sup.{u u} (Œª s : A, #s) :=
by { rw [sUnion_eq_Union], apply mk_Union_le }

lemma mk_bUnion_le {Œπ Œ± : Type u} (A : Œπ ‚Üí set Œ±) (s : set Œπ) :
  #(‚ãÉ(x ‚àà s), A x) ‚â§ #s * cardinal.sup.{u u} (Œª x : s, #(A x.1)) :=
by { rw [bUnion_eq_Union], apply mk_Union_le }

lemma finset_card_lt_omega (s : finset Œ±) : #(‚Üës : set Œ±) < œâ :=
by { rw [lt_omega_iff_fintype], exact ‚ü®finset.subtype.fintype s‚ü© }

theorem mk_eq_nat_iff_finset {Œ±} {s : set Œ±} {n : ‚Ñï} :
  #s = n ‚Üî ‚àÉ t : finset Œ±, (t : set Œ±) = s ‚àß t.card = n :=
begin
  split,
  { intro h,
    lift s to finset Œ± using lt_omega_iff_finite.1 (h.symm ‚ñ∏ nat_lt_omega n),
    simpa using h },
  { rintro ‚ü®t, rfl, rfl‚ü©,
    exact mk_finset }
end

theorem mk_union_add_mk_inter {Œ± : Type u} {S T : set Œ±} :
  #(S ‚à™ T : set Œ±) + #(S ‚à© T : set Œ±) = #S + #T :=
quot.sound ‚ü®equiv.set.union_sum_inter S T‚ü©

/-- The cardinality of a union is at most the sum of the cardinalities
of the two sets. -/
lemma mk_union_le {Œ± : Type u} (S T : set Œ±) : #(S ‚à™ T : set Œ±) ‚â§ #S + #T :=
@mk_union_add_mk_inter Œ± S T ‚ñ∏ self_le_add_right (#(S ‚à™ T : set Œ±)) (#(S ‚à© T : set Œ±))

theorem mk_union_of_disjoint {Œ± : Type u} {S T : set Œ±} (H : disjoint S T) :
  #(S ‚à™ T : set Œ±) = #S + #T :=
quot.sound ‚ü®equiv.set.union H‚ü©

theorem mk_insert {Œ± : Type u} {s : set Œ±} {a : Œ±} (h : a ‚àâ s) :
  #(insert a s : set Œ±) = #s + 1 :=
by { rw [‚Üê union_singleton, mk_union_of_disjoint, mk_singleton], simpa }

lemma mk_sum_compl {Œ±} (s : set Œ±) : #s + #(s·∂ú : set Œ±) = #Œ± :=
mk_congr (equiv.set.sum_compl s)

lemma mk_le_mk_of_subset {Œ±} {s t : set Œ±} (h : s ‚äÜ t) : #s ‚â§ #t :=
‚ü®set.embedding_of_subset s t h‚ü©

lemma mk_subtype_mono {p q : Œ± ‚Üí Prop} (h : ‚àÄx, p x ‚Üí q x) : #{x // p x} ‚â§ #{x // q x} :=
‚ü®embedding_of_subset _ _ h‚ü©

lemma mk_union_le_omega {Œ±} {P Q : set Œ±} : #((P ‚à™ Q : set Œ±)) ‚â§ œâ ‚Üî #P ‚â§ œâ ‚àß #Q ‚â§ œâ :=
by simp

lemma mk_image_eq_lift {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (s : set Œ±) (h : injective f) :
  lift.{u} (#(f '' s)) = lift.{v} (#s) :=
lift_mk_eq.{v u 0}.mpr ‚ü®(equiv.set.image f s h).symm‚ü©

lemma mk_image_eq_of_inj_on_lift {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (s : set Œ±)
  (h : inj_on f s) : lift.{u} (#(f '' s)) = lift.{v} (#s) :=
lift_mk_eq.{v u 0}.mpr ‚ü®(equiv.set.image_of_inj_on f s h).symm‚ü©

lemma mk_image_eq_of_inj_on {Œ± Œ≤ : Type u} (f : Œ± ‚Üí Œ≤) (s : set Œ±) (h : inj_on f s) :
  #(f '' s) = #s :=
mk_congr ((equiv.set.image_of_inj_on f s h).symm)

lemma mk_subtype_of_equiv {Œ± Œ≤ : Type u} (p : Œ≤ ‚Üí Prop) (e : Œ± ‚âÉ Œ≤) :
  #{a : Œ± // p (e a)} = #{b : Œ≤ // p b} :=
mk_congr (equiv.subtype_equiv_of_subtype e)

lemma mk_sep (s : set Œ±) (t : Œ± ‚Üí Prop) : #({ x ‚àà s | t x } : set Œ±) = #{ x : s | t x.1 } :=
mk_congr (equiv.set.sep s t)

lemma mk_preimage_of_injective_lift {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (s : set Œ≤)
  (h : injective f) : lift.{v} (#(f ‚Åª¬π' s)) ‚â§ lift.{u} (#s) :=
begin
  rw lift_mk_le.{u v 0}, use subtype.coind (Œª x, f x.1) (Œª x, x.2),
  apply subtype.coind_injective, exact h.comp subtype.val_injective
end

lemma mk_preimage_of_subset_range_lift {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (s : set Œ≤)
  (h : s ‚äÜ range f) : lift.{u} (#s) ‚â§ lift.{v} (#(f ‚Åª¬π' s)) :=
begin
  rw lift_mk_le.{v u 0},
  refine ‚ü®‚ü®_, _‚ü©‚ü©,
  { rintro ‚ü®y, hy‚ü©, rcases classical.subtype_of_exists (h hy) with ‚ü®x, rfl‚ü©, exact ‚ü®x, hy‚ü© },
  rintro ‚ü®y, hy‚ü© ‚ü®y', hy'‚ü©, dsimp,
  rcases classical.subtype_of_exists (h hy) with ‚ü®x, rfl‚ü©,
  rcases classical.subtype_of_exists (h hy') with ‚ü®x', rfl‚ü©,
  simp, intro hxx', rw hxx'
end

lemma mk_preimage_of_injective_of_subset_range_lift {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (s : set Œ≤)
  (h : injective f) (h2 : s ‚äÜ range f) : lift.{v} (#(f ‚Åª¬π' s)) = lift.{u} (#s) :=
le_antisymm (mk_preimage_of_injective_lift f s h) (mk_preimage_of_subset_range_lift f s h2)

lemma mk_preimage_of_injective (f : Œ± ‚Üí Œ≤) (s : set Œ≤) (h : injective f) :
  #(f ‚Åª¬π' s) ‚â§ #s :=
by { convert mk_preimage_of_injective_lift.{u u} f s h using 1; rw [lift_id] }

lemma mk_preimage_of_subset_range (f : Œ± ‚Üí Œ≤) (s : set Œ≤)
  (h : s ‚äÜ range f) : #s ‚â§ #(f ‚Åª¬π' s) :=
by { convert mk_preimage_of_subset_range_lift.{u u} f s h using 1; rw [lift_id] }

lemma mk_preimage_of_injective_of_subset_range (f : Œ± ‚Üí Œ≤) (s : set Œ≤)
  (h : injective f) (h2 : s ‚äÜ range f) : #(f ‚Åª¬π' s) = #s :=
by { convert mk_preimage_of_injective_of_subset_range_lift.{u u} f s h h2 using 1; rw [lift_id] }

lemma mk_subset_ge_of_subset_image_lift {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) {s : set Œ±}
  {t : set Œ≤} (h : t ‚äÜ f '' s) :
    lift.{u} (#t) ‚â§ lift.{v} (#({ x ‚àà s | f x ‚àà t } : set Œ±)) :=
by { rw [image_eq_range] at h, convert mk_preimage_of_subset_range_lift _ _ h using 1,
     rw [mk_sep], refl }

lemma mk_subset_ge_of_subset_image (f : Œ± ‚Üí Œ≤) {s : set Œ±} {t : set Œ≤} (h : t ‚äÜ f '' s) :
  #t ‚â§ #({ x ‚àà s | f x ‚àà t } : set Œ±) :=
by { rw [image_eq_range] at h, convert mk_preimage_of_subset_range _ _ h using 1,
     rw [mk_sep], refl }

theorem le_mk_iff_exists_subset {c : cardinal} {Œ± : Type u} {s : set Œ±} :
  c ‚â§ #s ‚Üî ‚àÉ p : set Œ±, p ‚äÜ s ‚àß #p = c :=
begin
  rw [le_mk_iff_exists_set, ‚Üêsubtype.exists_set_subtype],
  apply exists_congr, intro t, rw [mk_image_eq], apply subtype.val_injective
end

lemma two_le_iff : (2 : cardinal) ‚â§ #Œ± ‚Üî ‚àÉx y : Œ±, x ‚â† y :=
begin
  split,
  { rintro ‚ü®f‚ü©, refine ‚ü®f $ sum.inl ‚ü®‚ü©, f $ sum.inr ‚ü®‚ü©, _‚ü©, intro h, cases f.2 h },
  { rintro ‚ü®x, y, h‚ü©, by_contra h',
    rw [not_le, ‚Üênat.cast_two, nat_succ, lt_succ, nat.cast_one, le_one_iff_subsingleton] at h',
    apply h, exactI subsingleton.elim _ _ }
end

lemma two_le_iff' (x : Œ±) : (2 : cardinal) ‚â§ #Œ± ‚Üî ‚àÉy : Œ±, x ‚â† y :=
begin
  rw [two_le_iff],
  split,
  { rintro ‚ü®y, z, h‚ü©, refine classical.by_cases (Œª(h' : x = y), _) (Œª h', ‚ü®y, h'‚ü©),
    rw [‚Üêh'] at h, exact ‚ü®z, h‚ü© },
  { rintro ‚ü®y, h‚ü©, exact ‚ü®x, y, h‚ü© }
end

lemma exists_not_mem_of_length_le {Œ± : Type*} (l : list Œ±) (h : ‚Üël.length < # Œ±) :
  ‚àÉ (z : Œ±), z ‚àâ l :=
begin
  contrapose! h,
  calc # Œ± = # (set.univ : set Œ±) : mk_univ.symm
    ... ‚â§ # l.to_finset           : mk_le_mk_of_subset (Œª x _, list.mem_to_finset.mpr (h x))
    ... = l.to_finset.card        : cardinal.mk_finset
    ... ‚â§ l.length                : cardinal.nat_cast_le.mpr (list.to_finset_card_le l),
end

lemma three_le {Œ± : Type*} (h : 3 ‚â§ # Œ±) (x : Œ±) (y : Œ±) :
  ‚àÉ (z : Œ±), z ‚â† x ‚àß z ‚â† y :=
begin
  have : ((3:nat) : cardinal) ‚â§ # Œ±, simpa using h,
  have : ((2:nat) : cardinal) < # Œ±, rwa [‚Üê cardinal.succ_le, ‚Üê cardinal.nat_succ],
  have := exists_not_mem_of_length_le [x, y] this,
  simpa [not_or_distrib] using this,
end

/-- The function Œ±^{<Œ≤}, defined to be sup_{Œ≥ < Œ≤} Œ±^Œ≥.
  We index over {s : set Œ≤.out // #s < Œ≤ } instead of {Œ≥ // Œ≥ < Œ≤}, because the latter lives in a
  higher universe -/
def powerlt (Œ± Œ≤ : cardinal.{u}) : cardinal.{u} :=
sup.{u u} (Œª(s : {s : set Œ≤.out // #s < Œ≤}), Œ± ^ mk.{u} s)

infix ` ^< `:80 := powerlt

theorem powerlt_aux {c c' : cardinal} (h : c < c') :
  ‚àÉ(s : {s : set c'.out // #s < c'}), #s = c :=
begin
  cases out_embedding.mp (le_of_lt h) with f,
  have : #‚Ü•(range ‚áëf) = c, { rwa [mk_range_eq, mk, quotient.out_eq c], exact f.2 },
  exact ‚ü®‚ü®range f, by convert h‚ü©, this‚ü©
end

lemma le_powerlt {c‚ÇÅ c‚ÇÇ c‚ÇÉ : cardinal} (h : c‚ÇÇ < c‚ÇÉ) : c‚ÇÅ ^ c‚ÇÇ ‚â§ c‚ÇÅ ^< c‚ÇÉ :=
by { rcases powerlt_aux h with ‚ü®s, rfl‚ü©, apply le_sup _ s }

lemma powerlt_le {c‚ÇÅ c‚ÇÇ c‚ÇÉ : cardinal} : c‚ÇÅ ^< c‚ÇÇ ‚â§ c‚ÇÉ ‚Üî ‚àÄ(c‚ÇÑ < c‚ÇÇ), c‚ÇÅ ^ c‚ÇÑ ‚â§ c‚ÇÉ :=
begin
  rw [powerlt, sup_le_iff],
  split,
  { intros h c‚ÇÑ hc‚ÇÑ, rcases powerlt_aux hc‚ÇÑ with ‚ü®s, rfl‚ü©, exact h s },
  intros h s, exact h _ s.2
end

lemma powerlt_le_powerlt_left {a b c : cardinal} (h : b ‚â§ c) : a ^< b ‚â§ a ^< c :=
by { rw [powerlt, sup_le_iff], exact Œª ‚ü®s, hs‚ü©, le_powerlt (lt_of_lt_of_le hs h) }

lemma powerlt_succ {c‚ÇÅ c‚ÇÇ : cardinal} (h : c‚ÇÅ ‚â† 0) : c‚ÇÅ ^< c‚ÇÇ.succ = c‚ÇÅ ^ c‚ÇÇ :=
begin
  apply le_antisymm,
  { rw powerlt_le, intros c‚ÇÉ h2, apply power_le_power_left h, rwa [‚Üêlt_succ] },
  { apply le_powerlt, apply lt_succ_self }
end

lemma powerlt_max {c‚ÇÅ c‚ÇÇ c‚ÇÉ : cardinal} : c‚ÇÅ ^< max c‚ÇÇ c‚ÇÉ = max (c‚ÇÅ ^< c‚ÇÇ) (c‚ÇÅ ^< c‚ÇÉ) :=
by { cases le_total c‚ÇÇ c‚ÇÉ; simp only [max_eq_left, max_eq_right, h, powerlt_le_powerlt_left] }

lemma zero_powerlt {a : cardinal} (h : a ‚â† 0) : 0 ^< a = 1 :=
begin
  apply le_antisymm,
  { rw [powerlt_le], intros c hc, apply zero_power_le },
  convert le_powerlt (pos_iff_ne_zero.2 h), rw [power_zero]
end

lemma powerlt_zero {a : cardinal} : a ^< 0 = 0 :=
begin
  convert sup_eq_zero,
  exact subtype.is_empty_of_false (Œª x, (zero_le _).not_lt),
end

end cardinal
