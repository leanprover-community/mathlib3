/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Floris van Doorn, Violeta Hern√°ndez Palacios
-/

import set_theory.cardinal.ordinal
import set_theory.ordinal.fixed_point

/-!
# Cofinality

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains the definition of cofinality of an ordinal number and regular cardinals

## Main Definitions

* `ordinal.cof o` is the cofinality of the ordinal `o`.
  If `o` is the order type of the relation `<` on `Œ±`, then `o.cof` is the smallest cardinality of a
  subset `s` of Œ± that is *cofinal* in `Œ±`, i.e. `‚àÄ x : Œ±, ‚àÉ y ‚àà s, ¬¨ y < x`.
* `cardinal.is_limit c` means that `c` is a (weak) limit cardinal: `c ‚â† 0 ‚àß ‚àÄ x < c, succ x < c`.
* `cardinal.is_strong_limit c` means that `c` is a strong limit cardinal:
  `c ‚â† 0 ‚àß ‚àÄ x < c, 2 ^ x < c`.
* `cardinal.is_regular c` means that `c` is a regular cardinal: `‚Ñµ‚ÇÄ ‚â§ c ‚àß c.ord.cof = c`.
* `cardinal.is_inaccessible c` means that `c` is strongly inaccessible:
  `‚Ñµ‚ÇÄ < c ‚àß is_regular c ‚àß is_strong_limit c`.

## Main Statements

* `ordinal.infinite_pigeonhole_card`: the infinite pigeonhole principle
* `cardinal.lt_power_cof`: A consequence of K√∂nig's theorem stating that `c < c ^ c.ord.cof` for
  `c ‚â• ‚Ñµ‚ÇÄ`
* `cardinal.univ_inaccessible`: The type of ordinals in `Type u` form an inaccessible cardinal
  (in `Type v` with `v > u`). This shows (externally) that in `Type u` there are at least `u`
  inaccessible cardinals.

## Implementation Notes

* The cofinality is defined for ordinals.
  If `c` is a cardinal number, its cofinality is `c.ord.cof`.

## Tags

cofinality, regular cardinals, limits cardinals, inaccessible cardinals,
infinite pigeonhole principle
-/

noncomputable theory

open function cardinal set order
open_locale classical cardinal ordinal

universes u v w
variables {Œ± : Type*} {r : Œ± ‚Üí Œ± ‚Üí Prop}

/-! ### Cofinality of orders -/

namespace order

/-- Cofinality of a reflexive order `‚âº`. This is the smallest cardinality
  of a subset `S : set Œ±` such that `‚àÄ a, ‚àÉ b ‚àà S, a ‚âº b`. -/
def cof (r : Œ± ‚Üí Œ± ‚Üí Prop) : cardinal :=
Inf {c | ‚àÉ S : set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = c}

/-- The set in the definition of `order.cof` is nonempty. -/
theorem cof_nonempty (r : Œ± ‚Üí Œ± ‚Üí Prop) [is_refl Œ± r] :
  {c | ‚àÉ S : set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = c}.nonempty :=
‚ü®_, set.univ, Œª a, ‚ü®a, ‚ü®‚ü©, refl _‚ü©, rfl‚ü©

lemma cof_le (r : Œ± ‚Üí Œ± ‚Üí Prop) {S : set Œ±} (h : ‚àÄ a, ‚àÉ b ‚àà S, r a b) : cof r ‚â§ #S :=
cInf_le' ‚ü®S, h, rfl‚ü©

lemma le_cof {r : Œ± ‚Üí Œ± ‚Üí Prop} [is_refl Œ± r] (c : cardinal) :
  c ‚â§ cof r ‚Üî ‚àÄ {S : set Œ±}, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚Üí c ‚â§ #S :=
begin
  rw [cof, le_cInf_iff'' (cof_nonempty r)],
  use Œª H S h, H _ ‚ü®S, h, rfl‚ü©,
  rintro H d ‚ü®S, h, rfl‚ü©,
  exact H h
end

end order

theorem rel_iso.cof_le_lift {Œ± : Type u} {Œ≤ : Type v} {r : Œ± ‚Üí Œ± ‚Üí Prop} {s}
  [is_refl Œ≤ s] (f : r ‚âÉr s) :
  cardinal.lift.{max u v} (order.cof r) ‚â§ cardinal.lift.{max u v} (order.cof s) :=
begin
  rw [order.cof, order.cof, lift_Inf, lift_Inf,
    le_cInf_iff'' (nonempty_image_iff.2 (order.cof_nonempty s))],
  rintros - ‚ü®-, ‚ü®u, H, rfl‚ü©, rfl‚ü©,
  apply cInf_le',
  refine ‚ü®_, ‚ü®f.symm '' u, Œª a, _, rfl‚ü©,
    lift_mk_eq.{u v (max u v)}.2 ‚ü®((f.symm).to_equiv.image u).symm‚ü©‚ü©,
  rcases H (f a) with ‚ü®b, hb, hb'‚ü©,
  refine ‚ü®f.symm b, mem_image_of_mem _ hb, f.map_rel_iff.1 _‚ü©,
  rwa rel_iso.apply_symm_apply
end

theorem rel_iso.cof_eq_lift {Œ± : Type u} {Œ≤ : Type v} {r s}
  [is_refl Œ± r] [is_refl Œ≤ s] (f : r ‚âÉr s) :
  cardinal.lift.{max u v} (order.cof r) = cardinal.lift.{max u v} (order.cof s) :=
(rel_iso.cof_le_lift f).antisymm (rel_iso.cof_le_lift f.symm)

theorem rel_iso.cof_le {Œ± Œ≤ : Type u} {r : Œ± ‚Üí Œ± ‚Üí Prop} {s} [is_refl Œ≤ s] (f : r ‚âÉr s) :
  order.cof r ‚â§ order.cof s :=
lift_le.1 (rel_iso.cof_le_lift f)

theorem rel_iso.cof_eq {Œ± Œ≤ : Type u} {r s} [is_refl Œ± r] [is_refl Œ≤ s] (f : r ‚âÉr s) :
  order.cof r = order.cof s :=
lift_inj.1 (rel_iso.cof_eq_lift f)

/-- Cofinality of a strict order `‚â∫`. This is the smallest cardinality of a set `S : set Œ±` such
that `‚àÄ a, ‚àÉ b ‚àà S, ¬¨ b ‚â∫ a`. -/
def strict_order.cof (r : Œ± ‚Üí Œ± ‚Üí Prop) : cardinal :=
order.cof (swap r)·∂ú

/-- The set in the definition of `order.strict_order.cof` is nonempty. -/
theorem strict_order.cof_nonempty (r : Œ± ‚Üí Œ± ‚Üí Prop) [is_irrefl Œ± r] :
  {c | ‚àÉ S : set Œ±, unbounded r S ‚àß #S = c}.nonempty :=
@order.cof_nonempty Œ± _ (is_refl.swap r·∂ú)

/-! ### Cofinality of ordinals -/

namespace ordinal

/-- Cofinality of an ordinal. This is the smallest cardinal of a
  subset `S` of the ordinal which is unbounded, in the sense
  `‚àÄ a, ‚àÉ b ‚àà S, a ‚â§ b`. It is defined for all ordinals, but
  `cof 0 = 0` and `cof (succ o) = 1`, so it is only really
  interesting on limit ordinals (when it is an infinite cardinal). -/
def cof (o : ordinal.{u}) : cardinal.{u} :=
o.lift_on (Œª a, strict_order.cof a.r)
begin
  rintros ‚ü®Œ±, r, wo‚ÇÅ‚ü© ‚ü®Œ≤, s, wo‚ÇÇ‚ü© ‚ü®‚ü®f, hf‚ü©‚ü©,
  haveI := wo‚ÇÅ, haveI := wo‚ÇÇ,
  apply @rel_iso.cof_eq _ _ _ _ _ _ ,
  { split, exact Œª a b, not_iff_not.2 hf },
  { exact ‚ü®(is_well_order.is_irrefl r).1‚ü© },
  { exact ‚ü®(is_well_order.is_irrefl s).1‚ü© }
end

lemma cof_type (r : Œ± ‚Üí Œ± ‚Üí Prop) [is_well_order Œ± r] : (type r).cof = strict_order.cof r := rfl

theorem le_cof_type [is_well_order Œ± r] {c} : c ‚â§ cof (type r) ‚Üî ‚àÄ S, unbounded r S ‚Üí c ‚â§ #S :=
(le_cInf_iff'' (strict_order.cof_nonempty r)).trans ‚ü®Œª H S h, H _ ‚ü®S, h, rfl‚ü©,
  by { rintros H d ‚ü®S, h, rfl‚ü©, exact H _ h }‚ü©

theorem cof_type_le [is_well_order Œ± r] {S : set Œ±} (h : unbounded r S) : cof (type r) ‚â§ #S :=
le_cof_type.1 le_rfl S h

theorem lt_cof_type [is_well_order Œ± r] {S : set Œ±} : #S < cof (type r) ‚Üí bounded r S :=
by simpa using not_imp_not.2 cof_type_le

theorem cof_eq (r : Œ± ‚Üí Œ± ‚Üí Prop) [is_well_order Œ± r] : ‚àÉ S, unbounded r S ‚àß #S = cof (type r) :=
Inf_mem (strict_order.cof_nonempty r)

theorem ord_cof_eq (r : Œ± ‚Üí Œ± ‚Üí Prop) [is_well_order Œ± r] :
  ‚àÉ S, unbounded r S ‚àß type (subrel r S) = (cof (type r)).ord :=
let ‚ü®S, hS, e‚ü© := cof_eq r, ‚ü®s, _, e'‚ü© := cardinal.ord_eq S,
    T : set Œ± := {a | ‚àÉ aS : a ‚àà S, ‚àÄ b : S, s b ‚ü®_, aS‚ü© ‚Üí r b a} in
begin
  resetI, suffices,
  { refine ‚ü®T, this,
      le_antisymm _ (cardinal.ord_le.2 $ cof_type_le this)‚ü©,
    rw [‚Üê e, e'],
    refine (rel_embedding.of_monotone (Œª a : T, (‚ü®a, let ‚ü®aS, _‚ü© := a.2 in aS‚ü© : S)) (Œª a b h, _))
      .ordinal_type_le,
    rcases a with ‚ü®a, aS, ha‚ü©, rcases b with ‚ü®b, bS, hb‚ü©,
    change s ‚ü®a, _‚ü© ‚ü®b, _‚ü©,
    refine ((trichotomous_of s _ _).resolve_left (Œª hn, _)).resolve_left _,
    { exact asymm h (ha _ hn) },
    { intro e, injection e with e, subst b,
      exact irrefl _ h } },
  { intro a,
    have : {b : S | ¬¨ r b a}.nonempty := let ‚ü®b, bS, ba‚ü© := hS a in ‚ü®‚ü®b, bS‚ü©, ba‚ü©,
    let b := (is_well_founded.wf).min _ this,
    have ba : ¬¨r b a := (is_well_founded.wf).min_mem _ this,
    refine ‚ü®b, ‚ü®b.2, Œª c, not_imp_not.1 $ Œª h, _‚ü©, ba‚ü©,
    rw [show ‚àÄb:S, (‚ü®b, b.2‚ü©:S) = b, by intro b; cases b; refl],
    exact (is_well_founded.wf).not_lt_min _ this
      (is_order_connected.neg_trans h ba) }
end

/-! ### Cofinality of suprema and least strict upper bounds -/

private theorem card_mem_cof {o} : ‚àÉ {Œπ} (f : Œπ ‚Üí ordinal), lsub.{u u} f = o ‚àß #Œπ = o.card :=
‚ü®_, _, lsub_typein o, mk_ordinal_out o‚ü©

/-- The set in the `lsub` characterization of `cof` is nonempty. -/
theorem cof_lsub_def_nonempty (o) :
  {a : cardinal | ‚àÉ {Œπ} (f : Œπ ‚Üí ordinal), lsub.{u u} f = o ‚àß #Œπ = a}.nonempty :=
‚ü®_, card_mem_cof‚ü©

theorem cof_eq_Inf_lsub (o : ordinal.{u}) :
  cof o = Inf {a : cardinal | ‚àÉ {Œπ : Type u} (f : Œπ ‚Üí ordinal), lsub.{u u} f = o ‚àß #Œπ = a} :=
begin
  refine le_antisymm (le_cInf (cof_lsub_def_nonempty o) _) (cInf_le' _),
  { rintros a ‚ü®Œπ, f, hf, rfl‚ü©,
    rw ‚Üêtype_lt o,
    refine (cof_type_le (Œª a, _)).trans (@mk_le_of_injective _ _
      (Œª s : (typein ((<) : o.out.Œ± ‚Üí o.out.Œ± ‚Üí Prop))‚Åª¬π' (set.range f), classical.some s.prop)
      (Œª s t hst, let H := congr_arg f hst in by rwa [classical.some_spec s.prop,
        classical.some_spec t.prop, typein_inj, subtype.coe_inj] at H)),
    have := typein_lt_self a,
    simp_rw [‚Üêhf, lt_lsub_iff] at this,
    cases this with i hi,
    refine ‚ü®enum (<) (f i) _, _, _‚ü©,
    { rw [type_lt, ‚Üêhf], apply lt_lsub },
    { rw [mem_preimage, typein_enum], exact mem_range_self i },
    { rwa [‚Üêtypein_le_typein, typein_enum] } },
  { rcases cof_eq (<) with ‚ü®S, hS, hS'‚ü©,
    let f : S ‚Üí ordinal := Œª s, typein (<) s.val,
    refine ‚ü®S, f, le_antisymm (lsub_le (Œª i, typein_lt_self i)) (le_of_forall_lt (Œª a ha, _)),
      by rwa type_lt o at hS'‚ü©,
    rw ‚Üêtype_lt o at ha,
    rcases hS (enum (<) a ha) with ‚ü®b, hb, hb'‚ü©,
    rw [‚Üêtypein_le_typein, typein_enum] at hb',
    exact hb'.trans_lt (lt_lsub.{u u} f ‚ü®b, hb‚ü©) }
end

@[simp] theorem lift_cof (o) : (cof o).lift = cof o.lift :=
begin
  refine induction_on o _,
  introsI Œ± r _,
  apply le_antisymm,
  { refine le_cof_type.2 (Œª S H, _),
    have : (#(ulift.up ‚Åª¬π' S)).lift ‚â§ #S,
    { rw [‚Üê cardinal.lift_umax, ‚Üê cardinal.lift_id' (#S)],
      exact mk_preimage_of_injective_lift ulift.up _ ulift.up_injective },
    refine (cardinal.lift_le.2 $ cof_type_le _).trans this,
    exact Œª a, let ‚ü®‚ü®b‚ü©, bs, br‚ü© := H ‚ü®a‚ü© in ‚ü®b, bs, br‚ü© },
  { rcases cof_eq r with ‚ü®S, H, e'‚ü©,
    have : #(ulift.down ‚Åª¬π' S) ‚â§ (#S).lift :=
     ‚ü®‚ü®Œª ‚ü®‚ü®x‚ü©, h‚ü©, ‚ü®‚ü®x, h‚ü©‚ü©,
       Œª ‚ü®‚ü®x‚ü©, h‚ÇÅ‚ü© ‚ü®‚ü®y‚ü©, h‚ÇÇ‚ü© e, by simp at e; congr; injections‚ü©‚ü©,
    rw e' at this,
    unfreezingI { refine (cof_type_le _).trans this },
    exact Œª ‚ü®a‚ü©, let ‚ü®b, bs, br‚ü© := H a in ‚ü®‚ü®b‚ü©, bs, br‚ü© }
end

theorem cof_le_card (o) : cof o ‚â§ card o :=
by { rw cof_eq_Inf_lsub, exact cInf_le' card_mem_cof }

theorem cof_ord_le (c : cardinal) : c.ord.cof ‚â§ c :=
by simpa using cof_le_card c.ord

theorem ord_cof_le (o : ordinal.{u}) : o.cof.ord ‚â§ o :=
(ord_le_ord.2 (cof_le_card o)).trans (ord_card_le o)

theorem exists_lsub_cof (o : ordinal) : ‚àÉ {Œπ} (f : Œπ ‚Üí ordinal), lsub.{u u} f = o ‚àß #Œπ = cof o :=
by { rw cof_eq_Inf_lsub, exact Inf_mem (cof_lsub_def_nonempty o) }

theorem cof_lsub_le {Œπ} (f : Œπ ‚Üí ordinal) : cof (lsub.{u u} f) ‚â§ #Œπ :=
by { rw cof_eq_Inf_lsub, exact cInf_le' ‚ü®Œπ, f, rfl, rfl‚ü© }

theorem cof_lsub_le_lift {Œπ} (f : Œπ ‚Üí ordinal) : cof (lsub f) ‚â§ cardinal.lift.{v u} (#Œπ) :=
begin
  rw ‚Üêmk_ulift,
  convert cof_lsub_le (Œª i : ulift Œπ, f i.down),
  exact lsub_eq_of_range_eq.{u (max u v) max u v}
    (set.ext (Œª x, ‚ü®Œª ‚ü®i, hi‚ü©, ‚ü®ulift.up i, hi‚ü©, Œª ‚ü®i, hi‚ü©, ‚ü®_, hi‚ü©‚ü©))
end

theorem le_cof_iff_lsub {o : ordinal} {a : cardinal} :
  a ‚â§ cof o ‚Üî ‚àÄ {Œπ} (f : Œπ ‚Üí ordinal), lsub.{u u} f = o ‚Üí a ‚â§ #Œπ :=
begin
  rw cof_eq_Inf_lsub,
  exact (le_cInf_iff'' (cof_lsub_def_nonempty o)).trans ‚ü®Œª H Œπ f hf, H _ ‚ü®Œπ, f, hf, rfl‚ü©,
    Œª H b ‚ü®Œπ, f, hf, hb‚ü©, ( by { rw ‚Üêhb, exact H _ hf} )‚ü©
end

theorem lsub_lt_ord_lift {Œπ} {f : Œπ ‚Üí ordinal} {c : ordinal} (hŒπ : cardinal.lift (#Œπ) < c.cof)
  (hf : ‚àÄ i, f i < c) : lsub.{u v} f < c :=
lt_of_le_of_ne (lsub_le hf) (Œª h, by { subst h, exact (cof_lsub_le_lift f).not_lt hŒπ })

theorem lsub_lt_ord {Œπ} {f : Œπ ‚Üí ordinal} {c : ordinal} (hŒπ : #Œπ < c.cof) :
  (‚àÄ i, f i < c) ‚Üí lsub.{u u} f < c :=
lsub_lt_ord_lift (by rwa (#Œπ).lift_id)

theorem cof_sup_le_lift {Œπ} {f : Œπ ‚Üí ordinal} (H : ‚àÄ i, f i < sup f) : cof (sup f) ‚â§ (#Œπ).lift :=
by { rw ‚Üêsup_eq_lsub_iff_lt_sup at H, rw H, exact cof_lsub_le_lift f }

theorem cof_sup_le {Œπ} {f : Œπ ‚Üí ordinal} (H : ‚àÄ i, f i < sup.{u u} f) : cof (sup.{u u} f) ‚â§ #Œπ :=
by { rw ‚Üê(#Œπ).lift_id, exact cof_sup_le_lift H }

theorem sup_lt_ord_lift {Œπ} {f : Œπ ‚Üí ordinal} {c : ordinal} (hŒπ : cardinal.lift (#Œπ) < c.cof)
  (hf : ‚àÄ i, f i < c) : sup.{u v} f < c :=
(sup_le_lsub.{u v} f).trans_lt (lsub_lt_ord_lift hŒπ hf)

theorem sup_lt_ord {Œπ} {f : Œπ ‚Üí ordinal} {c : ordinal} (hŒπ : #Œπ < c.cof) :
  (‚àÄ i, f i < c) ‚Üí sup.{u u} f < c :=
sup_lt_ord_lift (by rwa (#Œπ).lift_id)

theorem supr_lt_lift {Œπ} {f : Œπ ‚Üí cardinal} {c : cardinal} (hŒπ : cardinal.lift (#Œπ) < c.ord.cof)
  (hf : ‚àÄ i, f i < c) : supr f < c :=
begin
  rw [‚Üêord_lt_ord, supr_ord (cardinal.bdd_above_range _)],
  refine sup_lt_ord_lift hŒπ (Œª i, _),
  rw ord_lt_ord,
  apply hf
end

theorem supr_lt {Œπ} {f : Œπ ‚Üí cardinal} {c : cardinal} (hŒπ : #Œπ < c.ord.cof) :
  (‚àÄ i, f i < c) ‚Üí supr f < c :=
supr_lt_lift (by rwa (#Œπ).lift_id)

theorem nfp_family_lt_ord_lift {Œπ} {f : Œπ ‚Üí ordinal ‚Üí ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c)
  (hc' : (#Œπ).lift < cof c) (hf : ‚àÄ i (b < c), f i b < c) {a} (ha : a < c) :
  nfp_family.{u v} f a < c :=
begin
  refine sup_lt_ord_lift ((cardinal.lift_le.2 (mk_list_le_max Œπ)).trans_lt _) (Œª l, _),
  { rw lift_max,
    apply max_lt _ hc',
    rwa cardinal.lift_aleph_0 },
  { induction l with i l H,
    { exact ha },
    { exact hf _ _ H } }
end

theorem nfp_family_lt_ord {Œπ} {f : Œπ ‚Üí ordinal ‚Üí ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c)
  (hc' : #Œπ < cof c) (hf : ‚àÄ i (b < c), f i b < c) {a} : a < c ‚Üí nfp_family.{u u} f a < c :=
nfp_family_lt_ord_lift hc (by rwa (#Œπ).lift_id) hf

theorem nfp_bfamily_lt_ord_lift {o : ordinal} {f : Œ† a < o, ordinal ‚Üí ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c)
  (hc' : o.card.lift < cof c) (hf : ‚àÄ i hi (b < c), f i hi b < c) {a} :
  a < c ‚Üí nfp_bfamily.{u v} o f a < c :=
nfp_family_lt_ord_lift hc (by rwa mk_ordinal_out) (Œª i, hf _ _)

theorem nfp_bfamily_lt_ord {o : ordinal} {f : Œ† a < o, ordinal ‚Üí ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c)
  (hc' : o.card < cof c) (hf : ‚àÄ i hi (b < c), f i hi b < c) {a} :
  a < c ‚Üí nfp_bfamily.{u u} o f a < c :=
nfp_bfamily_lt_ord_lift hc (by rwa o.card.lift_id) hf

theorem nfp_lt_ord {f : ordinal ‚Üí ordinal} {c} (hc : ‚Ñµ‚ÇÄ < cof c) (hf : ‚àÄ i < c, f i < c) {a} :
  a < c ‚Üí nfp f a < c :=
nfp_family_lt_ord_lift hc (by simpa using cardinal.one_lt_aleph_0.trans hc) (Œª _, hf)

theorem exists_blsub_cof (o : ordinal) : ‚àÉ (f : Œ† a < (cof o).ord, ordinal), blsub.{u u} _ f = o :=
begin
  rcases exists_lsub_cof o with ‚ü®Œπ, f, hf, hŒπ‚ü©,
  rcases cardinal.ord_eq Œπ with ‚ü®r, hr, hŒπ'‚ü©,
  rw ‚Üê@blsub_eq_lsub' Œπ r hr at hf,
  rw [‚ÜêhŒπ, hŒπ'],
  exact ‚ü®_, hf‚ü©
end

theorem le_cof_iff_blsub {b : ordinal} {a : cardinal} :
  a ‚â§ cof b ‚Üî ‚àÄ {o} (f : Œ† a < o, ordinal), blsub.{u u} o f = b ‚Üí a ‚â§ o.card :=
le_cof_iff_lsub.trans ‚ü®Œª H o f hf, by simpa using H _ hf, Œª H Œπ f hf, begin
  rcases cardinal.ord_eq Œπ with ‚ü®r, hr, hŒπ'‚ü©,
  rw ‚Üê@blsub_eq_lsub' Œπ r hr at hf,
  simpa using H _ hf
end‚ü©

theorem cof_blsub_le_lift {o} (f : Œ† a < o, ordinal) :
  cof (blsub o f) ‚â§ cardinal.lift.{v u} (o.card) :=
by { convert cof_lsub_le_lift _, exact (mk_ordinal_out o).symm }

theorem cof_blsub_le {o} (f : Œ† a < o, ordinal) : cof (blsub.{u u} o f) ‚â§ o.card :=
by { rw ‚Üê(o.card).lift_id, exact cof_blsub_le_lift f }

theorem blsub_lt_ord_lift {o : ordinal} {f : Œ† a < o, ordinal} {c : ordinal}
  (ho : o.card.lift < c.cof) (hf : ‚àÄ i hi, f i hi < c) : blsub.{u v} o f < c :=
lt_of_le_of_ne (blsub_le hf) (Œª h, ho.not_le
  (by simpa [‚Üêsupr_ord, hf, h] using cof_blsub_le_lift.{u} f))

theorem blsub_lt_ord {o : ordinal} {f : Œ† a < o, ordinal} {c : ordinal} (ho : o.card < c.cof)
  (hf : ‚àÄ i hi, f i hi < c) : blsub.{u u} o f < c :=
blsub_lt_ord_lift (by rwa (o.card).lift_id) hf

theorem cof_bsup_le_lift {o : ordinal} {f : Œ† a < o, ordinal} (H : ‚àÄ i h, f i h < bsup o f) :
  cof (bsup o f) ‚â§ o.card.lift :=
by { rw ‚Üêbsup_eq_blsub_iff_lt_bsup at H, rw H, exact cof_blsub_le_lift f }

theorem cof_bsup_le {o : ordinal} {f : Œ† a < o, ordinal} :
  (‚àÄ i h, f i h < bsup.{u u} o f) ‚Üí cof (bsup.{u u} o f) ‚â§ o.card :=
by { rw ‚Üê(o.card).lift_id, exact cof_bsup_le_lift }

theorem bsup_lt_ord_lift {o : ordinal} {f : Œ† a < o, ordinal} {c : ordinal}
  (ho : o.card.lift < c.cof) (hf : ‚àÄ i hi, f i hi < c) : bsup.{u v} o f < c :=
(bsup_le_blsub f).trans_lt (blsub_lt_ord_lift ho hf)

theorem bsup_lt_ord {o : ordinal} {f : Œ† a < o, ordinal} {c : ordinal} (ho : o.card < c.cof) :
  (‚àÄ i hi, f i hi < c) ‚Üí bsup.{u u} o f < c :=
bsup_lt_ord_lift (by rwa (o.card).lift_id)

/-! ### Basic results -/

@[simp] theorem cof_zero : cof 0 = 0 :=
(cof_le_card 0).antisymm (cardinal.zero_le _)

@[simp] theorem cof_eq_zero {o} : cof o = 0 ‚Üî o = 0 :=
‚ü®induction_on o $ Œª Œ± r _ z, by exactI
  let ‚ü®S, hl, e‚ü© := cof_eq r in type_eq_zero_iff_is_empty.2 $
  ‚ü®Œª a, let ‚ü®b, h, _‚ü© := hl a in
    (mk_eq_zero_iff.1 (e.trans z)).elim' ‚ü®_, h‚ü©‚ü©,
Œª e, by simp [e]‚ü©

theorem cof_ne_zero {o} : cof o ‚â† 0 ‚Üî o ‚â† 0 := cof_eq_zero.not

@[simp] theorem cof_succ (o) : cof (succ o) = 1 :=
begin
  apply le_antisymm,
  { refine induction_on o (Œª Œ± r _, _),
    change cof (type _) ‚â§ _,
    rw [‚Üê (_ : #_ = 1)], apply cof_type_le,
    { refine Œª a, ‚ü®sum.inr punit.star, set.mem_singleton _, _‚ü©,
      rcases a with a|‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©; simp [empty_relation] },
    { rw [cardinal.mk_fintype, set.card_singleton], simp } },
  { rw [‚Üê cardinal.succ_zero, succ_le_iff],
    simpa [lt_iff_le_and_ne, cardinal.zero_le] using
      Œª h, succ_ne_zero o (cof_eq_zero.1 (eq.symm h)) }
end

@[simp] theorem cof_eq_one_iff_is_succ {o} : cof.{u} o = 1 ‚Üî ‚àÉ a, o = succ a :=
‚ü®induction_on o $ Œª Œ± r _ z, begin
  resetI,
  rcases cof_eq r with ‚ü®S, hl, e‚ü©, rw z at e,
  cases mk_ne_zero_iff.1 (by rw e; exact one_ne_zero) with a,
  refine ‚ü®typein r a, eq.symm $ quotient.sound
    ‚ü®rel_iso.of_surjective (rel_embedding.of_monotone _
      (Œª x y, _)) (Œª x, _)‚ü©‚ü©,
  { apply sum.rec; [exact subtype.val, exact Œª _, a] },
  { rcases x with x|‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©; rcases y with y|‚ü®‚ü®‚ü®‚ü©‚ü©‚ü©;
      simp [subrel, order.preimage, empty_relation],
    exact x.2 },
  { suffices : r x a ‚à® ‚àÉ (b : punit), ‚Üëa = x, {simpa},
    rcases trichotomous_of r x a with h|h|h,
    { exact or.inl h },
    { exact or.inr ‚ü®punit.star, h.symm‚ü© },
    { rcases hl x with ‚ü®a', aS, hn‚ü©,
      rw (_ : ‚Üëa = a') at h, {exact absurd h hn},
      refine congr_arg subtype.val (_ : a = ‚ü®a', aS‚ü©),
      haveI := le_one_iff_subsingleton.1 (le_of_eq e),
      apply subsingleton.elim } }
end, Œª ‚ü®a, e‚ü©, by simp [e]‚ü©

/-- A fundamental sequence for `a` is an increasing sequence of length `o = cof a` that converges at
    `a`. We provide `o` explicitly in order to avoid type rewrites. -/
def is_fundamental_sequence (a o : ordinal.{u}) (f : Œ† b < o, ordinal.{u}) : Prop :=
o ‚â§ a.cof.ord ‚àß (‚àÄ {i j} (hi hj), i < j ‚Üí f i hi < f j hj) ‚àß blsub.{u u} o f = a

namespace is_fundamental_sequence
variables {a o : ordinal.{u}} {f : Œ† b < o, ordinal.{u}}

protected theorem cof_eq (hf : is_fundamental_sequence a o f) : a.cof.ord = o :=
hf.1.antisymm' $ by { rw ‚Üêhf.2.2, exact (ord_le_ord.2 (cof_blsub_le f)).trans (ord_card_le o) }

protected theorem strict_mono (hf : is_fundamental_sequence a o f) {i j} :
  ‚àÄ hi hj, i < j ‚Üí f i hi < f j hj :=
hf.2.1

theorem blsub_eq (hf : is_fundamental_sequence a o f) : blsub.{u u} o f = a :=
hf.2.2

theorem ord_cof (hf : is_fundamental_sequence a o f) :
  is_fundamental_sequence a a.cof.ord (Œª i hi, f i (hi.trans_le (by rw hf.cof_eq))) :=
by { have H := hf.cof_eq, subst H, exact hf }

theorem id_of_le_cof (h : o ‚â§ o.cof.ord) : is_fundamental_sequence o o (Œª a _, a) :=
‚ü®h, Œª _ _ _ _, id, blsub_id o‚ü©

protected theorem zero {f : Œ† b < (0 : ordinal), ordinal} :
  is_fundamental_sequence 0 0 f :=
‚ü®by rw [cof_zero, ord_zero], Œª i j hi, (ordinal.not_lt_zero i hi).elim, blsub_zero f‚ü©

protected theorem succ : is_fundamental_sequence (succ o) 1 (Œª _ _, o) :=
begin
  refine ‚ü®_, Œª i j hi hj h, _, blsub_const ordinal.one_ne_zero o‚ü©,
  { rw [cof_succ, ord_one] },
  { rw lt_one_iff_zero at hi hj,
    rw [hi, hj] at h,
    exact h.false.elim }
end

protected theorem monotone (hf : is_fundamental_sequence a o f) {i j : ordinal} (hi : i < o)
  (hj : j < o) (hij : i ‚â§ j) : f i hi ‚â§ f j hj :=
begin
  rcases lt_or_eq_of_le hij with hij | rfl,
  { exact (hf.2.1 hi hj hij).le },
  { refl }
end

theorem trans {a o o' : ordinal.{u}} {f : Œ† b < o, ordinal.{u}}
  (hf : is_fundamental_sequence a o f) {g : Œ† b < o', ordinal.{u}}
  (hg : is_fundamental_sequence o o' g) :
  is_fundamental_sequence a o' (Œª i hi, f (g i hi) (by { rw ‚Üêhg.2.2, apply lt_blsub })) :=
begin
  refine ‚ü®_, Œª i j _ _ h, hf.2.1 _ _ (hg.2.1 _ _ h), _‚ü©,
  { rw hf.cof_eq,
    exact hg.1.trans (ord_cof_le o) },
  { rw @blsub_comp.{u u u} o _ f (@is_fundamental_sequence.monotone _ _ f hf),
    exact hf.2.2 }
end

end is_fundamental_sequence

/-- Every ordinal has a fundamental sequence. -/
theorem exists_fundamental_sequence (a : ordinal.{u}) :
  ‚àÉ f, is_fundamental_sequence a a.cof.ord f :=
begin
  rsuffices ‚ü®o, f, hf‚ü© : ‚àÉ o f, is_fundamental_sequence a o f,
  { exact ‚ü®_, hf.ord_cof‚ü© },
  rcases exists_lsub_cof a with ‚ü®Œπ, f, hf, hŒπ‚ü©,
  rcases ord_eq Œπ with ‚ü®r, wo, hr‚ü©,
  haveI := wo,
  let r' := subrel r {i | ‚àÄ j, r j i ‚Üí f j < f i},
  let hrr' : r' ‚Ü™r r := subrel.rel_embedding _ _,
  haveI := hrr'.is_well_order,
  refine ‚ü®_, _, hrr'.ordinal_type_le.trans _, Œª i j _ h _, (enum r' j h).prop _ _,
    le_antisymm (blsub_le (Œª i hi, lsub_le_iff.1 hf.le _)) _‚ü©,
  { rw [‚ÜêhŒπ, hr] },
  { change r (hrr'.1 _ ) (hrr'.1 _ ),
    rwa [hrr'.2, @enum_lt_enum _ r'] },
  { rw [‚Üêhf, lsub_le_iff],
    intro i,
    rsuffices ‚ü®i', hi', hfg‚ü© : ‚àÉ i' hi', f i ‚â§ bfamily_of_family' r' (Œª i, f i) i' hi',
    { exact hfg.trans_lt (lt_blsub _ _ _) },
    by_cases h : ‚àÄ j, r j i ‚Üí f j < f i,
    { refine ‚ü®typein r' ‚ü®i, h‚ü©, typein_lt_type _ _, _‚ü©,
      rw bfamily_of_family'_typein,
      refl },
    { push_neg at h,
      cases wo.wf.min_mem _ h with hji hij,
      refine ‚ü®typein r' ‚ü®_, Œª k hkj, lt_of_lt_of_le _ hij‚ü©, typein_lt_type _ _, _‚ü©,
      { by_contra' H,
        exact (wo.wf.not_lt_min _ h ‚ü®is_trans.trans _ _ _ hkj hji, H‚ü©) hkj },
      { rwa bfamily_of_family'_typein } } }
end

@[simp] theorem cof_cof (a : ordinal.{u}) : cof (cof a).ord = cof a :=
begin
  cases exists_fundamental_sequence a with f hf,
  cases exists_fundamental_sequence a.cof.ord with g hg,
  exact ord_injective ((hf.trans hg).cof_eq.symm)
end

protected theorem is_normal.is_fundamental_sequence {f : ordinal.{u} ‚Üí ordinal.{u}}
  (hf : is_normal f) {a o} (ha : is_limit a) {g} (hg : is_fundamental_sequence a o g) :
  is_fundamental_sequence (f a) o (Œª b hb, f (g b hb)) :=
begin
  refine ‚ü®_, Œª i j _ _ h, hf.strict_mono (hg.2.1 _ _ h), _‚ü©,
  { rcases exists_lsub_cof (f a) with ‚ü®Œπ, f', hf', hŒπ‚ü©,
    rw [‚Üêhg.cof_eq, ord_le_ord, ‚ÜêhŒπ],
    suffices : lsub.{u u} (Œª i, (Inf {b : ordinal | f' i ‚â§ f b})) = a,
    { rw ‚Üêthis,
      apply cof_lsub_le },
    have H : ‚àÄ i, ‚àÉ b < a, f' i ‚â§ f b := Œª i, begin
      have := lt_lsub.{u u} f' i,
      rwa [hf', ‚Üêis_normal.blsub_eq.{u u} hf ha, lt_blsub_iff] at this
    end,
    refine (lsub_le (Œª i, _)).antisymm (le_of_forall_lt (Œª b hb, _)),
    { rcases H i with ‚ü®b, hb, hb'‚ü©,
      exact lt_of_le_of_lt (cInf_le' hb') hb },
    { have := hf.strict_mono hb,
      rw [‚Üêhf', lt_lsub_iff] at this,
      cases this with i hi,
      rcases H i with ‚ü®b, _, hb‚ü©,
      exact ((le_cInf_iff'' ‚ü®b, hb‚ü©).2 (Œª c hc, hf.strict_mono.le_iff_le.1 (hi.trans hc))).trans_lt
        (lt_lsub _ i) } },
  { rw @blsub_comp.{u u u} a _ (Œª b _, f b) (Œª i j hi hj h, hf.strict_mono.monotone h) g hg.2.2,
    exact is_normal.blsub_eq.{u u} hf ha }
end

theorem is_normal.cof_eq {f} (hf : is_normal f) {a} (ha : is_limit a) : cof (f a) = cof a :=
let ‚ü®g, hg‚ü© := exists_fundamental_sequence a in
  ord_injective (hf.is_fundamental_sequence ha hg).cof_eq

theorem is_normal.cof_le {f} (hf : is_normal f) (a) : cof a ‚â§ cof (f a) :=
begin
  rcases zero_or_succ_or_limit a with rfl | ‚ü®b, rfl‚ü© | ha,
  { rw cof_zero,
    exact zero_le _ },
  { rw [cof_succ, cardinal.one_le_iff_ne_zero, cof_ne_zero, ‚Üêordinal.pos_iff_ne_zero],
    exact (ordinal.zero_le (f b)).trans_lt (hf.1 b) },
  { rw hf.cof_eq ha }
end

@[simp] theorem cof_add (a b : ordinal) : b ‚â† 0 ‚Üí cof (a + b) = cof b :=
Œª h, begin
  rcases zero_or_succ_or_limit b with rfl | ‚ü®c, rfl‚ü© | hb,
  { contradiction },
  { rw [add_succ, cof_succ, cof_succ] },
  { exact (add_is_normal a).cof_eq hb }
end

theorem aleph_0_le_cof {o} : ‚Ñµ‚ÇÄ ‚â§ cof o ‚Üî is_limit o :=
begin
  rcases zero_or_succ_or_limit o with rfl|‚ü®o,rfl‚ü©|l,
  { simp [not_zero_is_limit, cardinal.aleph_0_ne_zero] },
  { simp [not_succ_is_limit, cardinal.one_lt_aleph_0] },
  { simp [l], refine le_of_not_lt (Œª h, _),
    cases cardinal.lt_aleph_0.1 h with n e,
    have := cof_cof o,
    rw [e, ord_nat] at this,
    cases n,
    { simp at e, simpa [e, not_zero_is_limit] using l },
    { rw [nat_cast_succ, cof_succ] at this,
      rw [‚Üê this, cof_eq_one_iff_is_succ] at e,
      rcases e with ‚ü®a, rfl‚ü©,
      exact not_succ_is_limit _ l } }
end

@[simp] theorem aleph'_cof {o : ordinal} (ho : o.is_limit) : (aleph' o).ord.cof = o.cof :=
aleph'_is_normal.cof_eq ho

@[simp] theorem aleph_cof {o : ordinal} (ho : o.is_limit) : (aleph o).ord.cof = o.cof :=
aleph_is_normal.cof_eq ho

@[simp] theorem cof_omega : cof œâ = ‚Ñµ‚ÇÄ :=
(aleph_0_le_cof.2 omega_is_limit).antisymm' $ by { rw ‚Üêcard_omega, apply cof_le_card }

theorem cof_eq' (r : Œ± ‚Üí Œ± ‚Üí Prop) [is_well_order Œ± r] (h : is_limit (type r)) :
  ‚àÉ S : set Œ±, (‚àÄ a, ‚àÉ b ‚àà S, r a b) ‚àß #S = cof (type r) :=
let ‚ü®S, H, e‚ü© := cof_eq r in
‚ü®S, Œª a,
  let a' := enum r _ (h.2 _ (typein_lt_type r a)) in
  let ‚ü®b, h, ab‚ü© := H a' in
  ‚ü®b, h, (is_order_connected.conn a b a' $ (typein_lt_typein r).1
    (by { rw typein_enum, exact lt_succ (typein _ _) })).resolve_right ab‚ü©,
e‚ü©

@[simp] theorem cof_univ : cof univ.{u v} = cardinal.univ :=
le_antisymm (cof_le_card _) begin
  refine le_of_forall_lt (Œª c h, _),
  rcases lt_univ'.1 h with ‚ü®c, rfl‚ü©,
  rcases @cof_eq ordinal.{u} (<) _ with ‚ü®S, H, Se‚ü©,
  rw [univ, ‚Üê lift_cof, ‚Üê cardinal.lift_lift, cardinal.lift_lt, ‚Üê Se],
  refine lt_of_not_ge (Œª h, _),
  cases cardinal.lift_down h with a e,
  refine quotient.induction_on a (Œª Œ± e, _) e,
  cases quotient.exact e with f,
  have f := equiv.ulift.symm.trans f,
  let g := Œª a, (f a).1,
  let o := succ (sup.{u u} g),
  rcases H o with ‚ü®b, h, l‚ü©,
  refine l (lt_succ_iff.2 _),
  rw ‚Üê show g (f.symm ‚ü®b, h‚ü©) = b, by dsimp [g]; simp,
  apply le_sup
end

/-! ### Infinite pigeonhole principle -/

/-- If the union of s is unbounded and s is smaller than the cofinality,
  then s has an unbounded member -/
theorem unbounded_of_unbounded_sUnion (r : Œ± ‚Üí Œ± ‚Üí Prop) [wo : is_well_order Œ± r] {s : set (set Œ±)}
  (h‚ÇÅ : unbounded r $ ‚ãÉ‚ÇÄ s) (h‚ÇÇ : #s < strict_order.cof r) : ‚àÉ x ‚àà s, unbounded r x :=
begin
  by_contra' h,
  simp_rw not_unbounded_iff at h,
  let f : s ‚Üí Œ± := Œª x : s, wo.wf.sup x (h x.1 x.2),
  refine h‚ÇÇ.not_le (le_trans (cInf_le' ‚ü®range f, Œª x, _, rfl‚ü©) mk_range_le),
  rcases h‚ÇÅ x with ‚ü®y, ‚ü®c, hc, hy‚ü©, hxy‚ü©,
  exact ‚ü®f ‚ü®c, hc‚ü©, mem_range_self _, Œª hxz, hxy (trans (wo.wf.lt_sup _ hy) hxz)‚ü©
end

/-- If the union of s is unbounded and s is smaller than the cofinality,
  then s has an unbounded member -/
theorem unbounded_of_unbounded_Union {Œ± Œ≤ : Type u} (r : Œ± ‚Üí Œ± ‚Üí Prop) [wo : is_well_order Œ± r]
  (s : Œ≤ ‚Üí set Œ±)
  (h‚ÇÅ : unbounded r $ ‚ãÉ x, s x) (h‚ÇÇ : #Œ≤ < strict_order.cof r) : ‚àÉ x : Œ≤, unbounded r (s x) :=
begin
  rw ‚ÜêsUnion_range at h‚ÇÅ,
  rcases unbounded_of_unbounded_sUnion r h‚ÇÅ (mk_range_le.trans_lt h‚ÇÇ) with ‚ü®_, ‚ü®x, rfl‚ü©, u‚ü©,
  exact ‚ü®x, u‚ü©
end

/-- The infinite pigeonhole principle -/
theorem infinite_pigeonhole {Œ≤ Œ± : Type u} (f : Œ≤ ‚Üí Œ±) (h‚ÇÅ : ‚Ñµ‚ÇÄ ‚â§ #Œ≤)
  (h‚ÇÇ : #Œ± < (#Œ≤).ord.cof) : ‚àÉ a : Œ±, #(f ‚Åª¬π' {a}) = #Œ≤ :=
begin
  have : ‚àÉ a, #Œ≤ ‚â§ #(f ‚Åª¬π' {a}),
  { by_contra' h,
    apply mk_univ.not_lt,
    rw [‚Üêpreimage_univ, ‚ÜêUnion_of_singleton, preimage_Union],
    exact mk_Union_le_sum_mk.trans_lt ((sum_le_supr _).trans_lt $ mul_lt_of_lt h‚ÇÅ
      (h‚ÇÇ.trans_le $ cof_ord_le _) (supr_lt h‚ÇÇ h)) },
  cases this with x h,
  refine ‚ü®x, h.antisymm' _‚ü©,
  rw le_mk_iff_exists_set,
  exact ‚ü®_, rfl‚ü©
end

/-- Pigeonhole principle for a cardinality below the cardinality of the domain -/
theorem infinite_pigeonhole_card {Œ≤ Œ± : Type u} (f : Œ≤ ‚Üí Œ±) (Œ∏ : cardinal) (hŒ∏ : Œ∏ ‚â§ #Œ≤)
  (h‚ÇÅ : ‚Ñµ‚ÇÄ ‚â§ Œ∏) (h‚ÇÇ : #Œ± < Œ∏.ord.cof) : ‚àÉ a : Œ±, Œ∏ ‚â§ #(f ‚Åª¬π' {a}) :=
begin
  rcases le_mk_iff_exists_set.1 hŒ∏ with ‚ü®s, rfl‚ü©,
  cases infinite_pigeonhole (f ‚àò subtype.val : s ‚Üí Œ±) h‚ÇÅ h‚ÇÇ with a ha,
  use a, rw [‚Üêha, @preimage_comp _ _ _ subtype.val f],
  exact mk_preimage_of_injective _ _ subtype.val_injective
end

theorem infinite_pigeonhole_set {Œ≤ Œ± : Type u} {s : set Œ≤} (f : s ‚Üí Œ±) (Œ∏ : cardinal)
  (hŒ∏ : Œ∏ ‚â§ #s) (h‚ÇÅ : ‚Ñµ‚ÇÄ ‚â§ Œ∏) (h‚ÇÇ : #Œ± < Œ∏.ord.cof) :
    ‚àÉ (a : Œ±) (t : set Œ≤) (h : t ‚äÜ s), Œ∏ ‚â§ #t ‚àß ‚àÄ {{x}} (hx : x ‚àà t), f ‚ü®x, h hx‚ü© = a :=
begin
  cases infinite_pigeonhole_card f Œ∏ hŒ∏ h‚ÇÅ h‚ÇÇ with a ha,
  refine ‚ü®a, {x | ‚àÉ h, f ‚ü®x, h‚ü© = a}, _, _, _‚ü©,
  { rintro x ‚ü®hx, hx'‚ü©, exact hx },
  { refine ha.trans (ge_of_eq $ quotient.sound ‚ü®equiv.trans _
      (equiv.subtype_subtype_equiv_subtype_exists _ _).symm‚ü©),
    simp only [coe_eq_subtype, mem_singleton_iff, mem_preimage, mem_set_of_eq] },
  rintro x ‚ü®hx, hx'‚ü©, exact hx'
end

end ordinal

/-! ### Regular and inaccessible cardinals -/

namespace cardinal
open ordinal

local infixr (name := cardinal.pow) ^ := @pow cardinal.{u} cardinal cardinal.has_pow

/-- A cardinal is a limit if it is not zero or a successor
  cardinal. Note that `‚Ñµ‚ÇÄ` is a limit cardinal by this definition. -/
def is_limit (c : cardinal) : Prop :=
c ‚â† 0 ‚àß ‚àÄ x < c, succ x < c

theorem is_limit.ne_zero {c} (h : is_limit c) : c ‚â† 0 :=
h.1

theorem is_limit.succ_lt {x c} (h : is_limit c) : x < c ‚Üí succ x < c :=
h.2 x

theorem is_limit.aleph_0_le {c} (h : is_limit c) : ‚Ñµ‚ÇÄ ‚â§ c :=
begin
  by_contra' h',
  rcases lt_aleph_0.1 h' with ‚ü®_ | n, rfl‚ü©,
  { exact h.1.irrefl },
  { simpa using h.2 n }
end

/-- A cardinal is a strong limit if it is not zero and it is
  closed under powersets. Note that `‚Ñµ‚ÇÄ` is a strong limit by this definition. -/
def is_strong_limit (c : cardinal) : Prop :=
c ‚â† 0 ‚àß ‚àÄ x < c, 2 ^ x < c

theorem is_strong_limit.ne_zero {c} (h : is_strong_limit c) : c ‚â† 0 :=
h.1

theorem is_strong_limit.two_power_lt {x c} (h : is_strong_limit c) : x < c ‚Üí 2 ^ x < c :=
h.2 x

theorem is_strong_limit_aleph_0 : is_strong_limit ‚Ñµ‚ÇÄ :=
‚ü®aleph_0_ne_zero, Œª x hx, begin
  rcases lt_aleph_0.1 hx with ‚ü®n, rfl‚ü©,
  exact_mod_cast nat_lt_aleph_0 (pow 2 n)
end‚ü©

theorem is_strong_limit.is_limit {c} (H : is_strong_limit c) : is_limit c :=
‚ü®H.1, Œª x h, (succ_le_of_lt $ cantor x).trans_lt (H.2 _ h)‚ü©

theorem is_limit_aleph_0 : is_limit ‚Ñµ‚ÇÄ :=
is_strong_limit_aleph_0.is_limit

theorem is_strong_limit_beth {o : ordinal} (H : ‚àÄ a < o, succ a < o) : is_strong_limit (beth o) :=
begin
  rcases eq_or_ne o 0 with rfl | h,
  { rw beth_zero,
    exact is_strong_limit_aleph_0 },
  { refine ‚ü®beth_ne_zero o, Œª a ha, _‚ü©,
    rw beth_limit ‚ü®h, H‚ü© at ha,
    rcases exists_lt_of_lt_csupr' ha with ‚ü®‚ü®i, hi‚ü©, ha‚ü©,
    have := power_le_power_left two_ne_zero ha.le,
    rw ‚Üêbeth_succ at this,
    exact this.trans_lt (beth_lt.2 (H i hi)) }
end

theorem mk_bounded_subset {Œ± : Type*} (h : ‚àÄ x < #Œ±, 2 ^ x < #Œ±) {r : Œ± ‚Üí Œ± ‚Üí Prop}
  [is_well_order Œ± r] (hr : (#Œ±).ord = type r) : #{s : set Œ± // bounded r s} = #Œ± :=
begin
  rcases eq_or_ne (#Œ±) 0 with ha | ha,
  { rw ha,
    haveI := mk_eq_zero_iff.1 ha,
    rw mk_eq_zero_iff,
    split,
    rintro ‚ü®s, hs‚ü©,
    exact (not_unbounded_iff s).2 hs (unbounded_of_is_empty s) },
  have h' : is_strong_limit (#Œ±) := ‚ü®ha, h‚ü©,
  have ha := h'.is_limit.aleph_0_le,
  apply le_antisymm,
  { have : {s : set Œ± | bounded r s} = ‚ãÉ i, ùí´ {j | r j i} := set_of_exists _,
    rw [‚Üêcoe_set_of, this],
    convert mk_Union_le_sum_mk.trans ((sum_le_supr _).trans (mul_le_max_of_aleph_0_le_left ha)),
    apply (max_eq_left _).symm, apply csupr_le' (Œª i, _),
    rw mk_powerset,
    apply (h'.two_power_lt _).le,
    rw [coe_set_of, card_typein, ‚Üêlt_ord, hr],
    apply typein_lt_type },
  { refine @mk_le_of_injective Œ± _ (Œª x, subtype.mk {x} _) _,
    { apply bounded_singleton,
      rw ‚Üêhr,
      apply ord_is_limit ha },
    { intros a b hab,
      simpa only [singleton_eq_singleton_iff] using hab } }
end

theorem mk_subset_mk_lt_cof {Œ± : Type*} (h : ‚àÄ x < #Œ±, 2 ^ x < #Œ±) :
  #{s : set Œ± // #s < cof (#Œ±).ord} = #Œ± :=
begin
  rcases eq_or_ne (#Œ±) 0 with ha | ha,
  { rw ha,
    simp [Œª s, (cardinal.zero_le s).not_lt] },
  have h' : is_strong_limit (#Œ±) := ‚ü®ha, h‚ü©,
  rcases ord_eq Œ± with ‚ü®r, wo, hr‚ü©,
  haveI := wo,
  apply le_antisymm,
  { nth_rewrite_rhs 0 ‚Üêmk_bounded_subset h hr,
    apply mk_le_mk_of_subset (Œª s hs, _),
    rw hr at hs,
    exact lt_cof_type hs },
  { refine @mk_le_of_injective Œ± _ (Œª x, subtype.mk {x} _) _,
    { rw mk_singleton,
      exact one_lt_aleph_0.trans_le (aleph_0_le_cof.2 (ord_is_limit h'.is_limit.aleph_0_le)) },
    { intros a b hab,
      simpa only [singleton_eq_singleton_iff] using hab } }
end

/-- A cardinal is regular if it is infinite and it equals its own cofinality. -/
def is_regular (c : cardinal) : Prop :=
‚Ñµ‚ÇÄ ‚â§ c ‚àß c ‚â§ c.ord.cof

lemma is_regular.aleph_0_le {c : cardinal} (H : c.is_regular) : ‚Ñµ‚ÇÄ ‚â§ c :=
H.1

lemma is_regular.cof_eq {c : cardinal} (H : c.is_regular) : c.ord.cof = c :=
(cof_ord_le c).antisymm H.2

lemma is_regular.pos {c : cardinal} (H : c.is_regular) : 0 < c :=
aleph_0_pos.trans_le H.1

lemma is_regular.ord_pos {c : cardinal} (H : c.is_regular) : 0 < c.ord :=
by { rw cardinal.lt_ord, exact H.pos }

theorem is_regular_cof {o : ordinal} (h : o.is_limit) : is_regular o.cof :=
‚ü®aleph_0_le_cof.2 h, (cof_cof o).ge‚ü©

theorem is_regular_aleph_0 : is_regular ‚Ñµ‚ÇÄ :=
‚ü®le_rfl, by simp‚ü©

theorem is_regular_succ {c : cardinal.{u}} (h : ‚Ñµ‚ÇÄ ‚â§ c) : is_regular (succ c) :=
‚ü®h.trans (le_succ c), succ_le_of_lt begin
  cases quotient.exists_rep (@succ cardinal _ _ c) with Œ± Œ±e, simp at Œ±e,
  rcases ord_eq Œ± with ‚ü®r, wo, re‚ü©, resetI,
  have := ord_is_limit (h.trans (le_succ _)),
  rw [‚Üê Œ±e, re] at this ‚ä¢,
  rcases cof_eq' r this with ‚ü®S, H, Se‚ü©,
  rw [‚Üê Se],
  apply lt_imp_lt_of_le_imp_le (Œª h, mul_le_mul_right' h c),
  rw [mul_eq_self h, ‚Üê succ_le_iff, ‚Üê Œ±e, ‚Üê sum_const'],
  refine le_trans _ (sum_le_sum (Œª x, card (typein r x)) _ (Œª i, _)),
  { simp only [‚Üê card_typein, ‚Üê mk_sigma],
    exact ‚ü®embedding.of_surjective (Œª x, x.2.1)
      (Œª a, let ‚ü®b, h, ab‚ü© := H a in ‚ü®‚ü®‚ü®_, h‚ü©, _, ab‚ü©, rfl‚ü©)‚ü© },
  { rw [‚Üê lt_succ_iff, ‚Üê lt_ord, ‚Üê Œ±e, re],
    apply typein_lt_type }
end‚ü©

theorem is_regular_aleph_one : is_regular (aleph 1) :=
by { rw ‚Üêsucc_aleph_0, exact is_regular_succ le_rfl }

theorem is_regular_aleph'_succ {o : ordinal} (h : œâ ‚â§ o) : is_regular (aleph' (succ o)) :=
by { rw aleph'_succ, exact is_regular_succ (aleph_0_le_aleph'.2 h) }

theorem is_regular_aleph_succ (o : ordinal) : is_regular (aleph (succ o)) :=
by { rw aleph_succ, exact is_regular_succ (aleph_0_le_aleph o) }

/--
A function whose codomain's cardinality is infinite but strictly smaller than its domain's
has a fiber with cardinality strictly great than the codomain.
-/
theorem infinite_pigeonhole_card_lt {Œ≤ Œ± : Type u} (f : Œ≤ ‚Üí Œ±)
  (w : #Œ± < #Œ≤) (w' : ‚Ñµ‚ÇÄ ‚â§ #Œ±) :
  ‚àÉ a : Œ±, #Œ± < #(f ‚Åª¬π' {a}) :=
begin
  simp_rw [‚Üê succ_le_iff],
  exact ordinal.infinite_pigeonhole_card f (succ (#Œ±)) (succ_le_of_lt w)
    (w'.trans (lt_succ _).le)
    ((lt_succ _).trans_le (is_regular_succ w').2.ge),
end

/--
A function whose codomain's cardinality is infinite but strictly smaller than its domain's
has an infinite fiber.
-/
theorem exists_infinite_fiber {Œ≤ Œ± : Type*} (f : Œ≤ ‚Üí Œ±)
  (w : #Œ± < #Œ≤) (w' : _root_.infinite Œ±) :
  ‚àÉ a : Œ±, _root_.infinite (f ‚Åª¬π' {a}) :=
begin
  simp_rw [cardinal.infinite_iff] at ‚ä¢ w',
  cases infinite_pigeonhole_card_lt f w w' with a ha,
  exact ‚ü®a, w'.trans ha.le‚ü©,
end

/--
If an infinite type `Œ≤` can be expressed as a union of finite sets,
then the cardinality of the collection of those finite sets
must be at least the cardinality of `Œ≤`.
-/
lemma le_range_of_union_finset_eq_top
  {Œ± Œ≤ : Type*} [infinite Œ≤] (f : Œ± ‚Üí finset Œ≤) (w : (‚ãÉ a, (f a : set Œ≤)) = ‚ä§) :
  #Œ≤ ‚â§ #(range f) :=
begin
  have k : _root_.infinite (range f),
  { rw infinite_coe_iff,
    apply mt (union_finset_finite_of_range_finite f),
    rw w,
    exact infinite_univ, },
  by_contradiction h,
  simp only [not_le] at h,
  let u : Œ† b, ‚àÉ a, b ‚àà f a := Œª b, by simpa using (w.ge : _) (set.mem_univ b),
  let u' : Œ≤ ‚Üí range f := Œª b, ‚ü®f (u b).some, by simp‚ü©,
  have v' : ‚àÄ a, u' ‚Åª¬π' {‚ü®f a, by simp‚ü©} ‚â§ f a, begin rintros a p m,
    simp at m,
    rw ‚Üêm,
    apply (Œª b, (u b).some_spec),
  end,
  obtain ‚ü®‚ü®-, ‚ü®a, rfl‚ü©‚ü©, p‚ü© := exists_infinite_fiber u' h k,
  exact (@infinite.of_injective _ _ p (inclusion (v' a)) (inclusion_injective _)).false,
end

theorem lsub_lt_ord_lift_of_is_regular {Œπ} {f : Œπ ‚Üí ordinal} {c} (hc : is_regular c)
  (hŒπ : cardinal.lift (#Œπ) < c) : (‚àÄ i, f i < c.ord) ‚Üí ordinal.lsub f < c.ord :=
lsub_lt_ord_lift (by rwa hc.cof_eq)

theorem lsub_lt_ord_of_is_regular {Œπ} {f : Œπ ‚Üí ordinal} {c} (hc : is_regular c) (hŒπ : #Œπ < c) :
  (‚àÄ i, f i < c.ord) ‚Üí ordinal.lsub f < c.ord :=
lsub_lt_ord (by rwa hc.cof_eq)

theorem sup_lt_ord_lift_of_is_regular {Œπ} {f : Œπ ‚Üí ordinal} {c} (hc : is_regular c)
  (hŒπ : cardinal.lift (#Œπ) < c) : (‚àÄ i, f i < c.ord) ‚Üí ordinal.sup f < c.ord :=
sup_lt_ord_lift (by rwa hc.cof_eq)

theorem sup_lt_ord_of_is_regular {Œπ} {f : Œπ ‚Üí ordinal} {c} (hc : is_regular c) (hŒπ : #Œπ < c) :
  (‚àÄ i, f i < c.ord) ‚Üí ordinal.sup f < c.ord :=
sup_lt_ord (by rwa hc.cof_eq)

theorem blsub_lt_ord_lift_of_is_regular {o : ordinal} {f : Œ† a < o, ordinal} {c} (hc : is_regular c)
  (ho : cardinal.lift o.card < c) : (‚àÄ i hi, f i hi < c.ord) ‚Üí ordinal.blsub o f < c.ord :=
blsub_lt_ord_lift (by rwa hc.cof_eq)

theorem blsub_lt_ord_of_is_regular {o : ordinal} {f : Œ† a < o, ordinal} {c} (hc : is_regular c)
  (ho : o.card < c) : (‚àÄ i hi, f i hi < c.ord) ‚Üí ordinal.blsub o f < c.ord :=
blsub_lt_ord (by rwa hc.cof_eq)

theorem bsup_lt_ord_lift_of_is_regular {o : ordinal} {f : Œ† a < o, ordinal} {c} (hc : is_regular c)
  (hŒπ : cardinal.lift o.card < c) : (‚àÄ i hi, f i hi < c.ord) ‚Üí ordinal.bsup o f < c.ord :=
bsup_lt_ord_lift (by rwa hc.cof_eq)

theorem bsup_lt_ord_of_is_regular {o : ordinal} {f : Œ† a < o, ordinal} {c} (hc : is_regular c)
  (hŒπ : o.card < c) : (‚àÄ i hi, f i hi < c.ord) ‚Üí ordinal.bsup o f < c.ord :=
bsup_lt_ord (by rwa hc.cof_eq)

theorem supr_lt_lift_of_is_regular {Œπ} {f : Œπ ‚Üí cardinal} {c} (hc : is_regular c)
  (hŒπ : cardinal.lift (#Œπ) < c) : (‚àÄ i, f i < c) ‚Üí supr f < c :=
supr_lt_lift (by rwa hc.cof_eq)

theorem supr_lt_of_is_regular {Œπ} {f : Œπ ‚Üí cardinal} {c} (hc : is_regular c) (hŒπ : #Œπ < c) :
  (‚àÄ i, f i < c) ‚Üí supr f < c :=
supr_lt (by rwa hc.cof_eq)

theorem sum_lt_lift_of_is_regular {Œπ : Type u} {f : Œπ ‚Üí cardinal} {c : cardinal} (hc : is_regular c)
  (hŒπ : cardinal.lift.{v u} (#Œπ) < c) (hf : ‚àÄ i, f i < c) : sum f < c :=
(sum_le_supr_lift _).trans_lt $ mul_lt_of_lt hc.1 hŒπ (supr_lt_lift_of_is_regular hc hŒπ hf)

theorem sum_lt_of_is_regular {Œπ : Type u} {f : Œπ ‚Üí cardinal} {c : cardinal} (hc : is_regular c)
  (hŒπ : #Œπ < c) : (‚àÄ i, f i < c) ‚Üí sum f < c :=
sum_lt_lift_of_is_regular.{u u} hc (by rwa lift_id)

theorem nfp_family_lt_ord_lift_of_is_regular {Œπ} {f : Œπ ‚Üí ordinal ‚Üí ordinal} {c} (hc : is_regular c)
  (hŒπ : (#Œπ).lift < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) (hf : ‚àÄ i (b < c.ord), f i b < c.ord) {a} (ha : a < c.ord) :
  nfp_family.{u v} f a < c.ord :=
by { apply nfp_family_lt_ord_lift _ _ hf ha; rwa hc.cof_eq, exact lt_of_le_of_ne hc.1 hc'.symm }

theorem nfp_family_lt_ord_of_is_regular {Œπ} {f : Œπ ‚Üí ordinal ‚Üí ordinal} {c} (hc : is_regular c)
  (hŒπ : #Œπ < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) {a} (hf : ‚àÄ i (b < c.ord), f i b < c.ord) :
  a < c.ord ‚Üí nfp_family.{u u} f a < c.ord :=
nfp_family_lt_ord_lift_of_is_regular hc (by rwa lift_id) hc' hf

theorem nfp_bfamily_lt_ord_lift_of_is_regular {o : ordinal} {f : Œ† a < o, ordinal ‚Üí ordinal} {c}
  (hc : is_regular c) (ho : o.card.lift < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
  (hf : ‚àÄ i hi (b < c.ord), f i hi b < c.ord) {a} : a < c.ord ‚Üí nfp_bfamily.{u v} o f a < c.ord :=
nfp_family_lt_ord_lift_of_is_regular hc (by rwa mk_ordinal_out) hc' (Œª i, hf _ _)

theorem nfp_bfamily_lt_ord_of_is_regular {o : ordinal} {f : Œ† a < o, ordinal ‚Üí ordinal} {c}
  (hc : is_regular c) (ho : o.card < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) (hf : ‚àÄ i hi (b < c.ord), f i hi b < c.ord)
  {a} : a < c.ord ‚Üí nfp_bfamily.{u u} o f a < c.ord :=
nfp_bfamily_lt_ord_lift_of_is_regular hc (by rwa lift_id) hc' hf

theorem nfp_lt_ord_of_is_regular {f : ordinal ‚Üí ordinal} {c} (hc : is_regular c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
  (hf : ‚àÄ i < c.ord, f i < c.ord) {a} : (a < c.ord) ‚Üí nfp f a < c.ord :=
nfp_lt_ord (by { rw hc.cof_eq, exact lt_of_le_of_ne hc.1 hc'.symm }) hf

theorem deriv_family_lt_ord_lift {Œπ} {f : Œπ ‚Üí ordinal ‚Üí ordinal} {c} (hc : is_regular c)
  (hŒπ : (#Œπ).lift < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) (hf : ‚àÄ i (b < c.ord), f i b < c.ord) {a} :
  a < c.ord ‚Üí deriv_family.{u v} f a < c.ord :=
begin
  have hœâ : ‚Ñµ‚ÇÄ < c.ord.cof,
  { rw hc.cof_eq, exact lt_of_le_of_ne hc.1 hc'.symm },
  apply a.limit_rec_on,
  { rw deriv_family_zero,
    exact nfp_family_lt_ord_lift hœâ (by rwa hc.cof_eq) hf },
  { intros b hb hb',
    rw deriv_family_succ,
    exact nfp_family_lt_ord_lift hœâ (by rwa hc.cof_eq) hf
      ((ord_is_limit hc.1).2 _ (hb ((lt_succ b).trans hb'))) },
  { intros b hb H hb',
    rw deriv_family_limit f hb,
    exact bsup_lt_ord_of_is_regular hc (ord_lt_ord.1 ((ord_card_le b).trans_lt hb'))
      (Œª o' ho', H o' ho' (ho'.trans hb')) }
end

theorem deriv_family_lt_ord {Œπ} {f : Œπ ‚Üí ordinal ‚Üí ordinal} {c} (hc : is_regular c)
  (hŒπ : #Œπ < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) (hf : ‚àÄ i (b < c.ord), f i b < c.ord) {a} :
  a < c.ord ‚Üí deriv_family.{u u} f a < c.ord :=
deriv_family_lt_ord_lift hc (by rwa lift_id) hc' hf

theorem deriv_bfamily_lt_ord_lift {o : ordinal} {f : Œ† a < o, ordinal ‚Üí ordinal} {c}
  (hc : is_regular c) (hŒπ : o.card.lift < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
  (hf : ‚àÄ i hi (b < c.ord), f i hi b < c.ord) {a} :
  a < c.ord ‚Üí deriv_bfamily.{u v} o f a < c.ord :=
deriv_family_lt_ord_lift hc (by rwa mk_ordinal_out) hc' (Œª i, hf _ _)

theorem deriv_bfamily_lt_ord {o : ordinal} {f : Œ† a < o, ordinal ‚Üí ordinal} {c} (hc : is_regular c)
  (hŒπ : o.card < c) (hc' : c ‚â† ‚Ñµ‚ÇÄ) (hf : ‚àÄ i hi (b < c.ord), f i hi b < c.ord)
  {a} : a < c.ord ‚Üí deriv_bfamily.{u u} o f a < c.ord :=
deriv_bfamily_lt_ord_lift hc (by rwa lift_id) hc' hf

theorem deriv_lt_ord {f : ordinal.{u} ‚Üí ordinal} {c} (hc : is_regular c) (hc' : c ‚â† ‚Ñµ‚ÇÄ)
  (hf : ‚àÄ i < c.ord, f i < c.ord) {a} : a < c.ord ‚Üí deriv f a < c.ord :=
deriv_family_lt_ord_lift hc
  (by simpa using cardinal.one_lt_aleph_0.trans (lt_of_le_of_ne hc.1 hc'.symm))
  hc' (Œª _, hf)

/-- A cardinal is inaccessible if it is an uncountable regular strong limit cardinal. -/
def is_inaccessible (c : cardinal) :=
‚Ñµ‚ÇÄ < c ‚àß is_regular c ‚àß is_strong_limit c

theorem is_inaccessible.mk {c} (h‚ÇÅ : ‚Ñµ‚ÇÄ < c) (h‚ÇÇ : c ‚â§ c.ord.cof) (h‚ÇÉ : ‚àÄ x < c, 2 ^ x < c) :
  is_inaccessible c :=
‚ü®h‚ÇÅ, ‚ü®h‚ÇÅ.le, h‚ÇÇ‚ü©, (aleph_0_pos.trans h‚ÇÅ).ne', h‚ÇÉ‚ü©

/- Lean's foundations prove the existence of ‚Ñµ‚ÇÄ many inaccessible cardinals -/
theorem univ_inaccessible : is_inaccessible (univ.{u v}) :=
is_inaccessible.mk
  (by simpa using lift_lt_univ' ‚Ñµ‚ÇÄ)
  (by simp)
  (Œª c h, begin
    rcases lt_univ'.1 h with ‚ü®c, rfl‚ü©,
    rw ‚Üê lift_two_power.{u (max (u+1) v)},
    apply lift_lt_univ'
  end)

theorem lt_power_cof {c : cardinal.{u}} : ‚Ñµ‚ÇÄ ‚â§ c ‚Üí c < c ^ cof c.ord :=
quotient.induction_on c $ Œª Œ± h, begin
  rcases ord_eq Œ± with ‚ü®r, wo, re‚ü©, resetI,
  have := ord_is_limit h,
  rw [mk_def, re] at this ‚ä¢,
  rcases cof_eq' r this with ‚ü®S, H, Se‚ü©,
  have := sum_lt_prod (Œª a:S, #{x // r x a}) (Œª _, #Œ±) (Œª i, _),
  { simp only [cardinal.prod_const, cardinal.lift_id, ‚Üê Se, ‚Üê mk_sigma, power_def] at this ‚ä¢,
    refine lt_of_le_of_lt _ this,
    refine ‚ü®embedding.of_surjective _ _‚ü©,
    { exact Œª x, x.2.1 },
    { exact Œª a, let ‚ü®b, h, ab‚ü© := H a in ‚ü®‚ü®‚ü®_, h‚ü©, _, ab‚ü©, rfl‚ü© } },
  { have := typein_lt_type r i,
    rwa [‚Üê re, lt_ord] at this }
end

theorem lt_cof_power {a b : cardinal} (ha : ‚Ñµ‚ÇÄ ‚â§ a) (b1 : 1 < b) :
  a < cof (b ^ a).ord :=
begin
  have b0 : b ‚â† 0 := (zero_lt_one.trans b1).ne',
  apply lt_imp_lt_of_le_imp_le (power_le_power_left $ power_ne_zero a b0),
  rw [‚Üêpower_mul, mul_eq_self ha],
  exact lt_power_cof (ha.trans $ (cantor' _ b1).le),
end

end cardinal
