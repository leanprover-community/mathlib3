/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import set_theory.cardinal.ordinal

/-!
# Cardinality of continuum

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define `cardinal.continuum` (notation: `ğ” `, localized in `cardinal`) to be `2 ^ â„µâ‚€`.
We also prove some `simp` lemmas about cardinal arithmetic involving `ğ” `.

## Notation

- `ğ” ` : notation for `cardinal.continuum` in locale `cardinal`.
-/

namespace cardinal

universes u v

open_locale cardinal

/-- Cardinality of continuum. -/
def continuum : cardinal.{u} := 2 ^ aleph_0.{u}

localized "notation (name := cardinal.continuum) `ğ” ` := cardinal.continuum" in cardinal

@[simp] lemma two_power_aleph_0 : 2 ^ aleph_0.{u} = continuum.{u} := rfl

@[simp] lemma lift_continuum : lift.{v} ğ”  = ğ”  :=
by rw [â†two_power_aleph_0, lift_two_power, lift_aleph_0, two_power_aleph_0]

/-!
### Inequalities
-/

lemma aleph_0_lt_continuum : â„µâ‚€ < ğ”  := cantor â„µâ‚€

lemma aleph_0_le_continuum : â„µâ‚€ â‰¤ ğ”  := aleph_0_lt_continuum.le

@[simp] lemma beth_one : beth 1 = ğ”  := by simpa using beth_succ 0

lemma nat_lt_continuum (n : â„•) : â†‘n < ğ”  := (nat_lt_aleph_0 n).trans aleph_0_lt_continuum

lemma mk_set_nat : #(set â„•) = ğ”  := by simp

lemma continuum_pos : 0 < ğ”  := nat_lt_continuum 0

lemma continuum_ne_zero : ğ”  â‰  0 := continuum_pos.ne'

lemma aleph_one_le_continuum : aleph 1 â‰¤ ğ”  :=
by { rw â†succ_aleph_0, exact order.succ_le_of_lt aleph_0_lt_continuum }

@[simp] theorem continuum_to_nat : continuum.to_nat = 0 :=
to_nat_apply_of_aleph_0_le aleph_0_le_continuum

@[simp] theorem continuum_to_part_enat : continuum.to_part_enat = âŠ¤ :=
to_part_enat_apply_of_aleph_0_le aleph_0_le_continuum

/-!
### Addition
-/

@[simp] lemma aleph_0_add_continuum : â„µâ‚€ + ğ”  = ğ”  :=
add_eq_right aleph_0_le_continuum aleph_0_le_continuum

@[simp] lemma continuum_add_aleph_0 : ğ”  + â„µâ‚€ = ğ”  :=
(add_comm _ _).trans aleph_0_add_continuum

@[simp] lemma continuum_add_self : ğ”  + ğ”  = ğ”  :=
add_eq_right aleph_0_le_continuum le_rfl

@[simp] lemma nat_add_continuum (n : â„•) : â†‘n + ğ”  = ğ”  :=
add_eq_right aleph_0_le_continuum (nat_lt_continuum n).le

@[simp] lemma continuum_add_nat (n : â„•) : ğ”  + n = ğ”  :=
(add_comm _ _).trans (nat_add_continuum n)

/-!
### Multiplication
-/

@[simp] lemma continuum_mul_self : ğ”  * ğ”  = ğ”  :=
mul_eq_left aleph_0_le_continuum le_rfl continuum_ne_zero

@[simp] lemma continuum_mul_aleph_0 : ğ”  * â„µâ‚€ = ğ”  :=
mul_eq_left aleph_0_le_continuum aleph_0_le_continuum aleph_0_ne_zero

@[simp] lemma aleph_0_mul_continuum : â„µâ‚€ * ğ”  = ğ”  :=
(mul_comm _ _).trans continuum_mul_aleph_0

@[simp] lemma nat_mul_continuum {n : â„•} (hn : n â‰  0) : â†‘n * ğ”  = ğ”  :=
mul_eq_right aleph_0_le_continuum (nat_lt_continuum n).le (nat.cast_ne_zero.2 hn)

@[simp] lemma continuum_mul_nat {n : â„•} (hn : n â‰  0) : ğ”  * n = ğ”  :=
(mul_comm _ _).trans (nat_mul_continuum hn)

/-!
### Power
-/

@[simp] lemma aleph_0_power_aleph_0 : aleph_0.{u} ^ aleph_0.{u} = ğ”  :=
power_self_eq le_rfl

@[simp] lemma nat_power_aleph_0 {n : â„•} (hn : 2 â‰¤ n) : (n ^ aleph_0.{u} : cardinal.{u}) = ğ”  :=
nat_power_eq le_rfl hn

@[simp] lemma continuum_power_aleph_0 : continuum.{u} ^ aleph_0.{u} = ğ”  :=
by rw [â†two_power_aleph_0, â†power_mul, mul_eq_left le_rfl le_rfl aleph_0_ne_zero]

end cardinal
