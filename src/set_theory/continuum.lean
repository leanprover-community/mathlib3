/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import set_theory.cardinal_ordinal

/-!
# Cardinality of continuum

In this file we define `cardinal.continuum` (notation: `ğ” `, localized in `cardinal`) to be `2 ^ Ï‰`.
We also prove some `simp` lemmas about cardinal arithmetic involving `ğ” `.

## Notation

- `ğ” ` : notation for `cardinal.continuum` in locale `cardinal`.
-/

universes u v

/-- Cardinality of continuum. -/
def cardinal.continuum : cardinal.{u} := 2 ^ cardinal.omega.{u}

localized "notation `ğ” ` := cardinal.continuum" in cardinal

open_locale cardinal

namespace cardinal

@[simp] lemma two_power_omega : (2 ^ omega.{u} : cardinal.{u}) = ğ”  := rfl

@[simp] lemma lift_continuum : lift.{v} continuum.{u} = ğ”  :=
by rw [â† two_power_omega, lift_two_power, lift_omega, two_power_omega]

@[simp] lemma lift_le_continuum (c : cardinal.{v}) : lift.{u} c â‰¤ ğ”  â†” c â‰¤ ğ”  :=
by rw [â† lift_continuum, lift_le]

@[simp] lemma continuum_le_lift (c : cardinal.{v}) : ğ”  â‰¤ lift.{u} c â†” ğ”  â‰¤ c :=
by rw [â† lift_continuum, lift_le]

/-!
### Inequalities
-/

lemma omega_lt_continuum : Ï‰ < ğ”  := cantor Ï‰

lemma omega_le_continuum : Ï‰ â‰¤ ğ”  := omega_lt_continuum.le

lemma nat_lt_continuum (n : â„•) : â†‘n < ğ”  := (nat_lt_omega n).trans omega_lt_continuum

lemma mk_set_nat : #(set â„•) = ğ”  := by simp

lemma continuum_pos : 0 < ğ”  := nat_lt_continuum 0

lemma continuum_ne_zero : ğ”  â‰  0 := continuum_pos.ne'

lemma _root_.set.not_countable_of_continuum_le_mk {Î± : Type*} (s : set Î±) (hs : ğ”  â‰¤ #s) :
  Â¬s.countable :=
by { rw [â† mk_set_le_omega, not_le], exact omega_lt_continuum.trans_le hs }

/-!
### Addition
-/

@[simp] lemma omega_add_continuum : Ï‰ + ğ”  = ğ”  :=
add_eq_right omega_le_continuum omega_le_continuum

@[simp] lemma continuum_add_omega : ğ”  + Ï‰ = ğ”  :=
(add_comm _ _).trans omega_add_continuum

@[simp] lemma continuum_add_self : ğ”  + ğ”  = ğ”  :=
add_eq_right omega_le_continuum le_rfl

@[simp] lemma nat_add_continuum (n : â„•) : â†‘n + ğ”  = ğ”  :=
add_eq_right omega_le_continuum (nat_lt_continuum n).le

@[simp] lemma continuum_add_nat (n : â„•) : ğ”  + n = ğ”  :=
(add_comm _ _).trans (nat_add_continuum n)

/-!
### Multiplication
-/

@[simp] lemma continuum_mul_self : ğ”  * ğ”  = ğ”  :=
mul_eq_left omega_le_continuum le_rfl continuum_ne_zero

@[simp] lemma continuum_mul_omega : ğ”  * Ï‰ = ğ”  :=
mul_eq_left omega_le_continuum omega_le_continuum omega_ne_zero

@[simp] lemma omega_mul_continuum : Ï‰ * ğ”  = ğ”  :=
(mul_comm _ _).trans continuum_mul_omega

@[simp] lemma nat_mul_continuum {n : â„•} (hn : n â‰  0) :
  â†‘n * ğ”  = ğ”  :=
mul_eq_right omega_le_continuum (nat_lt_continuum n).le (nat.cast_ne_zero.2 hn)

@[simp] lemma continuum_mul_nat {n : â„•} (hn : n â‰  0) :
  ğ”  * n = ğ”  :=
(mul_comm _ _).trans (nat_mul_continuum hn)

/-!
### Power
-/

@[simp] lemma omega_power_omega : omega.{u} ^ omega.{u} = ğ”  :=
power_self_eq le_rfl

@[simp] lemma nat_power_omega {n : â„•} (hn : 2 â‰¤ n) : (n ^ omega.{u} : cardinal.{u}) = ğ”  :=
nat_power_eq le_rfl hn

@[simp] lemma continuum_power_omega : continuum.{u} ^ omega.{u} = ğ”  :=
by rw [â† two_power_omega, â† power_mul, mul_eq_left le_rfl le_rfl omega_ne_zero]

end cardinal

open cardinal

/-- A typeclass saying that `cardinal.mk Î± = cardinal.continuum`. -/
class has_card_continuum (Î± : Type u) : Prop :=
(mk_eq_continuum [] : #Î± = ğ” )

export has_card_continuum (mk_eq_continuum)
attribute [simp] mk_eq_continuum

instance _root_.set.univ.has_card_continuum {Î±} [has_card_continuum Î±] :
  has_card_continuum (set.univ : set Î±) :=
âŸ¨mk_univ.trans (mk_eq_continuum _)âŸ©

/-- A typeclass saying that `cardinal.mk Î± â‰¤ cardinal.continuum`. -/
class has_card_le_continuum (Î± : Type u) : Prop :=
(mk_le_continuum [] : #Î± â‰¤ ğ” )

export has_card_le_continuum (mk_le_continuum)

instance _root_.set.univ.has_card_le_continuum {Î±} [has_card_le_continuum Î±] :
  has_card_le_continuum (set.univ : set Î±) :=
âŸ¨mk_univ.trans_le (mk_le_continuum _)âŸ©

@[priority 100] -- See Note [lower instance priority]
instance has_card_continuum.to_has_card_le_continuum (Î± : Type u) [has_card_continuum Î±] :
  has_card_le_continuum Î± :=
âŸ¨(mk_eq_continuum Î±).leâŸ©

@[priority 100] -- See Note [lower instance priority]
instance encodable.to_has_card_le_continuum (Î± : Type u) [encodable Î±] :
  has_card_le_continuum Î± :=
âŸ¨mk_le_omega.trans omega_le_continuumâŸ©

@[priority 100] -- See Note [lower instance priority]
instance fintype.to_has_card_le_continuum (Î± : Type u) [fintype Î±] :
  has_card_le_continuum Î± :=
by { haveI := fintype.encodable Î±, exact encodable.to_has_card_le_continuum Î± }

@[priority 100] -- See Note [lower instance priority]
instance has_card_continuum.to_infinite (Î± : Type u) [has_card_continuum Î±] : infinite Î± :=
by simp [infinite_iff, omega_le_continuum]

lemma nonempty_equiv_of_continuum (Î± : Type u) (Î² : Type v) [has_card_continuum Î±]
  [has_card_continuum Î²] : nonempty (Î± â‰ƒ Î²) :=
lift_mk_eq'.1 $ by simp

lemma equiv.has_card_continuum {Î± : Type u} {Î² : Type v} [has_card_continuum Î²] (e : Î± â‰ƒ Î²) :
  has_card_continuum Î± :=
âŸ¨by rw [â† lift_inj, lift_mk_eq'.2 âŸ¨eâŸ©, mk_eq_continuum, lift_continuum, lift_continuum]âŸ©

lemma equiv.has_card_continuum_congr {Î± : Type u} {Î² : Type v} (e : Î± â‰ƒ Î²) :
  has_card_continuum Î± â†” has_card_continuum Î² :=
âŸ¨Î» H, @equiv.has_card_continuum Î² Î± H e.symm, Î» H, @equiv.has_card_continuum Î± Î² H eâŸ©

instance (Î± : Type u) (Ï€ : Î± â†’ Type v) [denumerable Î±] [âˆ€ a, nontrivial (Ï€ a)]
  [Î  a, encodable (Ï€ a)] : has_card_continuum (Î  a, Ï€ a) :=
âŸ¨calc #(Î  a, Ï€ a) = prod (Î» a : Î±, #(Ï€ a)) : mk_pi _
              ... = 2 ^ lift.{v} (#Î±)      :
   prod_eq_two_power (Î» i, two_le_iff.2 $ exists_pair_ne _) $ Î» i, by simp
              ... = ğ”                       : by simpâŸ©

instance pi.has_card_continuum' (Î± : Type u) (Ï€ : Î± â†’ Type v) [denumerable Î±]
  [âˆ€ a, nontrivial (Ï€ a)] [Î  a, fintype (Ï€ a)] : has_card_continuum (Î  a, Ï€ a) :=
by { haveI := Î» a, fintype.encodable (Ï€ a), exact pi.has_card_continuum Î± Ï€ }

instance (Î± : Type u) [denumerable Î±] : has_card_continuum (set Î±) :=
pi.has_card_continuum _ _

instance prod.has_card_continuum_left (Î± : Type u) (Î² : Type v)
  [has_card_continuum Î±] [has_card_le_continuum Î²] [nonempty Î²] :
  has_card_continuum (Î± Ã— Î²) :=
âŸ¨begin
  rw [mk_prod, mk_eq_continuum, lift_continuum, mul_eq_left omega_le_continuum],
  { simp [mk_le_continuum] },
  { rw [ne.def, lift_eq_zero], exact mk_ne_zero Î² }
endâŸ©

instance prod.has_card_continuum_right (Î± : Type u) (Î² : Type v)
  [has_card_le_continuum Î±] [nonempty Î±] [has_card_continuum Î²] :
  has_card_continuum (Î± Ã— Î²) :=
(equiv.prod_comm Î± Î²).has_card_continuum
