/-
Copyright (c) 2020 E.W.Ayers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.

Author: E.W.Ayers
-/

/-!
# Widgets used for tactic state and term-mode goal display

The vscode extension supports the display of interactive widgets.
Default implementation of these widgets are included in the core
library.  We override them here using `vm_override` so that we can
change them quickly without waiting for the next Lean release.

The function `widget_override.interactive_expression.mk` renders a single
expression as a widget component.  Each goal in a tactic state is rendered
using the `widget_override.tactic_view_goal` function,
a complete tactic state is rendered using
`widget_override.tactic_view_component`.

Lean itself calls the `widget_override.term_goal_widget` function to render
term-mode goals and `widget_override.tactic_state_widget` to render the
tactic state in a tactic proof.
-/

namespace widget_override
open widget

open tagged_format
open widget.html widget.attr

namespace interactive_expression

/-- eformat but without any of the formatting stuff like highlighting, groups etc. -/
meta inductive sf : Type
| tag_expr : expr.address ‚Üí expr ‚Üí sf ‚Üí sf
| compose : sf ‚Üí  sf ‚Üí  sf
| of_string : string ‚Üí  sf

private meta def to_simple : eformat ‚Üí sf
| (tag ‚ü®ea,e‚ü© m) := sf.tag_expr ea e $ to_simple m
| (group m) := to_simple m
| (nest i m) := to_simple m
| (highlight i m) := to_simple m
| (of_format f) := sf.of_string $ format.to_string f
| (compose x y) := sf.compose (to_simple x) (to_simple y)

private meta def sf.flatten : sf ‚Üí sf
| (sf.tag_expr e ea m) := (sf.tag_expr e ea $ sf.flatten m)
| (sf.compose x y) :=
  match (sf.flatten x), (sf.flatten y) with
  | (sf.of_string sx), (sf.of_string sy) := sf.of_string (sx ++ sy)
  | (sf.of_string sx), (sf.compose (sf.of_string sy) z) := sf.compose (sf.of_string (sx ++ sy)) z
  | (sf.compose x (sf.of_string sy)), (sf.of_string sz) := sf.compose x (sf.of_string (sy ++ sz))
  | (sf.compose x (sf.of_string sy1)), (sf.compose (sf.of_string sy2) z) := sf.compose x (sf.compose (sf.of_string (sy1 ++ sy2)) z)
  | x, y := sf.compose x y
  end
| (sf.of_string s) := sf.of_string s

/--
The actions accepted by an expression widget.
-/
meta inductive action (Œ≥ : Type)
| on_mouse_enter : subexpr ‚Üí action
| on_mouse_leave_all : action
| on_click : subexpr ‚Üí action
| on_tooltip_action : Œ≥ ‚Üí action
| on_close_tooltip : action

/--
Renders a subexpression as a list of html elements.
-/
meta def view {Œ≥} (tooltip_component : tc subexpr (action Œ≥)) (click_address : option expr.address) (select_address : option expr.address) :
  subexpr ‚Üí sf ‚Üí tactic (list (html (action Œ≥)))
| ‚ü®ce, current_address‚ü© (sf.tag_expr ea e m) := do
  let new_address := current_address ++ ea,
  let select_attrs : list (attr (action Œ≥)) := if some new_address = select_address then [className "highlight"] else [],
  click_attrs  : list (attr (action Œ≥)) ‚Üê
    if some new_address = click_address then do
      content ‚Üê tc.to_html tooltip_component (e, new_address),
      pure [tooltip $ h "div" [] [
          h "button" [cn "fr pointer ba br3", on_click (Œª _, action.on_close_tooltip)] ["x"],
          content
      ]]
    else pure [],
  let as := [className "expr-boundary", key (ea)] ++ select_attrs ++ click_attrs,
  inner ‚Üê view (e,new_address) m,
  pure [h "span" as inner]
| ca (sf.compose x y) := pure (++) <*> view ca x <*> view ca y
| ca (sf.of_string s) := pure
  [h "span" [
    on_mouse_enter (Œª _, action.on_mouse_enter ca),
    on_click (Œª _, action.on_click ca),
    key s
  ] [html.of_string s]]


/-- Make an interactive expression. -/
meta def mk {Œ≥} (tooltip : tc subexpr Œ≥) : tc expr Œ≥ :=
let tooltip_comp :=
   component.with_props_eq (Œª (x y : tactic_state √ó expr √ó expr.address), x.2.2 = y.2.2)
   $ component.map_action (action.on_tooltip_action) tooltip in
tc.mk_simple
  (action Œ≥)
  (option subexpr √ó option subexpr)
  (Œª e, pure $ (none, none))
  (Œª e ‚ü®ca, sa‚ü© act, pure $
    match act with
    | (action.on_mouse_enter ‚ü®e, ea‚ü©) := ((ca, some (e, ea)), none)
    | (action.on_mouse_leave_all)     := ((ca, none), none)
    | (action.on_click ‚ü®e, ea‚ü©)       := if some (e,ea) = ca then ((none, sa), none) else ((some (e, ea), sa), none)
    | (action.on_tooltip_action g)    := ((none, sa), some g)
    | (action.on_close_tooltip)       := ((none, sa), none)
    end
  )
  (Œª e ‚ü®ca, sa‚ü©, do
    ts ‚Üê tactic.read,
    let m : sf  := sf.flatten $ to_simple $ tactic_state.pp_tagged ts e,
    let m : sf  := sf.tag_expr [] e m, -- [hack] in pp.cpp I forgot to add an expr-boundary for the root expression.
    v ‚Üê view tooltip_comp (prod.snd <$> ca) (prod.snd <$> sa) ‚ü®e, []‚ü© m,
    pure $
    [ h "span" [
          className "expr",
          key e.hash,
          on_mouse_leave (Œª _, action.on_mouse_leave_all) ] $ v
      ]
  )

/-- Render the implicit arguments for an expression in fancy, little pills. -/
meta def implicit_arg_list (tooltip : tc subexpr empty) (e : expr) : tactic $ html empty := do
  fn ‚Üê (mk tooltip) $ expr.get_app_fn e,
  args ‚Üê list.mmap (mk tooltip) $ expr.get_app_args e,
  pure $ h "div" []
    ( (h "span" [className "bg-blue br3 ma1 ph2 white"] [fn]) ::
      list.map (Œª a, h "span" [className "bg-gray br3 ma1 ph2 white"] [a]) args
    )

/--
Component for the type tooltip.
-/
meta def type_tooltip : tc subexpr empty :=
tc.stateless (Œª ‚ü®e,ea‚ü©, do
    y ‚Üê tactic.infer_type e,
    y_comp ‚Üê mk type_tooltip y,
    implicit_args ‚Üê implicit_arg_list type_tooltip e,
    pure [
        h "div" [] [
          h "div" [] [y_comp],
          h "hr" [] [],
          implicit_args
        ]
      ]
  )

end interactive_expression

/--
Supported tactic state filters.
-/
@[derive decidable_eq]
meta inductive filter_type
| none
| no_instances
| only_props

/--
Filters a local constant using the given filter.
-/
meta def filter_local : filter_type ‚Üí expr ‚Üí tactic bool
| (filter_type.none) e := pure tt
| (filter_type.no_instances) e := do
  t ‚Üê tactic.infer_type e,
  bnot <$> tactic.is_class t
| (filter_type.only_props) e := do
  t ‚Üê tactic.infer_type e,
  tactic.is_prop t

/--
Component for the filter dropdown.
-/
meta def filter_component : component filter_type filter_type :=
component.stateless (Œª lf,
  [ h "label" [] ["filter: "],
    select [
      ‚ü®filter_type.none, "0", ["no filter"]‚ü©,
      ‚ü®filter_type.no_instances, "1", ["no instances"]‚ü©,
      ‚ü®filter_type.only_props, "2", ["only props"]‚ü©
    ] lf
  ]
)

/--
Converts a name into an html element.
-/
meta def html.of_name {Œ± : Type} : name ‚Üí html Œ±
| n := html.of_string $ name.to_string n

open tactic

/--
Component that shows a type.
-/
meta def show_type_component : tc expr empty :=
tc.stateless (Œª x, do
  y ‚Üê infer_type x,
  y_comp ‚Üê interactive_expression.mk interactive_expression.type_tooltip $ y,
  pure y_comp
)

/-- A group of local constants in the context that should be rendered as one line. -/
@[derive decidable_eq]
meta structure local_collection :=
(key : string)
(locals : list expr)
(type : expr)
(value : option expr)

/-- Converts a single local constant into a (singleton) `local_collection` -/
meta def to_local_collection (l : expr) : tactic local_collection :=
tactic.unsafe.type_context.run $ do
lctx ‚Üê tactic.unsafe.type_context.get_local_context,
some ldecl ‚Üê pure $ lctx.get_local_decl l.local_uniq_name,
pure {
  key := l.local_uniq_name.repr,
  locals := [l],
  type := ldecl.type,
  value := ldecl.value }

/-- Groups consecutive local collections by type -/
meta def group_local_collection : list local_collection ‚Üí list local_collection
| (a :: b :: rest) :=
  if a.type = b.type ‚àß a.value = b.value then
    group_local_collection $
      { locals := a.locals ++ b.locals, ..a } :: rest
  else
    a :: group_local_collection (b :: rest)
| ls := ls

/-- Component that displays the main (first) goal. -/
meta def tactic_view_goal {Œ≥} (local_c : tc local_collection Œ≥) (target_c : tc expr Œ≥) : tc filter_type Œ≥ :=
tc.stateless $ Œª ft, do
  g@(expr.mvar u_n pp_n y) ‚Üê main_goal,
  t ‚Üê get_tag g,
  let case_tag : list (html Œ≥) :=
    match interactive.case_tag.parse t with
    | some t :=
      [h "li" [key "_case"] $ [h "span" [cn "goal-case b"] ["case"]] ++
        (t.case_names.bind $ Œª n, [" ", n])]
    | none := []
    end,
  lcs ‚Üê local_context,
  lcs ‚Üê list.mfilter (filter_local ft) lcs,
  lcs ‚Üê lcs.mmap $ to_local_collection,
  let lcs := group_local_collection lcs,
  lchs ‚Üê lcs.mmap (Œª lc, do
    lh ‚Üê local_c lc,
    let ns : list (html Œ≥) := lc.locals.map $ Œª n,
      h "span" [cn "goal-hyp b pr2", key n.local_uniq_name] [html.of_name n.local_pp_name],
    pure $ h "li" [key lc.key] (ns ++ [": ", h "span" [cn "goal-hyp-type", key "type"] [lh]])),
  t_comp ‚Üê target_c g,
  pure $ h "ul" [key g.hash, className "list pl0 font-code"] $ case_tag ++ lchs ++ [
    h "li" [key u_n] [
      h "span" [cn "goal-vdash b"] ["‚ä¢ "],
      t_comp
  ]]

/--
Actions accepted by the `tactic_view_component`.
-/
meta inductive tactic_view_action (Œ≥ : Type)
| out (a:Œ≥): tactic_view_action
| filter (f: filter_type): tactic_view_action

/-- Component that displays all goals, together with the `$n goals` message. -/
meta def tactic_view_component {Œ≥} (local_c : tc local_collection Œ≥) (target_c : tc expr Œ≥) : tc unit Œ≥ :=
tc.mk_simple
  (tactic_view_action Œ≥)
  (filter_type)
  (Œª _, pure $ filter_type.none)
  (Œª ‚ü®‚ü© ft a, match a with
              | (tactic_view_action.out a) := pure (ft, some a)
              | (tactic_view_action.filter ft) := pure (ft, none)
              end)
  (Œª ‚ü®‚ü© ft, do
    gs ‚Üê get_goals,
    hs ‚Üê gs.mmap (Œª g, do set_goals [g], flip tc.to_html ft $ tactic_view_goal local_c target_c),
    set_goals gs,
    let goal_message :=
      if gs.length = 0 then
        "goals accomplished üéâ"
      else if gs.length = 1 then
        "1 goal"
      else
        to_string gs.length ++ " goals",
    let goal_message : html Œ≥ := h "strong" [cn "goal-goals"] [goal_message],
    let goals : html Œ≥ := h "ul" [className "list pl0"]
        $ list.map_with_index (Œª i x,
          h "li" [className $ "lh-copy mt2", key i] [x])
        $ (goal_message :: hs),
    pure [
      h "div" [className "fr"] [html.of_component ft $ component.map_action tactic_view_action.filter filter_component],
      html.map_action tactic_view_action.out goals
    ])

/-- Component that displays the term-mode goal. -/
meta def tactic_view_term_goal {Œ≥} (local_c : tc local_collection Œ≥) (target_c : tc expr Œ≥) : tc unit Œ≥ :=
tc.stateless $ Œª _, do
  goal ‚Üê flip tc.to_html (filter_type.none) $ tactic_view_goal local_c target_c,
  pure [h "ul" [className "list pl0"] [
    h "li" [className "lh-copy"] [h "strong" [cn "goal-goals"] ["expected type:"]],
    h "li" [className "lh-copy"] [goal]]]

/--
Component showing a local collection.
-/
meta def show_local_collection_component : tc local_collection empty :=
tc.stateless (Œª lc, do
  (l::_) ‚Üê pure lc.locals,
  c ‚Üê show_type_component l,
  match lc.value with
  | some v := do
    v ‚Üê interactive_expression.mk interactive_expression.type_tooltip v,
    pure [c, " := ", v]
  | none := pure [c]
  end)

/--
Renders the current tactic state.
-/
meta def tactic_render : tc unit string :=
component.ignore_action $ tactic_view_component show_local_collection_component show_type_component

/--
Component showing the current tactic state.
-/
meta def tactic_state_widget : component tactic_state string :=
tc.to_component tactic_render

/--
Widget used to display term-proof goals.
-/
meta def term_goal_widget : component tactic_state string :=
(tactic_view_term_goal show_local_collection_component show_type_component).to_component

end widget_override

attribute [vm_override widget_override.term_goal_widget] widget.term_goal_widget
attribute [vm_override widget_override.tactic_state_widget] widget.tactic_state_widget
