/-
Copyright (c) 2021 Yourong Zang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yourong Zang, Yury Kudryashov
-/
import topology.separation
import topology.opens

/-!
# The Alexandroff Compactification

We construct the Alexandroff compactification (the one-point compactification) of an arbitrary
topological space `X` and prove some properties inherited from `X`.

## Main definitions

* `alexandroff`: the Alexandroff compactification, we use coercion for the canonical embedding
  `X â†’ alexandroff X`; when `X` is already compact, the compactification adds an isolated point
  to the space.
* `alexandroff.infty`: the extra point

## Main results

* The topological structure of `alexandroff X`
* The connectedness of `alexandroff X` for a noncompact, preconnected `X`
* `alexandroff X` is `Tâ‚€` for a Tâ‚€ space `X`
* `alexandroff X` is `Tâ‚` for a Tâ‚ space `X`
* `alexandroff X` is normal if `X` is a locally compact Hausdorff space

## Tags

one-point compactification, compactness
-/

open set filter
open_locale classical topological_space filter

/-!
### Definition and basic properties

In this section we define `alexandroff X` to be the disjoint union of `X` and `âˆ`, implemented as
`option X`. Then we restate some lemmas about `option X` for `alexandroff X`.
-/

/-- The Alexandroff extension of an arbitrary topological space `X` -/
def alexandroff (X : Type*) := option X

namespace alexandroff

variables {X : Type*}

/-- The point at infinity -/
def infty : alexandroff X := none
localized "notation `âˆ` := alexandroff.infty" in alexandroff

instance : has_coe_t X (alexandroff X) := âŸ¨option.someâŸ©

instance : inhabited (alexandroff X) := âŸ¨âˆâŸ©

lemma coe_injective : function.injective (coe : X â†’ alexandroff X) :=
option.some_injective X

@[norm_cast] lemma coe_eq_coe {x y : X} : (x : alexandroff X) = y â†” x = y :=
coe_injective.eq_iff

@[simp] lemma coe_ne_infty (x : X) : (x : alexandroff X) â‰  âˆ  .
@[simp] lemma infty_ne_coe (x : X) : âˆ â‰  (x : alexandroff X) .

/-- Recursor for `alexandroff` using the preferred forms `âˆ` and `â†‘x`. -/
@[elab_as_eliminator]
protected def rec (C : alexandroff X â†’ Sort*) (hâ‚ : C âˆ) (hâ‚‚ : Î  x : X, C x) :
  Î  (z : alexandroff X), C z :=
option.rec hâ‚ hâ‚‚

lemma is_compl_range_coe_infty : is_compl (range (coe : X â†’ alexandroff X)) {âˆ} :=
is_compl_range_some_none X

@[simp] lemma range_coe_union_infty : (range (coe : X â†’ alexandroff X) âˆª {âˆ}) = univ :=
range_some_union_none X

@[simp] lemma range_coe_inter_infty : (range (coe : X â†’ alexandroff X) âˆ© {âˆ}) = âˆ… :=
range_some_inter_none X

@[simp] lemma compl_range_coe : (range (coe : X â†’ alexandroff X))á¶œ = {âˆ} :=
compl_range_some X

lemma compl_infty : ({âˆ}á¶œ : set (alexandroff X)) = range (coe : X â†’ alexandroff X) :=
(@is_compl_range_coe_infty X).symm.compl_eq

lemma compl_image_coe (s : set X) : (coe '' s : set (alexandroff X))á¶œ = coe '' sá¶œ âˆª {âˆ} :=
by rw [coe_injective.compl_image_eq, compl_range_coe]

lemma ne_infty_iff_exists {x : alexandroff X} :
  x â‰  âˆ â†” âˆƒ (y : X), (y : alexandroff X) = x :=
by induction x using alexandroff.rec; simp

instance : can_lift (alexandroff X) X :=
{ coe := coe,
  cond := Î» x, x â‰  âˆ,
  prf := Î» x, ne_infty_iff_exists.1 }

lemma not_mem_range_coe_iff {x : alexandroff X} :
  x âˆ‰ range (coe : X â†’ alexandroff X) â†” x = âˆ :=
by rw [â† mem_compl_iff, compl_range_coe, mem_singleton_iff]

lemma infty_not_mem_range_coe : âˆ âˆ‰ range (coe : X â†’ alexandroff X) :=
not_mem_range_coe_iff.2 rfl

lemma infty_not_mem_image_coe {s : set X} : âˆ âˆ‰ (coe : X â†’ alexandroff X) '' s :=
not_mem_subset (image_subset_range _ _) infty_not_mem_range_coe

@[simp] lemma coe_preimage_infty : (coe : X â†’ alexandroff X) â»Â¹' {âˆ} = âˆ… :=
by { ext, simp }

/-!
### Topological space structure on `alexandroff X`

We define a topological space structure on `alexandroff X` so that `s` is open if and only if

* `coe â»Â¹' s` is open in `X`;
* if `âˆ âˆˆ s`, then `(coe â»Â¹' s)á¶œ` is compact.

Then we reformulate this definition in a few different ways, and prove that
`coe : X â†’ alexandroff X` is an open embedding. If `X` is not a compact space, then we also prove
that `coe` has dense range, so it is a dense embedding.
-/

variables [topological_space X]

instance : topological_space (alexandroff X) :=
{ is_open := Î» s, (âˆ âˆˆ s â†’ is_compact ((coe : X â†’ alexandroff X) â»Â¹' s)á¶œ) âˆ§
    is_open ((coe : X â†’ alexandroff X) â»Â¹' s),
  is_open_univ := by simp,
  is_open_inter := Î» s t,
  begin
    rintros âŸ¨hms, hsâŸ© âŸ¨hmt, htâŸ©,
    refine âŸ¨_, hs.inter htâŸ©,
    rintros âŸ¨hms', hmt'âŸ©,
    simpa [compl_inter] using (hms hms').union (hmt hmt')
  end,
  is_open_sUnion := Î» S ho,
  begin
    suffices : is_open (coe â»Â¹' â‹ƒâ‚€ S : set X),
    { refine âŸ¨_, thisâŸ©,
      rintro âŸ¨s, hsS : s âˆˆ S, hs : âˆ âˆˆ sâŸ©,
      refine compact_of_is_closed_subset ((ho s hsS).1 hs) this.is_closed_compl _,
      exact compl_subset_compl.mpr (preimage_mono $ subset_sUnion_of_mem hsS) },
    rw [preimage_sUnion],
    exact is_open_bUnion (Î» s hs, (ho s hs).2)
  end }

variables {s : set (alexandroff X)} {t : set X}

lemma is_open_def :
  is_open s â†” (âˆ âˆˆ s â†’ is_compact (coe â»Â¹' s : set X)á¶œ) âˆ§ is_open (coe â»Â¹' s : set X) :=
iff.rfl

lemma is_open_iff_of_mem' (h : âˆ âˆˆ s) :
  is_open s â†” is_compact (coe â»Â¹' s : set X)á¶œ âˆ§ is_open (coe â»Â¹' s : set X) :=
by simp [is_open_def, h]

lemma is_open_iff_of_mem (h : âˆ âˆˆ s) :
  is_open s â†” is_closed (coe â»Â¹' s : set X)á¶œ âˆ§ is_compact (coe â»Â¹' s : set X)á¶œ :=
by simp only [is_open_iff_of_mem' h, is_closed_compl_iff, and.comm]

lemma is_open_iff_of_not_mem (h : âˆ âˆ‰ s) :
  is_open s â†” is_open (coe â»Â¹' s : set X) :=
by simp [is_open_def, h]

lemma is_closed_iff_of_mem (h : âˆ âˆˆ s) :
  is_closed s â†” is_closed (coe â»Â¹' s : set X) :=
have âˆ âˆ‰ sá¶œ, from Î» H, H h,
by rw [â† is_open_compl_iff, is_open_iff_of_not_mem this, â† is_open_compl_iff, preimage_compl]

lemma is_closed_iff_of_not_mem (h : âˆ âˆ‰ s) :
  is_closed s â†” is_closed (coe â»Â¹' s : set X) âˆ§ is_compact (coe â»Â¹' s : set X) :=
by rw [â† is_open_compl_iff, is_open_iff_of_mem (mem_compl h), â† preimage_compl, compl_compl]

@[simp] lemma is_open_image_coe {s : set X} :
  is_open (coe '' s : set (alexandroff X)) â†” is_open s :=
by rw [is_open_iff_of_not_mem infty_not_mem_image_coe, preimage_image_eq _ coe_injective]

lemma is_open_compl_image_coe {s : set X} :
  is_open (coe '' s : set (alexandroff X))á¶œ â†” is_closed s âˆ§ is_compact s :=
begin
  rw [is_open_iff_of_mem, â† preimage_compl, compl_compl, preimage_image_eq _ coe_injective],
  exact infty_not_mem_image_coe
end

@[simp] lemma is_closed_image_coe {s : set X} :
  is_closed (coe '' s : set (alexandroff X)) â†” is_closed s âˆ§ is_compact s :=
by rw [â† is_open_compl_iff, is_open_compl_image_coe]

/-- An open set in `alexandroff X` constructed from a closed compact set in `X` -/
def opens_of_compl (s : set X) (hâ‚ : is_closed s) (hâ‚‚ : is_compact s) :
  topological_space.opens (alexandroff X) :=
âŸ¨(coe '' s)á¶œ, is_open_compl_image_coe.2 âŸ¨hâ‚, hâ‚‚âŸ©âŸ©

lemma infty_mem_opens_of_compl {s : set X} (hâ‚ : is_closed s) (hâ‚‚ : is_compact s) :
  âˆ âˆˆ opens_of_compl s hâ‚ hâ‚‚ :=
mem_compl infty_not_mem_image_coe

@[continuity] lemma continuous_coe : continuous (coe : X â†’ alexandroff X) :=
continuous_def.mpr (Î» s hs, hs.right)

lemma is_open_map_coe  : is_open_map (coe : X â†’ alexandroff X) :=
Î» s, is_open_image_coe.2

lemma open_embedding_coe : open_embedding (coe : X â†’ alexandroff X) :=
open_embedding_of_continuous_injective_open continuous_coe coe_injective is_open_map_coe

lemma is_open_range_coe : is_open (range (coe : X â†’ alexandroff X)) :=
open_embedding_coe.open_range

lemma is_closed_infty : is_closed ({âˆ} : set (alexandroff X)) :=
by { rw [â† compl_range_coe, is_closed_compl_iff], exact is_open_range_coe }

lemma nhds_coe_eq (x : X) : ğ“ â†‘x = map (coe : X â†’ alexandroff X) (ğ“ x) :=
(open_embedding_coe.map_nhds_eq x).symm

lemma nhds_within_coe_image (s : set X) (x : X) :
  ğ“[coe '' s] (x : alexandroff X) = map coe (ğ“[s] x) :=
(open_embedding_coe.to_embedding.map_nhds_within_eq _ _).symm

lemma nhds_within_coe (s : set (alexandroff X)) (x : X) :
  ğ“[s] â†‘x = map coe (ğ“[coe â»Â¹' s] x) :=
(open_embedding_coe.map_nhds_within_preimage_eq _ _).symm

lemma comap_coe_nhds (x : X) : comap (coe : X â†’ alexandroff X) (ğ“ x) = ğ“ x :=
(open_embedding_coe.to_inducing.nhds_eq_comap x).symm

/-- If `x` is not an isolated point of `X`, then `x : alexandroff X` is not an isolated point
of `alexandroff X`. -/
instance nhds_within_compl_coe_ne_bot (x : X) [h : ne_bot (ğ“[{x}á¶œ] x)] :
  ne_bot (ğ“[{x}á¶œ] (x : alexandroff X)) :=
by simpa [nhds_within_coe, preimage, coe_eq_coe] using h.map coe

lemma nhds_within_compl_infty_eq : ğ“[{âˆ}á¶œ] (âˆ : alexandroff X) = map coe (coclosed_compact X) :=
begin
  refine (nhds_within_basis_open âˆ _).ext (has_basis_coclosed_compact.map _) _ _,
  { rintro s âŸ¨hs, hsoâŸ©,
    refine âŸ¨_, (is_open_iff_of_mem hs).mp hso, _âŸ©,
    simp },
  { rintro s âŸ¨hâ‚, hâ‚‚âŸ©,
    refine âŸ¨_, âŸ¨mem_compl infty_not_mem_image_coe, is_open_compl_image_coe.2 âŸ¨hâ‚, hâ‚‚âŸ©âŸ©, _âŸ©,
    simp [compl_image_coe, â† diff_eq, subset_preimage_image] }
end

/-- If `X` is a non-compact space, then `âˆ` is not an isolated point of `alexandroff X`. -/
instance nhds_within_compl_infty_ne_bot [noncompact_space X] :
  ne_bot (ğ“[{âˆ}á¶œ] (âˆ : alexandroff X)) :=
by { rw nhds_within_compl_infty_eq, apply_instance }

@[priority 900]
instance nhds_within_compl_ne_bot [âˆ€ x : X, ne_bot (ğ“[{x}á¶œ] x)] [noncompact_space X]
  (x : alexandroff X) : ne_bot (ğ“[{x}á¶œ] x) :=
alexandroff.rec _ alexandroff.nhds_within_compl_infty_ne_bot
  (Î» y, alexandroff.nhds_within_compl_coe_ne_bot y) x

lemma nhds_infty_eq : ğ“ (âˆ : alexandroff X) = map coe (coclosed_compact X) âŠ” pure âˆ :=
by rw [â† nhds_within_compl_infty_eq, nhds_within_compl_singleton_sup_pure]

lemma has_basis_nhds_infty :
  (ğ“ (âˆ : alexandroff X)).has_basis (Î» s : set X, is_closed s âˆ§ is_compact s)
    (Î» s, coe '' sá¶œ âˆª {âˆ}) :=
begin
  rw nhds_infty_eq,
  exact (has_basis_coclosed_compact.map _).sup_pure _
end

@[simp] lemma comap_coe_nhds_infty : comap (coe : X â†’ alexandroff X) (ğ“ âˆ) = coclosed_compact X :=
by simp [nhds_infty_eq, comap_sup, comap_map coe_injective]

lemma le_nhds_infty {f : filter (alexandroff X)} :
  f â‰¤ ğ“ âˆ â†” âˆ€ s : set X, is_closed s â†’ is_compact s â†’ coe '' sá¶œ âˆª {âˆ} âˆˆ f :=
by simp only [has_basis_nhds_infty.ge_iff, and_imp]

lemma ultrafilter_le_nhds_infty {f : ultrafilter (alexandroff X)} :
  (f : filter (alexandroff X)) â‰¤ ğ“ âˆ â†” âˆ€ s : set X, is_closed s â†’ is_compact s â†’ coe '' s âˆ‰ f :=
by simp only [le_nhds_infty, â† compl_image_coe, ultrafilter.mem_coe,
  ultrafilter.compl_mem_iff_not_mem]

lemma tendsto_nhds_infty' {Î± : Type*} {f : alexandroff X â†’ Î±} {l : filter Î±} :
  tendsto f (ğ“ âˆ) l â†” tendsto f (pure âˆ) l âˆ§ tendsto (f âˆ˜ coe) (coclosed_compact X) l :=
by simp [nhds_infty_eq, and_comm]

lemma tendsto_nhds_infty {Î± : Type*} {f : alexandroff X â†’ Î±} {l : filter Î±} :
  tendsto f (ğ“ âˆ) l â†”
    âˆ€ s âˆˆ l, f âˆ âˆˆ s âˆ§ âˆƒ t : set X, is_closed t âˆ§ is_compact t âˆ§ maps_to (f âˆ˜ coe) tá¶œ s :=
tendsto_nhds_infty'.trans $ by simp only [tendsto_pure_left,
  has_basis_coclosed_compact.tendsto_left_iff, forall_and_distrib, and_assoc, exists_prop]

lemma continuous_at_infty' {Y : Type*} [topological_space Y] {f : alexandroff X â†’ Y} :
  continuous_at f âˆ â†” tendsto (f âˆ˜ coe) (coclosed_compact X) (ğ“ (f âˆ)) :=
tendsto_nhds_infty'.trans $ and_iff_right (tendsto_pure_nhds _ _)

lemma continuous_at_infty {Y : Type*} [topological_space Y] {f : alexandroff X â†’ Y} :
  continuous_at f âˆ â†”
    âˆ€ s âˆˆ ğ“ (f âˆ), âˆƒ t : set X, is_closed t âˆ§ is_compact t âˆ§ maps_to (f âˆ˜ coe) tá¶œ s :=
continuous_at_infty'.trans $
  by simp only [has_basis_coclosed_compact.tendsto_left_iff, exists_prop, and_assoc]

lemma continuous_at_coe {Y : Type*} [topological_space Y] {f : alexandroff X â†’ Y} {x : X} :
  continuous_at f x â†” continuous_at (f âˆ˜ coe) x :=
by rw [continuous_at, nhds_coe_eq, tendsto_map'_iff, continuous_at]

/-- If `X` is not a compact space, then the natural embedding `X â†’ alexandroff X` has dense range.
-/
lemma dense_range_coe [noncompact_space X] :
  dense_range (coe : X â†’ alexandroff X) :=
begin
  rw [dense_range, â† compl_infty],
  exact dense_compl_singleton _
end

lemma dense_embedding_coe [noncompact_space X] :
  dense_embedding (coe : X â†’ alexandroff X) :=
{ dense := dense_range_coe, .. open_embedding_coe }

/-!
### Compactness and separation properties

In this section we prove that `alexandroff X` is a compact space; it is a Tâ‚€ (resp., Tâ‚) space if
the original space satisfies the same separation axiom. If the original space is a locally compact
Hausdorff space, then `alexandroff X` is a normal (hence, regular and Hausdorff) space.

Finally, if the original space `X` is *not* compact and is a preconnected space, then
`alexandroff X` is a connected space.
-/

/-- For any topological space `X`, its one point compactification is a compact space. -/
instance : compact_space (alexandroff X) :=
{ compact_univ :=
  begin
    refine is_compact_iff_ultrafilter_le_nhds.2 (Î» f hf, _), clear hf,
    by_cases hf : (f : filter (alexandroff X)) â‰¤ ğ“ âˆ,
    { exact âŸ¨âˆ, mem_univ _, hfâŸ© },
    { simp only [ultrafilter_le_nhds_infty, not_forall, not_not] at hf,
      rcases hf with âŸ¨s, hâ‚, hâ‚‚, hsfâŸ©,
      have hf : range (coe : X â†’ alexandroff X) âˆˆ f,
        from mem_of_superset hsf (image_subset_range _ _),
      have hsf' : s âˆˆ f.comap coe_injective hf, from (f.mem_comap _ _).2 hsf,
      rcases hâ‚‚.ultrafilter_le_nhds _ (le_principal_iff.2 hsf') with âŸ¨a, has, hleâŸ©,
      rw [ultrafilter.coe_comap, â† comap_coe_nhds, comap_le_comap_iff hf] at hle,
      exact âŸ¨a, mem_univ _, hleâŸ© }
  end }

/-- The one point compactification of a `t0_space` space is a `t0_space`. -/
instance [t0_space X] : t0_space (alexandroff X) :=
begin
  refine âŸ¨Î» x y hxy, _âŸ©,
  induction x using alexandroff.rec; induction y using alexandroff.rec,
  { exact (hxy rfl).elim },
  { use {âˆ}á¶œ, simp [is_closed_infty] },
  { use {âˆ}á¶œ, simp [is_closed_infty] },
  { rcases t0_space.t0 x y (mt coe_eq_coe.mpr hxy) with âŸ¨U, hUo, hUâŸ©,
    refine âŸ¨coe '' U, is_open_image_coe.2 hUo, _âŸ©,
    simpa [coe_eq_coe] }
end

/-- The one point compactification of a `t1_space` space is a `t1_space`. -/
instance [t1_space X] : t1_space (alexandroff X) :=
{ t1 := Î» z,
  begin
    induction z using alexandroff.rec,
    { exact is_closed_infty },
    { simp only [â† image_singleton, is_closed_image_coe],
      exact âŸ¨is_closed_singleton, is_compact_singletonâŸ© }
  end }

/-- The one point compactification of a locally compact Hausdorff space is a normal (hence,
Hausdorff and regular) topological space. -/
instance [locally_compact_space X] [t2_space X] : normal_space (alexandroff X) :=
begin
  have key : âˆ€ z : X,
    âˆƒ u v : set (alexandroff X), is_open u âˆ§ is_open v âˆ§ â†‘z âˆˆ u âˆ§ âˆ âˆˆ v âˆ§ u âˆ© v = âˆ…,
  { intro z,
    rcases exists_open_with_compact_closure z with âŸ¨u, hu, huy', HuâŸ©,
    refine âŸ¨coe '' u, (coe '' closure u)á¶œ, is_open_image_coe.2 hu,
      is_open_compl_image_coe.2 âŸ¨is_closed_closure, HuâŸ©, mem_image_of_mem _ huy',
      mem_compl infty_not_mem_image_coe, _âŸ©,
    rw [â† subset_compl_iff_disjoint, compl_compl],
    exact image_subset _ subset_closure },
  refine @normal_of_compact_t2 _ _ _ âŸ¨Î» x y hxy, _âŸ©,
  induction x using alexandroff.rec; induction y using alexandroff.rec,
  { exact (hxy rfl).elim },
  { rcases key y with âŸ¨u, v, hu, hv, hxu, hyv, huvâŸ©,
    exact âŸ¨v, u, hv, hu, hyv, hxu, (inter_comm u v) â–¸ huvâŸ© },
  { exact key x },
  { exact separated_by_open_embedding open_embedding_coe (mt coe_eq_coe.mpr hxy) }
end

/-- If `X` is not a compact space, then `alexandroff X` is a connected space. -/
instance [preconnected_space X] [noncompact_space X] : connected_space (alexandroff X) :=
{ to_preconnected_space := dense_embedding_coe.to_dense_inducing.preconnected_space,
  to_nonempty := infer_instance }

end alexandroff
