/-
Copyright (c) 2020 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/
import linear_algebra.affine_space.affine_equiv
import linear_algebra.affine_space.midpoint
import topology.algebra.group
import topology.algebra.mul_action

/-!
# Topological properties of affine spaces and maps

For now, this contains only a few facts regarding the continuity of affine maps in the special
case when the point space and vector space are the same.

TODO: Deal with the case where the point spaces are different from the vector spaces. Note that
we do have some results in this direction under the assumption that the topologies are induced by
(semi)norms.
-/

open filter affine_map
open_locale topological_space

variables {Œ± X : Type*} [topological_space X]

class topological_add_torsor (E : out_param Type*) [out_param (topological_space E)]
  [out_param (add_group E)] (P : Type*) [topological_space P]
  extends add_torsor E P,  has_continuous_vadd E P :=
(continuous_vsub : continuous (Œª p : P √ó P, p.1 -·µ• p.2))

@[priority 200]
instance topological_add_group.to_topological_add_torsor {G : Type*} [add_group G]
  [topological_space G] [topological_add_group G] : topological_add_torsor G G :=
‚ü®continuous_sub‚ü©

section add_torsor

variables {E P : Type*} [topological_space E] [add_group E] [topological_space P]
  [topological_add_torsor E P]

include E

lemma filter.tendsto.vsub {f g : Œ± ‚Üí P} {l : filter Œ±} {x y : P} (hf : tendsto f l (ùìù x))
  (hg : tendsto g l (ùìù y)) : tendsto (Œª x, f x -·µ• g x) l (ùìù (x -·µ• y)) :=
(topological_add_torsor.continuous_vsub.tendsto (x, y)).comp (hf.prod_mk_nhds hg)

variables {f g : X ‚Üí P} {s : set X} {a : X}

lemma continuous_at.vsub (hf : continuous_at f a) (hg : continuous_at g a) :
  continuous_at (Œª x, f x -·µ• g x) a :=
hf.vsub hg

lemma continuous_within_at.vsub (hf : continuous_within_at f s a)
  (hg : continuous_within_at g s a) : continuous_within_at (Œª x, f x -·µ• g x) s a :=
hf.vsub hg

lemma continuous_on.vsub (hf : continuous_on f s) (hg : continuous_on g s) :
  continuous_on (Œª x, f x -·µ• g x) s :=
Œª a ha, (hf a ha).vsub (hg a ha)

@[continuity] lemma continuous.vsub (hf : continuous f) (hg : continuous g) :
  continuous (Œª x, f x -·µ• g x) :=
continuous.comp ‚Äπtopological_add_torsor E P‚Ä∫.continuous_vsub (hf.prod_mk hg)

section

variables (E P)
include P

lemma topological_add_torsor.to_topological_add_group : topological_add_group E :=
begin
  inhabit P,
  refine topological_add_group_iff_has_continuous_sub.2 ‚ü®_‚ü©,
  simpa only [‚Üê vadd_vsub_vadd_cancel_right _ _ (default : P)]
    using (continuous_fst.vadd continuous_const).vsub (continuous_snd.vadd continuous_const)
end

end

namespace homeomorph

/-- `equiv.vadd_const` as a homeomorphism. -/
@[simps {fully_applied := ff}] def vadd_const (p : P) : E ‚âÉ‚Çú P :=
{ to_equiv := equiv.vadd_const p,
  continuous_to_fun := continuous_id.vadd continuous_const,
  continuous_inv_fun := continuous_id.vsub continuous_const }

/-- `equiv.const_vsub` as a homeomorphism. -/
@[simps {fully_applied := ff}] def const_vsub (p : P) : P ‚âÉ‚Çú E :=
{ to_equiv := equiv.const_vsub p,
  continuous_to_fun := continuous_const.vsub continuous_id,
  continuous_inv_fun :=
    begin
      haveI := topological_add_torsor.to_topological_add_group E P,
      exact continuous_neg.vadd continuous_const
    end }

/-- `equiv.point_reflection` as a homeomorphism. -/
@[simps apply {fully_applied := ff}] def point_reflection (x : P) : P ‚âÉ‚Çú P :=
(const_vsub x).trans (vadd_const x)

@[simp] lemma point_reflection_symm (x : P) : (point_reflection x).symm = point_reflection x :=
by { ext y, simp [point_reflection] }

variable (P)

/-- `equiv.const_vadd` as a homeomorphism. -/
@[simps {fully_applied := ff}] def const_vadd (v : E) : P ‚âÉ‚Çú P :=
{ to_equiv := equiv.const_vadd P v,
  continuous_to_fun := continuous_const.vadd continuous_id,
  continuous_inv_fun := continuous_const.vadd continuous_id }

end homeomorph

end add_torsor

variables {R E PE F PF : Type*}
variables [add_comm_group E] [topological_space E] [topological_space PE]
  [topological_add_torsor E PE]
variables [add_comm_group F] [topological_space F] [topological_space PF]
  [topological_add_torsor F PF]

section ring

variables [ring R] [module R E] [module R F]
include E F

/-- The linear part of an affine map is continuous iff the affine map is continuous. -/
lemma affine_map.continuous_linear_iff (f : PE ‚Üí·µÉ[R] PF) : continuous f.linear ‚Üî continuous f :=
begin
  inhabit PE,
  have : ‚áëf.linear = (homeomorph.vadd_const (f default)).symm ‚àò f ‚àò (homeomorph.vadd_const default),
    from f.coe_linear default,
  rw [this, homeomorph.comp_continuous_iff, homeomorph.comp_continuous_iff']
end

variables [topological_space R] [has_continuous_smul R E]
omit F

lemma filter.tendsto.line_map {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí PE} {g : Œ± ‚Üí R} {p‚ÇÅ p‚ÇÇ : PE} {c : R} {l : filter Œ±}
  (h‚ÇÅ : tendsto f‚ÇÅ l (ùìù p‚ÇÅ)) (h‚ÇÇ : tendsto f‚ÇÇ l (ùìù p‚ÇÇ)) (hg : tendsto g l (ùìù c)) :
  tendsto (Œª x, line_map (f‚ÇÅ x) (f‚ÇÇ x) (g x)) l (ùìù $ line_map p‚ÇÅ p‚ÇÇ c) :=
(hg.smul (h‚ÇÇ.vsub h‚ÇÅ)).vadd h‚ÇÅ

lemma continuous_at.line_map {f‚ÇÅ f‚ÇÇ : X ‚Üí PE} {g : X ‚Üí R} {a : X}
  (h‚ÇÅ : continuous_at f‚ÇÅ a) (h‚ÇÇ : continuous_at f‚ÇÇ a) (hg : continuous_at g a) :
  continuous_at (Œª x, line_map (f‚ÇÅ x) (f‚ÇÇ x) (g x)) a :=
h‚ÇÅ.line_map h‚ÇÇ hg

lemma continuous_within_at.line_map {f‚ÇÅ f‚ÇÇ : X ‚Üí PE} {g : X ‚Üí R} {s : set X} {a : X}
  (h‚ÇÅ : continuous_within_at f‚ÇÅ s a) (h‚ÇÇ : continuous_within_at f‚ÇÇ s a)
  (hg : continuous_within_at g s a) :
  continuous_within_at (Œª x, line_map (f‚ÇÅ x) (f‚ÇÇ x) (g x)) s a :=
h‚ÇÅ.line_map h‚ÇÇ hg

lemma continuous_on.line_map {f‚ÇÅ f‚ÇÇ : X ‚Üí PE} {g : X ‚Üí R} {s : set X}
  (h‚ÇÅ : continuous_on f‚ÇÅ s) (h‚ÇÇ : continuous_on f‚ÇÇ s) (hg : continuous_on g s) :
  continuous_on (Œª x, line_map (f‚ÇÅ x) (f‚ÇÇ x) (g x)) s :=
Œª a ha, (h‚ÇÅ a ha).line_map (h‚ÇÇ a ha) (hg a ha)

@[continuity] lemma continuous.line_map {f‚ÇÅ f‚ÇÇ : X ‚Üí PE} {g : X ‚Üí R} (h‚ÇÅ : continuous f‚ÇÅ)
  (h‚ÇÇ : continuous f‚ÇÇ) (hg : continuous g) :
  continuous (Œª x, line_map (f‚ÇÅ x) (f‚ÇÇ x) (g x)) :=
(hg.smul (h‚ÇÇ.vsub h‚ÇÅ)).vadd h‚ÇÅ

end ring

section midpoint

variables [ring R] [invertible (2 : R)] [module R E] [has_continuous_const_smul R E]
include E

lemma filter.tendsto.midpoint {f g : Œ± ‚Üí PE} {l : filter Œ±} {a b : PE} (hf : tendsto f l (ùìù a))
  (hg : tendsto g l (ùìù b)) :
  tendsto (Œª x, midpoint R (f x) (g x)) l (ùìù (midpoint R a b)) :=
((hg.vsub hf).const_smul _).vadd hf

variables {f g : X ‚Üí PE} {s : set X} {a : X}

lemma continuous_at.midpoint (hf : continuous_at f a) (hg : continuous_at g a) :
  continuous_at (Œª x, midpoint R (f x) (g x)) a :=
hf.midpoint hg

lemma continuous_within_at.midpoint (hf : continuous_within_at f s a)
  (hg : continuous_within_at g s a) :
  continuous_within_at (Œª x, midpoint R (f x) (g x)) s a :=
hf.midpoint hg

lemma continuous_on.midpoint (hf : continuous_on f s) (hg : continuous_on g s) :
  continuous_on (Œª x, midpoint R (f x) (g x)) s :=
Œª a ha, (hf a ha).midpoint (hg a ha)

lemma continuous.midpoint (hf : continuous f) (hg : continuous g) :
  continuous (Œª x, midpoint R (f x) (g x)) :=
continuous_iff_continuous_at.2 $ Œª x, hf.continuous_at.midpoint hg.continuous_at

end midpoint

section midpoint

variables [ring R] [invertible (2 : R)] [module R E] [has_continuous_const_smul R E]
  [has_continuous_add E]
include E

lemma filter.tendsto.midpoint' {f g : Œ± ‚Üí E} {l : filter Œ±} {a b : E} (hf : tendsto f l (ùìù a))
  (hg : tendsto g l (ùìù b)) :
  tendsto (Œª x, midpoint R (f x) (g x)) l (ùìù (midpoint R a b)) :=
by simpa only [midpoint_eq_smul_add] using (hf.add hg).const_smul _

variables {f g : X ‚Üí E} {s : set X} {a : X}

lemma continuous_at.midpoint' (hf : continuous_at f a) (hg : continuous_at g a) :
  continuous_at (Œª x, midpoint R (f x) (g x)) a :=
hf.midpoint' hg

lemma continuous_within_at.midpoint' (hf : continuous_within_at f s a)
  (hg : continuous_within_at g s a) :
  continuous_within_at (Œª x, midpoint R (f x) (g x)) s a :=
hf.midpoint' hg

lemma continuous_on.midpoint' (hf : continuous_on f s) (hg : continuous_on g s) :
  continuous_on (Œª x, midpoint R (f x) (g x)) s :=
Œª a ha, (hf a ha).midpoint' (hg a ha)

lemma continuous.midpoint' (hf : continuous f) (hg : continuous g) :
  continuous (Œª x, midpoint R (f x) (g x)) :=
continuous_iff_continuous_at.2 $ Œª x, hf.continuous_at.midpoint' hg.continuous_at

end midpoint

section homothety

variables [topological_space R] [comm_ring R] [module R E] [has_continuous_smul R E]
include E

lemma filter.tendsto.homothety {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí PE} {g : Œ± ‚Üí R} {p‚ÇÅ p‚ÇÇ : PE} {c : R} {l : filter Œ±}
  (h‚ÇÅ : tendsto f‚ÇÅ l (ùìù p‚ÇÅ)) (hg : tendsto g l (ùìù c)) (h‚ÇÇ : tendsto f‚ÇÇ l (ùìù p‚ÇÇ)) :
  tendsto (Œª x, homothety (f‚ÇÅ x) (g x) (f‚ÇÇ x)) l (ùìù $ homothety p‚ÇÅ c p‚ÇÇ) :=
h‚ÇÅ.line_map h‚ÇÇ hg

lemma continuous_at.homothety {f‚ÇÅ f‚ÇÇ : X ‚Üí PE} {g : X ‚Üí R} {a : X}
  (h‚ÇÅ : continuous_at f‚ÇÅ a) (hg : continuous_at g a) (h‚ÇÇ : continuous_at f‚ÇÇ a) :
  continuous_at (Œª x, homothety (f‚ÇÅ x) (g x) (f‚ÇÇ x)) a :=
h‚ÇÅ.homothety hg h‚ÇÇ

lemma continuous_within_at.homothety {f‚ÇÅ f‚ÇÇ : X ‚Üí PE} {g : X ‚Üí R} {s : set X} {a : X}
  (h‚ÇÅ : continuous_within_at f‚ÇÅ s a) (hg : continuous_within_at g s a)
  (h‚ÇÇ : continuous_within_at f‚ÇÇ s a) :
  continuous_within_at (Œª x, homothety (f‚ÇÅ x) (g x) (f‚ÇÇ x)) s a :=
h‚ÇÅ.homothety hg h‚ÇÇ

lemma continuous_on.homothety {f‚ÇÅ f‚ÇÇ : X ‚Üí PE} {g : X ‚Üí R} {s : set X}
  (h‚ÇÅ : continuous_on f‚ÇÅ s) (hg : continuous_on g s) (h‚ÇÇ : continuous_on f‚ÇÇ s) :
  continuous_on (Œª x, homothety (f‚ÇÅ x) (g x) (f‚ÇÇ x)) s :=
h‚ÇÅ.line_map h‚ÇÇ hg

@[continuity] lemma continuous.homothety {f‚ÇÅ f‚ÇÇ : X ‚Üí PE} {g : X ‚Üí R} (h‚ÇÅ : continuous f‚ÇÅ)
  (hg : continuous g) (h‚ÇÇ : continuous f‚ÇÇ) :
  continuous (Œª x, homothety (f‚ÇÅ x) (g x) (f‚ÇÇ x)) :=
h‚ÇÅ.line_map h‚ÇÇ hg

end homothety

section const_smul

variables [comm_ring R] [module R E] [has_continuous_const_smul R E]
include E

@[continuity] lemma continuous_homothety {c : PE} {t : R} : continuous (homothety c t) :=
show continuous (Œª x, t ‚Ä¢ (x -·µ• c) +·µ• c),
from ((continuous_id.vsub continuous_const).const_smul _).vadd continuous_const

/-- Homothety about `c` with scale factor `t : RÀ£` as a homeomorphism. -/
@[simps apply {fully_applied := ff}] def homeomorph.homothety (c : PE) (t : RÀ£) : PE ‚âÉ‚Çú PE :=
{ to_equiv := affine_equiv.homothety_units_mul_hom c t,
  continuous_to_fun := continuous_homothety,
  continuous_inv_fun := continuous_homothety }

@[simp] lemma homeomorph.homothety_symm (c : PE) (t : RÀ£) :
  (homeomorph.homothety c t).symm = homeomorph.homothety c t‚Åª¬π :=
rfl

lemma is_unit.is_open_map_homothety {t : R} (ht : is_unit t) (c : PE) :
  is_open_map (homothety c t) :=
(homeomorph.homothety c ht.unit).is_open_map

lemma is_open_map_homothety {k : Type*} [field k] [module k E] [has_continuous_const_smul k E]
  (c : PE) (t : k) (ht : t ‚â† 0) : is_open_map (homothety c t) :=
(is_unit.mk0 t ht).is_open_map_homothety c

end const_smul
