/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Nicol√≤ Cavalleri
-/
import topology.algebra.module
import topology.continuous_map
import algebra.algebra.subalgebra

/-!
# Algebraic structures over continuous functions

In this file we define instances of algebraic structures over continuous functions. Instances are
present both in the case of the subtype of continuous functions and the type of continuous bundled
functions. Both implementations have advantages and disadvantages, but many experienced people in
Zulip have expressed a preference towards continuous bundled maps, so when there is no particular
reason to use the subtype, continuous bundled functions should be used for the sake of uniformity.
-/

local attribute [elab_simple] continuous.comp

namespace continuous_functions

variables {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
variables {f g : {f : Œ± ‚Üí Œ≤ | continuous f }}

instance : has_coe_to_fun {f : Œ± ‚Üí Œ≤ | continuous f} :=  ‚ü®_, subtype.val‚ü©

end continuous_functions

namespace continuous_map
variables {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]

@[to_additive]
instance has_mul [has_mul Œ≤] [has_continuous_mul Œ≤] : has_mul C(Œ±, Œ≤) :=
‚ü®Œª f g, ‚ü®f * g, continuous_mul.comp (f.continuous.prod_mk g.continuous : _)‚ü©‚ü©

@[simp, norm_cast, to_additive]
lemma mul_coe [has_mul Œ≤] [has_continuous_mul Œ≤] (f g : C(Œ±, Œ≤)) :
  ((f * g : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤) * (g : Œ± ‚Üí Œ≤) := rfl

@[to_additive]
instance [has_one Œ≤] : has_one C(Œ±, Œ≤) := ‚ü®const (1 : Œ≤)‚ü©

@[simp, norm_cast, to_additive]
lemma one_coe [has_one Œ≤]  :
  ((1 : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (1 : Œ± ‚Üí Œ≤) := rfl

@[simp, to_additive] lemma mul_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [semigroup Œ≥] [has_continuous_mul Œ≥] (f‚ÇÅ f‚ÇÇ : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) :
  (f‚ÇÅ * f‚ÇÇ).comp g = f‚ÇÅ.comp g * f‚ÇÇ.comp g :=
by { ext, simp, }

end continuous_map

section group_structure

/-!
### Group stucture

In this section we show that continuous functions valued in a topological group inherit
the structure of a group.
-/

section subtype

@[to_additive]
instance continuous_submonoid (Œ± : Type*) (Œ≤ : Type*) [topological_space Œ±] [topological_space Œ≤]
  [monoid Œ≤] [has_continuous_mul Œ≤] : is_submonoid { f : Œ± ‚Üí Œ≤ | continuous f } :=
{ one_mem := @continuous_const _ _ _ _ 1,
  mul_mem := Œª f g fc gc, continuous.comp
  has_continuous_mul.continuous_mul (continuous.prod_mk fc gc : _) }

@[to_additive]
instance continuous_subgroup (Œ± : Type*) (Œ≤ : Type*) [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] : is_subgroup { f : Œ± ‚Üí Œ≤ | continuous f } :=
{ inv_mem := Œª f fc, continuous.comp (@topological_group.continuous_inv Œ≤ _ _ _) fc,
  ..continuous_submonoid Œ± Œ≤, }.

@[to_additive]
instance continuous_monoid {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [monoid Œ≤] [has_continuous_mul Œ≤] : monoid { f : Œ± ‚Üí Œ≤ | continuous f } :=
subtype.monoid

@[to_additive]
instance continuous_group {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] : group { f : Œ± ‚Üí Œ≤ | continuous f } :=
subtype.group

@[to_additive]
instance continuous_comm_group {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [comm_group Œ≤] [topological_group Œ≤] : comm_group { f : Œ± ‚Üí Œ≤ | continuous f } :=
@subtype.comm_group _ _ _ (continuous_subgroup Œ± Œ≤) -- infer_instance doesn't work?!

end subtype

section continuous_map

@[to_additive]
instance continuous_map_semigroup {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
  [topological_space Œ≤] [semigroup Œ≤] [has_continuous_mul Œ≤] : semigroup C(Œ±, Œ≤) :=
{ mul_assoc := Œª a b c, by ext; exact mul_assoc _ _ _,
  ..continuous_map.has_mul}

@[to_additive]
instance continuous_map_monoid {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [monoid Œ≤] [has_continuous_mul Œ≤] : monoid C(Œ±, Œ≤) :=
{ one_mul := Œª a, by ext; exact one_mul _,
  mul_one := Œª a, by ext; exact mul_one _,
  ..continuous_map_semigroup,
  ..continuous_map.has_one }

@[simp, norm_cast]
lemma pow_coe {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [monoid Œ≤] [has_continuous_mul Œ≤] (f : C(Œ±, Œ≤)) (n : ‚Ñï) :
  ((f^n : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤)^n :=
begin
  ext x,
  induction n with n ih,
  { simp, },
  { simp [pow_succ, ih], },
end

@[simp] lemma continuous_map.pow_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [monoid Œ≥] [has_continuous_mul Œ≥] (f : C(Œ≤, Œ≥)) (n : ‚Ñï) (g : C(Œ±, Œ≤)) :
  (f^n).comp g = (f.comp g)^n :=
begin
  induction n with n ih,
  { ext, simp, },
  { simp [pow_succ, ih], }
end

@[to_additive]
instance continuous_map_comm_monoid {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
[topological_space Œ≤] [comm_monoid Œ≤] [has_continuous_mul Œ≤] : comm_monoid C(Œ±, Œ≤) :=
{ one_mul := Œª a, by ext; exact one_mul _,
  mul_one := Œª a, by ext; exact mul_one _,
  mul_comm := Œª a b, by ext; exact mul_comm _ _,
  ..continuous_map_semigroup,
  ..continuous_map.has_one }

@[to_additive]
instance continuous_map_group {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] : group C(Œ±, Œ≤) :=
{ inv := Œª f, ‚ü®Œª x, (f x)‚Åª¬π, continuous_inv.comp f.continuous‚ü©,
  mul_left_inv := Œª a, by ext; exact mul_left_inv _,
  ..continuous_map_monoid }

@[simp, norm_cast, to_additive]
lemma inv_coe {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] (f : C(Œ±, Œ≤)) :
  ((f‚Åª¬π : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (f‚Åª¬π : Œ± ‚Üí Œ≤) :=
rfl

@[simp, norm_cast, to_additive]
lemma div_coe {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] (f g : C(Œ±, Œ≤)) :
  ((f / g : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤) / (g : Œ± ‚Üí Œ≤) :=
by { simp only [div_eq_mul_inv], refl, }

@[simp, to_additive] lemma continuous_map.inv_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [group Œ≥] [topological_group Œ≥] (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) :
  (f‚Åª¬π).comp g = (f.comp g)‚Åª¬π :=
by { ext, simp, }

@[simp, to_additive] lemma continuous_map.div_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [group Œ≥] [topological_group Œ≥] (f g : C(Œ≤, Œ≥)) (h : C(Œ±, Œ≤)) :
  (f / g).comp h = (f.comp h) / (g.comp h) :=
by { ext, simp, }

@[to_additive]
instance continuous_map_comm_group {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
  [topological_space Œ≤] [comm_group Œ≤] [topological_group Œ≤] : comm_group C(Œ±, Œ≤) :=
{ ..continuous_map_group,
  ..continuous_map_comm_monoid }

end continuous_map

end group_structure

section ring_structure

/-!
### Ring stucture

In this section we show that continuous functions valued in a topological ring `R` inherit
the structure of a ring.
-/

section subtype

instance continuous_subring (Œ± : Type*) (R : Type*) [topological_space Œ±] [topological_space R]
  [ring R] [topological_ring R] : is_subring { f : Œ± ‚Üí R | continuous f } :=
{ ..continuous_add_subgroup Œ± R,
  ..continuous_submonoid Œ± R }.

instance continuous_ring {Œ± : Type*} {R : Type*} [topological_space Œ±] [topological_space R]
  [ring R] [topological_ring R] : ring { f : Œ± ‚Üí R | continuous f } :=
@subtype.ring _ _ _ (continuous_subring Œ± R) -- infer_instance doesn't work?!

instance continuous_comm_ring {Œ± : Type*} {R : Type*} [topological_space Œ±] [topological_space R]
  [comm_ring R] [topological_ring R] : comm_ring { f : Œ± ‚Üí R | continuous f } :=
@subtype.comm_ring _ _ _ (continuous_subring Œ± R) -- infer_instance doesn't work?!

end subtype

section continuous_map

instance continuous_map_semiring {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [semiring Œ≤] [topological_semiring Œ≤] : semiring C(Œ±, Œ≤) :=
{ left_distrib := Œª a b c, by ext; exact left_distrib _ _ _,
  right_distrib := Œª a b c, by ext; exact right_distrib _ _ _,
  zero_mul := Œª a, by ext; exact zero_mul _,
  mul_zero := Œª a, by ext; exact mul_zero _,
  ..continuous_map_add_comm_monoid,
  ..continuous_map_monoid }

instance continuous_map_ring {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [ring Œ≤] [topological_ring Œ≤] : ring C(Œ±, Œ≤) :=
{ ..continuous_map_semiring,
  ..continuous_map_add_comm_group, }

instance continuous_map_comm_ring {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
[topological_space Œ≤] [comm_ring Œ≤] [topological_ring Œ≤] : comm_ring C(Œ±, Œ≤) :=
{ ..continuous_map_semiring,
  ..continuous_map_add_comm_group,
  ..continuous_map_comm_monoid,}

end continuous_map

end ring_structure

local attribute [ext] subtype.eq

section semimodule_structure

/-!
### Semiodule stucture

In this section we show that continuous functions valued in a topological semimodule `M` over a
topological semiring `R` inherit the structure of a semimodule.
-/

section subtype

instance continuous_has_scalar {Œ± : Type*} [topological_space Œ±]
  {R : Type*} [semiring R] [topological_space R]
  {M : Type*} [topological_space M] [add_comm_group M]
  [semimodule R M] [topological_semimodule R M] :
  has_scalar R { f : Œ± ‚Üí M | continuous f } :=
‚ü®Œª r f, ‚ü®r ‚Ä¢ f, continuous_const.smul f.property‚ü©‚ü©

instance continuous_semimodule {Œ± : Type*} [topological_space Œ±]
{R : Type*} [semiring R] [topological_space R]
{M : Type*} [topological_space M] [add_comm_group M] [topological_add_group M]
[semimodule R M] [topological_semimodule R M] :
  semimodule R { f : Œ± ‚Üí M | continuous f } :=
  semimodule.of_core $
{ smul     := (‚Ä¢),
  smul_add := Œª c f g, by ext x; exact smul_add c (f x) (g x),
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, by ext x; exact add_smul c‚ÇÅ c‚ÇÇ (f x),
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, by ext x; exact mul_smul c‚ÇÅ c‚ÇÇ (f x),
  one_smul := Œª f, by ext x; exact one_smul R (f x) }

end subtype

section continuous_map
variables {Œ± : Type*} [topological_space Œ±]
  {R : Type*} [semiring R] [topological_space R]
  {M : Type*} [topological_space M] [add_comm_monoid M]

instance continuous_map_has_scalar
  [semimodule R M] [topological_semimodule R M] :
  has_scalar R C(Œ±, M) :=
‚ü®Œª r f, ‚ü®r ‚Ä¢ f, continuous_const.smul f.continuous‚ü©‚ü©

@[simp] lemma continuous_map.smul_apply [semimodule R M] [topological_semimodule R M]
  (c : R) (f : C(Œ±, M)) (a : Œ±) : (c ‚Ä¢ f) a = c ‚Ä¢ (f a) :=
rfl

@[simp] lemma continuous_map.smul_comp {Œ± : Type*} {Œ≤ : Type*}
  [topological_space Œ±] [topological_space Œ≤]
   [semimodule R M] [topological_semimodule R M] (r : R) (f : C(Œ≤, M)) (g : C(Œ±, Œ≤)) :
  (r ‚Ä¢ f).comp g = r ‚Ä¢ (f.comp g) :=
by { ext, simp, }

variables [has_continuous_add M] [semimodule R M] [topological_semimodule R M]

instance continuous_map_semimodule : semimodule R C(Œ±, M) :=
{ smul     := (‚Ä¢),
  smul_add := Œª c f g, by { ext, exact smul_add c (f x) (g x) },
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, by { ext, exact add_smul c‚ÇÅ c‚ÇÇ (f x) },
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, by { ext, exact mul_smul c‚ÇÅ c‚ÇÇ (f x) },
  one_smul := Œª f, by { ext, exact one_smul R (f x) },
  zero_smul := Œª f, by { ext, exact zero_smul _ _ },
  smul_zero := Œª r, by { ext, exact smul_zero _ } }

end continuous_map

end semimodule_structure

section algebra_structure

/-!
### Algebra structure

In this section we show that continuous functions valued in a topological algebra `A` over a ring
`R` inherit the structure of an algebra. Note that the hypothesis that `A` is a topological algebra
is obtained by requiring that `A` be both a `topological_semimodule` and a `topological_semiring`
(by now we require `topological_ring`: see TODO below).-/

section subtype

variables {Œ± : Type*} [topological_space Œ±]
{R : Type*} [comm_semiring R]
{A : Type*} [topological_space A] [ring A]
[algebra R A] [topological_ring A]

/-- Continuous constant functions as a `ring_hom`. -/
def continuous.C : R ‚Üí+* { f : Œ± ‚Üí A | continuous f } :=
{ to_fun    := Œª c : R, ‚ü®Œª x: Œ±, ((algebra_map R A) c), continuous_const‚ü©,
  map_one'  := by ext x; exact (algebra_map R A).map_one,
  map_mul'  := Œª c‚ÇÅ c‚ÇÇ, by ext x; exact (algebra_map R A).map_mul _ _,
  map_zero' := by ext x; exact (algebra_map R A).map_zero,
  map_add'  := Œª c‚ÇÅ c‚ÇÇ, by ext x; exact (algebra_map R A).map_add _ _ }

variables [topological_space R] [topological_semimodule R A]

instance : algebra R { f : Œ± ‚Üí A | continuous f } :=
{ to_ring_hom := continuous.C,
  commutes' := Œª c f, by ext x; exact algebra.commutes' _ _,
  smul_def' := Œª c f, by ext x; exact algebra.smul_def' _ _,
  ..continuous_semimodule,
  ..continuous_ring }

/- TODO: We are assuming `A` to be a ring and not a semiring just because there is not yet an
instance of semiring. In turn, we do not want to define yet an instance of semiring because there is
no `is_subsemiring` but only `subsemiring`, and it will make sense to change this when the whole
file will have no more `is_subobject`s but only `subobject`s. It does not make sense to change
it yet in this direction as `subring` does not exist yet, so everything is being blocked by
`subring`: afterwards everything will need to be updated to the new conventions of Mathlib.
Then the instance of `topological_ring` can also be removed, as it is below for `continuous_map`. -/

end subtype

section continuous_map

variables {Œ± : Type*} [topological_space Œ±]
{R : Type*} [comm_semiring R]
{A : Type*} [topological_space A] [semiring A]
[algebra R A] [topological_semiring A]

/-- Continuous constant functions as a `ring_hom`. -/
def continuous_map.C : R ‚Üí+* C(Œ±, A) :=
{ to_fun    := Œª c : R, ‚ü®Œª x: Œ±, ((algebra_map R A) c), continuous_const‚ü©,
  map_one'  := by ext x; exact (algebra_map R A).map_one,
  map_mul'  := Œª c‚ÇÅ c‚ÇÇ, by ext x; exact (algebra_map R A).map_mul _ _,
  map_zero' := by ext x; exact (algebra_map R A).map_zero,
  map_add'  := Œª c‚ÇÅ c‚ÇÇ, by ext x; exact (algebra_map R A).map_add _ _ }

@[simp] lemma continuous_map.C_apply (r : R) (a : Œ±) : continuous_map.C r a = algebra_map R A r :=
rfl

variables [topological_space R] [topological_semimodule R A]

instance continuous_map_algebra : algebra R C(Œ±, A) :=
{ to_ring_hom := continuous_map.C,
  commutes' := Œª c f, by ext x; exact algebra.commutes' _ _,
  smul_def' := Œª c f, by ext x; exact algebra.smul_def' _ _,
  ..continuous_map_semiring }

/--
A version of `separates_points` for subalgebras of the continuous functions,
used for stating the Stone-Weierstrass theorem.
-/
abbreviation subalgebra.separates_points (s : subalgebra R C(Œ±, A)) : Prop :=
separates_points ((Œª f : C(Œ±, A), (f : Œ± ‚Üí A)) '' (s : set C(Œ±, A)))

lemma subalgebra.separates_points_monotone :
  monotone (Œª s : subalgebra R C(Œ±, A), s.separates_points) :=
Œª s s' r h x y n,
begin
  obtain ‚ü®f, m, w‚ü© := h x y n,
  rcases m with ‚ü®f, ‚ü®m, rfl‚ü©‚ü©,
  exact ‚ü®_, ‚ü®f, ‚ü®r m, rfl‚ü©‚ü©, w‚ü©,
end

@[simp] lemma algebra_map_apply (k : R) (a : Œ±) :
  algebra_map R C(Œ±, A) k a = k ‚Ä¢ 1 :=
by { rw algebra.algebra_map_eq_smul_one, refl, }

variables {ùïú : Type*} [field ùïú] [topological_space ùïú] [topological_ring ùïú]

/--
Working in continuous functions into a topological field,
a subalgebra of functions that separates points also separates points strongly.

By the hypothesis, we can find a function `f` so `f x ‚â† f y`.
By an affine transformation in the field we can arrange so that `f x = a` and `f x = b`.
-/
lemma subalgebra.separates_points.strongly {s : subalgebra ùïú C(Œ±, ùïú)} (h : s.separates_points) :
  separates_points_strongly ((Œª f : C(Œ±, ùïú), (f : Œ± ‚Üí ùïú)) '' (s : set C(Œ±, ùïú))) :=
Œª x y n,
begin
  obtain ‚ü®f, ‚ü®f, ‚ü®m, rfl‚ü©‚ü©, w‚ü© := h x y n,
  replace w : f x - f y ‚â† 0 := sub_ne_zero_of_ne w,
  intros a b,
  let f' := ((b - a) * (f x - f y)‚Åª¬π) ‚Ä¢ (continuous_map.C (f x) - f) + continuous_map.C a,
  refine ‚ü®f', _, _, _‚ü©,
  { simp only [set.mem_image, coe_coe],
    refine ‚ü®f', _, rfl‚ü©,
    simp only [f', submodule.mem_coe, subalgebra.mem_to_submodule],
    -- TODO should there be a tactic for this?
    -- We could add an attribute `@[subobject_mem]`, and a tactic
    -- ``def subobject_mem := `[solve_by_elim with subobject_mem { max_depth := 10 }]``
    solve_by_elim
      [subalgebra.add_mem, subalgebra.smul_mem, subalgebra.sub_mem, subalgebra.algebra_map_mem]
      { max_depth := 6 }, },
  { simp [f'], },
  { simp [f', inv_mul_cancel_right' w], },
end

end continuous_map

end algebra_structure

section module_over_continuous_functions

/-!
### Structure as module over scalar functions

If `M` is a module over `R`, then we show that the space of continuous functions from `Œ±` to `M`
is naturally a module over the ring of continuous functions from `Œ±` to `M`. -/

section subtype

instance continuous_has_scalar' {Œ± : Type*} [topological_space Œ±]
  {R : Type*} [semiring R] [topological_space R]
  {M : Type*} [topological_space M] [add_comm_group M]
  [semimodule R M] [topological_semimodule R M] :
  has_scalar { f : Œ± ‚Üí R | continuous f } { f : Œ± ‚Üí M | continuous f } :=
‚ü®Œª f g, ‚ü®Œª x, (f x) ‚Ä¢ (g x), (continuous.smul f.2 g.2)‚ü©‚ü©

instance continuous_module' {Œ± : Type*} [topological_space Œ±]
  (R : Type*) [ring R] [topological_space R] [topological_ring R]
  (M : Type*) [topological_space M] [add_comm_group M] [topological_add_group M]
  [module R M] [topological_module R M]
  : module { f : Œ± ‚Üí R | continuous f } { f : Œ± ‚Üí M | continuous f } :=
  semimodule.of_core $
{ smul     := (‚Ä¢),
  smul_add := Œª c f g, by ext x; exact smul_add (c x) (f x) (g x),
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, by ext x; exact add_smul (c‚ÇÅ x) (c‚ÇÇ x) (f x),
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, by ext x; exact mul_smul (c‚ÇÅ x) (c‚ÇÇ x) (f x),
  one_smul := Œª f, by ext x; exact one_smul R (f x) }

end subtype

section continuous_map

instance continuous_map_has_scalar' {Œ± : Type*} [topological_space Œ±]
  {R : Type*} [semiring R] [topological_space R]
  {M : Type*} [topological_space M] [add_comm_monoid M]
  [semimodule R M] [topological_semimodule R M] :
  has_scalar C(Œ±, R) C(Œ±, M) :=
‚ü®Œª f g, ‚ü®Œª x, (f x) ‚Ä¢ (g x), (continuous.smul f.2 g.2)‚ü©‚ü©

instance continuous_map_module' {Œ± : Type*} [topological_space Œ±]
  (R : Type*) [ring R] [topological_space R] [topological_ring R]
  (M : Type*) [topological_space M] [add_comm_monoid M] [has_continuous_add M]
  [semimodule R M] [topological_semimodule R M] :
  semimodule C(Œ±, R) C(Œ±, M) :=
{ smul     := (‚Ä¢),
  smul_add := Œª c f g, by ext x; exact smul_add (c x) (f x) (g x),
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, by ext x; exact add_smul (c‚ÇÅ x) (c‚ÇÇ x) (f x),
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, by ext x; exact mul_smul (c‚ÇÅ x) (c‚ÇÇ x) (f x),
  one_smul := Œª f, by ext x; exact one_smul R (f x),
  zero_smul := Œª f, by ext x; exact zero_smul _ _,
  smul_zero := Œª r, by ext x; exact smul_zero _, }

end continuous_map

end module_over_continuous_functions

/-!
We now provide formulas for `f ‚äì g` and `f ‚äî g`, where `f g : C(Œ±, Œ≤)`,
in terms of `continuous_map.abs`.
-/

section
variables {R : Type*} [linear_ordered_field R]

-- TODO:
-- This lemma (and the next) could go all the way back in `algebra.ordered_field`,
-- except that it is tedious to prove without tactics.
-- Rather than stranding it at some intermediate location,
-- it's here, immediately prior to the point of use.
lemma min_eq_half_add_sub_abs_sub {x y : R} : min x y = 2‚Åª¬π * (x + y - abs (x - y)) :=
begin
  dsimp [min, max, abs],
  simp only [neg_le_self_iff, if_congr, sub_nonneg, neg_sub],
  split_ifs; ring; linarith,
end

lemma max_eq_half_add_add_abs_sub {x y : R} : max x y = 2‚Åª¬π * (x + y + abs (x - y)) :=
begin
  dsimp [min, max, abs],
  simp only [neg_le_self_iff, if_congr, sub_nonneg, neg_sub],
  split_ifs; ring; linarith,
end
end

namespace continuous_map

section lattice
variables {Œ± : Type*} [topological_space Œ±]
variables {Œ≤ : Type*} [linear_ordered_field Œ≤] [topological_space Œ≤]
  [order_topology Œ≤] [topological_ring Œ≤]

lemma inf_eq (f g : C(Œ±, Œ≤)) : f ‚äì g = (2‚Åª¬π : Œ≤) ‚Ä¢ (f + g - (f - g).abs) :=
ext (Œª x, by simpa using min_eq_half_add_sub_abs_sub)

-- Not sure why this is grosser than `inf_eq`:
lemma sup_eq (f g : C(Œ±, Œ≤)) : f ‚äî g = (2‚Åª¬π : Œ≤) ‚Ä¢ (f + g + (f - g).abs) :=
ext (Œª x, by simpa [mul_add] using @max_eq_half_add_add_abs_sub _ _ (f x) (g x))

end lattice

end continuous_map
