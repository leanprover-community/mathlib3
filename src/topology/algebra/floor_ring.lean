/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import algebra.order.floor
import topology.algebra.ordered.basic

/-!
# Topological facts about `int.floor`, `int.ceil` and `int.fract`

This file proves statements about limits and continuity of functions involving `floor`, `ceil` and
`fract`.

## Main declarations

* `tendsto_floor_at_top`, `tendsto_floor_at_bot`, `tendsto_ceil_at_top`, `tendsto_ceil_at_bot`:
  `int.floor` and `int.ceil` tend to +-âˆ in +-âˆ.
* `continuous_on_floor`: `int.floor` is continuous on `Ico n (n + 1)`, because constant.
* `continuous_on_ceil`: `int.ceil` is continuous on `Ioc n (n + 1)`, because constant.
* `continuous_on_fract`: `int.fract` is continuous on `Ico n (n + 1)`.
* `continuous_on.comp_fract`: Precomposing a continuous function satisfying `f 0 = f 1` with
  `int.fract` yields another continuous function.
-/

open filter function int set
open_locale topological_space

variables {Î± : Type*} [linear_ordered_ring Î±] [floor_ring Î±]

lemma tendsto_floor_at_top : tendsto (floor : Î± â†’ â„¤) at_top at_top :=
floor_mono.tendsto_at_top_at_top $ Î» b, âŸ¨(b + 1 : â„¤), by { rw floor_coe, exact (lt_add_one _).le }âŸ©

lemma tendsto_floor_at_bot : tendsto (floor : Î± â†’ â„¤) at_bot at_bot :=
floor_mono.tendsto_at_bot_at_bot $ Î» b, âŸ¨b, (floor_coe _).leâŸ©

lemma tendsto_ceil_at_top : tendsto (ceil : Î± â†’ â„¤) at_top at_top :=
ceil_mono.tendsto_at_top_at_top $ Î» b, âŸ¨b, (ceil_coe _).geâŸ©

lemma tendsto_ceil_at_bot : tendsto (ceil : Î± â†’ â„¤) at_bot at_bot :=
ceil_mono.tendsto_at_bot_at_bot $ Î» b, âŸ¨(b - 1 : â„¤), by { rw ceil_coe, exact (sub_one_lt _).le }âŸ©

variables [topological_space Î±]

lemma continuous_on_floor (n : â„¤) : continuous_on (Î» x, floor x : Î± â†’ Î±) (Ico n (n+1) : set Î±) :=
(continuous_on_congr $ floor_eq_on_Ico' n).mpr continuous_on_const

lemma continuous_on_ceil (n : â„¤) : continuous_on (Î» x, ceil x : Î± â†’ Î±) (Ioc (n-1) n : set Î±) :=
(continuous_on_congr $ ceil_eq_on_Ioc' n).mpr continuous_on_const

lemma tendsto_floor_right' [order_closed_topology Î±] (n : â„¤) :
  tendsto (Î» x, floor x : Î± â†’ Î±) (ğ“[â‰¥] n) (ğ“ n) :=
begin
  rw â† nhds_within_Ico_eq_nhds_within_Ici (lt_add_one (n : Î±)),
  simpa only [floor_coe] using
    (continuous_on_floor n _ (left_mem_Ico.mpr $ lt_add_one (_ : Î±))).tendsto
end

lemma tendsto_ceil_left' [order_closed_topology Î±] (n : â„¤) :
  tendsto (Î» x, ceil x : Î± â†’ Î±) (ğ“[â‰¤] n) (ğ“ n) :=
begin
  rw â† nhds_within_Ioc_eq_nhds_within_Iic (sub_one_lt (n : Î±)),
  simpa only [ceil_coe] using
    (continuous_on_ceil _ _ (right_mem_Ioc.mpr $ sub_one_lt (_ : Î±))).tendsto
end

lemma tendsto_floor_right [order_closed_topology Î±] (n : â„¤) :
  tendsto (Î» x, floor x : Î± â†’ Î±) (ğ“[â‰¥] n) (ğ“[â‰¥] n) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _ (tendsto_floor_right' _)
begin
  refine (eventually_nhds_within_of_forall $ Î» x (hx : (n : Î±) â‰¤ x), _),
  change _ â‰¤ _,
  norm_cast,
  convert â† floor_mono hx,
  rw floor_eq_iff,
  exact âŸ¨le_refl _, lt_add_one _âŸ©
end

lemma tendsto_ceil_left [order_closed_topology Î±] (n : â„¤) :
  tendsto (Î» x, ceil x : Î± â†’ Î±) (ğ“[â‰¤] n) (ğ“[â‰¤] n) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _ (tendsto_ceil_left' _)
begin
  refine (eventually_nhds_within_of_forall $ Î» x (hx : x â‰¤ (n : Î±)), _),
  change _ â‰¤ _,
  norm_cast,
  convert â† ceil_mono hx,
  rw ceil_eq_iff,
  exact âŸ¨sub_one_lt _, le_refl _âŸ©
end

lemma tendsto_floor_left [order_closed_topology Î±] (n : â„¤) :
  tendsto (Î» x, floor x : Î± â†’ Î±) (ğ“[<] n) (ğ“[â‰¤] (n-1)) :=
begin
  rw â† nhds_within_Ico_eq_nhds_within_Iio (sub_one_lt (n : Î±)),
  convert (tendsto_nhds_within_congr $ (Î» x hx, (floor_eq_on_Ico' (n-1) x hx).symm))
    (tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _ tendsto_const_nhds
      (eventually_of_forall (Î» _, mem_Iic.mpr $ le_refl _)));
  norm_cast <|> apply_instance,
  ring
end

lemma tendsto_ceil_right [order_closed_topology Î±] (n : â„¤) :
  tendsto (Î» x, ceil x : Î± â†’ Î±) (ğ“[>] n) (ğ“[â‰¥] (n+1)) :=
begin
  rw â† nhds_within_Ioc_eq_nhds_within_Ioi (lt_add_one (n : Î±)),
  convert (tendsto_nhds_within_congr $ (Î» x hx, (ceil_eq_on_Ioc' (n+1) x hx).symm))
    (tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _ tendsto_const_nhds
      (eventually_of_forall (Î» _, mem_Ici.mpr $ le_refl _)));
  norm_cast <|> apply_instance,
  ring
end

lemma tendsto_floor_left' [order_closed_topology Î±] (n : â„¤) :
  tendsto (Î» x, floor x : Î± â†’ Î±) (ğ“[<] n) (ğ“ (n-1)) :=
begin
  rw â† nhds_within_univ,
  exact tendsto_nhds_within_mono_right (subset_univ _) (tendsto_floor_left n),
end

lemma tendsto_ceil_right' [order_closed_topology Î±] (n : â„¤) :
  tendsto (Î» x, ceil x : Î± â†’ Î±) (ğ“[>] n) (ğ“ (n+1)) :=
begin
  rw â† nhds_within_univ,
  exact tendsto_nhds_within_mono_right (subset_univ _) (tendsto_ceil_right n),
end

lemma continuous_on_fract [topological_add_group Î±] (n : â„¤) :
  continuous_on (fract : Î± â†’ Î±) (Ico n (n+1) : set Î±) :=
continuous_on_id.sub (continuous_on_floor n)

lemma tendsto_fract_left' [order_closed_topology Î±] [topological_add_group Î±]
  (n : â„¤) : tendsto (fract : Î± â†’ Î±) (ğ“[<] n) (ğ“ 1) :=
begin
  convert (tendsto_nhds_within_of_tendsto_nhds tendsto_id).sub (tendsto_floor_left' n);
  [{norm_cast, ring}, apply_instance, apply_instance]
end

lemma tendsto_fract_left [order_closed_topology Î±] [topological_add_group Î±]
  (n : â„¤) : tendsto (fract : Î± â†’ Î±) (ğ“[<] n) (ğ“[<] 1) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
  (tendsto_fract_left' _) (eventually_of_forall fract_lt_one)

lemma tendsto_fract_right' [order_closed_topology Î±] [topological_add_group Î±]
  (n : â„¤) : tendsto (fract : Î± â†’ Î±) (ğ“[â‰¥] n) (ğ“ 0) :=
begin
  convert (tendsto_nhds_within_of_tendsto_nhds tendsto_id).sub (tendsto_floor_right' n);
  [exact (sub_self _).symm, apply_instance, apply_instance]
end

lemma tendsto_fract_right [order_closed_topology Î±] [topological_add_group Î±]
  (n : â„¤) : tendsto (fract : Î± â†’ Î±) (ğ“[â‰¥] n) (ğ“[â‰¥] 0) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
  (tendsto_fract_right' _) (eventually_of_forall fract_nonneg)

local notation `I` := (Icc 0 1 : set Î±)

lemma continuous_on.comp_fract' {Î² Î³ : Type*} [order_topology Î±]
  [topological_add_group Î±] [topological_space Î²] [topological_space Î³] {f : Î² â†’ Î± â†’ Î³}
  (h : continuous_on (uncurry f) $ (univ : set Î²).prod I) (hf : âˆ€ s, f s 0 = f s 1) :
  continuous (Î» st : Î² Ã— Î±, f st.1 $ fract st.2) :=
begin
  change continuous ((uncurry f) âˆ˜ (prod.map id (fract))),
  rw continuous_iff_continuous_at,
  rintro âŸ¨s, tâŸ©,
  by_cases ht : t = floor t,
  { rw ht,
    rw â† continuous_within_at_univ,
    have : (univ : set (Î² Ã— Î±)) âŠ† (set.prod univ (Iio $ floor t)) âˆª (set.prod univ (Ici $ floor t)),
    { rintros p -,
      rw â† prod_union,
      exact âŸ¨true.intro, lt_or_le _ _âŸ© },
    refine continuous_within_at.mono _ this,
    refine continuous_within_at.union _ _,
    { simp only [continuous_within_at, fract_coe, nhds_within_prod_eq,
                  nhds_within_univ, id.def, comp_app, prod.map_mk],
      have : (uncurry f) (s, 0) = (uncurry f) (s, (1 : Î±)),
        by simp [uncurry, hf],
      rw this,
      refine (h _ âŸ¨true.intro, by exact_mod_cast right_mem_Icc.mpr zero_le_oneâŸ©).tendsto.comp _,
      rw [nhds_within_prod_eq, nhds_within_univ],
      rw nhds_within_Icc_eq_nhds_within_Iic (@zero_lt_one Î± _ _),
      exact tendsto_id.prod_map
        (tendsto_nhds_within_mono_right Iio_subset_Iic_self $ tendsto_fract_left _) },
    { simp only [continuous_within_at, fract_coe, nhds_within_prod_eq,
                  nhds_within_univ, id.def, comp_app, prod.map_mk],
      refine (h _ âŸ¨true.intro, by exact_mod_cast left_mem_Icc.mpr zero_le_oneâŸ©).tendsto.comp _,
      rw [nhds_within_prod_eq, nhds_within_univ,
        nhds_within_Icc_eq_nhds_within_Ici (@zero_lt_one Î± _ _)],
      exact tendsto_id.prod_map (tendsto_fract_right _) } },
  { have : t âˆˆ Ioo (floor t : Î±) ((floor t : Î±) + 1),
      from âŸ¨lt_of_le_of_ne (floor_le t) (ne.symm ht), lt_floor_add_one _âŸ©,
    apply (h ((prod.map _ fract) _) âŸ¨trivial, âŸ¨fract_nonneg _, (fract_lt_one _).leâŸ©âŸ©).tendsto.comp,
    simp only [nhds_prod_eq, nhds_within_prod_eq, nhds_within_univ, id.def, prod.map_mk],
    exact continuous_at_id.tendsto.prod_map
            (tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
              (((continuous_on_fract _ _ (Ioo_subset_Ico_self this)).mono
                  Ioo_subset_Ico_self).continuous_at (Ioo_mem_nhds this.1 this.2))
              (eventually_of_forall (Î» x, âŸ¨fract_nonneg _, (fract_lt_one _).leâŸ©)) ) }
end

lemma continuous_on.comp_fract {Î² : Type*} [order_topology Î±]
  [topological_add_group Î±] [topological_space Î²] {f : Î± â†’ Î²}
  (h : continuous_on f I) (hf : f 0 = f 1) : continuous (f âˆ˜ fract) :=
begin
  let f' : unit â†’ Î± â†’ Î² := Î» x y, f y,
  have : continuous_on (uncurry f') ((univ : set unit).prod I),
  { rintros âŸ¨s, tâŸ© âŸ¨-, ht : t âˆˆ IâŸ©,
    simp only [continuous_within_at, uncurry, nhds_within_prod_eq, nhds_within_univ, f'],
    rw tendsto_prod_iff,
    intros W hW,
    specialize h t ht hW,
    rw mem_map_iff_exists_image at h,
    rcases h with âŸ¨V, hV, hVWâŸ©,
    rw image_subset_iff at hVW,
    use [univ, univ_mem, V, hV],
    intros x y hx hy,
    exact hVW hy },
  have key : continuous (Î» s, âŸ¨unit.star, sâŸ© : Î± â†’ unit Ã— Î±) := by continuity,
  exact (this.comp_fract' (Î» s, hf)).comp key
end
