/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot

Theory of topological groups.

-/
import order.filter.pointwise
import group_theory.quotient_group
import topology.algebra.monoid
import topology.homeomorph

open classical set filter topological_space
open_locale classical topological_space filter

universes u v w x
variables {Î± : Type u} {Î² : Type v} {G : Type w} {H : Type x}

section topological_group

/-- A topological (additive) group is a group in which the addition and negation operations are
continuous. -/
class topological_add_group (G : Type u) [topological_space G] [add_group G]
  extends has_continuous_add G : Prop :=
(continuous_neg : continuous (Î»a:G, -a))

/-- A topological group is a group in which the multiplication and inversion operations are
continuous. -/
@[to_additive]
class topological_group (G : Type*) [topological_space G] [group G]
  extends has_continuous_mul G : Prop :=
(continuous_inv : continuous (Î»a:G, aâ»Â¹))

variables [topological_space G] [group G]

@[to_additive]
lemma continuous_inv [topological_group G] : continuous (Î»x:G, xâ»Â¹) :=
topological_group.continuous_inv

@[to_additive, continuity]
lemma continuous.inv [topological_group G] [topological_space Î±] {f : Î± â†’ G}
  (hf : continuous f) : continuous (Î»x, (f x)â»Â¹) :=
continuous_inv.comp hf

attribute [continuity] continuous.neg

@[to_additive]
lemma continuous_on_inv [topological_group G] {s : set G} : continuous_on (Î»x:G, xâ»Â¹) s :=
continuous_inv.continuous_on

@[to_additive]
lemma continuous_on.inv [topological_group G] [topological_space Î±] {f : Î± â†’ G} {s : set Î±}
  (hf : continuous_on f s) : continuous_on (Î»x, (f x)â»Â¹) s :=
continuous_inv.comp_continuous_on hf

@[to_additive]
lemma tendsto_inv {G : Type*} [group G]
  [topological_space G] [topological_group G] (a : G) :
  tendsto (Î» x, xâ»Â¹) (nhds a) (nhds (aâ»Â¹)) :=
continuous_inv.tendsto a

/-- If a function converges to a value in a multiplicative topological group, then its inverse
converges to the inverse of this value. For the version in normed fields assuming additionally
that the limit is nonzero, use `tendsto.inv'`. -/
@[to_additive]
lemma filter.tendsto.inv [topological_group G] {f : Î± â†’ G} {x : filter Î±} {a : G}
  (hf : tendsto f x (ğ“ a)) : tendsto (Î»x, (f x)â»Â¹) x (ğ“ aâ»Â¹) :=
tendsto.comp (continuous_iff_continuous_at.mp topological_group.continuous_inv a) hf

@[to_additive]
lemma continuous_at.inv [topological_group G] [topological_space Î±] {f : Î± â†’ G} {x : Î±}
  (hf : continuous_at f x) : continuous_at (Î»x, (f x)â»Â¹) x :=
hf.inv

@[to_additive]
lemma continuous_within_at.inv [topological_group G] [topological_space Î±] {f : Î± â†’ G}
  {s : set Î±} {x : Î±} (hf : continuous_within_at f s x) :
  continuous_within_at (Î»x, (f x)â»Â¹) s x :=
hf.inv

@[to_additive]
instance [topological_group G] [topological_space H] [group H] [topological_group H] :
  topological_group (G Ã— H) :=
{ continuous_inv := continuous_fst.inv.prod_mk continuous_snd.inv }

attribute [instance] prod.topological_add_group

/-- Multiplication from the left in a topological group as a homeomorphism.-/
@[to_additive "Addition from the left in a topological additive group as a homeomorphism."]
protected def homeomorph.mul_left [topological_group G] (a : G) : G â‰ƒâ‚œ G :=
{ continuous_to_fun  := continuous_const.mul continuous_id,
  continuous_inv_fun := continuous_const.mul continuous_id,
  .. equiv.mul_left a }

@[to_additive]
lemma is_open_map_mul_left [topological_group G] (a : G) : is_open_map (Î» x, a * x) :=
(homeomorph.mul_left a).is_open_map

@[to_additive]
lemma is_closed_map_mul_left [topological_group G] (a : G) : is_closed_map (Î» x, a * x) :=
(homeomorph.mul_left a).is_closed_map

/-- Multiplication from the right in a topological group as a homeomorphism.-/
@[to_additive "Addition from the right in a topological additive group as a homeomorphism."]
protected def homeomorph.mul_right
  {G : Type*} [topological_space G] [group G] [topological_group G] (a : G) :
  G â‰ƒâ‚œ G :=
{ continuous_to_fun  := continuous_id.mul continuous_const,
  continuous_inv_fun := continuous_id.mul continuous_const,
  .. equiv.mul_right a }

@[to_additive]
lemma is_open_map_mul_right [topological_group G] (a : G) : is_open_map (Î» x, x * a) :=
(homeomorph.mul_right a).is_open_map

@[to_additive]
lemma is_closed_map_mul_right [topological_group G] (a : G) : is_closed_map (Î» x, x * a) :=
(homeomorph.mul_right a).is_closed_map

/-- Inversion in a topological group as a homeomorphism.-/
@[to_additive "Negation in a topological group as a homeomorphism."]
protected def homeomorph.inv (G : Type*) [topological_space G] [group G] [topological_group G] :
  G â‰ƒâ‚œ G :=
{ continuous_to_fun  := continuous_inv,
  continuous_inv_fun := continuous_inv,
  .. equiv.inv G }

@[to_additive exists_nhds_half]
lemma exists_nhds_split [topological_group G] {s : set G} (hs : s âˆˆ ğ“ (1 : G)) :
  âˆƒ V âˆˆ ğ“ (1 : G), âˆ€ v w âˆˆ V, v * w âˆˆ s :=
begin
  have : ((Î»a:GÃ—G, a.1 * a.2) â»Â¹' s) âˆˆ ğ“ ((1, 1) : G Ã— G) :=
    tendsto_mul (by simpa using hs),
  rw nhds_prod_eq at this,
  rcases mem_prod_iff.1 this with âŸ¨Vâ‚, Hâ‚, Vâ‚‚, Hâ‚‚, HâŸ©,
  exact âŸ¨Vâ‚ âˆ© Vâ‚‚, inter_mem_sets Hâ‚ Hâ‚‚, assume v w âŸ¨hv, _âŸ© âŸ¨_, hwâŸ©, @H (v, w) âŸ¨hv, hwâŸ©âŸ©
end

@[to_additive exists_nhds_half_neg]
lemma exists_nhds_split_inv [topological_group G] {s : set G} (hs : s âˆˆ ğ“ (1 : G)) :
  âˆƒ V âˆˆ ğ“ (1 : G), âˆ€ (v âˆˆ V) (w âˆˆ V), v * wâ»Â¹ âˆˆ s :=
begin
  have : tendsto (Î»a:GÃ—G, a.1 * (a.2)â»Â¹) (ğ“ (1:G) Ã—á¶  ğ“ (1:G)) (ğ“ 1),
  { simpa using (@tendsto_fst G G (ğ“ 1) (ğ“ 1)).mul tendsto_snd.inv },
  have : ((Î»a:GÃ—G, a.1 * (a.2)â»Â¹) â»Â¹' s) âˆˆ ğ“ (1:G) Ã—á¶  ğ“ (1:G) :=
    this (by simpa using hs),
  rcases mem_prod_self_iff.1 this with âŸ¨V, H, H'âŸ©,
  exact âŸ¨V, H, prod_subset_iff.1 H'âŸ©
end

@[to_additive exists_nhds_quarter]
lemma exists_nhds_split4 [topological_group G] {u : set G} (hu : u âˆˆ ğ“ (1 : G)) :
  âˆƒ V âˆˆ ğ“ (1 : G), âˆ€ {v w s t}, v âˆˆ V â†’ w âˆˆ V â†’ s âˆˆ V â†’ t âˆˆ V â†’ v * w * s * t âˆˆ u :=
begin
  rcases exists_nhds_split hu with âŸ¨W, W_nhd, hâŸ©,
  rcases exists_nhds_split W_nhd with âŸ¨V, V_nhd, h'âŸ©,
  existsi [V, V_nhd],
  intros v w s t v_in w_in s_in t_in,
  simpa [mul_assoc] using h _ _ (h' v w v_in w_in) (h' s t s_in t_in)
end

section
variable (G)
@[to_additive]
lemma nhds_one_symm [topological_group G] : comap (Î»r:G, râ»Â¹) (ğ“ (1 : G)) = ğ“ (1 : G) :=
begin
  have lim : tendsto (Î»r:G, râ»Â¹) (ğ“ 1) (ğ“ 1),
  { simpa using (@tendsto_id G (ğ“ 1)).inv },
  refine comap_eq_of_inverse _ _ lim lim,
  { funext x, simp },
end
end

@[to_additive]
lemma nhds_translation_mul_inv [topological_group G] (x : G) :
  comap (Î»y:G, y * xâ»Â¹) (ğ“ 1) = ğ“ x :=
begin
  refine comap_eq_of_inverse (Î»y:G, y * x) _ _ _,
  { funext x; simp },
  { suffices : tendsto (Î»y:G, y * xâ»Â¹) (ğ“ x) (ğ“ (x * xâ»Â¹)), { simpa },
    exact tendsto_id.mul tendsto_const_nhds },
  { suffices : tendsto (Î»y:G, y * x) (ğ“ 1) (ğ“ (1 * x)), { simpa },
    exact tendsto_id.mul tendsto_const_nhds }
end

@[to_additive]
lemma topological_group.ext {G : Type*} [group G] {t t' : topological_space G}
  (tg : @topological_group G t _) (tg' : @topological_group G t' _)
  (h : @nhds G t 1 = @nhds G t' 1) : t = t' :=
eq_of_nhds_eq_nhds $ Î» x, by
  rw [â† @nhds_translation_mul_inv G t _ _ x , â† @nhds_translation_mul_inv G t' _ _ x , â† h]
end topological_group

section quotient_topological_group
variables [topological_space G] [group G] [topological_group G] (N : subgroup G) (n : N.normal)

@[to_additive]
instance {G : Type u} [group G] [topological_space G] (N : subgroup G) :
  topological_space (quotient_group.quotient N) :=
by dunfold quotient_group.quotient; apply_instance

open quotient_group
@[to_additive]
lemma quotient_group_saturate {G : Type u} [group G] (N : subgroup G) (s : set G) :
  (coe : G â†’ quotient N) â»Â¹' ((coe : G â†’ quotient N) '' s) = (â‹ƒ x : N, (Î» y, y*x.1) '' s) :=
begin
  ext x,
  simp only [mem_preimage, mem_image, mem_Union, quotient_group.eq],
  split,
  { exact assume âŸ¨a, a_in, hâŸ©, âŸ¨âŸ¨_, hâŸ©, a, a_in, mul_inv_cancel_left _ _âŸ© },
  { exact assume âŸ¨âŸ¨i, hiâŸ©, a, ha, eqâŸ©,
      âŸ¨a, ha, by { simp only [eq.symm, (mul_assoc _ _ _).symm, inv_mul_cancel_left], exact hi }âŸ© }
end

@[to_additive]
lemma quotient_group.open_coe : is_open_map (coe : G â†’  quotient N) :=
begin
  intros s s_op,
  change is_open ((coe : G â†’  quotient N) â»Â¹' (coe '' s)),
  rw quotient_group_saturate N s,
  apply is_open_Union,
  rintro âŸ¨n, _âŸ©,
  exact is_open_map_mul_right n s s_op
end

@[to_additive]
instance topological_group_quotient (n : N.normal) : topological_group (quotient N) :=
{ continuous_mul := begin
    have cont : continuous ((coe : G â†’ quotient N) âˆ˜ (Î» (p : G Ã— G), p.fst * p.snd)) :=
      continuous_quot_mk.comp continuous_mul,
    have quot : quotient_map (Î» p : G Ã— G, ((p.1:quotient N), (p.2:quotient N))),
    { apply is_open_map.to_quotient_map,
      { exact is_open_map.prod (quotient_group.open_coe N) (quotient_group.open_coe N) },
      { exact (continuous_quot_mk.comp continuous_fst).prod_mk
              (continuous_quot_mk.comp continuous_snd) },
      { rintro âŸ¨âŸ¨xâŸ©, âŸ¨yâŸ©âŸ©,
        exact âŸ¨(x, y), rflâŸ© } },
    exact (quotient_map.continuous_iff quot).2 cont,
  end,
  continuous_inv := begin
    apply continuous_quotient_lift,
    change continuous ((coe : G â†’ quotient N) âˆ˜ (Î» (a : G), aâ»Â¹)),
    exact continuous_quot_mk.comp continuous_inv
  end }

attribute [instance] topological_add_group_quotient

end quotient_topological_group


section topological_add_group
variables [topological_space G] [add_group G]

@[continuity] lemma continuous.sub [topological_add_group G] [topological_space Î±] {f : Î± â†’ G} {g : Î± â†’ G}
  (hf : continuous f) (hg : continuous g) : continuous (Î»x, f x - g x) :=
by simp [sub_eq_add_neg]; exact hf.add hg.neg

lemma continuous_sub [topological_add_group G] : continuous (Î»p:GÃ—G, p.1 - p.2) :=
continuous_fst.sub continuous_snd

lemma continuous_on.sub [topological_add_group G] [topological_space Î±] {f : Î± â†’ G} {g : Î± â†’ G} {s : set Î±}
  (hf : continuous_on f s) (hg : continuous_on g s) : continuous_on (Î»x, f x - g x) s :=
continuous_sub.comp_continuous_on (hf.prod hg)

lemma filter.tendsto.sub [topological_add_group G] {f : Î± â†’ G} {g : Î± â†’ G} {x : filter Î±} {a b : G}
  (hf : tendsto f x (ğ“ a)) (hg : tendsto g x (ğ“ b)) : tendsto (Î»x, f x - g x) x (ğ“ (a - b)) :=
by simp [sub_eq_add_neg]; exact hf.add hg.neg

lemma nhds_translation [topological_add_group G] (x : G) : comap (Î»y:G, y - x) (ğ“ 0) = ğ“ x :=
nhds_translation_add_neg x

end topological_add_group

/-- additive group with a neighbourhood around 0.
Only used to construct a topology and uniform space.

This is currently only available for commutative groups, but it can be extended to
non-commutative groups too.
-/
class add_group_with_zero_nhd (G : Type u) extends add_comm_group G :=
(Z [] : filter G)
(zero_Z : pure 0 â‰¤ Z)
(sub_Z : tendsto (Î»p:GÃ—G, p.1 - p.2) (Z Ã—á¶  Z) Z)

namespace add_group_with_zero_nhd
variables (G) [add_group_with_zero_nhd G]

local notation `Z` := add_group_with_zero_nhd.Z

@[priority 100] -- see Note [lower instance priority]
instance : topological_space G :=
topological_space.mk_of_nhds $ Î»a, map (Î»x, x + a) (Z G)

variables {G}

lemma neg_Z : tendsto (Î»a:G, - a) (Z G) (Z G) :=
have tendsto (Î»a, (0:G)) (Z G) (Z G),
  by refine le_trans (assume h, _) zero_Z; simp [univ_mem_sets'] {contextual := tt},
have tendsto (Î»a:G, 0 - a) (Z G) (Z G), from
  sub_Z.comp (tendsto.prod_mk this tendsto_id),
by simpa

lemma add_Z : tendsto (Î»p:GÃ—G, p.1 + p.2) (Z G Ã—á¶  Z G) (Z G) :=
suffices tendsto (Î»p:GÃ—G, p.1 - -p.2) (Z G Ã—á¶  Z G) (Z G),
  by simpa [sub_eq_add_neg],
sub_Z.comp (tendsto.prod_mk tendsto_fst (neg_Z.comp tendsto_snd))

lemma exists_Z_half {s : set G} (hs : s âˆˆ Z G) : âˆƒ V âˆˆ Z G, âˆ€ (v âˆˆ V) (w âˆˆ V), v + w âˆˆ s :=
begin
  have : ((Î»a:GÃ—G, a.1 + a.2) â»Â¹' s) âˆˆ Z G Ã—á¶  Z G := add_Z (by simpa using hs),
  rcases mem_prod_self_iff.1 this with âŸ¨V, H, H'âŸ©,
  exact âŸ¨V, H, prod_subset_iff.1 H'âŸ©
end

lemma nhds_eq (a : G) : ğ“ a = map (Î»x, x + a) (Z G) :=
topological_space.nhds_mk_of_nhds _ _
  (assume a, calc pure a = map (Î»x, x + a) (pure 0) : by simp
    ... â‰¤ _ : map_mono zero_Z)
  (assume b s hs,
    let âŸ¨t, ht, eqtâŸ© := exists_Z_half hs in
    have t0 : (0:G) âˆˆ t, by simpa using zero_Z ht,
    begin
      refine âŸ¨(Î»x:G, x + b) '' t, image_mem_map ht, _, _âŸ©,
      { refine set.image_subset_iff.2 (assume b hbt, _),
        simpa using eqt 0 t0 b hbt },
      { rintros _ âŸ¨c, hb, rflâŸ©,
        refine (Z G).sets_of_superset ht (assume x hxt, _),
        simpa [add_assoc] using eqt _ hxt _ hb }
    end)

lemma nhds_zero_eq_Z : ğ“ 0 = Z G := by simp [nhds_eq]; exact filter.map_id

@[priority 100] -- see Note [lower instance priority]
instance : has_continuous_add G :=
âŸ¨ continuous_iff_continuous_at.2 $ assume âŸ¨a, bâŸ©,
  begin
    rw [continuous_at, nhds_prod_eq, nhds_eq, nhds_eq, nhds_eq, filter.prod_map_map_eq,
      tendsto_map'_iff],
    suffices :  tendsto ((Î»x:G, (a + b) + x) âˆ˜ (Î»p:GÃ—G,p.1 + p.2)) (Z G Ã—á¶  Z G)
      (map (Î»x:G, (a + b) + x) (Z G)),
    { simpa [(âˆ˜), add_comm, add_left_comm] },
    exact tendsto_map.comp add_Z
  end âŸ©

@[priority 100] -- see Note [lower instance priority]
instance : topological_add_group G :=
âŸ¨continuous_iff_continuous_at.2 $ assume a,
  begin
    rw [continuous_at, nhds_eq, nhds_eq, tendsto_map'_iff],
    suffices : tendsto ((Î»x:G, x - a) âˆ˜ (Î»x:G, -x)) (Z G) (map (Î»x:G, x - a) (Z G)),
    { simpa [(âˆ˜), add_comm, sub_eq_add_neg] using this },
    exact tendsto_map.comp neg_Z
  endâŸ©

end add_group_with_zero_nhd

section filter_mul

section
variables [topological_space G] [group G] [topological_group G]

@[to_additive]
lemma is_open_mul_left {s t : set G} : is_open t â†’ is_open (s * t) := Î» ht,
begin
  have : âˆ€a, is_open ((Î» (x : G), a * x) '' t),
    assume a, apply is_open_map_mul_left, exact ht,
  rw â† Union_mul_left_image,
  exact is_open_Union (Î»a, is_open_Union $ Î»ha, this _),
end

@[to_additive]
lemma is_open_mul_right {s t : set G} : is_open s â†’ is_open (s * t) := Î» hs,
begin
  have : âˆ€a, is_open ((Î» (x : G), x * a) '' s),
    assume a, apply is_open_map_mul_right, exact hs,
  rw â† Union_mul_right_image,
  exact is_open_Union (Î»a, is_open_Union $ Î»ha, this _),
end

variables (G)

lemma topological_group.t1_space (h : @is_closed G _ {1}) : t1_space G :=
âŸ¨assume x, by { convert is_closed_map_mul_right x _ h, simp }âŸ©

lemma topological_group.regular_space [t1_space G] : regular_space G :=
âŸ¨assume s a hs ha,
 let f := Î» p : G Ã— G, p.1 * (p.2)â»Â¹ in
 have hf : continuous f :=
   continuous_mul.comp (continuous_fst.prod_mk (continuous_inv.comp continuous_snd)),
 -- a âˆˆ -s implies f (a, 1) âˆˆ -s, and so (a, 1) âˆˆ fâ»Â¹' (-s);
 -- and so can find tâ‚ tâ‚‚ open such that a âˆˆ tâ‚ Ã— tâ‚‚ âŠ† fâ»Â¹' (-s)
 let âŸ¨tâ‚, tâ‚‚, htâ‚, htâ‚‚, a_mem_tâ‚, one_mem_tâ‚‚, t_subsetâŸ© :=
   is_open_prod_iff.1 (hf _ (is_open_compl_iff.2 hs)) a (1:G) (by simpa [f]) in
 begin
   use s * tâ‚‚,
   use is_open_mul_left htâ‚‚,
   use Î» x hx, âŸ¨x, 1, hx, one_mem_tâ‚‚, mul_one _âŸ©,
   apply inf_principal_eq_bot,
   rw mem_nhds_sets_iff,
   refine âŸ¨tâ‚, _, htâ‚, a_mem_tâ‚âŸ©,
   rintros x hx âŸ¨y, z, hy, hz, yzâŸ©,
   have : x * zâ»Â¹ âˆˆ sá¶œ := (prod_subset_iff.1 t_subset) x hx z hz,
   have : x * zâ»Â¹ âˆˆ s, rw â† yz, simpa,
   contradiction
 endâŸ©

local attribute [instance] topological_group.regular_space

lemma topological_group.t2_space [t1_space G] : t2_space G := regular_space.t2_space G

end

section

/-! Some results about an open set containing the product of two sets in a topological group. -/

variables [topological_space G] [group G] [topological_group G]
/-- Given a open neighborhood `U` of `1` there is a open neighborhood `V` of `1`
  such that `VV âŠ† U`. -/
@[to_additive "Given a open neighborhood `U` of `0` there is a open neighborhood `V` of `0`
  such that `V + V âŠ† U`."]
lemma one_open_separated_mul {U : set G} (h1U : is_open U) (h2U : (1 : G) âˆˆ U) :
  âˆƒ V : set G, is_open V âˆ§ (1 : G) âˆˆ V âˆ§ V * V âŠ† U :=
begin
  rcases exists_nhds_split hU with âŸ¨V, Vo, V1, hVâŸ©,
  use [V, Vo, V1],
  rintros _ âŸ¨x, y, hx, hy, rflâŸ©,
  exact hV _ hx _ hy
end

/-- Given a compact set `K` inside an open set `U`, there is a open neighborhood `V` of `1`
  such that `KV âŠ† U`. -/
@[to_additive "Given a compact set `K` inside an open set `U`, there is a open neighborhood `V` of `0`
  such that `K + V âŠ† U`."]
lemma compact_open_separated_mul {K U : set G} (hK : is_compact K) (hU : is_open U) (hKU : K âŠ† U) :
  âˆƒ V : set G, is_open V âˆ§ (1 : G) âˆˆ V âˆ§ K * V âŠ† U :=
begin
  let W : G â†’ set G := Î» x, (Î» y, x * y) â»Â¹' U,
  have h1W : âˆ€ x, is_open (W x) := Î» x, continuous_mul_left x U hU,
  have h2W : âˆ€ x âˆˆ K, (1 : G) âˆˆ W x := Î» x hx, by simp only [mem_preimage, mul_one, hKU hx],
  choose V hV using Î» x : K, one_open_separated_mul (h1W x) (h2W x.1 x.2),
  let X : K â†’ set G := Î» x, (Î» y, (x : G)â»Â¹ * y) â»Â¹' (V x),
  cases hK.elim_finite_subcover X (Î» x, continuous_mul_left xâ»Â¹ (V x) (hV x).1) _ with t ht, swap,
  { intros x hx, rw [mem_Union], use âŸ¨x, hxâŸ©, rw [mem_preimage], convert (hV _).2.1,
    simp only [mul_left_inv, subtype.coe_mk] },
  refine âŸ¨â‹‚ x âˆˆ t, V x, is_open_bInter (finite_mem_finset _) (Î» x hx, (hV x).1), _, _âŸ©,
  { simp only [mem_Inter], intros x hx, exact (hV x).2.1 },
  rintro _ âŸ¨x, y, hx, hy, rflâŸ©, simp only [mem_Inter] at hy,
  have := ht hx, simp only [mem_Union, mem_preimage] at this, rcases this with âŸ¨z, h1z, h2zâŸ©,
  have : (z : G)â»Â¹ * x * y âˆˆ W z := (hV z).2.2 (mul_mem_mul h2z (hy z h1z)),
  rw [mem_preimage] at this, convert this using 1, simp only [mul_assoc, mul_inv_cancel_left]
end

/-- A compact set is covered by finitely many left multiplicative translates of a set
  with non-empty interior. -/
@[to_additive "A compact set is covered by finitely many left additive translates of a set
  with non-empty interior."]
lemma compact_covered_by_mul_left_translates {K V : set G} (hK : is_compact K)
  (hV : (interior V).nonempty) : âˆƒ t : finset G, K âŠ† â‹ƒ g âˆˆ t, (Î» h, g * h) â»Â¹' V :=
begin
  cases hV with gâ‚€ hgâ‚€,
  rcases is_compact.elim_finite_subcover hK (Î» x : G, interior $ (Î» h, x * h) â»Â¹' V) _ _ with âŸ¨t, htâŸ©,
  { refine âŸ¨t, subset.trans ht _âŸ©,
    apply Union_subset_Union, intro g, apply Union_subset_Union, intro hg, apply interior_subset },
  { intro g, apply is_open_interior },
  { intros g hg, rw [mem_Union], use gâ‚€ * gâ»Â¹,
    apply preimage_interior_subset_interior_preimage, exact continuous_const.mul continuous_id,
    rwa [mem_preimage, inv_mul_cancel_right] }
end

end

section
variables [topological_space G] [comm_group G] [topological_group G]

@[to_additive]
lemma nhds_mul (x y : G) : ğ“ (x * y) = ğ“ x * ğ“ y :=
filter_eq $ set.ext $ assume s,
begin
  rw [â† nhds_translation_mul_inv x, â† nhds_translation_mul_inv y, â† nhds_translation_mul_inv (x*y)],
  split,
  { rintros âŸ¨t, ht, tsâŸ©,
    rcases exists_nhds_split ht with âŸ¨V, V_mem, hâŸ©,
    refine âŸ¨(Î»a, a * xâ»Â¹) â»Â¹' V, (Î»a, a * yâ»Â¹) â»Â¹' V,
            âŸ¨V, V_mem, subset.refl _âŸ©, âŸ¨V, V_mem, subset.refl _âŸ©, _âŸ©,
    rintros a âŸ¨v, w, v_mem, w_mem, rflâŸ©,
    apply ts,
    simpa [mul_comm, mul_assoc, mul_left_comm] using h (v * xâ»Â¹) (w * yâ»Â¹) v_mem w_mem },
  { rintros âŸ¨a, c, âŸ¨b, hb, baâŸ©, âŸ¨d, hd, dcâŸ©, acâŸ©,
    refine âŸ¨b âˆ© d, inter_mem_sets hb hd, assume v, _âŸ©,
    simp only [preimage_subset_iff, mul_inv_rev, mem_preimage] at *,
    rintros âŸ¨vb, vdâŸ©,
    refine ac âŸ¨v * yâ»Â¹, y, _, _, _âŸ©,
    { rw â† mul_assoc _ _ _ at vb, exact ba _ vb },
    { apply dc y, rw mul_right_inv, exact mem_of_nhds hd },
    { simp only [inv_mul_cancel_right] } }
end

@[to_additive]
lemma nhds_is_mul_hom : is_mul_hom (Î»x:G, ğ“ x) := âŸ¨Î»_ _, nhds_mul _ _âŸ©

end

end filter_mul
