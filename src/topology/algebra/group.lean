/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot

Theory of topological groups.

-/
import order.filter.pointwise
import group_theory.quotient_group
import topology.algebra.monoid
import topology.homeomorph

open classical set filter topological_space
open_locale classical topological_space

universes u v w
variables {Î± : Type u} {Î² : Type v} {Î³ : Type w}

section topological_group

section prio
set_option default_priority 100 -- see Note [default priority]
/-- A topological (additive) group is a group in which the addition and negation operations are
continuous. -/
class topological_add_group (Î± : Type u) [topological_space Î±] [add_group Î±]
  extends topological_add_monoid Î± : Prop :=
(continuous_neg : continuous (Î»a:Î±, -a))

/-- A topological group is a group in which the multiplication and inversion operations are
continuous. -/
@[to_additive topological_add_group]
class topological_group (Î± : Type*) [topological_space Î±] [group Î±]
  extends topological_monoid Î± : Prop :=
(continuous_inv : continuous (Î»a:Î±, aâ»Â¹))
end prio

variables [topological_space Î±] [group Î±]

@[to_additive]
lemma continuous_inv [topological_group Î±] : continuous (Î»x:Î±, xâ»Â¹) :=
topological_group.continuous_inv

@[to_additive]
lemma continuous.inv [topological_group Î±] [topological_space Î²] {f : Î² â†’ Î±}
  (hf : continuous f) : continuous (Î»x, (f x)â»Â¹) :=
continuous_inv.comp hf

@[to_additive]
lemma continuous_on.inv [topological_group Î±] [topological_space Î²] {f : Î² â†’ Î±} {s : set Î²}
  (hf : continuous_on f s) : continuous_on (Î»x, (f x)â»Â¹) s :=
continuous_inv.comp_continuous_on hf

/-- If a function converges to a value in a multiplicative topological group, then its inverse
converges to the inverse of this value. For the version in normed fields assuming additionally
that the limit is nonzero, use `tendsto.inv'`. -/
@[to_additive]
lemma filter.tendsto.inv [topological_group Î±] {f : Î² â†’ Î±} {x : filter Î²} {a : Î±}
  (hf : tendsto f x (ğ“ a)) : tendsto (Î»x, (f x)â»Â¹) x (ğ“ aâ»Â¹) :=
tendsto.comp (continuous_iff_continuous_at.mp topological_group.continuous_inv a) hf

@[to_additive]
lemma continuous_at.inv [topological_group Î±] [topological_space Î²] {f : Î² â†’ Î±} {x : Î²}
  (hf : continuous_at f x) : continuous_at (Î»x, (f x)â»Â¹) x :=
hf.inv

@[to_additive]
lemma continuous_within_at.inv [topological_group Î±] [topological_space Î²] {f : Î² â†’ Î±}
  {s : set Î²} {x : Î²} (hf : continuous_within_at f s x) :
  continuous_within_at (Î»x, (f x)â»Â¹) s x :=
hf.inv

@[to_additive topological_add_group]
instance [topological_group Î±] [topological_space Î²] [group Î²] [topological_group Î²] :
  topological_group (Î± Ã— Î²) :=
{ continuous_inv := continuous_fst.inv.prod_mk continuous_snd.inv }

attribute [instance] prod.topological_add_group

@[to_additive]
protected def homeomorph.mul_left [topological_group Î±] (a : Î±) : Î± â‰ƒâ‚œ Î± :=
{ continuous_to_fun  := continuous_const.mul continuous_id,
  continuous_inv_fun := continuous_const.mul continuous_id,
  .. equiv.mul_left a }

@[to_additive]
lemma is_open_map_mul_left [topological_group Î±] (a : Î±) : is_open_map (Î» x, a * x) :=
(homeomorph.mul_left a).is_open_map

@[to_additive]
lemma is_closed_map_mul_left [topological_group Î±] (a : Î±) : is_closed_map (Î» x, a * x) :=
(homeomorph.mul_left a).is_closed_map

@[to_additive]
protected def homeomorph.mul_right
  {Î± : Type*} [topological_space Î±] [group Î±] [topological_group Î±] (a : Î±) :
  Î± â‰ƒâ‚œ Î± :=
{ continuous_to_fun  := continuous_id.mul continuous_const,
  continuous_inv_fun := continuous_id.mul continuous_const,
  .. equiv.mul_right a }

@[to_additive]
lemma is_open_map_mul_right [topological_group Î±] (a : Î±) : is_open_map (Î» x, x * a) :=
(homeomorph.mul_right a).is_open_map

@[to_additive]
lemma is_closed_map_mul_right [topological_group Î±] (a : Î±) : is_closed_map (Î» x, x * a) :=
(homeomorph.mul_right a).is_closed_map

@[to_additive]
protected def homeomorph.inv (Î± : Type*) [topological_space Î±] [group Î±] [topological_group Î±] :
  Î± â‰ƒâ‚œ Î± :=
{ continuous_to_fun  := continuous_inv,
  continuous_inv_fun := continuous_inv,
  .. equiv.inv Î± }

@[to_additive exists_nhds_half]
lemma exists_nhds_split [topological_group Î±] {s : set Î±} (hs : s âˆˆ ğ“ (1 : Î±)) :
  âˆƒ V âˆˆ ğ“ (1 : Î±), âˆ€ v w âˆˆ V, v * w âˆˆ s :=
begin
  have : ((Î»a:Î±Ã—Î±, a.1 * a.2) â»Â¹' s) âˆˆ ğ“ ((1, 1) : Î± Ã— Î±) :=
    tendsto_mul (by simpa using hs),
  rw nhds_prod_eq at this,
  rcases mem_prod_iff.1 this with âŸ¨Vâ‚, Hâ‚, Vâ‚‚, Hâ‚‚, HâŸ©,
  exact âŸ¨Vâ‚ âˆ© Vâ‚‚, inter_mem_sets Hâ‚ Hâ‚‚, assume v w âŸ¨hv, _âŸ© âŸ¨_, hwâŸ©, @H (v, w) âŸ¨hv, hwâŸ©âŸ©
end

@[to_additive exists_nhds_half_neg]
lemma exists_nhds_split_inv [topological_group Î±] {s : set Î±} (hs : s âˆˆ ğ“ (1 : Î±)) :
  âˆƒ V âˆˆ ğ“ (1 : Î±), âˆ€ v w âˆˆ V, v * wâ»Â¹ âˆˆ s :=
begin
  have : tendsto (Î»a:Î±Ã—Î±, a.1 * (a.2)â»Â¹) ((ğ“ (1:Î±)).prod (ğ“ (1:Î±))) (ğ“ 1),
  { simpa using (@tendsto_fst Î± Î± (ğ“ 1) (ğ“ 1)).mul tendsto_snd.inv },
  have : ((Î»a:Î±Ã—Î±, a.1 * (a.2)â»Â¹) â»Â¹' s) âˆˆ (ğ“ (1:Î±)).prod (ğ“ (1:Î±)) :=
    this (by simpa using hs),
  rcases mem_prod_iff.1 this with âŸ¨Vâ‚, Hâ‚, Vâ‚‚, Hâ‚‚, HâŸ©,
  exact âŸ¨Vâ‚ âˆ© Vâ‚‚, inter_mem_sets Hâ‚ Hâ‚‚, assume v w âŸ¨hv, _âŸ© âŸ¨_, hwâŸ©, @H (v, w) âŸ¨hv, hwâŸ©âŸ©
end

@[to_additive exists_nhds_quarter]
lemma exists_nhds_split4 [topological_group Î±] {u : set Î±} (hu : u âˆˆ ğ“ (1 : Î±)) :
  âˆƒ V âˆˆ ğ“ (1 : Î±), âˆ€ {v w s t}, v âˆˆ V â†’ w âˆˆ V â†’ s âˆˆ V â†’ t âˆˆ V â†’ v * w * s * t âˆˆ u :=
begin
  rcases exists_nhds_split hu with âŸ¨W, W_nhd, hâŸ©,
  rcases exists_nhds_split W_nhd with âŸ¨V, V_nhd, h'âŸ©,
  existsi [V, V_nhd],
  intros v w s t v_in w_in s_in t_in,
  simpa [mul_assoc] using h _ _ (h' v w v_in w_in) (h' s t s_in t_in)
end

section
variable (Î±)
@[to_additive]
lemma nhds_one_symm [topological_group Î±] : comap (Î»r:Î±, râ»Â¹) (ğ“ (1 : Î±)) = ğ“ (1 : Î±) :=
begin
  have lim : tendsto (Î»r:Î±, râ»Â¹) (ğ“ 1) (ğ“ 1),
  { simpa using (@tendsto_id Î± (ğ“ 1)).inv },
  refine comap_eq_of_inverse _ _ lim lim,
  { funext x, simp },
end
end

@[to_additive]
lemma nhds_translation_mul_inv [topological_group Î±] (x : Î±) :
  comap (Î»y:Î±, y * xâ»Â¹) (ğ“ 1) = ğ“ x :=
begin
  refine comap_eq_of_inverse (Î»y:Î±, y * x) _ _ _,
  { funext x; simp },
  { suffices : tendsto (Î»y:Î±, y * xâ»Â¹) (ğ“ x) (ğ“ (x * xâ»Â¹)), { simpa },
    exact tendsto_id.mul tendsto_const_nhds },
  { suffices : tendsto (Î»y:Î±, y * x) (ğ“ 1) (ğ“ (1 * x)), { simpa },
    exact tendsto_id.mul tendsto_const_nhds }
end

@[to_additive]
lemma topological_group.ext {G : Type*} [group G] {t t' : topological_space G}
  (tg : @topological_group G t _) (tg' : @topological_group G t' _)
  (h : @nhds G t 1 = @nhds G t' 1) : t = t' :=
eq_of_nhds_eq_nhds $ Î» x, by
  rw [â† @nhds_translation_mul_inv G t _ _ x , â† @nhds_translation_mul_inv G t' _ _ x , â† h]
end topological_group

section quotient_topological_group
variables [topological_space Î±] [group Î±] [topological_group Î±] (N : set Î±) [normal_subgroup N]

@[to_additive]
instance {Î± : Type u} [group Î±] [topological_space Î±] (N : set Î±) [normal_subgroup N] :
  topological_space (quotient_group.quotient N) :=
by dunfold quotient_group.quotient; apply_instance

open quotient_group
@[to_additive quotient_add_group_saturate]
lemma quotient_group_saturate {Î± : Type u} [group Î±] (N : set Î±) [normal_subgroup N] (s : set Î±) :
  (coe : Î± â†’ quotient N) â»Â¹' ((coe : Î± â†’ quotient N) '' s) = (â‹ƒ x : N, (Î» y, y*x.1) '' s) :=
begin
  ext x,
  simp only [mem_preimage, mem_image, mem_Union, quotient_group.eq],
  split,
  { exact assume âŸ¨a, a_in, hâŸ©, âŸ¨âŸ¨_, hâŸ©, a, a_in, mul_inv_cancel_left _ _âŸ© },
  { exact assume âŸ¨âŸ¨i, hiâŸ©, a, ha, eqâŸ©,
      âŸ¨a, ha, by simp only [eq.symm, (mul_assoc _ _ _).symm, inv_mul_cancel_left, hi]âŸ© }
end

@[to_additive]
lemma quotient_group.open_coe : is_open_map (coe : Î± â†’  quotient N) :=
begin
  intros s s_op,
  change is_open ((coe : Î± â†’  quotient N) â»Â¹' (coe '' s)),
  rw quotient_group_saturate N s,
  apply is_open_Union,
  rintro âŸ¨n, _âŸ©,
  exact is_open_map_mul_right n s s_op
end

@[to_additive topological_add_group_quotient]
instance topological_group_quotient : topological_group (quotient N) :=
{ continuous_mul := begin
    have cont : continuous ((coe : Î± â†’ quotient N) âˆ˜ (Î» (p : Î± Ã— Î±), p.fst * p.snd)) :=
      continuous_quot_mk.comp continuous_mul,
    have quot : quotient_map (Î» p : Î± Ã— Î±, ((p.1:quotient N), (p.2:quotient N))),
    { apply is_open_map.to_quotient_map,
      { exact is_open_map.prod (quotient_group.open_coe N) (quotient_group.open_coe N) },
      { exact (continuous_quot_mk.comp continuous_fst).prod_mk
              (continuous_quot_mk.comp continuous_snd) },
      { rintro âŸ¨âŸ¨xâŸ©, âŸ¨yâŸ©âŸ©,
        exact âŸ¨(x, y), rflâŸ© } },
    exact (quotient_map.continuous_iff quot).2 cont,
  end,
  continuous_inv := begin
    apply continuous_quotient_lift,
    change continuous ((coe : Î± â†’ quotient N) âˆ˜ (Î» (a : Î±), aâ»Â¹)),
    exact continuous_quot_mk.comp continuous_inv
  end }

attribute [instance] topological_add_group_quotient

end quotient_topological_group


section topological_add_group
variables [topological_space Î±] [add_group Î±]

lemma continuous.sub [topological_add_group Î±] [topological_space Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) : continuous (Î»x, f x - g x) :=
by simp [sub_eq_add_neg]; exact hf.add hg.neg

lemma continuous_sub [topological_add_group Î±] : continuous (Î»p:Î±Ã—Î±, p.1 - p.2) :=
continuous_fst.sub continuous_snd

lemma continuous_on.sub [topological_add_group Î±] [topological_space Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±} {s : set Î²}
  (hf : continuous_on f s) (hg : continuous_on g s) : continuous_on (Î»x, f x - g x) s :=
continuous_sub.comp_continuous_on (hf.prod hg)

lemma filter.tendsto.sub [topological_add_group Î±] {f : Î² â†’ Î±} {g : Î² â†’ Î±} {x : filter Î²} {a b : Î±}
  (hf : tendsto f x (ğ“ a)) (hg : tendsto g x (ğ“ b)) : tendsto (Î»x, f x - g x) x (ğ“ (a - b)) :=
by simp [sub_eq_add_neg]; exact hf.add hg.neg

lemma nhds_translation [topological_add_group Î±] (x : Î±) : comap (Î»y:Î±, y - x) (ğ“ 0) = ğ“ x :=
nhds_translation_add_neg x

end topological_add_group

section prio
set_option default_priority 100 -- see Note [default priority]
/-- additive group with a neighbourhood around 0.
Only used to construct a topology and uniform space.

This is currently only available for commutative groups, but it can be extended to
non-commutative groups too.
-/
class add_group_with_zero_nhd (Î± : Type u) extends add_comm_group Î± :=
(Z [] : filter Î±)
(zero_Z : pure 0 â‰¤ Z)
(sub_Z : tendsto (Î»p:Î±Ã—Î±, p.1 - p.2) (Z.prod Z) Z)
end prio

namespace add_group_with_zero_nhd
variables (Î±) [add_group_with_zero_nhd Î±]

local notation `Z` := add_group_with_zero_nhd.Z

@[priority 100] -- see Note [lower instance priority]
instance : topological_space Î± :=
topological_space.mk_of_nhds $ Î»a, map (Î»x, x + a) (Z Î±)

variables {Î±}

lemma neg_Z : tendsto (Î»a:Î±, - a) (Z Î±) (Z Î±) :=
have tendsto (Î»a, (0:Î±)) (Z Î±) (Z Î±),
  by refine le_trans (assume h, _) zero_Z; simp [univ_mem_sets'] {contextual := tt},
have tendsto (Î»a:Î±, 0 - a) (Z Î±) (Z Î±), from
  sub_Z.comp (tendsto.prod_mk this tendsto_id),
by simpa

lemma add_Z : tendsto (Î»p:Î±Ã—Î±, p.1 + p.2) ((Z Î±).prod (Z Î±)) (Z Î±) :=
suffices tendsto (Î»p:Î±Ã—Î±, p.1 - -p.2) ((Z Î±).prod (Z Î±)) (Z Î±),
  by simpa [sub_eq_add_neg],
sub_Z.comp (tendsto.prod_mk tendsto_fst (neg_Z.comp tendsto_snd))

lemma exists_Z_half {s : set Î±} (hs : s âˆˆ Z Î±) : âˆƒ V âˆˆ Z Î±, âˆ€ v w âˆˆ V, v + w âˆˆ s :=
begin
  have : ((Î»a:Î±Ã—Î±, a.1 + a.2) â»Â¹' s) âˆˆ (Z Î±).prod (Z Î±) := add_Z (by simpa using hs),
  rcases mem_prod_iff.1 this with âŸ¨Vâ‚, Hâ‚, Vâ‚‚, Hâ‚‚, HâŸ©,
  exact âŸ¨Vâ‚ âˆ© Vâ‚‚, inter_mem_sets Hâ‚ Hâ‚‚, assume v w âŸ¨hv, _âŸ© âŸ¨_, hwâŸ©, @H (v, w) âŸ¨hv, hwâŸ©âŸ©
end

lemma nhds_eq (a : Î±) : ğ“ a = map (Î»x, x + a) (Z Î±) :=
topological_space.nhds_mk_of_nhds _ _
  (assume a, calc pure a = map (Î»x, x + a) (pure 0) : by simp
    ... â‰¤ _ : map_mono zero_Z)
  (assume b s hs,
    let âŸ¨t, ht, eqtâŸ© := exists_Z_half hs in
    have t0 : (0:Î±) âˆˆ t, by simpa using zero_Z ht,
    begin
      refine âŸ¨(Î»x:Î±, x + b) '' t, image_mem_map ht, _, _âŸ©,
      { refine set.image_subset_iff.2 (assume b hbt, _),
        simpa using eqt 0 b t0 hbt },
      { rintros _ âŸ¨c, hb, rflâŸ©,
        refine (Z Î±).sets_of_superset ht (assume x hxt, _),
        simpa [add_assoc] using eqt _ _ hxt hb }
    end)

lemma nhds_zero_eq_Z : ğ“ 0 = Z Î± := by simp [nhds_eq]; exact filter.map_id

@[priority 100] -- see Note [lower instance priority]
instance : topological_add_monoid Î± :=
âŸ¨ continuous_iff_continuous_at.2 $ assume âŸ¨a, bâŸ©,
  begin
    rw [continuous_at, nhds_prod_eq, nhds_eq, nhds_eq, nhds_eq, filter.prod_map_map_eq,
      tendsto_map'_iff],
    suffices :  tendsto ((Î»x:Î±, (a + b) + x) âˆ˜ (Î»p:Î±Ã—Î±,p.1 + p.2)) (filter.prod (Z Î±) (Z Î±))
      (map (Î»x:Î±, (a + b) + x) (Z Î±)),
    { simpa [(âˆ˜), add_comm, add_left_comm] },
    exact tendsto_map.comp add_Z
  endâŸ©

@[priority 100] -- see Note [lower instance priority]
instance : topological_add_group Î± :=
âŸ¨continuous_iff_continuous_at.2 $ assume a,
  begin
    rw [continuous_at, nhds_eq, nhds_eq, tendsto_map'_iff],
    suffices : tendsto ((Î»x:Î±, x - a) âˆ˜ (Î»x:Î±, -x)) (Z Î±) (map (Î»x:Î±, x - a) (Z Î±)),
    { simpa [(âˆ˜), add_comm, sub_eq_add_neg] using this },
    exact tendsto_map.comp neg_Z
  endâŸ©

end add_group_with_zero_nhd

section filter_mul
local attribute [instance]
  set.pointwise_one set.pointwise_mul set.pointwise_add filter.pointwise_mul filter.pointwise_add
  filter.pointwise_one

section
variables [topological_space Î±] [group Î±] [topological_group Î±]

@[to_additive]
lemma is_open_pointwise_mul_left {s t : set Î±} : is_open t â†’ is_open (s * t) := Î» ht,
begin
  have : âˆ€a, is_open ((Î» (x : Î±), a * x) '' t),
    assume a, apply is_open_map_mul_left, exact ht,
  rw pointwise_mul_eq_Union_mul_left,
  exact is_open_Union (Î»a, is_open_Union $ Î»ha, this _),
end

@[to_additive]
lemma is_open_pointwise_mul_right {s t : set Î±} : is_open s â†’ is_open (s * t) := Î» hs,
begin
  have : âˆ€a, is_open ((Î» (x : Î±), x * a) '' s),
    assume a, apply is_open_map_mul_right, exact hs,
  rw pointwise_mul_eq_Union_mul_right,
  exact is_open_Union (Î»a, is_open_Union $ Î»ha, this _),
end

variables (Î±)

lemma topological_group.t1_space (h : @is_closed Î± _ {1}) : t1_space Î± :=
âŸ¨assume x, by { convert is_closed_map_mul_right x _ h, simp }âŸ©

lemma topological_group.regular_space [t1_space Î±] : regular_space Î± :=
âŸ¨assume s a hs ha,
 let f := Î» p : Î± Ã— Î±, p.1 * (p.2)â»Â¹ in
 have hf : continuous f :=
   continuous_mul.comp (continuous_fst.prod_mk (continuous_inv.comp continuous_snd)),
 -- a âˆˆ -s implies f (a, 1) âˆˆ -s, and so (a, 1) âˆˆ fâ»Â¹' (-s);
 -- and so can find tâ‚ tâ‚‚ open such that a âˆˆ tâ‚ Ã— tâ‚‚ âŠ† fâ»Â¹' (-s)
 let âŸ¨tâ‚, tâ‚‚, htâ‚, htâ‚‚, a_mem_tâ‚, one_mem_tâ‚‚, t_subsetâŸ© :=
   is_open_prod_iff.1 (hf _ (is_open_compl_iff.2 hs)) a (1:Î±) (by simpa [f]) in
 begin
   use s * tâ‚‚,
   use is_open_pointwise_mul_left htâ‚‚,
   use Î» x hx, âŸ¨x, hx, 1, one_mem_tâ‚‚, (mul_one _).symmâŸ©,
   apply inf_principal_eq_bot,
   rw mem_nhds_sets_iff,
   refine âŸ¨tâ‚, _, htâ‚, a_mem_tâ‚âŸ©,
   rintros x hx âŸ¨y, hy, z, hz, yzâŸ©,
   have : x * zâ»Â¹ âˆˆ sá¶œ := (prod_subset_iff.1 t_subset) x hx z hz,
   have : x * zâ»Â¹ âˆˆ s, rw yz, simpa,
   contradiction
 endâŸ©

local attribute [instance] topological_group.regular_space

lemma topological_group.t2_space [t1_space Î±] : t2_space Î± := regular_space.t2_space Î±

end

section
variables [topological_space Î±] [comm_group Î±] [topological_group Î±]

@[to_additive]
lemma nhds_pointwise_mul (x y : Î±) : ğ“ (x * y) = ğ“ x * ğ“ y :=
filter_eq $ set.ext $ assume s,
begin
  rw [â† nhds_translation_mul_inv x, â† nhds_translation_mul_inv y, â† nhds_translation_mul_inv (x*y)],
  split,
  { rintros âŸ¨t, ht, tsâŸ©,
    rcases exists_nhds_split ht with âŸ¨V, V_mem, hâŸ©,
    refine âŸ¨(Î»a, a * xâ»Â¹) â»Â¹' V, âŸ¨V, V_mem, subset.refl _âŸ©,
            (Î»a, a * yâ»Â¹) â»Â¹' V, âŸ¨V, V_mem, subset.refl _âŸ©, _âŸ©,
    rintros a âŸ¨v, v_mem, w, w_mem, rflâŸ©,
    apply ts,
    simpa [mul_comm, mul_assoc, mul_left_comm] using h (v * xâ»Â¹) (w * yâ»Â¹) v_mem w_mem },
  { rintros âŸ¨a, âŸ¨b, hb, baâŸ©, c, âŸ¨d, hd, dcâŸ©, acâŸ©,
    refine âŸ¨b âˆ© d, inter_mem_sets hb hd, assume v, _âŸ©,
    simp only [preimage_subset_iff, mul_inv_rev, mem_preimage] at *,
    rintros âŸ¨vb, vdâŸ©,
    refine ac âŸ¨v * yâ»Â¹, _, y, _, _âŸ©,
    { rw â† mul_assoc _ _ _ at vb, exact ba _ vb },
    { apply dc y, rw mul_right_inv, exact mem_of_nhds hd },
    { simp only [inv_mul_cancel_right] } }
end

@[to_additive]
lemma nhds_is_mul_hom : is_mul_hom (Î»x:Î±, ğ“ x) := âŸ¨Î»_ _, nhds_pointwise_mul _ _âŸ©

end

end filter_mul
