/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl
-/
import algebra.hom.group_instances
import topology.algebra.uniform_group
import topology.uniform_space.completion

/-!
# Completion of topological groups:

This files endows the completion of a topological abelian group with a group structure.
More precisely the instance `uniform_space.completion.add_group` builds an abelian group structure
on the completion of an abelian group endowed with a compatible uniform structure.
Then the instance `uniform_space.completion.uniform_add_group` proves this group structure is
compatible with the completed uniform structure. The compatibility condition is `uniform_add_group`.

## Main declarations:

Beyond the instances explained above (that don't have to be explicitly invoked),
the main constructions deal with continuous group morphisms.

* `add_monoid_hom.extension`: extends a continuous group morphism from `G`
  to a complete separated group `H` to `completion G`.
* `add_monoid_hom.completion`: promotes a continuous group morphism
  from `G` to `H` into a continuous group morphism
  from `completion G` to `completion H`.
-/

noncomputable theory

universes u v

open_locale topological_space

section group
open uniform_space Cauchy filter set
variables {Œ± : Type u} [uniform_space Œ±]

instance [has_zero Œ±] : has_zero (completion Œ±) := ‚ü®(0 : Œ±)‚ü©
instance [has_neg Œ±] : has_neg (completion Œ±) := ‚ü®completion.map (Œªa, -a : Œ± ‚Üí Œ±)‚ü©
instance [has_add Œ±] : has_add (completion Œ±) := ‚ü®completion.map‚ÇÇ (+)‚ü©
instance [has_sub Œ±] : has_sub (completion Œ±) := ‚ü®completion.map‚ÇÇ has_sub.sub‚ü©

@[norm_cast]
lemma uniform_space.completion.coe_zero [has_zero Œ±] : ((0 : Œ±) : completion Œ±) = 0 := rfl
end group

namespace uniform_space.completion
section uniform_add_group
open uniform_space uniform_space.completion
variables {Œ± : Type*} [uniform_space Œ±] [add_group Œ±] [uniform_add_group Œ±]

@[norm_cast]
lemma coe_neg (a : Œ±) : ((- a : Œ±) : completion Œ±) = - a :=
(map_coe uniform_continuous_neg a).symm

@[norm_cast]
lemma coe_sub (a b : Œ±) : ((a - b : Œ±) : completion Œ±) = a - b :=
(map‚ÇÇ_coe_coe a b has_sub.sub uniform_continuous_sub).symm

@[norm_cast]
lemma coe_add (a b : Œ±) : ((a + b : Œ±) : completion Œ±) = a + b :=
(map‚ÇÇ_coe_coe a b (+) uniform_continuous_add).symm

instance : add_monoid (completion Œ±) :=
{ zero_add     := assume a, completion.induction_on a
   (is_closed_eq (continuous_map‚ÇÇ continuous_const continuous_id) continuous_id)
    (assume a, show 0 + (a : completion Œ±) = a, by rw_mod_cast zero_add),
  add_zero     := assume a, completion.induction_on a
    (is_closed_eq (continuous_map‚ÇÇ continuous_id continuous_const) continuous_id)
    (assume a, show (a : completion Œ±) + 0 = a, by rw_mod_cast add_zero),
  add_assoc    := assume a b c, completion.induction_on‚ÇÉ a b c
    (is_closed_eq
      (continuous_map‚ÇÇ
        (continuous_map‚ÇÇ continuous_fst (continuous_fst.comp continuous_snd))
        (continuous_snd.comp continuous_snd))
      (continuous_map‚ÇÇ continuous_fst
        (continuous_map‚ÇÇ
          (continuous_fst.comp continuous_snd)
          (continuous_snd.comp continuous_snd))))
    (assume a b c, show (a : completion Œ±) + b + c = a + (b + c),
      by repeat { rw_mod_cast add_assoc }),
  .. completion.has_zero, .. completion.has_neg, ..completion.has_add, .. completion.has_sub }

instance : sub_neg_monoid (completion Œ±) :=
{ sub_eq_add_neg := Œª a b, completion.induction_on‚ÇÇ a b
    (is_closed_eq (continuous_map‚ÇÇ continuous_fst continuous_snd)
      (continuous_map‚ÇÇ continuous_fst (completion.continuous_map.comp continuous_snd)))
   (Œª a b, by exact_mod_cast congr_arg coe (sub_eq_add_neg a b)),
  .. completion.add_monoid, .. completion.has_neg, .. completion.has_sub }

instance : add_group (completion Œ±) :=
{ add_left_neg := assume a, completion.induction_on a
    (is_closed_eq (continuous_map‚ÇÇ completion.continuous_map continuous_id) continuous_const)
    (assume a, show - (a : completion Œ±) + a = 0, by { rw_mod_cast add_left_neg, refl }),
  .. completion.sub_neg_monoid }

instance : uniform_add_group (completion Œ±) :=
‚ü®uniform_continuous_map‚ÇÇ has_sub.sub‚ü©

/-- The map from a group to its completion as a group hom. -/
@[simps] def to_compl : Œ± ‚Üí+ completion Œ± :=
{ to_fun := coe,
  map_add' := coe_add,
  map_zero' := coe_zero }

lemma continuous_to_compl : continuous (to_compl : Œ± ‚Üí completion Œ±) :=
continuous_coe Œ±

variables {Œ≤ : Type v} [uniform_space Œ≤] [add_group Œ≤] [uniform_add_group Œ≤]

instance {Œ± : Type u} [uniform_space Œ±] [add_comm_group Œ±] [uniform_add_group Œ±] :
  add_comm_group (completion Œ±) :=
{ add_comm  := assume a b, completion.induction_on‚ÇÇ a b
    (is_closed_eq (continuous_map‚ÇÇ continuous_fst continuous_snd)
      (continuous_map‚ÇÇ continuous_snd continuous_fst))
    (assume x y, by { change ‚Üëx + ‚Üëy = ‚Üëy + ‚Üëx, rw [‚Üê coe_add, ‚Üê coe_add, add_comm]}),
  .. completion.add_group }

-- Bourbaki GT III ¬ß3 no.4 Proposition 7
lemma _root_.filter.has_basis.completion_has_basis_closure_nhds_zero
  {Œπ : Type*} {s : Œπ ‚Üí set Œ±} {p : Œπ ‚Üí Prop} (h : (ùìù (0 : Œ±)).has_basis p s) :
  (ùìù (0 : completion Œ±)).has_basis p $ Œª i, closure $ coe '' (s i) :=
begin
  rw filter.has_basis_iff at h ‚ä¢,
  intro T,
  refine ‚ü®Œª hT, _, Œª hT, _‚ü©,
  { obtain ‚ü®V, hV_unif, hV_ball‚ü© := uniform_space.mem_nhds_iff.mp hT,
    obtain ‚ü®Z, hZ_unif, hZ_closed, hZV‚ü© := mem_uniformity_is_closed hV_unif,
    have h_coe : coe ‚Åª¬π' (ball 0 Z) ‚àà ùìù (0 : Œ±),
    { rw uniform_space.mem_nhds_iff,
      refine ‚ü®(Œª (p : Œ± √ó Œ±), (‚Üë(p.fst), ‚Üë(p.snd))) ‚Åª¬π' Z, _, rfl.subset‚ü©,
      { rw ‚Üê uniform_space.completion.comap_coe_eq_uniformity,
        use [Z, hZ_unif], }},
    obtain ‚ü®i, hi, hsi‚ü© := (h (coe ‚Åª¬π' (ball 0 Z))).mp h_coe,
    have hZ_ball_closed : is_closed (ball 0 Z),
    { exact is_closed.preimage (continuous.prod.mk 0) hZ_closed, },
    exact ‚ü®i, hi, subset_trans ((is_closed.closure_subset_iff (hZ_ball_closed)).mpr
      (set.image_subset_iff.mpr hsi)) (subset_trans (ball_mono hZV 0) hV_ball)‚ü©, },
  { obtain ‚ü®i, hi, hi'‚ü© := hT,
    suffices : closure (coe '' s i) ‚àà ùìù (0 : completion Œ±), { filter_upwards [this] using hi', },
    replace h := (h (s i)).mpr ‚ü®i, hi, set.subset.rfl‚ü©,
    exact completion.dense_inducing_coe.closure_image_mem_nhds h, },
end

end uniform_add_group

end uniform_space.completion

section add_monoid_hom
variables {Œ± Œ≤ : Type*} [uniform_space Œ±] [add_group Œ±] [uniform_add_group Œ±]
                        [uniform_space Œ≤] [add_group Œ≤] [uniform_add_group Œ≤]

open uniform_space uniform_space.completion

/-- Extension to the completion of a continuous group hom. -/
def add_monoid_hom.extension [complete_space Œ≤] [separated_space Œ≤] (f : Œ± ‚Üí+ Œ≤)
  (hf : continuous f) : completion Œ± ‚Üí+ Œ≤ :=
have hf : uniform_continuous f, from uniform_continuous_add_monoid_hom_of_continuous hf,
{ to_fun := completion.extension f,
  map_zero' := by rw [‚Üê coe_zero, extension_coe hf, f.map_zero],
  map_add' := assume a b, completion.induction_on‚ÇÇ a b
  (is_closed_eq
    (continuous_extension.comp continuous_add)
    ((continuous_extension.comp continuous_fst).add (continuous_extension.comp continuous_snd)))
  (Œª a b, by rw_mod_cast [extension_coe hf, extension_coe hf, extension_coe hf,
    f.map_add]) }

lemma add_monoid_hom.extension_coe [complete_space Œ≤] [separated_space Œ≤] (f : Œ± ‚Üí+ Œ≤)
  (hf : continuous f) (a : Œ±) : f.extension hf a = f a :=
extension_coe (uniform_continuous_add_monoid_hom_of_continuous hf) a

@[continuity]
lemma add_monoid_hom.continuous_extension [complete_space Œ≤] [separated_space Œ≤] (f : Œ± ‚Üí+ Œ≤)
  (hf : continuous f) : continuous (f.extension hf) :=
continuous_extension

/-- Completion of a continuous group hom, as a group hom. -/
def add_monoid_hom.completion (f : Œ± ‚Üí+ Œ≤) (hf : continuous f) : completion Œ± ‚Üí+ completion Œ≤ :=
(to_compl.comp f).extension (continuous_to_compl.comp hf)

@[continuity]
lemma add_monoid_hom.continuous_completion (f : Œ± ‚Üí+ Œ≤)
  (hf : continuous f) : continuous (f.completion hf : completion Œ± ‚Üí completion Œ≤) :=
continuous_map

lemma add_monoid_hom.completion_coe (f : Œ± ‚Üí+ Œ≤)
  (hf : continuous f) (a : Œ±) : f.completion hf a = f a :=
map_coe (uniform_continuous_add_monoid_hom_of_continuous hf) a

lemma add_monoid_hom.completion_zero : (0 : Œ± ‚Üí+ Œ≤).completion continuous_const = 0 :=
begin
  ext x,
  apply completion.induction_on x,
  { apply is_closed_eq ((0 : Œ± ‚Üí+ Œ≤).continuous_completion continuous_const),
    simp [continuous_const] },
  { intro a,
    simp [(0 : Œ± ‚Üí+ Œ≤).completion_coe continuous_const, coe_zero] }
end

lemma add_monoid_hom.completion_add {Œ≥ : Type*} [add_comm_group Œ≥] [uniform_space Œ≥]
  [uniform_add_group Œ≥] (f g : Œ± ‚Üí+ Œ≥) (hf : continuous f) (hg : continuous g) :
  (f + g).completion (hf.add hg) = f.completion hf + g.completion hg :=
begin
  have hfg := hf.add hg,
  ext x,
  apply completion.induction_on x,
  { exact is_closed_eq ((f+g).continuous_completion hfg)
    ((f.continuous_completion hf).add (g.continuous_completion hg)) },
  { intro a,
    simp [(f+g).completion_coe hfg, coe_add, f.completion_coe hf, g.completion_coe hg] }
end

end add_monoid_hom
