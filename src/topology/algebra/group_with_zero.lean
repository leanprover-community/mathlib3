/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import topology.algebra.monoid
import algebra.group.pi
import algebra.group_with_zero.power
import topology.homeomorph

/-!
# Topological group with zero

In this file we define `has_continuous_inv'` to be a mixin typeclass a type with `has_inv` and
`has_zero` (e.g., a `group_with_zero`) such that `Œª x, x‚Åª¬π` is continuous at all nonzero points. Any
normed (semi)field has this property. Currently the only example of `has_continuous_inv'` in
`mathlib` which is not a normed field is the type `nnnreal` (a.k.a. `‚Ñù‚â•0`) of nonnegative real
numbers.

Then we prove lemmas about continuity of `x ‚Ü¶ x‚Åª¬π` and `f / g` providing dot-style `*.inv'` and
`*.div` operations on `filter.tendsto`, `continuous_at`, `continuous_within_at`, `continuous_on`,
and `continuous`. As a special case, we provide `*.div_const` operations that require only
`group_with_zero` and `has_continuous_mul` instances.

All lemmas about `(‚Åª¬π)` use `inv'` in their names because lemmas without `'` are used for
`topological_group`s. We also use `'` in the typeclass name `has_continuous_inv'` for the sake of
consistency of notation.

On a `group_with_zero` with continuous multiplication, we also define left and right multiplication
as homeomorphisms.
-/

open_locale topological_space
open filter

/-!
### A group with zero with continuous multiplication

If `G‚ÇÄ` is a group with zero with continuous `(*)`, then `(/y)` is continuous for any `y`. In this
section we prove lemmas that immediately follow from this fact providing `*.div_const` dot-style
operations on `filter.tendsto`, `continuous_at`, `continuous_within_at`, `continuous_on`, and
`continuous`.
-/

variables {Œ± G‚ÇÄ : Type*}

section div_const

variables [group_with_zero G‚ÇÄ] [topological_space G‚ÇÄ] [has_continuous_mul G‚ÇÄ]
  {f : Œ± ‚Üí G‚ÇÄ} {s : set Œ±} {l : filter Œ±}

lemma filter.tendsto.div_const {x y : G‚ÇÄ} (hf : tendsto f l (ùìù x)) :
  tendsto (Œªa, f a / y) l (ùìù (x / y)) :=
by simpa only [div_eq_mul_inv] using hf.mul tendsto_const_nhds

variables [topological_space Œ±]

lemma continuous_at.div_const (hf : continuous f) {y : G‚ÇÄ} : continuous (Œª x, f x / y) :=
by simpa only [div_eq_mul_inv] using hf.mul continuous_const

lemma continuous_within_at.div_const {a} (hf : continuous_within_at f s a) {y : G‚ÇÄ} :
  continuous_within_at (Œª x, f x / y) s a :=
hf.div_const

lemma continuous_on.div_const (hf : continuous_on f s) {y : G‚ÇÄ} : continuous_on (Œª x, f x / y) s :=
by simpa only [div_eq_mul_inv] using hf.mul continuous_on_const

@[continuity] lemma continuous.div_const (hf : continuous f) {y : G‚ÇÄ} :
  continuous (Œª x, f x / y) :=
by simpa only [div_eq_mul_inv] using hf.mul continuous_const

end div_const

/-- A type with `0` and `has_inv` such that `Œª x, x‚Åª¬π` is continuous at all nonzero points. Any
normed (semi)field has this property. -/
class has_continuous_inv' (G‚ÇÄ : Type*) [has_zero G‚ÇÄ] [has_inv G‚ÇÄ] [topological_space G‚ÇÄ] :=
(continuous_at_inv' : ‚àÄ ‚¶Éx : G‚ÇÄ‚¶Ñ, x ‚â† 0 ‚Üí continuous_at has_inv.inv x)

export has_continuous_inv' (continuous_at_inv')

section inv'

variables [has_zero G‚ÇÄ] [has_inv G‚ÇÄ] [topological_space G‚ÇÄ] [has_continuous_inv' G‚ÇÄ]
  {l : filter Œ±} {f : Œ± ‚Üí G‚ÇÄ} {s : set Œ±} {a : Œ±}

/-!
### Continuity of `Œª x, x‚Åª¬π` at a non-zero point

We define `topological_group_with_zero` to be a `group_with_zero` such that the operation `x ‚Ü¶ x‚Åª¬π`
is continuous at all nonzero points. In this section we prove dot-style `*.inv'` lemmas for
`filter.tendsto`, `continuous_at`, `continuous_within_at`, `continuous_on`, and `continuous`.
-/

lemma tendsto_inv' {x : G‚ÇÄ}  (hx : x ‚â† 0) : tendsto has_inv.inv (ùìù x) (ùìù x‚Åª¬π) :=
continuous_at_inv' hx

lemma continuous_on_inv' : continuous_on (has_inv.inv : G‚ÇÄ ‚Üí G‚ÇÄ) {0}·∂ú :=
Œª x hx, (continuous_at_inv' hx).continuous_within_at

/-- If a function converges to a nonzero value, its inverse converges to the inverse of this value.
We use the name `tendsto.inv'` as `tendsto.inv` is already used in multiplicative topological
groups. -/
lemma filter.tendsto.inv' {a : G‚ÇÄ} (hf : tendsto f l (ùìù a))
  (ha : a ‚â† 0) :
  tendsto (Œª x, (f x)‚Åª¬π) l (ùìù a‚Åª¬π) :=
(tendsto_inv' ha).comp hf

variables [topological_space Œ±]

lemma continuous_within_at.inv' (hf : continuous_within_at f s a) (ha : f a ‚â† 0) :
  continuous_within_at (Œª x, (f x)‚Åª¬π) s a :=
hf.inv' ha

lemma continuous_at.inv' (hf : continuous_at f a) (ha : f a ‚â† 0) :
  continuous_at (Œª x, (f x)‚Åª¬π) a :=
hf.inv' ha

@[continuity] lemma continuous.inv' (hf : continuous f) (h0 : ‚àÄ x, f x ‚â† 0) :
  continuous (Œª x, (f x)‚Åª¬π) :=
continuous_iff_continuous_at.2 $ Œª x, (hf.tendsto x).inv' (h0 x)

lemma continuous_on.inv' (hf : continuous_on f s) (h0 : ‚àÄ x ‚àà s, f x ‚â† 0) :
  continuous_on (Œª x, (f x)‚Åª¬π) s :=
Œª x hx, (hf x hx).inv' (h0 x hx)

end inv'

/-!
### Continuity of division

If `G‚ÇÄ` is a `group_with_zero` with `x ‚Ü¶ x‚Åª¬π` continuous at all nonzero points and `(*)`, then
division `(/)` is continuous at any point where the denominator is continuous.
-/

section div

variables [group_with_zero G‚ÇÄ] [topological_space G‚ÇÄ] [has_continuous_inv' G‚ÇÄ]
  [has_continuous_mul G‚ÇÄ] {f g : Œ± ‚Üí G‚ÇÄ}

lemma filter.tendsto.div {l : filter Œ±} {a b : G‚ÇÄ} (hf : tendsto f l (ùìù a))
  (hg : tendsto g l (ùìù b)) (hy : b ‚â† 0) :
  tendsto (f / g) l (ùìù (a / b)) :=
by simpa only [div_eq_mul_inv] using hf.mul (hg.inv' hy)

variables [topological_space Œ±] {s : set Œ±} {a : Œ±}

lemma continuous_within_at.div (hf : continuous_within_at f s a) (hg : continuous_within_at g s a)
  (h‚ÇÄ : g a ‚â† 0) :
  continuous_within_at (f / g) s a :=
hf.div hg h‚ÇÄ

lemma continuous_on.div (hf : continuous_on f s) (hg : continuous_on g s) (h‚ÇÄ : ‚àÄ x ‚àà s, g x ‚â† 0) :
  continuous_on (f / g) s :=
Œª x hx, (hf x hx).div (hg x hx) (h‚ÇÄ x hx)

/-- Continuity at a point of the result of dividing two functions continuous at that point, where
the denominator is nonzero. -/
lemma continuous_at.div (hf : continuous_at f a) (hg : continuous_at g a) (h‚ÇÄ : g a ‚â† 0) :
  continuous_at (f / g) a :=
hf.div hg h‚ÇÄ

@[continuity] lemma continuous.div (hf : continuous f) (hg : continuous g) (h‚ÇÄ : ‚àÄ x, g x ‚â† 0) :
  continuous (f / g) :=
by simpa only [div_eq_mul_inv] using hf.mul (hg.inv' h‚ÇÄ)

lemma continuous_on_div : continuous_on (Œª p : G‚ÇÄ √ó G‚ÇÄ, p.1 / p.2) {p | p.2 ‚â† 0} :=
continuous_on_fst.div continuous_on_snd $ Œª _, id

end div

/-! ### Left and right multiplication as homeomorphisms -/

namespace homeomorph

variables [topological_space Œ±] [group_with_zero Œ±] [has_continuous_mul Œ±]

/-- Left multiplication by a nonzero element in a `group_with_zero` with continuous multiplication
is a homeomorphism of the underlying type. -/
protected def mul_left' (c : Œ±) (hc : c ‚â† 0) : Œ± ‚âÉ‚Çú Œ± :=
{ continuous_to_fun := continuous_mul_left _,
  continuous_inv_fun := continuous_mul_left _,
  .. equiv.mul_left' c hc }

/-- Right multiplication by a nonzero element in a `group_with_zero` with continuous multiplication
is a homeomorphism of the underlying type. -/
protected def mul_right' (c : Œ±) (hc : c ‚â† 0) : Œ± ‚âÉ‚Çú Œ± :=
{ continuous_to_fun := continuous_mul_right _,
  continuous_inv_fun := continuous_mul_right _,
  .. equiv.mul_right' c hc }

@[simp] lemma coe_mul_left' (c : Œ±) (hc : c ‚â† 0) : ‚áë(homeomorph.mul_left' c hc) = (*) c := rfl

@[simp] lemma mul_left'_symm_apply (c : Œ±) (hc : c ‚â† 0) :
  ((homeomorph.mul_left' c hc).symm : Œ± ‚Üí Œ±) = (*) c‚Åª¬π := rfl

@[simp] lemma coe_mul_right' (c : Œ±) (hc : c ‚â† 0) :
  ‚áë(homeomorph.mul_right' c hc) = Œª x, x * c := rfl

@[simp] lemma mul_right'_symm_apply (c : Œ±) (hc : c ‚â† 0) :
  ((homeomorph.mul_right' c hc).symm : Œ± ‚Üí Œ±) = Œª x, x * c‚Åª¬π := rfl

end homeomorph

section fpow

variables [group_with_zero G‚ÇÄ] [topological_space G‚ÇÄ] [has_continuous_inv' G‚ÇÄ]
  [has_continuous_mul G‚ÇÄ]

lemma continuous_at_fpow (x : G‚ÇÄ) (m : ‚Ñ§) (h : x ‚â† 0 ‚à® 0 ‚â§ m) : continuous_at (Œª x, x ^ m) x :=
begin
  cases m,
  { simpa only [gpow_of_nat] using continuous_at_pow x m },
  { simp only [gpow_neg_succ_of_nat],
    have hx : x ‚â† 0, from h.resolve_right (int.neg_succ_of_nat_lt_zero m).not_le,
    exact (continuous_at_pow x (m + 1)).inv' (pow_ne_zero _ hx) }
end

lemma continuous_on_fpow (m : ‚Ñ§) : continuous_on (Œª x : G‚ÇÄ, x ^ m) {0}·∂ú :=
Œª x hx, (continuous_at_fpow _ _ (or.inl hx)).continuous_within_at

lemma filter.tendsto.fpow {f : Œ± ‚Üí G‚ÇÄ} {l : filter Œ±} {a : G‚ÇÄ} (hf : tendsto f l (ùìù a)) (m : ‚Ñ§)
  (h : a ‚â† 0 ‚à® 0 ‚â§ m) :
  tendsto (Œª x, (f x) ^ m) l (ùìù (a ^ m)) :=
(continuous_at_fpow _ m h).tendsto.comp hf

variables {X : Type*} [topological_space X] {a : X} {s : set X} {f : X ‚Üí G‚ÇÄ}

lemma continuous_at.fpow (hf : continuous_at f a) (m : ‚Ñ§) (h : f a ‚â† 0 ‚à® 0 ‚â§ m) :
  continuous_at (Œª x, (f x) ^ m) a :=
hf.fpow m h

lemma continuous_within_at.fpow (hf : continuous_within_at f s a) (m : ‚Ñ§) (h : f a ‚â† 0 ‚à® 0 ‚â§ m) :
  continuous_within_at (Œª x, f x ^ m) s a :=
hf.fpow m h

lemma continuous_on.fpow (hf : continuous_on f s) (m : ‚Ñ§) (h : ‚àÄ a ‚àà s, f a ‚â† 0 ‚à® 0 ‚â§ m) :
  continuous_on (Œª x, f x ^ m) s :=
Œª a ha, (hf a ha).fpow m (h a ha)

@[continuity] lemma continuous.fpow (hf : continuous f) (m : ‚Ñ§) (h0 : ‚àÄ a, f a ‚â† 0 ‚à® 0 ‚â§ m) :
  continuous (Œª x, (f x) ^ m) :=
continuous_iff_continuous_at.2 $ Œª x, (hf.tendsto x).fpow m (h0 x)

end fpow
