/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import algebra.big_operators.intervals
import algebra.big_operators.nat_antidiagonal
import logic.encodable.lattice
import topology.algebra.mul_action
import topology.algebra.order.monotone_convergence
import topology.instances.real

/-!
# Infinite sum over a topological monoid

This sum is known as unconditionally convergent, as it sums to the same value under all possible
permutations. For Euclidean spaces (finite dimensional Banach spaces) this is equivalent to absolute
convergence.

Note: There are summable sequences which are not unconditionally convergent! The other way holds
generally, see `has_sum.tendsto_sum_nat`.

## References

* Bourbaki: General Topology (1995), Chapter 3 Â§5 (Infinite sums in commutative groups)

-/

noncomputable theory
open finset filter function classical
open_locale topological_space classical big_operators nnreal

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

section has_sum
variables [add_comm_monoid Î±] [topological_space Î±]

/-- Infinite sum on a topological monoid

The `at_top` filter on `finset Î²` is the limit of all finite sets towards the entire type. So we sum
up bigger and bigger sets. This sum operation is invariant under reordering. In particular,
the function `â„• â†’ â„` sending `n` to `(-1)^n / (n+1)` does not have a
sum for this definition, but a series which is absolutely convergent will have the correct sum.

This is based on Mario Carneiro's
[infinite sum `df-tsms` in Metamath](http://us.metamath.org/mpeuni/df-tsms.html).

For the definition or many statements, `Î±` does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant.
-/
def has_sum (f : Î² â†’ Î±) (a : Î±) : Prop := tendsto (Î»s:finset Î², âˆ‘ b in s, f b) at_top (ğ“ a)

/-- `summable f` means that `f` has some (infinite) sum. Use `tsum` to get the value. -/
def summable (f : Î² â†’ Î±) : Prop := âˆƒa, has_sum f a

/-- `âˆ‘' i, f i` is the sum of `f` it exists, or 0 otherwise -/
@[irreducible] def tsum {Î²} (f : Î² â†’ Î±) := if h : summable f then classical.some h else 0

-- see Note [operator precedence of big operators]
notation `âˆ‘'` binders `, ` r:(scoped:67 f, tsum f) := r

variables {f g : Î² â†’ Î±} {a b : Î±} {s : finset Î²}

lemma summable.has_sum (ha : summable f) : has_sum f (âˆ‘'b, f b) :=
by simp [ha, tsum]; exact some_spec ha

lemma has_sum.summable (h : has_sum f a) : summable f := âŸ¨a, hâŸ©

/-- Constant zero function has sum `0` -/
lemma has_sum_zero : has_sum (Î»b, 0 : Î² â†’ Î±) 0 :=
by simp [has_sum, tendsto_const_nhds]

lemma has_sum_empty [is_empty Î²] : has_sum f 0 :=
by convert has_sum_zero

lemma summable_zero : summable (Î»b, 0 : Î² â†’ Î±) := has_sum_zero.summable

lemma summable_empty [is_empty Î²] : summable f := has_sum_empty.summable

lemma tsum_eq_zero_of_not_summable (h : Â¬ summable f) : âˆ‘'b, f b = 0 :=
by simp [tsum, h]

lemma summable_congr (hfg : âˆ€b, f b = g b) :
  summable f â†” summable g :=
iff_of_eq (congr_arg summable $ funext hfg)

lemma summable.congr (hf : summable f) (hfg : âˆ€b, f b = g b) :
  summable g :=
(summable_congr hfg).mp hf

lemma has_sum.has_sum_of_sum_eq {g : Î³ â†’ Î±}
  (h_eq : âˆ€u:finset Î³, âˆƒv:finset Î², âˆ€v', v âŠ† v' â†’ âˆƒu', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
  (hf : has_sum g a) :
  has_sum f a :=
le_trans (map_at_top_finset_sum_le_of_sum_eq h_eq) hf

lemma has_sum_iff_has_sum {g : Î³ â†’ Î±}
  (hâ‚ : âˆ€u:finset Î³, âˆƒv:finset Î², âˆ€v', v âŠ† v' â†’ âˆƒu', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
  (hâ‚‚ : âˆ€v:finset Î², âˆƒu:finset Î³, âˆ€u', u âŠ† u' â†’ âˆƒv', v âŠ† v' âˆ§ âˆ‘ b in v', f b = âˆ‘ x in u', g x) :
  has_sum f a â†” has_sum g a :=
âŸ¨has_sum.has_sum_of_sum_eq hâ‚‚, has_sum.has_sum_of_sum_eq hâ‚âŸ©

lemma function.injective.has_sum_iff {g : Î³ â†’ Î²} (hg : injective g)
  (hf : âˆ€ x âˆ‰ set.range g, f x = 0) :
  has_sum (f âˆ˜ g) a â†” has_sum f a :=
by simp only [has_sum, tendsto, hg.map_at_top_finset_sum_eq hf]

lemma function.injective.summable_iff {g : Î³ â†’ Î²} (hg : injective g)
  (hf : âˆ€ x âˆ‰ set.range g, f x = 0) :
  summable (f âˆ˜ g) â†” summable f :=
exists_congr $ Î» _, hg.has_sum_iff hf

lemma has_sum_subtype_iff_of_support_subset {s : set Î²} (hf : support f âŠ† s) :
  has_sum (f âˆ˜ coe : s â†’ Î±) a â†” has_sum f a :=
subtype.coe_injective.has_sum_iff $ by simpa using support_subset_iff'.1 hf

lemma has_sum_subtype_iff_indicator {s : set Î²} :
  has_sum (f âˆ˜ coe : s â†’ Î±) a â†” has_sum (s.indicator f) a :=
by rw [â† set.indicator_range_comp, subtype.range_coe,
  has_sum_subtype_iff_of_support_subset set.support_indicator_subset]

lemma summable_subtype_iff_indicator {s : set Î²} :
  summable (f âˆ˜ coe : s â†’ Î±) â†” summable (s.indicator f) :=
exists_congr (Î» _, has_sum_subtype_iff_indicator)

@[simp] lemma has_sum_subtype_support : has_sum (f âˆ˜ coe : support f â†’ Î±) a â†” has_sum f a :=
has_sum_subtype_iff_of_support_subset $ set.subset.refl _

lemma has_sum_fintype [fintype Î²] (f : Î² â†’ Î±) : has_sum f (âˆ‘ b, f b) :=
order_top.tendsto_at_top_nhds _

protected lemma finset.has_sum (s : finset Î²) (f : Î² â†’ Î±) :
  has_sum (f âˆ˜ coe : (â†‘s : set Î²) â†’ Î±) (âˆ‘ b in s, f b) :=
by { rw â† sum_attach, exact has_sum_fintype _ }

protected lemma finset.summable (s : finset Î²) (f : Î² â†’ Î±) :
  summable (f âˆ˜ coe : (â†‘s : set Î²) â†’ Î±) :=
(s.has_sum f).summable

protected lemma set.finite.summable {s : set Î²} (hs : s.finite) (f : Î² â†’ Î±) :
  summable (f âˆ˜ coe : s â†’ Î±) :=
by convert hs.to_finset.summable f; simp only [hs.coe_to_finset]

/-- If a function `f` vanishes outside of a finite set `s`, then it `has_sum` `âˆ‘ b in s, f b`. -/
lemma has_sum_sum_of_ne_finset_zero (hf : âˆ€bâˆ‰s, f b = 0) : has_sum f (âˆ‘ b in s, f b) :=
(has_sum_subtype_iff_of_support_subset $ support_subset_iff'.2 hf).1 $ s.has_sum f

lemma summable_of_ne_finset_zero (hf : âˆ€bâˆ‰s, f b = 0) : summable f :=
(has_sum_sum_of_ne_finset_zero hf).summable

lemma has_sum_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€b' â‰  b, f b' = 0) :
  has_sum f (f b) :=
suffices has_sum f (âˆ‘ b' in {b}, f b'),
  by simpa using this,
has_sum_sum_of_ne_finset_zero $ by simpa [hf]

lemma has_sum_ite_eq (b : Î²) [decidable_pred (= b)] (a : Î±) :
  has_sum (Î»b', if b' = b then a else 0) a :=
begin
  convert has_sum_single b _,
  { exact (if_pos rfl).symm },
  assume b' hb',
  exact if_neg hb'
end

lemma equiv.has_sum_iff (e : Î³ â‰ƒ Î²) :
  has_sum (f âˆ˜ e) a â†” has_sum f a :=
e.injective.has_sum_iff $ by simp

lemma function.injective.has_sum_range_iff {g : Î³ â†’ Î²} (hg : injective g) :
  has_sum (Î» x : set.range g, f x) a â†” has_sum (f âˆ˜ g) a :=
(equiv.of_injective g hg).has_sum_iff.symm

lemma equiv.summable_iff (e : Î³ â‰ƒ Î²) :
  summable (f âˆ˜ e) â†” summable f :=
exists_congr $ Î» a, e.has_sum_iff

lemma summable.prod_symm {f : Î² Ã— Î³ â†’ Î±} (hf : summable f) : summable (Î» p : Î³ Ã— Î², f p.swap) :=
(equiv.prod_comm Î³ Î²).summable_iff.2 hf

lemma equiv.has_sum_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x : support f, g (e x) = f x) :
  has_sum f a â†” has_sum g a :=
have (g âˆ˜ coe) âˆ˜ e = f âˆ˜ coe, from funext he,
by rw [â† has_sum_subtype_support, â† this, e.has_sum_iff, has_sum_subtype_support]

lemma has_sum_iff_has_sum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
  (hi : âˆ€ â¦ƒx yâ¦„, i x = i y â†’ (x : Î³) = y)
  (hf : support f âŠ† set.range i) (hfg : âˆ€ x, f (i x) = g x) :
  has_sum f a â†” has_sum g a :=
iff.symm $ equiv.has_sum_iff_of_support
  (equiv.of_bijective (Î» x, âŸ¨i x, Î» hx, x.coe_prop $ hfg x â–¸ hxâŸ©)
    âŸ¨Î» x y h, subtype.ext $ hi $ subtype.ext_iff.1 h,
      Î» y, (hf y.coe_prop).imp $ Î» x hx, subtype.ext hxâŸ©)
  hfg

lemma equiv.summable_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x : support f, g (e x) = f x) :
  summable f â†” summable g :=
exists_congr $ Î» _, e.has_sum_iff_of_support he

protected lemma has_sum.map [add_comm_monoid Î³] [topological_space Î³] (hf : has_sum f a)
  {G} [add_monoid_hom_class G Î± Î³] (g : G) (hg : continuous g) :
  has_sum (g âˆ˜ f) (g a) :=
have g âˆ˜ (Î»s:finset Î², âˆ‘ b in s, f b) = (Î»s:finset Î², âˆ‘ b in s, g (f b)),
  from funext $ map_sum g _,
show tendsto (Î»s:finset Î², âˆ‘ b in s, g (f b)) at_top (ğ“ (g a)),
  from this â–¸ (hg.tendsto a).comp hf

protected lemma summable.map [add_comm_monoid Î³] [topological_space Î³] (hf : summable f)
  {G} [add_monoid_hom_class G Î± Î³] (g : G) (hg : continuous g) :
  summable (g âˆ˜ f) :=
(hf.has_sum.map g hg).summable

protected lemma summable.map_iff_of_left_inverse [add_comm_monoid Î³] [topological_space Î³]
  {G G'} [add_monoid_hom_class G Î± Î³] [add_monoid_hom_class G' Î³ Î±] (g : G) (g' : G')
  (hg : continuous g) (hg' : continuous g') (hinv : function.left_inverse g' g) :
  summable (g âˆ˜ f) â†” summable f :=
âŸ¨Î» h, begin
  have := h.map _ hg',
  rwa [â†function.comp.assoc, hinv.id] at this,
end, Î» h, h.map _ hgâŸ©

/-- A special case of `summable.map_iff_of_left_inverse` for convenience -/
protected lemma summable.map_iff_of_equiv [add_comm_monoid Î³] [topological_space Î³]
  {G} [add_equiv_class G Î± Î³] (g : G)
  (hg : continuous g) (hg' : continuous (add_equiv_class.inv g : Î³ â†’ Î±)) :
  summable (g âˆ˜ f) â†” summable f :=
summable.map_iff_of_left_inverse g (g : Î± â‰ƒ+ Î³).symm hg hg' (add_equiv_class.left_inv g)

/-- If `f : â„• â†’ Î±` has sum `a`, then the partial sums `âˆ‘_{i=0}^{n-1} f i` converge to `a`. -/
lemma has_sum.tendsto_sum_nat {f : â„• â†’ Î±} (h : has_sum f a) :
  tendsto (Î»n:â„•, âˆ‘ i in range n, f i) at_top (ğ“ a) :=
h.comp tendsto_finset_range

lemma has_sum.unique {aâ‚ aâ‚‚ : Î±} [t2_space Î±] : has_sum f aâ‚ â†’ has_sum f aâ‚‚ â†’ aâ‚ = aâ‚‚ :=
tendsto_nhds_unique

lemma summable.has_sum_iff_tendsto_nat [t2_space Î±] {f : â„• â†’ Î±} {a : Î±} (hf : summable f) :
  has_sum f a â†” tendsto (Î»n:â„•, âˆ‘ i in range n, f i) at_top (ğ“ a) :=
begin
  refine âŸ¨Î» h, h.tendsto_sum_nat, Î» h, _âŸ©,
  rw tendsto_nhds_unique h hf.has_sum.tendsto_sum_nat,
  exact hf.has_sum
end

lemma function.surjective.summable_iff_of_has_sum_iff {Î±' : Type*} [add_comm_monoid Î±']
  [topological_space Î±'] {e : Î±' â†’ Î±} (hes : function.surjective e) {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
  (he : âˆ€ {a}, has_sum f (e a) â†” has_sum g a) :
  summable f â†” summable g :=
hes.exists.trans $ exists_congr $ @he

section mul_opposite
open mul_opposite

lemma has_sum.op (hf : has_sum f a) : has_sum (Î» a, op (f a)) (op a) :=
(hf.map (@op_add_equiv Î± _) continuous_op : _)

lemma summable.op (hf : summable f) : summable (op âˆ˜ f) := hf.has_sum.op.summable

lemma has_sum.unop {f : Î² â†’ Î±áµáµ’áµ–} {a : Î±áµáµ’áµ–} (hf : has_sum f a) :
  has_sum (Î» a, unop (f a)) (unop a) :=
(hf.map (@op_add_equiv Î± _).symm continuous_unop : _)

lemma summable.unop {f : Î² â†’ Î±áµáµ’áµ–} (hf : summable f) : summable (unop âˆ˜ f) :=
hf.has_sum.unop.summable

@[simp] lemma has_sum_op : has_sum (Î» a, op (f a)) (op a) â†” has_sum f a :=
âŸ¨has_sum.unop, has_sum.opâŸ©

@[simp] lemma has_sum_unop {f : Î² â†’ Î±áµáµ’áµ–} {a : Î±áµáµ’áµ–} :
  has_sum (Î» a, unop (f a)) (unop a) â†” has_sum f a :=
âŸ¨has_sum.op, has_sum.unopâŸ©

@[simp] lemma summable_op : summable (Î» a, op (f a)) â†” summable f :=
âŸ¨summable.unop, summable.opâŸ©

@[simp] lemma summable_unop {f : Î² â†’ Î±áµáµ’áµ–} : summable (Î» a, unop (f a)) â†” summable f :=
âŸ¨summable.op, summable.unopâŸ©

end mul_opposite

section has_continuous_star
variables [star_add_monoid Î±] [has_continuous_star Î±]

lemma has_sum.star (h : has_sum f a) : has_sum (Î» b, star (f b)) (star a) :=
by simpa only using h.map (star_add_equiv : Î± â‰ƒ+ Î±) continuous_star

lemma summable.star (hf : summable f) : summable (Î» b, star (f b)) :=
hf.has_sum.star.summable

lemma summable.of_star (hf : summable (Î» b, star (f b))) : summable f :=
by simpa only [star_star] using hf.star

@[simp] lemma summable_star_iff : summable (Î» b, star (f b)) â†” summable f :=
âŸ¨summable.of_star, summable.starâŸ©

@[simp] lemma summable_star_iff' : summable (star f) â†” summable f :=
summable_star_iff

end has_continuous_star

variable [has_continuous_add Î±]

lemma has_sum.add (hf : has_sum f a) (hg : has_sum g b) : has_sum (Î»b, f b + g b) (a + b) :=
by simp only [has_sum, sum_add_distrib]; exact hf.add hg

lemma summable.add (hf : summable f) (hg : summable g) : summable (Î»b, f b + g b) :=
(hf.has_sum.add hg.has_sum).summable

lemma has_sum_sum {f : Î³ â†’ Î² â†’ Î±} {a : Î³ â†’ Î±} {s : finset Î³} :
  (âˆ€iâˆˆs, has_sum (f i) (a i)) â†’ has_sum (Î»b, âˆ‘ i in s, f i b) (âˆ‘ i in s, a i) :=
finset.induction_on s (by simp only [has_sum_zero, sum_empty, forall_true_iff])
  (by simp only [has_sum.add, sum_insert, mem_insert, forall_eq_or_imp,
        forall_2_true_iff, not_false_iff, forall_true_iff] {contextual := tt})

lemma summable_sum {f : Î³ â†’ Î² â†’ Î±} {s : finset Î³} (hf : âˆ€iâˆˆs, summable (f i)) :
  summable (Î»b, âˆ‘ i in s, f i b) :=
(has_sum_sum $ assume i hi, (hf i hi).has_sum).summable

lemma has_sum.add_disjoint {s t : set Î²} (hs : disjoint s t)
  (ha : has_sum (f âˆ˜ coe : s â†’ Î±) a) (hb : has_sum (f âˆ˜ coe : t â†’ Î±) b) :
  has_sum (f âˆ˜ coe : s âˆª t â†’ Î±) (a + b) :=
begin
  rw has_sum_subtype_iff_indicator at *,
  rw set.indicator_union_of_disjoint hs,
  exact ha.add hb
end

lemma has_sum.add_is_compl {s t : set Î²} (hs : is_compl s t)
  (ha : has_sum (f âˆ˜ coe : s â†’ Î±) a) (hb : has_sum (f âˆ˜ coe : t â†’ Î±) b) :
  has_sum f (a + b) :=
by simpa [â† hs.compl_eq]
  using (has_sum_subtype_iff_indicator.1 ha).add (has_sum_subtype_iff_indicator.1 hb)

lemma has_sum.add_compl {s : set Î²} (ha : has_sum (f âˆ˜ coe : s â†’ Î±) a)
  (hb : has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) b) :
  has_sum f (a + b) :=
ha.add_is_compl is_compl_compl hb

lemma summable.add_compl {s : set Î²} (hs : summable (f âˆ˜ coe : s â†’ Î±))
  (hsc : summable (f âˆ˜ coe : sá¶œ â†’ Î±)) :
  summable f :=
(hs.has_sum.add_compl hsc.has_sum).summable

lemma has_sum.compl_add {s : set Î²} (ha : has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) a)
  (hb : has_sum (f âˆ˜ coe : s â†’ Î±) b) :
  has_sum f (a + b) :=
ha.add_is_compl is_compl_compl.symm hb

lemma has_sum.even_add_odd {f : â„• â†’ Î±} (he : has_sum (Î» k, f (2 * k)) a)
  (ho : has_sum (Î» k, f (2 * k + 1)) b) :
  has_sum f (a + b) :=
begin
  have := mul_right_injectiveâ‚€ (@two_ne_zero â„• _ _),
  replace he := this.has_sum_range_iff.2 he,
  replace ho := ((add_left_injective 1).comp this).has_sum_range_iff.2 ho,
  refine he.add_is_compl _ ho,
  simpa [(âˆ˜)] using nat.is_compl_even_odd
end

lemma summable.compl_add {s : set Î²} (hs : summable (f âˆ˜ coe : sá¶œ â†’ Î±))
  (hsc : summable (f âˆ˜ coe : s â†’ Î±)) :
  summable f :=
(hs.has_sum.compl_add hsc.has_sum).summable

lemma summable.even_add_odd {f : â„• â†’ Î±} (he : summable (Î» k, f (2 * k)))
  (ho : summable (Î» k, f (2 * k + 1))) :
  summable f :=
(he.has_sum.even_add_odd ho.has_sum).summable

lemma has_sum.sigma [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£ b:Î², Î³ b) â†’ Î±} {g : Î² â†’ Î±} {a : Î±}
  (ha : has_sum f a) (hf : âˆ€b, has_sum (Î»c, f âŸ¨b, câŸ©) (g b)) : has_sum g a :=
begin
  refine (at_top_basis.tendsto_iff (closed_nhds_basis a)).mpr _,
  rintros s âŸ¨hs, hscâŸ©,
  rcases mem_at_top_sets.mp (ha hs) with âŸ¨u, huâŸ©,
  use [u.image sigma.fst, trivial],
  intros bs hbs,
  simp only [set.mem_preimage, ge_iff_le, finset.le_iff_subset] at hu,
  have : tendsto (Î» t : finset (Î£ b, Î³ b), âˆ‘ p in t.filter (Î» p, p.1 âˆˆ bs), f p)
    at_top (ğ“ $ âˆ‘ b in bs, g b),
  { simp only [â† sigma_preimage_mk, sum_sigma],
    refine tendsto_finset_sum _ (Î» b hb, _),
    change tendsto (Î» t, (Î» t, âˆ‘ s in t, f âŸ¨b, sâŸ©) (preimage t (sigma.mk b) _)) at_top (ğ“ (g b)),
    exact tendsto.comp (hf b) (tendsto_finset_preimage_at_top_at_top _) },
  refine hsc.mem_of_tendsto this (eventually_at_top.2 âŸ¨u, Î» t ht, hu _ (Î» x hx, _)âŸ©),
  exact mem_filter.2 âŸ¨ht hx, hbs $ mem_image_of_mem _ hxâŸ©
end

/-- If a series `f` on `Î² Ã— Î³` has sum `a` and for each `b` the restriction of `f` to `{b} Ã— Î³`
has sum `g b`, then the series `g` has sum `a`. -/
lemma has_sum.prod_fiberwise [regular_space Î±] {f : Î² Ã— Î³ â†’ Î±} {g : Î² â†’ Î±} {a : Î±}
  (ha : has_sum f a) (hf : âˆ€b, has_sum (Î»c, f (b, c)) (g b)) :
  has_sum g a :=
has_sum.sigma ((equiv.sigma_equiv_prod Î² Î³).has_sum_iff.2 ha) hf

lemma summable.sigma' [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) (hf : âˆ€b, summable (Î»c, f âŸ¨b, câŸ©)) :
  summable (Î»b, âˆ‘'c, f âŸ¨b, câŸ©) :=
(ha.has_sum.sigma (assume b, (hf b).has_sum)).summable

lemma has_sum.sigma_of_has_sum [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£ b:Î², Î³ b) â†’ Î±} {g : Î² â†’ Î±}
  {a : Î±} (ha : has_sum g a) (hf : âˆ€b, has_sum (Î»c, f âŸ¨b, câŸ©) (g b)) (hf' : summable f) :
  has_sum f a :=
by simpa [(hf'.has_sum.sigma hf).unique ha] using hf'.has_sum

end has_sum

section tsum
variables [add_comm_monoid Î±] [topological_space Î±]

lemma tsum_congr_subtype (f : Î² â†’ Î±) {s t : set Î²} (h : s = t) :
  âˆ‘' (x : s), f x = âˆ‘' (x : t), f x :=
by rw h

variables [t2_space Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma has_sum.tsum_eq (ha : has_sum f a) : âˆ‘'b, f b = a :=
(summable.has_sum âŸ¨a, haâŸ©).unique ha

lemma summable.has_sum_iff (h : summable f) : has_sum f a â†” âˆ‘'b, f b = a :=
iff.intro has_sum.tsum_eq (assume eq, eq â–¸ h.has_sum)

@[simp] lemma tsum_zero : âˆ‘'b:Î², (0:Î±) = 0 := has_sum_zero.tsum_eq

@[simp] lemma tsum_empty [is_empty Î²] : âˆ‘'b, f b = 0 := has_sum_empty.tsum_eq

lemma tsum_eq_sum {f : Î² â†’ Î±} {s : finset Î²} (hf : âˆ€bâˆ‰s, f b = 0)  :
  âˆ‘' b, f b = âˆ‘ b in s, f b :=
(has_sum_sum_of_ne_finset_zero hf).tsum_eq

lemma tsum_congr {Î± Î² : Type*} [add_comm_monoid Î±] [topological_space Î±]
  {f g : Î² â†’ Î±} (hfg : âˆ€ b, f b = g b) : âˆ‘' b, f b = âˆ‘' b, g b :=
congr_arg tsum (funext hfg)

lemma tsum_fintype [fintype Î²] (f : Î² â†’ Î±) : âˆ‘'b, f b = âˆ‘ b, f b :=
(has_sum_fintype f).tsum_eq

lemma tsum_bool (f : bool â†’ Î±) : âˆ‘' i : bool, f i = f false + f true :=
by { rw [tsum_fintype, finset.sum_eq_add]; simp }

@[simp] lemma finset.tsum_subtype (s : finset Î²) (f : Î² â†’ Î±) :
  âˆ‘' x : {x // x âˆˆ s}, f x = âˆ‘ x in s, f x :=
(s.has_sum f).tsum_eq

@[simp] lemma finset.tsum_subtype' (s : finset Î²) (f : Î² â†’ Î±) :
  âˆ‘' x : (s : set Î²), f x = âˆ‘ x in s, f x :=
s.tsum_subtype f

lemma tsum_eq_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€b' â‰  b, f b' = 0)  :
  âˆ‘'b, f b = f b :=
(has_sum_single b hf).tsum_eq

@[simp] lemma tsum_ite_eq (b : Î²) [decidable_pred (= b)] (a : Î±) :
  âˆ‘' b', (if b' = b then a else 0) = a :=
(has_sum_ite_eq b a).tsum_eq

lemma tsum_dite_right (P : Prop) [decidable P] (x : Î² â†’ Â¬ P â†’ Î±) :
  âˆ‘' (b : Î²), (if h : P then (0 : Î±) else x b h) = if h : P then (0 : Î±) else âˆ‘' (b : Î²), x b h :=
by by_cases hP : P; simp [hP]

lemma tsum_dite_left (P : Prop) [decidable P] (x : Î² â†’ P â†’ Î±) :
  âˆ‘' (b : Î²), (if h : P then x b h else 0) = if h : P then (âˆ‘' (b : Î²), x b h) else 0 :=
by by_cases hP : P; simp [hP]

lemma function.surjective.tsum_eq_tsum_of_has_sum_iff_has_sum {Î±' : Type*} [add_comm_monoid Î±']
  [topological_space Î±'] {e : Î±' â†’ Î±} (hes : function.surjective e) (h0 : e 0 = 0)
  {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
  (h : âˆ€ {a}, has_sum f (e a) â†” has_sum g a) :
  âˆ‘' b, f b = e (âˆ‘' c, g c) :=
by_cases
  (assume : summable g, (h.mpr this.has_sum).tsum_eq)
  (assume hg : Â¬ summable g,
    have hf : Â¬ summable f, from mt (hes.summable_iff_of_has_sum_iff @h).1 hg,
    by simp [tsum, hf, hg, h0])

lemma tsum_eq_tsum_of_has_sum_iff_has_sum {f : Î² â†’ Î±} {g : Î³ â†’ Î±}
  (h : âˆ€{a}, has_sum f a â†” has_sum g a) :
  âˆ‘'b, f b = âˆ‘'c, g c :=
surjective_id.tsum_eq_tsum_of_has_sum_iff_has_sum rfl @h

lemma equiv.tsum_eq (j : Î³ â‰ƒ Î²) (f : Î² â†’ Î±) : âˆ‘'c, f (j c) = âˆ‘'b, f b :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» a, j.has_sum_iff

lemma equiv.tsum_eq_tsum_of_support {f : Î² â†’ Î±} {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x, g (e x) = f x) :
  (âˆ‘' x, f x) = âˆ‘' y, g y :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, e.has_sum_iff_of_support he

lemma tsum_eq_tsum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
  (hi : âˆ€ â¦ƒx yâ¦„, i x = i y â†’ (x : Î³) = y)
  (hf : support f âŠ† set.range i) (hfg : âˆ€ x, f (i x) = g x) :
  âˆ‘' x, f x  = âˆ‘' y, g y :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, has_sum_iff_has_sum_of_ne_zero_bij i hi hf hfg

lemma tsum_subtype (s : set Î²) (f : Î² â†’ Î±) :
  âˆ‘' x:s, f x = âˆ‘' x, s.indicator f x :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, has_sum_subtype_iff_indicator

lemma tsum_op : âˆ‘' x, mul_opposite.op (f x) = mul_opposite.op (âˆ‘' x, f x) :=
begin
  by_cases h : summable f,
  { exact h.has_sum.op.tsum_eq, },
  { have ho := summable_op.not.mpr h,
    rw [tsum_eq_zero_of_not_summable h, tsum_eq_zero_of_not_summable ho, mul_opposite.op_zero] },
end

lemma tsum_unop {f : Î² â†’ Î±áµáµ’áµ–} : âˆ‘' x, mul_opposite.unop (f x) = mul_opposite.unop (âˆ‘' x, f x) :=
mul_opposite.op_injective tsum_op.symm

section has_continuous_add
variable [has_continuous_add Î±]

lemma tsum_add (hf : summable f) (hg : summable g) : âˆ‘'b, (f b + g b) = (âˆ‘'b, f b) + (âˆ‘'b, g b) :=
(hf.has_sum.add hg.has_sum).tsum_eq

lemma tsum_sum {f : Î³ â†’ Î² â†’ Î±} {s : finset Î³} (hf : âˆ€iâˆˆs, summable (f i)) :
  âˆ‘'b, âˆ‘ i in s, f i b = âˆ‘ i in s, âˆ‘'b, f i b :=
(has_sum_sum $ assume i hi, (hf i hi).has_sum).tsum_eq

lemma tsum_sigma' [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (hâ‚ : âˆ€b, summable (Î»c, f âŸ¨b, câŸ©)) (hâ‚‚ : summable f) : âˆ‘'p, f p = âˆ‘'b c, f âŸ¨b, câŸ© :=
(hâ‚‚.has_sum.sigma (assume b, (hâ‚ b).has_sum)).tsum_eq.symm

lemma tsum_prod' [regular_space Î±] {f : Î² Ã— Î³ â†’ Î±} (h : summable f)
  (hâ‚ : âˆ€b, summable (Î»c, f (b, c))) :
  âˆ‘'p, f p = âˆ‘'b c, f (b, c) :=
(h.has_sum.prod_fiberwise (assume b, (hâ‚ b).has_sum)).tsum_eq.symm

lemma tsum_comm' [regular_space Î±] {f : Î² â†’ Î³ â†’ Î±} (h : summable (function.uncurry f))
  (hâ‚ : âˆ€b, summable (f b)) (hâ‚‚ : âˆ€ c, summable (Î» b, f b c)) :
  âˆ‘' c b, f b c = âˆ‘' b c, f b c :=
begin
  erw [â† tsum_prod' h hâ‚, â† tsum_prod' h.prod_symm hâ‚‚, â† (equiv.prod_comm Î² Î³).tsum_eq],
  refl,
  assumption
end

end has_continuous_add

section has_continuous_star
variables [star_add_monoid Î±] [has_continuous_star Î±]

lemma tsum_star : star (âˆ‘' b, f b) = âˆ‘' b, star (f b) :=
begin
  by_cases hf : summable f,
  { exact hf.has_sum.star.tsum_eq.symm, },
  { rw [tsum_eq_zero_of_not_summable hf, tsum_eq_zero_of_not_summable (mt summable.of_star hf),
        star_zero] },
end

end has_continuous_star

section encodable
open encodable
variable [encodable Î³]

/-- You can compute a sum over an encodably type by summing over the natural numbers and
  taking a supremum. This is useful for outer measures. -/
theorem tsum_supr_decodeâ‚‚ [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (s : Î³ â†’ Î²) : âˆ‘' i : â„•, m (â¨† b âˆˆ decodeâ‚‚ Î³ i, s b) = âˆ‘' b : Î³, m (s b) :=
begin
  have H : âˆ€ n, m (â¨† b âˆˆ decodeâ‚‚ Î³ n, s b) â‰  0 â†’ (decodeâ‚‚ Î³ n).is_some,
  { intros n h,
    cases decodeâ‚‚ Î³ n with b,
    { refine (h $ by simp [m0]).elim },
    { exact rfl } },
  symmetry, refine tsum_eq_tsum_of_ne_zero_bij (Î» a, option.get (H a.1 a.2)) _ _ _,
  { rintros âŸ¨m, hmâŸ© âŸ¨n, hnâŸ© e,
    have := mem_decodeâ‚‚.1 (option.get_mem (H n hn)),
    rwa [â† e, mem_decodeâ‚‚.1 (option.get_mem (H m hm))] at this },
  { intros b h,
    refine âŸ¨âŸ¨encode b, _âŸ©, _âŸ©,
    { simp only [mem_support, encodekâ‚‚] at h âŠ¢, convert h, simp [set.ext_iff, encodekâ‚‚] },
    { exact option.get_of_mem _ (encodekâ‚‚ _) } },
  { rintros âŸ¨n, hâŸ©, dsimp only [subtype.coe_mk],
    transitivity, swap,
    rw [show decodeâ‚‚ Î³ n = _, from option.get_mem (H n h)],
    congr, simp [ext_iff, -option.some_get] }
end

/-- `tsum_supr_decodeâ‚‚` specialized to the complete lattice of sets. -/
theorem tsum_Union_decodeâ‚‚ (m : set Î² â†’ Î±) (m0 : m âˆ… = 0)
  (s : Î³ â†’ set Î²) : âˆ‘' i, m (â‹ƒ b âˆˆ decodeâ‚‚ Î³ i, s b) = âˆ‘' b, m (s b) :=
tsum_supr_decodeâ‚‚ m m0 s

/-! Some properties about measure-like functions.
  These could also be functions defined on complete sublattices of sets, with the property
  that they are countably sub-additive.
  `R` will probably be instantiated with `(â‰¤)` in all applications.
-/

/-- If a function is countably sub-additive then it is sub-additive on encodable types -/
theorem rel_supr_tsum [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) âˆ‘' i, m (s i))
  (s : Î³ â†’ Î²) : R (m (â¨† b : Î³, s b)) âˆ‘' b : Î³, m (s b) :=
by { rw [â† supr_decodeâ‚‚, â† tsum_supr_decodeâ‚‚ _ m0 s], exact m_supr _ }

/-- If a function is countably sub-additive then it is sub-additive on finite sets -/
theorem rel_supr_sum [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) (âˆ‘' i, m (s i)))
  (s : Î´ â†’ Î²) (t : finset Î´) :
  R (m (â¨† d âˆˆ t, s d)) (âˆ‘ d in t, m (s d)) :=
by { cases t.nonempty_encodable, rw [supr_subtype'], convert rel_supr_tsum m m0 R m_supr _,
     rw [â† finset.tsum_subtype], assumption }

/-- If a function is countably sub-additive then it is binary sub-additive -/
theorem rel_sup_add [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) (âˆ‘' i, m (s i)))
  (sâ‚ sâ‚‚ : Î²) : R (m (sâ‚ âŠ” sâ‚‚)) (m sâ‚ + m sâ‚‚) :=
begin
  convert rel_supr_tsum m m0 R m_supr (Î» b, cond b sâ‚ sâ‚‚),
  { simp only [supr_bool_eq, cond] },
  { rw [tsum_fintype, fintype.sum_bool, cond, cond] }
end

end encodable

variables [has_continuous_add Î±]

lemma tsum_add_tsum_compl {s : set Î²} (hs : summable (f âˆ˜ coe : s â†’ Î±))
  (hsc : summable (f âˆ˜ coe : sá¶œ â†’ Î±)) :
  (âˆ‘' x : s, f x) + (âˆ‘' x : sá¶œ, f x) = âˆ‘' x, f x :=
(hs.has_sum.add_compl hsc.has_sum).tsum_eq.symm

lemma tsum_union_disjoint {s t : set Î²} (hd : disjoint s t)
  (hs : summable (f âˆ˜ coe : s â†’ Î±)) (ht : summable (f âˆ˜ coe : t â†’ Î±)) :
  (âˆ‘' x : s âˆª t, f x) = (âˆ‘' x : s, f x) + (âˆ‘' x : t, f x) :=
(hs.has_sum.add_disjoint hd ht.has_sum).tsum_eq

lemma tsum_even_add_odd {f : â„• â†’ Î±} (he : summable (Î» k, f (2 * k)))
  (ho : summable (Î» k, f (2 * k + 1))) :
  (âˆ‘' k, f (2 * k)) + (âˆ‘' k, f (2 * k + 1)) = âˆ‘' k, f k :=
(he.has_sum.even_add_odd ho.has_sum).tsum_eq.symm

end tsum

section prod

variables [add_comm_monoid Î±] [topological_space Î±] [add_comm_monoid Î³] [topological_space Î³]

lemma has_sum.prod_mk {f : Î² â†’ Î±} {g : Î² â†’ Î³} {a : Î±} {b : Î³}
  (hf : has_sum f a) (hg : has_sum g b) :
  has_sum (Î» x, (âŸ¨f x, g xâŸ© : Î± Ã— Î³)) âŸ¨a, bâŸ© :=
by simp [has_sum, â† prod_mk_sum, filter.tendsto.prod_mk_nhds hf hg]

end prod

section pi
variables {Î¹ : Type*} {Ï€ : Î± â†’ Type*} [âˆ€ x, add_comm_monoid (Ï€ x)] [âˆ€ x, topological_space (Ï€ x)]

lemma pi.has_sum {f : Î¹ â†’ âˆ€ x, Ï€ x} {g : âˆ€ x, Ï€ x} :
  has_sum f g â†” âˆ€ x, has_sum (Î» i, f i x) (g x) :=
by simp only [has_sum, tendsto_pi_nhds, sum_apply]

lemma pi.summable {f : Î¹ â†’ âˆ€ x, Ï€ x} : summable f â†” âˆ€ x, summable (Î» i, f i x) :=
by simp only [summable, pi.has_sum, skolem]

lemma tsum_apply [âˆ€ x, t2_space (Ï€ x)] {f : Î¹ â†’ âˆ€ x, Ï€ x}{x : Î±} (hf : summable f) :
  (âˆ‘' i, f i) x = âˆ‘' i, f i x :=
(pi.has_sum.mp hf.has_sum x).tsum_eq.symm

end pi

section topological_group
variables [add_comm_group Î±] [topological_space Î±] [topological_add_group Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

-- `by simpa using` speeds up elaboration. Why?
lemma has_sum.neg (h : has_sum f a) : has_sum (Î»b, - f b) (- a) :=
by simpa only using h.map (-add_monoid_hom.id Î±) continuous_neg

lemma summable.neg (hf : summable f) : summable (Î»b, - f b) :=
hf.has_sum.neg.summable

lemma summable.of_neg (hf : summable (Î»b, - f b)) : summable f :=
by simpa only [neg_neg] using hf.neg

lemma summable_neg_iff : summable (Î» b, - f b) â†” summable f :=
âŸ¨summable.of_neg, summable.negâŸ©

lemma has_sum.sub (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) : has_sum (Î»b, f b - g b) (aâ‚ - aâ‚‚) :=
by { simp only [sub_eq_add_neg], exact hf.add hg.neg }

lemma summable.sub (hf : summable f) (hg : summable g) : summable (Î»b, f b - g b) :=
(hf.has_sum.sub hg.has_sum).summable

lemma summable.trans_sub (hg : summable g) (hfg : summable (Î»b, f b - g b)) :
  summable f :=
by simpa only [sub_add_cancel] using hfg.add hg

lemma summable_iff_of_summable_sub (hfg : summable (Î»b, f b - g b)) :
  summable f â†” summable g :=
âŸ¨Î» hf, hf.trans_sub $ by simpa only [neg_sub] using hfg.neg, Î» hg, hg.trans_sub hfgâŸ©

lemma has_sum.update (hf : has_sum f aâ‚) (b : Î²) [decidable_eq Î²] (a : Î±) :
  has_sum (update f b a) (a - f b + aâ‚) :=
begin
  convert ((has_sum_ite_eq b _).add hf),
  ext b',
  by_cases h : b' = b,
  { rw [h, update_same],
    simp only [eq_self_iff_true, if_true, sub_add_cancel] },
  simp only [h, update_noteq, if_false, ne.def, zero_add, not_false_iff],
end

lemma summable.update (hf : summable f) (b : Î²) [decidable_eq Î²] (a : Î±) :
  summable (update f b a) :=
(hf.has_sum.update b a).summable

lemma has_sum.has_sum_compl_iff {s : set Î²} (hf : has_sum (f âˆ˜ coe : s â†’ Î±) aâ‚) :
  has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) aâ‚‚ â†” has_sum f (aâ‚ + aâ‚‚) :=
begin
  refine âŸ¨Î» h, hf.add_compl h, Î» h, _âŸ©,
  rw [has_sum_subtype_iff_indicator] at hf âŠ¢,
  rw [set.indicator_compl],
  simpa only [add_sub_cancel'] using h.sub hf
end

lemma has_sum.has_sum_iff_compl {s : set Î²} (hf : has_sum (f âˆ˜ coe : s â†’ Î±) aâ‚) :
  has_sum f aâ‚‚ â†” has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) (aâ‚‚ - aâ‚) :=
iff.symm $ hf.has_sum_compl_iff.trans $ by rw [add_sub_cancel'_right]

lemma summable.summable_compl_iff {s : set Î²} (hf : summable (f âˆ˜ coe : s â†’ Î±)) :
  summable (f âˆ˜ coe : sá¶œ â†’ Î±) â†” summable f :=
âŸ¨Î» âŸ¨a, haâŸ©, (hf.has_sum.has_sum_compl_iff.1 ha).summable,
  Î» âŸ¨a, haâŸ©, (hf.has_sum.has_sum_iff_compl.1 ha).summableâŸ©

protected lemma finset.has_sum_compl_iff (s : finset Î²) :
  has_sum (Î» x : {x // x âˆ‰ s}, f x) a â†” has_sum f (a + âˆ‘ i in s, f i) :=
(s.has_sum f).has_sum_compl_iff.trans $ by rw [add_comm]

protected lemma finset.has_sum_iff_compl (s : finset Î²) :
  has_sum f a â†” has_sum (Î» x : {x // x âˆ‰ s}, f x) (a - âˆ‘ i in s, f i) :=
(s.has_sum f).has_sum_iff_compl

protected lemma finset.summable_compl_iff (s : finset Î²) :
  summable (Î» x : {x // x âˆ‰ s}, f x) â†” summable f :=
(s.summable f).summable_compl_iff

lemma set.finite.summable_compl_iff {s : set Î²} (hs : s.finite) :
  summable (f âˆ˜ coe : sá¶œ â†’ Î±) â†” summable f :=
(hs.summable f).summable_compl_iff

lemma has_sum_ite_eq_extract [decidable_eq Î²] (hf : has_sum f a) (b : Î²) :
  has_sum (Î» n, ite (n = b) 0 (f n)) (a - f b) :=
begin
  convert hf.update b 0 using 1,
  { ext n, rw function.update_apply, },
  { rw [sub_add_eq_add_sub, zero_add], },
end

section tsum
variables [t2_space Î±]

lemma tsum_neg : âˆ‘'b, - f b = - âˆ‘'b, f b :=
begin
  by_cases hf : summable f,
  { exact hf.has_sum.neg.tsum_eq, },
  { simp [tsum_eq_zero_of_not_summable hf, tsum_eq_zero_of_not_summable (mt summable.of_neg hf)] },
end

lemma tsum_sub (hf : summable f) (hg : summable g) : âˆ‘'b, (f b - g b) = âˆ‘'b, f b - âˆ‘'b, g b :=
(hf.has_sum.sub hg.has_sum).tsum_eq

lemma sum_add_tsum_compl {s : finset Î²} (hf : summable f) :
  (âˆ‘ x in s, f x) + (âˆ‘' x : (â†‘s : set Î²)á¶œ, f x) = âˆ‘' x, f x :=
((s.has_sum f).add_compl (s.summable_compl_iff.2 hf).has_sum).tsum_eq.symm

/-- Let `f : Î² â†’ Î±` be a sequence with summable series and let `b âˆˆ Î²` be an index.
Lemma `tsum_ite_eq_extract` writes `Î£ f n` as the sum of `f b` plus the series of the
remaining terms. -/
lemma tsum_ite_eq_extract [decidable_eq Î²] (hf : summable f) (b : Î²) :
  âˆ‘' n, f n = f b + âˆ‘' n, ite (n = b) 0 (f n) :=
begin
  rw (has_sum_ite_eq_extract hf.has_sum b).tsum_eq,
  exact (add_sub_cancel'_right _ _).symm,
end

end tsum

/-!
### Sums on subtypes

If `s` is a finset of `Î±`, we show that the summability of `f` in the whole space and on the subtype
`univ - s` are equivalent, and relate their sums. For a function defined on `â„•`, we deduce the
formula `(âˆ‘ i in range k, f i) + (âˆ‘' i, f (i + k)) = (âˆ‘' i, f i)`, in `sum_add_tsum_nat_add`.
-/
section subtype

lemma has_sum_nat_add_iff {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
  has_sum (Î» n, f (n + k)) a â†” has_sum f (a + âˆ‘ i in range k, f i) :=
begin
  refine iff.trans _ ((range k).has_sum_compl_iff),
  rw [â† (not_mem_range_equiv k).symm.has_sum_iff],
  refl
end

lemma summable_nat_add_iff {f : â„• â†’ Î±} (k : â„•) : summable (Î» n, f (n + k)) â†” summable f :=
iff.symm $ (equiv.add_right (âˆ‘ i in range k, f i)).surjective.summable_iff_of_has_sum_iff $
  Î» a, (has_sum_nat_add_iff k).symm

lemma has_sum_nat_add_iff' {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
  has_sum (Î» n, f (n + k)) (a - âˆ‘ i in range k, f i) â†” has_sum f a :=
by simp [has_sum_nat_add_iff]

lemma sum_add_tsum_nat_add [t2_space Î±] {f : â„• â†’ Î±} (k : â„•) (h : summable f) :
  (âˆ‘ i in range k, f i) + (âˆ‘' i, f (i + k)) = âˆ‘' i, f i :=
by simpa only [add_comm] using
  ((has_sum_nat_add_iff k).1 ((summable_nat_add_iff k).2 h).has_sum).unique h.has_sum

lemma tsum_eq_zero_add [t2_space Î±] {f : â„• â†’ Î±} (hf : summable f) :
  âˆ‘'b, f b = f 0 + âˆ‘'b, f (b + 1) :=
by simpa only [sum_range_one] using (sum_add_tsum_nat_add 1 hf).symm

/-- For `f : â„• â†’ Î±`, then `âˆ‘' k, f (k + i)` tends to zero. This does not require a summability
assumption on `f`, as otherwise all sums are zero. -/
lemma tendsto_sum_nat_add [t2_space Î±] (f : â„• â†’ Î±) : tendsto (Î» i, âˆ‘' k, f (k + i)) at_top (ğ“ 0) :=
begin
  by_cases hf : summable f,
  { have hâ‚€ : (Î» i, (âˆ‘' i, f i) - âˆ‘ j in range i, f j) = Î» i, âˆ‘' (k : â„•), f (k + i),
    { ext1 i,
      rw [sub_eq_iff_eq_add, add_comm, sum_add_tsum_nat_add i hf] },
    have hâ‚ : tendsto (Î» i : â„•, âˆ‘' i, f i) at_top (ğ“ (âˆ‘' i, f i)) := tendsto_const_nhds,
    simpa only [hâ‚€, sub_self] using tendsto.sub hâ‚ hf.has_sum.tendsto_sum_nat },
  { convert tendsto_const_nhds,
    ext1 i,
    rw â† summable_nat_add_iff i at hf,
    { exact tsum_eq_zero_of_not_summable hf },
    { apply_instance } }
end

end subtype

end topological_group

section topological_semiring
variables [non_unital_non_assoc_semiring Î±] [topological_space Î±] [topological_semiring Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}
lemma has_sum.mul_left (aâ‚‚) (h : has_sum f aâ‚) : has_sum (Î»b, aâ‚‚ * f b) (aâ‚‚ * aâ‚) :=
by simpa only using h.map (add_monoid_hom.mul_left aâ‚‚) (continuous_const.mul continuous_id)

lemma has_sum.mul_right (aâ‚‚) (hf : has_sum f aâ‚) : has_sum (Î»b, f b * aâ‚‚) (aâ‚ * aâ‚‚) :=
by simpa only using hf.map (add_monoid_hom.mul_right aâ‚‚) (continuous_id.mul continuous_const)

lemma summable.mul_left (a) (hf : summable f) : summable (Î»b, a * f b) :=
(hf.has_sum.mul_left _).summable

lemma summable.mul_right (a) (hf : summable f) : summable (Î»b, f b * a) :=
(hf.has_sum.mul_right _).summable

section tsum
variables [t2_space Î±]

lemma summable.tsum_mul_left (a) (hf : summable f) : âˆ‘'b, a * f b = a * âˆ‘'b, f b :=
(hf.has_sum.mul_left _).tsum_eq

lemma summable.tsum_mul_right (a) (hf : summable f) : (âˆ‘'b, f b * a) = (âˆ‘'b, f b) * a :=
(hf.has_sum.mul_right _).tsum_eq

lemma commute.tsum_right (a) (h : âˆ€ b, commute a (f b)) : commute a (âˆ‘' b, f b) :=
if hf : summable f then
  (hf.tsum_mul_left a).symm.trans ((congr_arg _ $ funext h).trans (hf.tsum_mul_right a))
else
  (tsum_eq_zero_of_not_summable hf).symm â–¸ commute.zero_right _

lemma commute.tsum_left (a) (h : âˆ€ b, commute (f b) a) : commute (âˆ‘' b, f b) a :=
(commute.tsum_right _ $ Î» b, (h b).symm).symm

end tsum

end topological_semiring

section const_smul
variables {R : Type*}
[monoid R]
[topological_space Î±] [add_comm_monoid Î±]
[distrib_mul_action R Î±] [has_continuous_const_smul R Î±]
{f : Î² â†’ Î±}

lemma has_sum.const_smul {a : Î±} {r : R} (hf : has_sum f a) : has_sum (Î» z, r â€¢ f z) (r â€¢ a) :=
hf.map (distrib_mul_action.to_add_monoid_hom Î± r) (continuous_const_smul r)

lemma summable.const_smul {r : R} (hf : summable f) : summable (Î» z, r â€¢ f z) :=
hf.has_sum.const_smul.summable

lemma tsum_const_smul [t2_space Î±] {r : R} (hf : summable f) : âˆ‘' z, r â€¢ f z = r â€¢ âˆ‘' z, f z :=
hf.has_sum.const_smul.tsum_eq

end const_smul

section smul_const
variables {R : Type*}
[semiring R] [topological_space R]
[topological_space Î±] [add_comm_monoid Î±]
[module R Î±] [has_continuous_smul R Î±]
{f : Î² â†’ R}

lemma has_sum.smul_const {a : Î±} {r : R} (hf : has_sum f r) : has_sum (Î» z, f z â€¢ a) (r â€¢ a) :=
hf.map ((smul_add_hom R Î±).flip a) (continuous_id.smul continuous_const)

lemma summable.smul_const {a : Î±} (hf : summable f) : summable (Î» z, f z â€¢ a) :=
hf.has_sum.smul_const.summable

lemma tsum_smul_const [t2_space Î±] {a : Î±} (hf : summable f) : âˆ‘' z, f z â€¢ a = (âˆ‘' z, f z) â€¢ a :=
hf.has_sum.smul_const.tsum_eq

end smul_const

section division_ring

variables [division_ring Î±] [topological_space Î±] [topological_ring Î±]
{f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma has_sum.div_const (h : has_sum f a) (b : Î±) : has_sum (Î» x, f x / b) (a / b) :=
by simp only [div_eq_mul_inv, h.mul_right bâ»Â¹]

lemma summable.div_const (h : summable f) (b : Î±) : summable (Î» x, f x / b) :=
(h.has_sum.div_const b).summable

lemma has_sum_mul_left_iff (h : aâ‚‚ â‰  0) : has_sum f aâ‚ â†” has_sum (Î»b, aâ‚‚ * f b) (aâ‚‚ * aâ‚) :=
âŸ¨has_sum.mul_left _, Î» H, by simpa only [inv_mul_cancel_leftâ‚€ h] using H.mul_left aâ‚‚â»Â¹âŸ©

lemma has_sum_mul_right_iff (h : aâ‚‚ â‰  0) : has_sum f aâ‚ â†” has_sum (Î»b, f b * aâ‚‚) (aâ‚ * aâ‚‚) :=
âŸ¨has_sum.mul_right _, Î» H, by simpa only [mul_inv_cancel_rightâ‚€ h] using H.mul_right aâ‚‚â»Â¹âŸ©

lemma summable_mul_left_iff (h : a â‰  0) : summable f â†” summable (Î»b, a * f b) :=
âŸ¨Î» H, H.mul_left _, Î» H, by simpa only [inv_mul_cancel_leftâ‚€ h] using H.mul_left aâ»Â¹âŸ©

lemma summable_mul_right_iff (h : a â‰  0) : summable f â†” summable (Î»b, f b * a) :=
âŸ¨Î» H, H.mul_right _, Î» H, by simpa only [mul_inv_cancel_rightâ‚€ h] using H.mul_right aâ»Â¹âŸ©

lemma tsum_mul_left [t2_space Î±] : (âˆ‘' x, a * f x) = a * âˆ‘' x, f x :=
if hf : summable f then hf.tsum_mul_left a
else if ha : a = 0 then by simp [ha]
else by rw [tsum_eq_zero_of_not_summable hf,
  tsum_eq_zero_of_not_summable (mt (summable_mul_left_iff ha).2 hf), mul_zero]

lemma tsum_mul_right [t2_space Î±] : (âˆ‘' x, f x * a) = (âˆ‘' x, f x) * a :=
if hf : summable f then hf.tsum_mul_right a
else if ha : a = 0 then by simp [ha]
else by rw [tsum_eq_zero_of_not_summable hf,
  tsum_eq_zero_of_not_summable (mt (summable_mul_right_iff ha).2 hf), zero_mul]

end division_ring

section order_topology
variables [ordered_add_comm_monoid Î±] [topological_space Î±] [order_closed_topology Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma has_sum_le (h : âˆ€b, f b â‰¤ g b) (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) : aâ‚ â‰¤ aâ‚‚ :=
le_of_tendsto_of_tendsto' hf hg $ assume s, sum_le_sum $ assume b _, h b

@[mono] lemma has_sum_mono (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) (h : f â‰¤ g) : aâ‚ â‰¤ aâ‚‚ :=
has_sum_le h hf hg

lemma has_sum_le_of_sum_le (hf : has_sum f a) (h : âˆ€ s : finset Î², âˆ‘ b in s, f b â‰¤ aâ‚‚) :
  a â‰¤ aâ‚‚ :=
le_of_tendsto' hf h

lemma le_has_sum_of_le_sum (hf : has_sum f a) (h : âˆ€ s : finset Î², aâ‚‚ â‰¤ âˆ‘ b in s, f b) :
  aâ‚‚ â‰¤ a :=
ge_of_tendsto' hf h

lemma has_sum_le_inj {g : Î³ â†’ Î±} (i : Î² â†’ Î³) (hi : injective i) (hs : âˆ€câˆ‰set.range i, 0 â‰¤ g c)
  (h : âˆ€b, f b â‰¤ g (i b)) (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) : aâ‚ â‰¤ aâ‚‚ :=
have has_sum (Î»c, (partial_inv i c).cases_on' 0 f) aâ‚,
begin
  refine (has_sum_iff_has_sum_of_ne_zero_bij (i âˆ˜ coe) _ _ _).2 hf,
  { exact assume câ‚ câ‚‚ eq, hi eq },
  { intros c hc,
    rw [mem_support] at hc,
    cases eq : partial_inv i c with b; rw eq at hc,
    { contradiction },
    { rw [partial_inv_of_injective hi] at eq,
      exact âŸ¨âŸ¨b, hcâŸ©, eqâŸ© } },
  { assume c, simp [partial_inv_left hi, option.cases_on'] }
end,
begin
  refine has_sum_le (assume c, _) this hg,
  by_cases c âˆˆ set.range i,
  { rcases h with âŸ¨b, rflâŸ©,
    rw [partial_inv_left hi, option.cases_on'],
    exact h _ },
  { have : partial_inv i c = none := dif_neg h,
    rw [this, option.cases_on'],
    exact hs _ h }
end

lemma tsum_le_tsum_of_inj {g : Î³ â†’ Î±} (i : Î² â†’ Î³) (hi : injective i) (hs : âˆ€câˆ‰set.range i, 0 â‰¤ g c)
  (h : âˆ€b, f b â‰¤ g (i b)) (hf : summable f) (hg : summable g) : tsum f â‰¤ tsum g :=
has_sum_le_inj i hi hs h hf.has_sum hg.has_sum

lemma sum_le_has_sum (s : finset Î²) (hs : âˆ€ bâˆ‰s, 0 â‰¤ f b) (hf : has_sum f a) :
  âˆ‘ b in s, f b â‰¤ a :=
ge_of_tendsto hf (eventually_at_top.2 âŸ¨s, Î» t hst,
  sum_le_sum_of_subset_of_nonneg hst $ Î» b hbt hbs, hs b hbsâŸ©)

lemma is_lub_has_sum (h : âˆ€ b, 0 â‰¤ f b) (hf : has_sum f a) :
  is_lub (set.range (Î» s : finset Î², âˆ‘ b in s, f b)) a :=
is_lub_of_tendsto_at_top (finset.sum_mono_set_of_nonneg h) hf

lemma le_has_sum (hf : has_sum f a) (b : Î²) (hb : âˆ€ b' â‰  b, 0 â‰¤ f b') : f b â‰¤ a :=
calc f b = âˆ‘ b in {b}, f b : finset.sum_singleton.symm
... â‰¤ a : sum_le_has_sum _ (by { convert hb, simp }) hf

lemma sum_le_tsum {f : Î² â†’ Î±} (s : finset Î²) (hs : âˆ€ bâˆ‰s, 0 â‰¤ f b) (hf : summable f) :
  âˆ‘ b in s, f b â‰¤ âˆ‘' b, f b :=
sum_le_has_sum s hs hf.has_sum

lemma le_tsum (hf : summable f) (b : Î²) (hb : âˆ€ b' â‰  b, 0 â‰¤ f b') : f b â‰¤ âˆ‘' b, f b :=
le_has_sum (summable.has_sum hf) b hb

lemma tsum_le_tsum (h : âˆ€b, f b â‰¤ g b) (hf : summable f) (hg : summable g) : âˆ‘'b, f b â‰¤ âˆ‘'b, g b :=
has_sum_le h hf.has_sum hg.has_sum

@[mono] lemma tsum_mono (hf : summable f) (hg : summable g) (h : f â‰¤ g) :
  âˆ‘' n, f n â‰¤ âˆ‘' n, g n :=
tsum_le_tsum h hf hg

lemma tsum_le_of_sum_le (hf : summable f) (h : âˆ€ s : finset Î², âˆ‘ b in s, f b â‰¤ aâ‚‚) :
  âˆ‘' b, f b â‰¤ aâ‚‚ :=
has_sum_le_of_sum_le hf.has_sum h

lemma tsum_le_of_sum_le' (haâ‚‚ : 0 â‰¤ aâ‚‚) (h : âˆ€ s : finset Î², âˆ‘ b in s, f b â‰¤ aâ‚‚) :
  âˆ‘' b, f b â‰¤ aâ‚‚ :=
begin
  by_cases hf : summable f,
  { exact tsum_le_of_sum_le hf h },
  { rw tsum_eq_zero_of_not_summable hf,
    exact haâ‚‚ }
end

lemma has_sum.nonneg (h : âˆ€ b, 0 â‰¤ g b) (ha : has_sum g a) : 0 â‰¤ a :=
has_sum_le h has_sum_zero ha

lemma has_sum.nonpos (h : âˆ€ b, g b â‰¤ 0) (ha : has_sum g a) : a â‰¤ 0 :=
has_sum_le h ha has_sum_zero

lemma tsum_nonneg (h : âˆ€ b, 0 â‰¤ g b) : 0 â‰¤ âˆ‘'b, g b :=
begin
  by_cases hg : summable g,
  { exact hg.has_sum.nonneg h },
  { simp [tsum_eq_zero_of_not_summable hg] }
end

lemma tsum_nonpos (h : âˆ€ b, f b â‰¤ 0) : âˆ‘'b, f b â‰¤ 0 :=
begin
  by_cases hf : summable f,
  { exact hf.has_sum.nonpos h },
  { simp [tsum_eq_zero_of_not_summable hf] }
end

end order_topology

section ordered_topological_group

variables [ordered_add_comm_group Î±] [topological_space Î±] [topological_add_group Î±]
  [order_closed_topology Î±] {f g : Î² â†’ Î±} {aâ‚ aâ‚‚ : Î±}

lemma has_sum_lt {i : Î²} (h : âˆ€ (b : Î²), f b â‰¤ g b) (hi : f i < g i)
  (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) :
  aâ‚ < aâ‚‚ :=
have update f i 0 â‰¤ update g i 0 := update_le_update_iff.mpr âŸ¨rfl.le, Î» i _, h iâŸ©,
have 0 - f i + aâ‚ â‰¤ 0 - g i + aâ‚‚ := has_sum_le this (hf.update i 0) (hg.update i 0),
by simpa only [zero_sub, add_neg_cancel_left] using add_lt_add_of_lt_of_le hi this

@[mono] lemma has_sum_strict_mono (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) (h : f < g) : aâ‚ < aâ‚‚ :=
let âŸ¨hle, i, hiâŸ© := pi.lt_def.mp h in has_sum_lt hle hi hf hg

lemma tsum_lt_tsum {i : Î²} (h : âˆ€ (b : Î²), f b â‰¤ g b) (hi : f i < g i)
  (hf : summable f) (hg : summable g) :
  âˆ‘' n, f n < âˆ‘' n, g n :=
has_sum_lt h hi hf.has_sum hg.has_sum

@[mono] lemma tsum_strict_mono (hf : summable f) (hg : summable g) (h : f < g) :
  âˆ‘' n, f n < âˆ‘' n, g n :=
let âŸ¨hle, i, hiâŸ© := pi.lt_def.mp h in tsum_lt_tsum hle hi hf hg

lemma tsum_pos (hsum : summable g) (hg : âˆ€ b, 0 â‰¤ g b) (i : Î²) (hi : 0 < g i) :
  0 < âˆ‘' b, g b :=
by { rw â† tsum_zero, exact tsum_lt_tsum hg hi summable_zero hsum }

lemma has_sum_zero_iff_of_nonneg (hf : âˆ€ i, 0 â‰¤ f i) : has_sum f 0 â†” f = 0 :=
begin
  split,
  { intros hf',
    ext i,
    by_contra hi',
    have hi : 0 < f i := lt_of_le_of_ne (hf i) (ne.symm hi'),
    simpa using has_sum_lt hf hi has_sum_zero hf' },
  { rintros rfl,
    exact has_sum_zero },
end

end ordered_topological_group

section canonically_ordered
variables [canonically_ordered_add_monoid Î±] [topological_space Î±] [order_closed_topology Î±]
variables {f : Î² â†’ Î±} {a : Î±}

lemma le_has_sum' (hf : has_sum f a) (b : Î²) : f b â‰¤ a :=
le_has_sum hf b $ Î» _ _, zero_le _

lemma le_tsum' (hf : summable f) (b : Î²) : f b â‰¤ âˆ‘' b, f b :=
le_tsum hf b $ Î» _ _, zero_le _

lemma has_sum_zero_iff : has_sum f 0 â†” âˆ€ x, f x = 0 :=
begin
  refine âŸ¨_, Î» h, _âŸ©,
  { contrapose!,
    exact Î» âŸ¨x, hxâŸ© h, irrefl _ (lt_of_lt_of_le (pos_iff_ne_zero.2 hx) (le_has_sum' h x)) },
  { convert has_sum_zero,
    exact funext h }
end

lemma tsum_eq_zero_iff (hf : summable f) : âˆ‘' i, f i = 0 â†” âˆ€ x, f x = 0 :=
by rw [â†has_sum_zero_iff, hf.has_sum_iff]

lemma tsum_ne_zero_iff (hf : summable f) : âˆ‘' i, f i â‰  0 â†” âˆƒ x, f x â‰  0 :=
by rw [ne.def, tsum_eq_zero_iff hf, not_forall]

lemma is_lub_has_sum' (hf : has_sum f a) : is_lub (set.range (Î» s : finset Î², âˆ‘ b in s, f b)) a :=
is_lub_of_tendsto_at_top (finset.sum_mono_set f) hf

end canonically_ordered

section uniform_group

variables [add_comm_group Î±] [uniform_space Î±]

/-- The **Cauchy criterion** for infinite sums, also known as the **Cauchy convergence test** -/
lemma summable_iff_cauchy_seq_finset [complete_space Î±] {f : Î² â†’ Î±} :
  summable f â†” cauchy_seq (Î» (s : finset Î²), âˆ‘ b in s, f b) :=
cauchy_map_iff_exists_tendsto.symm

variables [uniform_add_group Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma cauchy_seq_finset_iff_vanishing :
  cauchy_seq (Î» (s : finset Î²), âˆ‘ b in s, f b)
  â†” âˆ€ e âˆˆ ğ“ (0:Î±), (âˆƒs:finset Î², âˆ€t, disjoint t s â†’ âˆ‘ b in t, f b âˆˆ e) :=
begin
  simp only [cauchy_seq, cauchy_map_iff, and_iff_right at_top_ne_bot,
    prod_at_top_at_top_eq, uniformity_eq_comap_nhds_zero Î±, tendsto_comap_iff, (âˆ˜)],
  rw [tendsto_at_top'],
  split,
  { assume h e he,
    rcases h e he with âŸ¨âŸ¨sâ‚, sâ‚‚âŸ©, hâŸ©,
    use [sâ‚ âˆª sâ‚‚],
    assume t ht,
    specialize h (sâ‚ âˆª sâ‚‚, (sâ‚ âˆª sâ‚‚) âˆª t) âŸ¨le_sup_left, le_sup_of_le_left le_sup_rightâŸ©,
    simpa only [finset.sum_union ht.symm, add_sub_cancel'] using h },
  { assume h e he,
    rcases exists_nhds_half_neg he with âŸ¨d, hd, hdeâŸ©,
    rcases h d hd with âŸ¨s, hâŸ©,
    use [(s, s)],
    rintros âŸ¨tâ‚, tâ‚‚âŸ© âŸ¨htâ‚, htâ‚‚âŸ©,
    have : âˆ‘ b in tâ‚‚, f b - âˆ‘ b in tâ‚, f b = âˆ‘ b in tâ‚‚ \ s, f b - âˆ‘ b in tâ‚ \ s, f b,
    { simp only [(finset.sum_sdiff htâ‚).symm, (finset.sum_sdiff htâ‚‚).symm,
        add_sub_add_right_eq_sub] },
    simp only [this],
    exact hde _ (h _ finset.sdiff_disjoint) _ (h _ finset.sdiff_disjoint) }
end

local attribute [instance] topological_add_group.regular_space

/-- The sum over the complement of a finset tends to `0` when the finset grows to cover the whole
space. This does not need a summability assumption, as otherwise all sums are zero. -/
lemma tendsto_tsum_compl_at_top_zero [t1_space Î±] (f : Î² â†’ Î±) :
  tendsto (Î» (s : finset Î²), âˆ‘' b : {x // x âˆ‰ s}, f b) at_top (ğ“ 0) :=
begin
  by_cases H : summable f,
  { assume e he,
    rcases nhds_is_closed he with âŸ¨o, ho, oe, o_closedâŸ©,
    simp only [le_eq_subset, set.mem_preimage, mem_at_top_sets, filter.mem_map, ge_iff_le],
    obtain âŸ¨s, hsâŸ© : âˆƒ (s : finset Î²), âˆ€ (t : finset Î²), disjoint t s â†’ âˆ‘ (b : Î²) in t, f b âˆˆ o :=
      cauchy_seq_finset_iff_vanishing.1 (tendsto.cauchy_seq H.has_sum) o ho,
    refine âŸ¨s, Î» a sa, oe _âŸ©,
    have A : summable (Î» b : {x // x âˆ‰ a}, f b) := a.summable_compl_iff.2 H,
    apply is_closed.mem_of_tendsto o_closed A.has_sum (eventually_of_forall (Î» b, _)),
    have : disjoint (finset.image (Î» (i : {x // x âˆ‰ a}), (i : Î²)) b) s,
    { apply disjoint_left.2 (Î» i hi his, _),
      rcases mem_image.1 hi with âŸ¨i', hi', rflâŸ©,
      exact i'.2 (sa his), },
    convert hs _ this using 1,
    rw sum_image,
    assume i hi j hj hij,
    exact subtype.ext hij },
  { convert tendsto_const_nhds,
    ext s,
    apply tsum_eq_zero_of_not_summable,
    rwa finset.summable_compl_iff }
end

variable [complete_space Î±]

lemma summable_iff_vanishing :
  summable f â†” âˆ€ e âˆˆ ğ“ (0:Î±), (âˆƒs:finset Î², âˆ€t, disjoint t s â†’ âˆ‘ b in t, f b âˆˆ e) :=
by rw [summable_iff_cauchy_seq_finset, cauchy_seq_finset_iff_vanishing]

/- TODO: generalize to monoid with a uniform continuous subtraction operator: `(a + b) - b = a` -/
lemma summable.summable_of_eq_zero_or_self (hf : summable f) (h : âˆ€b, g b = 0 âˆ¨ g b = f b) :
  summable g :=
summable_iff_vanishing.2 $
  assume e he,
  let âŸ¨s, hsâŸ© := summable_iff_vanishing.1 hf e he in
  âŸ¨s, assume t ht,
    have eq : âˆ‘ b in t.filter (Î»b, g b = f b), f b = âˆ‘ b in t, g b :=
      calc âˆ‘ b in t.filter (Î»b, g b = f b), f b = âˆ‘ b in t.filter (Î»b, g b = f b), g b :
          finset.sum_congr rfl (assume b hb, (finset.mem_filter.1 hb).2.symm)
        ... = âˆ‘ b in t, g b :
        begin
          refine finset.sum_subset (finset.filter_subset _ _) _,
          assume b hbt hb,
          simp only [(âˆ‰), finset.mem_filter, and_iff_right hbt] at hb,
          exact (h b).resolve_right hb
        end,
    eq â–¸ hs _ $ finset.disjoint_of_subset_left (finset.filter_subset _ _) htâŸ©

protected lemma summable.indicator (hf : summable f) (s : set Î²) :
  summable (s.indicator f) :=
hf.summable_of_eq_zero_or_self $ set.indicator_eq_zero_or_self _ _

lemma summable.comp_injective {i : Î³ â†’ Î²} (hf : summable f) (hi : injective i) :
  summable (f âˆ˜ i) :=
begin
  simpa only [set.indicator_range_comp]
    using (hi.summable_iff _).2 (hf.indicator (set.range i)),
  exact Î» x hx, set.indicator_of_not_mem hx _
end

lemma summable.subtype (hf : summable f) (s : set Î²) : summable (f âˆ˜ coe : s â†’ Î±) :=
hf.comp_injective subtype.coe_injective

lemma summable_subtype_and_compl {s : set Î²} :
  summable (Î» x : s, f x) âˆ§ summable (Î» x : sá¶œ, f x) â†” summable f :=
âŸ¨and_imp.2 summable.add_compl, Î» h, âŸ¨h.subtype s, h.subtype sá¶œâŸ©âŸ©

lemma summable.sigma_factor {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) (b : Î²) : summable (Î»c, f âŸ¨b, câŸ©) :=
ha.comp_injective sigma_mk_injective

lemma summable.sigma [t1_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) : summable (Î»b, âˆ‘'c, f âŸ¨b, câŸ©) :=
ha.sigma' (Î» b, ha.sigma_factor b)

lemma summable.prod_factor {f : Î² Ã— Î³ â†’ Î±} (h : summable f) (b : Î²) :
  summable (Î» c, f (b, c)) :=
h.comp_injective $ Î» câ‚ câ‚‚ h, (prod.ext_iff.1 h).2

lemma tsum_sigma [t1_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) : âˆ‘'p, f p = âˆ‘'b c, f âŸ¨b, câŸ© :=
tsum_sigma' (Î» b, ha.sigma_factor b) ha

lemma tsum_prod [t1_space Î±] {f : Î² Ã— Î³ â†’ Î±} (h : summable f) :
  âˆ‘'p, f p = âˆ‘'b c, f âŸ¨b, câŸ© :=
tsum_prod' h h.prod_factor

lemma tsum_comm [t1_space Î±] {f : Î² â†’ Î³ â†’ Î±} (h : summable (function.uncurry f)) :
  âˆ‘' c b, f b c = âˆ‘' b c, f b c :=
tsum_comm' h h.prod_factor h.prod_symm.prod_factor

end uniform_group

section topological_group

variables {G : Type*} [topological_space G] [add_comm_group G] [topological_add_group G]
  {f : Î± â†’ G}

lemma summable.vanishing (hf : summable f) â¦ƒe : set Gâ¦„ (he : e âˆˆ ğ“ (0 : G)) :
  âˆƒ s : finset Î±, âˆ€ t, disjoint t s â†’ âˆ‘ k in t, f k âˆˆ e :=
begin
  letI : uniform_space G := topological_add_group.to_uniform_space G,
  letI : uniform_add_group G := topological_add_group_is_uniform,
  rcases hf with âŸ¨y, hyâŸ©,
  exact cauchy_seq_finset_iff_vanishing.1 hy.cauchy_seq e he
end

/-- Series divergence test: if `f` is a convergent series, then `f x` tends to zero along
`cofinite`. -/
lemma summable.tendsto_cofinite_zero (hf : summable f) : tendsto f cofinite (ğ“ 0) :=
begin
  intros e he,
  rw [filter.mem_map],
  rcases hf.vanishing he with âŸ¨s, hsâŸ©,
  refine s.eventually_cofinite_nmem.mono (Î» x hx, _),
  by simpa using hs {x} (disjoint_singleton_left.2 hx)
end

lemma summable.tendsto_at_top_zero {f : â„• â†’ G} (hf : summable f) : tendsto f at_top (ğ“ 0) :=
by { rw â†nat.cofinite_eq_at_top, exact hf.tendsto_cofinite_zero }

lemma summable.tendsto_top_of_pos {Î± : Type*}
  [linear_ordered_field Î±] [topological_space Î±] [order_topology Î±] {f : â„• â†’ Î±}
  (hf : summable fâ»Â¹) (hf' : âˆ€ n, 0 < f n) : tendsto f at_top at_top :=
begin
  rw [show f = fâ»Â¹â»Â¹, by { ext, simp }],
  apply filter.tendsto.inv_tendsto_zero,
  apply tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
    (summable.tendsto_at_top_zero hf),
  rw eventually_iff_exists_mem,
  refine âŸ¨set.Ioi 0, Ioi_mem_at_top _, Î» _ _, _âŸ©,
  rw [set.mem_Ioi, inv_eq_one_div, one_div, pi.inv_apply, _root_.inv_pos],
  exact hf' _,
end

end topological_group

section linear_order

/-! For infinite sums taking values in a linearly ordered monoid, the existence of a least upper
bound for the finite sums is a criterion for summability.

This criterion is useful when applied in a linearly ordered monoid which is also a complete or
conditionally complete linear order, such as `â„`, `â„â‰¥0`, `â„â‰¥0âˆ`, because it is then easy to check
the existence of a least upper bound.
-/

lemma has_sum_of_is_lub_of_nonneg [linear_ordered_add_comm_monoid Î²] [topological_space Î²]
  [order_topology Î²] {f : Î± â†’ Î²} (b : Î²) (h : âˆ€ b, 0 â‰¤ f b)
  (hf : is_lub (set.range (Î» s, âˆ‘ a in s, f a)) b) :
  has_sum f b :=
tendsto_at_top_is_lub (finset.sum_mono_set_of_nonneg h) hf

lemma has_sum_of_is_lub [canonically_linear_ordered_add_monoid Î²] [topological_space Î²]
   [order_topology Î²] {f : Î± â†’ Î²} (b : Î²) (hf : is_lub (set.range (Î» s, âˆ‘ a in s, f a)) b) :
  has_sum f b :=
tendsto_at_top_is_lub (finset.sum_mono_set f) hf

lemma summable_abs_iff [linear_ordered_add_comm_group Î²] [uniform_space Î²]
  [uniform_add_group Î²] [complete_space Î²] {f : Î± â†’ Î²} :
  summable (Î» x, |f x|) â†” summable f :=
have h1 : âˆ€ x : {x | 0 â‰¤ f x}, |f x| = f x := Î» x, abs_of_nonneg x.2,
have h2 : âˆ€ x : {x | 0 â‰¤ f x}á¶œ, |f x| = -f x := Î» x, abs_of_neg (not_le.1 x.2),
calc summable (Î» x, |f x|) â†”
  summable (Î» x : {x | 0 â‰¤ f x}, |f x|) âˆ§ summable (Î» x : {x | 0 â‰¤ f x}á¶œ, |f x|) :
  summable_subtype_and_compl.symm
... â†” summable (Î» x : {x | 0 â‰¤ f x}, f x) âˆ§ summable (Î» x : {x | 0 â‰¤ f x}á¶œ, -f x) :
  by simp only [h1, h2]
... â†” _ : by simp only [summable_neg_iff, summable_subtype_and_compl]

alias summable_abs_iff â†” summable.of_abs summable.abs

lemma finite_of_summable_const [linear_ordered_add_comm_group Î²] [archimedean Î²]
  [topological_space Î²] [order_closed_topology Î²] {b : Î²} (hb : 0 < b)
  (hf : summable (Î» a : Î±, b)) :
  set.finite (set.univ : set Î±) :=
begin
  have H : âˆ€ s : finset Î±, s.card â€¢ b â‰¤ âˆ‘' a : Î±, b,
  { intros s,
    simpa using sum_le_has_sum s (Î» a ha, hb.le) hf.has_sum },
  obtain âŸ¨n, hnâŸ© := archimedean.arch (âˆ‘' a : Î±, b) hb,
  have : âˆ€ s : finset Î±, s.card â‰¤ n,
  { intros s,
    simpa [nsmul_le_nsmul_iff hb] using (H s).trans hn },
  haveI : fintype Î± := fintype_of_finset_card_le n this,
  exact set.finite_univ
end

end linear_order

section cauchy_seq
open filter

/-- If the extended distance between consecutive points of a sequence is estimated
by a summable series of `nnreal`s, then the original sequence is a Cauchy sequence. -/
lemma cauchy_seq_of_edist_le_of_summable [pseudo_emetric_space Î±] {f : â„• â†’ Î±} (d : â„• â†’ â„â‰¥0)
  (hf : âˆ€ n, edist (f n) (f n.succ) â‰¤ d n) (hd : summable d) : cauchy_seq f :=
begin
  refine emetric.cauchy_seq_iff_nnreal.2 (Î» Îµ Îµpos, _),
  -- Actually we need partial sums of `d` to be a Cauchy sequence
  replace hd : cauchy_seq (Î» (n : â„•), âˆ‘ x in range n, d x) :=
    let âŸ¨_, HâŸ© := hd in H.tendsto_sum_nat.cauchy_seq,
  -- Now we take the same `N` as in one of the definitions of a Cauchy sequence
  refine (metric.cauchy_seq_iff'.1 hd Îµ (nnreal.coe_pos.2 Îµpos)).imp (Î» N hN n hn, _),
  have hsum := hN n hn,
  -- We simplify the known inequality
  rw [dist_nndist, nnreal.nndist_eq, â† sum_range_add_sum_Ico _ hn, add_tsub_cancel_left] at hsum,
  norm_cast at hsum,
  replace hsum := lt_of_le_of_lt (le_max_left _ _) hsum,
  rw edist_comm,
  -- Then use `hf` to simplify the goal to the same form
  apply lt_of_le_of_lt (edist_le_Ico_sum_of_edist_le hn (Î» k _ _, hf k)),
  assumption_mod_cast
end

/-- If the distance between consecutive points of a sequence is estimated by a summable series,
then the original sequence is a Cauchy sequence. -/
lemma cauchy_seq_of_dist_le_of_summable [pseudo_metric_space Î±] {f : â„• â†’ Î±} (d : â„• â†’ â„)
  (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n) (hd : summable d) : cauchy_seq f :=
begin
  refine metric.cauchy_seq_iff'.2 (Î»Îµ Îµpos, _),
  replace hd : cauchy_seq (Î» (n : â„•), âˆ‘ x in range n, d x) :=
    let âŸ¨_, HâŸ© := hd in H.tendsto_sum_nat.cauchy_seq,
  refine (metric.cauchy_seq_iff'.1 hd Îµ Îµpos).imp (Î» N hN n hn, _),
  have hsum := hN n hn,
  rw [real.dist_eq, â† sum_Ico_eq_sub _ hn] at hsum,
  calc dist (f n) (f N) = dist (f N) (f n) : dist_comm _ _
  ... â‰¤ âˆ‘ x in Ico N n, d x : dist_le_Ico_sum_of_dist_le hn (Î» k _ _, hf k)
  ... â‰¤ |âˆ‘ x in Ico N n, d x| : le_abs_self _
  ... < Îµ : hsum
end

lemma cauchy_seq_of_summable_dist [pseudo_metric_space Î±] {f : â„• â†’ Î±}
  (h : summable (Î»n, dist (f n) (f n.succ))) : cauchy_seq f :=
cauchy_seq_of_dist_le_of_summable _ (Î» _, le_rfl) h

lemma dist_le_tsum_of_dist_le_of_tendsto [pseudo_metric_space Î±] {f : â„• â†’ Î±} (d : â„• â†’ â„)
  (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n) (hd : summable d) {a : Î±} (ha : tendsto f at_top (ğ“ a))
  (n : â„•) :
  dist (f n) a â‰¤ âˆ‘' m, d (n + m) :=
begin
  refine le_of_tendsto (tendsto_const_nhds.dist ha)
    (eventually_at_top.2 âŸ¨n, Î» m hnm, _âŸ©),
  refine le_trans (dist_le_Ico_sum_of_dist_le hnm (Î» k _ _, hf k)) _,
  rw [sum_Ico_eq_sum_range],
  refine sum_le_tsum (range _) (Î» _ _, le_trans dist_nonneg (hf _)) _,
  exact hd.comp_injective (add_right_injective n)
end

lemma dist_le_tsum_of_dist_le_of_tendstoâ‚€ [pseudo_metric_space Î±] {f : â„• â†’ Î±} (d : â„• â†’ â„)
  (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n) (hd : summable d) {a : Î±} (ha : tendsto f at_top (ğ“ a)) :
  dist (f 0) a â‰¤ tsum d :=
by simpa only [zero_add] using dist_le_tsum_of_dist_le_of_tendsto d hf hd ha 0

lemma dist_le_tsum_dist_of_tendsto [pseudo_metric_space Î±] {f : â„• â†’ Î±}
  (h : summable (Î»n, dist (f n) (f n.succ))) {a : Î±} (ha : tendsto f at_top (ğ“ a)) (n) :
  dist (f n) a â‰¤ âˆ‘' m, dist (f (n+m)) (f (n+m).succ) :=
show dist (f n) a â‰¤ âˆ‘' m, (Î»x, dist (f x) (f x.succ)) (n + m), from
dist_le_tsum_of_dist_le_of_tendsto (Î» n, dist (f n) (f n.succ)) (Î» _, le_rfl) h ha n

lemma dist_le_tsum_dist_of_tendstoâ‚€ [pseudo_metric_space Î±] {f : â„• â†’ Î±}
  (h : summable (Î»n, dist (f n) (f n.succ))) {a : Î±} (ha : tendsto f at_top (ğ“ a)) :
  dist (f 0) a â‰¤ âˆ‘' n, dist (f n) (f n.succ) :=
by simpa only [zero_add] using dist_le_tsum_dist_of_tendsto h ha 0

end cauchy_seq

/-! ## Multipliying two infinite sums

In this section, we prove various results about `(âˆ‘' x : Î², f x) * (âˆ‘' y : Î³, g y)`. Note that we
always assume that the family `Î» x : Î² Ã— Î³, f x.1 * g x.2` is summable, since there is no way to
deduce this from the summmabilities of `f` and `g` in general, but if you are working in a normed
space, you may want to use the analogous lemmas in `analysis/normed_space/basic`
(e.g `tsum_mul_tsum_of_summable_norm`).

We first establish results about arbitrary index types, `Î²` and `Î³`, and then we specialize to
`Î² = Î³ = â„•` to prove the Cauchy product formula (see `tsum_mul_tsum_eq_tsum_sum_antidiagonal`).

### Arbitrary index types
-/

section tsum_mul_tsum

variables [topological_space Î±] [regular_space Î±] [non_unital_non_assoc_semiring Î±]
  [topological_semiring Î±] {f : Î² â†’ Î±} {g : Î³ â†’ Î±} {s t u : Î±}

lemma has_sum.mul_eq (hf : has_sum f s) (hg : has_sum g t)
  (hfg : has_sum (Î» (x : Î² Ã— Î³), f x.1 * g x.2) u) :
  s * t = u :=
have keyâ‚ : has_sum (Î» b, f b * t) (s * t),
  from hf.mul_right t,
have this : âˆ€ b : Î², has_sum (Î» c : Î³, f b * g c) (f b * t),
  from Î» b, hg.mul_left (f b),
have keyâ‚‚ : has_sum (Î» b, f b * t) u,
  from has_sum.prod_fiberwise hfg this,
keyâ‚.unique keyâ‚‚

lemma has_sum.mul (hf : has_sum f s) (hg : has_sum g t)
  (hfg : summable (Î» (x : Î² Ã— Î³), f x.1 * g x.2)) :
  has_sum (Î» (x : Î² Ã— Î³), f x.1 * g x.2) (s * t) :=
let âŸ¨u, huâŸ© := hfg in
(hf.mul_eq hg hu).symm â–¸ hu

/-- Product of two infinites sums indexed by arbitrary types.
    See also `tsum_mul_tsum_of_summable_norm` if `f` and `g` are abolutely summable. -/
lemma tsum_mul_tsum (hf : summable f) (hg : summable g)
  (hfg : summable (Î» (x : Î² Ã— Î³), f x.1 * g x.2)) :
  (âˆ‘' x, f x) * (âˆ‘' y, g y) = (âˆ‘' z : Î² Ã— Î³, f z.1 * g z.2) :=
hf.has_sum.mul_eq hg.has_sum hfg.has_sum

end tsum_mul_tsum

section cauchy_product

/-! ### `â„•`-indexed families (Cauchy product)

We prove two versions of the Cauchy product formula. The first one is
`tsum_mul_tsum_eq_tsum_sum_range`, where the `n`-th term is a sum over `finset.range (n+1)`
involving `nat` substraction.
In order to avoid `nat` substraction, we also provide `tsum_mul_tsum_eq_tsum_sum_antidiagonal`,
where the `n`-th term is a sum over all pairs `(k, l)` such that `k+l=n`, which corresponds to the
`finset` `finset.nat.antidiagonal n` -/

variables {f : â„• â†’ Î±} {g : â„• â†’ Î±}

open finset

variables [topological_space Î±] [non_unital_non_assoc_semiring Î±]

/- The family `(k, l) : â„• Ã— â„• â†¦ f k * g l` is summable if and only if the family
`(n, k, l) : Î£ (n : â„•), nat.antidiagonal n â†¦ f k * g l` is summable. -/
lemma summable_mul_prod_iff_summable_mul_sigma_antidiagonal {f g : â„• â†’ Î±} :
  summable (Î» x : â„• Ã— â„•, f x.1 * g x.2) â†”
  summable (Î» x : (Î£ (n : â„•), nat.antidiagonal n), f (x.2 : â„• Ã— â„•).1 * g (x.2 : â„• Ã— â„•).2) :=
nat.sigma_antidiagonal_equiv_prod.summable_iff.symm

variables [regular_space Î±] [topological_semiring Î±]

lemma summable_sum_mul_antidiagonal_of_summable_mul {f g : â„• â†’ Î±}
  (h : summable (Î» x : â„• Ã— â„•, f x.1 * g x.2)) :
  summable (Î» n, âˆ‘ kl in nat.antidiagonal n, f kl.1 * g kl.2) :=
begin
  rw summable_mul_prod_iff_summable_mul_sigma_antidiagonal at h,
  conv {congr, funext, rw [â† finset.sum_finset_coe, â† tsum_fintype]},
  exact h.sigma' (Î» n, (has_sum_fintype _).summable),
end

/-- The Cauchy product formula for the product of two infinites sums indexed by `â„•`,
    expressed by summing on `finset.nat.antidiagonal`.
    See also `tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm`
    if `f` and `g` are absolutely summable. -/
lemma tsum_mul_tsum_eq_tsum_sum_antidiagonal (hf : summable f) (hg : summable g)
  (hfg : summable (Î» (x : â„• Ã— â„•), f x.1 * g x.2)) :
  (âˆ‘' n, f n) * (âˆ‘' n, g n) = (âˆ‘' n, âˆ‘ kl in nat.antidiagonal n, f kl.1 * g kl.2) :=
begin
  conv_rhs {congr, funext, rw [â† finset.sum_finset_coe, â† tsum_fintype]},
  rw [tsum_mul_tsum hf hg hfg, â† nat.sigma_antidiagonal_equiv_prod.tsum_eq (_ : â„• Ã— â„• â†’ Î±)],
  exact tsum_sigma' (Î» n, (has_sum_fintype _).summable)
    (summable_mul_prod_iff_summable_mul_sigma_antidiagonal.mp hfg)
end

lemma summable_sum_mul_range_of_summable_mul {f g : â„• â†’ Î±}
  (h : summable (Î» x : â„• Ã— â„•, f x.1 * g x.2)) :
  summable (Î» n, âˆ‘ k in range (n+1), f k * g (n - k)) :=
begin
  simp_rw â† nat.sum_antidiagonal_eq_sum_range_succ (Î» k l, f k * g l),
  exact summable_sum_mul_antidiagonal_of_summable_mul h
end

/-- The Cauchy product formula for the product of two infinites sums indexed by `â„•`,
    expressed by summing on `finset.range`.
    See also `tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm`
    if `f` and `g` are absolutely summable. -/
lemma tsum_mul_tsum_eq_tsum_sum_range (hf : summable f) (hg : summable g)
  (hfg : summable (Î» (x : â„• Ã— â„•), f x.1 * g x.2)) :
  (âˆ‘' n, f n) * (âˆ‘' n, g n) = (âˆ‘' n, âˆ‘ k in range (n+1), f k * g (n - k)) :=
begin
  simp_rw â† nat.sum_antidiagonal_eq_sum_range_succ (Î» k l, f k * g l),
  exact tsum_mul_tsum_eq_tsum_sum_antidiagonal hf hg hfg
end

end cauchy_product
