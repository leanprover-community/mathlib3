/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import algebra.big_operators.intervals
import topology.instances.real
import data.indicator_function
import data.equiv.encodable.lattice
import order.filter.at_top_bot

/-!
# Infinite sum over a topological monoid

This sum is known as unconditionally convergent, as it sums to the same value under all possible
permutations. For Euclidean spaces (finite dimensional Banach spaces) this is equivalent to absolute
convergence.

Note: There are summable sequences which are not unconditionally convergent! The other way holds
generally, see `has_sum.tendsto_sum_nat`.

## References

* Bourbaki: General Topology (1995), Chapter 3 Â§5 (Infinite sums in commutative groups)

-/

noncomputable theory
open finset filter function classical
open_locale topological_space classical big_operators

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

section has_sum
variables [add_comm_monoid Î±] [topological_space Î±]

/-- Infinite sum on a topological monoid
The `at_top` filter on `finset Î±` is the limit of all finite sets towards the entire type. So we sum
up bigger and bigger sets. This sum operation is still invariant under reordering, and a absolute
sum operator.

This is based on Mario Carneiro's infinite sum in Metamath.

For the definition or many statements, Î± does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant.
-/
def has_sum (f : Î² â†’ Î±) (a : Î±) : Prop := tendsto (Î»s:finset Î², âˆ‘ b in s, f b) at_top (ğ“ a)

/-- `summable f` means that `f` has some (infinite) sum. Use `tsum` to get the value. -/
def summable (f : Î² â†’ Î±) : Prop := âˆƒa, has_sum f a

/-- `âˆ‘' i, f i` is the sum of `f` it exists, or 0 otherwise -/
def tsum (f : Î² â†’ Î±) := if h : summable f then classical.some h else 0

notation `âˆ‘'` binders `, ` r:(scoped f, tsum f) := r

variables {f g : Î² â†’ Î±} {a b : Î±} {s : finset Î²}

lemma summable.has_sum (ha : summable f) : has_sum f (âˆ‘'b, f b) :=
by simp [ha, tsum]; exact some_spec ha

lemma has_sum.summable (h : has_sum f a) : summable f := âŸ¨a, hâŸ©

/-- Constant zero function has sum `0` -/
lemma has_sum_zero : has_sum (Î»b, 0 : Î² â†’ Î±) 0 :=
by simp [has_sum, tendsto_const_nhds]

lemma summable_zero : summable (Î»b, 0 : Î² â†’ Î±) := has_sum_zero.summable

lemma tsum_eq_zero_of_not_summable (h : Â¬ summable f) : (âˆ‘'b, f b) = 0 :=
by simp [tsum, h]

lemma has_sum.has_sum_of_sum_eq {g : Î³ â†’ Î±}
  (h_eq : âˆ€u:finset Î³, âˆƒv:finset Î², âˆ€v', v âŠ† v' â†’ âˆƒu', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
  (hf : has_sum g a) :
  has_sum f a :=
le_trans (map_at_top_finset_sum_le_of_sum_eq h_eq) hf

lemma has_sum_iff_has_sum {g : Î³ â†’ Î±}
  (hâ‚ : âˆ€u:finset Î³, âˆƒv:finset Î², âˆ€v', v âŠ† v' â†’ âˆƒu', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
  (hâ‚‚ : âˆ€v:finset Î², âˆƒu:finset Î³, âˆ€u', u âŠ† u' â†’ âˆƒv', v âŠ† v' âˆ§ âˆ‘ b in v', f b = âˆ‘ x in u', g x) :
  has_sum f a â†” has_sum g a :=
âŸ¨has_sum.has_sum_of_sum_eq hâ‚‚, has_sum.has_sum_of_sum_eq hâ‚âŸ©

lemma function.injective.has_sum_iff {g : Î³ â†’ Î²} (hg : injective g)
  (hf : âˆ€ x âˆ‰ set.range g, f x = 0) :
  has_sum (f âˆ˜ g) a â†” has_sum f a :=
by simp only [has_sum, tendsto, hg.map_at_top_finset_sum_eq hf]

lemma function.injective.summable_iff {g : Î³ â†’ Î²} (hg : injective g)
  (hf : âˆ€ x âˆ‰ set.range g, f x = 0) :
  summable (f âˆ˜ g) â†” summable f :=
exists_congr $ Î» _, hg.has_sum_iff hf

lemma has_sum_subtype_iff_of_support_subset {s : set Î²} (hf : support f âŠ† s) :
  has_sum (f âˆ˜ coe : s â†’ Î±) a â†” has_sum f a :=
subtype.coe_injective.has_sum_iff $ by simpa using support_subset_iff'.1 hf

lemma has_sum_subtype_iff_indicator {s : set Î²} :
  has_sum (f âˆ˜ coe : s â†’ Î±) a â†” has_sum (s.indicator f) a :=
by rw [â† set.indicator_range_comp, subtype.range_coe,
  has_sum_subtype_iff_of_support_subset set.support_indicator]

@[simp] lemma has_sum_subtype_support : has_sum (f âˆ˜ coe : support f â†’ Î±) a â†” has_sum f a :=
has_sum_subtype_iff_of_support_subset $ set.subset.refl _

lemma has_sum_fintype [fintype Î²] (f : Î² â†’ Î±) : has_sum f (âˆ‘ b, f b) :=
order_top.tendsto_at_top _

protected lemma finset.has_sum (s : finset Î²) (f : Î² â†’ Î±) :
  has_sum (f âˆ˜ coe : (â†‘s : set Î²) â†’ Î±) (âˆ‘ b in s, f b) :=
by { rw â† sum_attach, exact has_sum_fintype _ }

protected lemma finset.summable (s : finset Î²) (f : Î² â†’ Î±) :
  summable (f âˆ˜ coe : (â†‘s : set Î²) â†’ Î±) :=
(s.has_sum f).summable

protected lemma set.finite.summable {s : set Î²} (hs : s.finite) (f : Î² â†’ Î±) :
  summable (f âˆ˜ coe : s â†’ Î±) :=
by convert hs.to_finset.summable f; simp only [hs.coe_to_finset]

/-- If a function `f` vanishes outside of a finite set `s`, then it `has_sum` `âˆ‘ b in s, f b`. -/
lemma has_sum_sum_of_ne_finset_zero (hf : âˆ€bâˆ‰s, f b = 0) : has_sum f (âˆ‘ b in s, f b) :=
(has_sum_subtype_iff_of_support_subset $ support_subset_iff'.2 hf).1 $ s.has_sum f

lemma summable_of_ne_finset_zero (hf : âˆ€bâˆ‰s, f b = 0) : summable f :=
(has_sum_sum_of_ne_finset_zero hf).summable

lemma has_sum_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€b' â‰  b, f b' = 0) :
  has_sum f (f b) :=
suffices has_sum f (âˆ‘ b' in {b}, f b'),
  by simpa using this,
has_sum_sum_of_ne_finset_zero $ by simpa [hf]

lemma has_sum_ite_eq (b : Î²) (a : Î±) : has_sum (Î»b', if b' = b then a else 0) a :=
begin
  convert has_sum_single b _,
  { exact (if_pos rfl).symm },
  assume b' hb',
  exact if_neg hb'
end

lemma equiv.has_sum_iff (e : Î³ â‰ƒ Î²) :
  has_sum (f âˆ˜ e) a â†” has_sum f a :=
e.injective.has_sum_iff $ by simp

lemma equiv.summable_iff (e : Î³ â‰ƒ Î²) :
  summable (f âˆ˜ e) â†” summable f :=
exists_congr $ Î» a, e.has_sum_iff

lemma summable.prod_symm {f : Î² Ã— Î³ â†’ Î±} (hf : summable f) : summable (Î» p : Î³ Ã— Î², f p.swap) :=
(equiv.prod_comm Î³ Î²).summable_iff.2 hf

lemma equiv.has_sum_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x : support f, g (e x) = f x) :
  has_sum f a â†” has_sum g a :=
have (g âˆ˜ coe) âˆ˜ e = f âˆ˜ coe, from funext he,
by rw [â† has_sum_subtype_support, â† this, e.has_sum_iff, has_sum_subtype_support]

lemma has_sum_iff_has_sum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
  (hi : âˆ€ â¦ƒx yâ¦„, i x = i y â†’ (x : Î³) = y)
  (hf : support f âŠ† set.range i) (hfg : âˆ€ x, f (i x) = g x) :
  has_sum f a â†” has_sum g a :=
iff.symm $ equiv.has_sum_iff_of_support
  (equiv.of_bijective (Î» x, âŸ¨i x, Î» hx, x.coe_prop $ hfg x â–¸ hxâŸ©)
    âŸ¨Î» x y h, subtype.ext $ hi $ subtype.ext_iff.1 h,
      Î» y, (hf y.coe_prop).imp $ Î» x hx, subtype.ext hxâŸ©)
  hfg

lemma equiv.summable_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x : support f, g (e x) = f x) :
  summable f â†” summable g :=
exists_congr $ Î» _, e.has_sum_iff_of_support he

protected lemma has_sum.map [add_comm_monoid Î³] [topological_space Î³] (hf : has_sum f a)
  (g : Î± â†’+ Î³) (hâ‚ƒ : continuous g) :
  has_sum (g âˆ˜ f) (g a) :=
have g âˆ˜ (Î»s:finset Î², âˆ‘ b in s, f b) = (Î»s:finset Î², âˆ‘ b in s, g (f b)),
  from funext $ g.map_sum _,
show tendsto (Î»s:finset Î², âˆ‘ b in s, g (f b)) at_top (ğ“ (g a)),
  from this â–¸ (hâ‚ƒ.tendsto a).comp hf

/-- If `f : â„• â†’ Î±` has sum `a`, then the partial sums `âˆ‘_{i=0}^{n-1} f i` converge to `a`. -/
lemma has_sum.tendsto_sum_nat {f : â„• â†’ Î±} (h : has_sum f a) :
  tendsto (Î»n:â„•, âˆ‘ i in range n, f i) at_top (ğ“ a) :=
h.comp tendsto_finset_range

lemma has_sum.unique {aâ‚ aâ‚‚ : Î±} [t2_space Î±] : has_sum f aâ‚ â†’ has_sum f aâ‚‚ â†’ aâ‚ = aâ‚‚ :=
tendsto_nhds_unique

lemma summable.has_sum_iff_tendsto_nat [t2_space Î±] {f : â„• â†’ Î±} {a : Î±} (hf : summable f) :
  has_sum f a â†” tendsto (Î»n:â„•, âˆ‘ i in range n, f i) at_top (ğ“ a) :=
begin
  refine âŸ¨Î» h, h.tendsto_sum_nat, Î» h, _âŸ©,
  rw tendsto_nhds_unique h hf.has_sum.tendsto_sum_nat,
  exact hf.has_sum
end

lemma equiv.summable_iff_of_has_sum_iff {Î±' : Type*} [add_comm_monoid Î±']
  [topological_space Î±'] (e : Î±' â‰ƒ Î±) {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
  (he : âˆ€ {a}, has_sum f (e a) â†” has_sum g a) :
  summable f â†” summable g :=
âŸ¨Î» âŸ¨a, haâŸ©, âŸ¨e.symm a, he.1 $ by rwa [e.apply_symm_apply]âŸ©, Î» âŸ¨a, haâŸ©, âŸ¨e a, he.2 haâŸ©âŸ©

variable [has_continuous_add Î±]

lemma has_sum.add (hf : has_sum f a) (hg : has_sum g b) : has_sum (Î»b, f b + g b) (a + b) :=
by simp only [has_sum, sum_add_distrib]; exact hf.add hg

lemma summable.add (hf : summable f) (hg : summable g) : summable (Î»b, f b + g b) :=
(hf.has_sum.add hg.has_sum).summable

lemma has_sum_sum {f : Î³ â†’ Î² â†’ Î±} {a : Î³ â†’ Î±} {s : finset Î³} :
  (âˆ€iâˆˆs, has_sum (f i) (a i)) â†’ has_sum (Î»b, âˆ‘ i in s, f i b) (âˆ‘ i in s, a i) :=
finset.induction_on s (by simp [has_sum_zero]) (by simp [has_sum.add] {contextual := tt})

lemma summable_sum {f : Î³ â†’ Î² â†’ Î±} {s : finset Î³} (hf : âˆ€iâˆˆs, summable (f i)) :
  summable (Î»b, âˆ‘ i in s, f i b) :=
(has_sum_sum $ assume i hi, (hf i hi).has_sum).summable

lemma has_sum.add_compl {s : set Î²} (ha : has_sum (f âˆ˜ coe : s â†’ Î±) a)
  (hb : has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) b) :
  has_sum f (a + b) :=
by simpa using (has_sum_subtype_iff_indicator.1 ha).add (has_sum_subtype_iff_indicator.1 hb)

lemma summable.add_compl {s : set Î²} (hs : summable (f âˆ˜ coe : s â†’ Î±))
  (hsc : summable (f âˆ˜ coe : sá¶œ â†’ Î±)) :
  summable f :=
(hs.has_sum.add_compl hsc.has_sum).summable

lemma has_sum.compl_add {s : set Î²} (ha : has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) a)
  (hb : has_sum (f âˆ˜ coe : s â†’ Î±) b) :
  has_sum f (a + b) :=
by simpa using (has_sum_subtype_iff_indicator.1 ha).add (has_sum_subtype_iff_indicator.1 hb)

lemma summable.compl_add {s : set Î²} (hs : summable (f âˆ˜ coe : sá¶œ â†’ Î±))
  (hsc : summable (f âˆ˜ coe : s â†’ Î±)) :
  summable f :=
(hs.has_sum.compl_add hsc.has_sum).summable

lemma has_sum.sigma [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£ b:Î², Î³ b) â†’ Î±} {g : Î² â†’ Î±} {a : Î±}
  (ha : has_sum f a) (hf : âˆ€b, has_sum (Î»c, f âŸ¨b, câŸ©) (g b)) : has_sum g a :=
assume s' hs',
let
  âŸ¨s, hs, hss', hscâŸ© := nhds_is_closed hs',
  âŸ¨u, huâŸ© := mem_at_top_sets.mp $ ha hs,
  fsts := u.image sigma.fst,
  snds := Î»b, u.bind (Î»p, (if h : p.1 = b then {cast (congr_arg Î³ h) p.2} else âˆ… : finset (Î³ b)))
in
have u_subset : u âŠ† fsts.sigma snds,
  from subset_iff.mpr $ assume âŸ¨b, câŸ© hu,
  have hb : b âˆˆ fsts, from finset.mem_image.mpr âŸ¨_, hu, rflâŸ©,
  have hc : c âˆˆ snds b, from mem_bind.mpr âŸ¨_, hu, by simp; reflâŸ©,
  by simp [mem_sigma, hb, hc] ,
mem_at_top_sets.mpr $ exists.intro fsts $ assume bs (hbs : fsts âŠ† bs),
  have h : âˆ€cs : Î  b âˆˆ bs, finset (Î³ b),
      ((â‹‚b (hb : b âˆˆ bs), (Î»p:Î b, finset (Î³ b), p b) â»Â¹' {cs' | cs b hb âŠ† cs' }) âˆ©
      (Î»p, âˆ‘ b in bs, âˆ‘ c in p b, f âŸ¨b, câŸ©) â»Â¹' s).nonempty,
    from assume cs,
    let cs' := Î»b, (if h : b âˆˆ bs then cs b h else âˆ…) âˆª snds b in
    have sum_eq : âˆ‘ b in bs, âˆ‘ c in cs' b, f âŸ¨b, câŸ© = âˆ‘ x in bs.sigma cs', f x,
      from sum_sigma.symm,
    have âˆ‘ x in bs.sigma cs', f x âˆˆ s,
      from hu _ $ finset.subset.trans u_subset $ sigma_mono hbs $
        assume b, @finset.subset_union_right (Î³ b) _ _ _,
    exists.intro cs' $
    by simp [sum_eq, this]; { intros b hb, simp [cs', hb, finset.subset_union_left] },
  have tendsto (Î»p:(Î b:Î², finset (Î³ b)), âˆ‘ b in bs, âˆ‘ c in p b, f âŸ¨b, câŸ©)
      (â¨…b (h : b âˆˆ bs), at_top.comap (Î»p, p b)) (ğ“ (âˆ‘ b in bs, g b)),
    from tendsto_finset_sum bs $
      assume c hc, tendsto_infi' c $ tendsto_infi' hc $ by apply tendsto.comp (hf c) tendsto_comap,
  have âˆ‘ b in bs, g b âˆˆ s,
    from @mem_of_closed_of_tendsto' _ _ _ _ _ _ _ this hsc $ forall_sets_nonempty_iff_ne_bot.mp $
      begin
        simp only [mem_inf_sets, exists_imp_distrib, forall_and_distrib, and_imp,
               filter.mem_infi_sets_finset, mem_comap_sets, mem_at_top_sets, and_comm,
               mem_principal_sets, set.preimage_subset_iff, exists_prop, skolem],
        intros sâ‚ sâ‚‚ sâ‚ƒ hsâ‚ hsâ‚ƒ p hsâ‚‚ p' hp cs hp',
        have : (â‹‚b (h : b âˆˆ bs), (Î»p:(Î b, finset (Î³ b)), p b) â»Â¹' {cs' | cs b h âŠ† cs' }) â‰¤ (â¨…bâˆˆbs, p b),
          from (infi_le_infi $ assume b, infi_le_infi $ assume hb,
            le_trans (set.preimage_mono $ hp' b hb) (hp b hb)),
        exact (h _).mono (set.subset.trans (set.inter_subset_inter (le_trans this hsâ‚‚) hsâ‚ƒ) hsâ‚)
      end,
  hss' this

/-- If a series `f` on `Î² Ã— Î³` has sum `a` and for each `b` the restriction of `f` to `{b} Ã— Î³`
has sum `g b`, then the series `g` has sum `a`. -/
lemma has_sum.prod_fiberwise [regular_space Î±] {f : Î² Ã— Î³ â†’ Î±} {g : Î² â†’ Î±} {a : Î±}
  (ha : has_sum f a) (hf : âˆ€b, has_sum (Î»c, f (b, c)) (g b)) :
  has_sum g a :=
has_sum.sigma ((equiv.sigma_equiv_prod Î² Î³).has_sum_iff.2 ha) hf

lemma summable.sigma' [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) (hf : âˆ€b, summable (Î»c, f âŸ¨b, câŸ©)) :
  summable (Î»b, âˆ‘'c, f âŸ¨b, câŸ©) :=
(ha.has_sum.sigma (assume b, (hf b).has_sum)).summable

lemma has_sum.sigma_of_has_sum [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£ b:Î², Î³ b) â†’ Î±} {g : Î² â†’ Î±}
  {a : Î±} (ha : has_sum g a) (hf : âˆ€b, has_sum (Î»c, f âŸ¨b, câŸ©) (g b)) (hf' : summable f) :
  has_sum f a :=
by simpa [(hf'.has_sum.sigma hf).unique ha] using hf'.has_sum

end has_sum

section tsum
variables [add_comm_monoid Î±] [topological_space Î±] [t2_space Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma has_sum.tsum_eq (ha : has_sum f a) : (âˆ‘'b, f b) = a :=
(summable.has_sum âŸ¨a, haâŸ©).unique ha

lemma summable.has_sum_iff (h : summable f) : has_sum f a â†” (âˆ‘'b, f b) = a :=
iff.intro has_sum.tsum_eq (assume eq, eq â–¸ h.has_sum)

@[simp] lemma tsum_zero : (âˆ‘'b:Î², 0:Î±) = 0 := has_sum_zero.tsum_eq

lemma tsum_eq_sum {f : Î² â†’ Î±} {s : finset Î²} (hf : âˆ€bâˆ‰s, f b = 0)  :
  (âˆ‘'b, f b) = âˆ‘ b in s, f b :=
(has_sum_sum_of_ne_finset_zero hf).tsum_eq

lemma tsum_fintype [fintype Î²] (f : Î² â†’ Î±) : (âˆ‘'b, f b) = âˆ‘ b, f b :=
(has_sum_fintype f).tsum_eq

@[simp] lemma finset.tsum_subtype (s : finset Î²) (f : Î² â†’ Î±) :
  (âˆ‘'x : {x // x âˆˆ s}, f x) = âˆ‘ x in s, f x :=
(s.has_sum f).tsum_eq

lemma tsum_eq_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€b' â‰  b, f b' = 0)  :
  (âˆ‘'b, f b) = f b :=
(has_sum_single b hf).tsum_eq

@[simp] lemma tsum_ite_eq (b : Î²) (a : Î±) : (âˆ‘'b', if b' = b then a else 0) = a :=
(has_sum_ite_eq b a).tsum_eq

lemma equiv.tsum_eq_tsum_of_has_sum_iff_has_sum {Î±' : Type*} [add_comm_monoid Î±']
  [topological_space Î±'] (e : Î±' â‰ƒ Î±) (h0 : e 0 = 0) {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
  (h : âˆ€ {a}, has_sum f (e a) â†” has_sum g a) :
  (âˆ‘' b, f b) = e (âˆ‘' c, g c) :=
by_cases
  (assume : summable g, (h.mpr this.has_sum).tsum_eq)
  (assume hg : Â¬ summable g,
    have hf : Â¬ summable f, from mt (e.summable_iff_of_has_sum_iff @h).1 hg,
    by simp [tsum, hf, hg, h0])

lemma tsum_eq_tsum_of_has_sum_iff_has_sum {f : Î² â†’ Î±} {g : Î³ â†’ Î±}
  (h : âˆ€{a}, has_sum f a â†” has_sum g a) :
  (âˆ‘'b, f b) = (âˆ‘'c, g c) :=
(equiv.refl Î±).tsum_eq_tsum_of_has_sum_iff_has_sum rfl @h

lemma equiv.tsum_eq (j : Î³ â‰ƒ Î²) (f : Î² â†’ Î±) : (âˆ‘'c, f (j c)) = (âˆ‘'b, f b) :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» a, j.has_sum_iff

lemma equiv.tsum_eq_tsum_of_support {f : Î² â†’ Î±} {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x, g (e x) = f x) :
  (âˆ‘' x, f x) = âˆ‘' y, g y :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, e.has_sum_iff_of_support he

lemma tsum_eq_tsum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
  (hi : âˆ€ â¦ƒx yâ¦„, i x = i y â†’ (x : Î³) = y)
  (hf : support f âŠ† set.range i) (hfg : âˆ€ x, f (i x) = g x) :
  (âˆ‘' x, f x)  = âˆ‘' y, g y :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, has_sum_iff_has_sum_of_ne_zero_bij i hi hf hfg

lemma tsum_subtype (s : set Î²) (f : Î² â†’ Î±) :
  (âˆ‘' x : s, f x) = âˆ‘' x, s.indicator f x :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, has_sum_subtype_iff_indicator

section has_continuous_add
variable [has_continuous_add Î±]

lemma tsum_add (hf : summable f) (hg : summable g) : (âˆ‘'b, f b + g b) = (âˆ‘'b, f b) + (âˆ‘'b, g b) :=
(hf.has_sum.add hg.has_sum).tsum_eq

lemma tsum_sum {f : Î³ â†’ Î² â†’ Î±} {s : finset Î³} (hf : âˆ€iâˆˆs, summable (f i)) :
  (âˆ‘'b, âˆ‘ i in s, f i b) = âˆ‘ i in s, âˆ‘'b, f i b :=
(has_sum_sum $ assume i hi, (hf i hi).has_sum).tsum_eq

lemma tsum_sigma' [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (hâ‚ : âˆ€b, summable (Î»c, f âŸ¨b, câŸ©)) (hâ‚‚ : summable f) : (âˆ‘'p, f p) = (âˆ‘'b c, f âŸ¨b, câŸ©) :=
(hâ‚‚.has_sum.sigma (assume b, (hâ‚ b).has_sum)).tsum_eq.symm

lemma tsum_prod' [regular_space Î±] {f : Î² Ã— Î³ â†’ Î±} (h : summable f)
  (hâ‚ : âˆ€b, summable (Î»c, f (b, c))) :
  (âˆ‘'p, f p) = (âˆ‘'b c, f (b, c)) :=
(h.has_sum.prod_fiberwise (assume b, (hâ‚ b).has_sum)).tsum_eq.symm

lemma tsum_comm' [regular_space Î±] {f : Î² â†’ Î³ â†’ Î±} (h : summable (function.uncurry f))
  (hâ‚ : âˆ€b, summable (f b)) (hâ‚‚ : âˆ€ c, summable (Î» b, f b c)) :
  (âˆ‘' c b, f b c) = (âˆ‘' b c, f b c) :=
begin
  erw [â† tsum_prod' h hâ‚, â† tsum_prod' h.prod_symm hâ‚‚, â† (equiv.prod_comm Î² Î³).tsum_eq],
  refl,
  assumption
end

end has_continuous_add

section encodable
open encodable
variable [encodable Î³]

/-- You can compute a sum over an encodably type by summing over the natural numbers and
  taking a supremum. This is useful for outer measures. -/
theorem tsum_supr_decode2 [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (s : Î³ â†’ Î²) : (âˆ‘' i : â„•, m (â¨† b âˆˆ decode2 Î³ i, s b)) = (âˆ‘' b : Î³, m (s b)) :=
begin
  have H : âˆ€ n, m (â¨† b âˆˆ decode2 Î³ n, s b) â‰  0 â†’ (decode2 Î³ n).is_some,
  { intros n h,
    cases decode2 Î³ n with b,
    { refine (h $ by simp [m0]).elim },
    { exact rfl } },
  symmetry, refine tsum_eq_tsum_of_ne_zero_bij (Î» a, option.get (H a.1 a.2)) _ _ _,
  { rintros âŸ¨m, hmâŸ© âŸ¨n, hnâŸ© e,
    have := mem_decode2.1 (option.get_mem (H n hn)),
    rwa [â† e, mem_decode2.1 (option.get_mem (H m hm))] at this },
  { intros b h,
    refine âŸ¨âŸ¨encode b, _âŸ©, _âŸ©,
    { simp only [mem_support, encodek2] at h âŠ¢, convert h, simp [set.ext_iff, encodek2] },
    { exact option.get_of_mem _ (encodek2 _) } },
  { rintros âŸ¨n, hâŸ©, dsimp only [subtype.coe_mk],
    transitivity, swap,
    rw [show decode2 Î³ n = _, from option.get_mem (H n h)],
    congr, simp [ext_iff, -option.some_get] }
end

/-- `tsum_supr_decode2` specialized to the complete lattice of sets. -/
theorem tsum_Union_decode2 (m : set Î² â†’ Î±) (m0 : m âˆ… = 0)
  (s : Î³ â†’ set Î²) : (âˆ‘' i, m (â‹ƒ b âˆˆ decode2 Î³ i, s b)) = (âˆ‘' b, m (s b)) :=
tsum_supr_decode2 m m0 s

/-! Some properties about measure-like functions.
  These could also be functions defined on complete sublattices of sets, with the property
  that they are countably sub-additive.
  `R` will probably be instantiated with `(â‰¤)` in all applications.
-/

/-- If a function is countably sub-additive then it is sub-additive on encodable types -/
theorem rel_supr_tsum [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) (âˆ‘' i, m (s i)))
  (s : Î³ â†’ Î²) : R (m (â¨† b : Î³, s b)) (âˆ‘' b : Î³, m (s b)) :=
by { rw [â† supr_decode2, â† tsum_supr_decode2 _ m0 s], exact m_supr _ }

/-- If a function is countably sub-additive then it is sub-additive on finite sets -/
theorem rel_supr_sum [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) (âˆ‘' i, m (s i)))
  (s : Î´ â†’ Î²) (t : finset Î´) :
  R (m (â¨† d âˆˆ t, s d)) (âˆ‘ d in t, m (s d)) :=
by { cases t.nonempty_encodable, rw [supr_subtype'], convert rel_supr_tsum m m0 R m_supr _,
     rw [â† finset.tsum_subtype], assumption }

/-- If a function is countably sub-additive then it is binary sub-additive -/
theorem rel_sup_add [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) (âˆ‘' i, m (s i)))
  (sâ‚ sâ‚‚ : Î²) : R (m (sâ‚ âŠ” sâ‚‚)) (m sâ‚ + m sâ‚‚) :=
begin
  convert rel_supr_tsum m m0 R m_supr (Î» b, cond b sâ‚ sâ‚‚),
  { simp only [supr_bool_eq, cond] },
  { rw tsum_fintype, simp only [finset.sum_insert, not_false_iff, fintype.univ_bool,
      finset.mem_singleton, cond, finset.sum_singleton] }
end

end encodable

end tsum

section topological_group
variables [add_comm_group Î±] [topological_space Î±] [topological_add_group Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

-- `by simpa using` speeds up elaboration. Why?
lemma has_sum.neg (h : has_sum f a) : has_sum (Î»b, - f b) (- a) :=
by simpa only using h.map (-add_monoid_hom.id Î±) continuous_neg

lemma summable.neg (hf : summable f) : summable (Î»b, - f b) :=
hf.has_sum.neg.summable

lemma has_sum.sub (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) : has_sum (Î»b, f b - g b) (aâ‚ - aâ‚‚) :=
by { simp [sub_eq_add_neg], exact hf.add hg.neg }

lemma summable.sub (hf : summable f) (hg : summable g) : summable (Î»b, f b - g b) :=
(hf.has_sum.sub hg.has_sum).summable

lemma has_sum.has_sum_compl_iff {s : set Î²} (hf : has_sum (f âˆ˜ coe : s â†’ Î±) aâ‚) :
  has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) aâ‚‚ â†” has_sum f (aâ‚ + aâ‚‚) :=
begin
  refine âŸ¨Î» h, hf.add_compl h, Î» h, _âŸ©,
  rw [has_sum_subtype_iff_indicator] at hf âŠ¢,
  rw [set.indicator_compl],
  simpa only [add_sub_cancel'] using h.sub hf
end

lemma has_sum.has_sum_iff_compl {s : set Î²} (hf : has_sum (f âˆ˜ coe : s â†’ Î±) aâ‚) :
  has_sum f aâ‚‚ â†” has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) (aâ‚‚ - aâ‚) :=
iff.symm $ hf.has_sum_compl_iff.trans $ by rw [add_sub_cancel'_right]

lemma summable.summable_compl_iff {s : set Î²} (hf : summable (f âˆ˜ coe : s â†’ Î±)) :
  summable (f âˆ˜ coe : sá¶œ â†’ Î±) â†” summable f :=
âŸ¨Î» âŸ¨a, haâŸ©, (hf.has_sum.has_sum_compl_iff.1 ha).summable,
  Î» âŸ¨a, haâŸ©, (hf.has_sum.has_sum_iff_compl.1 ha).summableâŸ©

protected lemma finset.has_sum_compl_iff (s : finset Î²) :
  has_sum (Î» x : {x // x âˆ‰ s}, f x) a â†” has_sum f (a + âˆ‘ i in s, f i) :=
(s.has_sum f).has_sum_compl_iff.trans $ by rw [add_comm]

protected lemma finset.has_sum_iff_compl (s : finset Î²) :
  has_sum f a â†” has_sum (Î» x : {x // x âˆ‰ s}, f x) (a - âˆ‘ i in s, f i) :=
(s.has_sum f).has_sum_iff_compl

protected lemma finset.summable_compl_iff (s : finset Î²) :
  summable (Î» x : {x // x âˆ‰ s}, f x) â†” summable f :=
(s.summable f).summable_compl_iff

lemma set.finite.summable_compl_iff {s : set Î²} (hs : s.finite) :
  summable (f âˆ˜ coe : sá¶œ â†’ Î±) â†” summable f :=
(hs.summable f).summable_compl_iff

section tsum
variables [t2_space Î±]

lemma tsum_neg (hf : summable f) : (âˆ‘'b, - f b) = - (âˆ‘'b, f b) :=
hf.has_sum.neg.tsum_eq

lemma tsum_sub (hf : summable f) (hg : summable g) : (âˆ‘'b, f b - g b) = (âˆ‘'b, f b) - (âˆ‘'b, g b) :=
(hf.has_sum.sub hg.has_sum).tsum_eq

lemma tsum_add_tsum_compl {s : set Î²} (hs : summable (f âˆ˜ coe : s â†’ Î±))
  (hsc : summable (f âˆ˜ coe : sá¶œ â†’ Î±)) :
  (âˆ‘' x : s, f x) + (âˆ‘' x : sá¶œ, f x) = âˆ‘' x, f x :=
(hs.has_sum.add_compl hsc.has_sum).tsum_eq.symm

lemma sum_add_tsum_compl {s : finset Î²} (hf : summable f) :
  (âˆ‘ x in s, f x) + (âˆ‘' x : (â†‘s : set Î²)á¶œ, f x) = âˆ‘' x, f x :=
((s.has_sum f).add_compl (s.summable_compl_iff.2 hf).has_sum).tsum_eq.symm

end tsum

/-!
### Sums on subtypes

If `s` is a finset of `Î±`, we show that the summability of `f` in the whole space and on the subtype
`univ - s` are equivalent, and relate their sums. For a function defined on `â„•`, we deduce the
formula `(âˆ‘ i in range k, f i) + (âˆ‘' i, f (i + k)) = (âˆ‘' i, f i)`, in `sum_add_tsum_nat_add`.
-/
section subtype
variables {s : finset Î²}

lemma has_sum_nat_add_iff {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
  has_sum (Î» n, f (n + k)) a â†” has_sum f (a + âˆ‘ i in range k, f i) :=
begin
  refine iff.trans _ ((range k).has_sum_compl_iff),
  rw [â† (not_mem_range_equiv k).symm.has_sum_iff],
  refl
end

lemma summable_nat_add_iff {f : â„• â†’ Î±} (k : â„•) : summable (Î» n, f (n + k)) â†” summable f :=
iff.symm $ (equiv.add_right (âˆ‘ i in range k, f i)).summable_iff_of_has_sum_iff $
  Î» a, (has_sum_nat_add_iff k).symm

lemma has_sum_nat_add_iff' {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
  has_sum (Î» n, f (n + k)) (a - âˆ‘ i in range k, f i) â†” has_sum f a :=
by simp [has_sum_nat_add_iff]

lemma sum_add_tsum_nat_add [t2_space Î±] {f : â„• â†’ Î±} (k : â„•) (h : summable f) :
  (âˆ‘ i in range k, f i) + (âˆ‘' i, f (i + k)) = (âˆ‘' i, f i) :=
by simpa [add_comm] using
  ((has_sum_nat_add_iff k).1 ((summable_nat_add_iff k).2 h).has_sum).unique h.has_sum

lemma tsum_eq_zero_add [t2_space Î±] {f : â„• â†’ Î±} (hf : summable f) :
  (âˆ‘'b, f b) = f 0 + (âˆ‘'b, f (b + 1)) :=
by simpa only [range_one, sum_singleton] using (sum_add_tsum_nat_add 1 hf).symm

end subtype

end topological_group

section topological_semiring
variables [semiring Î±] [topological_space Î±] [topological_semiring Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}
lemma has_sum.mul_left (aâ‚‚) (h : has_sum f aâ‚) : has_sum (Î»b, aâ‚‚ * f b) (aâ‚‚ * aâ‚) :=
by simpa only using h.map (add_monoid_hom.mul_left aâ‚‚) (continuous_const.mul continuous_id)

lemma has_sum.mul_right (aâ‚‚) (hf : has_sum f aâ‚) : has_sum (Î»b, f b * aâ‚‚) (aâ‚ * aâ‚‚) :=
by simpa only using hf.map (add_monoid_hom.mul_right aâ‚‚) (continuous_id.mul continuous_const)

lemma summable.mul_left (a) (hf : summable f) : summable (Î»b, a * f b) :=
(hf.has_sum.mul_left _).summable

lemma summable.mul_right (a) (hf : summable f) : summable (Î»b, f b * a) :=
(hf.has_sum.mul_right _).summable

section tsum
variables [t2_space Î±]

lemma tsum_mul_left (a) (hf : summable f) : (âˆ‘'b, a * f b) = a * (âˆ‘'b, f b) :=
(hf.has_sum.mul_left _).tsum_eq

lemma tsum_mul_right (a) (hf : summable f) : (âˆ‘'b, f b * a) = (âˆ‘'b, f b) * a :=
(hf.has_sum.mul_right _).tsum_eq

end tsum

end topological_semiring

section division_ring

variables [division_ring Î±] [topological_space Î±] [topological_semiring Î±]
{f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma has_sum_mul_left_iff (h : aâ‚‚ â‰  0) : has_sum f aâ‚ â†” has_sum (Î»b, aâ‚‚ * f b) (aâ‚‚ * aâ‚) :=
âŸ¨has_sum.mul_left _, Î» H, by simpa only [inv_mul_cancel_left' h] using H.mul_left aâ‚‚â»Â¹âŸ©

lemma has_sum_mul_right_iff (h : aâ‚‚ â‰  0) : has_sum f aâ‚ â†” has_sum (Î»b, f b * aâ‚‚) (aâ‚ * aâ‚‚) :=
âŸ¨has_sum.mul_right _, Î» H, by simpa only [mul_inv_cancel_right' h] using H.mul_right aâ‚‚â»Â¹âŸ©

lemma summable_mul_left_iff (h : a â‰  0) : summable f â†” summable (Î»b, a * f b) :=
âŸ¨Î» H, H.mul_left _, Î» H, by simpa only [inv_mul_cancel_left' h] using H.mul_left aâ»Â¹âŸ©

lemma summable_mul_right_iff (h : a â‰  0) : summable f â†” summable (Î»b, f b * a) :=
âŸ¨Î» H, H.mul_right _, Î» H, by simpa only [mul_inv_cancel_right' h] using H.mul_right aâ»Â¹âŸ©

end division_ring

section order_topology
variables [ordered_add_comm_monoid Î±] [topological_space Î±] [order_closed_topology Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma has_sum_le (h : âˆ€b, f b â‰¤ g b) (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) : aâ‚ â‰¤ aâ‚‚ :=
le_of_tendsto_of_tendsto' hf hg $ assume s, sum_le_sum $ assume b _, h b

lemma has_sum_le_inj {g : Î³ â†’ Î±} (i : Î² â†’ Î³) (hi : injective i) (hs : âˆ€câˆ‰set.range i, 0 â‰¤ g c)
  (h : âˆ€b, f b â‰¤ g (i b)) (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) : aâ‚ â‰¤ aâ‚‚ :=
have has_sum (Î»c, (partial_inv i c).cases_on' 0 f) aâ‚,
begin
  refine (has_sum_iff_has_sum_of_ne_zero_bij (i âˆ˜ coe) _ _ _).2 hf,
  { exact assume câ‚ câ‚‚ eq, hi eq },
  { intros c hc,
    rw [mem_support] at hc,
    cases eq : partial_inv i c with b; rw eq at hc,
    { contradiction },
    { rw [partial_inv_of_injective hi] at eq,
      exact âŸ¨âŸ¨b, hcâŸ©, eqâŸ© } },
  { assume c, simp [partial_inv_left hi, option.cases_on'] }
end,
begin
  refine has_sum_le (assume c, _) this hg,
  by_cases c âˆˆ set.range i,
  { rcases h with âŸ¨b, rflâŸ©,
    rw [partial_inv_left hi, option.cases_on'],
    exact h _ },
  { have : partial_inv i c = none := dif_neg h,
    rw [this, option.cases_on'],
    exact hs _ h }
end

lemma tsum_le_tsum_of_inj {g : Î³ â†’ Î±} (i : Î² â†’ Î³) (hi : injective i) (hs : âˆ€câˆ‰set.range i, 0 â‰¤ g c)
  (h : âˆ€b, f b â‰¤ g (i b)) (hf : summable f) (hg : summable g) : tsum f â‰¤ tsum g :=
has_sum_le_inj i hi hs h hf.has_sum hg.has_sum

lemma sum_le_has_sum {f : Î² â†’ Î±} (s : finset Î²) (hs : âˆ€ bâˆ‰s, 0 â‰¤ f b) (hf : has_sum f a) :
  âˆ‘ b in s, f b â‰¤ a :=
ge_of_tendsto hf (eventually_at_top.2 âŸ¨s, Î» t hst,
  sum_le_sum_of_subset_of_nonneg hst $ Î» b hbt hbs, hs b hbsâŸ©)

lemma sum_le_tsum {f : Î² â†’ Î±} (s : finset Î²) (hs : âˆ€ bâˆ‰s, 0 â‰¤ f b) (hf : summable f) :
  âˆ‘ b in s, f b â‰¤ tsum f :=
sum_le_has_sum s hs hf.has_sum

lemma tsum_le_tsum (h : âˆ€b, f b â‰¤ g b) (hf : summable f) (hg : summable g) : (âˆ‘'b, f b) â‰¤ (âˆ‘'b, g b) :=
has_sum_le h hf.has_sum hg.has_sum

lemma tsum_nonneg (h : âˆ€ b, 0 â‰¤ g b) : 0 â‰¤ (âˆ‘'b, g b) :=
begin
  by_cases hg : summable g,
  { simpa using tsum_le_tsum h summable_zero hg },
  { simp [tsum_eq_zero_of_not_summable hg] }
end

lemma tsum_nonpos (h : âˆ€ b, f b â‰¤ 0) : (âˆ‘'b, f b) â‰¤ 0 :=
begin
  by_cases hf : summable f,
  { simpa using tsum_le_tsum h hf summable_zero},
  { simp [tsum_eq_zero_of_not_summable hf] }
end

end order_topology

section uniform_group

variables [add_comm_group Î±] [uniform_space Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma summable_iff_cauchy_seq_finset [complete_space Î±] :
  summable f â†” cauchy_seq (Î» (s : finset Î²), âˆ‘ b in s, f b) :=
cauchy_map_iff_exists_tendsto.symm

variable [uniform_add_group Î±]

lemma cauchy_seq_finset_iff_vanishing :
  cauchy_seq (Î» (s : finset Î²), âˆ‘ b in s, f b)
  â†” âˆ€ e âˆˆ ğ“ (0:Î±), (âˆƒs:finset Î², âˆ€t, disjoint t s â†’ âˆ‘ b in t, f b âˆˆ e) :=
begin
  simp only [cauchy_seq, cauchy_map_iff, and_iff_right at_top_ne_bot,
    prod_at_top_at_top_eq, uniformity_eq_comap_nhds_zero Î±, tendsto_comap_iff, (âˆ˜)],
  rw [tendsto_at_top' (_ : finset Î² Ã— finset Î² â†’ Î±)],
  split,
  { assume h e he,
    rcases h e he with âŸ¨âŸ¨sâ‚, sâ‚‚âŸ©, hâŸ©,
    use [sâ‚ âˆª sâ‚‚],
    assume t ht,
    specialize h (sâ‚ âˆª sâ‚‚, (sâ‚ âˆª sâ‚‚) âˆª t) âŸ¨le_sup_left, le_sup_left_of_le le_sup_rightâŸ©,
    simpa only [finset.sum_union ht.symm, add_sub_cancel'] using h },
  { assume h e he,
    rcases exists_nhds_half_neg he with âŸ¨d, hd, hdeâŸ©,
    rcases h d hd with âŸ¨s, hâŸ©,
    use [(s, s)],
    rintros âŸ¨tâ‚, tâ‚‚âŸ© âŸ¨htâ‚, htâ‚‚âŸ©,
    have : âˆ‘ b in tâ‚‚, f b - âˆ‘ b in tâ‚, f b = âˆ‘ b in tâ‚‚ \ s, f b - âˆ‘ b in tâ‚ \ s, f b,
    { simp only [(finset.sum_sdiff htâ‚).symm, (finset.sum_sdiff htâ‚‚).symm,
        add_sub_add_right_eq_sub] },
    simp only [this],
    exact hde _ _ (h _ finset.sdiff_disjoint) (h _ finset.sdiff_disjoint) }
end

variable [complete_space Î±]

lemma summable_iff_vanishing :
  summable f â†” âˆ€ e âˆˆ ğ“ (0:Î±), (âˆƒs:finset Î², âˆ€t, disjoint t s â†’ âˆ‘ b in t, f b âˆˆ e) :=
by rw [summable_iff_cauchy_seq_finset, cauchy_seq_finset_iff_vanishing]

/- TODO: generalize to monoid with a uniform continuous subtraction operator: `(a + b) - b = a` -/
lemma summable.summable_of_eq_zero_or_self (hf : summable f) (h : âˆ€b, g b = 0 âˆ¨ g b = f b) :
  summable g :=
summable_iff_vanishing.2 $
  assume e he,
  let âŸ¨s, hsâŸ© := summable_iff_vanishing.1 hf e he in
  âŸ¨s, assume t ht,
    have eq : âˆ‘ b in t.filter (Î»b, g b = f b), f b = âˆ‘ b in t, g b :=
      calc âˆ‘ b in t.filter (Î»b, g b = f b), f b = âˆ‘ b in t.filter (Î»b, g b = f b), g b :
          finset.sum_congr rfl (assume b hb, (finset.mem_filter.1 hb).2.symm)
        ... = âˆ‘ b in t, g b :
        begin
          refine finset.sum_subset (finset.filter_subset _) _,
          assume b hbt hb,
          simp only [(âˆ‰), finset.mem_filter, and_iff_right hbt] at hb,
          exact (h b).resolve_right hb
        end,
    eq â–¸ hs _ $ finset.disjoint_of_subset_left (finset.filter_subset _) htâŸ©

protected lemma summable.indicator (hf : summable f) (s : set Î²) :
  summable (s.indicator f) :=
hf.summable_of_eq_zero_or_self $ Î» b,
  if hb : b âˆˆ s then or.inr (set.indicator_of_mem hb _)
  else or.inl (set.indicator_of_not_mem hb _)

lemma summable.comp_injective {i : Î³ â†’ Î²} (hf : summable f) (hi : injective i) :
  summable (f âˆ˜ i) :=
begin
  simpa only [set.indicator_range_comp]
    using (hi.summable_iff _).2 (hf.indicator (set.range i)),
  exact Î» x hx, set.indicator_of_not_mem hx _
end

lemma summable.subtype (hf : summable f) (s : set Î²) : summable (f âˆ˜ coe : s â†’ Î±) :=
hf.comp_injective subtype.coe_injective

lemma summable.sigma_factor {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) (b : Î²) : summable (Î»c, f âŸ¨b, câŸ©) :=
ha.comp_injective sigma_mk_injective

lemma summable.sigma [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) : summable (Î»b, âˆ‘'c, f âŸ¨b, câŸ©) :=
ha.sigma' (Î» b, ha.sigma_factor b)

lemma summable.prod_factor {f : Î² Ã— Î³ â†’ Î±} (h : summable f) (b : Î²) :
  summable (Î» c, f (b, c)) :=
h.comp_injective $ Î» câ‚ câ‚‚ h, (prod.ext_iff.1 h).2

lemma tsum_sigma [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) : (âˆ‘'p, f p) = (âˆ‘'b c, f âŸ¨b, câŸ©) :=
tsum_sigma' (Î» b, ha.sigma_factor b) ha

lemma tsum_prod [regular_space Î±] {f : Î² Ã— Î³ â†’ Î±} (h : summable f) :
  (âˆ‘'p, f p) = (âˆ‘'b c, f âŸ¨b, câŸ©) :=
tsum_prod' h h.prod_factor

lemma tsum_comm [regular_space Î±] {f : Î² â†’ Î³ â†’ Î±} (h : summable (function.uncurry f)) :
  (âˆ‘' c b, f b c) = (âˆ‘' b c, f b c) :=
tsum_comm' h h.prod_factor h.prod_symm.prod_factor

end uniform_group

section cauchy_seq
open finset.Ico filter

/-- If the extended distance between consequent points of a sequence is estimated
by a summable series of `nnreal`s, then the original sequence is a Cauchy sequence. -/
lemma cauchy_seq_of_edist_le_of_summable [emetric_space Î±] {f : â„• â†’ Î±} (d : â„• â†’ nnreal)
  (hf : âˆ€ n, edist (f n) (f n.succ) â‰¤ d n) (hd : summable d) : cauchy_seq f :=
begin
  refine emetric.cauchy_seq_iff_nnreal.2 (Î» Îµ Îµpos, _),
  -- Actually we need partial sums of `d` to be a Cauchy sequence
  replace hd : cauchy_seq (Î» (n : â„•), âˆ‘ x in range n, d x) :=
    let âŸ¨_, HâŸ© := hd in cauchy_seq_of_tendsto_nhds _ H.tendsto_sum_nat,
  -- Now we take the same `N` as in one of the definitions of a Cauchy sequence
  refine (metric.cauchy_seq_iff'.1 hd Îµ (nnreal.coe_pos.2 Îµpos)).imp (Î» N hN n hn, _),
  have hsum := hN n hn,
  -- We simplify the known inequality
  rw [dist_nndist, nnreal.nndist_eq, â† sum_range_add_sum_Ico _ hn, nnreal.add_sub_cancel'] at hsum,
  norm_cast at hsum,
  replace hsum := lt_of_le_of_lt (le_max_left _ _) hsum,
  rw edist_comm,

  -- Then use `hf` to simplify the goal to the same form
  apply lt_of_le_of_lt (edist_le_Ico_sum_of_edist_le hn (Î» k _ _, hf k)),
  assumption_mod_cast
end

/-- If the distance between consequent points of a sequence is estimated by a summable series,
then the original sequence is a Cauchy sequence. -/
lemma cauchy_seq_of_dist_le_of_summable [metric_space Î±] {f : â„• â†’ Î±} (d : â„• â†’ â„)
  (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n) (hd : summable d) : cauchy_seq f :=
begin
  refine metric.cauchy_seq_iff'.2 (Î»Îµ Îµpos, _),
  replace hd : cauchy_seq (Î» (n : â„•), âˆ‘ x in range n, d x) :=
    let âŸ¨_, HâŸ© := hd in cauchy_seq_of_tendsto_nhds _ H.tendsto_sum_nat,
  refine (metric.cauchy_seq_iff'.1 hd Îµ Îµpos).imp (Î» N hN n hn, _),
  have hsum := hN n hn,
  rw [real.dist_eq, â† sum_Ico_eq_sub _ hn] at hsum,
  calc dist (f n) (f N) = dist (f N) (f n) : dist_comm _ _
  ... â‰¤ âˆ‘ x in Ico N n, d x : dist_le_Ico_sum_of_dist_le hn (Î» k _ _, hf k)
  ... â‰¤ abs (âˆ‘ x in Ico N n, d x) : le_abs_self _
  ... < Îµ : hsum
end

lemma cauchy_seq_of_summable_dist [metric_space Î±] {f : â„• â†’ Î±}
  (h : summable (Î»n, dist (f n) (f n.succ))) : cauchy_seq f :=
cauchy_seq_of_dist_le_of_summable _ (Î» _, le_refl _) h

lemma dist_le_tsum_of_dist_le_of_tendsto [metric_space Î±] {f : â„• â†’ Î±} (d : â„• â†’ â„)
  (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n) (hd : summable d) {a : Î±} (ha : tendsto f at_top (ğ“ a))
  (n : â„•) :
  dist (f n) a â‰¤ âˆ‘' m, d (n + m) :=
begin
  refine le_of_tendsto (tendsto_const_nhds.dist ha)
    (eventually_at_top.2 âŸ¨n, Î» m hnm, _âŸ©),
  refine le_trans (dist_le_Ico_sum_of_dist_le hnm (Î» k _ _, hf k)) _,
  rw [sum_Ico_eq_sum_range],
  refine sum_le_tsum (range _) (Î» _ _, le_trans dist_nonneg (hf _)) _,
  exact hd.comp_injective (add_right_injective n)
end

lemma dist_le_tsum_of_dist_le_of_tendstoâ‚€ [metric_space Î±] {f : â„• â†’ Î±} (d : â„• â†’ â„)
  (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n) (hd : summable d) {a : Î±} (ha : tendsto f at_top (ğ“ a)) :
  dist (f 0) a â‰¤ tsum d :=
by simpa only [zero_add] using dist_le_tsum_of_dist_le_of_tendsto d hf hd ha 0

lemma dist_le_tsum_dist_of_tendsto [metric_space Î±] {f : â„• â†’ Î±}
  (h : summable (Î»n, dist (f n) (f n.succ))) {a : Î±} (ha : tendsto f at_top (ğ“ a)) (n) :
  dist (f n) a â‰¤ âˆ‘' m, dist (f (n+m)) (f (n+m).succ) :=
show dist (f n) a â‰¤ âˆ‘' m, (Î»x, dist (f x) (f x.succ)) (n + m), from
dist_le_tsum_of_dist_le_of_tendsto (Î» n, dist (f n) (f n.succ)) (Î» _, le_refl _) h ha n

lemma dist_le_tsum_dist_of_tendstoâ‚€ [metric_space Î±] {f : â„• â†’ Î±}
  (h : summable (Î»n, dist (f n) (f n.succ))) {a : Î±} (ha : tendsto f at_top (ğ“ a)) :
  dist (f 0) a â‰¤ âˆ‘' n, dist (f n) (f n.succ) :=
by simpa only [zero_add] using dist_le_tsum_dist_of_tendsto h ha 0

end cauchy_seq
