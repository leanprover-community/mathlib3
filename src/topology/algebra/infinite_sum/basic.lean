/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import data.nat.parity
import logic.encodable.lattice
import topology.algebra.uniform_group
import topology.algebra.star

/-!
# Infinite sum over a topological monoid

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This sum is known as unconditionally convergent, as it sums to the same value under all possible
permutations. For Euclidean spaces (finite dimensional Banach spaces) this is equivalent to absolute
convergence.

Note: There are summable sequences which are not unconditionally convergent! The other way holds
generally, see `has_sum.tendsto_sum_nat`.

## References

* Bourbaki: General Topology (1995), Chapter 3 Â§5 (Infinite sums in commutative groups)

-/

noncomputable theory
open classical filter finset function
open_locale big_operators classical topology

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

section has_sum
variables [add_comm_monoid Î±] [topological_space Î±]

/-- Infinite sum on a topological monoid

The `at_top` filter on `finset Î²` is the limit of all finite sets towards the entire type. So we sum
up bigger and bigger sets. This sum operation is invariant under reordering. In particular,
the function `â„• â†’ â„` sending `n` to `(-1)^n / (n+1)` does not have a
sum for this definition, but a series which is absolutely convergent will have the correct sum.

This is based on Mario Carneiro's
[infinite sum `df-tsms` in Metamath](http://us.metamath.org/mpeuni/df-tsms.html).

For the definition or many statements, `Î±` does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant.
-/
def has_sum (f : Î² â†’ Î±) (a : Î±) : Prop := tendsto (Î»s:finset Î², âˆ‘ b in s, f b) at_top (ğ“ a)

/-- `summable f` means that `f` has some (infinite) sum. Use `tsum` to get the value. -/
def summable (f : Î² â†’ Î±) : Prop := âˆƒa, has_sum f a

/-- `âˆ‘' i, f i` is the sum of `f` it exists, or 0 otherwise -/
@[irreducible] def tsum {Î²} (f : Î² â†’ Î±) := if h : summable f then classical.some h else 0

-- see Note [operator precedence of big operators]
notation `âˆ‘'` binders `, ` r:(scoped:67 f, tsum f) := r

variables {f g : Î² â†’ Î±} {a b : Î±} {s : finset Î²}

lemma summable.has_sum (ha : summable f) : has_sum f (âˆ‘'b, f b) :=
by simp [ha, tsum]; exact some_spec ha

lemma has_sum.summable (h : has_sum f a) : summable f := âŸ¨a, hâŸ©

/-- Constant zero function has sum `0` -/
lemma has_sum_zero : has_sum (Î»b, 0 : Î² â†’ Î±) 0 :=
by simp [has_sum, tendsto_const_nhds]

lemma has_sum_empty [is_empty Î²] : has_sum f 0 :=
by convert has_sum_zero

lemma summable_zero : summable (Î»b, 0 : Î² â†’ Î±) := has_sum_zero.summable

lemma summable_empty [is_empty Î²] : summable f := has_sum_empty.summable

lemma tsum_eq_zero_of_not_summable (h : Â¬ summable f) : âˆ‘'b, f b = 0 :=
by simp [tsum, h]

lemma summable_congr (hfg : âˆ€b, f b = g b) :
  summable f â†” summable g :=
iff_of_eq (congr_arg summable $ funext hfg)

lemma summable.congr (hf : summable f) (hfg : âˆ€b, f b = g b) :
  summable g :=
(summable_congr hfg).mp hf

lemma has_sum.has_sum_of_sum_eq {g : Î³ â†’ Î±}
  (h_eq : âˆ€u:finset Î³, âˆƒv:finset Î², âˆ€v', v âŠ† v' â†’ âˆƒu', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
  (hf : has_sum g a) :
  has_sum f a :=
le_trans (map_at_top_finset_sum_le_of_sum_eq h_eq) hf

lemma has_sum_iff_has_sum {g : Î³ â†’ Î±}
  (hâ‚ : âˆ€u:finset Î³, âˆƒv:finset Î², âˆ€v', v âŠ† v' â†’ âˆƒu', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
  (hâ‚‚ : âˆ€v:finset Î², âˆƒu:finset Î³, âˆ€u', u âŠ† u' â†’ âˆƒv', v âŠ† v' âˆ§ âˆ‘ b in v', f b = âˆ‘ x in u', g x) :
  has_sum f a â†” has_sum g a :=
âŸ¨has_sum.has_sum_of_sum_eq hâ‚‚, has_sum.has_sum_of_sum_eq hâ‚âŸ©

lemma function.injective.has_sum_iff {g : Î³ â†’ Î²} (hg : injective g)
  (hf : âˆ€ x âˆ‰ set.range g, f x = 0) :
  has_sum (f âˆ˜ g) a â†” has_sum f a :=
by simp only [has_sum, tendsto, hg.map_at_top_finset_sum_eq hf]

lemma function.injective.summable_iff {g : Î³ â†’ Î²} (hg : injective g)
  (hf : âˆ€ x âˆ‰ set.range g, f x = 0) :
  summable (f âˆ˜ g) â†” summable f :=
exists_congr $ Î» _, hg.has_sum_iff hf

lemma has_sum_subtype_iff_of_support_subset {s : set Î²} (hf : support f âŠ† s) :
  has_sum (f âˆ˜ coe : s â†’ Î±) a â†” has_sum f a :=
subtype.coe_injective.has_sum_iff $ by simpa using support_subset_iff'.1 hf

lemma has_sum_subtype_iff_indicator {s : set Î²} :
  has_sum (f âˆ˜ coe : s â†’ Î±) a â†” has_sum (s.indicator f) a :=
by rw [â† set.indicator_range_comp, subtype.range_coe,
  has_sum_subtype_iff_of_support_subset set.support_indicator_subset]

lemma summable_subtype_iff_indicator {s : set Î²} :
  summable (f âˆ˜ coe : s â†’ Î±) â†” summable (s.indicator f) :=
exists_congr (Î» _, has_sum_subtype_iff_indicator)

@[simp] lemma has_sum_subtype_support : has_sum (f âˆ˜ coe : support f â†’ Î±) a â†” has_sum f a :=
has_sum_subtype_iff_of_support_subset $ set.subset.refl _

lemma has_sum_fintype [fintype Î²] (f : Î² â†’ Î±) : has_sum f (âˆ‘ b, f b) :=
order_top.tendsto_at_top_nhds _

protected lemma finset.has_sum (s : finset Î²) (f : Î² â†’ Î±) :
  has_sum (f âˆ˜ coe : (â†‘s : set Î²) â†’ Î±) (âˆ‘ b in s, f b) :=
by { rw â† sum_attach, exact has_sum_fintype _ }

protected lemma finset.summable (s : finset Î²) (f : Î² â†’ Î±) :
  summable (f âˆ˜ coe : (â†‘s : set Î²) â†’ Î±) :=
(s.has_sum f).summable

protected lemma set.finite.summable {s : set Î²} (hs : s.finite) (f : Î² â†’ Î±) :
  summable (f âˆ˜ coe : s â†’ Î±) :=
by convert hs.to_finset.summable f; simp only [hs.coe_to_finset]

/-- If a function `f` vanishes outside of a finite set `s`, then it `has_sum` `âˆ‘ b in s, f b`. -/
lemma has_sum_sum_of_ne_finset_zero (hf : âˆ€bâˆ‰s, f b = 0) : has_sum f (âˆ‘ b in s, f b) :=
(has_sum_subtype_iff_of_support_subset $ support_subset_iff'.2 hf).1 $ s.has_sum f

lemma summable_of_ne_finset_zero (hf : âˆ€bâˆ‰s, f b = 0) : summable f :=
(has_sum_sum_of_ne_finset_zero hf).summable

lemma has_sum_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€b' â‰  b, f b' = 0) :
  has_sum f (f b) :=
suffices has_sum f (âˆ‘ b' in {b}, f b'),
  by simpa using this,
has_sum_sum_of_ne_finset_zero $ by simpa [hf]

lemma has_sum_ite_eq (b : Î²) [decidable_pred (= b)] (a : Î±) :
  has_sum (Î»b', if b' = b then a else 0) a :=
begin
  convert has_sum_single b _,
  { exact (if_pos rfl).symm },
  assume b' hb',
  exact if_neg hb'
end

lemma has_sum_pi_single [decidable_eq Î²] (b : Î²) (a : Î±) :
  has_sum (pi.single b a) a :=
show has_sum (Î» x, pi.single b a x) a, by simpa only [pi.single_apply] using has_sum_ite_eq b a

lemma equiv.has_sum_iff (e : Î³ â‰ƒ Î²) :
  has_sum (f âˆ˜ e) a â†” has_sum f a :=
e.injective.has_sum_iff $ by simp

lemma function.injective.has_sum_range_iff {g : Î³ â†’ Î²} (hg : injective g) :
  has_sum (Î» x : set.range g, f x) a â†” has_sum (f âˆ˜ g) a :=
(equiv.of_injective g hg).has_sum_iff.symm

lemma equiv.summable_iff (e : Î³ â‰ƒ Î²) :
  summable (f âˆ˜ e) â†” summable f :=
exists_congr $ Î» a, e.has_sum_iff

lemma summable.prod_symm {f : Î² Ã— Î³ â†’ Î±} (hf : summable f) : summable (Î» p : Î³ Ã— Î², f p.swap) :=
(equiv.prod_comm Î³ Î²).summable_iff.2 hf

lemma equiv.has_sum_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x : support f, g (e x) = f x) :
  has_sum f a â†” has_sum g a :=
have (g âˆ˜ coe) âˆ˜ e = f âˆ˜ coe, from funext he,
by rw [â† has_sum_subtype_support, â† this, e.has_sum_iff, has_sum_subtype_support]

lemma has_sum_iff_has_sum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
  (hi : âˆ€ â¦ƒx yâ¦„, i x = i y â†’ (x : Î³) = y)
  (hf : support f âŠ† set.range i) (hfg : âˆ€ x, f (i x) = g x) :
  has_sum f a â†” has_sum g a :=
iff.symm $ equiv.has_sum_iff_of_support
  (equiv.of_bijective (Î» x, âŸ¨i x, Î» hx, x.coe_prop $ hfg x â–¸ hxâŸ©)
    âŸ¨Î» x y h, subtype.ext $ hi $ subtype.ext_iff.1 h,
      Î» y, (hf y.coe_prop).imp $ Î» x hx, subtype.ext hxâŸ©)
  hfg

lemma equiv.summable_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x : support f, g (e x) = f x) :
  summable f â†” summable g :=
exists_congr $ Î» _, e.has_sum_iff_of_support he

protected lemma has_sum.map [add_comm_monoid Î³] [topological_space Î³] (hf : has_sum f a)
  {G} [add_monoid_hom_class G Î± Î³] (g : G) (hg : continuous g) :
  has_sum (g âˆ˜ f) (g a) :=
have g âˆ˜ (Î»s:finset Î², âˆ‘ b in s, f b) = (Î»s:finset Î², âˆ‘ b in s, g (f b)),
  from funext $ map_sum g _,
show tendsto (Î»s:finset Î², âˆ‘ b in s, g (f b)) at_top (ğ“ (g a)),
  from this â–¸ (hg.tendsto a).comp hf

protected lemma summable.map [add_comm_monoid Î³] [topological_space Î³] (hf : summable f)
  {G} [add_monoid_hom_class G Î± Î³] (g : G) (hg : continuous g) :
  summable (g âˆ˜ f) :=
(hf.has_sum.map g hg).summable

protected lemma summable.map_iff_of_left_inverse [add_comm_monoid Î³] [topological_space Î³]
  {G G'} [add_monoid_hom_class G Î± Î³] [add_monoid_hom_class G' Î³ Î±] (g : G) (g' : G')
  (hg : continuous g) (hg' : continuous g') (hinv : function.left_inverse g' g) :
  summable (g âˆ˜ f) â†” summable f :=
âŸ¨Î» h, begin
  have := h.map _ hg',
  rwa [â†function.comp.assoc, hinv.id] at this,
end, Î» h, h.map _ hgâŸ©

/-- A special case of `summable.map_iff_of_left_inverse` for convenience -/
protected lemma summable.map_iff_of_equiv [add_comm_monoid Î³] [topological_space Î³]
  {G} [add_equiv_class G Î± Î³] (g : G)
  (hg : continuous g) (hg' : continuous (add_equiv_class.inv g : Î³ â†’ Î±)) :
  summable (g âˆ˜ f) â†” summable f :=
summable.map_iff_of_left_inverse g (g : Î± â‰ƒ+ Î³).symm hg hg' (add_equiv_class.left_inv g)

/-- If `f : â„• â†’ Î±` has sum `a`, then the partial sums `âˆ‘_{i=0}^{n-1} f i` converge to `a`. -/
lemma has_sum.tendsto_sum_nat {f : â„• â†’ Î±} (h : has_sum f a) :
  tendsto (Î»n:â„•, âˆ‘ i in range n, f i) at_top (ğ“ a) :=
h.comp tendsto_finset_range

lemma has_sum.unique {aâ‚ aâ‚‚ : Î±} [t2_space Î±] : has_sum f aâ‚ â†’ has_sum f aâ‚‚ â†’ aâ‚ = aâ‚‚ :=
tendsto_nhds_unique

lemma summable.has_sum_iff_tendsto_nat [t2_space Î±] {f : â„• â†’ Î±} {a : Î±} (hf : summable f) :
  has_sum f a â†” tendsto (Î»n:â„•, âˆ‘ i in range n, f i) at_top (ğ“ a) :=
begin
  refine âŸ¨Î» h, h.tendsto_sum_nat, Î» h, _âŸ©,
  rw tendsto_nhds_unique h hf.has_sum.tendsto_sum_nat,
  exact hf.has_sum
end

lemma function.surjective.summable_iff_of_has_sum_iff {Î±' : Type*} [add_comm_monoid Î±']
  [topological_space Î±'] {e : Î±' â†’ Î±} (hes : function.surjective e) {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
  (he : âˆ€ {a}, has_sum f (e a) â†” has_sum g a) :
  summable f â†” summable g :=
hes.exists.trans $ exists_congr $ @he

variable [has_continuous_add Î±]

lemma has_sum.add (hf : has_sum f a) (hg : has_sum g b) : has_sum (Î»b, f b + g b) (a + b) :=
by simp only [has_sum, sum_add_distrib]; exact hf.add hg

lemma summable.add (hf : summable f) (hg : summable g) : summable (Î»b, f b + g b) :=
(hf.has_sum.add hg.has_sum).summable

lemma has_sum_sum {f : Î³ â†’ Î² â†’ Î±} {a : Î³ â†’ Î±} {s : finset Î³} :
  (âˆ€iâˆˆs, has_sum (f i) (a i)) â†’ has_sum (Î»b, âˆ‘ i in s, f i b) (âˆ‘ i in s, a i) :=
finset.induction_on s (by simp only [has_sum_zero, sum_empty, forall_true_iff])
  (by simp only [has_sum.add, sum_insert, mem_insert, forall_eq_or_imp,
        forall_2_true_iff, not_false_iff, forall_true_iff] {contextual := tt})

lemma summable_sum {f : Î³ â†’ Î² â†’ Î±} {s : finset Î³} (hf : âˆ€iâˆˆs, summable (f i)) :
  summable (Î»b, âˆ‘ i in s, f i b) :=
(has_sum_sum $ assume i hi, (hf i hi).has_sum).summable

lemma has_sum.add_disjoint {s t : set Î²} (hs : disjoint s t)
  (ha : has_sum (f âˆ˜ coe : s â†’ Î±) a) (hb : has_sum (f âˆ˜ coe : t â†’ Î±) b) :
  has_sum (f âˆ˜ coe : s âˆª t â†’ Î±) (a + b) :=
begin
  rw has_sum_subtype_iff_indicator at *,
  rw set.indicator_union_of_disjoint hs,
  exact ha.add hb
end

lemma has_sum_sum_disjoint {Î¹} (s : finset Î¹) {t : Î¹ â†’ set Î²} {a : Î¹ â†’ Î±}
  (hs : (s : set Î¹).pairwise (disjoint on t))
  (hf : âˆ€ i âˆˆ s, has_sum (f âˆ˜ coe : t i â†’ Î±) (a i)) :
  has_sum (f âˆ˜ coe : (â‹ƒ i âˆˆ s, t i) â†’ Î±) (âˆ‘ i in s, a i) :=
begin
  simp_rw has_sum_subtype_iff_indicator at *,
  rw set.indicator_finset_bUnion _ _ hs,
  exact has_sum_sum hf,
end

lemma has_sum.add_is_compl {s t : set Î²} (hs : is_compl s t)
  (ha : has_sum (f âˆ˜ coe : s â†’ Î±) a) (hb : has_sum (f âˆ˜ coe : t â†’ Î±) b) :
  has_sum f (a + b) :=
by simpa [â† hs.compl_eq]
  using (has_sum_subtype_iff_indicator.1 ha).add (has_sum_subtype_iff_indicator.1 hb)

lemma has_sum.add_compl {s : set Î²} (ha : has_sum (f âˆ˜ coe : s â†’ Î±) a)
  (hb : has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) b) :
  has_sum f (a + b) :=
ha.add_is_compl is_compl_compl hb

lemma summable.add_compl {s : set Î²} (hs : summable (f âˆ˜ coe : s â†’ Î±))
  (hsc : summable (f âˆ˜ coe : sá¶œ â†’ Î±)) :
  summable f :=
(hs.has_sum.add_compl hsc.has_sum).summable

lemma has_sum.compl_add {s : set Î²} (ha : has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) a)
  (hb : has_sum (f âˆ˜ coe : s â†’ Î±) b) :
  has_sum f (a + b) :=
ha.add_is_compl is_compl_compl.symm hb

lemma has_sum.even_add_odd {f : â„• â†’ Î±} (he : has_sum (Î» k, f (2 * k)) a)
  (ho : has_sum (Î» k, f (2 * k + 1)) b) :
  has_sum f (a + b) :=
begin
  have := mul_right_injectiveâ‚€ (two_ne_zero' â„•),
  replace he := this.has_sum_range_iff.2 he,
  replace ho := ((add_left_injective 1).comp this).has_sum_range_iff.2 ho,
  refine he.add_is_compl _ ho,
  simpa [(âˆ˜)] using nat.is_compl_even_odd
end

lemma summable.compl_add {s : set Î²} (hs : summable (f âˆ˜ coe : sá¶œ â†’ Î±))
  (hsc : summable (f âˆ˜ coe : s â†’ Î±)) :
  summable f :=
(hs.has_sum.compl_add hsc.has_sum).summable

lemma summable.even_add_odd {f : â„• â†’ Î±} (he : summable (Î» k, f (2 * k)))
  (ho : summable (Î» k, f (2 * k + 1))) :
  summable f :=
(he.has_sum.even_add_odd ho.has_sum).summable

lemma has_sum.sigma [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£ b:Î², Î³ b) â†’ Î±} {g : Î² â†’ Î±} {a : Î±}
  (ha : has_sum f a) (hf : âˆ€b, has_sum (Î»c, f âŸ¨b, câŸ©) (g b)) : has_sum g a :=
begin
  refine (at_top_basis.tendsto_iff (closed_nhds_basis a)).mpr _,
  rintros s âŸ¨hs, hscâŸ©,
  rcases mem_at_top_sets.mp (ha hs) with âŸ¨u, huâŸ©,
  use [u.image sigma.fst, trivial],
  intros bs hbs,
  simp only [set.mem_preimage, ge_iff_le, finset.le_iff_subset] at hu,
  have : tendsto (Î» t : finset (Î£ b, Î³ b), âˆ‘ p in t.filter (Î» p, p.1 âˆˆ bs), f p)
    at_top (ğ“ $ âˆ‘ b in bs, g b),
  { simp only [â† sigma_preimage_mk, sum_sigma],
    refine tendsto_finset_sum _ (Î» b hb, _),
    change tendsto (Î» t, (Î» t, âˆ‘ s in t, f âŸ¨b, sâŸ©) (preimage t (sigma.mk b) _)) at_top (ğ“ (g b)),
    exact tendsto.comp (hf b) (tendsto_finset_preimage_at_top_at_top _) },
  refine hsc.mem_of_tendsto this (eventually_at_top.2 âŸ¨u, Î» t ht, hu _ (Î» x hx, _)âŸ©),
  exact mem_filter.2 âŸ¨ht hx, hbs $ mem_image_of_mem _ hxâŸ©
end

/-- If a series `f` on `Î² Ã— Î³` has sum `a` and for each `b` the restriction of `f` to `{b} Ã— Î³`
has sum `g b`, then the series `g` has sum `a`. -/
lemma has_sum.prod_fiberwise [regular_space Î±] {f : Î² Ã— Î³ â†’ Î±} {g : Î² â†’ Î±} {a : Î±}
  (ha : has_sum f a) (hf : âˆ€b, has_sum (Î»c, f (b, c)) (g b)) :
  has_sum g a :=
has_sum.sigma ((equiv.sigma_equiv_prod Î² Î³).has_sum_iff.2 ha) hf

lemma summable.sigma' [regular_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) (hf : âˆ€b, summable (Î»c, f âŸ¨b, câŸ©)) :
  summable (Î»b, âˆ‘'c, f âŸ¨b, câŸ©) :=
(ha.has_sum.sigma (assume b, (hf b).has_sum)).summable

lemma has_sum.sigma_of_has_sum [t3_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£ b:Î², Î³ b) â†’ Î±} {g : Î² â†’ Î±}
  {a : Î±} (ha : has_sum g a) (hf : âˆ€b, has_sum (Î»c, f âŸ¨b, câŸ©) (g b)) (hf' : summable f) :
  has_sum f a :=
by simpa [(hf'.has_sum.sigma hf).unique ha] using hf'.has_sum

/-- Version of `has_sum.update` for `add_comm_monoid` rather than `add_comm_group`.
Rather than showing that `f.update` has a specific sum in terms of `has_sum`,
it gives a relationship between the sums of `f` and `f.update` given that both exist. -/
lemma has_sum.update' {Î± Î² : Type*} [topological_space Î±] [add_comm_monoid Î±] [t2_space Î±]
  [has_continuous_add Î±] {f : Î² â†’ Î±} {a a' : Î±} (hf : has_sum f a)
  (b : Î²) (x : Î±) (hf' : has_sum (f.update b x) a') : a + x = a' + f b :=
begin
  have : âˆ€ b', f b' + ite (b' = b) x 0 = f.update b x b' + ite (b' = b) (f b) 0,
  { intro b',
    split_ifs with hb',
    { simpa only [function.update_apply, hb', eq_self_iff_true] using add_comm (f b) x },
    { simp only [function.update_apply, hb', if_false] } },
  have h := hf.add ((has_sum_ite_eq b x)),
  simp_rw this at h,
  exact has_sum.unique h (hf'.add (has_sum_ite_eq b (f b)))
end

/-- Version of `has_sum_ite_sub_has_sum` for `add_comm_monoid` rather than `add_comm_group`.
Rather than showing that the `ite` expression has a specific sum in terms of `has_sum`,
it gives a relationship between the sums of `f` and `ite (n = b) 0 (f n)` given that both exist. -/
lemma eq_add_of_has_sum_ite {Î± Î² : Type*} [topological_space Î±] [add_comm_monoid Î±]
  [t2_space Î±] [has_continuous_add Î±] {f : Î² â†’ Î±} {a : Î±} (hf : has_sum f a) (b : Î²) (a' : Î±)
  (hf' : has_sum (Î» n, ite (n = b) 0 (f n)) a') : a = a' + f b :=
begin
  refine (add_zero a).symm.trans (hf.update' b 0 _),
  convert hf',
  exact funext (f.update_apply b 0),
end

end has_sum

section tsum
variables [add_comm_monoid Î±] [topological_space Î±]

lemma tsum_congr_subtype (f : Î² â†’ Î±) {s t : set Î²} (h : s = t) :
  âˆ‘' (x : s), f x = âˆ‘' (x : t), f x :=
by rw h

lemma tsum_zero' (hz : is_closed ({0} : set Î±)) : âˆ‘' b : Î², (0 : Î±) = 0 :=
begin
  classical,
  rw [tsum, dif_pos summable_zero],
  suffices : âˆ€ (x : Î±), has_sum (Î» (b : Î²), (0 : Î±)) x â†’ x = 0,
  { exact this _ (classical.some_spec _) },
  intros x hx,
  contrapose! hx,
  simp only [has_sum, tendsto_nhds, finset.sum_const_zero, filter.mem_at_top_sets, ge_iff_le,
              finset.le_eq_subset, set.mem_preimage, not_forall, not_exists, exists_prop,
              exists_and_distrib_right],
  refine âŸ¨{0}á¶œ, âŸ¨is_open_compl_iff.mpr hz, _âŸ©, Î» y, âŸ¨âŸ¨y, subset_refl _âŸ©, _âŸ©âŸ©,
  { simpa using hx },
  { simp }
end

@[simp] lemma tsum_zero [t1_space Î±] : âˆ‘' b : Î², (0 : Î±) = 0 := tsum_zero' is_closed_singleton

variables [t2_space Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma has_sum.tsum_eq (ha : has_sum f a) : âˆ‘'b, f b = a :=
(summable.has_sum âŸ¨a, haâŸ©).unique ha

lemma summable.has_sum_iff (h : summable f) : has_sum f a â†” âˆ‘'b, f b = a :=
iff.intro has_sum.tsum_eq (assume eq, eq â–¸ h.has_sum)

@[simp] lemma tsum_empty [is_empty Î²] : âˆ‘'b, f b = 0 := has_sum_empty.tsum_eq

lemma tsum_eq_sum {f : Î² â†’ Î±} {s : finset Î²} (hf : âˆ€bâˆ‰s, f b = 0)  :
  âˆ‘' b, f b = âˆ‘ b in s, f b :=
(has_sum_sum_of_ne_finset_zero hf).tsum_eq

lemma sum_eq_tsum_indicator (f : Î² â†’ Î±) (s : finset Î²) :
  âˆ‘ x in s, f x = âˆ‘' x, set.indicator â†‘s f x :=
have âˆ€ x âˆ‰ s, set.indicator â†‘s f x = 0,
from Î» x hx, set.indicator_apply_eq_zero.2 (Î» hx', (hx $ finset.mem_coe.1 hx').elim),
(finset.sum_congr rfl (Î» x hx, (set.indicator_apply_eq_self.2 $
  Î» hx', (hx' $ finset.mem_coe.2 hx).elim).symm)).trans (tsum_eq_sum this).symm

lemma tsum_congr {Î± Î² : Type*} [add_comm_monoid Î±] [topological_space Î±]
  {f g : Î² â†’ Î±} (hfg : âˆ€ b, f b = g b) : âˆ‘' b, f b = âˆ‘' b, g b :=
congr_arg tsum (funext hfg)

lemma tsum_fintype [fintype Î²] (f : Î² â†’ Î±) : âˆ‘'b, f b = âˆ‘ b, f b :=
(has_sum_fintype f).tsum_eq

lemma tsum_bool (f : bool â†’ Î±) : âˆ‘' i : bool, f i = f false + f true :=
by { rw [tsum_fintype, finset.sum_eq_add]; simp }

lemma tsum_eq_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€b' â‰  b, f b' = 0)  :
  âˆ‘'b, f b = f b :=
(has_sum_single b hf).tsum_eq

lemma tsum_tsum_eq_single (f : Î² â†’ Î³ â†’ Î±) (b : Î²) (c : Î³) (hfb : âˆ€ b' â‰  b, f b' c = 0)
  (hfc : âˆ€ (b' : Î²) (c' : Î³), c' â‰  c â†’ f b' c' = 0) :
  âˆ‘' b' c', f b' c' = f b c :=
calc âˆ‘' b' c', f b' c' = âˆ‘' b', f b' c : tsum_congr $ Î» b', tsum_eq_single _ (hfc b')
... = f b c : tsum_eq_single _ hfb

@[simp] lemma tsum_ite_eq (b : Î²) [decidable_pred (= b)] (a : Î±) :
  âˆ‘' b', (if b' = b then a else 0) = a :=
(has_sum_ite_eq b a).tsum_eq

@[simp] lemma tsum_pi_single [decidable_eq Î²] (b : Î²) (a : Î±) :
  âˆ‘' b', pi.single b a b' = a :=
(has_sum_pi_single b a).tsum_eq

lemma tsum_dite_right (P : Prop) [decidable P] (x : Î² â†’ Â¬ P â†’ Î±) :
  âˆ‘' (b : Î²), (if h : P then (0 : Î±) else x b h) = if h : P then (0 : Î±) else âˆ‘' (b : Î²), x b h :=
by by_cases hP : P; simp [hP]

lemma tsum_dite_left (P : Prop) [decidable P] (x : Î² â†’ P â†’ Î±) :
  âˆ‘' (b : Î²), (if h : P then x b h else 0) = if h : P then (âˆ‘' (b : Î²), x b h) else 0 :=
by by_cases hP : P; simp [hP]

lemma function.surjective.tsum_eq_tsum_of_has_sum_iff_has_sum {Î±' : Type*} [add_comm_monoid Î±']
  [topological_space Î±'] {e : Î±' â†’ Î±} (hes : function.surjective e) (h0 : e 0 = 0)
  {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
  (h : âˆ€ {a}, has_sum f (e a) â†” has_sum g a) :
  âˆ‘' b, f b = e (âˆ‘' c, g c) :=
by_cases
  (assume : summable g, (h.mpr this.has_sum).tsum_eq)
  (assume hg : Â¬ summable g,
    have hf : Â¬ summable f, from mt (hes.summable_iff_of_has_sum_iff @h).1 hg,
    by simp [tsum, hf, hg, h0])

lemma tsum_eq_tsum_of_has_sum_iff_has_sum {f : Î² â†’ Î±} {g : Î³ â†’ Î±}
  (h : âˆ€{a}, has_sum f a â†” has_sum g a) :
  âˆ‘'b, f b = âˆ‘'c, g c :=
surjective_id.tsum_eq_tsum_of_has_sum_iff_has_sum rfl @h

lemma equiv.tsum_eq (j : Î³ â‰ƒ Î²) (f : Î² â†’ Î±) : âˆ‘'c, f (j c) = âˆ‘'b, f b :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» a, j.has_sum_iff

lemma equiv.tsum_eq_tsum_of_support {f : Î² â†’ Î±} {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
  (he : âˆ€ x, g (e x) = f x) :
  (âˆ‘' x, f x) = âˆ‘' y, g y :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, e.has_sum_iff_of_support he

lemma tsum_eq_tsum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
  (hi : âˆ€ â¦ƒx yâ¦„, i x = i y â†’ (x : Î³) = y)
  (hf : support f âŠ† set.range i) (hfg : âˆ€ x, f (i x) = g x) :
  âˆ‘' x, f x  = âˆ‘' y, g y :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, has_sum_iff_has_sum_of_ne_zero_bij i hi hf hfg

/-! ### `tsum` on subsets -/

@[simp] lemma finset.tsum_subtype (s : finset Î²) (f : Î² â†’ Î±) :
  âˆ‘' x : {x // x âˆˆ s}, f x = âˆ‘ x in s, f x :=
(s.has_sum f).tsum_eq

@[simp] lemma finset.tsum_subtype' (s : finset Î²) (f : Î² â†’ Î±) :
  âˆ‘' x : (s : set Î²), f x = âˆ‘ x in s, f x :=
s.tsum_subtype f

lemma tsum_subtype (s : set Î²) (f : Î² â†’ Î±) :
  âˆ‘' x : s, f x = âˆ‘' x, s.indicator f x :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» _, has_sum_subtype_iff_indicator

lemma tsum_subtype_eq_of_support_subset {f : Î² â†’ Î±} {s : set Î²} (hs : support f âŠ† s) :
  âˆ‘' x : s, f x = âˆ‘' x, f x :=
tsum_eq_tsum_of_has_sum_iff_has_sum $ Î» x, has_sum_subtype_iff_of_support_subset hs

@[simp] lemma tsum_univ (f : Î² â†’ Î±) : âˆ‘' x : (set.univ : set Î²), f x = âˆ‘' x, f x :=
tsum_subtype_eq_of_support_subset $ set.subset_univ _

@[simp] lemma tsum_singleton (b : Î²) (f : Î² â†’ Î±) :
  âˆ‘' x : ({b} : set Î²), f x = f b :=
begin
  rw [tsum_subtype, tsum_eq_single b],
  { simp },
  { intros b' hb',
    rw set.indicator_of_not_mem,
    rwa set.mem_singleton_iff },
  { apply_instance }
end

lemma tsum_image {g : Î³ â†’ Î²} (f : Î² â†’ Î±) {s : set Î³} (hg : set.inj_on g s) :
  âˆ‘' x : g '' s, f x = âˆ‘' x : s, f (g x) :=
((equiv.set.image_of_inj_on _ _ hg).tsum_eq (Î» x, f x)).symm

lemma tsum_range {g : Î³ â†’ Î²} (f : Î² â†’ Î±) (hg : injective g) :
  âˆ‘' x : set.range g, f x = âˆ‘' x, f (g x) :=
by rw [â† set.image_univ, tsum_image f (hg.inj_on _), tsum_univ (f âˆ˜ g)]

section has_continuous_add
variable [has_continuous_add Î±]

lemma tsum_add (hf : summable f) (hg : summable g) : âˆ‘'b, (f b + g b) = (âˆ‘'b, f b) + (âˆ‘'b, g b) :=
(hf.has_sum.add hg.has_sum).tsum_eq

lemma tsum_sum {f : Î³ â†’ Î² â†’ Î±} {s : finset Î³} (hf : âˆ€iâˆˆs, summable (f i)) :
  âˆ‘'b, âˆ‘ i in s, f i b = âˆ‘ i in s, âˆ‘'b, f i b :=
(has_sum_sum $ assume i hi, (hf i hi).has_sum).tsum_eq

/-- Version of `tsum_eq_add_tsum_ite` for `add_comm_monoid` rather than `add_comm_group`.
Requires a different convergence assumption involving `function.update`. -/
lemma tsum_eq_add_tsum_ite' {f : Î² â†’ Î±} (b : Î²) (hf : summable (f.update b 0)) :
  âˆ‘' x, f x = f b + âˆ‘' x, ite (x = b) 0 (f x) :=
calc âˆ‘' x, f x = âˆ‘' x, ((ite (x = b) (f x) 0) + (f.update b 0 x)) :
    tsum_congr (Î» n, by split_ifs; simp [function.update_apply, h])
  ... = âˆ‘' x, ite (x = b) (f x) 0 + âˆ‘' x, f.update b 0 x :
    tsum_add âŸ¨ite (b = b) (f b) 0, has_sum_single b (Î» b hb, if_neg hb)âŸ© (hf)
  ... = (ite (b = b) (f b) 0) + âˆ‘' x, f.update b 0 x :
    by { congr, exact (tsum_eq_single b (Î» b' hb', if_neg hb')) }
  ... = f b + âˆ‘' x, ite (x = b) 0 (f x) :
    by simp only [function.update, eq_self_iff_true, if_true, eq_rec_constant, dite_eq_ite]

variables [add_comm_monoid Î´] [topological_space Î´] [t3_space Î´] [has_continuous_add Î´]

lemma tsum_sigma' {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î´} (hâ‚ : âˆ€b, summable (Î»c, f âŸ¨b, câŸ©))
  (hâ‚‚ : summable f) : âˆ‘'p, f p = âˆ‘'b c, f âŸ¨b, câŸ© :=
(hâ‚‚.has_sum.sigma (assume b, (hâ‚ b).has_sum)).tsum_eq.symm

lemma tsum_prod' {f : Î² Ã— Î³ â†’ Î´} (h : summable f) (hâ‚ : âˆ€b, summable (Î»c, f (b, c))) :
  âˆ‘'p, f p = âˆ‘'b c, f (b, c) :=
(h.has_sum.prod_fiberwise (assume b, (hâ‚ b).has_sum)).tsum_eq.symm

lemma tsum_comm' {f : Î² â†’ Î³ â†’ Î´} (h : summable (function.uncurry f)) (hâ‚ : âˆ€b, summable (f b))
  (hâ‚‚ : âˆ€ c, summable (Î» b, f b c)) :
  âˆ‘' c b, f b c = âˆ‘' b c, f b c :=
begin
  erw [â† tsum_prod' h hâ‚, â† tsum_prod' h.prod_symm hâ‚‚, â† (equiv.prod_comm Î³ Î²).tsum_eq (uncurry f)],
  refl
end

end has_continuous_add

open encodable

section encodable
variable [encodable Î³]

/-- You can compute a sum over an encodably type by summing over the natural numbers and
  taking a supremum. This is useful for outer measures. -/
theorem tsum_supr_decodeâ‚‚ [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (s : Î³ â†’ Î²) : âˆ‘' i : â„•, m (â¨† b âˆˆ decodeâ‚‚ Î³ i, s b) = âˆ‘' b : Î³, m (s b) :=
begin
  have H : âˆ€ n, m (â¨† b âˆˆ decodeâ‚‚ Î³ n, s b) â‰  0 â†’ (decodeâ‚‚ Î³ n).is_some,
  { intros n h,
    cases decodeâ‚‚ Î³ n with b,
    { refine (h $ by simp [m0]).elim },
    { exact rfl } },
  symmetry, refine tsum_eq_tsum_of_ne_zero_bij (Î» a, option.get (H a.1 a.2)) _ _ _,
  { rintros âŸ¨m, hmâŸ© âŸ¨n, hnâŸ© e,
    have := mem_decodeâ‚‚.1 (option.get_mem (H n hn)),
    rwa [â† e, mem_decodeâ‚‚.1 (option.get_mem (H m hm))] at this },
  { intros b h,
    refine âŸ¨âŸ¨encode b, _âŸ©, _âŸ©,
    { simp only [mem_support, encodekâ‚‚] at h âŠ¢, convert h, simp [set.ext_iff, encodekâ‚‚] },
    { exact option.get_of_mem _ (encodekâ‚‚ _) } },
  { rintros âŸ¨n, hâŸ©, dsimp only [subtype.coe_mk],
    transitivity, swap,
    rw [show decodeâ‚‚ Î³ n = _, from option.get_mem (H n h)],
    congr, simp [ext_iff, -option.some_get] }
end

/-- `tsum_supr_decodeâ‚‚` specialized to the complete lattice of sets. -/
theorem tsum_Union_decodeâ‚‚ (m : set Î² â†’ Î±) (m0 : m âˆ… = 0)
  (s : Î³ â†’ set Î²) : âˆ‘' i, m (â‹ƒ b âˆˆ decodeâ‚‚ Î³ i, s b) = âˆ‘' b, m (s b) :=
tsum_supr_decodeâ‚‚ m m0 s

end encodable

/-! Some properties about measure-like functions.
  These could also be functions defined on complete sublattices of sets, with the property
  that they are countably sub-additive.
  `R` will probably be instantiated with `(â‰¤)` in all applications.
-/

section countable
variables [countable Î³]

/-- If a function is countably sub-additive then it is sub-additive on countable types -/
theorem rel_supr_tsum [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) âˆ‘' i, m (s i))
  (s : Î³ â†’ Î²) : R (m (â¨† b : Î³, s b)) âˆ‘' b : Î³, m (s b) :=
by { casesI nonempty_encodable Î³, rw [â†supr_decodeâ‚‚, â†tsum_supr_decodeâ‚‚ _ m0 s], exact m_supr _ }

/-- If a function is countably sub-additive then it is sub-additive on finite sets -/
theorem rel_supr_sum [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) (âˆ‘' i, m (s i)))
  (s : Î´ â†’ Î²) (t : finset Î´) :
  R (m (â¨† d âˆˆ t, s d)) (âˆ‘ d in t, m (s d)) :=
by { rw [supr_subtype', â†finset.tsum_subtype], exact rel_supr_tsum m m0 R m_supr _ }

/-- If a function is countably sub-additive then it is binary sub-additive -/
theorem rel_sup_add [complete_lattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0)
  (R : Î± â†’ Î± â†’ Prop) (m_supr : âˆ€(s : â„• â†’ Î²), R (m (â¨† i, s i)) (âˆ‘' i, m (s i)))
  (sâ‚ sâ‚‚ : Î²) : R (m (sâ‚ âŠ” sâ‚‚)) (m sâ‚ + m sâ‚‚) :=
begin
  convert rel_supr_tsum m m0 R m_supr (Î» b, cond b sâ‚ sâ‚‚),
  { simp only [supr_bool_eq, cond] },
  { rw [tsum_fintype, fintype.sum_bool, cond, cond] }
end

end countable

variables [has_continuous_add Î±]

lemma tsum_add_tsum_compl {s : set Î²} (hs : summable (f âˆ˜ coe : s â†’ Î±))
  (hsc : summable (f âˆ˜ coe : sá¶œ â†’ Î±)) :
  (âˆ‘' x : s, f x) + (âˆ‘' x : sá¶œ, f x) = âˆ‘' x, f x :=
(hs.has_sum.add_compl hsc.has_sum).tsum_eq.symm

lemma tsum_union_disjoint {s t : set Î²} (hd : disjoint s t)
  (hs : summable (f âˆ˜ coe : s â†’ Î±)) (ht : summable (f âˆ˜ coe : t â†’ Î±)) :
  (âˆ‘' x : s âˆª t, f x) = (âˆ‘' x : s, f x) + (âˆ‘' x : t, f x) :=
(hs.has_sum.add_disjoint hd ht.has_sum).tsum_eq

lemma tsum_finset_bUnion_disjoint {Î¹} {s : finset Î¹} {t : Î¹ â†’ set Î²}
  (hd : (s : set Î¹).pairwise (disjoint on t))
  (hf : âˆ€ i âˆˆ s, summable (f âˆ˜ coe : t i â†’ Î±)) :
  (âˆ‘' x : (â‹ƒ i âˆˆ s, t i), f x) = âˆ‘ i in s, âˆ‘' x : t i, f x :=
(has_sum_sum_disjoint _ hd (Î» i hi, (hf i hi).has_sum)).tsum_eq

lemma tsum_even_add_odd {f : â„• â†’ Î±} (he : summable (Î» k, f (2 * k)))
  (ho : summable (Î» k, f (2 * k + 1))) :
  (âˆ‘' k, f (2 * k)) + (âˆ‘' k, f (2 * k + 1)) = âˆ‘' k, f k :=
(he.has_sum.even_add_odd ho.has_sum).tsum_eq.symm

end tsum

section topological_group
variables [add_comm_group Î±] [topological_space Î±] [topological_add_group Î±]
variables {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

-- `by simpa using` speeds up elaboration. Why?
lemma has_sum.neg (h : has_sum f a) : has_sum (Î»b, - f b) (- a) :=
by simpa only using h.map (-add_monoid_hom.id Î±) continuous_neg

lemma summable.neg (hf : summable f) : summable (Î»b, - f b) :=
hf.has_sum.neg.summable

lemma summable.of_neg (hf : summable (Î»b, - f b)) : summable f :=
by simpa only [neg_neg] using hf.neg

lemma summable_neg_iff : summable (Î» b, - f b) â†” summable f :=
âŸ¨summable.of_neg, summable.negâŸ©

lemma has_sum.sub (hf : has_sum f aâ‚) (hg : has_sum g aâ‚‚) : has_sum (Î»b, f b - g b) (aâ‚ - aâ‚‚) :=
by { simp only [sub_eq_add_neg], exact hf.add hg.neg }

lemma summable.sub (hf : summable f) (hg : summable g) : summable (Î»b, f b - g b) :=
(hf.has_sum.sub hg.has_sum).summable

lemma summable.trans_sub (hg : summable g) (hfg : summable (Î»b, f b - g b)) :
  summable f :=
by simpa only [sub_add_cancel] using hfg.add hg

lemma summable_iff_of_summable_sub (hfg : summable (Î»b, f b - g b)) :
  summable f â†” summable g :=
âŸ¨Î» hf, hf.trans_sub $ by simpa only [neg_sub] using hfg.neg, Î» hg, hg.trans_sub hfgâŸ©

lemma has_sum.update (hf : has_sum f aâ‚) (b : Î²) [decidable_eq Î²] (a : Î±) :
  has_sum (update f b a) (a - f b + aâ‚) :=
begin
  convert ((has_sum_ite_eq b _).add hf),
  ext b',
  by_cases h : b' = b,
  { rw [h, update_same],
    simp only [eq_self_iff_true, if_true, sub_add_cancel] },
  simp only [h, update_noteq, if_false, ne.def, zero_add, not_false_iff],
end

lemma summable.update (hf : summable f) (b : Î²) [decidable_eq Î²] (a : Î±) :
  summable (update f b a) :=
(hf.has_sum.update b a).summable

lemma has_sum.has_sum_compl_iff {s : set Î²} (hf : has_sum (f âˆ˜ coe : s â†’ Î±) aâ‚) :
  has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) aâ‚‚ â†” has_sum f (aâ‚ + aâ‚‚) :=
begin
  refine âŸ¨Î» h, hf.add_compl h, Î» h, _âŸ©,
  rw [has_sum_subtype_iff_indicator] at hf âŠ¢,
  rw [set.indicator_compl],
  simpa only [add_sub_cancel'] using h.sub hf
end

lemma has_sum.has_sum_iff_compl {s : set Î²} (hf : has_sum (f âˆ˜ coe : s â†’ Î±) aâ‚) :
  has_sum f aâ‚‚ â†” has_sum (f âˆ˜ coe : sá¶œ â†’ Î±) (aâ‚‚ - aâ‚) :=
iff.symm $ hf.has_sum_compl_iff.trans $ by rw [add_sub_cancel'_right]

lemma summable.summable_compl_iff {s : set Î²} (hf : summable (f âˆ˜ coe : s â†’ Î±)) :
  summable (f âˆ˜ coe : sá¶œ â†’ Î±) â†” summable f :=
âŸ¨Î» âŸ¨a, haâŸ©, (hf.has_sum.has_sum_compl_iff.1 ha).summable,
  Î» âŸ¨a, haâŸ©, (hf.has_sum.has_sum_iff_compl.1 ha).summableâŸ©

protected lemma finset.has_sum_compl_iff (s : finset Î²) :
  has_sum (Î» x : {x // x âˆ‰ s}, f x) a â†” has_sum f (a + âˆ‘ i in s, f i) :=
(s.has_sum f).has_sum_compl_iff.trans $ by rw [add_comm]

protected lemma finset.has_sum_iff_compl (s : finset Î²) :
  has_sum f a â†” has_sum (Î» x : {x // x âˆ‰ s}, f x) (a - âˆ‘ i in s, f i) :=
(s.has_sum f).has_sum_iff_compl

protected lemma finset.summable_compl_iff (s : finset Î²) :
  summable (Î» x : {x // x âˆ‰ s}, f x) â†” summable f :=
(s.summable f).summable_compl_iff

lemma set.finite.summable_compl_iff {s : set Î²} (hs : s.finite) :
  summable (f âˆ˜ coe : sá¶œ â†’ Î±) â†” summable f :=
(hs.summable f).summable_compl_iff

lemma has_sum_ite_sub_has_sum [decidable_eq Î²] (hf : has_sum f a) (b : Î²) :
  has_sum (Î» n, ite (n = b) 0 (f n)) (a - f b) :=
begin
  convert hf.update b 0 using 1,
  { ext n, rw function.update_apply, },
  { rw [sub_add_eq_add_sub, zero_add], },
end

section tsum
variables [t2_space Î±]

lemma tsum_neg : âˆ‘'b, - f b = - âˆ‘'b, f b :=
begin
  by_cases hf : summable f,
  { exact hf.has_sum.neg.tsum_eq, },
  { simp [tsum_eq_zero_of_not_summable hf, tsum_eq_zero_of_not_summable (mt summable.of_neg hf)] },
end

lemma tsum_sub (hf : summable f) (hg : summable g) : âˆ‘'b, (f b - g b) = âˆ‘'b, f b - âˆ‘'b, g b :=
(hf.has_sum.sub hg.has_sum).tsum_eq

lemma sum_add_tsum_compl {s : finset Î²} (hf : summable f) :
  (âˆ‘ x in s, f x) + (âˆ‘' x : (â†‘s : set Î²)á¶œ, f x) = âˆ‘' x, f x :=
((s.has_sum f).add_compl (s.summable_compl_iff.2 hf).has_sum).tsum_eq.symm

/-- Let `f : Î² â†’ Î±` be a sequence with summable series and let `b âˆˆ Î²` be an index.
Lemma `tsum_eq_add_tsum_ite` writes `Î£ f n` as the sum of `f b` plus the series of the
remaining terms. -/
lemma tsum_eq_add_tsum_ite [decidable_eq Î²] (hf : summable f) (b : Î²) :
  âˆ‘' n, f n = f b + âˆ‘' n, ite (n = b) 0 (f n) :=
begin
  rw (has_sum_ite_sub_has_sum hf.has_sum b).tsum_eq,
  exact (add_sub_cancel'_right _ _).symm,
end

end tsum

/-!
### Sums on nat

We show the formula `(âˆ‘ i in range k, f i) + (âˆ‘' i, f (i + k)) = (âˆ‘' i, f i)`, in
`sum_add_tsum_nat_add`, as well as several results relating sums on `â„•` and `â„¤`.
-/
section nat

lemma has_sum_nat_add_iff {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
  has_sum (Î» n, f (n + k)) a â†” has_sum f (a + âˆ‘ i in range k, f i) :=
begin
  refine iff.trans _ ((range k).has_sum_compl_iff),
  rw [â† (not_mem_range_equiv k).symm.has_sum_iff],
  refl
end

lemma summable_nat_add_iff {f : â„• â†’ Î±} (k : â„•) : summable (Î» n, f (n + k)) â†” summable f :=
iff.symm $ (equiv.add_right (âˆ‘ i in range k, f i)).surjective.summable_iff_of_has_sum_iff $
  Î» a, (has_sum_nat_add_iff k).symm

lemma has_sum_nat_add_iff' {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
  has_sum (Î» n, f (n + k)) (a - âˆ‘ i in range k, f i) â†” has_sum f a :=
by simp [has_sum_nat_add_iff]

lemma sum_add_tsum_nat_add [t2_space Î±] {f : â„• â†’ Î±} (k : â„•) (h : summable f) :
  (âˆ‘ i in range k, f i) + (âˆ‘' i, f (i + k)) = âˆ‘' i, f i :=
by simpa only [add_comm] using
  ((has_sum_nat_add_iff k).1 ((summable_nat_add_iff k).2 h).has_sum).unique h.has_sum

lemma tsum_eq_zero_add [t2_space Î±] {f : â„• â†’ Î±} (hf : summable f) :
  âˆ‘'b, f b = f 0 + âˆ‘'b, f (b + 1) :=
by simpa only [sum_range_one] using (sum_add_tsum_nat_add 1 hf).symm

/-- For `f : â„• â†’ Î±`, then `âˆ‘' k, f (k + i)` tends to zero. This does not require a summability
assumption on `f`, as otherwise all sums are zero. -/
lemma tendsto_sum_nat_add [t2_space Î±] (f : â„• â†’ Î±) : tendsto (Î» i, âˆ‘' k, f (k + i)) at_top (ğ“ 0) :=
begin
  by_cases hf : summable f,
  { have hâ‚€ : (Î» i, (âˆ‘' i, f i) - âˆ‘ j in range i, f j) = Î» i, âˆ‘' (k : â„•), f (k + i),
    { ext1 i,
      rw [sub_eq_iff_eq_add, add_comm, sum_add_tsum_nat_add i hf] },
    have hâ‚ : tendsto (Î» i : â„•, âˆ‘' i, f i) at_top (ğ“ (âˆ‘' i, f i)) := tendsto_const_nhds,
    simpa only [hâ‚€, sub_self] using tendsto.sub hâ‚ hf.has_sum.tendsto_sum_nat },
  { convert tendsto_const_nhds,
    ext1 i,
    rw â† summable_nat_add_iff i at hf,
    { exact tsum_eq_zero_of_not_summable hf },
    { apply_instance } }
end

/-- If `fâ‚€, fâ‚, fâ‚‚, ...` and `gâ‚€, gâ‚, gâ‚‚, ...` are both convergent then so is the `â„¤`-indexed
sequence: `..., gâ‚‚, gâ‚, gâ‚€, fâ‚€, fâ‚, fâ‚‚, ...`. -/
lemma has_sum.int_rec {b : Î±} {f g : â„• â†’ Î±} (hf : has_sum f a) (hg : has_sum g b) :
  @has_sum Î± _ _ _ (@int.rec (Î» _, Î±) f g : â„¤ â†’ Î±) (a + b) :=
begin
  -- note this proof works for any two-case inductive
  have hâ‚ : injective (coe : â„• â†’ â„¤) := @int.of_nat.inj,
  have hâ‚‚ : injective int.neg_succ_of_nat := @int.neg_succ_of_nat.inj,
  have : is_compl (set.range (coe : â„• â†’ â„¤)) (set.range int.neg_succ_of_nat),
  { split,
    { rw disjoint_iff_inf_le,
      rintros _ âŸ¨âŸ¨i, rflâŸ©, âŸ¨j, âŸ¨âŸ©âŸ©âŸ© },
    { rw codisjoint_iff_le_sup,
      rintros (i | j) h,
      exacts [or.inl âŸ¨_, rflâŸ©, or.inr âŸ¨_, rflâŸ©] } },
  exact has_sum.add_is_compl this (hâ‚.has_sum_range_iff.mpr hf) (hâ‚‚.has_sum_range_iff.mpr hg),
end

lemma has_sum.nonneg_add_neg {b : Î±} {f : â„¤ â†’ Î±}
  (hnonneg : has_sum (Î» n : â„•, f n) a) (hneg : has_sum (Î» (n : â„•), f (-n.succ)) b) :
  has_sum f (a + b) :=
begin
  simp_rw â† int.neg_succ_of_nat_coe at hneg,
  convert hnonneg.int_rec hneg using 1,
  ext (i | j); refl,
end

lemma has_sum.pos_add_zero_add_neg {b : Î±} {f : â„¤ â†’ Î±}
  (hpos : has_sum (Î» n:â„•, f(n + 1)) a) (hneg : has_sum (Î» (n : â„•), f (-n.succ)) b) :
  has_sum f (a + f 0 + b) :=
begin
  have : âˆ€ g : â„• â†’ Î±, has_sum (Î» k, g (k + 1)) a â†’ has_sum g (a + g 0),
  { intros g hg, simpa using (has_sum_nat_add_iff _).mp hg },
  exact (this (Î» n, f n) hpos).nonneg_add_neg hneg,
end

lemma summable_int_of_summable_nat {f : â„¤ â†’ Î±}
  (hp : summable (Î» n:â„•, f n)) (hn : summable (Î» n:â„•, f (-n))) : summable f :=
(has_sum.nonneg_add_neg hp.has_sum $ summable.has_sum $ (summable_nat_add_iff 1).mpr hn).summable

lemma has_sum.sum_nat_of_sum_int {Î± : Type*} [add_comm_monoid Î±] [topological_space Î±]
  [has_continuous_add Î±] {a : Î±} {f : â„¤ â†’ Î±} (hf : has_sum f a) :
  has_sum (Î» n:â„•, f n + f (-n)) (a + f 0) :=
begin
  apply (hf.add (has_sum_ite_eq (0 : â„¤) (f 0))).has_sum_of_sum_eq (Î» u, _),
  refine âŸ¨u.image int.nat_abs, Î» v' hv', _âŸ©,
  let u1 := v'.image (Î» (x : â„•), (x : â„¤)),
  let u2 := v'.image (Î» (x : â„•), - (x : â„¤)),
  have A : u âŠ† u1 âˆª u2,
  { assume x hx,
    simp only [mem_union, mem_image, exists_prop],
    rcases le_total 0 x with h'x|h'x,
    { left,
      refine âŸ¨int.nat_abs x, hv' _, _âŸ©,
      { simp only [mem_image, exists_prop],
        exact âŸ¨x, hx, rflâŸ© },
      { simp only [h'x, int.coe_nat_abs, abs_eq_self] } },
    { right,
      refine âŸ¨int.nat_abs x, hv' _, _âŸ©,
      { simp only [mem_image, exists_prop],
        exact âŸ¨x, hx, rflâŸ© },
      { simp only [abs_of_nonpos h'x, int.coe_nat_abs, neg_neg] } } },
  refine âŸ¨u1 âˆª u2, A, _âŸ©,
  calc âˆ‘ x in u1 âˆª u2, (f x + ite (x = 0) (f 0) 0)
      = âˆ‘ x in u1 âˆª u2, f x + âˆ‘ x in u1 âˆ© u2, f x :
    begin
      rw sum_add_distrib,
      congr' 1,
      refine (sum_subset_zero_on_sdiff inter_subset_union _ _).symm,
      { assume x hx,
        suffices : x â‰  0, by simp only [this, if_false],
        rintros rfl,
        simpa only [mem_sdiff, mem_union, mem_image, neg_eq_zero, or_self, mem_inter, and_self,
          and_not_self] using hx },
      { assume x hx,
        simp only [mem_inter, mem_image, exists_prop] at hx,
        have : x = 0,
        { apply le_antisymm,
          { rcases hx.2 with âŸ¨a, ha, rflâŸ©,
            simp only [right.neg_nonpos_iff, nat.cast_nonneg] },
          { rcases hx.1 with âŸ¨a, ha, rflâŸ©,
            simp only [nat.cast_nonneg] } },
        simp only [this, eq_self_iff_true, if_true] }
    end
  ... = âˆ‘ x in u1, f x + âˆ‘ x in u2, f x : sum_union_inter
  ... = âˆ‘ b in v', f b + âˆ‘ b in v', f (-b) :
    by simp only [sum_image, nat.cast_inj, imp_self, implies_true_iff, neg_inj]
  ... = âˆ‘ b in v', (f b + f (-b)) : sum_add_distrib.symm
end

end nat

end topological_group

section uniform_group

variables [add_comm_group Î±] [uniform_space Î±]

/-- The **Cauchy criterion** for infinite sums, also known as the **Cauchy convergence test** -/
lemma summable_iff_cauchy_seq_finset [complete_space Î±] {f : Î² â†’ Î±} :
  summable f â†” cauchy_seq (Î» (s : finset Î²), âˆ‘ b in s, f b) :=
cauchy_map_iff_exists_tendsto.symm

variables [uniform_add_group Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

lemma cauchy_seq_finset_iff_vanishing :
  cauchy_seq (Î» (s : finset Î²), âˆ‘ b in s, f b)
  â†” âˆ€ e âˆˆ ğ“ (0:Î±), (âˆƒs:finset Î², âˆ€t, disjoint t s â†’ âˆ‘ b in t, f b âˆˆ e) :=
begin
  simp only [cauchy_seq, cauchy_map_iff, and_iff_right at_top_ne_bot,
    prod_at_top_at_top_eq, uniformity_eq_comap_nhds_zero Î±, tendsto_comap_iff, (âˆ˜)],
  rw [tendsto_at_top'],
  split,
  { assume h e he,
    rcases h e he with âŸ¨âŸ¨sâ‚, sâ‚‚âŸ©, hâŸ©,
    use [sâ‚ âˆª sâ‚‚],
    assume t ht,
    specialize h (sâ‚ âˆª sâ‚‚, (sâ‚ âˆª sâ‚‚) âˆª t) âŸ¨le_sup_left, le_sup_of_le_left le_sup_rightâŸ©,
    simpa only [finset.sum_union ht.symm, add_sub_cancel'] using h },
  { assume h e he,
    rcases exists_nhds_half_neg he with âŸ¨d, hd, hdeâŸ©,
    rcases h d hd with âŸ¨s, hâŸ©,
    use [(s, s)],
    rintros âŸ¨tâ‚, tâ‚‚âŸ© âŸ¨htâ‚, htâ‚‚âŸ©,
    have : âˆ‘ b in tâ‚‚, f b - âˆ‘ b in tâ‚, f b = âˆ‘ b in tâ‚‚ \ s, f b - âˆ‘ b in tâ‚ \ s, f b,
    { simp only [(finset.sum_sdiff htâ‚).symm, (finset.sum_sdiff htâ‚‚).symm,
        add_sub_add_right_eq_sub] },
    simp only [this],
    exact hde _ (h _ finset.sdiff_disjoint) _ (h _ finset.sdiff_disjoint) }
end

local attribute [instance] topological_add_group.t3_space

/-- The sum over the complement of a finset tends to `0` when the finset grows to cover the whole
space. This does not need a summability assumption, as otherwise all sums are zero. -/
lemma tendsto_tsum_compl_at_top_zero (f : Î² â†’ Î±) :
  tendsto (Î» (s : finset Î²), âˆ‘' b : {x // x âˆ‰ s}, f b) at_top (ğ“ 0) :=
begin
  by_cases H : summable f,
  { assume e he,
    rcases exists_mem_nhds_is_closed_subset he with âŸ¨o, ho, o_closed, oeâŸ©,
    simp only [le_eq_subset, set.mem_preimage, mem_at_top_sets, filter.mem_map, ge_iff_le],
    obtain âŸ¨s, hsâŸ© : âˆƒ (s : finset Î²), âˆ€ (t : finset Î²), disjoint t s â†’ âˆ‘ (b : Î²) in t, f b âˆˆ o :=
      cauchy_seq_finset_iff_vanishing.1 (tendsto.cauchy_seq H.has_sum) o ho,
    refine âŸ¨s, Î» a sa, oe _âŸ©,
    have A : summable (Î» b : {x // x âˆ‰ a}, f b) := a.summable_compl_iff.2 H,
    apply is_closed.mem_of_tendsto o_closed A.has_sum (eventually_of_forall (Î» b, _)),
    have : disjoint (finset.image (Î» (i : {x // x âˆ‰ a}), (i : Î²)) b) s,
    { apply disjoint_left.2 (Î» i hi his, _),
      rcases mem_image.1 hi with âŸ¨i', hi', rflâŸ©,
      exact i'.2 (sa his), },
    convert hs _ this using 1,
    rw sum_image,
    assume i hi j hj hij,
    exact subtype.ext hij },
  { convert tendsto_const_nhds,
    ext s,
    apply tsum_eq_zero_of_not_summable,
    rwa finset.summable_compl_iff }
end

variable [complete_space Î±]

lemma summable_iff_vanishing :
  summable f â†” âˆ€ e âˆˆ ğ“ (0:Î±), (âˆƒs:finset Î², âˆ€t, disjoint t s â†’ âˆ‘ b in t, f b âˆˆ e) :=
by rw [summable_iff_cauchy_seq_finset, cauchy_seq_finset_iff_vanishing]

/- TODO: generalize to monoid with a uniform continuous subtraction operator: `(a + b) - b = a` -/
lemma summable.summable_of_eq_zero_or_self (hf : summable f) (h : âˆ€b, g b = 0 âˆ¨ g b = f b) :
  summable g :=
summable_iff_vanishing.2 $
  assume e he,
  let âŸ¨s, hsâŸ© := summable_iff_vanishing.1 hf e he in
  âŸ¨s, assume t ht,
    have eq : âˆ‘ b in t.filter (Î»b, g b = f b), f b = âˆ‘ b in t, g b :=
      calc âˆ‘ b in t.filter (Î»b, g b = f b), f b = âˆ‘ b in t.filter (Î»b, g b = f b), g b :
          finset.sum_congr rfl (assume b hb, (finset.mem_filter.1 hb).2.symm)
        ... = âˆ‘ b in t, g b :
        begin
          refine finset.sum_subset (finset.filter_subset _ _) _,
          assume b hbt hb,
          simp only [(âˆ‰), finset.mem_filter, and_iff_right hbt] at hb,
          exact (h b).resolve_right hb
        end,
    eq â–¸ hs _ $ finset.disjoint_of_subset_left (finset.filter_subset _ _) htâŸ©

protected lemma summable.indicator (hf : summable f) (s : set Î²) :
  summable (s.indicator f) :=
hf.summable_of_eq_zero_or_self $ set.indicator_eq_zero_or_self _ _

lemma summable.comp_injective {i : Î³ â†’ Î²} (hf : summable f) (hi : injective i) :
  summable (f âˆ˜ i) :=
begin
  simpa only [set.indicator_range_comp]
    using (hi.summable_iff _).2 (hf.indicator (set.range i)),
  exact Î» x hx, set.indicator_of_not_mem hx _
end

lemma summable.subtype (hf : summable f) (s : set Î²) : summable (f âˆ˜ coe : s â†’ Î±) :=
hf.comp_injective subtype.coe_injective

lemma summable_subtype_and_compl {s : set Î²} :
  summable (Î» x : s, f x) âˆ§ summable (Î» x : sá¶œ, f x) â†” summable f :=
âŸ¨and_imp.2 summable.add_compl, Î» h, âŸ¨h.subtype s, h.subtype sá¶œâŸ©âŸ©

lemma summable.sigma_factor {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) (b : Î²) : summable (Î»c, f âŸ¨b, câŸ©) :=
ha.comp_injective sigma_mk_injective

lemma summable.sigma {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) : summable (Î»b, âˆ‘'c, f âŸ¨b, câŸ©) :=
ha.sigma' (Î» b, ha.sigma_factor b)

lemma summable.prod_factor {f : Î² Ã— Î³ â†’ Î±} (h : summable f) (b : Î²) :
  summable (Î» c, f (b, c)) :=
h.comp_injective $ Î» câ‚ câ‚‚ h, (prod.ext_iff.1 h).2

lemma tsum_sigma [t1_space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b:Î², Î³ b) â†’ Î±}
  (ha : summable f) : âˆ‘'p, f p = âˆ‘'b c, f âŸ¨b, câŸ© :=
tsum_sigma' (Î» b, ha.sigma_factor b) ha

lemma tsum_prod [t1_space Î±] {f : Î² Ã— Î³ â†’ Î±} (h : summable f) :
  âˆ‘'p, f p = âˆ‘'b c, f âŸ¨b, câŸ© :=
tsum_prod' h h.prod_factor

lemma tsum_comm [t1_space Î±] {f : Î² â†’ Î³ â†’ Î±} (h : summable (function.uncurry f)) :
  âˆ‘' c b, f b c = âˆ‘' b c, f b c :=
tsum_comm' h h.prod_factor h.prod_symm.prod_factor

lemma tsum_subtype_add_tsum_subtype_compl [t2_space Î±] {f : Î² â†’ Î±} (hf : summable f) (s : set Î²) :
  âˆ‘' x : s, f x + âˆ‘' x : sá¶œ, f x = âˆ‘' x, f x :=
((hf.subtype s).has_sum.add_compl (hf.subtype {x | x âˆ‰ s}).has_sum).unique hf.has_sum

lemma sum_add_tsum_subtype_compl [t2_space Î±] {f : Î² â†’ Î±} (hf : summable f) (s : finset Î²) :
  âˆ‘ x in s, f x + âˆ‘' x : {x // x âˆ‰ s}, f x = âˆ‘' x, f x :=
begin
  rw â† tsum_subtype_add_tsum_subtype_compl hf s,
  simp only [finset.tsum_subtype', add_right_inj],
  refl,
end

end uniform_group

section topological_group

variables {G : Type*} [topological_space G] [add_comm_group G] [topological_add_group G]
  {f : Î± â†’ G}

lemma summable.vanishing (hf : summable f) â¦ƒe : set Gâ¦„ (he : e âˆˆ ğ“ (0 : G)) :
  âˆƒ s : finset Î±, âˆ€ t, disjoint t s â†’ âˆ‘ k in t, f k âˆˆ e :=
begin
  letI : uniform_space G := topological_add_group.to_uniform_space G,
  letI : uniform_add_group G := topological_add_comm_group_is_uniform,
  rcases hf with âŸ¨y, hyâŸ©,
  exact cauchy_seq_finset_iff_vanishing.1 hy.cauchy_seq e he
end

/-- Series divergence test: if `f` is a convergent series, then `f x` tends to zero along
`cofinite`. -/
lemma summable.tendsto_cofinite_zero (hf : summable f) : tendsto f cofinite (ğ“ 0) :=
begin
  intros e he,
  rw [filter.mem_map],
  rcases hf.vanishing he with âŸ¨s, hsâŸ©,
  refine s.eventually_cofinite_nmem.mono (Î» x hx, _),
  by simpa using hs {x} (disjoint_singleton_left.2 hx)
end

lemma summable.tendsto_at_top_zero {f : â„• â†’ G} (hf : summable f) : tendsto f at_top (ğ“ 0) :=
by { rw â†nat.cofinite_eq_at_top, exact hf.tendsto_cofinite_zero }

end topological_group

section const_smul
variables [monoid Î³] [topological_space Î±] [add_comm_monoid Î±] [distrib_mul_action Î³ Î±]
  [has_continuous_const_smul Î³ Î±] {f : Î² â†’ Î±}

lemma has_sum.const_smul {a : Î±} (b : Î³) (hf : has_sum f a) : has_sum (Î» i, b â€¢ f i) (b â€¢ a) :=
hf.map (distrib_mul_action.to_add_monoid_hom Î± _) $ continuous_const_smul _

lemma summable.const_smul (b : Î³) (hf : summable f) : summable (Î» i, b â€¢ f i) :=
(hf.has_sum.const_smul _).summable

lemma tsum_const_smul [t2_space Î±] (b : Î³) (hf : summable f) : âˆ‘' i, b â€¢ f i = b â€¢ âˆ‘' i, f i :=
(hf.has_sum.const_smul _).tsum_eq

end const_smul

/-! ### Product and pi types -/

section prod
variables [add_comm_monoid Î±] [topological_space Î±] [add_comm_monoid Î³] [topological_space Î³]

lemma has_sum.prod_mk {f : Î² â†’ Î±} {g : Î² â†’ Î³} {a : Î±} {b : Î³}
  (hf : has_sum f a) (hg : has_sum g b) :
  has_sum (Î» x, (âŸ¨f x, g xâŸ© : Î± Ã— Î³)) âŸ¨a, bâŸ© :=
by simp [has_sum, â† prod_mk_sum, filter.tendsto.prod_mk_nhds hf hg]

end prod

section pi
variables {Î¹ : Type*} {Ï€ : Î± â†’ Type*} [âˆ€ x, add_comm_monoid (Ï€ x)] [âˆ€ x, topological_space (Ï€ x)]

lemma pi.has_sum {f : Î¹ â†’ âˆ€ x, Ï€ x} {g : âˆ€ x, Ï€ x} :
  has_sum f g â†” âˆ€ x, has_sum (Î» i, f i x) (g x) :=
by simp only [has_sum, tendsto_pi_nhds, sum_apply]

lemma pi.summable {f : Î¹ â†’ âˆ€ x, Ï€ x} : summable f â†” âˆ€ x, summable (Î» i, f i x) :=
by simp only [summable, pi.has_sum, skolem]

lemma tsum_apply [âˆ€ x, t2_space (Ï€ x)] {f : Î¹ â†’ âˆ€ x, Ï€ x}{x : Î±} (hf : summable f) :
  (âˆ‘' i, f i) x = âˆ‘' i, f i x :=
(pi.has_sum.mp hf.has_sum x).tsum_eq.symm

end pi

/-! ### Multiplicative opposite -/

section mul_opposite
open mul_opposite
variables [add_comm_monoid Î±] [topological_space Î±] {f : Î² â†’ Î±} {a : Î±}

lemma has_sum.op (hf : has_sum f a) : has_sum (Î» a, op (f a)) (op a) :=
(hf.map (@op_add_equiv Î± _) continuous_op : _)

lemma summable.op (hf : summable f) : summable (op âˆ˜ f) := hf.has_sum.op.summable

lemma has_sum.unop {f : Î² â†’ Î±áµáµ’áµ–} {a : Î±áµáµ’áµ–} (hf : has_sum f a) :
  has_sum (Î» a, unop (f a)) (unop a) :=
(hf.map (@op_add_equiv Î± _).symm continuous_unop : _)

lemma summable.unop {f : Î² â†’ Î±áµáµ’áµ–} (hf : summable f) : summable (unop âˆ˜ f) :=
hf.has_sum.unop.summable

@[simp] lemma has_sum_op : has_sum (Î» a, op (f a)) (op a) â†” has_sum f a :=
âŸ¨has_sum.unop, has_sum.opâŸ©

@[simp] lemma has_sum_unop {f : Î² â†’ Î±áµáµ’áµ–} {a : Î±áµáµ’áµ–} :
  has_sum (Î» a, unop (f a)) (unop a) â†” has_sum f a :=
âŸ¨has_sum.op, has_sum.unopâŸ©

@[simp] lemma summable_op : summable (Î» a, op (f a)) â†” summable f := âŸ¨summable.unop, summable.opâŸ©

@[simp] lemma summable_unop {f : Î² â†’ Î±áµáµ’áµ–} : summable (Î» a, unop (f a)) â†” summable f :=
âŸ¨summable.op, summable.unopâŸ©

variables [t2_space Î±]

lemma tsum_op : âˆ‘' x, mul_opposite.op (f x) = mul_opposite.op (âˆ‘' x, f x) :=
begin
  by_cases h : summable f,
  { exact h.has_sum.op.tsum_eq },
  { have ho := summable_op.not.mpr h,
    rw [tsum_eq_zero_of_not_summable h, tsum_eq_zero_of_not_summable ho, mul_opposite.op_zero] }
end

lemma tsum_unop {f : Î² â†’ Î±áµáµ’áµ–} : âˆ‘' x, mul_opposite.unop (f x) = mul_opposite.unop (âˆ‘' x, f x) :=
mul_opposite.op_injective tsum_op.symm

end mul_opposite

/-! ### Interaction with the star -/

section has_continuous_star
variables [add_comm_monoid Î±] [topological_space Î±] [star_add_monoid Î±] [has_continuous_star Î±]
  {f : Î² â†’ Î±} {a : Î±}

lemma has_sum.star (h : has_sum f a) : has_sum (Î» b, star (f b)) (star a) :=
by simpa only using h.map (star_add_equiv : Î± â‰ƒ+ Î±) continuous_star

lemma summable.star (hf : summable f) : summable (Î» b, star (f b)) :=
hf.has_sum.star.summable

lemma summable.of_star (hf : summable (Î» b, star (f b))) : summable f :=
by simpa only [star_star] using hf.star

@[simp] lemma summable_star_iff : summable (Î» b, star (f b)) â†” summable f :=
âŸ¨summable.of_star, summable.starâŸ©

@[simp] lemma summable_star_iff' : summable (star f) â†” summable f := summable_star_iff

variables [t2_space Î±]

lemma tsum_star : star (âˆ‘' b, f b) = âˆ‘' b, star (f b) :=
begin
  by_cases hf : summable f,
  { exact hf.has_sum.star.tsum_eq.symm, },
  { rw [tsum_eq_zero_of_not_summable hf, tsum_eq_zero_of_not_summable (mt summable.of_star hf),
        star_zero] },
end

end has_continuous_star
