/-
Copyright (c) 2022 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import topology.algebra.module.weak_dual
import algebra.algebra.spectrum

/-!
# Character space of a topological algebra

The character space of a topological algebra is the subset of elements of the weak dual that
are also algebra homomorphisms. This space is used in the Gelfand transform, which gives an
isomorphism between a commutative Câ‹†-algebra and continuous functions on the character space
of the algebra. This, in turn, is used to construct the continuous functional calculus on
Câ‹†-algebras.


## Implementation notes

We define `character_space ğ•œ A` as a subset of the weak dual, which automatically puts the
correct topology on the space. We then define `to_alg_hom` which provides the algebra homomorphism
corresponding to any element. We also provide `to_clm` which provides the element as a
continuous linear map. (Even though `weak_dual ğ•œ A` is a type copy of `A â†’L[ğ•œ] ğ•œ`, this is
often more convenient.)

## Tags

character space, Gelfand transform, functional calculus

-/

namespace weak_dual

/-- The character space of a topological algebra is the subset of elements of the weak dual that
are also algebra homomorphisms. -/
def character_space (ğ•œ : Type*) (A : Type*) [comm_semiring ğ•œ] [topological_space ğ•œ]
  [has_continuous_add ğ•œ] [has_continuous_const_smul ğ•œ ğ•œ]
  [non_unital_non_assoc_semiring A] [topological_space A] [module ğ•œ A] :=
  {Ï† : weak_dual ğ•œ A | (Ï† â‰  0) âˆ§ (âˆ€ (x y : A), Ï† (x * y) = (Ï† x) * (Ï† y))}

variables {ğ•œ : Type*} {A : Type*}

namespace character_space

section non_unital_non_assoc_semiring

variables [comm_semiring ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [has_continuous_const_smul ğ•œ ğ•œ] [non_unital_non_assoc_semiring A] [topological_space A]
  [module ğ•œ A]

lemma coe_apply (Ï† : character_space ğ•œ A) (x : A) : (Ï† : weak_dual ğ•œ A) x = Ï† x := rfl

/-- An element of the character space, as a continuous linear map. -/
def to_clm (Ï† : character_space ğ•œ A) : A â†’L[ğ•œ] ğ•œ := (Ï† : weak_dual ğ•œ A)

lemma to_clm_apply (Ï† : character_space ğ•œ A) (x : A) : Ï† x = to_clm Ï† x := rfl

/-- An element of the character space, as an non-unital algebra homomorphism. -/
@[simps] def to_non_unital_alg_hom (Ï† : character_space ğ•œ A) : A â†’â‚™â‚[ğ•œ] ğ•œ :=
{ to_fun := (Ï† : A â†’ ğ•œ),
  map_mul' := Ï†.prop.2,
  map_smul' := (to_clm Ï†).map_smul,
  map_zero' := continuous_linear_map.map_zero _,
  map_add' := continuous_linear_map.map_add _ }

lemma map_zero (Ï† : character_space ğ•œ A) : Ï† 0 = 0 := (to_non_unital_alg_hom Ï†).map_zero
lemma map_add (Ï† : character_space ğ•œ A) (x y : A) : Ï† (x + y) = Ï† x + Ï† y :=
  (to_non_unital_alg_hom Ï†).map_add _ _
lemma map_smul (Ï† : character_space ğ•œ A) (r : ğ•œ) (x : A) : Ï† (r â€¢ x) = r â€¢ (Ï† x) :=
  (to_clm Ï†).map_smul _ _
lemma map_mul (Ï† : character_space ğ•œ A) (x y : A) : Ï† (x * y) = Ï† x * Ï† y :=
  (to_non_unital_alg_hom Ï†).map_mul _ _
lemma continuous (Ï† : character_space ğ•œ A) : continuous Ï† := (to_clm Ï†).continuous

end non_unital_non_assoc_semiring

section unital

variables [comm_ring ğ•œ] [no_zero_divisors ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [has_continuous_const_smul ğ•œ ğ•œ] [topological_space A] [semiring A] [algebra ğ•œ A]

lemma map_one (Ï† : character_space ğ•œ A) : Ï† 1 = 1 :=
begin
  have hâ‚ : (Ï† 1) * (1 - Ï† 1) = 0 := by rw [mul_sub, sub_eq_zero, mul_one, â†map_mul Ï†, one_mul],
  rcases mul_eq_zero.mp hâ‚ with hâ‚‚|hâ‚‚,
  { exfalso,
    apply Ï†.prop.1,
    ext,
    rw [continuous_linear_map.zero_apply, â†one_mul x, coe_apply, map_mul Ï†, hâ‚‚, zero_mul] },
  { rw [sub_eq_zero] at hâ‚‚,
    exact hâ‚‚.symm },
end

/-- An element of the character space, as an algebra homomorphism. -/
@[simps] def to_alg_hom (Ï† : character_space ğ•œ A) : A â†’â‚[ğ•œ] ğ•œ :=
{ map_one' := map_one Ï†,
  commutes' := Î» r, by
  { rw [algebra.algebra_map_eq_smul_one, algebra.id.map_eq_id, ring_hom.id_apply],
    change ((Ï† : weak_dual ğ•œ A) : A â†’L[ğ•œ] ğ•œ) (r â€¢ 1) = r,
    rw [continuous_linear_map.map_smul, algebra.id.smul_eq_mul, coe_apply, map_one Ï†, mul_one] },
  ..to_non_unital_alg_hom Ï† }

lemma eq_set_map_one_map_mul [nontrivial ğ•œ] : character_space ğ•œ A =
  {Ï† : weak_dual ğ•œ A | (Ï† 1 = 1) âˆ§ (âˆ€ (x y : A), Ï† (x * y) = (Ï† x) * (Ï† y))} :=
begin
  ext x,
  refine âŸ¨Î» h, âŸ¨map_one âŸ¨x, hâŸ©, h.2âŸ©, Î» h, âŸ¨_, h.2âŸ©âŸ©,
  rintro rfl,
  simpa using h.1,
end

lemma is_closed [nontrivial ğ•œ] [t2_space ğ•œ] [has_continuous_mul ğ•œ] :
  is_closed (character_space ğ•œ A) :=
begin
  rw [eq_set_map_one_map_mul],
  refine is_closed.inter (is_closed_eq (eval_continuous _) continuous_const) _,
  change is_closed {Ï† : weak_dual ğ•œ A | âˆ€ x y : A, Ï† (x * y) = Ï† x * Ï† y},
  rw [set.set_of_forall],
  refine is_closed_Inter (Î» a, _),
  rw [set.set_of_forall],
  exact is_closed_Inter (Î» _, is_closed_eq (eval_continuous _)
    ((eval_continuous _).mul (eval_continuous _)))
end

end unital

section ring

variables [comm_ring ğ•œ] [no_zero_divisors ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [has_continuous_const_smul ğ•œ ğ•œ] [topological_space A] [ring A] [algebra ğ•œ A]

lemma apply_mem_spectrum [nontrivial ğ•œ] (Ï† : character_space ğ•œ A) (a : A) : Ï† a âˆˆ spectrum ğ•œ a :=
(to_alg_hom Ï†).apply_mem_spectrum a

end ring

end character_space

end weak_dual
