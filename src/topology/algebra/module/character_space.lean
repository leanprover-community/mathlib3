/-
Copyright (c) 2022 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import topology.algebra.module.weak_dual
import algebra.algebra.spectrum
import topology.continuous_function.algebra

/-!
# Character space of a topological algebra

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

The character space of a topological algebra is the subset of elements of the weak dual that
are also algebra homomorphisms. This space is used in the Gelfand transform, which gives an
isomorphism between a commutative Câ‹†-algebra and continuous functions on the character space
of the algebra. This, in turn, is used to construct the continuous functional calculus on
Câ‹†-algebras.


## Implementation notes

We define `character_space ğ•œ A` as a subset of the weak dual, which automatically puts the
correct topology on the space. We then define `to_alg_hom` which provides the algebra homomorphism
corresponding to any element. We also provide `to_clm` which provides the element as a
continuous linear map. (Even though `weak_dual ğ•œ A` is a type copy of `A â†’L[ğ•œ] ğ•œ`, this is
often more convenient.)

## Tags

character space, Gelfand transform, functional calculus

-/

namespace weak_dual

/-- The character space of a topological algebra is the subset of elements of the weak dual that
are also algebra homomorphisms. -/
def character_space (ğ•œ : Type*) (A : Type*) [comm_semiring ğ•œ] [topological_space ğ•œ]
  [has_continuous_add ğ•œ] [has_continuous_const_smul ğ•œ ğ•œ]
  [non_unital_non_assoc_semiring A] [topological_space A] [module ğ•œ A] :=
  {Ï† : weak_dual ğ•œ A | (Ï† â‰  0) âˆ§ (âˆ€ (x y : A), Ï† (x * y) = (Ï† x) * (Ï† y))}

variables {ğ•œ : Type*} {A : Type*}

namespace character_space

section non_unital_non_assoc_semiring

variables [comm_semiring ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [has_continuous_const_smul ğ•œ ğ•œ] [non_unital_non_assoc_semiring A] [topological_space A]
  [module ğ•œ A]

@[simp, norm_cast, protected]
lemma coe_coe (Ï† : character_space ğ•œ A) : â‡‘(Ï† : weak_dual ğ•œ A) = Ï† := rfl

/-- Elements of the character space are continuous linear maps. -/
instance : continuous_linear_map_class (character_space ğ•œ A) ğ•œ A ğ•œ :=
{ coe := Î» Ï†, (Ï† : A â†’ ğ•œ),
  coe_injective' := Î» Ï† Ïˆ h, by { ext, exact congr_fun h x },
  map_smulâ‚›â‚— := Î» Ï†, (Ï† : weak_dual ğ•œ A).map_smul,
  map_add := Î» Ï†, (Ï† : weak_dual ğ•œ A).map_add,
  map_continuous := Î» Ï†, (Ï† : weak_dual ğ•œ A).cont }

@[ext] lemma ext {Ï† Ïˆ : character_space ğ•œ A} (h : âˆ€ x, Ï† x = Ïˆ x) : Ï† = Ïˆ := fun_like.ext _ _ h

/-- An element of the character space, as a continuous linear map. -/
def to_clm (Ï† : character_space ğ•œ A) : A â†’L[ğ•œ] ğ•œ := (Ï† : weak_dual ğ•œ A)

@[simp] lemma coe_to_clm (Ï† : character_space ğ•œ A) : â‡‘(to_clm Ï†) = Ï† := rfl

/-- Elements of the character space are non-unital algebra homomorphisms. -/
instance : non_unital_alg_hom_class (character_space ğ•œ A) ğ•œ A ğ•œ :=
{ map_smul := Î» Ï†, map_smul Ï†,
  map_zero := Î» Ï†, map_zero Ï†,
  map_mul := Î» Ï†, Ï†.prop.2,
  .. character_space.continuous_linear_map_class }

/-- An element of the character space, as an non-unital algebra homomorphism. -/
def to_non_unital_alg_hom (Ï† : character_space ğ•œ A) : A â†’â‚™â‚[ğ•œ] ğ•œ :=
{ to_fun := (Ï† : A â†’ ğ•œ),
  map_mul' := map_mul Ï†,
  map_smul' := map_smul Ï†,
  map_zero' := map_zero Ï†,
  map_add' := map_add Ï† }

@[simp]
lemma coe_to_non_unital_alg_hom (Ï† : character_space ğ•œ A) : â‡‘(to_non_unital_alg_hom Ï†) = Ï† := rfl

instance [subsingleton A] : is_empty (character_space ğ•œ A) :=
âŸ¨Î» Ï†, Ï†.prop.1 $ continuous_linear_map.ext (Î» x, by simp only [subsingleton.elim x 0, map_zero])âŸ©

variables (ğ•œ A)

lemma union_zero :
  character_space ğ•œ A âˆª {0} = {Ï† : weak_dual ğ•œ A | âˆ€ (x y : A), Ï† (x * y) = (Ï† x) * (Ï† y)} :=
le_antisymm
  (by { rintros Ï† (hÏ† | hâ‚€), { exact hÏ†.2 }, { exact Î» x y, by simp [set.eq_of_mem_singleton hâ‚€] }})
  (Î» Ï† hÏ†, or.elim (em $ Ï† = 0) (Î» hâ‚€, or.inr hâ‚€) (Î» hâ‚€, or.inl âŸ¨hâ‚€, hÏ†âŸ©))

/-- The `character_space ğ•œ A` along with `0` is always a closed set in `weak_dual ğ•œ A`. -/
lemma union_zero_is_closed [t2_space ğ•œ] [has_continuous_mul ğ•œ] :
  is_closed (character_space ğ•œ A âˆª {0}) :=
begin
  simp only [union_zero, set.set_of_forall],
  exact is_closed_Inter (Î» x, is_closed_Inter $ Î» y, is_closed_eq (eval_continuous _) $
    (eval_continuous _).mul (eval_continuous _))
end

end non_unital_non_assoc_semiring

section unital

variables [comm_ring ğ•œ] [no_zero_divisors ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [has_continuous_const_smul ğ•œ ğ•œ] [topological_space A] [semiring A] [algebra ğ•œ A]

/-- In a unital algebra, elements of the character space are algebra homomorphisms. -/
instance : alg_hom_class (character_space ğ•œ A) ğ•œ A ğ•œ :=
have map_one' : âˆ€ Ï† : character_space ğ•œ A, Ï† 1 = 1 := Î» Ï†,
begin
  have hâ‚ : (Ï† 1) * (1 - Ï† 1) = 0 := by rw [mul_sub, sub_eq_zero, mul_one, â†map_mul Ï†, one_mul],
  rcases mul_eq_zero.mp hâ‚ with hâ‚‚ | hâ‚‚,
  { have : âˆ€ a, Ï† (a * 1) = 0 := Î» a, by simp only [map_mul Ï†, hâ‚‚, mul_zero],
    exact false.elim (Ï†.prop.1 $ continuous_linear_map.ext $ by simpa only [mul_one] using this) },
  { exact (sub_eq_zero.mp hâ‚‚).symm },
end,
{ map_one := map_one',
  commutes := Î» Ï† r,
  begin
  { rw [algebra.algebra_map_eq_smul_one, algebra.id.map_eq_id, ring_hom.id_apply],
    change ((Ï† : weak_dual ğ•œ A) : A â†’L[ğ•œ] ğ•œ) (r â€¢ 1) = r,
    rw [map_smul, algebra.id.smul_eq_mul, character_space.coe_coe, map_one' Ï†, mul_one] },
  end,
  .. character_space.non_unital_alg_hom_class }

/-- An element of the character space of a unital algebra, as an algebra homomorphism. -/
@[simps] def to_alg_hom (Ï† : character_space ğ•œ A) : A â†’â‚[ğ•œ] ğ•œ :=
{ map_one' := map_one Ï†,
  commutes' := alg_hom_class.commutes Ï†,
  ..to_non_unital_alg_hom Ï† }

lemma eq_set_map_one_map_mul [nontrivial ğ•œ] : character_space ğ•œ A =
  {Ï† : weak_dual ğ•œ A | (Ï† 1 = 1) âˆ§ (âˆ€ (x y : A), Ï† (x * y) = (Ï† x) * (Ï† y))} :=
begin
  ext x,
  refine âŸ¨Î» h, âŸ¨map_one (âŸ¨x, hâŸ© : character_space ğ•œ A), h.2âŸ©, Î» h, âŸ¨_, h.2âŸ©âŸ©,
  rintro rfl,
  simpa using h.1,
end

/-- under suitable mild assumptions on `ğ•œ`, the character space is a closed set in
`weak_dual ğ•œ A`. -/
protected lemma is_closed [nontrivial ğ•œ] [t2_space ğ•œ] [has_continuous_mul ğ•œ] :
  is_closed (character_space ğ•œ A) :=
begin
  rw [eq_set_map_one_map_mul, set.set_of_and],
  refine is_closed.inter (is_closed_eq (eval_continuous _) continuous_const) _,
  simpa only [(union_zero ğ•œ A).symm] using union_zero_is_closed _ _,
end

end unital

section ring

variables [comm_ring ğ•œ] [no_zero_divisors ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [has_continuous_const_smul ğ•œ ğ•œ] [topological_space A] [ring A] [algebra ğ•œ A]

lemma apply_mem_spectrum [nontrivial ğ•œ] (Ï† : character_space ğ•œ A) (a : A) : Ï† a âˆˆ spectrum ğ•œ a :=
alg_hom.apply_mem_spectrum Ï† a

lemma ext_ker {Ï† Ïˆ : character_space ğ•œ A} (h : ring_hom.ker Ï† = ring_hom.ker Ïˆ) : Ï† = Ïˆ :=
begin
  ext,
  have : x - algebra_map ğ•œ A (Ïˆ x) âˆˆ ring_hom.ker Ï†,
  { simpa only [h, ring_hom.mem_ker, map_sub, alg_hom_class.commutes] using sub_self (Ïˆ x) },
  { rwa [ring_hom.mem_ker, map_sub, alg_hom_class.commutes, sub_eq_zero] at this, }
end

end ring

end character_space

section kernel

variables [field ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ] [has_continuous_const_smul ğ•œ ğ•œ]
variables [ring A] [topological_space A] [algebra ğ•œ A]

/-- The `ring_hom.ker` of `Ï† : character_space ğ•œ A` is maximal. -/
instance ker_is_maximal (Ï† : character_space ğ•œ A) : (ring_hom.ker Ï†).is_maximal :=
ring_hom.ker_is_maximal_of_surjective Ï† $ Î» z, âŸ¨algebra_map ğ•œ A z,
  by simp only [alg_hom_class.commutes, algebra.id.map_eq_id, ring_hom.id_apply]âŸ©

end kernel

section gelfand_transform

open continuous_map

variables (ğ•œ A) [comm_ring ğ•œ] [no_zero_divisors ğ•œ] [topological_space ğ•œ]
  [topological_ring ğ•œ] [topological_space A] [semiring A] [algebra ğ•œ A]

/-- The **Gelfand transform** is an algebra homomorphism (over `ğ•œ`) from a topological `ğ•œ`-algebra
`A` into the `ğ•œ`-algebra of continuous `ğ•œ`-valued functions on the `character_space ğ•œ A`.
The character space itself consists of all algebra homomorphisms from `A` to `ğ•œ`.  -/
@[simps] def gelfand_transform : A â†’â‚[ğ•œ] C(character_space ğ•œ A, ğ•œ) :=
{ to_fun := Î» a,
  { to_fun := Î» Ï†, Ï† a,
    continuous_to_fun := (eval_continuous a).comp continuous_induced_dom },
    map_one' := by {ext, simp only [coe_mk, coe_one, pi.one_apply, map_one a] },
    map_mul' := Î» a b, by {ext, simp only [map_mul, coe_mk, coe_mul, pi.mul_apply] },
    map_zero' := by {ext, simp only [map_zero, coe_mk, coe_mul, coe_zero, pi.zero_apply], },
    map_add' :=  Î» a b, by {ext, simp only [map_add, coe_mk, coe_add, pi.add_apply] },
    commutes' := Î» k, by {ext, simp only [alg_hom_class.commutes, algebra.id.map_eq_id,
      ring_hom.id_apply, coe_mk, algebra_map_apply, algebra.id.smul_eq_mul, mul_one] } }

end gelfand_transform


end weak_dual
