/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Anatole Dedecker
-/
import analysis.locally_convex.balanced_core_hull
import topology.algebra.module.determinant

/-!
# Finite dimensional topological vector spaces over complete fields

Let `ğ•œ` be a complete nontrivially normed field, and `E` a topological vector space (TVS) over
`ğ•œ` (i.e we have `[add_comm_group E] [module ğ•œ E] [topological_space E] [topological_add_group E]`
and `[has_continuous_smul ğ•œ E]`).

If `E` is finite dimensional and Hausdorff, then all linear maps from `E` to any other TVS are
continuous.

When `E` is a normed space, this gets us the equivalence of norms in finite dimension.

## Main results :

* `linear_map.continuous_iff_is_closed_ker` : a linear form is continuous if and only if its kernel
  is closed.
* `linear_map.continuous_of_finite_dimensional` : a linear map on a finite-dimensional Hausdorff
  space over a complete field is continuous.

## TODO

Generalize more of `analysis/normed_space/finite_dimension` to general TVSs.

## Implementation detail

The main result from which everything follows is the fact that, if `Î¾ : Î¹ â†’ E` is a finite basis,
then `Î¾.equiv_fun : E â†’â‚— (Î¹ â†’ ğ•œ)` is continuous. However, for technical reasons, it is easier to
prove this when `Î¹` and `E` live ine the same universe. So we start by doing that as a private
lemma, then we deduce `linear_map.continuous_of_finite_dimensional` from it, and then the general
result follows as `continuous_equiv_fun_basis`.

-/

universes u v w x

noncomputable theory

open set finite_dimensional topological_space filter
open_locale big_operators

section field

variables {ğ•œ E F : Type*} [field ğ•œ] [topological_space ğ•œ] [add_comm_group E] [module ğ•œ E]
  [topological_space E] [add_comm_group F] [module ğ•œ F] [topological_space F]
  [topological_add_group F] [has_continuous_smul ğ•œ F]

/-- The space of continuous linear maps between finite-dimensional spaces is finite-dimensional. -/
instance [finite_dimensional ğ•œ E] [finite_dimensional ğ•œ F] :
  finite_dimensional ğ•œ (E â†’L[ğ•œ] F) :=
finite_dimensional.of_injective
  (continuous_linear_map.coe_lm ğ•œ : (E â†’L[ğ•œ] F) â†’â‚—[ğ•œ] (E â†’â‚—[ğ•œ] F))
  continuous_linear_map.coe_injective

end field

section normed_field

variables {ğ•œ : Type u} [hnorm : nontrivially_normed_field ğ•œ]
  {E : Type v} [add_comm_group E] [module ğ•œ E] [topological_space E]
  [topological_add_group E] [has_continuous_smul ğ•œ E]
  {F : Type w} [add_comm_group F] [module ğ•œ F] [topological_space F]
  [topological_add_group F] [has_continuous_smul ğ•œ F]
  {F' : Type x} [add_comm_group F'] [module ğ•œ F'] [topological_space F']
  [topological_add_group F'] [has_continuous_smul ğ•œ F']

include hnorm

/-- If `ğ•œ` is a nontrivially normed field, any T2 topology on `ğ•œ` which makes it a topological
vector space over itself (with the norm topology) is *equal* to the norm topology. -/
lemma unique_topology_of_t2 {t : topological_space ğ•œ}
  (hâ‚ : @topological_add_group ğ•œ t _)
  (hâ‚‚ : @has_continuous_smul ğ•œ ğ•œ _ hnorm.to_uniform_space.to_topological_space t)
  (hâ‚ƒ : @t2_space ğ•œ t) :
  t = hnorm.to_uniform_space.to_topological_space :=
begin
  -- Let `ğ“£â‚€` denote the topology on `ğ•œ` induced by the norm, and `ğ“£` be any T2 vector
  -- topology on `ğ•œ`. To show that `ğ“£â‚€ = ğ“£`, it suffices to show that they have the same
  -- neighborhoods of 0.
  refine topological_add_group.ext hâ‚ infer_instance (le_antisymm _ _),
  { -- To show `ğ“£ â‰¤ ğ“£â‚€`, we have to show that closed balls are `ğ“£`-neighborhoods of 0.
    rw metric.nhds_basis_closed_ball.ge_iff,
    -- Let `Îµ > 0`. Since `ğ•œ` is nontrivially normed, we have `0 < â€–Î¾â‚€â€– < Îµ` for some `Î¾â‚€ : ğ•œ`.
    intros Îµ hÎµ,
    rcases normed_field.exists_norm_lt ğ•œ hÎµ with âŸ¨Î¾â‚€, hÎ¾â‚€, hÎ¾â‚€ÎµâŸ©,
    -- Since `Î¾â‚€ â‰  0` and `ğ“£` is T2, we know that `{Î¾â‚€}á¶œ` is a `ğ“£`-neighborhood of 0.
    have : {Î¾â‚€}á¶œ âˆˆ @nhds ğ•œ t 0 :=
      is_open.mem_nhds is_open_compl_singleton (ne.symm $ norm_ne_zero_iff.mp hÎ¾â‚€.ne.symm),
    -- Thus, its balanced core `ğ“‘` is too. Let's show that the closed ball of radius `Îµ` contains
    -- `ğ“‘`, which will imply that the closed ball is indeed a `ğ“£`-neighborhood of 0.
    have : balanced_core ğ•œ {Î¾â‚€}á¶œ âˆˆ @nhds ğ•œ t 0 := balanced_core_mem_nhds_zero this,
    refine mem_of_superset this (Î» Î¾ hÎ¾, _),
    -- Let `Î¾ âˆˆ ğ“‘`. We want to show `â€–Î¾â€– < Îµ`. If `Î¾ = 0`, this is trivial.
    by_cases hÎ¾0 : Î¾ = 0,
    { rw hÎ¾0,
      exact metric.mem_closed_ball_self hÎµ.le },
    { rw [mem_closed_ball_zero_iff],
      -- Now suppose `Î¾ â‰  0`. By contradiction, let's assume `Îµ < â€–Î¾â€–`, and show that
      -- `Î¾â‚€ âˆˆ ğ“‘ âŠ† {Î¾â‚€}á¶œ`, which is a contradiction.
      by_contra' h,
      suffices : (Î¾â‚€ * Î¾â»Â¹) â€¢ Î¾ âˆˆ balanced_core ğ•œ {Î¾â‚€}á¶œ,
      { rw [smul_eq_mul ğ•œ, mul_assoc, inv_mul_cancel hÎ¾0, mul_one] at this,
        exact not_mem_compl_iff.mpr (mem_singleton Î¾â‚€) ((balanced_core_subset _) this) },
      -- For that, we use that `ğ“‘` is balanced : since `â€–Î¾â‚€â€– < Îµ < â€–Î¾â€–`, we have `â€–Î¾â‚€ / Î¾â€– â‰¤ 1`,
      -- hence `Î¾â‚€ = (Î¾â‚€ / Î¾) â€¢ Î¾ âˆˆ ğ“‘` because `Î¾ âˆˆ ğ“‘`.
      refine (balanced_core_balanced _).smul_mem _ hÎ¾,
      rw [norm_mul, norm_inv, mul_inv_le_iff (norm_pos_iff.mpr hÎ¾0), mul_one],
      exact (hÎ¾â‚€Îµ.trans h).le } },
  { -- Finally, to show `ğ“£â‚€ â‰¤ ğ“£`, we simply argue that `id = (Î» x, x â€¢ 1)` is continuous from
    -- `(ğ•œ, ğ“£â‚€)` to `(ğ•œ, ğ“£)` because `(â€¢) : (ğ•œ, ğ“£â‚€) Ã— (ğ•œ, ğ“£) â†’ (ğ•œ, ğ“£)` is continuous.
    calc (@nhds ğ•œ hnorm.to_uniform_space.to_topological_space 0)
        = map id (@nhds ğ•œ hnorm.to_uniform_space.to_topological_space 0) : map_id.symm
    ... = map (Î» x, id x â€¢ 1) (@nhds ğ•œ hnorm.to_uniform_space.to_topological_space 0) :
        by conv_rhs {congr, funext, rw [smul_eq_mul, mul_one]}; refl
    ... â‰¤ (@nhds ğ•œ t ((0 : ğ•œ) â€¢ 1)) :
        @tendsto.smul_const _ _ _ hnorm.to_uniform_space.to_topological_space t _ _ _ _ _
          tendsto_id (1 : ğ•œ)
    ... = (@nhds ğ•œ t 0) : by rw zero_smul }
end

/-- Any linear form on a topological vector space over a nontrivially normed field is continuous if
    its kernel is closed. -/
lemma linear_map.continuous_of_is_closed_ker (l : E â†’â‚—[ğ•œ] ğ•œ) (hl : is_closed (l.ker : set E)) :
  continuous l :=
begin
  -- `l` is either constant or surjective. If it is constant, the result is trivial.
  by_cases H : finrank ğ•œ l.range = 0,
  { rw [finrank_eq_zero, linear_map.range_eq_bot] at H,
    rw H,
    exact continuous_zero },
  { -- In the case where `l` is surjective, we factor it as `Ï† : (E â§¸ l.ker) â‰ƒâ‚—[ğ•œ] ğ•œ`. Note that
    -- `E â§¸ l.ker` is T2 since `l.ker` is closed.
    have : finrank ğ•œ l.range = 1,
      from le_antisymm (finrank_self ğ•œ â–¸ l.range.finrank_le) (zero_lt_iff.mpr H),
    have hi : function.injective (l.ker.liftq l (le_refl _)),
    { rw [â† linear_map.ker_eq_bot],
      exact submodule.ker_liftq_eq_bot _ _ _ (le_refl _) },
    have hs : function.surjective (l.ker.liftq l (le_refl _)),
    { rw [â† linear_map.range_eq_top, submodule.range_liftq],
      exact eq_top_of_finrank_eq ((finrank_self ğ•œ).symm â–¸ this) },
    let Ï† : (E â§¸ l.ker) â‰ƒâ‚—[ğ•œ] ğ•œ := linear_equiv.of_bijective (l.ker.liftq l (le_refl _)) âŸ¨hi, hsâŸ©,
    have hlÏ† : (l : E â†’ ğ•œ) = Ï† âˆ˜ l.ker.mkq,
      by ext; refl,
    -- Since the quotient map `E â†’â‚—[ğ•œ] (E â§¸ l.ker)` is continuous, the continuity of `l` will follow
    -- form the continuity of `Ï†`.
    suffices : continuous Ï†.to_equiv,
    { rw hlÏ†,
      exact this.comp continuous_quot_mk },
    -- The pullback by `Ï†.symm` of the quotient topology is a T2 topology on `ğ•œ`, because `Ï†.symm`
    -- is injective. Since `Ï†.symm` is linear, it is also a vector space topology.
    -- Hence, we know that it is equal to the topology induced by the norm.
    have : induced Ï†.to_equiv.symm infer_instance = hnorm.to_uniform_space.to_topological_space,
    { refine unique_topology_of_t2 (topological_add_group_induced Ï†.symm.to_linear_map)
        (has_continuous_smul_induced Ï†.symm.to_linear_map) _,
      rw t2_space_iff,
      exact Î» x y hxy, @separated_by_continuous _ _ (induced _ _) _ _ _
        continuous_induced_dom _ _ (Ï†.to_equiv.symm.injective.ne hxy) },
    -- Finally, the pullback by `Ï†.symm` is exactly the pushforward by `Ï†`, so we have to prove
    -- that `Ï†` is continuous when `ğ•œ` is endowed with the pushforward by `Ï†` of the quotient
    -- topology, which is trivial by definition of the pushforward.
    rw [this.symm, equiv.induced_symm],
    exact continuous_coinduced_rng }
end

/-- Any linear form on a topological vector space over a nontrivially normed field is continuous if
    and only if its kernel is closed. -/
lemma linear_map.continuous_iff_is_closed_ker (l : E â†’â‚—[ğ•œ] ğ•œ) :
  continuous l â†” is_closed (l.ker : set E) :=
âŸ¨Î» h, is_closed_singleton.preimage h, l.continuous_of_is_closed_kerâŸ©

/-- Over a nontrivially normed field, any linear form which is nonzero on a nonempty open set is
    automatically continuous. -/
lemma linear_map.continuous_of_nonzero_on_open (l : E â†’â‚—[ğ•œ] ğ•œ) (s : set E) (hsâ‚ : is_open s)
  (hsâ‚‚ : s.nonempty) (hsâ‚ƒ : âˆ€ x âˆˆ s, l x â‰  0) : continuous l :=
begin
  refine l.continuous_of_is_closed_ker (l.is_closed_or_dense_ker.resolve_right $ Î» hl, _),
  rcases hsâ‚‚ with âŸ¨x, hxâŸ©,
  have : x âˆˆ interior (l.ker : set E)á¶œ,
  { rw mem_interior_iff_mem_nhds,
    exact mem_of_superset (hsâ‚.mem_nhds hx) hsâ‚ƒ },
  rwa hl.interior_compl at this
end

variables [complete_space ğ•œ]

/-- This version imposes `Î¹` and `E` to live in the same universe, so you should instead use
`continuous_equiv_fun_basis` which gives the same result without universe restrictions. -/
private lemma continuous_equiv_fun_basis_aux [ht2 : t2_space E] {Î¹ : Type v} [fintype Î¹]
  (Î¾ : basis Î¹ ğ•œ E) : continuous Î¾.equiv_fun :=
begin
  letI : uniform_space E := topological_add_group.to_uniform_space E,
  letI : uniform_add_group E := topological_add_comm_group_is_uniform,
  letI : separated_space E := separated_iff_t2.mpr ht2,
  unfreezingI { induction hn : fintype.card Î¹ with n IH generalizing Î¹ E },
  { rw fintype.card_eq_zero_iff at hn,
    exact continuous_of_const (Î» x y, funext hn.elim) },
  { haveI : finite_dimensional ğ•œ E := of_fintype_basis Î¾,
    -- first step: thanks to the induction hypothesis, any n-dimensional subspace is equivalent
    -- to a standard space of dimension n, hence it is complete and therefore closed.
    have Hâ‚ : âˆ€s : submodule ğ•œ E, finrank ğ•œ s = n â†’ is_closed (s : set E),
    { assume s s_dim,
      letI : uniform_add_group s := s.to_add_subgroup.uniform_add_group,
      let b := basis.of_vector_space ğ•œ s,
      have U : uniform_embedding b.equiv_fun.symm.to_equiv,
      { have : fintype.card (basis.of_vector_space_index ğ•œ s) = n,
          by { rw â† s_dim, exact (finrank_eq_card_basis b).symm },
        have : continuous b.equiv_fun := IH b this,
        exact b.equiv_fun.symm.uniform_embedding b.equiv_fun.symm.to_linear_map.continuous_on_pi
          this },
      have : is_complete (s : set E),
        from complete_space_coe_iff_is_complete.1 ((complete_space_congr U).1 (by apply_instance)),
      exact this.is_closed },
    -- second step: any linear form is continuous, as its kernel is closed by the first step
    have Hâ‚‚ : âˆ€f : E â†’â‚—[ğ•œ] ğ•œ, continuous f,
    { assume f,
      by_cases H : finrank ğ•œ f.range = 0,
      { rw [finrank_eq_zero, linear_map.range_eq_bot] at H,
        rw H,
        exact continuous_zero },
      { have : finrank ğ•œ f.ker = n,
        { have Z := f.finrank_range_add_finrank_ker,
          rw [finrank_eq_card_basis Î¾, hn] at Z,
          have : finrank ğ•œ f.range = 1,
            from le_antisymm (finrank_self ğ•œ â–¸ f.range.finrank_le) (zero_lt_iff.mpr H),
          rw [this, add_comm, nat.add_one] at Z,
          exact nat.succ.inj Z },
        have : is_closed (f.ker : set E),
          from Hâ‚ _ this,
        exact linear_map.continuous_of_is_closed_ker f this } },
    rw continuous_pi_iff,
    intros i,
    change continuous (Î¾.coord i),
    exact Hâ‚‚ (Î¾.coord i) },
end

/-- Any linear map on a finite dimensional space over a complete field is continuous. -/
theorem linear_map.continuous_of_finite_dimensional [t2_space E] [finite_dimensional ğ•œ E]
  (f : E â†’â‚—[ğ•œ] F') :
  continuous f :=
begin
  -- for the proof, go to a model vector space `b â†’ ğ•œ` thanks to `continuous_equiv_fun_basis`, and
  -- argue that all linear maps there are continuous.
  let b := basis.of_vector_space ğ•œ E,
  have A : continuous b.equiv_fun :=
    continuous_equiv_fun_basis_aux b,
  have B : continuous (f.comp (b.equiv_fun.symm : (basis.of_vector_space_index ğ•œ E â†’ ğ•œ) â†’â‚—[ğ•œ] E)) :=
    linear_map.continuous_on_pi _,
  have : continuous ((f.comp (b.equiv_fun.symm : (basis.of_vector_space_index ğ•œ E â†’ ğ•œ) â†’â‚—[ğ•œ] E))
                      âˆ˜ b.equiv_fun) := B.comp A,
  convert this,
  ext x,
  dsimp,
  rw [basis.equiv_fun_symm_apply, basis.sum_repr]
end

instance linear_map.continuous_linear_map_class_of_finite_dimensional
  [t2_space E] [finite_dimensional ğ•œ E] :
  continuous_linear_map_class (E â†’â‚—[ğ•œ] F') ğ•œ E F' :=
{ map_continuous := Î» f, f.continuous_of_finite_dimensional,
  ..linear_map.semilinear_map_class }

/-- In finite dimensions over a non-discrete complete normed field, the canonical identification
(in terms of a basis) with `ğ•œ^n` (endowed with the product topology) is continuous.
This is the key fact wich makes all linear maps from a T2 finite dimensional TVS over such a field
continuous (see `linear_map.continuous_of_finite_dimensional`), which in turn implies that all
norms are equivalent in finite dimensions. -/
theorem continuous_equiv_fun_basis [t2_space E] {Î¹ : Type*} [fintype Î¹] (Î¾ : basis Î¹ ğ•œ E) :
  continuous Î¾.equiv_fun :=
begin
  haveI : finite_dimensional ğ•œ E := of_fintype_basis Î¾,
  exact Î¾.equiv_fun.to_linear_map.continuous_of_finite_dimensional
end

namespace linear_map

variables [t2_space E] [finite_dimensional ğ•œ E]

/-- The continuous linear map induced by a linear map on a finite dimensional space -/
def to_continuous_linear_map : (E â†’â‚—[ğ•œ] F') â‰ƒâ‚—[ğ•œ] E â†’L[ğ•œ] F' :=
{ to_fun := Î» f, âŸ¨f, f.continuous_of_finite_dimensionalâŸ©,
  inv_fun := coe,
  map_add' := Î» f g, rfl,
  map_smul' := Î» c f, rfl,
  left_inv := Î» f, rfl,
  right_inv := Î» f, continuous_linear_map.coe_injective rfl }

@[simp] lemma coe_to_continuous_linear_map' (f : E â†’â‚—[ğ•œ] F') :
  â‡‘f.to_continuous_linear_map = f := rfl

@[simp] lemma coe_to_continuous_linear_map (f : E â†’â‚—[ğ•œ] F') :
  (f.to_continuous_linear_map : E â†’â‚—[ğ•œ] F') = f := rfl

@[simp] lemma coe_to_continuous_linear_map_symm :
  â‡‘(to_continuous_linear_map : (E â†’â‚—[ğ•œ] F') â‰ƒâ‚—[ğ•œ] E â†’L[ğ•œ] F').symm = coe := rfl

@[simp] lemma det_to_continuous_linear_map (f : E â†’â‚—[ğ•œ] E) :
  f.to_continuous_linear_map.det = f.det :=
rfl

@[simp] lemma ker_to_continuous_linear_map (f : E â†’â‚—[ğ•œ] F') :
  ker f.to_continuous_linear_map = ker f :=
rfl

@[simp] lemma range_to_continuous_linear_map (f : E â†’â‚—[ğ•œ] F') :
  range f.to_continuous_linear_map = range f :=
rfl

/-- A surjective linear map `f` with finite dimensional codomain is an open map. -/
lemma is_open_map_of_finite_dimensional (f : F â†’â‚—[ğ•œ] E) (hf : function.surjective f) :
  is_open_map f :=
begin
  rcases f.exists_right_inverse_of_surjective (linear_map.range_eq_top.2 hf) with âŸ¨g, hgâŸ©,
  refine is_open_map.of_sections (Î» x, âŸ¨Î» y, g (y - f x) + x, _, _, Î» y, _âŸ©),
  { exact ((g.continuous_of_finite_dimensional.comp $ continuous_id.sub continuous_const).add
      continuous_const).continuous_at },
  { rw [sub_self, map_zero, zero_add] },
  { simp only [map_sub, map_add, â† comp_apply f g, hg, id_apply, sub_add_cancel] }
end

instance can_lift_continuous_linear_map : can_lift (E â†’â‚—[ğ•œ] F) (E â†’L[ğ•œ] F) coe (Î» _, true) :=
âŸ¨Î» f _, âŸ¨f.to_continuous_linear_map, rflâŸ©âŸ©

end linear_map

namespace linear_equiv

variables [t2_space E] [t2_space F] [finite_dimensional ğ•œ E]

/-- The continuous linear equivalence induced by a linear equivalence on a finite dimensional
space. -/
def to_continuous_linear_equiv (e : E â‰ƒâ‚—[ğ•œ] F) : E â‰ƒL[ğ•œ] F :=
{ continuous_to_fun := e.to_linear_map.continuous_of_finite_dimensional,
  continuous_inv_fun := begin
    haveI : finite_dimensional ğ•œ F := e.finite_dimensional,
    exact e.symm.to_linear_map.continuous_of_finite_dimensional
  end,
  ..e }

@[simp] lemma coe_to_continuous_linear_equiv (e : E â‰ƒâ‚—[ğ•œ] F) :
  (e.to_continuous_linear_equiv : E â†’â‚—[ğ•œ] F) = e := rfl

@[simp] lemma coe_to_continuous_linear_equiv' (e : E â‰ƒâ‚—[ğ•œ] F) :
  (e.to_continuous_linear_equiv : E â†’ F) = e := rfl

@[simp] lemma coe_to_continuous_linear_equiv_symm (e : E â‰ƒâ‚—[ğ•œ] F) :
  (e.to_continuous_linear_equiv.symm : F â†’â‚—[ğ•œ] E) = e.symm := rfl

@[simp] lemma coe_to_continuous_linear_equiv_symm' (e : E â‰ƒâ‚—[ğ•œ] F) :
  (e.to_continuous_linear_equiv.symm : F â†’ E) = e.symm := rfl

@[simp] lemma to_linear_equiv_to_continuous_linear_equiv (e : E â‰ƒâ‚—[ğ•œ] F) :
  e.to_continuous_linear_equiv.to_linear_equiv = e :=
by { ext x, refl }

@[simp] lemma to_linear_equiv_to_continuous_linear_equiv_symm (e : E â‰ƒâ‚—[ğ•œ] F) :
  e.to_continuous_linear_equiv.symm.to_linear_equiv = e.symm :=
by { ext x, refl }

instance can_lift_continuous_linear_equiv :
  can_lift (E â‰ƒâ‚—[ğ•œ] F) (E â‰ƒL[ğ•œ] F) continuous_linear_equiv.to_linear_equiv (Î» _, true) :=
âŸ¨Î» f _, âŸ¨_, f.to_linear_equiv_to_continuous_linear_equivâŸ©âŸ©

end linear_equiv

namespace continuous_linear_map

variables [t2_space E] [finite_dimensional ğ•œ E]

/-- Builds a continuous linear equivalence from a continuous linear map on a finite-dimensional
vector space whose determinant is nonzero. -/
def to_continuous_linear_equiv_of_det_ne_zero
  (f : E â†’L[ğ•œ] E) (hf : f.det â‰  0) : E â‰ƒL[ğ•œ] E :=
((f : E â†’â‚—[ğ•œ] E).equiv_of_det_ne_zero hf).to_continuous_linear_equiv

@[simp] lemma coe_to_continuous_linear_equiv_of_det_ne_zero (f : E â†’L[ğ•œ] E) (hf : f.det â‰  0) :
  (f.to_continuous_linear_equiv_of_det_ne_zero hf : E â†’L[ğ•œ] E) = f :=
by { ext x, refl }

@[simp] lemma to_continuous_linear_equiv_of_det_ne_zero_apply
  (f : E â†’L[ğ•œ] E) (hf : f.det â‰  0) (x : E) :
  f.to_continuous_linear_equiv_of_det_ne_zero hf x = f x :=
rfl

lemma _root_.matrix.to_lin_fin_two_prod_to_continuous_linear_map (a b c d : ğ•œ) :
  (matrix.to_lin (basis.fin_two_prod ğ•œ) (basis.fin_two_prod ğ•œ)
      !![a, b; c, d]).to_continuous_linear_map =
  (a â€¢ continuous_linear_map.fst ğ•œ ğ•œ ğ•œ + b â€¢ continuous_linear_map.snd ğ•œ ğ•œ ğ•œ).prod
  (c â€¢ continuous_linear_map.fst ğ•œ ğ•œ ğ•œ + d â€¢ continuous_linear_map.snd ğ•œ ğ•œ ğ•œ) :=
continuous_linear_map.ext $ matrix.to_lin_fin_two_prod_apply _ _ _ _

end continuous_linear_map

end normed_field
