/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import topology.algebra.module.basic
import linear_algebra.multilinear.basic

/-!
# Continuous multilinear maps

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We define continuous multilinear maps as maps from `Œ†(i : Œπ), M‚ÇÅ i` to `M‚ÇÇ` which are multilinear
and continuous, by extending the space of multilinear maps with a continuity assumption.
Here, `M‚ÇÅ i` and `M‚ÇÇ` are modules over a ring `R`, and `Œπ` is an arbitrary type, and all these
spaces are also topological spaces.

## Main definitions

* `continuous_multilinear_map R M‚ÇÅ M‚ÇÇ` is the space of continuous multilinear maps from
  `Œ†(i : Œπ), M‚ÇÅ i` to `M‚ÇÇ`. We show that it is an `R`-module.

## Implementation notes

We mostly follow the API of multilinear maps.

## Notation

We introduce the notation `M [√ón]‚ÜíL[R] M'` for the space of continuous `n`-multilinear maps from
`M^n` to `M'`. This is a particular case of the general notion (where we allow varying dependent
types as the arguments of our continuous multilinear maps), but arguably the most important one,
especially when defining iterated derivatives.
-/

open function fin set
open_locale big_operators

universes u v w w‚ÇÅ w‚ÇÅ' w‚ÇÇ w‚ÇÉ w‚ÇÑ
variables {R : Type u} {Œπ : Type v} {n : ‚Ñï} {M : fin n.succ ‚Üí Type w} {M‚ÇÅ : Œπ ‚Üí Type w‚ÇÅ}
  {M‚ÇÅ' : Œπ ‚Üí Type w‚ÇÅ'} {M‚ÇÇ : Type w‚ÇÇ} {M‚ÇÉ : Type w‚ÇÉ} {M‚ÇÑ : Type w‚ÇÑ}

/-- Continuous multilinear maps over the ring `R`, from `Œ†i, M‚ÇÅ i` to `M‚ÇÇ` where `M‚ÇÅ i` and `M‚ÇÇ`
are modules over `R` with a topological structure. In applications, there will be compatibility
conditions between the algebraic and the topological structures, but this is not needed for the
definition. -/
structure continuous_multilinear_map (R : Type u) {Œπ : Type v} (M‚ÇÅ : Œπ ‚Üí Type w‚ÇÅ) (M‚ÇÇ : Type w‚ÇÇ)
  [semiring R] [‚àÄi, add_comm_monoid (M‚ÇÅ i)] [add_comm_monoid M‚ÇÇ]
  [‚àÄi, module R (M‚ÇÅ i)] [module R M‚ÇÇ] [‚àÄi, topological_space (M‚ÇÅ i)] [topological_space M‚ÇÇ]
  extends multilinear_map R M‚ÇÅ M‚ÇÇ :=
(cont : continuous to_fun)

notation M `[√ó`:25 n `]‚ÜíL[`:25 R `] ` M' := continuous_multilinear_map R (Œª (i : fin n), M) M'

namespace continuous_multilinear_map

section semiring

variables [semiring R]
[Œ†i, add_comm_monoid (M i)] [Œ†i, add_comm_monoid (M‚ÇÅ i)] [Œ†i, add_comm_monoid (M‚ÇÅ' i)]
  [add_comm_monoid M‚ÇÇ] [add_comm_monoid M‚ÇÉ] [add_comm_monoid M‚ÇÑ] [Œ† i, module R (M i)]
  [Œ† i, module R (M‚ÇÅ i)]  [Œ† i, module R (M‚ÇÅ' i)] [module R M‚ÇÇ]
  [module R M‚ÇÉ] [module R M‚ÇÑ]
  [Œ† i, topological_space (M i)] [Œ† i, topological_space (M‚ÇÅ i)] [Œ† i, topological_space (M‚ÇÅ' i)]
  [topological_space M‚ÇÇ] [topological_space M‚ÇÉ] [topological_space M‚ÇÑ]
(f f' : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ)

theorem to_multilinear_map_injective :
  function.injective (continuous_multilinear_map.to_multilinear_map :
    continuous_multilinear_map R M‚ÇÅ M‚ÇÇ ‚Üí multilinear_map R M‚ÇÅ M‚ÇÇ)
| ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© rfl := rfl

instance continuous_map_class :
  continuous_map_class (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) (Œ† i, M‚ÇÅ i) M‚ÇÇ :=
{ coe := Œª f, f.to_fun,
  coe_injective' := Œª f g h, to_multilinear_map_injective $ multilinear_map.coe_injective h,
  map_continuous := continuous_multilinear_map.cont }

instance : has_coe_to_fun (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) (Œª _, (Œ† i, M‚ÇÅ i) ‚Üí M‚ÇÇ) :=
‚ü®Œª f, f‚ü©

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def simps.apply (L‚ÇÅ : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) (v : Œ† i, M‚ÇÅ i) : M‚ÇÇ := L‚ÇÅ v

initialize_simps_projections continuous_multilinear_map
  (-to_multilinear_map, to_multilinear_map_to_fun ‚Üí apply)

@[continuity] lemma coe_continuous : continuous (f : (Œ† i, M‚ÇÅ i) ‚Üí M‚ÇÇ) := f.cont

@[simp] lemma coe_coe : (f.to_multilinear_map : (Œ† i, M‚ÇÅ i) ‚Üí M‚ÇÇ) = f := rfl

@[ext] theorem ext {f f' : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ} (H : ‚àÄ x, f x = f' x) : f = f' :=
fun_like.ext _ _ H

theorem ext_iff {f f' : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ} : f = f' ‚Üî ‚àÄ x, f x = f' x :=
by rw [‚Üê to_multilinear_map_injective.eq_iff, multilinear_map.ext_iff]; refl

@[simp] lemma map_add [decidable_eq Œπ] (m : Œ†i, M‚ÇÅ i) (i : Œπ) (x y : M‚ÇÅ i) :
  f (update m i (x + y)) = f (update m i x) + f (update m i y) :=
f.map_add' m i x y

@[simp] lemma map_smul [decidable_eq Œπ] (m : Œ†i, M‚ÇÅ i) (i : Œπ) (c : R) (x : M‚ÇÅ i) :
  f (update m i (c ‚Ä¢ x)) = c ‚Ä¢ f (update m i x) :=
f.map_smul' m i c x

lemma map_coord_zero {m : Œ†i, M‚ÇÅ i} (i : Œπ) (h : m i = 0) : f m = 0 :=
f.to_multilinear_map.map_coord_zero i h

@[simp] lemma map_zero [nonempty Œπ] : f 0 = 0 :=
f.to_multilinear_map.map_zero

instance : has_zero (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :=
‚ü®{ cont := continuous_const, ..(0 : multilinear_map R M‚ÇÅ M‚ÇÇ) }‚ü©

instance : inhabited (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) := ‚ü®0‚ü©

@[simp] lemma zero_apply (m : Œ†i, M‚ÇÅ i) : (0 : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) m = 0 := rfl

@[simp] lemma to_multilinear_map_zero :
  (0 : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ).to_multilinear_map = 0 :=
rfl
section has_smul

variables {R' R'' A : Type*} [monoid R'] [monoid R''] [semiring A]
  [Œ† i, module A (M‚ÇÅ i)] [module A M‚ÇÇ]
  [distrib_mul_action R' M‚ÇÇ] [has_continuous_const_smul R' M‚ÇÇ] [smul_comm_class A R' M‚ÇÇ]
  [distrib_mul_action R'' M‚ÇÇ] [has_continuous_const_smul R'' M‚ÇÇ] [smul_comm_class A R'' M‚ÇÇ]

instance : has_smul R' (continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :=
‚ü®Œª c f, { cont := f.cont.const_smul c, .. c ‚Ä¢ f.to_multilinear_map }‚ü©

@[simp] lemma smul_apply (f : continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) (c : R') (m : Œ†i, M‚ÇÅ i) :
  (c ‚Ä¢ f) m = c ‚Ä¢ f m := rfl

@[simp] lemma to_multilinear_map_smul (c : R') (f : continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :
  (c ‚Ä¢ f).to_multilinear_map = c ‚Ä¢ f.to_multilinear_map :=
rfl

instance [smul_comm_class R' R'' M‚ÇÇ] :
  smul_comm_class R' R'' (continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :=
‚ü®Œª c‚ÇÅ c‚ÇÇ f, ext $ Œª x, smul_comm _ _ _‚ü©

instance [has_smul R' R''] [is_scalar_tower R' R'' M‚ÇÇ] :
  is_scalar_tower R' R'' (continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :=
‚ü®Œª c‚ÇÅ c‚ÇÇ f, ext $ Œª x, smul_assoc _ _ _‚ü©

instance [distrib_mul_action R'·µê·µí·µñ M‚ÇÇ] [is_central_scalar R' M‚ÇÇ] :
  is_central_scalar R' (continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :=
‚ü®Œª c‚ÇÅ f, ext $ Œª x, op_smul_eq_smul _ _‚ü©

instance : mul_action R' (continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :=
function.injective.mul_action to_multilinear_map to_multilinear_map_injective (Œª _ _, rfl)

end has_smul

section has_continuous_add
variable [has_continuous_add M‚ÇÇ]

instance : has_add (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :=
‚ü®Œª f f', ‚ü®f.to_multilinear_map + f'.to_multilinear_map, f.cont.add f'.cont‚ü©‚ü©

@[simp] lemma add_apply (m : Œ†i, M‚ÇÅ i) : (f + f') m = f m + f' m := rfl

@[simp] lemma to_multilinear_map_add (f g : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :
  (f + g).to_multilinear_map = f.to_multilinear_map + g.to_multilinear_map :=
rfl

instance add_comm_monoid : add_comm_monoid (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :=
to_multilinear_map_injective.add_comm_monoid _ rfl (Œª _ _, rfl) (Œª _ _, rfl)

/-- Evaluation of a `continuous_multilinear_map` at a vector as an `add_monoid_hom`. -/
def apply_add_hom (m : Œ† i, M‚ÇÅ i) : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ ‚Üí+ M‚ÇÇ :=
‚ü®Œª f, f m, rfl, Œª _ _, rfl‚ü©

@[simp] lemma sum_apply {Œ± : Type*} (f : Œ± ‚Üí continuous_multilinear_map R M‚ÇÅ M‚ÇÇ)
  (m : Œ†i, M‚ÇÅ i) {s : finset Œ±} : (‚àë a in s, f a) m = ‚àë a in s, f a m :=
(apply_add_hom m).map_sum f s

end has_continuous_add

/-- If `f` is a continuous multilinear map, then `f.to_continuous_linear_map m i` is the continuous
linear map obtained by fixing all coordinates but `i` equal to those of `m`, and varying the
`i`-th coordinate. -/
def to_continuous_linear_map [decidable_eq Œπ] (m : Œ†i, M‚ÇÅ i) (i : Œπ) : M‚ÇÅ i ‚ÜíL[R] M‚ÇÇ :=
{ cont := f.cont.comp (continuous_const.update i continuous_id),
  .. f.to_multilinear_map.to_linear_map m i }

/-- The cartesian product of two continuous multilinear maps, as a continuous multilinear map. -/
def prod (f : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) (g : continuous_multilinear_map R M‚ÇÅ M‚ÇÉ) :
  continuous_multilinear_map R M‚ÇÅ (M‚ÇÇ √ó M‚ÇÉ) :=
{ cont := f.cont.prod_mk g.cont,
  .. f.to_multilinear_map.prod g.to_multilinear_map }

@[simp] lemma prod_apply
  (f : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) (g : continuous_multilinear_map R M‚ÇÅ M‚ÇÉ) (m : Œ†i, M‚ÇÅ i) :
  (f.prod g) m = (f m, g m) := rfl

/-- Combine a family of continuous multilinear maps with the same domain and codomains `M' i` into a
continuous multilinear map taking values in the space of functions `Œ† i, M' i`. -/
def pi {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [Œ† i, add_comm_monoid (M' i)] [Œ† i, topological_space (M' i)]
  [Œ† i, module R (M' i)] (f : Œ† i, continuous_multilinear_map R M‚ÇÅ (M' i)) :
  continuous_multilinear_map R M‚ÇÅ (Œ† i, M' i) :=
{ cont := continuous_pi $ Œª i, (f i).coe_continuous,
  to_multilinear_map := multilinear_map.pi (Œª i, (f i).to_multilinear_map) }

@[simp] lemma coe_pi {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [Œ† i, add_comm_monoid (M' i)]
  [Œ† i, topological_space (M' i)] [Œ† i, module R (M' i)]
  (f : Œ† i, continuous_multilinear_map R M‚ÇÅ (M' i)) :
  ‚áë(pi f) = Œª m j, f j m :=
rfl

lemma pi_apply {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [Œ† i, add_comm_monoid (M' i)]
  [Œ† i, topological_space (M' i)] [Œ† i, module R (M' i)]
  (f : Œ† i, continuous_multilinear_map R M‚ÇÅ (M' i)) (m : Œ† i, M‚ÇÅ i) (j : Œπ') :
  pi f m j = f j m :=
rfl

section
variables (R M‚ÇÇ)

/-- The evaluation map from `Œπ ‚Üí M‚ÇÇ` to `M‚ÇÇ` is multilinear at a given `i` when `Œπ` is subsingleton.
-/
@[simps to_multilinear_map apply]
def of_subsingleton [subsingleton Œπ] (i' : Œπ) : continuous_multilinear_map R (Œª _ : Œπ, M‚ÇÇ) M‚ÇÇ :=
{ to_multilinear_map := multilinear_map.of_subsingleton R _ i',
  cont := continuous_apply _ }

variables (M‚ÇÅ) {M‚ÇÇ}

/-- The constant map is multilinear when `Œπ` is empty. -/
@[simps to_multilinear_map apply]
def const_of_is_empty [is_empty Œπ] (m : M‚ÇÇ) : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ :=
{ to_multilinear_map := multilinear_map.const_of_is_empty R _ m,
  cont := continuous_const }

end

/-- If `g` is continuous multilinear and `f` is a collection of continuous linear maps,
then `g (f‚ÇÅ m‚ÇÅ, ..., f‚Çô m‚Çô)` is again a continuous multilinear map, that we call
`g.comp_continuous_linear_map f`. -/
def comp_continuous_linear_map
  (g : continuous_multilinear_map R M‚ÇÅ' M‚ÇÑ) (f : Œ† i : Œπ, M‚ÇÅ i ‚ÜíL[R] M‚ÇÅ' i) :
  continuous_multilinear_map R M‚ÇÅ M‚ÇÑ :=
{ cont := g.cont.comp $ continuous_pi $ Œªj, (f j).cont.comp $ continuous_apply _,
  .. g.to_multilinear_map.comp_linear_map (Œª i, (f i).to_linear_map) }

@[simp] lemma comp_continuous_linear_map_apply (g : continuous_multilinear_map R M‚ÇÅ' M‚ÇÑ)
  (f : Œ† i : Œπ, M‚ÇÅ i ‚ÜíL[R] M‚ÇÅ' i) (m : Œ† i, M‚ÇÅ i) :
  g.comp_continuous_linear_map f m = g (Œª i, f i $ m i) :=
rfl

/-- Composing a continuous multilinear map with a continuous linear map gives again a
continuous multilinear map. -/
def _root_.continuous_linear_map.comp_continuous_multilinear_map
  (g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) (f : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :
  continuous_multilinear_map R M‚ÇÅ M‚ÇÉ :=
{ cont := g.cont.comp f.cont,
  .. g.to_linear_map.comp_multilinear_map f.to_multilinear_map }

@[simp] lemma _root_.continuous_linear_map.comp_continuous_multilinear_map_coe (g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ)
  (f : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :
  ((g.comp_continuous_multilinear_map f) : (Œ†i, M‚ÇÅ i) ‚Üí M‚ÇÉ) =
  (g : M‚ÇÇ ‚Üí M‚ÇÉ) ‚àò (f : (Œ†i, M‚ÇÅ i) ‚Üí M‚ÇÇ) :=
by { ext m, refl }

/-- `continuous_multilinear_map.pi` as an `equiv`. -/
@[simps]
def pi_equiv {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [Œ† i, add_comm_monoid (M' i)]
  [Œ† i, topological_space (M' i)] [Œ† i, module R (M' i)] :
  (Œ† i, continuous_multilinear_map R M‚ÇÅ (M' i)) ‚âÉ
  continuous_multilinear_map R M‚ÇÅ (Œ† i, M' i) :=
{ to_fun := continuous_multilinear_map.pi,
  inv_fun := Œª f i, (continuous_linear_map.proj i : _ ‚ÜíL[R] M' i).comp_continuous_multilinear_map f,
  left_inv := Œª f, by { ext, refl },
  right_inv := Œª f, by { ext, refl } }

/-- In the specific case of continuous multilinear maps on spaces indexed by `fin (n+1)`, where one
can build an element of `Œ†(i : fin (n+1)), M i` using `cons`, one can express directly the
additivity of a multilinear map along the first variable. -/
lemma cons_add (f : continuous_multilinear_map R M M‚ÇÇ) (m : Œ†(i : fin n), M i.succ) (x y : M 0) :
  f (cons (x+y) m) = f (cons x m) + f (cons y m) :=
f.to_multilinear_map.cons_add m x y

/-- In the specific case of continuous multilinear maps on spaces indexed by `fin (n+1)`, where one
can build an element of `Œ†(i : fin (n+1)), M i` using `cons`, one can express directly the
multiplicativity of a multilinear map along the first variable. -/
lemma cons_smul
  (f : continuous_multilinear_map R M M‚ÇÇ) (m : Œ†(i : fin n), M i.succ) (c : R) (x : M 0) :
  f (cons (c ‚Ä¢ x) m) = c ‚Ä¢ f (cons x m) :=
f.to_multilinear_map.cons_smul m c x

lemma map_piecewise_add [decidable_eq Œπ] (m m' : Œ†i, M‚ÇÅ i) (t : finset Œπ) :
  f (t.piecewise (m + m') m') = ‚àë s in t.powerset, f (s.piecewise m m') :=
f.to_multilinear_map.map_piecewise_add _ _ _

/-- Additivity of a continuous multilinear map along all coordinates at the same time,
writing `f (m + m')` as the sum  of `f (s.piecewise m m')` over all sets `s`. -/
lemma map_add_univ [decidable_eq Œπ] [fintype Œπ] (m m' : Œ†i, M‚ÇÅ i) :
  f (m + m') = ‚àë s : finset Œπ, f (s.piecewise m m') :=
f.to_multilinear_map.map_add_univ _ _

section apply_sum

open fintype finset

variables {Œ± : Œπ ‚Üí Type*} [fintype Œπ] (g : Œ† i, Œ± i ‚Üí M‚ÇÅ i) (A : Œ† i, finset (Œ± i))

/-- If `f` is continuous multilinear, then `f (Œ£_{j‚ÇÅ ‚àà A‚ÇÅ} g‚ÇÅ j‚ÇÅ, ..., Œ£_{j‚Çô ‚àà A‚Çô} g‚Çô j‚Çô)` is the
sum of `f (g‚ÇÅ (r 1), ..., g‚Çô (r n))` where `r` ranges over all functions with `r 1 ‚àà A‚ÇÅ`, ...,
`r n ‚àà A‚Çô`. This follows from multilinearity by expanding successively with respect to each
coordinate. -/

lemma map_sum_finset [decidable_eq Œπ] :
  f (Œª i, ‚àë j in A i, g i j) = ‚àë r in pi_finset A, f (Œª i, g i (r i)) :=
f.to_multilinear_map.map_sum_finset _ _

/-- If `f` is continuous multilinear, then `f (Œ£_{j‚ÇÅ} g‚ÇÅ j‚ÇÅ, ..., Œ£_{j‚Çô} g‚Çô j‚Çô)` is the sum of
`f (g‚ÇÅ (r 1), ..., g‚Çô (r n))` where `r` ranges over all functions `r`. This follows from
multilinearity by expanding successively with respect to each coordinate. -/
lemma map_sum [decidable_eq Œπ] [‚àÄ i, fintype (Œ± i)] :
  f (Œª i, ‚àë j, g i j) = ‚àë r : Œ† i, Œ± i, f (Œª i, g i (r i)) :=
f.to_multilinear_map.map_sum _

end apply_sum

section restrict_scalar

variables (R) {A : Type*} [semiring A] [has_smul R A] [Œ† (i : Œπ), module A (M‚ÇÅ i)]
  [module A M‚ÇÇ] [‚àÄ i, is_scalar_tower R A (M‚ÇÅ i)] [is_scalar_tower R A M‚ÇÇ]

/-- Reinterpret an `A`-multilinear map as an `R`-multilinear map, if `A` is an algebra over `R`
and their actions on all involved modules agree with the action of `R` on `A`. -/
def restrict_scalars (f : continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :
  continuous_multilinear_map R M‚ÇÅ M‚ÇÇ :=
{ to_multilinear_map := f.to_multilinear_map.restrict_scalars R,
  cont := f.cont }

@[simp] lemma coe_restrict_scalars (f : continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :
  ‚áë(f.restrict_scalars R) = f := rfl

end restrict_scalar

end semiring

section ring

variables [ring R] [‚àÄi, add_comm_group (M‚ÇÅ i)] [add_comm_group M‚ÇÇ]
[‚àÄi, module R (M‚ÇÅ i)] [module R M‚ÇÇ] [‚àÄi, topological_space (M‚ÇÅ i)] [topological_space M‚ÇÇ]
(f f' : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ)

@[simp] lemma map_sub [decidable_eq Œπ] (m : Œ†i, M‚ÇÅ i) (i : Œπ) (x y : M‚ÇÅ i) :
  f (update m i (x - y)) = f (update m i x) - f (update m i y) :=
f.to_multilinear_map.map_sub _ _ _ _

section topological_add_group
variable [topological_add_group M‚ÇÇ]

instance : has_neg (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :=
‚ü®Œª f, {cont := f.cont.neg, ..(-f.to_multilinear_map)}‚ü©

@[simp] lemma neg_apply (m : Œ†i, M‚ÇÅ i) : (-f) m = - (f m) := rfl

instance : has_sub (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :=
‚ü®Œª f g, { cont := f.cont.sub g.cont, .. (f.to_multilinear_map - g.to_multilinear_map) }‚ü©

@[simp] lemma sub_apply (m : Œ†i, M‚ÇÅ i) : (f - f') m = f m - f' m := rfl

instance : add_comm_group (continuous_multilinear_map R M‚ÇÅ M‚ÇÇ) :=
to_multilinear_map_injective.add_comm_group _
  rfl (Œª _ _, rfl) (Œª _, rfl) (Œª _ _, rfl) (Œª _ _, rfl) (Œª _ _, rfl)

end topological_add_group

end ring

section comm_semiring

variables [comm_semiring R]
[‚àÄi, add_comm_monoid (M‚ÇÅ i)] [add_comm_monoid M‚ÇÇ]
[‚àÄi, module R (M‚ÇÅ i)] [module R M‚ÇÇ]
[‚àÄi, topological_space (M‚ÇÅ i)] [topological_space M‚ÇÇ]
(f : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ)

lemma map_piecewise_smul [decidable_eq Œπ] (c : Œπ ‚Üí R) (m : Œ†i, M‚ÇÅ i) (s : finset Œπ) :
  f (s.piecewise (Œª i, c i ‚Ä¢ m i) m) = (‚àè i in s, c i) ‚Ä¢ f m :=
f.to_multilinear_map.map_piecewise_smul _ _ _

/-- Multiplicativity of a continuous multilinear map along all coordinates at the same time,
writing `f (Œª i, c i ‚Ä¢ m i)` as `(‚àè i, c i) ‚Ä¢ f m`. -/
lemma map_smul_univ [fintype Œπ] (c : Œπ ‚Üí R) (m : Œ†i, M‚ÇÅ i) :
  f (Œª i, c i ‚Ä¢ m i) = (‚àè i, c i) ‚Ä¢ f m :=
f.to_multilinear_map.map_smul_univ _ _

end comm_semiring

section distrib_mul_action

variables {R' R'' A : Type*} [monoid R'] [monoid R''] [semiring A]
  [Œ† i, add_comm_monoid (M‚ÇÅ i)] [add_comm_monoid M‚ÇÇ]
  [Œ† i, topological_space (M‚ÇÅ i)] [topological_space M‚ÇÇ]
  [Œ† i, module A (M‚ÇÅ i)] [module A M‚ÇÇ]
  [distrib_mul_action R' M‚ÇÇ] [has_continuous_const_smul R' M‚ÇÇ] [smul_comm_class A R' M‚ÇÇ]
  [distrib_mul_action R'' M‚ÇÇ] [has_continuous_const_smul R'' M‚ÇÇ] [smul_comm_class A R'' M‚ÇÇ]

instance [has_continuous_add M‚ÇÇ] : distrib_mul_action R' (continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :=
function.injective.distrib_mul_action
  ‚ü®to_multilinear_map, to_multilinear_map_zero, to_multilinear_map_add‚ü©
  to_multilinear_map_injective (Œª _ _, rfl)

end distrib_mul_action

section module

variables {R' A : Type*} [semiring R'] [semiring A]
  [Œ† i, add_comm_monoid (M‚ÇÅ i)] [add_comm_monoid M‚ÇÇ]
  [Œ† i, topological_space (M‚ÇÅ i)] [topological_space M‚ÇÇ] [has_continuous_add M‚ÇÇ]
  [Œ† i, module A (M‚ÇÅ i)] [module A M‚ÇÇ]
  [module R' M‚ÇÇ] [has_continuous_const_smul R' M‚ÇÇ] [smul_comm_class A R' M‚ÇÇ]

/-- The space of continuous multilinear maps over an algebra over `R` is a module over `R`, for the
pointwise addition and scalar multiplication. -/
instance : module R' (continuous_multilinear_map A M‚ÇÅ M‚ÇÇ) :=
function.injective.module _ ‚ü®to_multilinear_map, to_multilinear_map_zero, to_multilinear_map_add‚ü©
  to_multilinear_map_injective (Œª _ _, rfl)

/-- Linear map version of the map `to_multilinear_map` associating to a continuous multilinear map
the corresponding multilinear map. -/
@[simps] def to_multilinear_map_linear :
  continuous_multilinear_map A M‚ÇÅ M‚ÇÇ ‚Üí‚Çó[R'] multilinear_map A M‚ÇÅ M‚ÇÇ :=
{ to_fun    := to_multilinear_map,
  map_add'  := to_multilinear_map_add,
  map_smul' := to_multilinear_map_smul }

/-- `continuous_multilinear_map.pi` as a `linear_equiv`. -/
@[simps {simp_rhs := tt}]
def pi_linear_equiv {Œπ' : Type*} {M' : Œπ' ‚Üí Type*}
  [Œ† i, add_comm_monoid (M' i)] [Œ† i, topological_space (M' i)] [‚àÄ i, has_continuous_add (M' i)]
  [Œ† i, module R' (M' i)] [Œ† i, module A (M' i)] [‚àÄ i, smul_comm_class A R' (M' i)]
  [Œ† i, has_continuous_const_smul R' (M' i)] :
  (Œ† i, continuous_multilinear_map A M‚ÇÅ (M' i)) ‚âÉ‚Çó[R']
    continuous_multilinear_map A M‚ÇÅ (Œ† i, M' i) :=
{ map_add' := Œª x y, rfl,
  map_smul' := Œª c x, rfl,
  .. pi_equiv }

end module

section comm_algebra

variables (R Œπ) (A : Type*) [fintype Œπ] [comm_semiring R] [comm_semiring A] [algebra R A]
  [topological_space A] [has_continuous_mul A]

/-- The continuous multilinear map on `A^Œπ`, where `A` is a normed commutative algebra
over `ùïú`, associating to `m` the product of all the `m i`.

See also `continuous_multilinear_map.mk_pi_algebra_fin`. -/
protected def mk_pi_algebra : continuous_multilinear_map R (Œª i : Œπ, A) A :=
{ cont := continuous_finset_prod _ $ Œª i hi, continuous_apply _,
  to_multilinear_map := multilinear_map.mk_pi_algebra R Œπ A}

@[simp] lemma mk_pi_algebra_apply (m : Œπ ‚Üí A) :
  continuous_multilinear_map.mk_pi_algebra R Œπ A m = ‚àè i, m i :=
rfl

end comm_algebra

section algebra

variables (R n) (A : Type*) [comm_semiring R] [semiring A] [algebra R A]
  [topological_space A] [has_continuous_mul A]

/-- The continuous multilinear map on `A^n`, where `A` is a normed algebra over `ùïú`, associating to
`m` the product of all the `m i`.

See also: `continuous_multilinear_map.mk_pi_algebra`. -/
protected def mk_pi_algebra_fin : A [√ón]‚ÜíL[R] A :=
{ cont := begin
    change continuous (Œª m, (list.of_fn m).prod),
    simp_rw list.of_fn_eq_map,
    exact continuous_list_prod _ (Œª i hi, continuous_apply _),
  end,
  to_multilinear_map := multilinear_map.mk_pi_algebra_fin R n A}

variables {R n A}

@[simp] lemma mk_pi_algebra_fin_apply (m : fin n ‚Üí A) :
  continuous_multilinear_map.mk_pi_algebra_fin R n A m = (list.of_fn m).prod :=
rfl

end algebra

section smul_right

variables [comm_semiring R] [Œ† i, add_comm_monoid (M‚ÇÅ i)] [add_comm_monoid M‚ÇÇ]
  [Œ† i, module R (M‚ÇÅ i)] [module R M‚ÇÇ] [topological_space R] [Œ† i, topological_space (M‚ÇÅ i)]
  [topological_space M‚ÇÇ] [has_continuous_smul R M‚ÇÇ] (f : continuous_multilinear_map R M‚ÇÅ R) (z : M‚ÇÇ)

/-- Given a continuous `R`-multilinear map `f` taking values in `R`, `f.smul_right z` is the
continuous multilinear map sending `m` to `f m ‚Ä¢ z`. -/
@[simps to_multilinear_map apply] def smul_right : continuous_multilinear_map R M‚ÇÅ M‚ÇÇ :=
{ to_multilinear_map := f.to_multilinear_map.smul_right z,
  cont := f.cont.smul continuous_const }

end smul_right

end continuous_multilinear_map
