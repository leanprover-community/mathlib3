/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import topology.algebra.uniform_convergence

/-!
# Strong topologies on the space of continuous linear maps

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file, we define the strong topologies on `E â†’L[ğ•œ] F` associated with a family
`ğ”– : set (set E)` to be the topology of uniform convergence on the elements of `ğ”–` (also called
the topology of `ğ”–`-convergence).

The lemma `uniform_on_fun.has_continuous_smul_of_image_bounded` tells us that this is a
vector space topology if the continuous linear image of any element of `ğ”–` is bounded (in the sense
of `bornology.is_vonN_bounded`).

We then declare an instance for the case where `ğ”–` is exactly the set of all bounded subsets of
`E`, giving us the so-called "topology of uniform convergence on bounded sets" (or "topology of
bounded convergence"), which coincides with the operator norm topology in the case of
`normed_space`s.

Other useful examples include the weak-* topology (when `ğ”–` is the set of finite sets or the set
of singletons) and the topology of compact convergence (when `ğ”–` is the set of relatively compact
sets).

## Main definitions

* `continuous_linear_map.strong_topology` is the topology mentioned above for an arbitrary `ğ”–`.
* `continuous_linear_map.topological_space` is the topology of bounded convergence. This is
  declared as an instance.

## Main statements

* `continuous_linear_map.strong_topology.topological_add_group` and
  `continuous_linear_map.strong_topology.has_continuous_smul` show that the strong topology
  makes `E â†’L[ğ•œ] F` a topological vector space, with the assumptions on `ğ”–` mentioned above.
* `continuous_linear_map.topological_add_group` and
  `continuous_linear_map.has_continuous_smul` register these facts as instances for the special
  case of bounded convergence.

## References

* [N. Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## TODO

* add a type alias for continuous linear maps with the topology of `ğ”–`-convergence?

## Tags

uniform convergence, bounded convergence
-/

open_locale topology uniform_convergence

namespace continuous_linear_map

section general

variables {ğ•œâ‚ ğ•œâ‚‚ : Type*} [normed_field ğ•œâ‚] [normed_field ğ•œâ‚‚] (Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚)
  {E E' F F' : Type*} [add_comm_group E] [module ğ•œâ‚ E] [add_comm_group E'] [module â„ E']
  [add_comm_group F] [module ğ•œâ‚‚ F] [add_comm_group F'] [module â„ F']
  [topological_space E] [topological_space E'] (F)

/-- Given `E` and `F` two topological vector spaces and `ğ”– : set (set E)`, then
`strong_topology Ïƒ F ğ”–` is the "topology of uniform convergence on the elements of `ğ”–`" on
`E â†’L[ğ•œ] F`.

If the continuous linear image of any element of `ğ”–` is bounded, this makes `E â†’L[ğ•œ] F` a
topological vector space. -/
def strong_topology [topological_space F] [topological_add_group F]
  (ğ”– : set (set E)) : topological_space (E â†’SL[Ïƒ] F) :=
(@uniform_on_fun.topological_space E F
  (topological_add_group.to_uniform_space F) ğ”–).induced coe_fn

/-- The uniform structure associated with `continuous_linear_map.strong_topology`. We make sure
that this has nice definitional properties. -/
def strong_uniformity [uniform_space F] [uniform_add_group F]
  (ğ”– : set (set E)) : uniform_space (E â†’SL[Ïƒ] F) :=
@uniform_space.replace_topology _ (strong_topology Ïƒ F ğ”–)
  ((uniform_on_fun.uniform_space E F ğ”–).comap coe_fn)
  (by rw [strong_topology, uniform_add_group.to_uniform_space_eq]; refl)

@[simp] lemma strong_uniformity_topology_eq [uniform_space F] [uniform_add_group F]
  (ğ”– : set (set E)) :
  (strong_uniformity Ïƒ F ğ”–).to_topological_space = strong_topology Ïƒ F ğ”– :=
rfl

lemma strong_uniformity.uniform_embedding_coe_fn [uniform_space F] [uniform_add_group F]
  (ğ”– : set (set E)) :
  @uniform_embedding (E â†’SL[Ïƒ] F) (E â†’áµ¤[ğ”–] F) (strong_uniformity Ïƒ F ğ”–)
  (uniform_on_fun.uniform_space E F ğ”–) coe_fn :=
begin
  letI : uniform_space (E â†’SL[Ïƒ] F) := strong_uniformity Ïƒ F ğ”–,
  exact âŸ¨âŸ¨rflâŸ©, fun_like.coe_injectiveâŸ©
end

lemma strong_topology.embedding_coe_fn [uniform_space F] [uniform_add_group F]
  (ğ”– : set (set E)) :
  @embedding (E â†’SL[Ïƒ] F) (E â†’áµ¤[ğ”–] F) (strong_topology Ïƒ F ğ”–)
  (uniform_on_fun.topological_space E F ğ”–)
  (uniform_on_fun.of_fun ğ”– âˆ˜ coe_fn) :=
@uniform_embedding.embedding _ _ (_root_.id _) _ _
  (strong_uniformity.uniform_embedding_coe_fn _ _ _)

lemma strong_uniformity.uniform_add_group [uniform_space F] [uniform_add_group F]
  (ğ”– : set (set E)) : @uniform_add_group (E â†’SL[Ïƒ] F) (strong_uniformity Ïƒ F ğ”–) _ :=
begin
  letI : uniform_space (E â†’SL[Ïƒ] F) := strong_uniformity Ïƒ F ğ”–,
  rw [strong_uniformity, uniform_space.replace_topology_eq],
  let Ï† : (E â†’SL[Ïƒ] F) â†’+ E â†’áµ¤[ğ”–] F := âŸ¨(coe_fn : (E â†’SL[Ïƒ] F) â†’ E â†’áµ¤ F), rfl, Î» _ _, rflâŸ©,
  exact uniform_add_group_comap Ï†
end

lemma strong_topology.topological_add_group [topological_space F] [topological_add_group F]
  (ğ”– : set (set E)) : @topological_add_group (E â†’SL[Ïƒ] F) (strong_topology Ïƒ F ğ”–) _ :=
begin
  letI : uniform_space F := topological_add_group.to_uniform_space F,
  haveI : uniform_add_group F := topological_add_comm_group_is_uniform,
  letI : uniform_space (E â†’SL[Ïƒ] F) := strong_uniformity Ïƒ F ğ”–,
  haveI : uniform_add_group (E â†’SL[Ïƒ] F) := strong_uniformity.uniform_add_group Ïƒ F ğ”–,
  apply_instance
end

lemma strong_topology.t2_space [topological_space F] [topological_add_group F] [t2_space F]
  (ğ”– : set (set E)) (hğ”– : â‹ƒâ‚€ ğ”– = set.univ) : @t2_space (E â†’SL[Ïƒ] F) (strong_topology Ïƒ F ğ”–) :=
begin
  letI : uniform_space F := topological_add_group.to_uniform_space F,
  haveI : uniform_add_group F := topological_add_comm_group_is_uniform,
  letI : topological_space (E â†’SL[Ïƒ] F) := strong_topology Ïƒ F ğ”–,
  haveI : t2_space (E â†’áµ¤[ğ”–] F) := uniform_on_fun.t2_space_of_covering hğ”–,
  exact (strong_topology.embedding_coe_fn Ïƒ F ğ”–).t2_space
end

lemma strong_topology.has_continuous_smul [ring_hom_surjective Ïƒ] [ring_hom_isometric Ïƒ]
  [topological_space F] [topological_add_group F] [has_continuous_smul ğ•œâ‚‚ F] (ğ”– : set (set E))
  (hğ”–â‚ : ğ”–.nonempty) (hğ”–â‚‚ : directed_on (âŠ†) ğ”–) (hğ”–â‚ƒ : âˆ€ S âˆˆ ğ”–, bornology.is_vonN_bounded ğ•œâ‚ S) :
  @has_continuous_smul ğ•œâ‚‚ (E â†’SL[Ïƒ] F) _ _ (strong_topology Ïƒ F ğ”–) :=
begin
  letI : uniform_space F := topological_add_group.to_uniform_space F,
  haveI : uniform_add_group F := topological_add_comm_group_is_uniform,
  letI : topological_space (E â†’SL[Ïƒ] F) := strong_topology Ïƒ F ğ”–,
  let Ï† : (E â†’SL[Ïƒ] F) â†’â‚—[ğ•œâ‚‚] E â†’áµ¤[ğ”–] F :=
    âŸ¨(coe_fn : (E â†’SL[Ïƒ] F) â†’ E â†’ F), Î» _ _, rfl, Î» _ _, rflâŸ©,
  exact uniform_on_fun.has_continuous_smul_induced_of_image_bounded ğ•œâ‚‚ E F (E â†’SL[Ïƒ] F)
    hğ”–â‚ hğ”–â‚‚ Ï† âŸ¨rflâŸ© (Î» u s hs, (hğ”–â‚ƒ s hs).image u)
end

lemma strong_topology.has_basis_nhds_zero_of_basis [topological_space F] [topological_add_group F]
  {Î¹ : Type*} (ğ”– : set (set E)) (hğ”–â‚ : ğ”–.nonempty) (hğ”–â‚‚ : directed_on (âŠ†) ğ”–) {p : Î¹ â†’ Prop}
  {b : Î¹ â†’ set F} (h : (ğ“ 0 : filter F).has_basis p b) :
  (@nhds (E â†’SL[Ïƒ] F) (strong_topology Ïƒ F ğ”–) 0).has_basis
    (Î» Si : set E Ã— Î¹, Si.1 âˆˆ ğ”– âˆ§ p Si.2)
    (Î» Si, {f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ Si.1, f x âˆˆ b Si.2}) :=
begin
  letI : uniform_space F := topological_add_group.to_uniform_space F,
  haveI : uniform_add_group F := topological_add_comm_group_is_uniform,
  rw nhds_induced,
  exact (uniform_on_fun.has_basis_nhds_zero_of_basis ğ”– hğ”–â‚ hğ”–â‚‚ h).comap coe_fn
end

lemma strong_topology.has_basis_nhds_zero [topological_space F] [topological_add_group F]
  (ğ”– : set (set E)) (hğ”–â‚ : ğ”–.nonempty) (hğ”–â‚‚ : directed_on (âŠ†) ğ”–) :
  (@nhds (E â†’SL[Ïƒ] F) (strong_topology Ïƒ F ğ”–) 0).has_basis
    (Î» SV : set E Ã— set F, SV.1 âˆˆ ğ”– âˆ§ SV.2 âˆˆ (ğ“ 0 : filter F))
    (Î» SV, {f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2}) :=
strong_topology.has_basis_nhds_zero_of_basis Ïƒ F ğ”– hğ”–â‚ hğ”–â‚‚ (ğ“ 0).basis_sets

end general

section bounded_sets

variables {ğ•œâ‚ ğ•œâ‚‚ ğ•œâ‚ƒ : Type*} [normed_field ğ•œâ‚] [normed_field ğ•œâ‚‚] [normed_field ğ•œâ‚ƒ]
  {Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚} {Ï„ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ï : ğ•œâ‚ â†’+* ğ•œâ‚ƒ} [ring_hom_comp_triple Ïƒ Ï„ Ï]
  {E E' F F' G : Type*}
  [add_comm_group E] [module ğ•œâ‚ E] [add_comm_group E'] [module â„ E']
  [add_comm_group F] [module ğ•œâ‚‚ F] [add_comm_group F'] [module â„ F']
  [add_comm_group G] [module ğ•œâ‚ƒ G]
  [topological_space E]

/-- The topology of bounded convergence on `E â†’L[ğ•œ] F`. This coincides with the topology induced by
the operator norm when `E` and `F` are normed spaces. -/
instance [topological_space F] [topological_add_group F] : topological_space (E â†’SL[Ïƒ] F) :=
strong_topology Ïƒ F {S | bornology.is_vonN_bounded ğ•œâ‚ S}

instance [topological_space F] [topological_add_group F] : topological_add_group (E â†’SL[Ïƒ] F) :=
strong_topology.topological_add_group Ïƒ F _

instance [ring_hom_surjective Ïƒ] [ring_hom_isometric Ïƒ] [topological_space F]
  [topological_add_group F] [has_continuous_smul ğ•œâ‚‚ F] :
  has_continuous_smul ğ•œâ‚‚ (E â†’SL[Ïƒ] F) :=
strong_topology.has_continuous_smul Ïƒ F {S | bornology.is_vonN_bounded ğ•œâ‚ S}
  âŸ¨âˆ…, bornology.is_vonN_bounded_empty ğ•œâ‚ EâŸ©
  (directed_on_of_sup_mem $ Î» _ _, bornology.is_vonN_bounded.union)
  (Î» s hs, hs)

instance [uniform_space F] [uniform_add_group F] : uniform_space (E â†’SL[Ïƒ] F) :=
strong_uniformity Ïƒ F {S | bornology.is_vonN_bounded ğ•œâ‚ S}

instance [uniform_space F] [uniform_add_group F] : uniform_add_group (E â†’SL[Ïƒ] F) :=
strong_uniformity.uniform_add_group Ïƒ F _

instance [topological_space F] [topological_add_group F] [has_continuous_smul ğ•œâ‚ E] [t2_space F] :
  t2_space (E â†’SL[Ïƒ] F) :=
strong_topology.t2_space Ïƒ F _ (set.eq_univ_of_forall $ Î» x,
  set.mem_sUnion_of_mem (set.mem_singleton x) (bornology.is_vonN_bounded_singleton x))

protected lemma has_basis_nhds_zero_of_basis [topological_space F]
  [topological_add_group F] {Î¹ : Type*} {p : Î¹ â†’ Prop} {b : Î¹ â†’ set F}
  (h : (ğ“ 0 : filter F).has_basis p b) :
  (ğ“ (0 : E â†’SL[Ïƒ] F)).has_basis
    (Î» Si : set E Ã— Î¹, bornology.is_vonN_bounded ğ•œâ‚ Si.1 âˆ§ p Si.2)
    (Î» Si, {f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ Si.1, f x âˆˆ b Si.2}) :=
strong_topology.has_basis_nhds_zero_of_basis Ïƒ F
  {S | bornology.is_vonN_bounded ğ•œâ‚ S} âŸ¨âˆ…, bornology.is_vonN_bounded_empty ğ•œâ‚ EâŸ©
  (directed_on_of_sup_mem $ Î» _ _, bornology.is_vonN_bounded.union) h

protected lemma has_basis_nhds_zero [topological_space F]
  [topological_add_group F] :
  (ğ“ (0 : E â†’SL[Ïƒ] F)).has_basis
    (Î» SV : set E Ã— set F, bornology.is_vonN_bounded ğ•œâ‚ SV.1 âˆ§ SV.2 âˆˆ (ğ“ 0 : filter F))
    (Î» SV, {f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2}) :=
continuous_linear_map.has_basis_nhds_zero_of_basis (ğ“ 0).basis_sets

variables (G) [topological_space F] [topological_space G]

/-- Pre-composition by a *fixed* continuous linear map as a continuous linear map.
Note that in non-normed space it is not always true that composition is continuous
in both variables, so we have to fix one of them. -/
@[simps] def precomp [topological_add_group G] [has_continuous_const_smul ğ•œâ‚ƒ G]
  [ring_hom_surjective Ïƒ] [ring_hom_isometric Ïƒ] (L : E â†’SL[Ïƒ] F) :
  (F â†’SL[Ï„] G) â†’L[ğ•œâ‚ƒ] (E â†’SL[Ï] G) :=
{ to_fun := Î» f, f.comp L,
  map_add' := Î» f g, add_comp f g L,
  map_smul' := Î» a f, smul_comp a f L,
  cont :=
  begin
    letI : uniform_space G := topological_add_group.to_uniform_space G,
    haveI : uniform_add_group G := topological_add_comm_group_is_uniform,
    rw (strong_topology.embedding_coe_fn _ _ _).continuous_iff,
    refine (uniform_on_fun.precomp_uniform_continuous _).continuous.comp
      (strong_topology.embedding_coe_fn _ _ _).continuous,
    exact Î» S hS, hS.image L,
  end }

variables (E) {G}

/-- Post-composition by a *fixed* continuous linear map as a continuous linear map.
Note that in non-normed space it is not always true that composition is continuous
in both variables, so we have to fix one of them. -/
@[simps] def postcomp [topological_add_group F] [topological_add_group G]
  [has_continuous_const_smul ğ•œâ‚ƒ G] [has_continuous_const_smul ğ•œâ‚‚ F] (L : F â†’SL[Ï„] G) :
  (E â†’SL[Ïƒ] F) â†’SL[Ï„] (E â†’SL[Ï] G) :=
{ to_fun := Î» f, L.comp f,
  map_add' := comp_add L,
  map_smul' := comp_smulâ‚›â‚— L,
  cont :=
  begin
    letI : uniform_space G := topological_add_group.to_uniform_space G,
    haveI : uniform_add_group G := topological_add_comm_group_is_uniform,
    letI : uniform_space F := topological_add_group.to_uniform_space F,
    haveI : uniform_add_group F := topological_add_comm_group_is_uniform,
    rw (strong_topology.embedding_coe_fn _ _ _).continuous_iff,
    exact (uniform_on_fun.postcomp_uniform_continuous L.uniform_continuous).continuous.comp
      (strong_topology.embedding_coe_fn _ _ _).continuous
  end }

end bounded_sets

end continuous_linear_map

open continuous_linear_map

namespace continuous_linear_equiv

section semilinear

variables {ğ•œ : Type*} {ğ•œâ‚‚ : Type*} {ğ•œâ‚ƒ : Type*} {ğ•œâ‚„ : Type*}
  {E : Type*} {F : Type*} {G : Type*} {H : Type*}
  [add_comm_group E] [add_comm_group F] [add_comm_group G] [add_comm_group H]
  [nontrivially_normed_field ğ•œ] [nontrivially_normed_field ğ•œâ‚‚] [nontrivially_normed_field ğ•œâ‚ƒ]
    [nontrivially_normed_field ğ•œâ‚„]
  [module ğ•œ E] [module ğ•œâ‚‚ F] [module ğ•œâ‚ƒ G] [module ğ•œâ‚„ H]
  [topological_space E] [topological_space F] [topological_space G] [topological_space H]
  [topological_add_group G] [topological_add_group H]
  [has_continuous_const_smul ğ•œâ‚ƒ G] [has_continuous_const_smul ğ•œâ‚„ H]
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚ƒâ‚„ : ğ•œâ‚ƒ â†’+* ğ•œâ‚„}
    {Ïƒâ‚„â‚ƒ : ğ•œâ‚„ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚‚â‚„ : ğ•œâ‚‚ â†’+* ğ•œâ‚„} {Ïƒâ‚â‚„ : ğ•œ â†’+* ğ•œâ‚„}
  [ring_hom_inv_pair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [ring_hom_inv_pair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚] [ring_hom_inv_pair Ïƒâ‚ƒâ‚„ Ïƒâ‚„â‚ƒ]
    [ring_hom_inv_pair Ïƒâ‚„â‚ƒ Ïƒâ‚ƒâ‚„]
  [ring_hom_comp_triple Ïƒâ‚‚â‚ Ïƒâ‚â‚„ Ïƒâ‚‚â‚„] [ring_hom_comp_triple Ïƒâ‚‚â‚„ Ïƒâ‚„â‚ƒ Ïƒâ‚‚â‚ƒ]
    [ring_hom_comp_triple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ] [ring_hom_comp_triple Ïƒâ‚â‚ƒ Ïƒâ‚ƒâ‚„ Ïƒâ‚â‚„]
    [ring_hom_comp_triple Ïƒâ‚‚â‚ƒ Ïƒâ‚ƒâ‚„ Ïƒâ‚‚â‚„] [ring_hom_comp_triple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚„ Ïƒâ‚â‚„]
  [ring_hom_isometric Ïƒâ‚â‚‚] [ring_hom_isometric Ïƒâ‚‚â‚]

include Ïƒâ‚â‚„ Ïƒâ‚‚â‚„ Ïƒâ‚â‚ƒ Ïƒâ‚ƒâ‚„ Ïƒâ‚‚â‚ Ïƒâ‚‚â‚ƒ

/-- A pair of continuous (semi)linear equivalences generates a (semi)linear equivalence between the
spaces of continuous (semi)linear maps. -/
@[simps] def arrow_congrSL (eâ‚â‚‚ : E â‰ƒSL[Ïƒâ‚â‚‚] F) (eâ‚„â‚ƒ : H â‰ƒSL[Ïƒâ‚„â‚ƒ] G) :
  (E â†’SL[Ïƒâ‚â‚„] H) â‰ƒSL[Ïƒâ‚„â‚ƒ] (F â†’SL[Ïƒâ‚‚â‚ƒ] G) :=
{ -- given explicitly to help `simps`
  to_fun := Î» L, (eâ‚„â‚ƒ : H â†’SL[Ïƒâ‚„â‚ƒ] G).comp (L.comp (eâ‚â‚‚.symm : F â†’SL[Ïƒâ‚‚â‚] E)),
  -- given explicitly to help `simps`
  inv_fun := Î» L, (eâ‚„â‚ƒ.symm : G â†’SL[Ïƒâ‚ƒâ‚„] H).comp (L.comp (eâ‚â‚‚ : E â†’SL[Ïƒâ‚â‚‚] F)),
  map_add' := Î» f g, by rw [add_comp, comp_add],
  map_smul' := Î» t f, by rw [smul_comp, comp_smulâ‚›â‚—],
  continuous_to_fun :=
    ((postcomp F eâ‚„â‚ƒ.to_continuous_linear_map).comp
      (precomp H eâ‚â‚‚.symm.to_continuous_linear_map)).continuous,
  continuous_inv_fun :=
    ((precomp H eâ‚â‚‚.to_continuous_linear_map).comp
      (postcomp F eâ‚„â‚ƒ.symm.to_continuous_linear_map)).continuous,
  .. eâ‚â‚‚.arrow_congr_equiv eâ‚„â‚ƒ, }

end semilinear

section linear
variables {ğ•œ : Type*} {E : Type*} {F : Type*} {G : Type*} {H : Type*}
  [add_comm_group E] [add_comm_group F] [add_comm_group G] [add_comm_group H]
  [nontrivially_normed_field ğ•œ] [module ğ•œ E] [module ğ•œ F] [module ğ•œ G] [module ğ•œ H]
  [topological_space E] [topological_space F] [topological_space G] [topological_space H]
  [topological_add_group G] [topological_add_group H]
  [has_continuous_const_smul ğ•œ G] [has_continuous_const_smul ğ•œ H]

/-- A pair of continuous linear equivalences generates an continuous linear equivalence between
the spaces of continuous linear maps. -/
def arrow_congr (eâ‚ : E â‰ƒL[ğ•œ] F) (eâ‚‚ : H â‰ƒL[ğ•œ] G) : (E â†’L[ğ•œ] H) â‰ƒL[ğ•œ] (F â†’L[ğ•œ] G) :=
eâ‚.arrow_congrSL eâ‚‚

end linear

end continuous_linear_equiv
