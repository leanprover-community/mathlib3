/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤, Moritz Doll
-/
import topology.algebra.module.basic

/-!
# Weak dual topology

This file defines the weak topology given two vector spaces `E` and `F` over a commutative semiring
`ğ•œ` and a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`. The weak topology on `E` is the coarsest topology
such that for all `y : F` every map `Î» x, B x y` is continuous.

In the case that `F = E â†’L[ğ•œ] ğ•œ` and `B` being the canonical pairing, we obtain the weak-* topology,
`weak_dual ğ•œ E := (E â†’L[ğ•œ] ğ•œ)`. Interchanging the arguments in the bilinear form yields the
weak topology `weak_space ğ•œ E := E`.

## Main definitions

The main definitions are the types `weak_bilin B` for the general case and the two special cases
`weak_dual ğ•œ E` and `weak_space ğ•œ E` with the respective topology instances on it.

* Given `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`, the type `weak_bilin B` is a type synonym for `E`.
* The instance `weak_bilin.topological_space` is the weak topology induced by the bilinear form `B`.
* `weak_dual ğ•œ E` is a type synonym for `dual ğ•œ E` (when the latter is defined): both are equal to
  the type `E â†’L[ğ•œ] ğ•œ` of continuous linear maps from a module `E` over `ğ•œ` to the ring `ğ•œ`.
* The instance `weak_dual.topological_space` is the weak-* topology on `weak_dual ğ•œ E`, i.e., the
  coarsest topology making the evaluation maps at all `z : E` continuous.
* `weak_space ğ•œ E` is a type synonym for `E` (when the latter is defined).
* The instance `weak_dual.topological_space` is the weak topology on `E`, i.e., the
  coarsest topology such that all `v : dual ğ•œ E` remain continuous.

## Main results

We establish that `weak_bilin B` has the following structure:
* `weak_bilin.has_continuous_add`: The addition in `weak_bilin B` is continuous.
* `weak_bilin.has_continuous_smul`: The scalar multiplication in `weak_bilin B` is continuous.

We prove the following results characterizing the weak topology:
* `eval_continuous`: For any `y : F`, the evaluation mapping `Î» x, B x y` is continuous.
* `continuous_of_continuous_eval`: For a mapping to `weak_bilin B` to be continuous,
  it suffices that its compositions with pairing with `B` at all points `y : F` is continuous.
* `tendsto_iff_forall_eval_tendsto`: Convergence in `weak_bilin B` can be characterized
  in terms of convergence of the evaluations at all points `y : F`.

## Notations

No new notation is introduced.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

weak-star, weak dual, duality

-/

noncomputable theory
open filter
open_locale topological_space

variables {Î± ğ•œ ğ• R E F M : Type*}

section weak_topology

/-- The space `E` equipped with the weak topology induced by the bilinear form `B`. -/
@[derive [add_comm_monoid, module ğ•œ],
nolint has_inhabited_instance unused_arguments]
def weak_bilin [comm_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E] [add_comm_monoid F]
  [module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) := E

namespace weak_bilin

instance [comm_semiring ğ•œ] [a : add_comm_group E] [module ğ•œ E] [add_comm_monoid F]
  [module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : add_comm_group (weak_bilin B) := a

@[priority 100]
instance module' [comm_semiring ğ•œ] [comm_semiring ğ•] [add_comm_group E] [module ğ•œ E]
  [add_comm_group F] [module ğ•œ F] [m : module ğ• E] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) :
  module ğ• (weak_bilin B) := m

instance [comm_semiring ğ•œ] [comm_semiring ğ•] [add_comm_group E] [module ğ•œ E]
  [add_comm_group F] [module ğ•œ F] [has_scalar ğ• ğ•œ] [module ğ• E] [s : is_scalar_tower ğ• ğ•œ E]
  (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : is_scalar_tower ğ• ğ•œ (weak_bilin B) := s

section semiring

variables [topological_space ğ•œ] [comm_semiring ğ•œ]
variables [add_comm_monoid E] [module ğ•œ E]
variables [add_comm_monoid F] [module ğ•œ F]
variables (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

instance : topological_space (weak_bilin B) :=
topological_space.induced (Î» x y, B x y) Pi.topological_space

lemma coe_fn_continuous : continuous (Î» (x : weak_bilin B) y, B x y) :=
continuous_induced_dom

lemma eval_continuous (y : F) : continuous (Î» x : weak_bilin B, B x y) :=
( continuous_pi_iff.mp (coe_fn_continuous B)) y

lemma continuous_of_continuous_eval [topological_space Î±] {g : Î± â†’ weak_bilin B}
  (h : âˆ€ y, continuous (Î» a, B (g a) y)) : continuous g :=
continuous_induced_rng (continuous_pi_iff.mpr h)

/-- The coercion `(Î» x y, B x y) : E â†’ (F â†’ ğ•œ)` is an embedding. -/
lemma embedding {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (hB : function.injective B) :
  embedding (Î» (x : weak_bilin B)  y, B x y) :=
function.injective.embedding_induced $ linear_map.coe_injective.comp hB

theorem tendsto_iff_forall_eval_tendsto {l : filter Î±} {f : Î± â†’ (weak_bilin B)} {x : weak_bilin B}
  (hB : function.injective B) : tendsto f l (ğ“ x) â†” âˆ€ y, tendsto (Î» i, B (f i) y) l (ğ“ (B x y)) :=
by rw [â† tendsto_pi_nhds, embedding.tendsto_nhds_iff (embedding hB)]

/-- Addition in `weak_space B` is continuous. -/
instance [has_continuous_add ğ•œ] : has_continuous_add (weak_bilin B) :=
begin
  refine âŸ¨continuous_induced_rng _âŸ©,
  refine cast (congr_arg _ _) (((coe_fn_continuous B).comp continuous_fst).add
    ((coe_fn_continuous B).comp continuous_snd)),
  ext,
  simp only [function.comp_app, pi.add_apply, map_add, linear_map.add_apply],
end

/-- Scalar multiplication by `ğ•œ` on `weak_bilin B` is continuous. -/
instance [has_continuous_smul ğ•œ ğ•œ] : has_continuous_smul ğ•œ (weak_bilin B) :=
begin
  refine âŸ¨continuous_induced_rng _âŸ©,
  refine cast (congr_arg _ _) (continuous_fst.smul ((coe_fn_continuous B).comp continuous_snd)),
  ext,
  simp only [function.comp_app, pi.smul_apply, linear_map.map_smulâ‚›â‚—, ring_hom.id_apply,
    linear_map.smul_apply],
end

end semiring

section ring

variables [topological_space ğ•œ] [comm_ring ğ•œ]
variables [add_comm_group E] [module ğ•œ E]
variables [add_comm_group F] [module ğ•œ F]
variables (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

/-- `weak_space B` is a `topological_add_group`, meaning that addition and negation are
continuous. -/
instance [has_continuous_add ğ•œ] : topological_add_group (weak_bilin B) :=
{ to_has_continuous_add := by apply_instance,
  continuous_neg := begin
    refine continuous_induced_rng (continuous_pi_iff.mpr (Î» y, _)),
    refine cast (congr_arg _ _) (eval_continuous B (-y)),
    ext,
    simp only [map_neg, function.comp_app, linear_map.neg_apply],
  end }

end ring

end weak_bilin

end weak_topology

section weak_star_topology

/-- The canonical pairing of a vector space and its topological dual. -/
def top_dual_pairing (ğ•œ E) [comm_semiring ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [add_comm_monoid E] [module ğ•œ E] [topological_space E]
  [has_continuous_const_smul ğ•œ ğ•œ] :
  (E â†’L[ğ•œ] ğ•œ) â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] ğ•œ := continuous_linear_map.coe_lm ğ•œ

variables [comm_semiring ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
variables [has_continuous_const_smul ğ•œ ğ•œ]
variables [add_comm_monoid E] [module ğ•œ E] [topological_space E]

lemma dual_pairing_apply (v : (E â†’L[ğ•œ] ğ•œ)) (x : E) : top_dual_pairing ğ•œ E v x = v x := rfl

/-- The weak star topology is the topology coarsest topology on `E â†’L[ğ•œ] ğ•œ` such that all
functionals `Î» v, top_dual_pairing ğ•œ E v x` are continuous. -/
@[derive [add_comm_monoid, module ğ•œ, topological_space, has_continuous_add]]
def weak_dual (ğ•œ E) [comm_semiring ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [has_continuous_const_smul ğ•œ ğ•œ] [add_comm_monoid E] [module ğ•œ E] [topological_space E] :=
weak_bilin (top_dual_pairing ğ•œ E)

namespace weak_dual

instance : inhabited (weak_dual ğ•œ E) := continuous_linear_map.inhabited

instance weak_dual.add_monoid_hom_class : add_monoid_hom_class (weak_dual ğ•œ E) E ğ•œ :=
continuous_linear_map.add_monoid_hom_class

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`
directly. -/
instance : has_coe_to_fun (weak_dual ğ•œ E) (Î» _, E â†’ ğ•œ) := fun_like.has_coe_to_fun

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it acts on `weak_dual ğ•œ E`. -/
instance (M) [monoid M] [distrib_mul_action M ğ•œ] [smul_comm_class ğ•œ M ğ•œ]
  [has_continuous_const_smul M ğ•œ] :
  mul_action M (weak_dual ğ•œ E) :=
continuous_linear_map.mul_action

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it acts distributively on `weak_dual ğ•œ E`. -/
instance (M) [monoid M] [distrib_mul_action M ğ•œ] [smul_comm_class ğ•œ M ğ•œ]
  [has_continuous_const_smul M ğ•œ] :
  distrib_mul_action M (weak_dual ğ•œ E) :=
continuous_linear_map.distrib_mul_action

/-- If `ğ•œ` is a topological module over a semiring `R` and scalar multiplication commutes with the
multiplication on `ğ•œ`, then `weak_dual ğ•œ E` is a module over `R`. -/
instance module' (R) [semiring R] [module R ğ•œ] [smul_comm_class ğ•œ R ğ•œ]
  [has_continuous_const_smul R ğ•œ] :
  module R (weak_dual ğ•œ E) :=
continuous_linear_map.module

instance (M) [monoid M] [distrib_mul_action M ğ•œ] [smul_comm_class ğ•œ M ğ•œ]
  [has_continuous_const_smul M ğ•œ] : has_continuous_const_smul M (weak_dual ğ•œ E) :=
âŸ¨Î» m, continuous_induced_rng $ (weak_bilin.coe_fn_continuous (top_dual_pairing ğ•œ E)).const_smul mâŸ©

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it continuously acts on `weak_dual ğ•œ E`. -/
instance (M) [monoid M] [distrib_mul_action M ğ•œ] [smul_comm_class ğ•œ M ğ•œ]
  [topological_space M] [has_continuous_smul M ğ•œ] :
  has_continuous_smul M (weak_dual ğ•œ E) :=
âŸ¨continuous_induced_rng $ continuous_fst.smul ((weak_bilin.coe_fn_continuous
                          (top_dual_pairing ğ•œ E)).comp continuous_snd)âŸ©

lemma coe_fn_continuous : continuous (Î» (x : weak_dual ğ•œ E) y, x y) :=
continuous_induced_dom

lemma eval_continuous (y : E) : continuous (Î» x : weak_dual ğ•œ E, x y) :=
continuous_pi_iff.mp coe_fn_continuous y

lemma continuous_of_continuous_eval [topological_space Î±] {g : Î± â†’ weak_dual ğ•œ E}
  (h : âˆ€ y, continuous (Î» a, (g a) y)) : continuous g :=
continuous_induced_rng (continuous_pi_iff.mpr h)

end weak_dual

/-- The weak topology is the topology coarsest topology on `E` such that all
functionals `Î» x, top_dual_pairing ğ•œ E v x` are continuous. -/
@[derive [add_comm_monoid, module ğ•œ, topological_space, has_continuous_add],
nolint has_inhabited_instance]
def weak_space (ğ•œ E) [comm_semiring ğ•œ] [topological_space ğ•œ] [has_continuous_add ğ•œ]
  [has_continuous_const_smul ğ•œ ğ•œ] [add_comm_monoid E] [module ğ•œ E] [topological_space E] :=
weak_bilin (top_dual_pairing ğ•œ E).flip

theorem tendsto_iff_forall_eval_tendsto_top_dual_pairing
  {l : filter Î±} {f : Î± â†’ weak_dual ğ•œ E} {x : weak_dual ğ•œ E} :
  tendsto f l (ğ“ x) â†”
    âˆ€ y, tendsto (Î» i, top_dual_pairing ğ•œ E (f i) y) l (ğ“ (top_dual_pairing ğ•œ E x y)) :=
weak_bilin.tendsto_iff_forall_eval_tendsto _ continuous_linear_map.coe_injective

end weak_star_topology
