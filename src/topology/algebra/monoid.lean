/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import topology.continuous_on
import group_theory.submonoid.basic
import algebra.group.prod
import algebra.pointwise

/-!
# Theory of topological monoids

In this file we define mixin classes `has_continuous_mul` and `has_continuous_add`. While in many
applications the underlying type is a monoid (multiplicative or additive), we do not require this in the definitions.
-/

open classical set filter topological_space
open_locale classical topological_space big_operators

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*}

/-- Basic hypothesis to talk about a topological additive monoid or a topological additive
semigroup. A topological additive monoid over `Î±`, for example, is obtained by requiring both the
instances `add_monoid Î±` and `has_continuous_add Î±`. -/
class has_continuous_add (Î± : Type*) [topological_space Î±] [has_add Î±] : Prop :=
(continuous_add : continuous (Î»p:Î±Ã—Î±, p.1 + p.2))

/-- Basic hypothesis to talk about a topological monoid or a topological semigroup.
A topological monoid over `Î±`, for example, is obtained by requiring both the instances `monoid Î±`
and `has_continuous_mul Î±`. -/
@[to_additive]
class has_continuous_mul (Î± : Type*) [topological_space Î±] [has_mul Î±] : Prop :=
(continuous_mul : continuous (Î»p:Î±Ã—Î±, p.1 * p.2))

section has_continuous_mul

variables [topological_space Î±] [has_mul Î±] [has_continuous_mul Î±]

@[to_additive]
lemma continuous_mul : continuous (Î»p:Î±Ã—Î±, p.1 * p.2) :=
has_continuous_mul.continuous_mul

@[to_additive, continuity]
lemma continuous.mul [topological_space Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) :
  continuous (Î»x, f x * g x) :=
continuous_mul.comp (hf.prod_mk hg)

attribute [continuity] continuous.add

@[to_additive]
lemma continuous_mul_left (a : Î±) : continuous (Î» b:Î±, a * b) :=
continuous_const.mul continuous_id

@[to_additive]
lemma continuous_mul_right (a : Î±) : continuous (Î» b:Î±, b * a) :=
continuous_id.mul continuous_const

@[to_additive]
lemma continuous_on.mul [topological_space Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±} {s : set Î²}
  (hf : continuous_on f s) (hg : continuous_on g s) :
  continuous_on (Î»x, f x * g x) s :=
(continuous_mul.comp_continuous_on (hf.prod hg) : _)

@[to_additive]
lemma tendsto_mul {a b : Î±} : tendsto (Î»p:Î±Ã—Î±, p.fst * p.snd) (ğ“ (a, b)) (ğ“ (a * b)) :=
continuous_iff_continuous_at.mp has_continuous_mul.continuous_mul (a, b)

@[to_additive]
lemma filter.tendsto.mul {f : Î² â†’ Î±} {g : Î² â†’ Î±} {x : filter Î²} {a b : Î±}
  (hf : tendsto f x (ğ“ a)) (hg : tendsto g x (ğ“ b)) :
  tendsto (Î»x, f x * g x) x (ğ“ (a * b)) :=
tendsto_mul.comp (hf.prod_mk_nhds hg)

@[to_additive]
lemma continuous_at.mul [topological_space Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±} {x : Î²}
  (hf : continuous_at f x) (hg : continuous_at g x) :
  continuous_at (Î»x, f x * g x) x :=
hf.mul hg

@[to_additive]
lemma continuous_within_at.mul [topological_space Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±} {s : set Î²} {x : Î²}
  (hf : continuous_within_at f s x) (hg : continuous_within_at g s x) :
  continuous_within_at (Î»x, f x * g x) s x :=
hf.mul hg

@[to_additive]
instance [topological_space Î²] [has_mul Î²] [has_continuous_mul Î²] : has_continuous_mul (Î± Ã— Î²) :=
âŸ¨((continuous_fst.comp continuous_fst).mul (continuous_fst.comp continuous_snd)).prod_mk
 ((continuous_snd.comp continuous_fst).mul (continuous_snd.comp continuous_snd))âŸ©

end has_continuous_mul

section has_continuous_mul

variables [topological_space Î±] [monoid Î±] [has_continuous_mul Î±]

@[to_additive exists_nhds_half]
lemma exists_nhds_split {s : set Î±} (hs : s âˆˆ ğ“ (1 : Î±)) :
  âˆƒ V : set Î±, is_open V âˆ§ (1 : Î±) âˆˆ V âˆ§ âˆ€ (v âˆˆ V) (w âˆˆ V), v * w âˆˆ s :=
have ((Î»a:Î±Ã—Î±, a.1 * a.2) â»Â¹' s) âˆˆ ğ“ ((1, 1) : Î± Ã— Î±),
  from tendsto_mul (by simpa only [one_mul] using hs),
by simpa only [prod_subset_iff] using exists_nhds_square this

@[to_additive exists_nhds_quarter]
lemma exists_nhds_split4 {u : set Î±} (hu : u âˆˆ ğ“ (1 : Î±)) :
  âˆƒ V : set Î±, is_open V âˆ§ (1 : Î±) âˆˆ V âˆ§
    âˆ€ {v w s t}, v âˆˆ V â†’ w âˆˆ V â†’ s âˆˆ V â†’ t âˆˆ V â†’ v * w * s * t âˆˆ u :=
begin
  rcases exists_nhds_split hu with âŸ¨W, W_open, W1, hâŸ©,
  rcases exists_nhds_split (mem_nhds_sets W_open W1) with âŸ¨V, V_open, V1, h'âŸ©,
  use [V, V_open, V1],
  intros v w s t v_in w_in s_in t_in,
  simpa only [mul_assoc] using h _ (h' v v_in w w_in) _ (h' s s_in t t_in)
end

/-- Given a open neighborhood `U` of `1` there is a open neighborhood `V` of `1`
  such that `VV âŠ† U`. -/
@[to_additive "Given a open neighborhood `U` of `0` there is a open neighborhood `V` of `0`
  such that `V + V âŠ† U`."]
lemma exists_open_mem_mul_subset {U : set Î±} (hU : U âˆˆ ğ“ (1 : Î±)) :
  âˆƒ V : set Î±, is_open V âˆ§ (1 : Î±) âˆˆ V âˆ§ V * V âŠ† U :=
begin
  rcases exists_nhds_split hU with âŸ¨V, Vo, V1, hVâŸ©,
  use [V, Vo, V1],
  rintros _ âŸ¨x, y, hx, hy, rflâŸ©,
  exact hV _ hx _ hy
end

@[to_additive]
lemma tendsto_list_prod {f : Î³ â†’ Î² â†’ Î±} {x : filter Î²} {a : Î³ â†’ Î±} :
  âˆ€l:list Î³, (âˆ€câˆˆl, tendsto (f c) x (ğ“ (a c))) â†’
    tendsto (Î»b, (l.map (Î»c, f c b)).prod) x (ğ“ ((l.map a).prod))
| []       _ := by simp [tendsto_const_nhds]
| (f :: l) h :=
  begin
    simp only [list.map_cons, list.prod_cons],
    exact (h f (list.mem_cons_self _ _)).mul
      (tendsto_list_prod l (assume c hc, h c (list.mem_cons_of_mem _ hc)))
  end

@[to_additive]
lemma continuous_list_prod [topological_space Î²] {f : Î³ â†’ Î² â†’ Î±} (l : list Î³)
  (h : âˆ€câˆˆl, continuous (f c)) :
  continuous (Î»a, (l.map (Î»c, f c a)).prod) :=
continuous_iff_continuous_at.2 $ assume x, tendsto_list_prod l $ assume c hc,
  continuous_iff_continuous_at.1 (h c hc) x

-- @[to_additive continuous_smul]
@[continuity]
lemma continuous_pow : âˆ€ n : â„•, continuous (Î» a : Î±, a ^ n)
| 0 := by simpa using continuous_const
| (k+1) := show continuous (Î» (a : Î±), a * a ^ k), from continuous_id.mul (continuous_pow _)

@[continuity]
lemma continuous.pow {f : Î² â†’ Î±} [topological_space Î²] (h : continuous f) (n : â„•) :
  continuous (Î» b, (f b) ^ n) :=
continuous.comp (continuous_pow n) h

end has_continuous_mul

section

variables [topological_space Î±] [comm_monoid Î±]

@[to_additive]
lemma submonoid.mem_nhds_one (Î² : submonoid Î±) (oÎ² : is_open (Î² : set Î±)) :
  (Î² : set Î±) âˆˆ ğ“ (1 : Î±) :=
mem_nhds_sets_iff.2 âŸ¨Î², (by refl), oÎ², Î².one_memâŸ©

variable [has_continuous_mul Î±]

@[to_additive]
lemma tendsto_multiset_prod {f : Î³ â†’ Î² â†’ Î±} {x : filter Î²} {a : Î³ â†’ Î±} (s : multiset Î³) :
  (âˆ€câˆˆs, tendsto (f c) x (ğ“ (a c))) â†’
    tendsto (Î»b, (s.map (Î»c, f c b)).prod) x (ğ“ ((s.map a).prod)) :=
by { rcases s with âŸ¨lâŸ©, simp, exact tendsto_list_prod l }

@[to_additive]
lemma tendsto_finset_prod {f : Î³ â†’ Î² â†’ Î±} {x : filter Î²} {a : Î³ â†’ Î±} (s : finset Î³) :
  (âˆ€câˆˆs, tendsto (f c) x (ğ“ (a c))) â†’ tendsto (Î»b, âˆ c in s, f c b) x (ğ“ (âˆ c in s, a c)) :=
tendsto_multiset_prod _

@[to_additive, continuity]
lemma continuous_multiset_prod [topological_space Î²] {f : Î³ â†’ Î² â†’ Î±} (s : multiset Î³) :
  (âˆ€câˆˆs, continuous (f c)) â†’ continuous (Î»a, (s.map (Î»c, f c a)).prod) :=
by { rcases s with âŸ¨lâŸ©, simp, exact continuous_list_prod l }

attribute [continuity] continuous_multiset_sum

@[to_additive, continuity]
lemma continuous_finset_prod [topological_space Î²] {f : Î³ â†’ Î² â†’ Î±} (s : finset Î³) :
  (âˆ€câˆˆs, continuous (f c)) â†’ continuous (Î»a, âˆ c in s, f c a) :=
continuous_multiset_prod _

attribute [continuity] continuous_finset_sum

end
