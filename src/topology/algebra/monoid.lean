/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro

Theory of topological monoids.
-/
import topology.continuous_on
import group_theory.submonoid.basic
import algebra.group.prod

open classical set filter topological_space
open_locale classical topological_space big_operators

variables {Î± Î² M N : Type*}

/-- Basic hypothesis to talk about a topological additive monoid or a topological additive
semigroup. A topological additive monoid over `Î±`, for example, is obtained by requiring both the
instances `add_monoid Î±` and `has_continuous_add Î±`. -/
class has_continuous_add (M : Type*) [topological_space M] [has_add M] : Prop :=
(continuous_add : continuous (Î» p : M Ã— M, p.1 + p.2))

/-- Basic hypothesis to talk about a topological monoid or a topological semigroup.
A topological monoid over `Î±`, for example, is obtained by requiring both the instances `monoid Î±`
and `has_continuous_mul Î±`. -/
@[to_additive]
class has_continuous_mul (M : Type*) [topological_space M] [has_mul M] : Prop :=
(continuous_mul : continuous (Î» p : M Ã— M, p.1 * p.2))

section has_continuous_mul

variables [topological_space M] [has_mul M] [has_continuous_mul M]

@[to_additive]
lemma continuous_mul : continuous (Î»p:MÃ—M, p.1 * p.2) :=
has_continuous_mul.continuous_mul

@[to_additive, continuity]
lemma continuous.mul [topological_space Î±] {f : Î± â†’ M} {g : Î± â†’ M}
  (hf : continuous f) (hg : continuous g) :
  continuous (Î»x, f x * g x) :=
continuous_mul.comp (hf.prod_mk hg)

attribute [continuity] continuous.add

@[to_additive]
lemma continuous_mul_left (a : M) : continuous (Î» b:M, a * b) :=
continuous_const.mul continuous_id

@[to_additive]
lemma continuous_mul_right (a : M) : continuous (Î» b:M, b * a) :=
continuous_id.mul continuous_const

@[to_additive]
lemma continuous_on.mul [topological_space Î±] {f : Î± â†’ M} {g : Î± â†’ M} {s : set Î±}
  (hf : continuous_on f s) (hg : continuous_on g s) :
  continuous_on (Î»x, f x * g x) s :=
(continuous_mul.comp_continuous_on (hf.prod hg) : _)

@[to_additive]
lemma tendsto_mul {a b : M} : tendsto (Î»p:MÃ—M, p.fst * p.snd) (ğ“ (a, b)) (ğ“ (a * b)) :=
continuous_iff_continuous_at.mp has_continuous_mul.continuous_mul (a, b)

@[to_additive]
lemma filter.tendsto.mul {f : Î± â†’ M} {g : Î± â†’ M} {x : filter Î±} {a b : M}
  (hf : tendsto f x (ğ“ a)) (hg : tendsto g x (ğ“ b)) :
  tendsto (Î»x, f x * g x) x (ğ“ (a * b)) :=
tendsto_mul.comp (hf.prod_mk_nhds hg)

@[to_additive]
lemma continuous_at.mul [topological_space Î±] {f : Î± â†’ M} {g : Î± â†’ M} {x : Î±}
  (hf : continuous_at f x) (hg : continuous_at g x) :
  continuous_at (Î»x, f x * g x) x :=
hf.mul hg

@[to_additive]
lemma continuous_within_at.mul [topological_space Î±] {f : Î± â†’ M} {g : Î± â†’ M} {s : set Î±} {x : Î±}
  (hf : continuous_within_at f s x) (hg : continuous_within_at g s x) :
  continuous_within_at (Î»x, f x * g x) s x :=
hf.mul hg

@[to_additive]
instance [topological_space N] [has_mul N] [has_continuous_mul N] : has_continuous_mul (M Ã— N) :=
âŸ¨((continuous_fst.comp continuous_fst).mul (continuous_fst.comp continuous_snd)).prod_mk
 ((continuous_snd.comp continuous_fst).mul (continuous_snd.comp continuous_snd))âŸ©

end has_continuous_mul

section has_continuous_mul

variables [topological_space M] [monoid M] [has_continuous_mul M]

@[to_additive]
lemma tendsto_list_prod {f : Î² â†’ Î± â†’ M} {x : filter Î±} {a : Î² â†’ M} :
  âˆ€l:list Î², (âˆ€câˆˆl, tendsto (f c) x (ğ“ (a c))) â†’
    tendsto (Î»b, (l.map (Î»c, f c b)).prod) x (ğ“ ((l.map a).prod))
| []       _ := by simp [tendsto_const_nhds]
| (f :: l) h :=
  begin
    simp only [list.map_cons, list.prod_cons],
    exact (h f (list.mem_cons_self _ _)).mul
      (tendsto_list_prod l (assume c hc, h c (list.mem_cons_of_mem _ hc)))
  end

@[to_additive]
lemma continuous_list_prod [topological_space Î±] {f : Î² â†’ Î± â†’ M} (l : list Î²)
  (h : âˆ€câˆˆl, continuous (f c)) :
  continuous (Î»a, (l.map (Î»c, f c a)).prod) :=
continuous_iff_continuous_at.2 $ assume x, tendsto_list_prod l $ assume c hc,
  continuous_iff_continuous_at.1 (h c hc) x

-- @[to_additive continuous_smul]
@[continuity]
lemma continuous_pow : âˆ€ n : â„•, continuous (Î» a : M, a ^ n)
| 0 := by simpa using continuous_const
| (k+1) := show continuous (Î» (a : M), a * a ^ k), from continuous_id.mul (continuous_pow _)

@[continuity]
lemma continuous.pow {f : Î± â†’ M} [topological_space Î±] (h : continuous f) (n : â„•) :
  continuous (Î» b, (f b) ^ n) :=
continuous.comp (continuous_pow n) h

end has_continuous_mul

section

variables [topological_space M] [comm_monoid M]

@[to_additive]
lemma submonoid.mem_nhds_one (S : submonoid M) (oS : is_open (S : set M)) :
  (S : set M) âˆˆ ğ“ (1 : M) :=
mem_nhds_sets oS S.one_mem

variable [has_continuous_mul M]

@[to_additive]
lemma tendsto_multiset_prod {f : Î² â†’ Î± â†’ M} {x : filter Î±} {a : Î² â†’ M} (s : multiset Î²) :
  (âˆ€câˆˆs, tendsto (f c) x (ğ“ (a c))) â†’
    tendsto (Î»b, (s.map (Î»c, f c b)).prod) x (ğ“ ((s.map a).prod)) :=
by { rcases s with âŸ¨lâŸ©, simp, exact tendsto_list_prod l }

@[to_additive]
lemma tendsto_finset_prod {f : Î² â†’ Î± â†’ M} {x : filter Î±} {a : Î² â†’ M} (s : finset Î²) :
  (âˆ€câˆˆs, tendsto (f c) x (ğ“ (a c))) â†’ tendsto (Î»b, âˆ c in s, f c b) x (ğ“ (âˆ c in s, a c)) :=
tendsto_multiset_prod _

@[to_additive, continuity]
lemma continuous_multiset_prod [topological_space Î±] {f : Î² â†’ Î± â†’ M} (s : multiset Î²) :
  (âˆ€câˆˆs, continuous (f c)) â†’ continuous (Î»a, (s.map (Î»c, f c a)).prod) :=
by { rcases s with âŸ¨lâŸ©, simp, exact continuous_list_prod l }

attribute [continuity] continuous_multiset_sum

@[to_additive, continuity]
lemma continuous_finset_prod [topological_space Î±] {f : Î² â†’ Î± â†’ M} (s : finset Î²) :
  (âˆ€câˆˆs, continuous (f c)) â†’ continuous (Î»a, âˆ c in s, f c a) :=
continuous_multiset_prod _

attribute [continuity] continuous_finset_sum

end
