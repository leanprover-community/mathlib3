/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import algebra.big_operators.finprod
import order.filter.pointwise
import topology.algebra.mul_action
import algebra.big_operators.pi
import topology.continuous_function.basic

/-!
# Theory of topological monoids

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define mixin classes `has_continuous_mul` and `has_continuous_add`. While in many
applications the underlying type is a monoid (multiplicative or additive), we do not require this in
the definitions.
-/

universes u v
open classical set filter topological_space
open_locale classical topology big_operators pointwise

variables {Î¹ Î± X M N : Type*} [topological_space X]

@[to_additive]
lemma continuous_one [topological_space M] [has_one M] : continuous (1 : X â†’ M) :=
@continuous_const _ _ _ _ 1

/-- Basic hypothesis to talk about a topological additive monoid or a topological additive
semigroup. A topological additive monoid over `M`, for example, is obtained by requiring both the
instances `add_monoid M` and `has_continuous_add M`.

Continuity in only the left/right argument can be stated using
`has_continuous_const_vadd Î± Î±`/`has_continuous_const_vadd Î±áµáµ’áµ– Î±`. -/
class has_continuous_add (M : Type u) [topological_space M] [has_add M] : Prop :=
(continuous_add : continuous (Î» p : M Ã— M, p.1 + p.2))

/-- Basic hypothesis to talk about a topological monoid or a topological semigroup.
A topological monoid over `M`, for example, is obtained by requiring both the instances `monoid M`
and `has_continuous_mul M`.

Continuity in only the left/right argument can be stated using
`has_continuous_const_smul Î± Î±`/`has_continuous_const_smul Î±áµáµ’áµ– Î±`. -/
@[to_additive]
class has_continuous_mul (M : Type u) [topological_space M] [has_mul M] : Prop :=
(continuous_mul : continuous (Î» p : M Ã— M, p.1 * p.2))

section has_continuous_mul

variables [topological_space M] [has_mul M] [has_continuous_mul M]

@[to_additive] instance : has_continuous_mul Máµ’áµˆ := â€¹has_continuous_mul Mâ€º

@[to_additive]
lemma continuous_mul : continuous (Î»p:MÃ—M, p.1 * p.2) :=
has_continuous_mul.continuous_mul

@[to_additive]
instance has_continuous_mul.to_has_continuous_smul : has_continuous_smul M M := âŸ¨continuous_mulâŸ©

@[to_additive]
instance has_continuous_mul.to_has_continuous_smul_op : has_continuous_smul Máµáµ’áµ– M :=
âŸ¨show continuous ((Î» p : M Ã— M, p.1 * p.2) âˆ˜ prod.swap âˆ˜ prod.map mul_opposite.unop id), from
  continuous_mul.comp $ continuous_swap.comp $ continuous.prod_map mul_opposite.continuous_unop
    continuous_idâŸ©

@[continuity, to_additive]
lemma continuous.mul {f g : X â†’ M} (hf : continuous f) (hg : continuous g) :
  continuous (Î»x, f x * g x) :=
continuous_mul.comp (hf.prod_mk hg : _)

@[to_additive]
lemma continuous_mul_left (a : M) : continuous (Î» b:M, a * b) :=
continuous_const.mul continuous_id

@[to_additive]
lemma continuous_mul_right (a : M) : continuous (Î» b:M, b * a) :=
continuous_id.mul continuous_const

@[to_additive]
lemma continuous_on.mul {f g : X â†’ M} {s : set X} (hf : continuous_on f s)
  (hg : continuous_on g s) :
  continuous_on (Î»x, f x * g x) s :=
(continuous_mul.comp_continuous_on (hf.prod hg) : _)

@[to_additive]
lemma tendsto_mul {a b : M} : tendsto (Î»p:MÃ—M, p.fst * p.snd) (ğ“ (a, b)) (ğ“ (a * b)) :=
continuous_iff_continuous_at.mp has_continuous_mul.continuous_mul (a, b)

@[to_additive]
lemma filter.tendsto.mul {f g : Î± â†’ M} {x : filter Î±} {a b : M}
  (hf : tendsto f x (ğ“ a)) (hg : tendsto g x (ğ“ b)) :
  tendsto (Î»x, f x * g x) x (ğ“ (a * b)) :=
tendsto_mul.comp (hf.prod_mk_nhds hg)

@[to_additive]
lemma filter.tendsto.const_mul (b : M) {c : M} {f : Î± â†’ M} {l : filter Î±}
  (h : tendsto (Î» (k:Î±), f k) l (ğ“ c)) : tendsto (Î» (k:Î±), b * f k) l (ğ“ (b * c)) :=
tendsto_const_nhds.mul h

@[to_additive]
lemma filter.tendsto.mul_const (b : M) {c : M} {f : Î± â†’ M} {l : filter Î±}
  (h : tendsto (Î» (k:Î±), f k) l (ğ“ c)) : tendsto (Î» (k:Î±), f k * b) l (ğ“ (c * b)) :=
h.mul tendsto_const_nhds

@[to_additive] lemma le_nhds_mul (a b : M) : ğ“ a * ğ“ b â‰¤ ğ“ (a * b) :=
by { rw [â† mapâ‚‚_mul, â† map_uncurry_prod, â† nhds_prod_eq], exact continuous_mul.tendsto _ }

@[simp, to_additive] lemma nhds_one_mul_nhds {M} [mul_one_class M] [topological_space M]
  [has_continuous_mul M] (a : M) : ğ“ (1 : M) * ğ“ a = ğ“ a :=
((le_nhds_mul _ _).trans_eq $ congr_arg _ (one_mul a)).antisymm $
  le_mul_of_one_le_left' $ pure_le_nhds 1

@[simp, to_additive] lemma nhds_mul_nhds_one {M} [mul_one_class M] [topological_space M]
  [has_continuous_mul M] (a : M) : ğ“ a * ğ“ 1 = ğ“ a :=
((le_nhds_mul _ _).trans_eq $ congr_arg _ (mul_one a)).antisymm $
  le_mul_of_one_le_right' $ pure_le_nhds 1

section tendsto_nhds

variables {ğ•œ : Type*}
  [preorder ğ•œ] [has_zero ğ•œ] [has_mul ğ•œ] [topological_space ğ•œ] [has_continuous_mul ğ•œ]
  {l : filter Î±} {f : Î± â†’ ğ•œ} {b c : ğ•œ} (hb : 0 < b)

lemma filter.tendsto_nhds_within_Ioi.const_mul [pos_mul_strict_mono ğ•œ] [pos_mul_reflect_lt ğ•œ]
  (h : tendsto f l (ğ“[>] c)) :
  tendsto (Î» a, b * f a) l (ğ“[>] (b * c)) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
  ((tendsto_nhds_of_tendsto_nhds_within h).const_mul b) $
  (tendsto_nhds_within_iff.mp h).2.mono (Î» j, (mul_lt_mul_left hb).mpr)

lemma filter.tendsto_nhds_within_Iio.const_mul [pos_mul_strict_mono ğ•œ] [pos_mul_reflect_lt ğ•œ]
  (h : tendsto f l (ğ“[<] c)) :
  tendsto (Î» a, b * f a) l (ğ“[<] (b * c)) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
  ((tendsto_nhds_of_tendsto_nhds_within h).const_mul b) $
  (tendsto_nhds_within_iff.mp h).2.mono (Î» j, (mul_lt_mul_left hb).mpr)

lemma filter.tendsto_nhds_within_Ioi.mul_const [mul_pos_strict_mono ğ•œ] [mul_pos_reflect_lt ğ•œ]
  (h : tendsto f l (ğ“[>] c)) :
  tendsto (Î» a, f a * b) l (ğ“[>] (c * b)) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
  ((tendsto_nhds_of_tendsto_nhds_within h).mul_const b) $
  (tendsto_nhds_within_iff.mp h).2.mono (Î» j, (mul_lt_mul_right hb).mpr)

lemma filter.tendsto_nhds_within_Iio.mul_const [mul_pos_strict_mono ğ•œ] [mul_pos_reflect_lt ğ•œ]
  (h : tendsto f l (ğ“[<] c)) :
  tendsto (Î» a, f a * b) l (ğ“[<] (c * b)) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
  ((tendsto_nhds_of_tendsto_nhds_within h).mul_const b) $
  (tendsto_nhds_within_iff.mp h).2.mono (Î» j, (mul_lt_mul_right hb).mpr)

end tendsto_nhds

/-- Construct a unit from limits of units and their inverses. -/
@[to_additive filter.tendsto.add_units "Construct an additive unit from limits of additive units
and their negatives.", simps]
def filter.tendsto.units [topological_space N] [monoid N] [has_continuous_mul N] [t2_space N]
  {f : Î¹ â†’ NË£} {râ‚ râ‚‚ : N} {l : filter Î¹} [l.ne_bot]
  (hâ‚ : tendsto (Î» x, â†‘(f x)) l (ğ“ râ‚)) (hâ‚‚ : tendsto (Î» x, â†‘(f x)â»Â¹) l (ğ“ râ‚‚)) : NË£ :=
{ val := râ‚,
  inv := râ‚‚,
  val_inv := by { symmetry, simpa using hâ‚.mul hâ‚‚ },
  inv_val := by { symmetry, simpa using hâ‚‚.mul hâ‚ } }

@[to_additive]
lemma continuous_at.mul {f g : X â†’ M} {x : X} (hf : continuous_at f x) (hg : continuous_at g x) :
  continuous_at (Î»x, f x * g x) x :=
hf.mul hg

@[to_additive]
lemma continuous_within_at.mul {f g : X â†’ M} {s : set X} {x : X} (hf : continuous_within_at f s x)
  (hg : continuous_within_at g s x) :
  continuous_within_at (Î»x, f x * g x) s x :=
hf.mul hg

@[to_additive]
instance [topological_space N] [has_mul N] [has_continuous_mul N] : has_continuous_mul (M Ã— N) :=
âŸ¨(continuous_fst.fst'.mul continuous_fst.snd').prod_mk
  (continuous_snd.fst'.mul continuous_snd.snd')âŸ©

@[to_additive]
instance pi.has_continuous_mul {C : Î¹ â†’ Type*} [âˆ€ i, topological_space (C i)]
  [âˆ€ i, has_mul (C i)] [âˆ€ i, has_continuous_mul (C i)] : has_continuous_mul (Î  i, C i) :=
{ continuous_mul := continuous_pi (Î» i, (continuous_apply i).fst'.mul (continuous_apply i).snd') }

/-- A version of `pi.has_continuous_mul` for non-dependent functions. It is needed because sometimes
Lean fails to use `pi.has_continuous_mul` for non-dependent functions. -/
@[to_additive "A version of `pi.has_continuous_add` for non-dependent functions. It is needed
because sometimes Lean fails to use `pi.has_continuous_add` for non-dependent functions."]
instance pi.has_continuous_mul' : has_continuous_mul (Î¹ â†’ M) :=
pi.has_continuous_mul

@[priority 100, to_additive]
instance has_continuous_mul_of_discrete_topology [topological_space N]
  [has_mul N] [discrete_topology N] : has_continuous_mul N :=
âŸ¨continuous_of_discrete_topologyâŸ©

open_locale filter

open function

@[to_additive]
lemma has_continuous_mul.of_nhds_one {M : Type u} [monoid M] [topological_space M]
  (hmul : tendsto (uncurry ((*) : M â†’ M â†’ M)) (ğ“ 1 Ã—á¶  ğ“ 1) $ ğ“ 1)
  (hleft : âˆ€ xâ‚€ : M, ğ“ xâ‚€ = map (Î» x, xâ‚€*x) (ğ“ 1))
  (hright : âˆ€ xâ‚€ : M, ğ“ xâ‚€ = map (Î» x, x*xâ‚€) (ğ“ 1)) : has_continuous_mul M :=
âŸ¨begin
    rw continuous_iff_continuous_at,
    rintros âŸ¨xâ‚€, yâ‚€âŸ©,
    have key : (Î» p : M Ã— M, xâ‚€ * p.1 * (p.2 * yâ‚€)) = ((Î» x, xâ‚€*x) âˆ˜ (Î» x, x*yâ‚€)) âˆ˜ (uncurry (*)),
    { ext p, simp [uncurry, mul_assoc] },
    have keyâ‚‚ : (Î» x, xâ‚€*x) âˆ˜ (Î» x, yâ‚€*x) = Î» x, (xâ‚€ *yâ‚€)*x,
    { ext x, simp },
    calc map (uncurry (*)) (ğ“ (xâ‚€, yâ‚€))
        = map (uncurry (*)) (ğ“ xâ‚€ Ã—á¶  ğ“ yâ‚€) : by rw nhds_prod_eq
    ... = map (Î» (p : M Ã— M), xâ‚€ * p.1 * (p.2 * yâ‚€)) ((ğ“ 1) Ã—á¶  (ğ“ 1))
            : by rw [uncurry, hleft xâ‚€, hright yâ‚€, prod_map_map_eq, filter.map_map]
    ... = map ((Î» x, xâ‚€ * x) âˆ˜ Î» x, x * yâ‚€) (map (uncurry (*)) (ğ“ 1 Ã—á¶  ğ“ 1))
            : by { rw [key, â† filter.map_map], }
    ... â‰¤ map ((Î» (x : M), xâ‚€ * x) âˆ˜ Î» x, x * yâ‚€) (ğ“ 1) : map_mono hmul
    ... = ğ“ (xâ‚€*yâ‚€) : by rw [â† filter.map_map, â† hright, hleft yâ‚€, filter.map_map, keyâ‚‚, â† hleft]
  endâŸ©

@[to_additive]
lemma has_continuous_mul_of_comm_of_nhds_one (M : Type u) [comm_monoid M] [topological_space M]
  (hmul : tendsto (uncurry ((*) : M â†’ M â†’ M)) (ğ“ 1 Ã—á¶  ğ“ 1) (ğ“ 1))
  (hleft : âˆ€ xâ‚€ : M, ğ“ xâ‚€ = map (Î» x, xâ‚€*x) (ğ“ 1)) : has_continuous_mul M :=
begin
  apply has_continuous_mul.of_nhds_one hmul hleft,
  intros xâ‚€,
  simp_rw [mul_comm, hleft xâ‚€]
end

end has_continuous_mul

section pointwise_limits

variables (Mâ‚ Mâ‚‚ : Type*) [topological_space Mâ‚‚] [t2_space Mâ‚‚]

@[to_additive] lemma is_closed_set_of_map_one [has_one Mâ‚] [has_one Mâ‚‚] :
  is_closed {f : Mâ‚ â†’ Mâ‚‚ | f 1 = 1} :=
is_closed_eq (continuous_apply 1) continuous_const

@[to_additive] lemma is_closed_set_of_map_mul [has_mul Mâ‚] [has_mul Mâ‚‚] [has_continuous_mul Mâ‚‚] :
  is_closed {f : Mâ‚ â†’ Mâ‚‚ | âˆ€ x y, f (x * y) = f x * f y} :=
begin
  simp only [set_of_forall],
  exact is_closed_Inter (Î» x, is_closed_Inter (Î» y, is_closed_eq (continuous_apply _)
    ((continuous_apply _).mul (continuous_apply _))))
end

variables {Mâ‚ Mâ‚‚} [mul_one_class Mâ‚] [mul_one_class Mâ‚‚] [has_continuous_mul Mâ‚‚]
  {F : Type*} [monoid_hom_class F Mâ‚ Mâ‚‚] {l : filter Î±}

/-- Construct a bundled monoid homomorphism `Mâ‚ â†’* Mâ‚‚` from a function `f` and a proof that it
belongs to the closure of the range of the coercion from `Mâ‚ â†’* Mâ‚‚` (or another type of bundled
homomorphisms that has a `monoid_hom_class` instance) to `Mâ‚ â†’ Mâ‚‚`. -/
@[to_additive "Construct a bundled additive monoid homomorphism `Mâ‚ â†’+ Mâ‚‚` from a function `f`
and a proof that it belongs to the closure of the range of the coercion from `Mâ‚ â†’+ Mâ‚‚` (or another
type of bundled homomorphisms that has a `add_monoid_hom_class` instance) to `Mâ‚ â†’ Mâ‚‚`.",
  simps { fully_applied := ff }]
def monoid_hom_of_mem_closure_range_coe (f : Mâ‚ â†’ Mâ‚‚)
  (hf : f âˆˆ closure (range (Î» (f : F) (x : Mâ‚), f x))) : Mâ‚ â†’* Mâ‚‚ :=
{ to_fun := f,
  map_one' := (is_closed_set_of_map_one Mâ‚ Mâ‚‚).closure_subset_iff.2 (range_subset_iff.2 map_one) hf,
  map_mul' := (is_closed_set_of_map_mul Mâ‚ Mâ‚‚).closure_subset_iff.2
    (range_subset_iff.2 map_mul) hf }

/-- Construct a bundled monoid homomorphism from a pointwise limit of monoid homomorphisms. -/
@[to_additive "Construct a bundled additive monoid homomorphism from a pointwise limit of additive
monoid homomorphisms", simps { fully_applied := ff }]
def monoid_hom_of_tendsto (f : Mâ‚ â†’ Mâ‚‚) (g : Î± â†’ F) [l.ne_bot]
  (h : tendsto (Î» a x, g a x) l (ğ“ f)) : Mâ‚ â†’* Mâ‚‚ :=
monoid_hom_of_mem_closure_range_coe f $ mem_closure_of_tendsto h $
  eventually_of_forall $ Î» a, mem_range_self _

variables (Mâ‚ Mâ‚‚)

@[to_additive] lemma monoid_hom.is_closed_range_coe :
  is_closed (range (coe_fn : (Mâ‚ â†’* Mâ‚‚) â†’ (Mâ‚ â†’ Mâ‚‚))) :=
is_closed_of_closure_subset $ Î» f hf, âŸ¨monoid_hom_of_mem_closure_range_coe f hf, rflâŸ©

end pointwise_limits

@[to_additive] lemma inducing.has_continuous_mul {M N F : Type*} [has_mul M] [has_mul N]
  [mul_hom_class F M N] [topological_space M] [topological_space N] [has_continuous_mul N]
  (f : F) (hf : inducing f) :
  has_continuous_mul M :=
âŸ¨hf.continuous_iff.2 $ by simpa only [(âˆ˜), map_mul f]
  using (hf.continuous.fst'.mul hf.continuous.snd')âŸ©

@[to_additive] lemma has_continuous_mul_induced {M N F : Type*} [has_mul M] [has_mul N]
  [mul_hom_class F M N] [topological_space N] [has_continuous_mul N] (f : F) :
  @has_continuous_mul M (induced f â€¹_â€º) _ :=
by { letI := induced f â€¹_â€º, exact inducing.has_continuous_mul f âŸ¨rflâŸ© }

@[to_additive] instance subsemigroup.has_continuous_mul [topological_space M] [semigroup M]
  [has_continuous_mul M] (S : subsemigroup M) :
  has_continuous_mul S :=
inducing.has_continuous_mul (âŸ¨coe, Î» _ _, rflâŸ© : mul_hom S M) âŸ¨rflâŸ©

@[to_additive] instance submonoid.has_continuous_mul [topological_space M] [monoid M]
  [has_continuous_mul M] (S : submonoid M) :
  has_continuous_mul S :=
S.to_subsemigroup.has_continuous_mul

section has_continuous_mul

variables [topological_space M] [monoid M] [has_continuous_mul M]

@[to_additive]
lemma submonoid.top_closure_mul_self_subset (s : submonoid M) :
  closure (s : set M) * closure s âŠ† closure s :=
image2_subset_iff.2 $ Î» x hx y hy, map_mem_closureâ‚‚ continuous_mul hx hy $
  Î» a ha b hb, s.mul_mem ha hb

@[to_additive]
lemma submonoid.top_closure_mul_self_eq (s : submonoid M) :
  closure (s : set M) * closure s = closure s :=
subset.antisymm
  s.top_closure_mul_self_subset
  (Î» x hx, âŸ¨x, 1, hx, subset_closure s.one_mem, mul_one _âŸ©)

/-- The (topological-space) closure of a submonoid of a space `M` with `has_continuous_mul` is
itself a submonoid. -/
@[to_additive "The (topological-space) closure of an additive submonoid of a space `M` with
`has_continuous_add` is itself an additive submonoid."]
def submonoid.topological_closure (s : submonoid M) : submonoid M :=
{ carrier := closure (s : set M),
  one_mem' := subset_closure s.one_mem,
  mul_mem' := Î» a b ha hb, s.top_closure_mul_self_subset âŸ¨a, b, ha, hb, rflâŸ© }

@[to_additive]
lemma submonoid.le_topological_closure (s : submonoid M) :
  s â‰¤ s.topological_closure :=
subset_closure

@[to_additive]
lemma submonoid.is_closed_topological_closure (s : submonoid M) :
  is_closed (s.topological_closure : set M) :=
by convert is_closed_closure

@[to_additive]
lemma submonoid.topological_closure_minimal
  (s : submonoid M) {t : submonoid M} (h : s â‰¤ t) (ht : is_closed (t : set M)) :
  s.topological_closure â‰¤ t :=
closure_minimal h ht

/-- If a submonoid of a topological monoid is commutative, then so is its topological closure. -/
@[to_additive "If a submonoid of an additive topological monoid is commutative, then so is its
topological closure."]
def submonoid.comm_monoid_topological_closure [t2_space M] (s : submonoid M)
  (hs : âˆ€ (x y : s), x * y = y * x) : comm_monoid s.topological_closure :=
{ mul_comm :=
    have âˆ€ (x âˆˆ s) (y âˆˆ s), x * y = y * x,
      from Î» x hx y hy, congr_arg subtype.val (hs âŸ¨x, hxâŸ© âŸ¨y, hyâŸ©),
    Î» âŸ¨x, hxâŸ© âŸ¨y, hyâŸ©, subtype.ext $
      eq_on_closureâ‚‚ this continuous_mul (continuous_snd.mul continuous_fst) x hx y hy,
  ..s.topological_closure.to_monoid }

@[to_additive exists_open_nhds_zero_half]
lemma exists_open_nhds_one_split {s : set M} (hs : s âˆˆ ğ“ (1 : M)) :
  âˆƒ V : set M, is_open V âˆ§ (1 : M) âˆˆ V âˆ§ âˆ€ (v âˆˆ V) (w âˆˆ V), v * w âˆˆ s :=
have ((Î»a:MÃ—M, a.1 * a.2) â»Â¹' s) âˆˆ ğ“ ((1, 1) : M Ã— M),
  from tendsto_mul (by simpa only [one_mul] using hs),
by simpa only [prod_subset_iff] using exists_nhds_square this

@[to_additive exists_nhds_zero_half]
lemma exists_nhds_one_split {s : set M} (hs : s âˆˆ ğ“ (1 : M)) :
  âˆƒ V âˆˆ ğ“ (1 : M), âˆ€ (v âˆˆ V) (w âˆˆ V), v * w âˆˆ s :=
let âŸ¨V, Vo, V1, hVâŸ© := exists_open_nhds_one_split hs
in âŸ¨V, is_open.mem_nhds Vo V1, hVâŸ©

@[to_additive exists_nhds_zero_quarter]
lemma exists_nhds_one_split4 {u : set M} (hu : u âˆˆ ğ“ (1 : M)) :
  âˆƒ V âˆˆ ğ“ (1 : M),
    âˆ€ {v w s t}, v âˆˆ V â†’ w âˆˆ V â†’ s âˆˆ V â†’ t âˆˆ V â†’ v * w * s * t âˆˆ u :=
begin
  rcases exists_nhds_one_split hu with âŸ¨W, W1, hâŸ©,
  rcases exists_nhds_one_split W1 with âŸ¨V, V1, h'âŸ©,
  use [V, V1],
  intros v w s t v_in w_in s_in t_in,
  simpa only [mul_assoc] using h _ (h' v v_in w w_in) _ (h' s s_in t t_in)
end

/-- Given a neighborhood `U` of `1` there is an open neighborhood `V` of `1`
such that `VV âŠ† U`. -/
@[to_additive "Given a open neighborhood `U` of `0` there is a open neighborhood `V` of `0`
  such that `V + V âŠ† U`."]
lemma exists_open_nhds_one_mul_subset {U : set M} (hU : U âˆˆ ğ“ (1 : M)) :
  âˆƒ V : set M, is_open V âˆ§ (1 : M) âˆˆ V âˆ§ V * V âŠ† U :=
begin
  rcases exists_open_nhds_one_split hU with âŸ¨V, Vo, V1, hVâŸ©,
  use [V, Vo, V1],
  rintros _ âŸ¨x, y, hx, hy, rflâŸ©,
  exact hV _ hx _ hy
end

@[to_additive]
lemma is_compact.mul {s t : set M} (hs : is_compact s) (ht : is_compact t) : is_compact (s * t) :=
by { rw [â† image_mul_prod], exact (hs.prod ht).image continuous_mul }

@[to_additive]
lemma tendsto_list_prod {f : Î¹ â†’ Î± â†’ M} {x : filter Î±} {a : Î¹ â†’ M} :
  âˆ€ l:list Î¹, (âˆ€iâˆˆl, tendsto (f i) x (ğ“ (a i))) â†’
    tendsto (Î»b, (l.map (Î»c, f c b)).prod) x (ğ“ ((l.map a).prod))
| []       _ := by simp [tendsto_const_nhds]
| (f :: l) h :=
  begin
    simp only [list.map_cons, list.prod_cons],
    exact (h f (list.mem_cons_self _ _)).mul
      (tendsto_list_prod l (assume c hc, h c (list.mem_cons_of_mem _ hc)))
  end

@[to_additive]
lemma continuous_list_prod {f : Î¹ â†’ X â†’ M} (l : list Î¹)
  (h : âˆ€ i âˆˆ l, continuous (f i)) :
  continuous (Î» a, (l.map (Î» i, f i a)).prod) :=
continuous_iff_continuous_at.2 $ assume x, tendsto_list_prod l $ assume c hc,
  continuous_iff_continuous_at.1 (h c hc) x

@[to_additive]
lemma continuous_on_list_prod {f : Î¹ â†’ X â†’ M} (l : list Î¹) {t : set X}
  (h : âˆ€ i âˆˆ l, continuous_on (f i) t) :
  continuous_on (Î» a, (l.map (Î» i, f i a)).prod) t :=
begin
  intros x hx,
  rw continuous_within_at_iff_continuous_at_restrict _ hx,
  refine tendsto_list_prod _ (Î» i hi, _),
  specialize h i hi x hx,
  rw continuous_within_at_iff_continuous_at_restrict _ hx at h,
  exact h,
end

@[continuity, to_additive]
lemma continuous_pow : âˆ€ n : â„•, continuous (Î» a : M, a ^ n)
| 0 := by simpa using continuous_const
| (k+1) := by { simp only [pow_succ], exact continuous_id.mul (continuous_pow _) }

instance add_monoid.has_continuous_const_smul_nat {A} [add_monoid A] [topological_space A]
  [has_continuous_add A] : has_continuous_const_smul â„• A := âŸ¨continuous_nsmulâŸ©

instance add_monoid.has_continuous_smul_nat {A} [add_monoid A] [topological_space A]
  [has_continuous_add A] : has_continuous_smul â„• A :=
âŸ¨continuous_uncurry_of_discrete_topology continuous_nsmulâŸ©

@[continuity, to_additive continuous.nsmul]
lemma continuous.pow {f : X â†’ M} (h : continuous f) (n : â„•) :
  continuous (Î» b, (f b) ^ n) :=
(continuous_pow n).comp h

@[to_additive]
lemma continuous_on_pow {s : set M} (n : â„•) : continuous_on (Î» x, x ^ n) s :=
(continuous_pow n).continuous_on

@[to_additive]
lemma continuous_at_pow (x : M) (n : â„•) : continuous_at (Î» x, x ^ n) x :=
(continuous_pow n).continuous_at

@[to_additive filter.tendsto.nsmul]
lemma filter.tendsto.pow {l : filter Î±} {f : Î± â†’ M} {x : M} (hf : tendsto f l (ğ“ x)) (n : â„•) :
  tendsto (Î» x, f x ^ n) l (ğ“ (x ^ n)) :=
(continuous_at_pow _ _).tendsto.comp hf

@[to_additive continuous_within_at.nsmul]
lemma continuous_within_at.pow {f : X â†’ M} {x : X} {s : set X} (hf : continuous_within_at f s x)
  (n : â„•) : continuous_within_at (Î» x, f x ^ n) s x :=
hf.pow n

@[to_additive continuous_at.nsmul]
lemma continuous_at.pow {f : X â†’ M} {x : X} (hf : continuous_at f x) (n : â„•) :
  continuous_at (Î» x, f x ^ n) x :=
hf.pow n

@[to_additive continuous_on.nsmul]
lemma continuous_on.pow {f : X â†’ M} {s : set X} (hf : continuous_on f s) (n : â„•) :
  continuous_on (Î» x, f x ^ n) s :=
Î» x hx, (hf x hx).pow n

/-- Left-multiplication by a left-invertible element of a topological monoid is proper, i.e.,
inverse images of compact sets are compact. -/
lemma filter.tendsto_cocompact_mul_left {a b : M} (ha : b * a = 1) :
  filter.tendsto (Î» x : M, a * x) (filter.cocompact M) (filter.cocompact M) :=
begin
  refine filter.tendsto.of_tendsto_comp _ (filter.comap_cocompact_le (continuous_mul_left b)),
  convert filter.tendsto_id,
  ext x,
  simp [ha],
end

/-- Right-multiplication by a right-invertible element of a topological monoid is proper, i.e.,
inverse images of compact sets are compact. -/
lemma filter.tendsto_cocompact_mul_right {a b : M} (ha : a * b = 1) :
  filter.tendsto (Î» x : M, x * a) (filter.cocompact M) (filter.cocompact M) :=
begin
  refine filter.tendsto.of_tendsto_comp _ (filter.comap_cocompact_le (continuous_mul_right b)),
  convert filter.tendsto_id,
  ext x,
  simp [ha],
end

/-- If `R` acts on `A` via `A`, then continuous multiplication implies continuous scalar
multiplication by constants.

Notably, this instances applies when `R = A`, or when `[algebra R A]` is available. -/
@[priority 100, to_additive  "If `R` acts on `A` via `A`, then continuous addition implies
continuous affine addition by constants."]
instance is_scalar_tower.has_continuous_const_smul {R A : Type*} [monoid A] [has_smul R A]
  [is_scalar_tower R A A] [topological_space A] [has_continuous_mul A] :
  has_continuous_const_smul R A :=
{ continuous_const_smul := Î» q, begin
    simp only [â†smul_one_mul q (_ : A)] { single_pass := tt },
    exact continuous_const.mul continuous_id,
  end }

/-- If the action of `R` on `A` commutes with left-multiplication, then continuous multiplication
implies continuous scalar multiplication by constants.

Notably, this instances applies when `R = Aáµáµ’áµ–` -/
@[priority 100, to_additive "If the action of `R` on `A` commutes with left-addition, then
continuous addition implies continuous affine addition by constants.

Notably, this instances applies when `R = Aáµƒáµ’áµ–`. "]
instance smul_comm_class.has_continuous_const_smul {R A : Type*} [monoid A] [has_smul R A]
  [smul_comm_class R A A] [topological_space A] [has_continuous_mul A] :
  has_continuous_const_smul R A :=
{ continuous_const_smul := Î» q, begin
    simp only [â†mul_smul_one q (_ : A)] { single_pass := tt },
    exact continuous_id.mul continuous_const,
  end }

end has_continuous_mul

namespace mul_opposite

/-- If multiplication is continuous in `Î±`, then it also is in `Î±áµáµ’áµ–`. -/
@[to_additive "If addition is continuous in `Î±`, then it also is in `Î±áµƒáµ’áµ–`."]
instance [topological_space Î±] [has_mul Î±] [has_continuous_mul Î±] : has_continuous_mul Î±áµáµ’áµ– :=
âŸ¨continuous_op.comp (continuous_unop.snd'.mul continuous_unop.fst')âŸ©

end mul_opposite

namespace units

open mul_opposite

variables [topological_space Î±] [monoid Î±] [has_continuous_mul Î±]

/-- If multiplication on a monoid is continuous, then multiplication on the units of the monoid,
with respect to the induced topology, is continuous.

Inversion is also continuous, but we register this in a later file, `topology.algebra.group`,
because the predicate `has_continuous_inv` has not yet been defined. -/
@[to_additive "If addition on an additive monoid is continuous, then addition on the additive units
of the monoid, with respect to the induced topology, is continuous.

Negation is also continuous, but we register this in a later file, `topology.algebra.group`, because
the predicate `has_continuous_neg` has not yet been defined."]
instance : has_continuous_mul Î±Ë£ := inducing_embed_product.has_continuous_mul (embed_product Î±)

end units

@[to_additive] lemma continuous.units_map [monoid M] [monoid N] [topological_space M]
  [topological_space N] (f : M â†’* N) (hf : continuous f) : continuous (units.map f) :=
units.continuous_iff.2 âŸ¨hf.comp units.continuous_coe, hf.comp units.continuous_coe_invâŸ©

section

variables [topological_space M] [comm_monoid M]

@[to_additive]
lemma submonoid.mem_nhds_one (S : submonoid M) (oS : is_open (S : set M)) :
  (S : set M) âˆˆ ğ“ (1 : M) :=
is_open.mem_nhds oS S.one_mem

variable [has_continuous_mul M]

@[to_additive]
lemma tendsto_multiset_prod {f : Î¹ â†’ Î± â†’ M} {x : filter Î±} {a : Î¹ â†’ M} (s : multiset Î¹) :
  (âˆ€ i âˆˆ s, tendsto (f i) x (ğ“ (a i))) â†’
    tendsto (Î»b, (s.map (Î»c, f c b)).prod) x (ğ“ ((s.map a).prod)) :=
by { rcases s with âŸ¨lâŸ©, simpa using tendsto_list_prod l }

@[to_additive]
lemma tendsto_finset_prod {f : Î¹ â†’ Î± â†’ M} {x : filter Î±} {a : Î¹ â†’ M} (s : finset Î¹) :
  (âˆ€ i âˆˆ s, tendsto (f i) x (ğ“ (a i))) â†’ tendsto (Î»b, âˆ c in s, f c b) x (ğ“ (âˆ c in s, a c)) :=
tendsto_multiset_prod _

@[continuity, to_additive]
lemma continuous_multiset_prod {f : Î¹ â†’ X â†’ M} (s : multiset Î¹) :
  (âˆ€ i âˆˆ s, continuous (f i)) â†’ continuous (Î» a, (s.map (Î» i, f i a)).prod) :=
by { rcases s with âŸ¨lâŸ©, simpa using continuous_list_prod l }

@[to_additive]
lemma continuous_on_multiset_prod {f : Î¹ â†’ X â†’ M} (s : multiset Î¹) {t : set X} :
  (âˆ€i âˆˆ s, continuous_on (f i) t) â†’ continuous_on (Î» a, (s.map (Î» i, f i a)).prod) t :=
by { rcases s with âŸ¨lâŸ©, simpa using continuous_on_list_prod l }

@[continuity, to_additive]
lemma continuous_finset_prod {f : Î¹ â†’ X â†’ M} (s : finset Î¹) :
  (âˆ€ i âˆˆ s, continuous (f i)) â†’ continuous (Î» a, âˆ i in s, f i a) :=
continuous_multiset_prod _

@[to_additive]
lemma continuous_on_finset_prod {f : Î¹ â†’ X â†’ M} (s : finset Î¹) {t : set X} :
  (âˆ€ i âˆˆ s, continuous_on (f i) t) â†’ continuous_on (Î» a, âˆ i in s, f i a) t :=
continuous_on_multiset_prod _

@[to_additive] lemma eventually_eq_prod {X M : Type*} [comm_monoid M]
  {s : finset Î¹} {l : filter X} {f g : Î¹ â†’ X â†’ M} (hs : âˆ€ i âˆˆ s, f i =á¶ [l] g i) :
  âˆ i in s, f i =á¶ [l] âˆ i in s, g i :=
begin
  replace hs: âˆ€á¶  x in l, âˆ€ i âˆˆ s, f i x = g i x,
  { rwa eventually_all_finset },
  filter_upwards [hs] with x hx,
  simp only [finset.prod_apply, finset.prod_congr rfl hx],
end

open function

@[to_additive]
lemma locally_finite.exists_finset_mul_support {M : Type*} [comm_monoid M] {f : Î¹ â†’ X â†’ M}
  (hf : locally_finite (Î» i, mul_support $ f i)) (xâ‚€ : X) :
  âˆƒ I : finset Î¹, âˆ€á¶  x in ğ“ xâ‚€, mul_support (Î» i, f i x) âŠ† I :=
begin
  rcases hf xâ‚€ with âŸ¨U, hxU, hUfâŸ©,
  refine âŸ¨hUf.to_finset, mem_of_superset hxU $ Î» y hy i hi, _âŸ©,
  rw [hUf.coe_to_finset],
  exact âŸ¨y, hi, hyâŸ©
end

@[to_additive] lemma finprod_eventually_eq_prod {M : Type*} [comm_monoid M]
  {f : Î¹ â†’ X â†’ M} (hf : locally_finite (Î» i, mul_support (f i))) (x : X) :
  âˆƒ s : finset Î¹, âˆ€á¶  y in ğ“ x, (âˆá¶  i, f i y) = âˆ i in s, f i y :=
let âŸ¨I, hIâŸ© := hf.exists_finset_mul_support x in
  âŸ¨I, hI.mono (Î» y hy, finprod_eq_prod_of_mul_support_subset _ $ Î» i hi, hy hi)âŸ©

@[to_additive] lemma continuous_finprod {f : Î¹ â†’ X â†’ M} (hc : âˆ€ i, continuous (f i))
  (hf : locally_finite (Î» i, mul_support (f i))) :
  continuous (Î» x, âˆá¶  i, f i x) :=
begin
  refine continuous_iff_continuous_at.2 (Î» x, _),
  rcases finprod_eventually_eq_prod hf x with âŸ¨s, hsâŸ©,
  refine continuous_at.congr _ (eventually_eq.symm hs),
  exact tendsto_finset_prod _ (Î» i hi, (hc i).continuous_at),
end

@[to_additive] lemma continuous_finprod_cond {f : Î¹ â†’ X â†’ M} {p : Î¹ â†’ Prop}
  (hc : âˆ€ i, p i â†’ continuous (f i)) (hf : locally_finite (Î» i, mul_support (f i))) :
  continuous (Î» x, âˆá¶  i (hi : p i), f i x) :=
begin
  simp only [â† finprod_subtype_eq_finprod_cond],
  exact continuous_finprod (Î» i, hc i i.2) (hf.comp_injective subtype.coe_injective)
end

end

instance [topological_space M] [has_mul M] [has_continuous_mul M] :
  has_continuous_add (additive M) :=
{ continuous_add := @continuous_mul M _ _ _ }

instance [topological_space M] [has_add M] [has_continuous_add M] :
  has_continuous_mul (multiplicative M) :=
{ continuous_mul := @continuous_add M _ _ _ }

section lattice_ops

variables {Î¹' : Sort*} [has_mul M]

@[to_additive] lemma has_continuous_mul_Inf {ts : set (topological_space M)}
  (h : Î  t âˆˆ ts, @has_continuous_mul M t _) :
  @has_continuous_mul M (Inf ts) _ :=
{ continuous_mul := continuous_Inf_rng.2 (Î» t ht, continuous_Inf_domâ‚‚ ht ht
  (@has_continuous_mul.continuous_mul M t _ (h t ht))) }

@[to_additive] lemma has_continuous_mul_infi {ts : Î¹' â†’ topological_space M}
  (h' : Î  i, @has_continuous_mul M (ts i) _) :
  @has_continuous_mul M (â¨… i, ts i) _ :=
by { rw â† Inf_range, exact has_continuous_mul_Inf (set.forall_range_iff.mpr h') }

@[to_additive] lemma has_continuous_mul_inf {tâ‚ tâ‚‚ : topological_space M}
  (hâ‚ : @has_continuous_mul M tâ‚ _) (hâ‚‚ : @has_continuous_mul M tâ‚‚ _) :
  @has_continuous_mul M (tâ‚ âŠ“ tâ‚‚) _ :=
by { rw inf_eq_infi, refine has_continuous_mul_infi (Î» b, _), cases b; assumption }

end lattice_ops

namespace continuous_map

variables [has_mul X] [has_continuous_mul X]

/-- The continuous map `Î» y, y * x` -/
@[to_additive "The continuous map `Î» y, y + x"]
protected def mul_right (x : X) : C(X, X) := mk _ (continuous_mul_right x)

@[to_additive, simp]
lemma coe_mul_right (x : X) : â‡‘(continuous_map.mul_right x) = Î» y, y * x := rfl

/-- The continuous map `Î» y, x * y` -/
@[to_additive "The continuous map `Î» y, x + y"]
protected def mul_left (x : X) : C(X, X) := mk _ (continuous_mul_left x)

@[to_additive, simp]
lemma coe_mul_left (x : X) : â‡‘(continuous_map.mul_left x) = Î» y, x * y := rfl

end continuous_map
