/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/

import topology.algebra.nonarchimedean.basic
import topology.algebra.filter_basis
import algebra.module.submodule.pointwise

/-!
# Neighborhood bases for non-archimedean rings and modules

This files contains special families of filter bases on rings and modules that give rise to
non-archimedean topologies.

The main definition is `ring_subgroups_basis` which is a predicate on a family of
additive subgroups of a ring. The predicate ensures there is a topology
`ring_subgroups_basis.topology` which is compatible with a ring structure and admits the given
family as a basis of neighborhoods of zero. In particular the given subgroups become open subgroups
(bundled in `ring_subgroups_basis.open_add_subgroup`) and we get a non-archimedean topological ring
(`ring_subgroups_basis.nonarchimedean`).

A special case of this construction is given by `submodules_basis` where the subgroups are
sub-modules in a commutative algebra. This important example gives rises to the adic topology
(studied in its own file).

-/

open set filter function lattice add_group_with_zero_nhd
open_locale topological_space filter pointwise

/-- A family of additive subgroups on a ring `A` is a subgroups basis if it satisfies some
axioms ensuring there is a topology on `A` which is compatible with the ring structure and
admits this family as a basis of neighborhoods of zero. -/
structure ring_subgroups_basis {A Œπ : Type*} [ring A] (B : Œπ ‚Üí add_subgroup A) : Prop :=
(inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j)
(mul : ‚àÄ i, ‚àÉ j, (B j : set A) * B j ‚äÜ B i)
(left_mul : ‚àÄ x : A, ‚àÄ i, ‚àÉ j, (B j : set A) ‚äÜ (Œª y : A, x*y) ‚Åª¬π' (B i))
(right_mul : ‚àÄ x : A, ‚àÄ i, ‚àÉ j, (B j : set A) ‚äÜ (Œª y : A, y*x) ‚Åª¬π' (B i))

namespace ring_subgroups_basis

variables {A Œπ : Type*} [ring A]

lemma of_comm {A Œπ : Type*} [comm_ring A] (B : Œπ ‚Üí add_subgroup A)
  (inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j)
  (mul : ‚àÄ i, ‚àÉ j, (B j : set A) * B j ‚äÜ B i)
  (left_mul : ‚àÄ x : A, ‚àÄ i, ‚àÉ j, (B j : set A) ‚äÜ (Œª y : A, x*y) ‚Åª¬π' (B i)) :
  ring_subgroups_basis B :=
{ inter := inter,
  mul := mul,
  left_mul := left_mul,
  right_mul := begin
    intros x i,
    cases left_mul x i with j hj,
    use j,
    simpa [mul_comm] using hj
  end }

/-- Every subgroups basis on a ring leads to a ring filter basis. -/
def to_ring_filter_basis [nonempty Œπ] {B : Œπ ‚Üí add_subgroup A}
  (hB : ring_subgroups_basis B) : ring_filter_basis A :=
{ sets := {U | ‚àÉ i, U = B i},
  nonempty := by { inhabit Œπ, exact ‚ü®B default, default, rfl‚ü© },
  inter_sets := begin
    rintros _ _ ‚ü®i, rfl‚ü© ‚ü®j, rfl‚ü©,
    cases hB.inter i j with k hk,
    use [B k, k, rfl, hk]
  end,
  zero' := by { rintros _ ‚ü®i, rfl‚ü©, exact (B i).zero_mem },
  add' := begin
    rintros _ ‚ü®i, rfl‚ü©,
    use [B i, i, rfl],
    rintros x ‚ü®y, z, y_in, z_in, rfl‚ü©,
    exact (B i).add_mem y_in z_in
  end,
  neg' := begin
    rintros _ ‚ü®i, rfl‚ü©,
    use [B i, i, rfl],
    intros x x_in,
    exact (B i).neg_mem x_in
  end,
  conj' := begin
    rintros x‚ÇÄ _ ‚ü®i, rfl‚ü©,
    use [B i, i, rfl],
    simp
  end,
  mul' := begin
    rintros _ ‚ü®i, rfl‚ü©,
    cases hB.mul i with k hk,
    use [B k, k, rfl, hk]
  end,
  mul_left' := begin
    rintros x‚ÇÄ _ ‚ü®i, rfl‚ü©,
    cases hB.left_mul x‚ÇÄ i with k hk,
    use [B k, k, rfl, hk]
  end,
  mul_right' := begin
    rintros x‚ÇÄ _ ‚ü®i, rfl‚ü©,
    cases hB.right_mul x‚ÇÄ i with k hk,
    use [B k, k, rfl, hk]
  end }

variables [nonempty Œπ] {B : Œπ ‚Üí add_subgroup A} (hB : ring_subgroups_basis B)

lemma mem_add_group_filter_basis_iff {V : set A} :
  V ‚àà hB.to_ring_filter_basis.to_add_group_filter_basis ‚Üî ‚àÉ i, V = B i :=
iff.rfl

lemma mem_add_group_filter_basis (i) :
  (B i : set A) ‚àà hB.to_ring_filter_basis.to_add_group_filter_basis :=
‚ü®i, rfl‚ü©

/-- The topology defined from a subgroups basis, admitting the given subgroups as a basis
of neighborhoods of zero. -/
def topology : topological_space A :=
hB.to_ring_filter_basis.to_add_group_filter_basis.topology

lemma has_basis_nhds_zero : has_basis (@nhds A hB.topology 0) (Œª _, true) (Œª i, B i) :=
‚ü®begin
  intros s,
  rw hB.to_ring_filter_basis.to_add_group_filter_basis.nhds_zero_has_basis.mem_iff,
  split,
  { rintro ‚ü®-, ‚ü®i, rfl‚ü©, hi‚ü©,
    exact ‚ü®i, trivial, hi‚ü© },
  { rintro ‚ü®i, -, hi‚ü©,
    exact ‚ü®B i, ‚ü®i, rfl‚ü©, hi‚ü© }
end‚ü©

lemma has_basis_nhds (a : A) :
  has_basis (@nhds A hB.topology a) (Œª _, true) (Œª i, {b | b - a ‚àà B i}) :=
‚ü®begin
  intros s,
  rw (hB.to_ring_filter_basis.to_add_group_filter_basis.nhds_has_basis a).mem_iff,
  simp only [exists_prop, exists_true_left],
  split,
  { rintro ‚ü®-, ‚ü®i, rfl‚ü©, hi‚ü©,
    use i,
    convert hi,
    ext b,
    split,
    { intros h,
      use [b - a, h],
      abel },
    { rintros ‚ü®c, hc, rfl‚ü©,
      simpa using hc } },
  { rintros ‚ü®i, hi‚ü©,
    use [B i, i, rfl],
    rw image_subset_iff,
    rintro b b_in,
    apply hi,
    simpa using b_in }
end‚ü©

/-- Given a subgroups basis, the basis elements as open additive subgroups in the associated
topology. -/
def open_add_subgroup (i : Œπ) : @open_add_subgroup A _ hB.topology:=
{ is_open' := begin
    letI := hB.topology,
    rw is_open_iff_mem_nhds,
    intros a a_in,
    rw (hB.has_basis_nhds a).mem_iff,
    use [i, trivial],
    rintros b b_in,
    simpa using (B i).add_mem a_in b_in
  end,
  ..B i }

-- see Note [nonarchimedean non instances]
lemma nonarchimedean : @nonarchimedean_ring A _ hB.topology :=
begin
  letI := hB.topology,
  constructor,
  intros U hU,
  obtain ‚ü®i, -, hi : (B i : set A) ‚äÜ U‚ü© := hB.has_basis_nhds_zero.mem_iff.mp hU,
  exact ‚ü®hB.open_add_subgroup i, hi‚ü©
end

end ring_subgroups_basis

variables {Œπ R A : Type*} [comm_ring R] [comm_ring A] [algebra R A]

/-- A family of submodules in a commutative `R`-algebra `A` is a submodules basis if it satisfies
some axioms ensuring there is a topology on `A` which is compatible with the ring structure and
admits this family as a basis of neighborhoods of zero. -/
structure submodules_ring_basis (B : Œπ ‚Üí submodule R A) : Prop :=
(inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j)
(left_mul : ‚àÄ (a : A) i, ‚àÉ j, a ‚Ä¢ B j ‚â§ B i)
(mul      : ‚àÄ i, ‚àÉ j, (B j : set A) * B j ‚äÜ B i)

namespace submodules_ring_basis

variables {B : Œπ ‚Üí submodule R A} (hB : submodules_ring_basis B)

lemma to_ring_subgroups_basis (hB : submodules_ring_basis B) :
  ring_subgroups_basis (Œª i, (B i).to_add_subgroup) :=
begin
  apply ring_subgroups_basis.of_comm (Œª i, (B i).to_add_subgroup) hB.inter hB.mul,
  intros a i,
  rcases hB.left_mul a i with ‚ü®j, hj‚ü©,
  use j,
  rintros b (b_in : b ‚àà B j),
  exact hj ‚ü®b, b_in, rfl‚ü©
end

/-- The topology associated to a basis of submodules in an algebra. -/
def topology [nonempty Œπ] (hB : submodules_ring_basis B) : topological_space A :=
hB.to_ring_subgroups_basis.topology

end submodules_ring_basis

variables {M : Type*} [add_comm_group M] [module R M]

/-- A family of submodules in an `R`-module `M` is a submodules basis if it satisfies
some axioms ensuring there is a topology on `M` which is compatible with the module structure and
admits this family as a basis of neighborhoods of zero. -/
structure submodules_basis [topological_space R]
  (B : Œπ ‚Üí submodule R M) : Prop :=
(inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j)
(smul : ‚àÄ (m : M) (i : Œπ), ‚àÄ·∂† a in ùìù (0 : R), a ‚Ä¢ m ‚àà B i)

namespace submodules_basis

variables [topological_space R] [nonempty Œπ] {B : Œπ ‚Üí submodule R M}
          (hB : submodules_basis B)

include hB

/-- The image of a submodules basis is a module filter basis. -/
def to_module_filter_basis : module_filter_basis R M :=
{ sets := {U | ‚àÉ i, U = B i},
  nonempty := by { inhabit Œπ, exact ‚ü®B default, default, rfl‚ü© },
  inter_sets := begin
    rintros _ _ ‚ü®i, rfl‚ü© ‚ü®j, rfl‚ü©,
    cases hB.inter i j with k hk,
    use [B k, k, rfl, hk]
  end,
  zero' := by { rintros _ ‚ü®i, rfl‚ü©, exact (B i).zero_mem },
  add' := begin
    rintros _ ‚ü®i, rfl‚ü©,
    use [B i, i, rfl],
    rintros x ‚ü®y, z, y_in, z_in, rfl‚ü©,
    exact (B i).add_mem y_in z_in
  end,
  neg' := begin
    rintros _ ‚ü®i, rfl‚ü©,
    use [B i, i, rfl],
    intros x x_in,
    exact (B i).neg_mem x_in
  end,
  conj' := begin
    rintros x‚ÇÄ _ ‚ü®i, rfl‚ü©,
    use [B i, i, rfl],
    simp
  end,
  smul' := begin
    rintros _ ‚ü®i, rfl‚ü©,
    use [univ, univ_mem, B i, i, rfl],
    rintros _ ‚ü®a, m, -, hm, rfl‚ü©,
    exact (B i).smul_mem _ hm
  end,
  smul_left' := begin
    rintros x‚ÇÄ _ ‚ü®i, rfl‚ü©,
    use [B i, i, rfl],
    intros m,
    exact (B i).smul_mem _
  end,
  smul_right' := begin
    rintros m‚ÇÄ _ ‚ü®i, rfl‚ü©,
    exact hB.smul m‚ÇÄ i
  end }

/-- The topology associated to a basis of submodules in a module. -/
def topology : topological_space M :=
hB.to_module_filter_basis.to_add_group_filter_basis.topology

/-- Given a submodules basis, the basis elements as open additive subgroups in the associated
topology. -/
def open_add_subgroup (i : Œπ) : @open_add_subgroup M _ hB.topology :=
{ is_open' := begin
    letI := hB.topology,
    rw is_open_iff_mem_nhds,
    intros a a_in,
    rw (hB.to_module_filter_basis.to_add_group_filter_basis.nhds_has_basis a).mem_iff,
    use [B i, i, rfl],
    rintros - ‚ü®b, b_in, rfl‚ü©,
    exact (B i).add_mem a_in b_in
  end,
  ..(B i).to_add_subgroup }

-- see Note [nonarchimedean non instances]
lemma nonarchimedean (hB : submodules_basis B) : @nonarchimedean_add_group M _ hB.topology:=
begin
  letI := hB.topology,
  constructor,
  intros U hU,
  obtain ‚ü®-, ‚ü®i, rfl‚ü©, hi : (B i : set M) ‚äÜ U‚ü© :=
    hB.to_module_filter_basis.to_add_group_filter_basis.nhds_zero_has_basis.mem_iff.mp hU,
  exact ‚ü®hB.open_add_subgroup i, hi‚ü©
end

/-- The non archimedean subgroup basis lemmas cannot be instances because some instances
(such as `measure_theory.ae_eq_fun.add_monoid ` or `topological_add_group.to_has_continuous_add`)
cause the search for `@topological_add_group Œ≤ ?m1 ?m2`, i.e. a search for a topological group where
the topology/group structure are unknown. -/
library_note "nonarchimedean non instances"
end submodules_basis

section
/-
In this section, we check that, in a `R`-algebra `A` over a ring equipped with a topology,
a basis of `R`-submodules which is compatible with the topology on `R` is also a submodule basis
in the sense of `R`-modules (forgetting about the ring structure on `A`) and those two points of
view definitionaly gives the same topology on `A`.
-/
variables [topological_space R] {B : Œπ ‚Üí submodule R A} (hB : submodules_ring_basis B)
          (hsmul : ‚àÄ (m : A) (i : Œπ), ‚àÄ·∂† (a : R) in ùìù 0, a ‚Ä¢ m ‚àà B i)

lemma submodules_ring_basis.to_submodules_basis : submodules_basis B :=
{ inter := hB.inter,
  smul := hsmul }

example [nonempty Œπ] : hB.topology = (hB.to_submodules_basis hsmul).topology := rfl
end

/-- Given a ring filter basis on a commutative ring `R`, define a compatibility condition
on a family of submodules of a `R`-module `M`. This compatibility condition allows to get
a topological module structure. -/
structure ring_filter_basis.submodules_basis (BR : ring_filter_basis R)
  (B : Œπ ‚Üí submodule R M) : Prop :=
(inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j)
(smul : ‚àÄ (m : M) (i : Œπ), ‚àÉ U ‚àà BR, U ‚äÜ (Œª a, a ‚Ä¢ m) ‚Åª¬π' B i)


lemma ring_filter_basis.submodules_basis_is_basis (BR : ring_filter_basis R) {B : Œπ ‚Üí submodule R M}
  (hB : BR.submodules_basis B) : @submodules_basis Œπ R _ M _ _ BR.topology B  :=
{ inter := hB.inter,
  smul := begin
    letI := BR.topology,
    intros m i,
    rcases hB.smul m i with ‚ü®V, V_in, hV‚ü©,
    exact mem_of_superset (BR.to_add_group_filter_basis.mem_nhds_zero V_in) hV
  end }

/-- The module filter basis associated to a ring filter basis and a compatible submodule basis.
This allows to build a topological module structure compatible with the given module structure
and the topology associated to the given ring filter basis. -/
def ring_filter_basis.module_filter_basis [nonempty Œπ] (BR : ring_filter_basis R)
  {B : Œπ ‚Üí submodule R M} (hB : BR.submodules_basis B) :
  @module_filter_basis R M _ BR.topology _ _ :=
@submodules_basis.to_module_filter_basis  Œπ R _ M _ _ BR.topology _ _
  (BR.submodules_basis_is_basis hB)
