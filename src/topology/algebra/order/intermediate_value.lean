/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Alistair Tucker
-/
import order.complete_lattice_intervals
import topology.order.basic

/-!
# Intermediate Value Theorem

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we prove the Intermediate Value Theorem: if `f : Œ± ‚Üí Œ≤` is a function defined on a
connected set `s` that takes both values `‚â§ a` and values `‚â• a` on `s`, then it is equal to `a` at
some point of `s`. We also prove that intervals in a dense conditionally complete order are
preconnected and any preconnected set is an interval. Then we specialize IVT to functions continuous
on intervals.

## Main results

* `is_preconnected_I??` : all intervals `I??` are preconnected,
* `is_preconnected.intermediate_value`, `intermediate_value_univ` : Intermediate Value Theorem for
  connected sets and connected spaces, respectively;
* `intermediate_value_Icc`, `intermediate_value_Icc'`: Intermediate Value Theorem for functions
  on closed intervals.

### Miscellaneous facts

* `is_closed.Icc_subset_of_forall_mem_nhds_within` : ‚ÄúContinuous induction‚Äù principle;
  if `s ‚à© [a, b]` is closed, `a ‚àà s`, and for each `x ‚àà [a, b) ‚à© s` some of its right neighborhoods
  is included `s`, then `[a, b] ‚äÜ s`.
* `is_closed.Icc_subset_of_forall_exists_gt`, `is_closed.mem_of_ge_of_forall_exists_gt` : two
  other versions of the ‚Äúcontinuous induction‚Äù principle.

## Tags

intermediate value theorem, connected space, connected set
-/

open filter order_dual topological_space function set
open_locale topology filter

universes u v w

/-!
### Intermediate value theorem on a (pre)connected space

In this section we prove the following theorem (see `is_preconnected.intermediate_value‚ÇÇ`): if `f`
and `g` are two functions continuous on a preconnected set `s`, `f a ‚â§ g a` at some `a ‚àà s` and
`g b ‚â§ f b` at some `b ‚àà s`, then `f c = g c` at some `c ‚àà s`. We prove several versions of this
statement, including the classical IVT that corresponds to a constant function `g`.
-/

section

variables {X : Type u} {Œ± : Type v} [topological_space X]
  [linear_order Œ±] [topological_space Œ±] [order_closed_topology Œ±]

/-- Intermediate value theorem for two functions: if `f` and `g` are two continuous functions
on a preconnected space and `f a ‚â§ g a` and `g b ‚â§ f b`, then for some `x` we have `f x = g x`. -/
lemma intermediate_value_univ‚ÇÇ [preconnected_space X] {a b : X} {f g : X ‚Üí Œ±} (hf : continuous f)
  (hg : continuous g) (ha : f a ‚â§ g a) (hb : g b ‚â§ f b) :
  ‚àÉ x, f x = g x :=
begin
  obtain ‚ü®x, h, hfg, hgf‚ü© : (univ ‚à© {x | f x ‚â§ g x ‚àß g x ‚â§ f x}).nonempty,
    from is_preconnected_closed_iff.1 preconnected_space.is_preconnected_univ _ _
      (is_closed_le hf hg) (is_closed_le hg hf) (Œª x hx, le_total _ _) ‚ü®a, trivial, ha‚ü©
      ‚ü®b, trivial, hb‚ü©,
  exact ‚ü®x, le_antisymm hfg hgf‚ü©
end

lemma intermediate_value_univ‚ÇÇ_eventually‚ÇÅ [preconnected_space X] {a : X} {l : filter X} [ne_bot l]
  {f g : X ‚Üí Œ±} (hf : continuous f) (hg : continuous g) (ha : f a ‚â§ g a) (he : g ‚â§·∂†[l] f) :
  ‚àÉ x, f x = g x :=
let ‚ü®c, hc‚ü© := he.frequently.exists in intermediate_value_univ‚ÇÇ hf hg ha hc

lemma intermediate_value_univ‚ÇÇ_eventually‚ÇÇ [preconnected_space X] {l‚ÇÅ l‚ÇÇ : filter X}
  [ne_bot l‚ÇÅ] [ne_bot l‚ÇÇ] {f g : X ‚Üí Œ±} (hf : continuous f) (hg : continuous g)
  (he‚ÇÅ : f ‚â§·∂†[l‚ÇÅ] g ) (he‚ÇÇ : g ‚â§·∂†[l‚ÇÇ] f) :
  ‚àÉ x, f x = g x :=
let ‚ü®c‚ÇÅ, hc‚ÇÅ‚ü© := he‚ÇÅ.frequently.exists, ‚ü®c‚ÇÇ, hc‚ÇÇ‚ü© := he‚ÇÇ.frequently.exists in
intermediate_value_univ‚ÇÇ hf hg hc‚ÇÅ hc‚ÇÇ

/-- Intermediate value theorem for two functions: if `f` and `g` are two functions continuous
on a preconnected set `s` and for some `a b ‚àà s` we have `f a ‚â§ g a` and `g b ‚â§ f b`,
then for some `x ‚àà s` we have `f x = g x`. -/
lemma is_preconnected.intermediate_value‚ÇÇ {s : set X} (hs : is_preconnected s)
  {a b : X} (ha : a ‚àà s) (hb : b ‚àà s) {f g : X ‚Üí Œ±}
  (hf : continuous_on f s) (hg : continuous_on g s) (ha' : f a ‚â§ g a) (hb' : g b ‚â§ f b) :
  ‚àÉ x ‚àà s, f x = g x :=
let ‚ü®x, hx‚ü© := @intermediate_value_univ‚ÇÇ s Œ± _ _ _ _ (subtype.preconnected_space hs) ‚ü®a, ha‚ü© ‚ü®b, hb‚ü©
  _ _ (continuous_on_iff_continuous_restrict.1 hf) (continuous_on_iff_continuous_restrict.1 hg)
  ha' hb'
in ‚ü®x, x.2, hx‚ü©

lemma is_preconnected.intermediate_value‚ÇÇ_eventually‚ÇÅ {s : set X} (hs : is_preconnected s)
  {a : X} {l : filter X} (ha : a ‚àà s) [ne_bot l] (hl : l ‚â§ ùìü s) {f g : X ‚Üí Œ±}
  (hf : continuous_on f s) (hg : continuous_on g s) (ha' : f a ‚â§ g a) (he : g ‚â§·∂†[l] f) :
  ‚àÉ x ‚àà s, f x = g x :=
begin
  rw continuous_on_iff_continuous_restrict at hf hg,
  obtain ‚ü®b, h‚ü© := @intermediate_value_univ‚ÇÇ_eventually‚ÇÅ _ _ _ _ _ _ (subtype.preconnected_space hs)
    ‚ü®a, ha‚ü© _ (comap_coe_ne_bot_of_le_principal hl) _ _ hf hg ha' (he.comap _),
  exact ‚ü®b, b.prop, h‚ü©,
end

lemma is_preconnected.intermediate_value‚ÇÇ_eventually‚ÇÇ {s : set X} (hs : is_preconnected s)
  {l‚ÇÅ l‚ÇÇ : filter X} [ne_bot l‚ÇÅ] [ne_bot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f g : X ‚Üí Œ±}
  (hf : continuous_on f s) (hg : continuous_on g s) (he‚ÇÅ : f ‚â§·∂†[l‚ÇÅ] g) (he‚ÇÇ : g ‚â§·∂†[l‚ÇÇ] f) :
  ‚àÉ x ‚àà s, f x = g x :=
begin
  rw continuous_on_iff_continuous_restrict at hf hg,
  obtain ‚ü®b, h‚ü© := @intermediate_value_univ‚ÇÇ_eventually‚ÇÇ _ _ _ _ _ _ (subtype.preconnected_space hs)
    _ _ (comap_coe_ne_bot_of_le_principal hl‚ÇÅ) (comap_coe_ne_bot_of_le_principal hl‚ÇÇ)
    _ _ hf hg (he‚ÇÅ.comap _) (he‚ÇÇ.comap _),
  exact ‚ü®b, b.prop, h‚ü©,
end

/-- **Intermediate Value Theorem** for continuous functions on connected sets. -/
lemma is_preconnected.intermediate_value {s : set X} (hs : is_preconnected s)
  {a b : X} (ha : a ‚àà s) (hb : b ‚àà s) {f : X ‚Üí Œ±} (hf : continuous_on f s) :
  Icc (f a) (f b) ‚äÜ f '' s :=
Œª x hx, mem_image_iff_bex.2 $ hs.intermediate_value‚ÇÇ ha hb hf continuous_on_const hx.1 hx.2

lemma is_preconnected.intermediate_value_Ico {s : set X} (hs : is_preconnected s)
  {a : X} {l : filter X} (ha : a ‚àà s) [ne_bot l] (hl : l ‚â§ ùìü s) {f : X ‚Üí Œ±}
  (hf : continuous_on f s) {v : Œ±} (ht : tendsto f l (ùìù v)) :
  Ico (f a) v ‚äÜ f '' s :=
Œª y h, bex_def.1 $ hs.intermediate_value‚ÇÇ_eventually‚ÇÅ ha hl
  hf continuous_on_const h.1 (eventually_ge_of_tendsto_gt h.2 ht)

lemma is_preconnected.intermediate_value_Ioc {s : set X} (hs : is_preconnected s)
  {a : X} {l : filter X} (ha : a ‚àà s) [ne_bot l] (hl : l ‚â§ ùìü s) {f : X ‚Üí Œ±}
  (hf : continuous_on f s) {v : Œ±} (ht : tendsto f l (ùìù v)) :
  Ioc v (f a) ‚äÜ f '' s :=
Œª y h, bex_def.1 $ bex.imp_right (Œª x _, eq.symm) $ hs.intermediate_value‚ÇÇ_eventually‚ÇÅ ha hl
  continuous_on_const hf h.2 (eventually_le_of_tendsto_lt h.1 ht)

lemma is_preconnected.intermediate_value_Ioo {s : set X} (hs : is_preconnected s)
  {l‚ÇÅ l‚ÇÇ : filter X} [ne_bot l‚ÇÅ] [ne_bot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f : X ‚Üí Œ±}
  (hf : continuous_on f s) {v‚ÇÅ v‚ÇÇ : Œ±} (ht‚ÇÅ : tendsto f l‚ÇÅ (ùìù v‚ÇÅ)) (ht‚ÇÇ : tendsto f l‚ÇÇ (ùìù v‚ÇÇ)) :
  Ioo v‚ÇÅ v‚ÇÇ ‚äÜ f '' s :=
Œª y h, bex_def.1 $ hs.intermediate_value‚ÇÇ_eventually‚ÇÇ hl‚ÇÅ hl‚ÇÇ
  hf continuous_on_const (eventually_le_of_tendsto_lt h.1 ht‚ÇÅ) (eventually_ge_of_tendsto_gt h.2 ht‚ÇÇ)

lemma is_preconnected.intermediate_value_Ici {s : set X} (hs : is_preconnected s)
  {a : X} {l : filter X} (ha : a ‚àà s) [ne_bot l] (hl : l ‚â§ ùìü s) {f : X ‚Üí Œ±}
  (hf : continuous_on f s) (ht : tendsto f l at_top) :
  Ici (f a) ‚äÜ f '' s :=
Œª y h, bex_def.1 $ hs.intermediate_value‚ÇÇ_eventually‚ÇÅ ha hl
  hf continuous_on_const h (tendsto_at_top.1 ht y)

lemma is_preconnected.intermediate_value_Iic {s : set X} (hs : is_preconnected s)
  {a : X} {l : filter X} (ha : a ‚àà s) [ne_bot l] (hl : l ‚â§ ùìü s) {f : X ‚Üí Œ±}
  (hf : continuous_on f s) (ht : tendsto f l at_bot) :
  Iic (f a) ‚äÜ f '' s :=
Œª y h, bex_def.1 $ bex.imp_right (Œª x _, eq.symm) $ hs.intermediate_value‚ÇÇ_eventually‚ÇÅ ha hl
  continuous_on_const hf h (tendsto_at_bot.1 ht y)

lemma is_preconnected.intermediate_value_Ioi {s : set X} (hs : is_preconnected s)
  {l‚ÇÅ l‚ÇÇ : filter X} [ne_bot l‚ÇÅ] [ne_bot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f : X ‚Üí Œ±}
  (hf : continuous_on f s) {v : Œ±} (ht‚ÇÅ : tendsto f l‚ÇÅ (ùìù v)) (ht‚ÇÇ : tendsto f l‚ÇÇ at_top) :
  Ioi v ‚äÜ f '' s :=
Œª y h, bex_def.1 $ hs.intermediate_value‚ÇÇ_eventually‚ÇÇ hl‚ÇÅ hl‚ÇÇ
  hf continuous_on_const (eventually_le_of_tendsto_lt h ht‚ÇÅ) (tendsto_at_top.1 ht‚ÇÇ y)

lemma is_preconnected.intermediate_value_Iio {s : set X} (hs : is_preconnected s)
  {l‚ÇÅ l‚ÇÇ : filter X} [ne_bot l‚ÇÅ] [ne_bot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f : X ‚Üí Œ±}
  (hf : continuous_on f s) {v : Œ±} (ht‚ÇÅ : tendsto f l‚ÇÅ at_bot) (ht‚ÇÇ : tendsto f l‚ÇÇ (ùìù v)) :
  Iio v ‚äÜ f '' s :=
Œª y h, bex_def.1 $ hs.intermediate_value‚ÇÇ_eventually‚ÇÇ hl‚ÇÅ hl‚ÇÇ
  hf continuous_on_const (tendsto_at_bot.1 ht‚ÇÅ y) (eventually_ge_of_tendsto_gt h ht‚ÇÇ)

lemma is_preconnected.intermediate_value_Iii {s : set X} (hs : is_preconnected s)
  {l‚ÇÅ l‚ÇÇ : filter X} [ne_bot l‚ÇÅ] [ne_bot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f : X ‚Üí Œ±}
  (hf : continuous_on f s) (ht‚ÇÅ : tendsto f l‚ÇÅ at_bot) (ht‚ÇÇ : tendsto f l‚ÇÇ at_top) :
  univ ‚äÜ f '' s :=
Œª y h, bex_def.1 $ hs.intermediate_value‚ÇÇ_eventually‚ÇÇ hl‚ÇÅ hl‚ÇÇ
  hf continuous_on_const (tendsto_at_bot.1 ht‚ÇÅ y) (tendsto_at_top.1 ht‚ÇÇ y)

/-- **Intermediate Value Theorem** for continuous functions on connected spaces. -/
lemma intermediate_value_univ [preconnected_space X] (a b : X) {f : X ‚Üí Œ±} (hf : continuous f) :
  Icc (f a) (f b) ‚äÜ range f :=
Œª x hx, intermediate_value_univ‚ÇÇ hf continuous_const hx.1 hx.2

/-- **Intermediate Value Theorem** for continuous functions on connected spaces. -/
lemma mem_range_of_exists_le_of_exists_ge [preconnected_space X] {c : Œ±} {f : X ‚Üí Œ±}
  (hf : continuous f) (h‚ÇÅ : ‚àÉ a, f a ‚â§ c) (h‚ÇÇ : ‚àÉ b, c ‚â§ f b) :
  c ‚àà range f :=
let ‚ü®a, ha‚ü© := h‚ÇÅ, ‚ü®b, hb‚ü© := h‚ÇÇ in intermediate_value_univ a b hf ‚ü®ha, hb‚ü©

/-!
### (Pre)connected sets in a linear order

In this section we prove the following results:

* `is_preconnected.ord_connected`: any preconnected set `s` in a linear order is `ord_connected`,
  i.e. `a ‚àà s` and `b ‚àà s` imply `Icc a b ‚äÜ s`;

* `is_preconnected.mem_intervals`: any preconnected set `s` in a conditionally complete linear order
  is one of the intervals `set.Icc`, `set.`Ico`, `set.Ioc`, `set.Ioo`, ``set.Ici`, `set.Iic`,
  `set.Ioi`, `set.Iio`; note that this is false for non-complete orders: e.g., in `‚Ñù \ {0}`, the set
  of positive numbers cannot be represented as `set.Ioi _`.

-/

/-- If a preconnected set contains endpoints of an interval, then it includes the whole interval. -/
lemma is_preconnected.Icc_subset {s : set Œ±} (hs : is_preconnected s)
  {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) :
  Icc a b ‚äÜ s :=
by simpa only [image_id] using hs.intermediate_value ha hb continuous_on_id

lemma is_preconnected.ord_connected {s : set Œ±} (h : is_preconnected s) :
  ord_connected s :=
‚ü®Œª x hx y hy, h.Icc_subset hx hy‚ü©

/-- If a preconnected set contains endpoints of an interval, then it includes the whole interval. -/
lemma is_connected.Icc_subset {s : set Œ±} (hs : is_connected s)
  {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) :
  Icc a b ‚äÜ s :=
hs.2.Icc_subset ha hb

/-- If preconnected set in a linear order space is unbounded below and above, then it is the whole
space. -/
lemma is_preconnected.eq_univ_of_unbounded {s : set Œ±} (hs : is_preconnected s) (hb : ¬¨bdd_below s)
  (ha : ¬¨bdd_above s) :
  s = univ :=
begin
  refine eq_univ_of_forall (Œª x, _),
  obtain ‚ü®y, ys, hy‚ü© : ‚àÉ y ‚àà s, y < x := not_bdd_below_iff.1 hb x,
  obtain ‚ü®z, zs, hz‚ü© : ‚àÉ z ‚àà s, x < z := not_bdd_above_iff.1 ha x,
  exact hs.Icc_subset ys zs ‚ü®le_of_lt hy, le_of_lt hz‚ü©
end

end

variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}
  [conditionally_complete_linear_order Œ±] [topological_space Œ±] [order_topology Œ±]
  [conditionally_complete_linear_order Œ≤] [topological_space Œ≤] [order_topology Œ≤]
  [nonempty Œ≥]

/-- A bounded connected subset of a conditionally complete linear order includes the open interval
`(Inf s, Sup s)`. -/
lemma is_connected.Ioo_cInf_cSup_subset {s : set Œ±} (hs : is_connected s) (hb : bdd_below s)
  (ha : bdd_above s) :
  Ioo (Inf s) (Sup s) ‚äÜ s :=
Œª x hx, let ‚ü®y, ys, hy‚ü© := (is_glb_lt_iff (is_glb_cInf hs.nonempty hb)).1 hx.1 in
let ‚ü®z, zs, hz‚ü© := (lt_is_lub_iff (is_lub_cSup hs.nonempty ha)).1 hx.2 in
hs.Icc_subset ys zs ‚ü®le_of_lt hy, le_of_lt hz‚ü©

lemma eq_Icc_cInf_cSup_of_connected_bdd_closed {s : set Œ±} (hc : is_connected s) (hb : bdd_below s)
  (ha : bdd_above s) (hcl : is_closed s) :
  s = Icc (Inf s) (Sup s) :=
subset.antisymm (subset_Icc_cInf_cSup hb ha) $
  hc.Icc_subset (hcl.cInf_mem hc.nonempty hb) (hcl.cSup_mem hc.nonempty ha)

lemma is_preconnected.Ioi_cInf_subset {s : set Œ±} (hs : is_preconnected s) (hb : bdd_below s)
  (ha : ¬¨bdd_above s) :
  Ioi (Inf s) ‚äÜ s :=
begin
  have sne : s.nonempty := @nonempty_of_not_bdd_above Œ± _ s ‚ü®Inf ‚àÖ‚ü© ha,
  intros x hx,
  obtain ‚ü®y, ys, hy‚ü© : ‚àÉ y ‚àà s, y < x := (is_glb_lt_iff (is_glb_cInf sne hb)).1 hx,
  obtain ‚ü®z, zs, hz‚ü© : ‚àÉ z ‚àà s, x < z := not_bdd_above_iff.1 ha x,
  exact hs.Icc_subset ys zs ‚ü®le_of_lt hy, le_of_lt hz‚ü©
end

lemma is_preconnected.Iio_cSup_subset {s : set Œ±} (hs : is_preconnected s) (hb : ¬¨bdd_below s)
  (ha : bdd_above s) :
  Iio (Sup s) ‚äÜ s :=
@is_preconnected.Ioi_cInf_subset Œ±·µí·µà _ _ _ s hs ha hb

/-- A preconnected set in a conditionally complete linear order is either one of the intervals
`[Inf s, Sup s]`, `[Inf s, Sup s)`, `(Inf s, Sup s]`, `(Inf s, Sup s)`, `[Inf s, +‚àû)`,
`(Inf s, +‚àû)`, `(-‚àû, Sup s]`, `(-‚àû, Sup s)`, `(-‚àû, +‚àû)`, or `‚àÖ`. The converse statement requires
`Œ±` to be densely ordererd. -/
lemma is_preconnected.mem_intervals {s : set Œ±} (hs : is_preconnected s) :
  s ‚àà ({Icc (Inf s) (Sup s), Ico (Inf s) (Sup s), Ioc (Inf s) (Sup s), Ioo (Inf s) (Sup s),
    Ici (Inf s), Ioi (Inf s), Iic (Sup s), Iio (Sup s), univ, ‚àÖ} : set (set Œ±)) :=
begin
  rcases s.eq_empty_or_nonempty with rfl|hne,
  { apply_rules [or.inr, mem_singleton] },
  have hs' : is_connected s := ‚ü®hne, hs‚ü©,
  by_cases hb : bdd_below s; by_cases ha : bdd_above s,
  { rcases mem_Icc_Ico_Ioc_Ioo_of_subset_of_subset (hs'.Ioo_cInf_cSup_subset hb ha)
      (subset_Icc_cInf_cSup hb ha) with hs|hs|hs|hs,
    { exact (or.inl hs) },
    { exact (or.inr $ or.inl hs) },
    { exact (or.inr $ or.inr $ or.inl hs) },
    { exact (or.inr $ or.inr $ or.inr $ or.inl hs) } },
  { refine (or.inr $ or.inr $ or.inr $ or.inr _),
    cases mem_Ici_Ioi_of_subset_of_subset (hs.Ioi_cInf_subset hb ha) (Œª x hx, cInf_le hb hx)
      with hs hs,
    { exact or.inl hs },
    { exact or.inr (or.inl hs) } },
  { iterate 6 { apply or.inr },
    cases mem_Iic_Iio_of_subset_of_subset (hs.Iio_cSup_subset hb ha) (Œª x hx, le_cSup ha hx)
      with hs hs,
    { exact or.inl hs },
    { exact or.inr (or.inl hs) } },
  { iterate 8 { apply or.inr },
    exact or.inl (hs.eq_univ_of_unbounded hb ha) }
end

/-- A preconnected set is either one of the intervals `Icc`, `Ico`, `Ioc`, `Ioo`, `Ici`, `Ioi`,
`Iic`, `Iio`, or `univ`, or `‚àÖ`. The converse statement requires `Œ±` to be densely ordered. Though
one can represent `‚àÖ` as `(Inf s, Inf s)`, we include it into the list of possible cases to improve
readability. -/
lemma set_of_is_preconnected_subset_of_ordered :
  {s : set Œ± | is_preconnected s} ‚äÜ
    -- bounded intervals
    (range (uncurry Icc) ‚à™ range (uncurry Ico) ‚à™ range (uncurry Ioc) ‚à™ range (uncurry Ioo)) ‚à™
    -- unbounded intervals and `univ`
    (range Ici ‚à™ range Ioi ‚à™ range Iic ‚à™ range Iio ‚à™ {univ, ‚àÖ}) :=
begin
  intros s hs,
  rcases hs.mem_intervals with hs|hs|hs|hs|hs|hs|hs|hs|hs|hs,
  { exact (or.inl $ or.inl $ or.inl $ or.inl ‚ü®(Inf s, Sup s), hs.symm‚ü©) },
  { exact (or.inl $ or.inl $ or.inl $ or.inr ‚ü®(Inf s, Sup s), hs.symm‚ü©) },
  { exact (or.inl $ or.inl $ or.inr ‚ü®(Inf s, Sup s), hs.symm‚ü©) },
  { exact (or.inl $ or.inr ‚ü®(Inf s, Sup s), hs.symm‚ü©) },
  { exact (or.inr $ or.inl $ or.inl $ or.inl $ or.inl ‚ü®Inf s, hs.symm‚ü©) },
  { exact (or.inr $ or.inl $ or.inl $ or.inl $ or.inr ‚ü®Inf s, hs.symm‚ü©) },
  { exact (or.inr $ or.inl $ or.inl  $ or.inr ‚ü®Sup s, hs.symm‚ü©) },
  { exact (or.inr $ or.inl $  or.inr ‚ü®Sup s, hs.symm‚ü©) },
  { exact (or.inr $ or.inr $ or.inl hs) },
  { exact (or.inr $ or.inr $ or.inr hs) }
end

/-!
### Intervals are connected

In this section we prove that a closed interval (hence, any `ord_connected` set) in a dense
conditionally complete linear order is preconnected.
-/

/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and the set `s ‚à© [a, b)` has no maximal point, then `b ‚àà s`. -/
lemma is_closed.mem_of_ge_of_forall_exists_gt {a b : Œ±} {s : set Œ±} (hs : is_closed (s ‚à© Icc a b))
  (ha : a ‚àà s) (hab : a ‚â§ b) (hgt : ‚àÄ x ‚àà s ‚à© Ico a b, (s ‚à© Ioc x b).nonempty) :
  b ‚àà s :=
begin
  let S := s ‚à© Icc a b,
  replace ha : a ‚àà S, from ‚ü®ha, left_mem_Icc.2 hab‚ü©,
  have Sbd : bdd_above S, from ‚ü®b, Œª z hz, hz.2.2‚ü©,
  let c := Sup (s ‚à© Icc a b),
  have c_mem : c ‚àà S, from hs.cSup_mem ‚ü®_, ha‚ü© Sbd,
  have c_le : c ‚â§ b, from cSup_le ‚ü®_, ha‚ü© (Œª x hx, hx.2.2),
  cases eq_or_lt_of_le c_le with hc hc, from hc ‚ñ∏ c_mem.1,
  exfalso,
  rcases hgt c ‚ü®c_mem.1, c_mem.2.1, hc‚ü© with ‚ü®x, xs, cx, xb‚ü©,
  exact not_lt_of_le (le_cSup Sbd ‚ü®xs, le_trans (le_cSup Sbd ha) (le_of_lt cx), xb‚ü©) cx
end

/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and for any `a ‚â§ x < y ‚â§ b`, `x ‚àà s`, the set `s ‚à© (x, y]`
is not empty, then `[a, b] ‚äÜ s`. -/
lemma is_closed.Icc_subset_of_forall_exists_gt {a b : Œ±} {s : set Œ±} (hs : is_closed (s ‚à© Icc a b))
  (ha : a ‚àà s) (hgt : ‚àÄ x ‚àà s ‚à© Ico a b, ‚àÄ y ‚àà Ioi x, (s ‚à© Ioc x y).nonempty) :
  Icc a b ‚äÜ s :=
begin
  assume y hy,
  have : is_closed (s ‚à© Icc a y),
  { suffices : s ‚à© Icc a y = s ‚à© Icc a b ‚à© Icc a y,
    { rw this, exact is_closed.inter hs is_closed_Icc },
    rw [inter_assoc],
    congr,
    exact (inter_eq_self_of_subset_right $ Icc_subset_Icc_right hy.2).symm },
  exact is_closed.mem_of_ge_of_forall_exists_gt this ha hy.1
    (Œª x hx, hgt x ‚ü®hx.1, Ico_subset_Ico_right hy.2 hx.2‚ü© y hx.2.2)
end

variables [densely_ordered Œ±] {a b : Œ±}

/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and for any `x ‚àà s ‚à© [a, b)` the set `s` includes some open
neighborhood of `x` within `(x, +‚àû)`, then `[a, b] ‚äÜ s`. -/
lemma is_closed.Icc_subset_of_forall_mem_nhds_within {a b : Œ±} {s : set Œ±}
  (hs : is_closed (s ‚à© Icc a b)) (ha : a ‚àà s)
  (hgt : ‚àÄ x ‚àà s ‚à© Ico a b, s ‚àà ùìù[>] x) :
  Icc a b ‚äÜ s :=
begin
  apply hs.Icc_subset_of_forall_exists_gt ha,
  rintros x ‚ü®hxs, hxab‚ü© y hyxb,
  have : s ‚à© Ioc x y ‚àà ùìù[>] x,
    from inter_mem (hgt x ‚ü®hxs, hxab‚ü©) (Ioc_mem_nhds_within_Ioi ‚ü®le_rfl, hyxb‚ü©),
  exact (nhds_within_Ioi_self_ne_bot' ‚ü®b, hxab.2‚ü©).nonempty_of_mem this
end

lemma is_preconnected_Icc_aux (x y : Œ±) (s t : set Œ±) (hxy : x ‚â§ y)
  (hs : is_closed s) (ht : is_closed t) (hab : Icc a b ‚äÜ s ‚à™ t)
  (hx : x ‚àà Icc a b ‚à© s) (hy : y ‚àà Icc a b ‚à© t) : (Icc a b ‚à© (s ‚à© t)).nonempty :=
begin
  have xyab : Icc x y ‚äÜ Icc a b := Icc_subset_Icc hx.1.1 hy.1.2,
  by_contradiction hst,
  suffices : Icc x y ‚äÜ s,
    from hst ‚ü®y, xyab $ right_mem_Icc.2 hxy, this $ right_mem_Icc.2 hxy, hy.2‚ü©,
  apply (is_closed.inter hs is_closed_Icc).Icc_subset_of_forall_mem_nhds_within hx.2,
  rintros z ‚ü®zs, hz‚ü©,
  have zt : z ‚àà t·∂ú, from Œª zt, hst ‚ü®z, xyab $ Ico_subset_Icc_self hz, zs, zt‚ü©,
  have : t·∂ú ‚à© Ioc z y ‚àà ùìù[>] z,
  { rw [‚Üê nhds_within_Ioc_eq_nhds_within_Ioi hz.2],
    exact mem_nhds_within.2 ‚ü®t·∂ú, ht.is_open_compl, zt, subset.refl _‚ü©},
  apply mem_of_superset this,
  have : Ioc z y ‚äÜ s ‚à™ t, from Œª w hw, hab (xyab ‚ü®le_trans hz.1 (le_of_lt hw.1), hw.2‚ü©),
  exact Œª w ‚ü®wt, wzy‚ü©, (this wzy).elim id (Œª h, (wt h).elim)
end

/-- A closed interval in a densely ordered conditionally complete linear order is preconnected. -/
lemma is_preconnected_Icc : is_preconnected (Icc a b) :=
is_preconnected_closed_iff.2
begin
  rintros s t hs ht hab ‚ü®x, hx‚ü© ‚ü®y, hy‚ü©,
  -- This used to use `wlog`, but it was causing timeouts.
  cases le_total x y,
  { exact is_preconnected_Icc_aux x y s t h hs ht hab hx hy, },
  { rw inter_comm s t,
    rw union_comm s t at hab,
    exact is_preconnected_Icc_aux y x t s h ht hs hab hy hx, },
end

lemma is_preconnected_uIcc : is_preconnected (uIcc a b) := is_preconnected_Icc

lemma set.ord_connected.is_preconnected {s : set Œ±} (h : s.ord_connected) :
  is_preconnected s :=
is_preconnected_of_forall_pair $ Œª x hx y hy, ‚ü®uIcc x y, h.uIcc_subset hx hy,
  left_mem_uIcc, right_mem_uIcc, is_preconnected_uIcc‚ü©

lemma is_preconnected_iff_ord_connected {s : set Œ±} :
  is_preconnected s ‚Üî ord_connected s :=
‚ü®is_preconnected.ord_connected, set.ord_connected.is_preconnected‚ü©

lemma is_preconnected_Ici : is_preconnected (Ici a) := ord_connected_Ici.is_preconnected
lemma is_preconnected_Iic : is_preconnected (Iic a) := ord_connected_Iic.is_preconnected
lemma is_preconnected_Iio : is_preconnected (Iio a) := ord_connected_Iio.is_preconnected
lemma is_preconnected_Ioi : is_preconnected (Ioi a) := ord_connected_Ioi.is_preconnected
lemma is_preconnected_Ioo : is_preconnected (Ioo a b) := ord_connected_Ioo.is_preconnected
lemma is_preconnected_Ioc : is_preconnected (Ioc a b) := ord_connected_Ioc.is_preconnected
lemma is_preconnected_Ico : is_preconnected (Ico a b) := ord_connected_Ico.is_preconnected

lemma is_connected_Ici : is_connected (Ici a) := ‚ü®nonempty_Ici, is_preconnected_Ici‚ü©

lemma is_connected_Iic : is_connected (Iic a) := ‚ü®nonempty_Iic, is_preconnected_Iic‚ü©

lemma is_connected_Ioi [no_max_order Œ±] : is_connected (Ioi a) :=
‚ü®nonempty_Ioi, is_preconnected_Ioi‚ü©

lemma is_connected_Iio [no_min_order Œ±] : is_connected (Iio a) :=
‚ü®nonempty_Iio, is_preconnected_Iio‚ü©

lemma is_connected_Icc (h : a ‚â§ b) : is_connected (Icc a b) :=
‚ü®nonempty_Icc.2 h, is_preconnected_Icc‚ü©

lemma is_connected_Ioo (h : a < b) : is_connected (Ioo a b) :=
‚ü®nonempty_Ioo.2 h, is_preconnected_Ioo‚ü©

lemma is_connected_Ioc (h : a < b) : is_connected (Ioc a b) :=
‚ü®nonempty_Ioc.2 h, is_preconnected_Ioc‚ü©

lemma is_connected_Ico (h : a < b) : is_connected (Ico a b) :=
‚ü®nonempty_Ico.2 h, is_preconnected_Ico‚ü©

@[priority 100]
instance ordered_connected_space : preconnected_space Œ± :=
‚ü®ord_connected_univ.is_preconnected‚ü©

/-- In a dense conditionally complete linear order, the set of preconnected sets is exactly
the set of the intervals `Icc`, `Ico`, `Ioc`, `Ioo`, `Ici`, `Ioi`, `Iic`, `Iio`, `(-‚àû, +‚àû)`,
or `‚àÖ`. Though one can represent `‚àÖ` as `(Inf s, Inf s)`, we include it into the list of
possible cases to improve readability. -/
lemma set_of_is_preconnected_eq_of_ordered :
  {s : set Œ± | is_preconnected s} =
    -- bounded intervals
    (range (uncurry Icc) ‚à™ range (uncurry Ico) ‚à™ range (uncurry Ioc) ‚à™ range (uncurry Ioo)) ‚à™
    -- unbounded intervals and `univ`
    (range Ici ‚à™ range Ioi ‚à™ range Iic ‚à™ range Iio ‚à™ {univ, ‚àÖ}) :=
begin
  refine subset.antisymm set_of_is_preconnected_subset_of_ordered _,
  simp only [subset_def, -mem_range, forall_range_iff, uncurry, or_imp_distrib, forall_and_distrib,
    mem_union, mem_set_of_eq, insert_eq, mem_singleton_iff, forall_eq, forall_true_iff, and_true,
    is_preconnected_Icc, is_preconnected_Ico, is_preconnected_Ioc,
    is_preconnected_Ioo, is_preconnected_Ioi, is_preconnected_Iio, is_preconnected_Ici,
    is_preconnected_Iic, is_preconnected_univ, is_preconnected_empty],
end

/-!
### Intermediate Value Theorem on an interval

In this section we prove several versions of the Intermediate Value Theorem for a function
continuous on an interval.
-/

variables {Œ¥ : Type*} [linear_order Œ¥] [topological_space Œ¥] [order_closed_topology Œ¥]

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, case
`f a ‚â§ t ‚â§ f b`.-/
lemma intermediate_value_Icc {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (Icc a b)) :
  Icc (f a) (f b) ‚äÜ f '' (Icc a b) :=
is_preconnected_Icc.intermediate_value (left_mem_Icc.2 hab) (right_mem_Icc.2 hab) hf

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, case
`f a ‚â• t ‚â• f b`.-/
lemma intermediate_value_Icc' {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (Icc a b)) :
  Icc (f b) (f a) ‚äÜ f '' (Icc a b) :=
is_preconnected_Icc.intermediate_value (right_mem_Icc.2 hab) (left_mem_Icc.2 hab) hf

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, unordered case. -/
lemma intermediate_value_uIcc {a b : Œ±} {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (uIcc a b)) :
  uIcc (f a) (f b) ‚äÜ f '' uIcc a b :=
by cases le_total (f a) (f b); simp [*, is_preconnected_uIcc.intermediate_value]

lemma intermediate_value_Ico {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (Icc a b)) :
  Ico (f a) (f b) ‚äÜ f '' (Ico a b) :=
or.elim (eq_or_lt_of_le hab) (Œª he y h, absurd h.2 (not_lt_of_le (he ‚ñ∏ h.1)))
(Œª hlt, @is_preconnected.intermediate_value_Ico _ _ _ _ _ _ _ (is_preconnected_Ico)
  _ _ ‚ü®refl a, hlt‚ü© (right_nhds_within_Ico_ne_bot hlt) inf_le_right _ (hf.mono Ico_subset_Icc_self)
  _ ((hf.continuous_within_at ‚ü®hab, refl b‚ü©).mono Ico_subset_Icc_self))

lemma intermediate_value_Ico' {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (Icc a b)) :
  Ioc (f b) (f a) ‚äÜ f '' (Ico a b) :=
or.elim (eq_or_lt_of_le hab) (Œª he y h, absurd h.1 (not_lt_of_le (he ‚ñ∏ h.2)))
(Œª hlt, @is_preconnected.intermediate_value_Ioc _ _ _ _ _ _ _ (is_preconnected_Ico)
  _ _ ‚ü®refl a, hlt‚ü© (right_nhds_within_Ico_ne_bot hlt) inf_le_right _ (hf.mono Ico_subset_Icc_self)
  _ ((hf.continuous_within_at ‚ü®hab, refl b‚ü©).mono Ico_subset_Icc_self))

lemma intermediate_value_Ioc {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (Icc a b)) :
  Ioc (f a) (f b) ‚äÜ f '' (Ioc a b) :=
or.elim (eq_or_lt_of_le hab) (Œª he y h, absurd h.2 (not_le_of_lt (he ‚ñ∏ h.1)))
(Œª hlt, @is_preconnected.intermediate_value_Ioc _ _ _ _ _ _ _ (is_preconnected_Ioc)
  _ _ ‚ü®hlt, refl b‚ü© (left_nhds_within_Ioc_ne_bot hlt) inf_le_right _ (hf.mono Ioc_subset_Icc_self)
  _ ((hf.continuous_within_at ‚ü®refl a, hab‚ü©).mono Ioc_subset_Icc_self))

lemma intermediate_value_Ioc' {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (Icc a b)) :
  Ico (f b) (f a) ‚äÜ f '' (Ioc a b) :=
or.elim (eq_or_lt_of_le hab) (Œª he y h, absurd h.1 (not_le_of_lt (he ‚ñ∏ h.2)))
(Œª hlt, @is_preconnected.intermediate_value_Ico _ _ _ _ _ _ _ (is_preconnected_Ioc)
  _ _ ‚ü®hlt, refl b‚ü© (left_nhds_within_Ioc_ne_bot hlt) inf_le_right _ (hf.mono Ioc_subset_Icc_self)
  _ ((hf.continuous_within_at ‚ü®refl a, hab‚ü©).mono Ioc_subset_Icc_self))

lemma intermediate_value_Ioo {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (Icc a b)) :
  Ioo (f a) (f b) ‚äÜ f '' (Ioo a b) :=
or.elim (eq_or_lt_of_le hab) (Œª he y h, absurd h.2 (not_lt_of_lt (he ‚ñ∏ h.1)))
(Œª hlt, @is_preconnected.intermediate_value_Ioo _ _ _ _ _ _ _ (is_preconnected_Ioo)
  _ _ (left_nhds_within_Ioo_ne_bot hlt) (right_nhds_within_Ioo_ne_bot hlt)
  inf_le_right inf_le_right _ (hf.mono Ioo_subset_Icc_self)
  _ _ ((hf.continuous_within_at ‚ü®refl a, hab‚ü©).mono Ioo_subset_Icc_self)
  ((hf.continuous_within_at ‚ü®hab, refl b‚ü©).mono Ioo_subset_Icc_self))

lemma intermediate_value_Ioo' {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : continuous_on f (Icc a b)) :
  Ioo (f b) (f a) ‚äÜ f '' (Ioo a b) :=
or.elim (eq_or_lt_of_le hab) (Œª he y h, absurd h.1 (not_lt_of_lt (he ‚ñ∏ h.2)))
(Œª hlt, @is_preconnected.intermediate_value_Ioo _ _ _ _ _ _ _ (is_preconnected_Ioo)
  _ _ (right_nhds_within_Ioo_ne_bot hlt) (left_nhds_within_Ioo_ne_bot hlt)
  inf_le_right inf_le_right _ (hf.mono Ioo_subset_Icc_self)
  _ _ ((hf.continuous_within_at ‚ü®hab, refl b‚ü©).mono Ioo_subset_Icc_self)
  ((hf.continuous_within_at ‚ü®refl a, hab‚ü©).mono Ioo_subset_Icc_self))

/-- **Intermediate value theorem**: if `f` is continuous on an order-connected set `s` and `a`,
`b` are two points of this set, then `f` sends `s` to a superset of `Icc (f x) (f y)`. -/
lemma continuous_on.surj_on_Icc {s : set Œ±} [hs : ord_connected s] {f : Œ± ‚Üí Œ¥}
  (hf : continuous_on f s) {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) :
  surj_on f s (Icc (f a) (f b)) :=
hs.is_preconnected.intermediate_value ha hb hf

/-- **Intermediate value theorem**: if `f` is continuous on an order-connected set `s` and `a`,
`b` are two points of this set, then `f` sends `s` to a superset of `[f x, f y]`. -/
lemma continuous_on.surj_on_uIcc {s : set Œ±} [hs : ord_connected s] {f : Œ± ‚Üí Œ¥}
  (hf : continuous_on f s) {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) :
  surj_on f s (uIcc (f a) (f b)) :=
by cases le_total (f a) (f b) with hab hab; simp [hf.surj_on_Icc, *]

/-- A continuous function which tendsto `at_top` `at_top` and to `at_bot` `at_bot` is surjective. -/
lemma continuous.surjective {f : Œ± ‚Üí Œ¥} (hf : continuous f) (h_top : tendsto f at_top at_top)
  (h_bot : tendsto f at_bot at_bot) :
  function.surjective f :=
Œª p, mem_range_of_exists_le_of_exists_ge hf
  (h_bot.eventually (eventually_le_at_bot p)).exists
  (h_top.eventually (eventually_ge_at_top p)).exists

/-- A continuous function which tendsto `at_bot` `at_top` and to `at_top` `at_bot` is surjective. -/
lemma continuous.surjective' {f : Œ± ‚Üí Œ¥} (hf : continuous f) (h_top : tendsto f at_bot at_top)
  (h_bot : tendsto f at_top at_bot) :
  function.surjective f :=
@continuous.surjective Œ±·µí·µà _ _ _ _ _ _ _ _ _ hf h_top h_bot

/-- If a function `f : Œ± ‚Üí Œ≤` is continuous on a nonempty interval `s`, its restriction to `s`
tends to `at_bot : filter Œ≤` along `at_bot : filter ‚Ü•s` and tends to `at_top : filter Œ≤` along
`at_top : filter ‚Ü•s`, then the restriction of `f` to `s` is surjective. We formulate the
conclusion as `surj_on f s univ`. -/
lemma continuous_on.surj_on_of_tendsto {f : Œ± ‚Üí Œ¥} {s : set Œ±} [ord_connected s]
  (hs : s.nonempty) (hf : continuous_on f s) (hbot : tendsto (Œª x : s, f x) at_bot at_bot)
  (htop : tendsto (Œª x : s, f x) at_top at_top) :
  surj_on f s univ :=
by haveI := classical.inhabited_of_nonempty hs.to_subtype;
  exact (surj_on_iff_surjective.2 $
    (continuous_on_iff_continuous_restrict.1 hf).surjective htop hbot)

/-- If a function `f : Œ± ‚Üí Œ≤` is continuous on a nonempty interval `s`, its restriction to `s`
tends to `at_top : filter Œ≤` along `at_bot : filter ‚Ü•s` and tends to `at_bot : filter Œ≤` along
`at_top : filter ‚Ü•s`, then the restriction of `f` to `s` is surjective. We formulate the
conclusion as `surj_on f s univ`. -/
lemma continuous_on.surj_on_of_tendsto' {f : Œ± ‚Üí Œ¥} {s : set Œ±} [ord_connected s]
  (hs : s.nonempty) (hf : continuous_on f s) (hbot : tendsto (Œª x : s, f x) at_bot at_top)
  (htop : tendsto (Œª x : s, f x) at_top at_bot) :
  surj_on f s univ :=
@continuous_on.surj_on_of_tendsto Œ± _ _ _ _ Œ¥·µí·µà _ _ _ _ _ _ hs hf hbot htop
