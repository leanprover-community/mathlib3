/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Anatole Dedecker
-/
import topology.algebra.order.extend_from
import topology.algebra.order.compact
import topology.algebra.order.t5
import topology.local_extr

/-!
# Rolle's Theorem (topological part)

In this file we prove the purely topological part of Rolle's Theorem: namely, that a function that
is continuous on an interval $[a, b]$, $a<b$, has a local extremum at a point $x âˆˆ (a, b)$ provided
that $f(a)=f(b)$. We also prove several variations of this statement.

In `analysis.calculus.local_extr.rolle` we use these lemmas to prove several versions of Rolle's
Theorem from calculus.

## Keywords

local minimum, local maximum, extremum, Rolle's Theorem
-/

open filter set
open_locale topology

variables {Î± Î² : Type*}
  [conditionally_complete_linear_order Î±] [densely_ordered Î±]
  [topological_space Î±] [order_topology Î±]
  [conditionally_complete_linear_order Î²] [topological_space Î²] [order_topology Î²]
  {f : Î± â†’ Î²} {a b : Î±} {l : Î²}

/-- A continuous function on a closed interval with `f a = f b` takes either its maximum or its
minimum value at a point in the interior of the interval. -/
lemma exists_Ioo_extr_on_Icc (hab : a < b) (hfc : continuous_on f (Icc a b)) (hfI : f a = f b) :
  âˆƒ c âˆˆ Ioo a b, is_extr_on f (Icc a b) c :=
begin
  have ne : (Icc a b).nonempty, from nonempty_Icc.2 (le_of_lt hab),
  -- Consider absolute min and max points
  obtain âŸ¨c, cmem, cleâŸ© : âˆƒ c âˆˆ Icc a b, âˆ€ x âˆˆ Icc a b, f c â‰¤ f x,
    from is_compact_Icc.exists_forall_le ne hfc,
  obtain âŸ¨C, Cmem, CgeâŸ© : âˆƒ C âˆˆ Icc a b, âˆ€ x âˆˆ Icc a b, f x â‰¤ f C,
    from is_compact_Icc.exists_forall_ge ne hfc,
  by_cases hc : f c = f a,
  { by_cases hC : f C = f a,
    { have : âˆ€ x âˆˆ Icc a b, f x = f a,
        from Î» x hx, le_antisymm (hC â–¸ Cge x hx) (hc â–¸ cle x hx),
      -- `f` is a constant, so we can take any point in `Ioo a b`
      rcases exists_between hab with âŸ¨c', hc'âŸ©,
      refine âŸ¨c', hc', or.inl _âŸ©,
      assume x hx,
      rw [mem_set_of_eq, this x hx, â† hC],
      exact Cge c' âŸ¨le_of_lt hc'.1, le_of_lt hc'.2âŸ© },
    { refine âŸ¨C, âŸ¨lt_of_le_of_ne Cmem.1 $ mt _ hC, lt_of_le_of_ne Cmem.2 $ mt _ hCâŸ©, or.inr CgeâŸ©,
      exacts [Î» h, by rw h, Î» h, by rw [h, hfI]] } },
  { refine âŸ¨c, âŸ¨lt_of_le_of_ne cmem.1 $ mt _ hc, lt_of_le_of_ne cmem.2 $ mt _ hcâŸ©, or.inl cleâŸ©,
      exacts [Î» h, by rw h, Î» h, by rw [h, hfI]] }
end

/-- A continuous function on a closed interval with `f a = f b` has a local extremum at some
point of the corresponding open interval. -/
lemma exists_local_extr_Ioo (hab : a < b) (hfc : continuous_on f (Icc a b)) (hfI : f a = f b) :
  âˆƒ c âˆˆ Ioo a b, is_local_extr f c :=
let âŸ¨c, cmem, hcâŸ© := exists_Ioo_extr_on_Icc hab hfc hfI
in âŸ¨c, cmem, hc.is_local_extr $ Icc_mem_nhds cmem.1 cmem.2âŸ©

/-- If a function `f` is continuous on an open interval and tends to the same value at its
endpoints, then it has an extremum on this open interval. -/
lemma exists_extr_on_Ioo_of_tendsto (hab : a < b) (hfc : continuous_on f (Ioo a b))
  (ha : tendsto f (ğ“[>] a) (ğ“ l)) (hb : tendsto f (ğ“[<] b) (ğ“ l)) :
  âˆƒ c âˆˆ Ioo a b, is_extr_on f (Ioo a b) c :=
begin
  have h : eq_on (extend_from (Ioo a b) f) f (Ioo a b) := extend_from_extends hfc,
  obtain âŸ¨c, hc, hfcâŸ© : âˆƒ c âˆˆ Ioo a b, is_extr_on (extend_from (Ioo a b) f) (Icc a b) c :=
    exists_Ioo_extr_on_Icc hab (continuous_on_Icc_extend_from_Ioo hab.ne hfc ha hb)
      ((eq_lim_at_left_extend_from_Ioo hab ha).trans (eq_lim_at_right_extend_from_Ioo hab hb).symm),
  exact âŸ¨c, hc, (hfc.on_subset Ioo_subset_Icc_self).congr h (h hc)âŸ©
end

/-- If a function `f` is continuous on an open interval and tends to the same value at its
endpoints, then it has a local extremum on this open interval. -/
lemma exists_is_local_extr_Ioo_of_tendsto (hab : a < b) (hfc : continuous_on f (Ioo a b))
  (ha : tendsto f (ğ“[>] a) (ğ“ l)) (hb : tendsto f (ğ“[<] b) (ğ“ l)) :
  âˆƒ c âˆˆ Ioo a b, is_local_extr f c :=
let âŸ¨c, cmem, hcâŸ© := exists_extr_on_Ioo_of_tendsto hab hfc ha hb
in âŸ¨c, cmem, hc.is_local_extr $ Ioo_mem_nhds cmem.1 cmem.2âŸ©
