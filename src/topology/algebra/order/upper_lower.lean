/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import algebra.order.upper_lower
import analysis.normed.field.basic

/-!
# Topological facts about upper/lower/order-connected sets

The topological closure and interior of an upper/lower/order-connected set is an
upper/lower/order-connected set (with the notable exception of the closure of an order-connected
set).

We also prove lemmas specific to `‚Ñù‚Åø`. Those are helpful to prove that order-connected sets in `‚Ñù‚Åø`
are measurable.
-/
-- move `real.norm_of_nonneg`

section
variables {Œ± : Type*} [preorder Œ±] {s : set Œ±} {a b : Œ±}

lemma is_antichain.not_lt (hs : is_antichain (‚â§) s) (ha : a ‚àà s) (hb : b ‚àà s) : ¬¨ a < b :=
Œª h, hs ha hb h.ne h.le

end

section
variables {Œ± : Type*} [partial_order Œ±] {s : set Œ±}

lemma is_antichain_iff_forall_not_lt : is_antichain (‚â§) s ‚Üî ‚àÄ ‚¶Éa‚¶Ñ, a ‚àà s ‚Üí ‚àÄ ‚¶Éb‚¶Ñ, b ‚àà s ‚Üí ¬¨ a < b :=
‚ü®Œª hs a ha b, hs.not_lt ha, Œª hs a ha b hb h h', hs ha hb $ h'.lt_of_ne h‚ü©

end

section left_cancel_monoid
variables {M : Type*} [left_cancel_monoid M] {a b : M}

@[simp, to_additive] lemma mul_right_ne_self : a * b ‚â† a ‚Üî b ‚â† 1 := mul_right_eq_self.not
@[simp, to_additive] lemma self_ne_mul_right : a ‚â† a * b ‚Üî b ‚â† 1 := self_eq_mul_right.not

end left_cancel_monoid

section right_cancel_monoid
variables {M : Type*} [right_cancel_monoid M] {a b : M}

@[simp, to_additive] lemma mul_left_ne_self : a * b ‚â† b ‚Üî a ‚â† 1 := mul_left_eq_self.not
@[simp, to_additive] lemma self_ne_mul_left : b ‚â† a * b ‚Üî a ‚â† 1 := self_eq_mul_left.not

end right_cancel_monoid

attribute [to_additive order_dual.has_vadd] order_dual.has_nsmul

section
variables {Œ± : Type*} [topological_space Œ±] [linear_order Œ±]

open filter set topological_space
open_locale topological_space

lemma exists_seq_strict_anti_tendsto_nhds_within [order_topology Œ±] [densely_ordered Œ±]
  [no_max_order Œ±] [first_countable_topology Œ±] (x : Œ±) :
  ‚àÉ u : ‚Ñï ‚Üí Œ±, strict_anti u ‚àß (‚àÄ n, x < u n) ‚àß tendsto u at_top (ùìù[>] x) :=
let ‚ü®u, hu, hx, h‚ü© := exists_seq_strict_anti_tendsto x in ‚ü®u, hu, hx,
  tendsto_nhds_within_mono_right (range_subset_iff.2 hx) $ tendsto_nhds_within_range.2 h‚ü©

end

section
variables {Œ± : Type*} [linear_ordered_semifield Œ±] {a : Œ±}

@[simp] lemma half_lt_self_iff : a / 2 < a ‚Üî 0 < a :=
by rw [div_lt_iff (zero_lt_two' Œ±), mul_two, lt_add_iff_pos_left]

end

namespace function
variables {Œ± Œ≤ : Type*} [nonempty Œ≤] {a : Œ±}

@[simp, to_additive] lemma const_eq_one [has_one Œ±] : const Œ≤ a = 1 ‚Üî a = 1 :=
by simp [funext_iff]

@[simp, to_additive] lemma const_ne_one [has_one Œ±] : const Œ≤ a ‚â† 1 ‚Üî a ‚â† 1 := const_eq_one.not

variables [has_zero Œ±] [preorder Œ±]

lemma const_nonneg_of_nonneg (Œ≤ : Type*) (ha : 0 ‚â§ a) : 0 ‚â§ const Œ≤ a := Œª _, ha

@[simp] lemma const_nonneg : 0 ‚â§ const Œ≤ a ‚Üî 0 ‚â§ a := by simp [pi.le_def]
@[simp] lemma const_pos : 0 < const Œ≤ a ‚Üî 0 < a := by simpa [pi.lt_def] using le_of_lt

end function

section
variables {Œ± : Type*} [partial_order Œ±] {s : set Œ±}

open set

lemma is_antichain.ord_connected (hs : is_antichain (‚â§) s) : s.ord_connected :=
‚ü®Œª x hx y hy z hz, by { obtain rfl := hs.eq hx hy (hz.1.trans hz.2),
  rw [Icc_self, mem_singleton_iff] at hz, rwa hz }‚ü©

end

namespace tactic
open positivity

private lemma ennreal_of_real_pos {r : ‚Ñù} : 0 < r ‚Üí 0 < ennreal.of_real r := ennreal.of_real_pos.2

/-- Extension for the `positivity` tactic: `ennreal.of_real` is positive if its input is. -/
@[positivity]
meta def positivity_ennreal_of_real : expr ‚Üí tactic strictness
| `(ennreal.of_real %%r) := do
    positive p ‚Üê core r,
    positive <$> mk_app ``ennreal_of_real_pos [p]
| e := pp e >>= fail ‚àò format.bracket "The expression `" "` is not of the form `ennreal.of_real r`"

open function

variables (Œπ : Type*) {Œ± : Type*}  [has_zero Œ±] {a : Œ±}
private lemma function_const_nonneg_of_pos [preorder Œ±] (ha : 0 < a) : 0 ‚â§ const Œπ a :=
const_nonneg_of_nonneg _ ha.le

variables [nonempty Œπ]

private lemma function_const_ne_zero : a ‚â† 0 ‚Üí const Œπ a ‚â† 0 := const_ne_zero.2
private lemma function_const_pos [preorder Œ±] : 0 < a ‚Üí 0 < const Œπ a := const_pos.2

/-- Extension for the `positivity` tactic: `function.const` is positive/nonnegative/nonzero if its
input is. -/
@[positivity]
meta def positivity_const : expr ‚Üí tactic strictness
| `(function.const %%Œπ %%a) := do
    strict_a ‚Üê core a,
    match strict_a with
    | positive p := positive <$> to_expr ``(function_const_pos %%Œπ %%p)
        <|> nonnegative <$> to_expr ``(function_const_nonneg_of_pos %%Œπ %%p)
    | nonnegative p := nonnegative <$> to_expr ``(const_nonneg_of_nonneg %%Œπ %%p)
    | nonzero p := nonzero <$> to_expr ``(function_const_ne_zero %%Œπ %%p)
    end
| e := pp e >>= fail ‚àò format.bracket "The expression `" "` is not of the form `function.const Œπ a`"

end tactic

section
open function
variables {Œπ Œ± : Type*}

example [nonempty Œπ] [has_zero Œ±] {a : Œ±} (ha : a ‚â† 0) : const Œπ a ‚â† 0 := by positivity
example [has_zero Œ±] [preorder Œ±] {a : Œ±} (ha : 0 < a) : 0 ‚â§ const Œπ a := by positivity
example [has_zero Œ±] [preorder Œ±] {a : Œ±} (ha : 0 ‚â§ a) : 0 ‚â§ const Œπ a := by positivity
example [nonempty Œπ] [has_zero Œ±] [preorder Œ±] {a : Œ±} (ha : 0 < a) : 0 < const Œπ a := by positivity

end

section
variables {Œπ E : Type*} [fintype Œπ] [seminormed_group E] [nonempty Œπ]

open function

@[simp, to_additive pi_norm_const''] lemma pi_norm_const''' (a : E) : ‚à•const Œπ a‚à• = ‚à•a‚à• :=
pi_norm_const' a

@[simp, to_additive pi_nnnorm_const''] lemma pi_nnnorm_const''' (a : E) : ‚à•const Œπ a‚à•‚Çä = ‚à•a‚à•‚Çä :=
pi_nnnorm_const' a

end

namespace ennreal
open_locale ennreal

variables {a b c : ‚Ñù‚â•0‚àû}

protected lemma div_le_div_left (h : a ‚â§ b) (c : ‚Ñù‚â•0‚àû) : c / b ‚â§ c / a :=
ennreal.div_le_div le_rfl h

protected lemma div_le_div_right (h : a ‚â§ b) (c : ‚Ñù‚â•0‚àû) : a / c ‚â§ b / c :=
ennreal.div_le_div h le_rfl

protected lemma sub_div (h : 0 < b ‚Üí b < a ‚Üí c ‚â† 0) : (a - b) / c = a / c - b / c :=
by { simp_rw div_eq_mul_inv, exact ennreal.sub_mul (by simpa using h) }

end ennreal

section
variables {Œπ Œ± : Type*} [fintype Œπ] [pseudo_emetric_space Œ±]

lemma edist_pi_const_le (a b : Œ±) : edist (Œª _ : Œπ, a) (Œª _, b) ‚â§ edist a b :=
edist_pi_le_iff.2 $ Œª _, le_rfl

end

section
variables {Œπ Œ± : Type*} [fintype Œπ] [pseudo_metric_space Œ±]

lemma dist_pi_const_le (a b : Œ±) : dist (Œª _ : Œπ, a) (Œª _, b) ‚â§ dist a b :=
(dist_pi_le_iff dist_nonneg).2 $ Œª _, le_rfl

lemma nndist_pi_const_le (a b : Œ±) : nndist (Œª _ : Œπ, a) (Œª _, b) ‚â§ nndist a b :=
nndist_pi_le_iff.2 $ Œª _, le_rfl

end

section
variables {Œ≤ : Type*} {œÄ : Œ≤ ‚Üí Type*} [nonempty Œ≤] [fintype Œ≤] [Œ† b, pseudo_metric_space (œÄ b)]
  {f g : Œ† b, œÄ b} {r : ‚Ñù}

lemma dist_pi_le_iff' : dist f g ‚â§ r ‚Üî ‚àÄ b, dist (f b) (g b) ‚â§ r :=
begin
  by_cases hr : 0 ‚â§ r,
  { exact dist_pi_le_iff hr },
  { exact iff_of_false (Œª h, hr $ dist_nonneg.trans h)
      (Œª h, hr $ dist_nonneg.trans $ h $ classical.arbitrary _) }
end

end

section
variables {Œ≤ : Type*} {œÄ : Œ≤ ‚Üí Type*} [nonempty Œ≤] [fintype Œ≤]
  [Œ† b, seminormed_add_comm_group (œÄ b)] {f : Œ† b, œÄ b} {r : ‚Ñù}

lemma pi_norm_le_iff'' : ‚à•f‚à• ‚â§ r ‚Üî ‚àÄ b, ‚à•f b‚à• ‚â§ r :=
begin
  by_cases hr : 0 ‚â§ r,
  { exact pi_norm_le_iff hr },
  { exact iff_of_false (Œª h, hr $ (norm_nonneg _).trans h)
      (Œª h, hr $ (norm_nonneg _).trans $ h $ classical.arbitrary _) }
end

end

section
variables {Œπ E : Type*} [fintype Œπ] [seminormed_add_comm_group E]

lemma pi_norm_const_le (a : E) : ‚à•(Œª _ : Œπ, a)‚à• ‚â§ ‚à•a‚à• :=
(pi_norm_le_iff $ norm_nonneg _).2 $ Œª _, le_rfl

end

section
variables {Œπ : Type*} [fintype Œπ] [nonempty Œπ] {Œµ : ‚Ñù}

open function metric

lemma pi.exists_gt_mem_ball (x : Œπ ‚Üí ‚Ñù) (hŒµ : 0 < Œµ) : ‚àÉ y, x < y ‚àß dist y x < Œµ :=
begin
  refine ‚ü®x + const Œπ (Œµ / 2), lt_add_of_pos_right _ $ by positivity, _‚ü©,
  simpa [@pi_norm_const'' Œπ ‚Ñù _ _ _ (Œµ / 2), abs_of_nonneg hŒµ.le],
end

lemma pi.exists_lt_mem_ball (x : Œπ ‚Üí ‚Ñù) (hŒµ : 0 < Œµ) : ‚àÉ y, y < x ‚àß dist y x < Œµ :=
begin
  refine ‚ü®x - const Œπ (Œµ / 2), sub_lt_self _ $ by positivity, _‚ü©,
  simpa [@pi_norm_const'' Œπ ‚Ñù _ _ _ (Œµ / 2), abs_of_nonneg hŒµ.le],
end

end

section
variables {Œ± Œ≤ : Type*}

@[to_additive] instance order_dual.has_smul' [h : has_smul Œ± Œ≤] : has_smul Œ±·µí·µà Œ≤ := h
@[to_additive order_dual.has_smul']
instance order_dual.has_pow' [h : has_pow Œ± Œ≤] : has_pow Œ± Œ≤·µí·µà := h

instance [topological_space Œ≤] [has_vadd Œ± Œ≤] [has_continuous_const_vadd Œ± Œ≤] :
  has_continuous_const_vadd Œ± Œ≤·µí·µà :=
‚Äπhas_continuous_const_vadd Œ± Œ≤‚Ä∫

@[to_additive] instance [topological_space Œ≤] [has_smul Œ± Œ≤] [has_continuous_const_smul Œ± Œ≤] :
  has_continuous_const_smul Œ± Œ≤·µí·µà :=
‚Äπhas_continuous_const_smul Œ± Œ≤‚Ä∫

@[to_additive] instance order_dual.has_continuous_const_smul' [topological_space Œ≤] [has_smul Œ± Œ≤]
  [has_continuous_const_smul Œ± Œ≤] :
  has_continuous_const_smul Œ±·µí·µà Œ≤ :=
‚Äπhas_continuous_const_smul Œ± Œ≤‚Ä∫

end

open function  metric set
open_locale pointwise

variables {Œ± Œπ : Type*}

section
variables [topological_space Œ±] [ordered_comm_group Œ±] [has_continuous_const_smul Œ± Œ±]
  {s : set Œ±}

@[to_additive is_upper_set.closure]
protected lemma is_upper_set.closure' (h : is_upper_set s) : is_upper_set (closure s) :=
Œª x y hxy hx, closure_mono (h.smul_subset $ one_le_div'.2 hxy) $
  by { rw closure_smul, exact ‚ü®x, hx, div_mul_cancel' _ _‚ü© }

@[to_additive is_lower_set.closure]
protected lemma is_lower_set.closure' (h : is_lower_set s) : is_lower_set (closure s) :=
h.of_dual.closure'

/-
Note: ` s.ord_connected` does not imply `(closure s).ord_connected`, as we can see by taking
`s := Ioo 0 1 √ó Ioo 1 2 ‚à™ Ioo 2 3 √ó Ioo 0 1` because then
`closure s = Icc 0 1 √ó Icc 1 2 ‚à™ Icc 2 3 √ó Icc 0 1` is not order-connected as
`(1, 1) ‚àà closure s`, `(2, 1) ‚àà closure s` but `Icc (1, 1) (2, 1) ‚äà closure s`.

`s` looks like
```
xxooooo
xxooooo
oooooxx
oooooxx
```
-/

@[to_additive is_upper_set.interior]
protected lemma is_upper_set.interior' (h : is_upper_set s) : is_upper_set (interior s) :=
by { rw [‚Üêis_lower_set_compl, ‚Üêclosure_compl], exact h.compl.closure' }

@[to_additive is_lower_set.interior]
protected lemma is_lower_set.interior' (h : is_lower_set s) : is_lower_set (interior s) :=
h.of_dual.interior'

@[to_additive set.ord_connected.interior]
protected lemma set.ord_connected.interior' (h : s.ord_connected) : (interior s).ord_connected :=
begin
  rw [‚Üêh.upper_closure_inter_lower_closure, interior_inter],
  exact (upper_closure s).upper.interior'.ord_connected.inter
    (lower_closure s).lower.interior'.ord_connected,
end

@[to_additive upper_closure_interior_subset]
lemma upper_closure_interior_subset' (s : set Œ±) :
  (upper_closure (interior s) : set Œ±) ‚äÜ interior (upper_closure s) :=
upper_closure_min (interior_mono subset_upper_closure) (upper_closure s).upper.interior'

@[to_additive lower_closure_interior_subset]
lemma lower_closure_interior_subset' (s : set Œ±) :
  (upper_closure (interior s) : set Œ±) ‚äÜ interior (upper_closure s) :=
upper_closure_min (interior_mono subset_upper_closure) (upper_closure s).upper.interior'

end

section
variables [topological_space Œ±] [ordered_comm_group Œ±] [has_continuous_mul Œ±] {s : set Œ±}

@[to_additive is_open.upper_closure]
protected lemma is_open.upper_closure' (hs : is_open s) : is_open (upper_closure s : set Œ±) :=
by { rw [‚Üêmul_one s, ‚Üêmul_upper_closure], exact hs.mul_right }

@[to_additive is_open.lower_closure]
protected lemma is_open.lower_closure' (hs : is_open s) : is_open (lower_closure s : set Œ±) :=
by { rw [‚Üêmul_one s, ‚Üêmul_lower_closure], exact hs.mul_right }

end

/-! ### `‚Ñù‚Åø` -/

section finite
variables [finite Œπ] {s : set (Œπ ‚Üí ‚Ñù)} {x y : Œπ ‚Üí ‚Ñù} {Œ¥ : ‚Ñù}

lemma is_upper_set.mem_interior_of_forall_lt (hs : is_upper_set s) (hx : x ‚àà closure s)
  (h : ‚àÄ i, x i < y i) :
  y ‚àà interior s :=
begin
  casesI nonempty_fintype Œπ,
  obtain ‚ü®Œµ, hŒµ, hxy‚ü© := pi.exists_forall_pos_add_lt h,
  obtain ‚ü®z, hz, hxz‚ü© := metric.mem_closure_iff.1 hx _ hŒµ,
  rw dist_pi_lt_iff hŒµ at hxz,
  have hyz : ‚àÄ i, z i < y i,
  { refine Œª i, (hxy _).trans_le' (sub_le_iff_le_add'.1 $ (le_abs_self _).trans _),
    rw [‚Üêreal.norm_eq_abs, ‚Üêdist_eq_norm'],
    exact (hxz _).le },
  obtain ‚ü®Œ¥, hŒ¥, hyz‚ü© := pi.exists_forall_pos_add_lt hyz,
  refine mem_interior.2 ‚ü®ball y Œ¥, _, is_open_ball, mem_ball_self hŒ¥‚ü©,
  rintro w hw,
  refine hs (Œª i, _) hz,
  simp_rw [ball_pi _ hŒ¥, real.ball_eq_Ioo] at hw,
  exact ((lt_sub_iff_add_lt.2 $ hyz _).trans (hw _ $ mem_univ _).1).le,
end

lemma is_lower_set.mem_interior_of_forall_lt (hs : is_lower_set s) (hx : x ‚àà closure s)
  (h : ‚àÄ i, y i < x i) :
  y ‚àà interior s :=
begin
  casesI nonempty_fintype Œπ,
  obtain ‚ü®Œµ, hŒµ, hxy‚ü© := pi.exists_forall_pos_add_lt h,
  obtain ‚ü®z, hz, hxz‚ü© := metric.mem_closure_iff.1 hx _ hŒµ,
  rw dist_pi_lt_iff hŒµ at hxz,
  have hyz : ‚àÄ i, y i < z i,
  { refine Œª i, (lt_sub_iff_add_lt.2 $ hxy _).trans_le (_root_.sub_le.1 $ (le_abs_self _).trans _),
    rw [‚Üêreal.norm_eq_abs, ‚Üêdist_eq_norm],
    exact (hxz _).le },
  obtain ‚ü®Œ¥, hŒ¥, hyz‚ü© := pi.exists_forall_pos_add_lt hyz,
  refine mem_interior.2 ‚ü®ball y Œ¥, _, is_open_ball, mem_ball_self hŒ¥‚ü©,
  rintro w hw,
  refine hs (Œª i, _) hz,
  simp_rw [ball_pi _ hŒ¥, real.ball_eq_Ioo] at hw,
  exact ((hw _ $ mem_univ _).2.trans $ hyz _).le,
end

end finite

section fintype
variables [fintype Œπ] {s : set (Œπ ‚Üí ‚Ñù)} {x y : Œπ ‚Üí ‚Ñù} {Œ¥ : ‚Ñù}

lemma is_antichain.interior_eq_empty [nonempty Œπ] (hs : is_antichain (‚â§) s) : interior s = ‚àÖ :=
begin
  refine eq_empty_of_forall_not_mem (Œª x hx, _),
  have hx' := interior_subset hx,
  rw [mem_interior_iff_mem_nhds, metric.mem_nhds_iff] at hx,
  obtain ‚ü®Œµ, hŒµ, hx‚ü© := hx,
  obtain ‚ü®y, hy, hyx‚ü© := pi.exists_gt_mem_ball x hŒµ,
  exact hs.not_lt hx' (hx hyx) hy,
end

/-
Note: The closure and frontier of an antichain might not be antichains. Take for example the union
of the open segments from `(0, 2)` to `(1, 1)` and from `(2, 1)` to `(3, 0)`. `(1, 1)` and `(2, 1)`
are comparable and both in the closure/frontier.
-/

protected lemma is_closed.upper_closure (hs : is_closed s) :
  is_closed (upper_closure s : set (Œπ ‚Üí ‚Ñù)) :=
sorry

protected lemma is_closed.lower_closure (hs : is_closed s) :
  is_closed (lower_closure s : set (Œπ ‚Üí ‚Ñù)) :=
sorry

protected lemma is_clopen.upper_closure (hs : is_clopen s) :
  is_clopen (upper_closure s : set (Œπ ‚Üí ‚Ñù)) :=
‚ü®hs.1.upper_closure, hs.2.upper_closure‚ü©

protected lemma is_clopen.lower_closure (hs : is_clopen s) :
  is_clopen (lower_closure s : set (Œπ ‚Üí ‚Ñù)) :=
‚ü®hs.1.lower_closure, hs.2.lower_closure‚ü©

lemma closure_upper_closure_comm (s : set (Œπ ‚Üí ‚Ñù)) :
  closure (upper_closure s : set (Œπ ‚Üí ‚Ñù)) = upper_closure (closure s) :=
(closure_minimal (upper_closure_anti subset_closure) is_closed_closure.upper_closure).antisymm
  (upper_closure_min (closure_mono subset_upper_closure) (upper_closure s).upper.closure)

lemma closure_lower_closure_comm (s : set (Œπ ‚Üí ‚Ñù)) :
  closure (lower_closure s : set (Œπ ‚Üí ‚Ñù)) = lower_closure (closure s) :=
(closure_minimal (lower_closure_mono subset_closure) is_closed_closure.lower_closure).antisymm
  (lower_closure_min (closure_mono subset_lower_closure) (lower_closure s).lower.closure)

lemma is_upper_set.exists_subset_ball (hs : is_upper_set s) (hx : x ‚àà closure s) (hŒ¥ : 0 < Œ¥) :
  ‚àÉ y, closed_ball y (Œ¥/4) ‚äÜ closed_ball x Œ¥ ‚àß closed_ball y (Œ¥/4) ‚äÜ interior s :=
begin
  refine ‚ü®x + const _ (3/4*Œ¥), closed_ball_subset_closed_ball' _, _‚ü©,
  { rw dist_self_add_left,
    refine (add_le_add_left (pi_norm_const_le _) _).trans_eq _,
    simp [real.norm_of_nonneg, hŒ¥.le, zero_le_three],
    ring_nf },
  obtain ‚ü®y, hy, hxy‚ü© := metric.mem_closure_iff.1 hx _ (div_pos hŒ¥ zero_lt_four),
  refine Œª z hz, hs.mem_interior_of_forall_lt (subset_closure hy) (Œª i, _),
  rw [mem_closed_ball, dist_eq_norm'] at hz,
  rw dist_eq_norm at hxy,
  replace hxy := (norm_le_pi_norm _ i).trans hxy.le,
  replace hz := (norm_le_pi_norm _ i).trans hz,
  dsimp at hxy hz,
  rw abs_sub_le_iff at hxy hz,
  linarith,
end

lemma is_lower_set.exists_subset_ball (hs : is_lower_set s) (hx : x ‚àà closure s) (hŒ¥ : 0 < Œ¥) :
  ‚àÉ y, closed_ball y (Œ¥/4) ‚äÜ closed_ball x Œ¥ ‚àß closed_ball y (Œ¥/4) ‚äÜ interior s :=
begin
  refine ‚ü®x - const _ (3/4*Œ¥), closed_ball_subset_closed_ball' _, _‚ü©,
  { rw dist_self_sub_left,
    refine (add_le_add_left (pi_norm_const_le _) _).trans_eq _,
    simp [real.norm_of_nonneg, hŒ¥.le, zero_le_three],
    ring_nf },
  obtain ‚ü®y, hy, hxy‚ü© := metric.mem_closure_iff.1 hx _ (div_pos hŒ¥ zero_lt_four),
  refine Œª z hz, hs.mem_interior_of_forall_lt (subset_closure hy) (Œª i, _),
  rw [mem_closed_ball, dist_eq_norm'] at hz,
  rw dist_eq_norm at hxy,
  replace hxy := (norm_le_pi_norm _ i).trans hxy.le,
  replace hz := (norm_le_pi_norm _ i).trans hz,
  dsimp at hxy hz,
  rw abs_sub_le_iff at hxy hz,
  linarith,
end

end fintype
