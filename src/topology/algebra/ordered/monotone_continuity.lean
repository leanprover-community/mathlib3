/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Heather Macbeth
-/
import topology.algebra.ordered.basic
import topology.algebra.ordered.left_right

/-!
# Continuity of monotone functions

In this file we prove the following fact: if `f` is a monotone function on a neighborhood of `a`
and the image of this neighborhood is a neighborhood of `f a`, then `f` is continuous at `a`, see
`continuous_at_of_monotone_on_of_image_mem_nhds`, as well as several similar facts.

We also prove that an `order_iso` is continuous.

## Tags

continuous, monotone
-/

open set filter
open_locale topological_space

section linear_order
variables {Œ± Œ≤ : Type*} [linear_order Œ±] [topological_space Œ±] [order_topology Œ±]
variables [linear_order Œ≤] [topological_space Œ≤] [order_topology Œ≤]

/-- If `f` is a function strictly monotone on a right neighborhood of `a` and the
image of this neighborhood under `f` meets every interval `(f a, b]`, `b > f a`, then `f` is
continuous at `a` from the right.

The assumption `hfs : ‚àÄ b > f a, ‚àÉ c ‚àà s, f c ‚àà Ioc (f a) b` is required because otherwise the
function `f : ‚Ñù ‚Üí ‚Ñù` given by `f x = if x ‚â§ 0 then x else x + 1` would be a counter-example at
`a = 0`. -/
lemma strict_mono_on.continuous_at_right_of_exists_between {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±}
  (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù[Ici a] a)
  (hfs : ‚àÄ b > f a, ‚àÉ c ‚àà s, f c ‚àà Ioc (f a) b) :
  continuous_within_at f (Ici a) a :=
begin
  have ha : a ‚àà Ici a := left_mem_Ici,
  have has : a ‚àà s := mem_of_mem_nhds_within ha hs,
  refine tendsto_order.2 ‚ü®Œª b hb, _, Œª b hb, _‚ü©,
  { filter_upwards [hs, self_mem_nhds_within],
    intros x hxs hxa,
    exact hb.trans_le ((h_mono.le_iff_le has hxs).2 hxa) },
  { rcases hfs b hb with ‚ü®c, hcs, hac, hcb‚ü©,
    rw [h_mono.lt_iff_lt has hcs] at hac,
    filter_upwards [hs, Ico_mem_nhds_within_Ici (left_mem_Ico.2 hac)],
    rintros x hx ‚ü®hax, hxc‚ü©,
    exact ((h_mono.lt_iff_lt hx hcs).2 hxc).trans_le hcb }
end

/-- If `f` is a monotone function on a right neighborhood of `a` and the image of this neighborhood
under `f` meets every interval `(f a, b)`, `b > f a`, then `f` is continuous at `a` from the right.

The assumption `hfs : ‚àÄ b > f a, ‚àÉ c ‚àà s, f c ‚àà Ioo (f a) b` cannot be replaced by the weaker
assumption `hfs : ‚àÄ b > f a, ‚àÉ c ‚àà s, f c ‚àà Ioc (f a) b` we use for strictly monotone functions
because otherwise the function `ceil : ‚Ñù ‚Üí ‚Ñ§` would be a counter-example at `a = 0`. -/
lemma continuous_at_right_of_monotone_on_of_exists_between {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±}
  (h_mono : monotone_on f s) (hs : s ‚àà ùìù[Ici a] a)
  (hfs : ‚àÄ b > f a, ‚àÉ c ‚àà s, f c ‚àà Ioo (f a) b) :
  continuous_within_at f (Ici a) a :=
begin
  have ha : a ‚àà Ici a := left_mem_Ici,
  have has : a ‚àà s := mem_of_mem_nhds_within ha hs,
  refine tendsto_order.2 ‚ü®Œª b hb, _, Œª b hb, _‚ü©,
  { filter_upwards [hs, self_mem_nhds_within],
    intros x hxs hxa,
    exact hb.trans_le (h_mono has hxs hxa) },
  { rcases hfs b hb with ‚ü®c, hcs, hac, hcb‚ü©,
    have : a < c, from not_le.1 (Œª h, hac.not_le $ h_mono hcs has h),
    filter_upwards [hs, Ico_mem_nhds_within_Ici (left_mem_Ico.2 this)],
    rintros x hx ‚ü®hax, hxc‚ü©,
    exact (h_mono hx hcs hxc.le).trans_lt hcb }
end

/-- If a function `f` with a densely ordered codomain is monotone on a right neighborhood of `a` and
the closure of the image of this neighborhood under `f` is a right neighborhood of `f a`, then `f`
is continuous at `a` from the right. -/
lemma continuous_at_right_of_monotone_on_of_closure_image_mem_nhds_within [densely_ordered Œ≤]
  {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±} (h_mono : monotone_on f s)
  (hs : s ‚àà ùìù[Ici a] a) (hfs : closure (f '' s) ‚àà ùìù[Ici (f a)] (f a)) :
  continuous_within_at f (Ici a) a :=
begin
  refine continuous_at_right_of_monotone_on_of_exists_between h_mono hs (Œª b hb, _),
  rcases (mem_nhds_within_Ici_iff_exists_mem_Ioc_Ico_subset hb).1 hfs with ‚ü®b', ‚ü®hab', hbb'‚ü©, hb'‚ü©,
  rcases exists_between hab' with ‚ü®c', hc'‚ü©,
  rcases mem_closure_iff.1 (hb' ‚ü®hc'.1.le, hc'.2‚ü©) (Ioo (f a) b') is_open_Ioo hc'
    with ‚ü®_, hc, ‚ü®c, hcs, rfl‚ü©‚ü©,
  exact ‚ü®c, hcs, hc.1, hc.2.trans_le hbb'‚ü©
end

/-- If a function `f` with a densely ordered codomain is monotone on a right neighborhood of `a` and
the image of this neighborhood under `f` is a right neighborhood of `f a`, then `f` is continuous at
`a` from the right. -/
lemma continuous_at_right_of_monotone_on_of_image_mem_nhds_within [densely_ordered Œ≤] {f : Œ± ‚Üí Œ≤}
  {s : set Œ±} {a : Œ±} (h_mono : monotone_on f s) (hs : s ‚àà ùìù[Ici a] a)
  (hfs : f '' s ‚àà ùìù[Ici (f a)] (f a)) :
  continuous_within_at f (Ici a) a :=
continuous_at_right_of_monotone_on_of_closure_image_mem_nhds_within h_mono hs $
  mem_of_superset hfs subset_closure

/-- If a function `f` with a densely ordered codomain is strictly monotone on a right neighborhood
of `a` and the closure of the image of this neighborhood under `f` is a right neighborhood of `f a`,
then `f` is continuous at `a` from the right. -/
lemma strict_mono_on.continuous_at_right_of_closure_image_mem_nhds_within [densely_ordered Œ≤]
  {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±} (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù[Ici a] a)
  (hfs : closure (f '' s) ‚àà ùìù[Ici (f a)] (f a)) :
  continuous_within_at f (Ici a) a :=
continuous_at_right_of_monotone_on_of_closure_image_mem_nhds_within
  (Œª x hx y hy, (h_mono.le_iff_le hx hy).2) hs hfs

/-- If a function `f` with a densely ordered codomain is strictly monotone on a right neighborhood
of `a` and the image of this neighborhood under `f` is a right neighborhood of `f a`, then `f` is
continuous at `a` from the right. -/
lemma strict_mono_on.continuous_at_right_of_image_mem_nhds_within [densely_ordered Œ≤]
  {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±} (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù[Ici a] a)
  (hfs : f '' s ‚àà ùìù[Ici (f a)] (f a)) :
  continuous_within_at f (Ici a) a :=
h_mono.continuous_at_right_of_closure_image_mem_nhds_within hs
  (mem_of_superset hfs subset_closure)

/-- If a function `f` is strictly monotone on a right neighborhood of `a` and the image of this
neighborhood under `f` includes `Ioi (f a)`, then `f` is continuous at `a` from the right. -/
lemma strict_mono_on.continuous_at_right_of_surj_on {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±}
  (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù[Ici a] a) (hfs : surj_on f s (Ioi (f a))) :
  continuous_within_at f (Ici a) a :=
h_mono.continuous_at_right_of_exists_between hs $ Œª b hb, let ‚ü®c, hcs, hcb‚ü© := hfs hb in
‚ü®c, hcs, hcb.symm ‚ñ∏ hb, hcb.le‚ü©

/-- If `f` is a strictly monotone function on a left neighborhood of `a` and the image of this
neighborhood under `f` meets every interval `[b, f a)`, `b < f a`, then `f` is continuous at `a`
from the left.

The assumption `hfs : ‚àÄ b < f a, ‚àÉ c ‚àà s, f c ‚àà Ico b (f a)` is required because otherwise the
function `f : ‚Ñù ‚Üí ‚Ñù` given by `f x = if x < 0 then x else x + 1` would be a counter-example at
`a = 0`. -/
lemma strict_mono_on.continuous_at_left_of_exists_between {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±}
  (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù[Iic a] a)
  (hfs : ‚àÄ b < f a, ‚àÉ c ‚àà s, f c ‚àà Ico b (f a)) :
  continuous_within_at f (Iic a) a :=
h_mono.dual.continuous_at_right_of_exists_between hs $
  Œª b hb, let ‚ü®c, hcs, hcb, hca‚ü© := hfs b hb in ‚ü®c, hcs, hca, hcb‚ü©

/-- If `f` is a monotone function on a left neighborhood of `a` and the image of this neighborhood
under `f` meets every interval `(b, f a)`, `b < f a`, then `f` is continuous at `a` from the left.

The assumption `hfs : ‚àÄ b < f a, ‚àÉ c ‚àà s, f c ‚àà Ioo b (f a)` cannot be replaced by the weaker
assumption `hfs : ‚àÄ b < f a, ‚àÉ c ‚àà s, f c ‚àà Ico b (f a)` we use for strictly monotone functions
because otherwise the function `floor : ‚Ñù ‚Üí ‚Ñ§` would be a counter-example at `a = 0`. -/
lemma continuous_at_left_of_monotone_on_of_exists_between {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±}
  (hf : monotone_on f s) (hs : s ‚àà ùìù[Iic a] a)
  (hfs : ‚àÄ b < f a, ‚àÉ c ‚àà s, f c ‚àà Ioo b (f a)) :
  continuous_within_at f (Iic a) a :=
@continuous_at_right_of_monotone_on_of_exists_between (order_dual Œ±) (order_dual Œ≤) _ _ _ _ _ _
  f s a hf.dual hs $
  Œª b hb, let ‚ü®c, hcs, hcb, hca‚ü© := hfs b hb in ‚ü®c, hcs, hca, hcb‚ü©

/-- If a function `f` with a densely ordered codomain is monotone on a left neighborhood of `a` and
the closure of the image of this neighborhood under `f` is a left neighborhood of `f a`, then `f` is
continuous at `a` from the left -/
lemma continuous_at_left_of_monotone_on_of_closure_image_mem_nhds_within [densely_ordered Œ≤]
  {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±} (hf : monotone_on f s)
  (hs : s ‚àà ùìù[Iic a] a) (hfs : closure (f '' s) ‚àà ùìù[Iic (f a)] (f a)) :
  continuous_within_at f (Iic a) a :=
@continuous_at_right_of_monotone_on_of_closure_image_mem_nhds_within (order_dual Œ±) (order_dual Œ≤)
  _ _ _ _ _ _ _ f s a hf.dual hs hfs

/-- If a function `f` with a densely ordered codomain is monotone on a left neighborhood of `a` and
the image of this neighborhood under `f` is a left neighborhood of `f a`, then `f` is continuous at
`a` from the left. -/
lemma continuous_at_left_of_monotone_on_of_image_mem_nhds_within [densely_ordered Œ≤]
  {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±} (h_mono : monotone_on f s)
  (hs : s ‚àà ùìù[Iic a] a) (hfs : f '' s ‚àà ùìù[Iic (f a)] (f a)) :
  continuous_within_at f (Iic a) a :=
continuous_at_left_of_monotone_on_of_closure_image_mem_nhds_within h_mono hs
  (mem_of_superset hfs subset_closure)

/-- If a function `f` with a densely ordered codomain is strictly monotone on a left neighborhood of
`a` and the closure of the image of this neighborhood under `f` is a left neighborhood of `f a`,
then `f` is continuous at `a` from the left. -/
lemma strict_mono_on.continuous_at_left_of_closure_image_mem_nhds_within [densely_ordered Œ≤]
  {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±} (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù[Iic a] a)
  (hfs : closure (f '' s) ‚àà ùìù[Iic (f a)] (f a)) :
  continuous_within_at f (Iic a) a :=
h_mono.dual.continuous_at_right_of_closure_image_mem_nhds_within hs hfs

/-- If a function `f` with a densely ordered codomain is strictly monotone on a left neighborhood of
`a` and the image of this neighborhood under `f` is a left neighborhood of `f a`, then `f` is
continuous at `a` from the left. -/
lemma strict_mono_on.continuous_at_left_of_image_mem_nhds_within [densely_ordered Œ≤]
  {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±} (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù[Iic a] a)
  (hfs : f '' s ‚àà ùìù[Iic (f a)] (f a)) :
  continuous_within_at f (Iic a) a :=
h_mono.dual.continuous_at_right_of_image_mem_nhds_within hs hfs

/-- If a function `f` is strictly monotone on a left neighborhood of `a` and the image of this
neighborhood under `f` includes `Iio (f a)`, then `f` is continuous at `a` from the left. -/
lemma strict_mono_on.continuous_at_left_of_surj_on {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±}
  (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù[Iic a] a) (hfs : surj_on f s (Iio (f a))) :
  continuous_within_at f (Iic a) a :=
h_mono.dual.continuous_at_right_of_surj_on hs hfs

/-- If a function `f` is strictly monotone on a neighborhood of `a` and the image of this
neighborhood under `f` meets every interval `[b, f a)`, `b < f a`, and every interval
`(f a, b]`, `b > f a`, then `f` is continuous at `a`. -/
lemma strict_mono_on.continuous_at_of_exists_between {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±}
  (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù a)
  (hfs_l : ‚àÄ b < f a, ‚àÉ c ‚àà s, f c ‚àà Ico b (f a)) (hfs_r : ‚àÄ b > f a, ‚àÉ c ‚àà s, f c ‚àà Ioc (f a) b) :
  continuous_at f a :=
continuous_at_iff_continuous_left_right.2
  ‚ü®h_mono.continuous_at_left_of_exists_between (mem_nhds_within_of_mem_nhds hs) hfs_l,
   h_mono.continuous_at_right_of_exists_between (mem_nhds_within_of_mem_nhds hs) hfs_r‚ü©

/-- If a function `f` with a densely ordered codomain is strictly monotone on a neighborhood of `a`
and the closure of the image of this neighborhood under `f` is a neighborhood of `f a`, then `f` is
continuous at `a`. -/
lemma strict_mono_on.continuous_at_of_closure_image_mem_nhds [densely_ordered Œ≤] {f : Œ± ‚Üí Œ≤}
  {s : set Œ±} {a : Œ±} (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù a)
  (hfs : closure (f '' s) ‚àà ùìù (f a)) :
  continuous_at f a :=
continuous_at_iff_continuous_left_right.2
  ‚ü®h_mono.continuous_at_left_of_closure_image_mem_nhds_within (mem_nhds_within_of_mem_nhds hs)
     (mem_nhds_within_of_mem_nhds hfs),
   h_mono.continuous_at_right_of_closure_image_mem_nhds_within (mem_nhds_within_of_mem_nhds hs)
     (mem_nhds_within_of_mem_nhds hfs)‚ü©

/-- If a function `f` with a densely ordered codomain is strictly monotone on a neighborhood of `a`
and the image of this set under `f` is a neighborhood of `f a`, then `f` is continuous at `a`. -/
lemma strict_mono_on.continuous_at_of_image_mem_nhds [densely_ordered Œ≤] {f : Œ± ‚Üí Œ≤}
  {s : set Œ±} {a : Œ±} (h_mono : strict_mono_on f s) (hs : s ‚àà ùìù a) (hfs : f '' s ‚àà ùìù (f a)) :
  continuous_at f a :=
h_mono.continuous_at_of_closure_image_mem_nhds hs (mem_of_superset hfs subset_closure)

/-- If `f` is a monotone function on a neighborhood of `a` and the image of this neighborhood under
`f` meets every interval `(b, f a)`, `b < f a`, and every interval `(f a, b)`, `b > f a`, then `f`
is continuous at `a`. -/
lemma continuous_at_of_monotone_on_of_exists_between {f : Œ± ‚Üí Œ≤} {s : set Œ±} {a : Œ±}
  (h_mono : monotone_on f s) (hs : s ‚àà ùìù a)
  (hfs_l : ‚àÄ b < f a, ‚àÉ c ‚àà s, f c ‚àà Ioo b (f a)) (hfs_r : ‚àÄ b > f a, ‚àÉ c ‚àà s, f c ‚àà Ioo (f a) b) :
  continuous_at f a :=
continuous_at_iff_continuous_left_right.2
  ‚ü®continuous_at_left_of_monotone_on_of_exists_between h_mono
     (mem_nhds_within_of_mem_nhds hs) hfs_l,
   continuous_at_right_of_monotone_on_of_exists_between h_mono
     (mem_nhds_within_of_mem_nhds hs) hfs_r‚ü©

/-- If a function `f` with a densely ordered codomain is monotone on a neighborhood of `a` and the
closure of the image of this neighborhood under `f` is a neighborhood of `f a`, then `f` is
continuous at `a`. -/
lemma continuous_at_of_monotone_on_of_closure_image_mem_nhds [densely_ordered Œ≤] {f : Œ± ‚Üí Œ≤}
  {s : set Œ±} {a : Œ±} (h_mono : monotone_on f s) (hs : s ‚àà ùìù a)
  (hfs : closure (f '' s) ‚àà ùìù (f a)) :
  continuous_at f a :=
continuous_at_iff_continuous_left_right.2
  ‚ü®continuous_at_left_of_monotone_on_of_closure_image_mem_nhds_within h_mono
     (mem_nhds_within_of_mem_nhds hs) (mem_nhds_within_of_mem_nhds hfs),
   continuous_at_right_of_monotone_on_of_closure_image_mem_nhds_within h_mono
     (mem_nhds_within_of_mem_nhds hs) (mem_nhds_within_of_mem_nhds hfs)‚ü©

/-- If a function `f` with a densely ordered codomain is monotone on a neighborhood of `a` and the
image of this neighborhood under `f` is a neighborhood of `f a`, then `f` is continuous at `a`. -/
lemma continuous_at_of_monotone_on_of_image_mem_nhds [densely_ordered Œ≤] {f : Œ± ‚Üí Œ≤}
  {s : set Œ±} {a : Œ±} (h_mono : monotone_on f s) (hs : s ‚àà ùìù a)
  (hfs : f '' s ‚àà ùìù (f a)) :
  continuous_at f a :=
continuous_at_of_monotone_on_of_closure_image_mem_nhds h_mono hs
  (mem_of_superset hfs subset_closure)

/-- A monotone function with densely ordered codomain and a dense range is continuous. -/
lemma monotone.continuous_of_dense_range [densely_ordered Œ≤] {f : Œ± ‚Üí Œ≤}
  (h_mono : monotone f) (h_dense : dense_range f) :
  continuous f :=
continuous_iff_continuous_at.mpr $ Œª a,
  continuous_at_of_monotone_on_of_closure_image_mem_nhds (Œª x hx y hy hxy, h_mono hxy)
    univ_mem $ by simp only [image_univ, h_dense.closure_eq, univ_mem]

/-- A monotone surjective function with a densely ordered codomain is continuous. -/
lemma monotone.continuous_of_surjective [densely_ordered Œ≤] {f : Œ± ‚Üí Œ≤} (h_mono : monotone f)
  (h_surj : function.surjective f) :
  continuous f :=
h_mono.continuous_of_dense_range h_surj.dense_range

end linear_order

/-!
### Continuity of order isomorphisms

In this section we prove that an `order_iso` is continuous, hence it is a `homeomorph`. We prove
this for an `order_iso` between to partial orders with order topology.
-/

namespace order_iso

variables {Œ± Œ≤ : Type*} [partial_order Œ±] [partial_order Œ≤] [topological_space Œ±]
  [topological_space Œ≤] [order_topology Œ±] [order_topology Œ≤]

protected lemma continuous (e : Œ± ‚âÉo Œ≤) : continuous e :=
begin
  rw [‚Äπorder_topology Œ≤‚Ä∫.topology_eq_generate_intervals],
  refine continuous_generated_from (Œª s hs, _),
  rcases hs with ‚ü®a, rfl|rfl‚ü©,
  { rw e.preimage_Ioi, apply is_open_lt' },
  { rw e.preimage_Iio, apply is_open_gt' }
end

/-- An order isomorphism between two linear order `order_topology` spaces is a homeomorphism. -/
def to_homeomorph (e : Œ± ‚âÉo Œ≤) : Œ± ‚âÉ‚Çú Œ≤ :=
{ continuous_to_fun := e.continuous,
  continuous_inv_fun := e.symm.continuous,
  .. e }

@[simp] lemma coe_to_homeomorph (e : Œ± ‚âÉo Œ≤) : ‚áëe.to_homeomorph = e := rfl
@[simp] lemma coe_to_homeomorph_symm (e : Œ± ‚âÉo Œ≤) : ‚áëe.to_homeomorph.symm = e.symm := rfl

end order_iso
