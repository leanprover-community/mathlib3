/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Yury Kudryashov
-/
import topology.algebra.ordered.basic

/-!
# Bounded monotone sequences converge

In this file we prove a few theorems of the form ‚Äúif the range of a monotone function `f : Œπ ‚Üí Œ±`
admits a least upper bound `a`, then `f x` tends to `a` as `x ‚Üí ‚àû`‚Äù, as well as version of this
statement for (conditionally) complete lattices that use `‚®Ü x, f x` instead of `is_lub`.

These theorems work for linear orders with order topologies as well as their products (both in terms
of `prod` and in terms of function types). In order to reduce code duplication, we introduce two
typeclasses (one for the property formulated above and one for the dual property), prove theorems
assuming one of these typeclasses, and provide instances for linear orders and their products.

We also prove some "inverse" results: if `f n` is a monotone sequence and `a` is its limit,
then `f n ‚â§ a` for all `n`.

## Tags

monotone convergence
-/

open filter set function
open_locale filter topological_space classical

variables {Œ± Œ≤ : Type*}

/-- We say that `Œ±` is a `Sup_convergence_class` if the following holds. Let `f : Œπ ‚Üí Œ±` be a
monotone function, let `a : Œ±` be a least upper bound of `set.range f`. Then `f x` tends to `ùìù a` as
`x ‚Üí ‚àû` (formally, at the filter `filter.at_top`). We require this for `Œπ = (s : set Œ±)`, `f = coe`
in the definition, then prove it for any `f` in `tendsto_at_top_is_lub`.

This property holds for linear orders with order topology as well as their products. -/
class Sup_convergence_class (Œ± : Type*) [preorder Œ±] [topological_space Œ±] : Prop :=
(tendsto_coe_at_top_is_lub : ‚àÄ (a : Œ±) (s : set Œ±), is_lub s a ‚Üí tendsto (coe : s ‚Üí Œ±) at_top (ùìù a))

/-- We say that `Œ±` is an `Inf_convergence_class` if the following holds. Let `f : Œπ ‚Üí Œ±` be a
monotone function, let `a : Œ±` be a greatest lower bound of `set.range f`. Then `f x` tends to `ùìù a`
as `x ‚Üí -‚àû` (formally, at the filter `filter.at_bot`). We require this for `Œπ = (s : set Œ±)`,
`f = coe` in the definition, then prove it for any `f` in `tendsto_at_bot_is_glb`.

This property holds for linear orders with order topology as well as their products. -/
class Inf_convergence_class (Œ± : Type*) [preorder Œ±] [topological_space Œ±] : Prop :=
(tendsto_coe_at_bot_is_glb : ‚àÄ (a : Œ±) (s : set Œ±), is_glb s a ‚Üí tendsto (coe : s ‚Üí Œ±) at_bot (ùìù a))

instance order_dual.Sup_convergence_class [preorder Œ±] [topological_space Œ±]
  [Inf_convergence_class Œ±] : Sup_convergence_class (order_dual Œ±) :=
‚ü®‚ÄπInf_convergence_class Œ±‚Ä∫.1‚ü©

instance order_dual.Inf_convergence_class [preorder Œ±] [topological_space Œ±]
  [Sup_convergence_class Œ±] : Inf_convergence_class (order_dual Œ±) :=
‚ü®‚ÄπSup_convergence_class Œ±‚Ä∫.1‚ü©

@[priority 100] -- see Note [lower instance priority]
instance linear_order.Sup_convergence_class [topological_space Œ±] [linear_order Œ±]
  [order_topology Œ±] : Sup_convergence_class Œ± :=
begin
  refine ‚ü®Œª a s ha, tendsto_order.2 ‚ü®Œª b hb, _, Œª b hb, _‚ü©‚ü©,
  { rcases ha.exists_between hb with ‚ü®c, hcs, bc, bca‚ü©,
    lift c to s using hcs,
    refine (eventually_ge_at_top c).mono (Œª x hx, bc.trans_le hx) },
  { exact eventually_of_forall (Œª x, (ha.1 x.2).trans_lt hb) }
end

@[priority 100] -- see Note [lower instance priority]
instance linear_order.Inf_convergence_class [topological_space Œ±] [linear_order Œ±]
  [order_topology Œ±] : Inf_convergence_class Œ± :=
show Inf_convergence_class (order_dual $ order_dual Œ±), from order_dual.Inf_convergence_class

section

variables {Œπ : Type*} [preorder Œπ] [topological_space Œ±]

section is_lub

variables [preorder Œ±] [Sup_convergence_class Œ±] {f : Œπ ‚Üí Œ±} {a : Œ±}

lemma tendsto_at_top_is_lub (h_mono : monotone f) (ha : is_lub (set.range f) a) :
  tendsto f at_top (ùìù a) :=
begin
  suffices : tendsto (range_factorization f) at_top at_top,
    from (Sup_convergence_class.tendsto_coe_at_top_is_lub _ _ ha).comp this,
  exact h_mono.range_factorization.tendsto_at_top_at_top (Œª b, b.2.imp $ Œª a ha, ha.ge)
end

lemma tendsto_at_bot_is_lub (h_anti : antitone f)
  (ha : is_lub (set.range f) a) : tendsto f at_bot (ùìù a) :=
@tendsto_at_top_is_lub Œ± (order_dual Œπ) _ _ _ _ f a h_anti.dual ha

end is_lub

section is_glb

variables [preorder Œ±] [Inf_convergence_class Œ±] {f : Œπ ‚Üí Œ±} {a : Œ±}

lemma tendsto_at_bot_is_glb (h_mono : monotone f) (ha : is_glb (set.range f) a) :
  tendsto f at_bot (ùìù a) :=
@tendsto_at_top_is_lub (order_dual Œ±) (order_dual Œπ) _ _ _ _ f a h_mono.dual ha

lemma tendsto_at_top_is_glb (h_anti : antitone f)
  (ha : is_glb (set.range f) a) :
  tendsto f at_top (ùìù a) :=
@tendsto_at_top_is_lub (order_dual Œ±) Œπ _ _ _ _ f a h_anti ha

end is_glb

section csupr

variables [conditionally_complete_lattice Œ±] [Sup_convergence_class Œ±] {f : Œπ ‚Üí Œ±} {a : Œ±}

lemma tendsto_at_top_csupr (h_mono : monotone f) (hbdd : bdd_above $ range f) :
  tendsto f at_top (ùìù (‚®Üi, f i)) :=
begin
  casesI is_empty_or_nonempty Œπ,
  exacts [tendsto_of_is_empty, tendsto_at_top_is_lub h_mono (is_lub_csupr hbdd)]
end

lemma tendsto_at_bot_csupr (h_anti : antitone f)
  (hbdd : bdd_above $ range f) :
  tendsto f at_bot (ùìù (‚®Üi, f i)) :=
@tendsto_at_top_csupr Œ± (order_dual Œπ) _ _ _ _ _ h_anti.dual hbdd

end csupr

section cinfi

variables [conditionally_complete_lattice Œ±] [Inf_convergence_class Œ±] {f : Œπ ‚Üí Œ±} {a : Œ±}

lemma tendsto_at_bot_cinfi (h_mono : monotone f) (hbdd : bdd_below $ range f) :
  tendsto f at_bot (ùìù (‚®Öi, f i)) :=
@tendsto_at_top_csupr (order_dual Œ±) (order_dual Œπ) _ _ _ _ _ h_mono.dual hbdd

lemma tendsto_at_top_cinfi (h_anti : antitone f)
  (hbdd : bdd_below $ range f) :
  tendsto f at_top (ùìù (‚®Öi, f i)) :=
@tendsto_at_top_csupr (order_dual Œ±) Œπ _ _ _ _ _ h_anti hbdd

end cinfi

section supr

variables [complete_lattice Œ±] [Sup_convergence_class Œ±] {f : Œπ ‚Üí Œ±} {a : Œ±}

lemma tendsto_at_top_supr (h_mono : monotone f) : tendsto f at_top (ùìù (‚®Üi, f i)) :=
tendsto_at_top_csupr h_mono (order_top.bdd_above _)

lemma tendsto_at_bot_supr (h_anti : antitone f) :
  tendsto f at_bot (ùìù (‚®Üi, f i)) :=
tendsto_at_bot_csupr h_anti (order_top.bdd_above _)

end supr

section infi

variables [complete_lattice Œ±] [Inf_convergence_class Œ±] {f : Œπ ‚Üí Œ±} {a : Œ±}

lemma tendsto_at_bot_infi (h_mono : monotone f) : tendsto f at_bot (ùìù (‚®Öi, f i)) :=
tendsto_at_bot_cinfi h_mono (order_bot.bdd_below _)

lemma tendsto_at_top_infi (h_anti : antitone f) :
  tendsto f at_top (ùìù (‚®Öi, f i)) :=
tendsto_at_top_cinfi h_anti (order_bot.bdd_below _)

end infi

end

instance [preorder Œ±] [preorder Œ≤] [topological_space Œ±] [topological_space Œ≤]
  [Sup_convergence_class Œ±] [Sup_convergence_class Œ≤] : Sup_convergence_class (Œ± √ó Œ≤) :=
begin
  constructor,
  rintro ‚ü®a, b‚ü© s h,
  rw [is_lub_prod, ‚Üê range_restrict, ‚Üê range_restrict] at h,
  have A : tendsto (Œª x : s, (x : Œ± √ó Œ≤).1) at_top (ùìù a),
    from tendsto_at_top_is_lub (monotone_fst.restrict s) h.1,
  have B : tendsto (Œª x : s, (x : Œ± √ó Œ≤).2) at_top (ùìù b),
    from tendsto_at_top_is_lub (monotone_snd.restrict s) h.2,
  convert A.prod_mk_nhds B,
  ext1 ‚ü®‚ü®x, y‚ü©, h‚ü©, refl
end

instance [preorder Œ±] [preorder Œ≤] [topological_space Œ±] [topological_space Œ≤]
  [Inf_convergence_class Œ±] [Inf_convergence_class Œ≤] : Inf_convergence_class (Œ± √ó Œ≤) :=
show Inf_convergence_class (order_dual $ (order_dual Œ± √ó order_dual Œ≤)),
  from order_dual.Inf_convergence_class

instance {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} [Œ† i, preorder (Œ± i)] [Œ† i, topological_space (Œ± i)]
  [Œ† i, Sup_convergence_class (Œ± i)] : Sup_convergence_class (Œ† i, Œ± i) :=
begin
  refine ‚ü®Œª f s h, _‚ü©,
  simp only [is_lub_pi, ‚Üê range_restrict] at h,
  exact tendsto_pi_nhds.2 (Œª i, tendsto_at_top_is_lub ((monotone_eval _).restrict _) (h i))
end

instance {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} [Œ† i, preorder (Œ± i)] [Œ† i, topological_space (Œ± i)]
  [Œ† i, Inf_convergence_class (Œ± i)] : Inf_convergence_class (Œ† i, Œ± i) :=
show Inf_convergence_class (order_dual $ Œ† i, order_dual (Œ± i)),
  from order_dual.Inf_convergence_class

instance pi.Sup_convergence_class' {Œπ : Type*} [preorder Œ±] [topological_space Œ±]
  [Sup_convergence_class Œ±] : Sup_convergence_class (Œπ ‚Üí Œ±) :=
pi.Sup_convergence_class

instance pi.Inf_convergence_class' {Œπ : Type*} [preorder Œ±] [topological_space Œ±]
  [Inf_convergence_class Œ±] : Inf_convergence_class (Œπ ‚Üí Œ±) :=
pi.Inf_convergence_class

lemma tendsto_of_monotone {Œπ Œ± : Type*} [preorder Œπ] [topological_space Œ±]
  [conditionally_complete_linear_order Œ±] [order_topology Œ±] {f : Œπ ‚Üí Œ±} (h_mono : monotone f) :
  tendsto f at_top at_top ‚à® (‚àÉ l, tendsto f at_top (ùìù l)) :=
if H : bdd_above (range f) then or.inr ‚ü®_, tendsto_at_top_csupr h_mono H‚ü©
else or.inl $ tendsto_at_top_at_top_of_monotone' h_mono H

lemma tendsto_iff_tendsto_subseq_of_monotone {Œπ‚ÇÅ Œπ‚ÇÇ Œ± : Type*} [semilattice_sup Œπ‚ÇÅ] [preorder Œπ‚ÇÇ]
  [nonempty Œπ‚ÇÅ] [topological_space Œ±] [conditionally_complete_linear_order Œ±] [order_topology Œ±]
  [no_top_order Œ±] {f : Œπ‚ÇÇ ‚Üí Œ±} {œÜ : Œπ‚ÇÅ ‚Üí Œπ‚ÇÇ} {l : Œ±} (hf : monotone f)
  (hg : tendsto œÜ at_top at_top) :
  tendsto f at_top (ùìù l) ‚Üî tendsto (f ‚àò œÜ) at_top (ùìù l) :=
begin
  split; intro h,
  { exact h.comp hg },
  { rcases tendsto_of_monotone hf with h' | ‚ü®l', hl'‚ü©,
    { exact (not_tendsto_at_top_of_tendsto_nhds h (h'.comp hg)).elim },
    { rwa tendsto_nhds_unique h (hl'.comp hg) } }
end

/-! The next family of results, such as `is_lub_of_tendsto_at_top` and `supr_eq_of_tendsto`, are
converses to the standard fact that bounded monotone functions converge. They state, that if a
monotone function `f` tends to `a` along `filter.at_top`, then that value `a` is a least upper bound
for the range of `f`.

Related theorems above (`is_lub.is_lub_of_tendsto`, `is_glb.is_glb_of_tendsto` etc) cover the case
when `f x` tends to `a` as `x` tends to some point `b` in the domain. -/

lemma monotone.ge_of_tendsto [topological_space Œ±] [preorder Œ±] [order_closed_topology Œ±]
  [semilattice_sup Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : monotone f)
  (ha : tendsto f at_top (ùìù a)) (b : Œ≤) :
  f b ‚â§ a :=
begin
  haveI : nonempty Œ≤ := nonempty.intro b,
  exact ge_of_tendsto ha ((eventually_ge_at_top b).mono (Œª _ hxy, hf hxy))
end

lemma monotone.le_of_tendsto [topological_space Œ±] [preorder Œ±] [order_closed_topology Œ±]
  [semilattice_inf Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : monotone f)
  (ha : tendsto f at_bot (ùìù a)) (b : Œ≤) :
  a ‚â§ f b :=
@monotone.ge_of_tendsto (order_dual Œ±) (order_dual Œ≤) _ _ _ _ f _ hf.dual ha b

lemma is_lub_of_tendsto_at_top [topological_space Œ±] [preorder Œ±] [order_closed_topology Œ±]
  [nonempty Œ≤] [semilattice_sup Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : monotone f)
  (ha : tendsto f at_top (ùìù a)) :
  is_lub (set.range f) a :=
begin
  split,
  { rintros _ ‚ü®b, rfl‚ü©,
    exact hf.ge_of_tendsto ha b },
  { exact Œª _ hb, le_of_tendsto' ha (Œª x, hb (set.mem_range_self x)) }
end

lemma is_glb_of_tendsto_at_bot [topological_space Œ±] [preorder Œ±] [order_closed_topology Œ±]
  [nonempty Œ≤] [semilattice_inf Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : monotone f)
  (ha : tendsto f at_bot (ùìù a)) :
  is_glb (set.range f) a :=
@is_lub_of_tendsto_at_top (order_dual Œ±) (order_dual Œ≤) _ _ _ _ _ _ _ hf.dual ha

lemma is_lub_of_tendsto_at_bot [topological_space Œ±] [preorder Œ±] [order_closed_topology Œ±]
  [nonempty Œ≤] [semilattice_inf Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : antitone f)
  (ha : tendsto f at_bot (ùìù a)) :
  is_lub (set.range f) a :=
@is_lub_of_tendsto_at_top Œ± (order_dual Œ≤)  _ _ _ _ _ _ _ hf.dual_left ha

lemma is_glb_of_tendsto_at_top [topological_space Œ±] [preorder Œ±] [order_closed_topology Œ±]
  [nonempty Œ≤] [semilattice_sup Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : antitone f)
  (ha : tendsto f at_top (ùìù a)) :
  is_glb (set.range f) a :=
@is_glb_of_tendsto_at_bot Œ± (order_dual Œ≤)  _ _ _ _ _ _ _ hf.dual_left ha

lemma supr_eq_of_tendsto {Œ± Œ≤} [topological_space Œ±] [complete_linear_order Œ±] [order_topology Œ±]
  [nonempty Œ≤] [semilattice_sup Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : monotone f) :
  tendsto f at_top (ùìù a) ‚Üí supr f = a :=
tendsto_nhds_unique (tendsto_at_top_supr hf)

lemma infi_eq_of_tendsto {Œ±} [topological_space Œ±] [complete_linear_order Œ±] [order_topology Œ±]
  [nonempty Œ≤] [semilattice_sup Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : antitone f) :
  tendsto f at_top (ùìù a) ‚Üí infi f = a :=
tendsto_nhds_unique (tendsto_at_top_infi hf)

lemma supr_eq_supr_subseq_of_monotone {Œπ‚ÇÅ Œπ‚ÇÇ Œ± : Type*} [preorder Œπ‚ÇÇ] [complete_lattice Œ±]
  {l : filter Œπ‚ÇÅ} [l.ne_bot] {f : Œπ‚ÇÇ ‚Üí Œ±} {œÜ : Œπ‚ÇÅ ‚Üí Œπ‚ÇÇ} (hf : monotone f)
  (hœÜ : tendsto œÜ l at_top) :
  (‚®Ü i, f i) = (‚®Ü i, f (œÜ i)) :=
le_antisymm
  (supr_le_supr2 $ Œª i, exists_imp_exists (Œª j (hj : i ‚â§ œÜ j), hf hj)
    (hœÜ.eventually $ eventually_ge_at_top i).exists)
  (supr_le_supr2 $ Œª i, ‚ü®œÜ i, le_refl _‚ü©)

lemma infi_eq_infi_subseq_of_monotone {Œπ‚ÇÅ Œπ‚ÇÇ Œ± : Type*} [preorder Œπ‚ÇÇ] [complete_lattice Œ±]
  {l : filter Œπ‚ÇÅ} [l.ne_bot] {f : Œπ‚ÇÇ ‚Üí Œ±} {œÜ : Œπ‚ÇÅ ‚Üí Œπ‚ÇÇ} (hf : monotone f)
  (hœÜ : tendsto œÜ l at_bot) :
  (‚®Ö i, f i) = (‚®Ö i, f (œÜ i)) :=
supr_eq_supr_subseq_of_monotone hf.dual hœÜ
