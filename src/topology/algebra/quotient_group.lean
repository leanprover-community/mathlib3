/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/

import topology.algebra.uniform_group

/-!
# Quotient group of a metrizable topological group is complete

A standard fact in analysis is that the quotient of a Banach space by a closed subspace is a
Banach space. In this file, we provide a proof of the completeness portion of this theorem; the
linear and norm structure appears elsewhere in mathlib.

The proof that appears most frequently in textbooks proceeds as follows: suppose `X` is a Banach
space and `S` is a closed subspace, and let `x : â„• â†’ X â§¸ S` be a sequence such that the series
`âˆ‘ âˆ¥x nâˆ¥ < âˆž`. For each `n : â„•` lift `x n` to some `x' n : X` so that `âˆ¥x' nâˆ¥ â‰¤ âˆ¥x nâˆ¥ + 2 ^ (-n)`.
Then `âˆ‘ âˆ¥x' nâˆ¥ â‰¤ (âˆ‘ âˆ¥x nâˆ¥) + 1 < âˆž` is an absolutely convergent series, and since `X` is complete,
the series `âˆ‘ (x' n)` converges. Then the series `âˆ‘ (x n)` converges in `X â§¸ S`. Therefore, every
absolutely convergent series in `X â§¸ S` converges, and hence `X â§¸ S` is a Banach space.

Because of its appeal to the equivalent condition for completeness in normed spaces in terms of
absolutely convergent series, the proof outlined above is not suitable for the more general setting
of topological groups, or more precisely, `uniform_group`s. It turns out that it is *not* always
the case that the quotient of a complete uniform group by a subgroup (even a closed subgroup) is
complete. However, when the prequotient is first countable, which in this context is equivalent
to metrizability, then the quotient is complete (see `quotient_group.complete_space`).

## Main statements

* `quotient_group.complete_space`
* `quotient_add_group.complete_space`

## References

* [N. Bourbaki, *General Topology*][bourbaki1966b]

## Tags

quotient group, complete space
-/

open topological_space filter set classical
open_locale topological_space uniformity pointwise

universes u v

/-- The map of a countably generated filter is countably generated -/
lemma filter.map.is_countably_generated {Î± Î² : Type*} (l : filter Î±) [l.is_countably_generated]
  (f : Î± â†’ Î²) : (map f l).is_countably_generated :=
begin
  rcases l.exists_antitone_basis with âŸ¨u, huâŸ©,
  exact has_countable_basis.is_countably_generated âŸ¨hu.map.to_has_basis, set.to_countable _âŸ©,
end

variables (G : Type u) [group G] [topological_space G] [topological_group G]

/-- Neighborhoods in the quotient are precisely the map of neighborhoods in the prequotient. -/
@[to_additive "Neighborhoods in the quotient are precisely the map of neighborhoods in
the prequotient."]
lemma quotient_group.nhds_eq
  (N : subgroup G) (x : G) : ð“ (x : G â§¸ N) = map coe (ð“ x) :=
le_antisymm ((quotient_group.is_open_map_coe N).nhds_le x) continuous_quot_mk.continuous_at

variables [first_countable_topology G] (N : subgroup G) [subgroup.normal N]

/-- In a first countable topological group `G` with normal subgroup `N`, `1 : G â§¸ N` has a
countable neighborhood basis. -/
@[to_additive "In a first countable topological additive group `G` with normal additive subgroup
`N`, `0 : G â§¸ N` has a countable neighborhood basis."]
instance quotient_group.nhds_one_is_countably_generated : (ð“ (1 : G â§¸ N)).is_countably_generated :=
(quotient_group.nhds_eq G N 1).symm â–¸ filter.map.is_countably_generated _ _

/-- In a first countable topological group `G` with normal subgroup `N`, `ð“¤ (G â§¸ N)` is countably
generated. -/
@[to_additive "In a first countable topological additive group `G` with normal additive subgroup
`N`, `ð“¤ (G â§¸ N)` is countably generated."]
instance quotient_group.uniformity_is_countably_generated :
  (@uniformity (G â§¸ N) (topological_group.to_uniform_space (G â§¸ N))).is_countably_generated :=
comap.is_countably_generated _ _

/-- Any first countable topological group has an antitone neighborhood basis `u : â„• â†’ set G` for
which `(u (n + 1)) ^ 2 âŠ† u n`. The existence of such a neighborhood basis is a key tool for
`quotient_group.complete_space` -/
@[to_additive "Any first countable topological additive group has an antitone neighborhood basis
`u : â„• â†’ set G` for which `u (n + 1) + u (n + 1) âŠ† u n`. The existence of such a neighborhood basis
is a key tool for `quotient_add_group.complete_space`"]
lemma topological_group.exists_antitone_basis_nhds_one :
  âˆƒ (u : â„• â†’ set G), (ð“ 1).has_antitone_basis u âˆ§ (âˆ€ n, u (n + 1) * u (n + 1) âŠ† u n) :=
begin
  rcases (ð“ (1 : G)).exists_antitone_basis with âŸ¨u, hu, u_antiâŸ©,
  have := ((hu.prod_nhds hu).tendsto_iff hu).mp
    (by simpa only [mul_one] using continuous_mul.tendsto ((1, 1) : G Ã— G)),
  simp only [and_self, mem_prod, and_imp, prod.forall, exists_true_left, prod.exists,
    forall_true_left] at this,
  have exists_mul : âˆ€ n : â„•, âˆƒ m, n < m âˆ§ u m * u m âŠ† u n,
  { intros n,
    rcases this n with âŸ¨j, k, hâŸ©,
    refine âŸ¨max n (max j k) + 1, (le_max_left _ _).trans_lt (lt_add_one _), _âŸ©,
    have h' : u j * u k âŠ† u n, { rintro - âŸ¨a, b, ha, hb, rflâŸ©, exact h a b ha hb, },
    refine (set.mul_subset_mul (u_anti _) (u_anti _)).trans h',
    exact (((le_max_left j k).trans $ le_max_right n (max j k)).trans $ (lt_add_one _).le),
    exact (((le_max_right j k).trans $ le_max_right n (max j k)).trans $ (lt_add_one _).le) },
  set y : â„• â†’ â„• := Î» (n : â„•), nat.rec_on n 0 (Î» k yk, (some (exists_mul yk))),
  have hy : âˆ€ n : â„•, y n < y (n + 1) âˆ§ u (y (n + 1)) * u (y (n + 1)) âŠ† u (y n),
    from Î» n, some_spec (exists_mul $ y n),
  have y_mono : strict_mono y := strict_mono_nat_of_lt_succ (Î» n, (hy n).1),
  exact âŸ¨u âˆ˜ y, (has_antitone_basis.comp_mono âŸ¨hu, u_antiâŸ©) y_mono.monotone y_mono.tendsto_at_top,
    Î» n, (hy n).2âŸ©,
end

/-- The quotient `G â§¸ N` of a complete first countable topological group `G` by a normal subgroup
is itself complete. -/
@[to_additive "The quotient `G â§¸ N` of a complete first countable topological additive group
`G` by a normal additive subgroup is itself complete. Consequently, quotients of Banach spaces by
subspaces are complete."]
instance quotient_group.complete_space (G : Type u) [group G] [topological_space G]
  [topological_group G] [first_countable_topology G] (N : subgroup G) [N.normal]
  [@complete_space G (topological_group.to_uniform_space G)] :
  @complete_space (G â§¸ N) (topological_group.to_uniform_space (G â§¸ N)) :=
begin
  /- Since `G â§¸ N` is a topological group it is a uniform space, and since `G` is first countable
  the uniformities of both `G` and `G â§¸ N` are countably generated. Moreover, we may choose a
  sequential antitone neighborhood basis `u` for `ð“ (1 : G)` so that `(u (n + 1)) ^ 2 âŠ† u n`, and
  this descends to an antitone neighborhood basis `v` for `ð“ (1 : G â§¸ N)`. Since `ð“¤ (G â§¸ N)` is
  countably generated, it suffices to show any Cauchy sequence `x` converges. -/
  letI : uniform_space (G â§¸ N) := topological_group.to_uniform_space (G â§¸ N),
  letI : uniform_space G := topological_group.to_uniform_space G,
  haveI : (ð“¤ (G â§¸ N)).is_countably_generated := comap.is_countably_generated _ _,
  obtain âŸ¨u, hu, u_mulâŸ© := topological_group.exists_antitone_basis_nhds_one G,
  obtain âŸ¨hv, v_antiâŸ© := @has_antitone_basis.map _ _ _ _ _ _ (coe : G â†’ G â§¸ N) hu,
  rw [â†quotient_group.nhds_eq G N 1, quotient_group.coe_one] at hv,
  refine uniform_space.complete_of_cauchy_seq_tendsto (Î» x hx, _),
  /- Given `n : â„•`, for sufficiently large `a b : â„•`, given any lift of `x b`, we can find a lift
  of `x a` such that the quotient of the lifts lies in `u n`. -/
  have keyâ‚€ : âˆ€ i j : â„•, âˆƒ M : â„•,
    j < M âˆ§ âˆ€ a b : â„•, M â‰¤ a â†’ M â‰¤ b â†’ âˆ€ g : G, x b = g â†’ âˆƒ g' : G, g / g' âˆˆ u i âˆ§ x a = g',
  { have hð“¤GN : (ð“¤ (G â§¸ N)).has_basis (Î» _, true) (Î» i, {x | x.snd / x.fst âˆˆ coe '' u i}),
    { simpa [uniformity_eq_comap_nhds_one'] using hv.comap _ },
    simp only [hð“¤GN.cauchy_seq_iff, ge_iff_le, mem_set_of_eq, forall_true_left, mem_image] at hx,
    intros i j,
    rcases hx i with âŸ¨M, hMâŸ©,
    refine âŸ¨max j M + 1, (le_max_left _ _).trans_lt (lt_add_one _), Î» a b ha hb g hg, _âŸ©,
    obtain âŸ¨y, y_mem, hyâŸ© := hM a (((le_max_right j _).trans (lt_add_one _).le).trans ha) b
      (((le_max_right j _).trans (lt_add_one _).le).trans hb),
    refine âŸ¨yâ»Â¹ * g,
      by simpa only [div_eq_mul_inv, mul_inv_rev, inv_inv, mul_inv_cancel_left] using y_mem, _âŸ©,
    rw [quotient_group.coe_mul, quotient_group.coe_inv, hy, hg, inv_div, div_mul_cancel'], },
  /- Inductively construct a subsequence `Ï† : â„• â†’ â„•` using `keyâ‚€` so that if `a b : â„•` exceed
  `Ï† (n + 1)`, then we may find lifts whose quotients lie within `u n`. -/
  set Ï† : â„• â†’ â„• := Î» n, nat.rec_on n (some $ keyâ‚€ 0 0) (Î» k yk, some $ keyâ‚€ (k + 1) yk),
  have hÏ† : âˆ€ n : â„•, Ï† n < Ï† (n + 1) âˆ§ âˆ€ a b : â„•, Ï† (n + 1) â‰¤ a â†’ Ï† (n + 1) â‰¤ b â†’
    (âˆ€ g : G, x b = g â†’ âˆƒ g' : G, g / g' âˆˆ u (n + 1) âˆ§ x a = g'),
    from Î» n, some_spec (keyâ‚€ (n + 1) (Ï† n)),
  /- Inductively construct a sequence `x' n : G` of lifts of `x (Ï† (n + 1))` such that quotients of
  successive terms lie in `x' n / x' (n + 1) âˆˆ u (n + 1)`. We actually need the proofs that each
  term is a lift to construct the next term, so we use a Î£-type. -/
  set x' : Î  n, psigma (Î» g : G, x (Ï† (n + 1)) = g) :=
    Î» n, nat.rec_on n
      âŸ¨some (quotient_group.mk_surjective (x (Ï† 1))),
       (some_spec (quotient_group.mk_surjective (x (Ï† 1)))).symmâŸ©
      (Î» k hk, âŸ¨some $ (hÏ† k).2 _ _ (hÏ† (k + 1)).1.le le_rfl hk.fst hk.snd,
          (some_spec $ (hÏ† k).2 _ _ (hÏ† (k + 1)).1.le le_rfl hk.fst hk.snd).2âŸ©),
  have hx' : âˆ€ n : â„•, (x' n).fst / (x' (n + 1)).fst âˆˆ u (n + 1) :=
    Î» n, (some_spec $ (hÏ† n).2 _ _ (hÏ† (n + 1)).1.le le_rfl (x' n).fst (x' n).snd).1,
  /- The sequence `x'` is Cauchy. This is where we exploit the condition on `u`. The key idea
  is to show by decreasing induction that `x' m / x' n âˆˆ u m` if `m â‰¤ n`. -/
  have x'_cauchy : cauchy_seq (Î» n, (x' n).fst),
  { have hð“¤G : (ð“¤ G).has_basis (Î» _, true) (Î» i, {x | x.snd / x.fst âˆˆ u i}),
    { simpa [uniformity_eq_comap_nhds_one'] using hu.to_has_basis.comap _ },
    simp only [hð“¤G.cauchy_seq_iff', ge_iff_le, mem_set_of_eq, forall_true_left],
    exact Î» m, âŸ¨m, Î» n hmn, nat.decreasing_induction'
      (Î» k hkn hkm hk, u_mul k âŸ¨_, _, hx' k, hk, div_mul_div_cancel' _ _ _âŸ©)
      hmn (by simpa only [div_self'] using mem_of_mem_nhds (hu.mem _))âŸ© },
  /- Since `G` is complete, `x'` converges to some `xâ‚€`, and so the image of this sequence under
  the quotient map converges to `â†‘xâ‚€`. The image of `x'` is a convergent subsequence of `x`, and
  since `x` is Cauchy, this implies it converges. -/
  rcases cauchy_seq_tendsto_of_complete x'_cauchy with âŸ¨xâ‚€, hxâ‚€âŸ©,
  refine âŸ¨â†‘xâ‚€, tendsto_nhds_of_cauchy_seq_of_subseq hx
    (strict_mono_nat_of_lt_succ $ Î» n, (hÏ† (n + 1)).1).tendsto_at_top _âŸ©,
  convert ((continuous_coinduced_rng : continuous (coe : G â†’ G â§¸ N)).tendsto xâ‚€).comp hxâ‚€,
  exact funext (Î» n, (x' n).snd),
end
