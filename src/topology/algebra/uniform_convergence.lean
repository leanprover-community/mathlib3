/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import topology.uniform_space.uniform_convergence_topology
import analysis.locally_convex.bounded
import topology.algebra.filter_basis

/-!
# Algebraic facts about the topology of uniform convergence

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains algebraic compatibility results about the uniform structure of uniform
convergence / `ğ”–`-convergence. They will mostly be useful for defining strong topologies on the
space of continuous linear maps between two topological vector spaces.

## Main statements

* `uniform_fun.uniform_group` : if `G` is a uniform group, then `Î± â†’áµ¤ G` a uniform group
* `uniform_on_fun.uniform_group` : if `G` is a uniform group, then for any `ğ”– : set (set Î±)`,
  `Î± â†’áµ¤[ğ”–] G` a uniform group.
* `uniform_on_fun.has_continuous_smul_of_image_bounded` : let `E` be a TVS, `ğ”– : set (set Î±)` and
  `H` a submodule of `Î± â†’áµ¤[ğ”–] E`. If the image of any `S âˆˆ ğ”–` by any `u âˆˆ H` is bounded (in the
  sense of `bornology.is_vonN_bounded`), then `H`, equipped with the topology induced from
  `Î± â†’áµ¤[ğ”–] E`, is a TVS.

## Implementation notes

Like in `topology/uniform_space/uniform_convergence_topology`, we use the type aliases
`uniform_fun` (denoted `Î± â†’áµ¤ Î²`) and `uniform_on_fun` (denoted `Î± â†’áµ¤[ğ”–] Î²`) for functions from `Î±`
to `Î²` endowed with the structures of uniform convergence and `ğ”–`-convergence.

## TODO

* `uniform_on_fun.has_continuous_smul_of_image_bounded` unnecessarily asks for `ğ”–` to be
  nonempty and directed. This will be easy to solve once we know that replacing `ğ”–` by its
  ***noncovering*** bornology (i.e ***not*** what `bornology` currently refers to in mathlib)
  doesn't change the topology.

## References

* [N. Bourbaki, *General Topology, Chapter X*][bourbaki1966]
* [N. Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

uniform convergence, strong dual

-/

open filter
open_locale topology pointwise uniform_convergence

section algebraic_instances

variables {Î± Î² Î¹ R : Type*} {ğ”– : set $ set Î±}

@[to_additive] instance [monoid Î²] : monoid (Î± â†’áµ¤ Î²) := pi.monoid
@[to_additive] instance [monoid Î²] : monoid (Î± â†’áµ¤[ğ”–] Î²) := pi.monoid

@[to_additive] instance [comm_monoid Î²] : comm_monoid (Î± â†’áµ¤ Î²) := pi.comm_monoid
@[to_additive] instance [comm_monoid Î²] : comm_monoid (Î± â†’áµ¤[ğ”–] Î²) := pi.comm_monoid

@[to_additive] instance [group Î²] : group (Î± â†’áµ¤ Î²) := pi.group
@[to_additive] instance [group Î²] : group (Î± â†’áµ¤[ğ”–] Î²) := pi.group

@[to_additive] instance [comm_group Î²] : comm_group (Î± â†’áµ¤ Î²) := pi.comm_group
@[to_additive] instance [comm_group Î²] : comm_group (Î± â†’áµ¤[ğ”–] Î²) := pi.comm_group

instance [semiring R] [add_comm_monoid Î²] [module R Î²] : module R (Î± â†’áµ¤ Î²) := pi.module _ _ _
instance [semiring R] [add_comm_monoid Î²] [module R Î²] : module R (Î± â†’áµ¤[ğ”–] Î²) := pi.module _ _ _

end algebraic_instances

section group

variables {Î± G Î¹ : Type*} [group G] {ğ”– : set $ set Î±} [uniform_space G] [uniform_group G]

/-- If `G` is a uniform group, then `Î± â†’áµ¤ G` is a uniform group as well. -/
@[to_additive "If `G` is a uniform additive group, then `Î± â†’áµ¤ G` is a uniform additive group
as well."]
instance : uniform_group (Î± â†’áµ¤ G) :=
-- Since `(/) : G Ã— G â†’ G` is uniformly continuous,
-- `uniform_fun.postcomp_uniform_continuous` tells us that
-- `((/) âˆ˜ â€”) : (Î± â†’áµ¤ G Ã— G) â†’ (Î± â†’áµ¤ G)` is uniformly continuous too. By precomposing with
-- `uniform_fun.uniform_equiv_prod_arrow`, this gives that
-- `(/) : (Î± â†’áµ¤ G) Ã— (Î± â†’áµ¤ G) â†’ (Î± â†’áµ¤ G)` is also uniformly continuous
âŸ¨(uniform_fun.postcomp_uniform_continuous uniform_continuous_div).comp
  uniform_fun.uniform_equiv_prod_arrow.symm.uniform_continuousâŸ©

@[to_additive]
protected lemma uniform_fun.has_basis_nhds_one_of_basis {p : Î¹ â†’ Prop}
  {b : Î¹ â†’ set G} (h : (ğ“ 1 : filter G).has_basis p b) :
  (ğ“ 1 : filter (Î± â†’áµ¤ G)).has_basis p
    (Î» i, {f : Î± â†’áµ¤ G | âˆ€ x, f x âˆˆ b i}) :=
begin
  have := h.comap (Î» p : G Ã— G, p.2 / p.1),
  rw â† uniformity_eq_comap_nhds_one at this,
  convert uniform_fun.has_basis_nhds_of_basis Î± _ 1 this,
  ext i f,
  simp [uniform_fun.gen]
end

@[to_additive]
protected lemma uniform_fun.has_basis_nhds_one :
  (ğ“ 1 : filter (Î± â†’áµ¤ G)).has_basis
    (Î» V : set G, V âˆˆ (ğ“ 1 : filter G))
    (Î» V, {f : Î± â†’ G | âˆ€ x, f x âˆˆ V}) :=
uniform_fun.has_basis_nhds_one_of_basis (basis_sets _)

/-- Let `ğ”– : set (set Î±)`. If `G` is a uniform group, then `Î± â†’áµ¤[ğ”–] G` is a uniform group as
well. -/
@[to_additive "Let `ğ”– : set (set Î±)`. If `G` is a uniform additive group, then `Î± â†’áµ¤[ğ”–] G` is a
uniform additive group as well. "]
instance : uniform_group (Î± â†’áµ¤[ğ”–] G) :=
-- Since `(/) : G Ã— G â†’ G` is uniformly continuous,
-- `uniform_on_fun.postcomp_uniform_continuous` tells us that
-- `((/) âˆ˜ â€”) : (Î± â†’áµ¤[ğ”–] G Ã— G) â†’ (Î± â†’áµ¤[ğ”–] G)` is uniformly continuous too. By precomposing with
-- `uniform_on_fun.uniform_equiv_prod_arrow`, this gives that
-- `(/) : (Î± â†’áµ¤[ğ”–] G) Ã— (Î± â†’áµ¤[ğ”–] G) â†’ (Î± â†’áµ¤[ğ”–] G)` is also uniformly continuous
âŸ¨(uniform_on_fun.postcomp_uniform_continuous uniform_continuous_div).comp
  uniform_on_fun.uniform_equiv_prod_arrow.symm.uniform_continuousâŸ©

@[to_additive]
protected lemma uniform_on_fun.has_basis_nhds_one_of_basis (ğ”– : set $ set Î±)
  (hğ”–â‚ : ğ”–.nonempty) (hğ”–â‚‚ : directed_on (âŠ†) ğ”–) {p : Î¹ â†’ Prop}
  {b : Î¹ â†’ set G} (h : (ğ“ 1 : filter G).has_basis p b) :
  (ğ“ 1 : filter (Î± â†’áµ¤[ğ”–] G)).has_basis
    (Î» Si : set Î± Ã— Î¹, Si.1 âˆˆ ğ”– âˆ§ p Si.2)
    (Î» Si, {f : Î± â†’áµ¤[ğ”–] G | âˆ€ x âˆˆ Si.1, f x âˆˆ b Si.2}) :=
begin
  have := h.comap (Î» p : G Ã— G, p.1 / p.2),
  rw â† uniformity_eq_comap_nhds_one_swapped at this,
  convert uniform_on_fun.has_basis_nhds_of_basis Î± _ ğ”– 1 hğ”–â‚ hğ”–â‚‚ this,
  ext i f,
  simp [uniform_on_fun.gen]
end

@[to_additive]
protected lemma uniform_on_fun.has_basis_nhds_one (ğ”– : set $ set Î±)
  (hğ”–â‚ : ğ”–.nonempty) (hğ”–â‚‚ : directed_on (âŠ†) ğ”–) :
  (ğ“ 1 : filter (Î± â†’áµ¤[ğ”–] G)).has_basis
    (Î» SV : set Î± Ã— set G, SV.1 âˆˆ ğ”– âˆ§ SV.2 âˆˆ (ğ“ 1 : filter G))
    (Î» SV, {f : Î± â†’áµ¤[ğ”–] G | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2}) :=
uniform_on_fun.has_basis_nhds_one_of_basis ğ”– hğ”–â‚ hğ”–â‚‚ (basis_sets _)

end group

section module

variables (ğ•œ Î± E H : Type*) {hom : Type*} [normed_field ğ•œ] [add_comm_group H] [module ğ•œ H]
  [add_comm_group E] [module ğ•œ E] [topological_space H] [uniform_space E] [uniform_add_group E]
  [has_continuous_smul ğ•œ E] {ğ”– : set $ set Î±} [linear_map_class hom ğ•œ H (Î± â†’áµ¤[ğ”–] E)]

/-- Let `E` be a TVS, `ğ”– : set (set Î±)` and `H` a submodule of `Î± â†’áµ¤[ğ”–] E`. If the image of any
`S âˆˆ ğ”–` by any `u âˆˆ H` is bounded (in the sense of `bornology.is_vonN_bounded`), then `H`,
equipped with the topology of `ğ”–`-convergence, is a TVS.

For convenience, we don't literally ask for `H : submodule (Î± â†’áµ¤[ğ”–] E)`. Instead, we prove the
result for any vector space `H` equipped with a linear inducing to `Î± â†’áµ¤[ğ”–] E`, which is often
easier to use. We also state the `submodule` version as
`uniform_on_fun.has_continuous_smul_submodule_of_image_bounded`. -/
lemma uniform_on_fun.has_continuous_smul_induced_of_image_bounded
  (hğ”–â‚ : ğ”–.nonempty) (hğ”–â‚‚ : directed_on (âŠ†) ğ”–)
  (Ï† : hom) (hÏ† : inducing Ï†)
  (h : âˆ€ u : H, âˆ€ s âˆˆ ğ”–, bornology.is_vonN_bounded ğ•œ ((Ï† u : Î± â†’ E) '' s)) :
  has_continuous_smul ğ•œ H :=
begin
  haveI : topological_add_group H,
  { rw hÏ†.induced,
    exact topological_add_group_induced Ï† },
  have : (ğ“ 0 : filter H).has_basis _ _,
  { rw [hÏ†.induced, nhds_induced, map_zero],
    exact ((uniform_on_fun.has_basis_nhds_zero ğ”– hğ”–â‚ hğ”–â‚‚).comap Ï†) },
  refine has_continuous_smul.of_basis_zero this _ _ _,
  { rintros âŸ¨S, VâŸ© âŸ¨hS, hVâŸ©,
    have : tendsto (Î» kx : (ğ•œ Ã— E), kx.1 â€¢ kx.2) (ğ“ (0, 0)) (ğ“ $ (0 : ğ•œ) â€¢ 0) :=
      continuous_smul.tendsto (0 : ğ•œ Ã— E),
    rw [zero_smul, nhds_prod_eq] at this,
    have := this hV,
    rw [mem_map, mem_prod_iff] at this,
    rcases this with âŸ¨U, hU, W, hW, hUWâŸ©,
    refine âŸ¨U, hU, âŸ¨S, WâŸ©, âŸ¨hS, hWâŸ©, _âŸ©,
    rw set.smul_subset_iff,
    intros a ha u hu x hx,
    rw smul_hom_class.map_smul,
    exact hUW (âŸ¨ha, hu x hxâŸ© : (a, Ï† u x) âˆˆ U Ã—Ë¢ W) },
  { rintros a âŸ¨S, VâŸ© âŸ¨hS, hVâŸ©,
    have : tendsto (Î» x : E, a â€¢ x) (ğ“ 0) (ğ“ $ a â€¢ 0) := tendsto_id.const_smul a,
    rw [smul_zero] at this,
    refine âŸ¨âŸ¨S, ((â€¢) a) â»Â¹' VâŸ©, âŸ¨hS, this hVâŸ©, Î» f hf x hx, _âŸ©,
    rw [smul_hom_class.map_smul],
    exact hf x hx },
  { rintros u âŸ¨S, VâŸ© âŸ¨hS, hVâŸ©,
    rcases h u S hS hV with âŸ¨r, hrpos, hrâŸ©,
    rw metric.eventually_nhds_iff_ball,
    refine âŸ¨râ»Â¹, inv_pos.mpr hrpos, Î» a ha x hx, _âŸ©,
    by_cases ha0 : a = 0,
    { rw ha0,
      simp [mem_of_mem_nhds hV] },
    { rw mem_ball_zero_iff at ha,
      rw [smul_hom_class.map_smul, pi.smul_apply],
      have : Ï† u x âˆˆ aâ»Â¹ â€¢ V,
      { have ha0 : 0<â€–aâ€– := norm_pos_iff.mpr ha0,
        refine (hr aâ»Â¹ _) (set.mem_image_of_mem (Ï† u) hx),
        rw [norm_inv, le_inv hrpos ha0],
        exact ha.le },
      rwa set.mem_inv_smul_set_iffâ‚€ ha0 at this } }
end

/-- Let `E` be a TVS, `ğ”– : set (set Î±)` and `H` a submodule of `Î± â†’áµ¤[ğ”–] E`. If the image of any
`S âˆˆ ğ”–` by any `u âˆˆ H` is bounded (in the sense of `bornology.is_vonN_bounded`), then `H`,
equipped with the topology of `ğ”–`-convergence, is a TVS.

If you have a hard time using this lemma, try the one above instead. -/
lemma uniform_on_fun.has_continuous_smul_submodule_of_image_bounded
  (hğ”–â‚ : ğ”–.nonempty) (hğ”–â‚‚ : directed_on (âŠ†) ğ”–) (H : submodule ğ•œ (Î± â†’áµ¤[ğ”–] E))
  (h : âˆ€ u âˆˆ H, âˆ€ s âˆˆ ğ”–, bornology.is_vonN_bounded ğ•œ (u '' s)) :
  @has_continuous_smul ğ•œ H _ _
  ((uniform_on_fun.topological_space Î± E ğ”–).induced (coe : H â†’ Î± â†’áµ¤[ğ”–] E)) :=
begin
  haveI : topological_add_group H := topological_add_group_induced
    (linear_map.id.dom_restrict H : H â†’â‚—[ğ•œ] Î± â†’ E),
  exact uniform_on_fun.has_continuous_smul_induced_of_image_bounded ğ•œ Î± E H hğ”–â‚ hğ”–â‚‚
    (linear_map.id.dom_restrict H : H â†’â‚—[ğ•œ] Î± â†’ E) inducing_coe (Î» âŸ¨u, huâŸ©, h u hu)
end

end module
