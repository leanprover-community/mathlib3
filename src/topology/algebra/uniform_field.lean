/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import topology.algebra.uniform_ring
import topology.algebra.field
import field_theory.subfield

/-!
# Completion of topological fields

The goal of this file is to prove the main part of Proposition 7 of Bourbaki GT III 6.8 :

The completion `hat K` of a Hausdorff topological field is a field if the image under
the mapping `x ‚Ü¶ x‚Åª¬π` of every Cauchy filter (with respect to the additive uniform structure)
which does not have a cluster point at `0` is a Cauchy filter
(with respect to the additive uniform structure).

Bourbaki does not give any detail here, he refers to the general discussion of extending
functions defined on a dense subset with values in a complete Hausdorff space. In particular
the subtlety about clustering at zero is totally left to readers.

Note that the separated completion of a non-separated topological field is the zero ring, hence
the separation assumption is needed. Indeed the kernel of the completion map is the closure of
zero which is an ideal. Hence it's either zero (and the field is separated) or the full field,
which implies one is sent to zero and the completion ring is trivial.

The main definition is `completable_top_field` which packages the assumptions as a Prop-valued
type class and the main results are the instances `uniform_space.completion.field` and
`uniform_space.completion.topological_division_ring`.
-/


noncomputable theory

open_locale classical uniformity topology

open set uniform_space uniform_space.completion filter

variables (K : Type*) [field K]  [uniform_space K]

local notation `hat` := completion

/--
A topological field is completable if it is separated and the image under
the mapping x ‚Ü¶ x‚Åª¬π of every Cauchy filter (with respect to the additive uniform structure)
which does not have a cluster point at 0 is a Cauchy filter
(with respect to the additive uniform structure). This ensures the completion is
a field.
-/
class completable_top_field extends separated_space K : Prop :=
(nice : ‚àÄ F : filter K, cauchy F ‚Üí ùìù 0 ‚äì F = ‚ä• ‚Üí cauchy (map (Œª x, x‚Åª¬π) F))

namespace uniform_space
namespace completion

@[priority 100]
instance [separated_space K] : nontrivial (hat K) :=
‚ü®‚ü®0, 1, Œª h, zero_ne_one $ (uniform_embedding_coe K).inj h‚ü©‚ü©

variables {K}

/-- extension of inversion to the completion of a field. -/
def hat_inv : hat K ‚Üí hat K := dense_inducing_coe.extend (Œª x : K, (coe x‚Åª¬π : hat K))

lemma continuous_hat_inv [completable_top_field K] {x : hat K} (h : x ‚â† 0) :
  continuous_at hat_inv x :=
begin
  haveI : t3_space (hat K) := completion.t3_space K,
  refine dense_inducing_coe.continuous_at_extend _,
  apply mem_of_superset (compl_singleton_mem_nhds h),
  intros y y_ne,
  rw mem_compl_singleton_iff at y_ne,
  apply complete_space.complete,
  rw ‚Üê filter.map_map,
  apply cauchy.map _ (completion.uniform_continuous_coe K),
  apply completable_top_field.nice,
  { haveI := dense_inducing_coe.comap_nhds_ne_bot y,
    apply cauchy_nhds.comap,
    { rw completion.comap_coe_eq_uniformity,
      exact le_rfl } },
  { have eq_bot : ùìù (0 : hat K) ‚äì ùìù y = ‚ä•,
    { by_contradiction h,
      exact y_ne (eq_of_nhds_ne_bot $ ne_bot_iff.mpr h).symm },
    erw [dense_inducing_coe.nhds_eq_comap (0 : K), ‚Üê filter.comap_inf, eq_bot],
    exact comap_bot },
end

/-
The value of `hat_inv` at zero is not really specified, although it's probably zero.
Here we explicitly enforce the `inv_zero` axiom.
-/
instance : has_inv (hat K) := ‚ü®Œª x, if x = 0 then 0 else hat_inv x‚ü©

variables [topological_division_ring K]

lemma hat_inv_extends {x : K} (h : x ‚â† 0) : hat_inv (x : hat K) = coe (x‚Åª¬π : K) :=
dense_inducing_coe.extend_eq_at
    ((continuous_coe K).continuous_at.comp (continuous_at_inv‚ÇÄ h))

variables [completable_top_field K]

@[norm_cast]
lemma coe_inv (x : K) : (x : hat K)‚Åª¬π = ((x‚Åª¬π : K) : hat K) :=
begin
  by_cases h : x = 0,
  { rw [h, inv_zero],
    dsimp [has_inv.inv],
    norm_cast,
    simp },
  { conv_lhs { dsimp [has_inv.inv] },
    rw if_neg,
    { exact hat_inv_extends h },
    { exact Œª H, h (dense_embedding_coe.inj H) } }
end

variables [uniform_add_group K]

lemma mul_hat_inv_cancel {x : hat K} (x_ne : x ‚â† 0) : x*hat_inv x = 1 :=
begin
  haveI : t1_space (hat K) := t2_space.t1_space,
  let f := Œª x : hat K, x*hat_inv x,
  let c := (coe : K ‚Üí hat K),
  change f x = 1,
  have cont : continuous_at f x,
  { letI : topological_space (hat K √ó hat K) := prod.topological_space,
    have : continuous_at (Œª y : hat K, ((y, hat_inv y) : hat K √ó hat K)) x,
      from continuous_id.continuous_at.prod (continuous_hat_inv x_ne),
    exact (_root_.continuous_mul.continuous_at.comp this : _) },
  have clo : x ‚àà closure (c '' {0}·∂ú),
  { have := dense_inducing_coe.dense x,
    rw [‚Üê image_univ, show (univ : set K) = {0} ‚à™ {0}·∂ú,
                      from (union_compl_self _).symm, image_union] at this,
    apply mem_closure_of_mem_closure_union this,
    rw image_singleton,
    exact compl_singleton_mem_nhds x_ne },
  have fxclo : f x ‚àà closure (f '' (c '' {0}·∂ú)) := mem_closure_image cont clo,
  have : f '' (c '' {0}·∂ú) ‚äÜ {1},
  { rw image_image,
    rintros _ ‚ü®z, z_ne, rfl‚ü©,
    rw mem_singleton_iff,
    rw mem_compl_singleton_iff at z_ne,
    dsimp [c, f],
    rw hat_inv_extends z_ne,
    norm_cast,
    rw mul_inv_cancel z_ne, },
  replace fxclo := closure_mono this fxclo,
  rwa [closure_singleton, mem_singleton_iff] at fxclo
end

instance : field (hat K) :=
{ exists_pair_ne := ‚ü®0, 1, Œª h, zero_ne_one ((uniform_embedding_coe K).inj h)‚ü©,
  mul_inv_cancel := Œª x x_ne, by { dsimp [has_inv.inv],
                                   simp [if_neg x_ne, mul_hat_inv_cancel x_ne], },
  inv_zero := show ((0 : K) : hat K)‚Åª¬π = ((0 : K) : hat K), by rw [coe_inv, inv_zero],
  ..completion.has_inv,
  ..(by apply_instance : comm_ring (hat K)) }

instance : topological_division_ring (hat K) :=
{ continuous_at_inv‚ÇÄ := begin
    intros x x_ne,
    have : {y | hat_inv y = y‚Åª¬π } ‚àà ùìù x,
    { have : {(0 : hat K)}·∂ú ‚äÜ {y : hat K | hat_inv y = y‚Åª¬π },
      { intros y y_ne,
        rw mem_compl_singleton_iff at y_ne,
        dsimp [has_inv.inv],
        rw if_neg y_ne },
      exact mem_of_superset (compl_singleton_mem_nhds x_ne) this },
    exact continuous_at.congr (continuous_hat_inv x_ne) this
  end,
  ..completion.top_ring_compl }

end completion
end uniform_space

variables (L : Type*) [field L]  [uniform_space L] [completable_top_field L]

instance subfield.completable_top_field (K : subfield L) : completable_top_field K :=
{ nice := begin
    intros F F_cau inf_F,
    let i : K ‚Üí+* L := K.subtype,
    have hi : uniform_inducing i, from uniform_embedding_subtype_coe.to_uniform_inducing,
    rw ‚Üê hi.cauchy_map_iff at F_cau ‚ä¢,
    rw [map_comm (show (i ‚àò Œª x, x‚Åª¬π) = (Œª x, x‚Åª¬π) ‚àò i, by {ext, refl})],
    apply completable_top_field.nice _ F_cau,
    rw [‚Üê filter.push_pull', ‚Üê map_zero i, ‚Üê hi.inducing.nhds_eq_comap, inf_F, filter.map_bot]
  end,
  ..subtype.separated_space (K : set L) }

@[priority 100]
instance completable_top_field_of_complete (L : Type*) [field L]
  [uniform_space L] [topological_division_ring L] [separated_space L] [complete_space L] :
  completable_top_field L :=
{ nice := Œª F cau_F hF, begin
    haveI : ne_bot F := cau_F.1,
    rcases complete_space.complete cau_F with ‚ü®x, hx‚ü©,
    have hx' : x ‚â† 0,
    { rintro rfl,
      rw inf_eq_right.mpr hx at hF,
      exact cau_F.1.ne hF },
    exact filter.tendsto.cauchy_map (calc map (Œª x, x‚Åª¬π) F ‚â§ map (Œª x, x‚Åª¬π) (ùìù x) : map_mono hx
                                                       ... ‚â§ ùìù (x‚Åª¬π) : continuous_at_inv‚ÇÄ hx')
  end,
  ..‚Äπseparated_space L‚Ä∫}
