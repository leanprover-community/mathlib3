/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl
-/
import topology.uniform_space.uniform_convergence
import topology.uniform_space.uniform_embedding
import topology.uniform_space.complete_separated
import topology.uniform_space.compact
import topology.algebra.group.basic
import tactic.abel

/-!
# Uniform structure on topological groups

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines uniform groups and its additive counterpart. These typeclasses should be
preferred over using `[topological_space Î±] [topological_group Î±]` since every topological
group naturally induces a uniform structure.

## Main declarations
* `uniform_group` and `uniform_add_group`: Multiplicative and additive uniform groups, that
  i.e., groups with uniformly continuous `(*)` and `(â»Â¹)` / `(+)` and `(-)`.

## Main results

* `topological_add_group.to_uniform_space` and `topological_add_comm_group_is_uniform` can be used
  to construct a canonical uniformity for a topological add group.

* extension of â„¤-bilinear maps to complete groups (useful for ring completions)

* `quotient_group.complete_space` and `quotient_add_group.complete_space` guarantee that quotients
  of first countable topological groups by normal subgroups are themselves complete. In particular,
  the quotient of a Banach space by a subspace is complete.
-/

noncomputable theory
open_locale classical uniformity topology filter pointwise

section uniform_group
open filter set

variables {Î± : Type*} {Î² : Type*}

/-- A uniform group is a group in which multiplication and inversion are uniformly continuous. -/
class uniform_group (Î± : Type*) [uniform_space Î±] [group Î±] : Prop :=
(uniform_continuous_div : uniform_continuous (Î»p:Î±Ã—Î±, p.1 / p.2))

/-- A uniform additive group is an additive group in which addition
  and negation are uniformly continuous.-/
class uniform_add_group (Î± : Type*) [uniform_space Î±] [add_group Î±] : Prop :=
(uniform_continuous_sub : uniform_continuous (Î»p:Î±Ã—Î±, p.1 - p.2))

attribute [to_additive] uniform_group

@[to_additive] theorem uniform_group.mk' {Î±} [uniform_space Î±] [group Î±]
  (hâ‚ : uniform_continuous (Î»p:Î±Ã—Î±, p.1 * p.2))
  (hâ‚‚ : uniform_continuous (Î»p:Î±, pâ»Â¹)) : uniform_group Î± :=
âŸ¨by simpa only [div_eq_mul_inv] using
  hâ‚.comp (uniform_continuous_fst.prod_mk (hâ‚‚.comp uniform_continuous_snd))âŸ©

variables [uniform_space Î±] [group Î±] [uniform_group Î±]

@[to_additive] lemma uniform_continuous_div : uniform_continuous (Î»p:Î±Ã—Î±, p.1 / p.2) :=
uniform_group.uniform_continuous_div

@[to_additive] lemma uniform_continuous.div [uniform_space Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±}
  (hf : uniform_continuous f) (hg : uniform_continuous g) : uniform_continuous (Î»x, f x / g x) :=
uniform_continuous_div.comp (hf.prod_mk hg)

@[to_additive] lemma uniform_continuous.inv [uniform_space Î²] {f : Î² â†’ Î±}
  (hf : uniform_continuous f) : uniform_continuous (Î»x, (f x)â»Â¹) :=
have uniform_continuous (Î»x, 1 / f x),
  from uniform_continuous_const.div hf,
by simp * at *

@[to_additive] lemma uniform_continuous_inv : uniform_continuous (Î»x:Î±, xâ»Â¹) :=
uniform_continuous_id.inv

@[to_additive] lemma uniform_continuous.mul [uniform_space Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±}
  (hf : uniform_continuous f) (hg : uniform_continuous g) : uniform_continuous (Î»x, f x * g x) :=
have uniform_continuous (Î»x, f x / (g x)â»Â¹), from hf.div hg.inv,
by simp * at *

@[to_additive] lemma uniform_continuous_mul : uniform_continuous (Î»p:Î±Ã—Î±, p.1 * p.2) :=
uniform_continuous_fst.mul uniform_continuous_snd

@[to_additive uniform_continuous.const_nsmul]
lemma uniform_continuous.pow_const [uniform_space Î²] {f : Î² â†’ Î±}
  (hf : uniform_continuous f) : âˆ€ n : â„•, uniform_continuous (Î» x, f x ^ n)
| 0 := by { simp_rw pow_zero, exact uniform_continuous_const }
| (n + 1) := by { simp_rw pow_succ, exact hf.mul (uniform_continuous.pow_const n) }

@[to_additive uniform_continuous_const_nsmul] lemma uniform_continuous_pow_const (n : â„•) :
  uniform_continuous (Î»x:Î±, x ^ n) :=
uniform_continuous_id.pow_const n

@[to_additive uniform_continuous.const_zsmul]
lemma uniform_continuous.zpow_const [uniform_space Î²] {f : Î² â†’ Î±}
  (hf : uniform_continuous f) : âˆ€ n : â„¤, uniform_continuous (Î» x, f x ^ n)
| (n : â„•) := by { simp_rw zpow_coe_nat, exact hf.pow_const _, }
| -[1+ n] := by { simp_rw zpow_neg_succ_of_nat, exact (hf.pow_const _).inv }

@[to_additive uniform_continuous_const_zsmul] lemma uniform_continuous_zpow_const (n : â„¤) :
  uniform_continuous (Î»x:Î±, x ^ n) :=
uniform_continuous_id.zpow_const n

@[priority 10, to_additive]
instance uniform_group.to_topological_group : topological_group Î± :=
{ continuous_mul := uniform_continuous_mul.continuous,
  continuous_inv := uniform_continuous_inv.continuous }

@[to_additive] instance [uniform_space Î²] [group Î²] [uniform_group Î²] : uniform_group (Î± Ã— Î²) :=
âŸ¨((uniform_continuous_fst.comp uniform_continuous_fst).div
  (uniform_continuous_fst.comp uniform_continuous_snd)).prod_mk
 ((uniform_continuous_snd.comp uniform_continuous_fst).div
  (uniform_continuous_snd.comp uniform_continuous_snd))âŸ©

@[to_additive] lemma uniformity_translate_mul (a : Î±) :
  (ğ“¤ Î±).map (Î»x:Î±Ã—Î±, (x.1 * a, x.2 * a)) = ğ“¤ Î± :=
le_antisymm
  (uniform_continuous_id.mul uniform_continuous_const)
  (calc ğ“¤ Î± =
    ((ğ“¤ Î±).map (Î»x:Î±Ã—Î±, (x.1 * aâ»Â¹, x.2 * aâ»Â¹))).map (Î»x:Î±Ã—Î±, (x.1 * a, x.2 * a)) :
      by simp [filter.map_map, (âˆ˜)]; exact filter.map_id.symm
    ... â‰¤ (ğ“¤ Î±).map (Î»x:Î±Ã—Î±, (x.1 * a, x.2 * a)) :
      filter.map_mono (uniform_continuous_id.mul uniform_continuous_const))

@[to_additive] lemma uniform_embedding_translate_mul (a : Î±) : uniform_embedding (Î»x:Î±, x * a) :=
{ comap_uniformity := begin
    rw [â† uniformity_translate_mul a, comap_map] {occs := occurrences.pos [1]},
    rintros âŸ¨pâ‚, pâ‚‚âŸ© âŸ¨qâ‚, qâ‚‚âŸ©,
    simp [prod.eq_iff_fst_eq_snd_eq] {contextual := tt}
  end,
  inj := mul_left_injective a }


namespace mul_opposite

@[to_additive] instance : uniform_group Î±áµáµ’áµ– :=
âŸ¨uniform_continuous_op.comp ((uniform_continuous_unop.comp uniform_continuous_snd).inv.mul $
  uniform_continuous_unop.comp uniform_continuous_fst)âŸ©

end mul_opposite

namespace subgroup

@[to_additive] instance (S : subgroup Î±) : uniform_group S :=
âŸ¨uniform_continuous_comap' (uniform_continuous_div.comp $
  uniform_continuous_subtype_val.prod_map uniform_continuous_subtype_val)âŸ©

end subgroup

section lattice_ops

variables [group Î²]

@[to_additive] lemma uniform_group_Inf {us : set (uniform_space Î²)}
  (h : âˆ€ u âˆˆ us, @uniform_group Î² u _) :
  @uniform_group Î² (Inf us) _ :=
{ uniform_continuous_div := uniform_continuous_Inf_rng (Î» u hu, uniform_continuous_Inf_domâ‚‚ hu hu
  (@uniform_group.uniform_continuous_div Î² u _ (h u hu))) }

@[to_additive] lemma uniform_group_infi {Î¹ : Sort*} {us' : Î¹ â†’ uniform_space Î²}
  (h' : âˆ€ i, @uniform_group Î² (us' i) _) :
  @uniform_group Î² (â¨… i, us' i) _ :=
by {rw â† Inf_range, exact uniform_group_Inf (set.forall_range_iff.mpr h')}

@[to_additive] lemma uniform_group_inf {uâ‚ uâ‚‚ : uniform_space Î²}
  (hâ‚ : @uniform_group Î² uâ‚ _) (hâ‚‚ : @uniform_group Î² uâ‚‚ _) :
  @uniform_group Î² (uâ‚ âŠ“ uâ‚‚) _ :=
by {rw inf_eq_infi, refine uniform_group_infi (Î» b, _), cases b; assumption}

@[to_additive] lemma uniform_group_comap {Î³ : Type*} [group Î³] {u : uniform_space Î³}
  [uniform_group Î³] {F : Type*} [monoid_hom_class F Î² Î³] (f : F) :
  @uniform_group Î² (u.comap f) _ :=
{ uniform_continuous_div :=
    begin
      letI : uniform_space Î² := u.comap f,
      refine uniform_continuous_comap' _,
      simp_rw [function.comp, map_div],
      change uniform_continuous ((Î» p : Î³ Ã— Î³, p.1 / p.2) âˆ˜ (prod.map f f)),
      exact uniform_continuous_div.comp
        (uniform_continuous_comap.prod_map uniform_continuous_comap),
    end }

end lattice_ops

section
variables (Î±)

@[to_additive] lemma uniformity_eq_comap_nhds_one : ğ“¤ Î± = comap (Î»x:Î±Ã—Î±, x.2 / x.1) (ğ“ (1:Î±)) :=
begin
  rw [nhds_eq_comap_uniformity, filter.comap_comap],
  refine le_antisymm (filter.map_le_iff_le_comap.1 _) _,
  { assume s hs,
    rcases mem_uniformity_of_uniform_continuous_invariant uniform_continuous_div hs
      with âŸ¨t, ht, htsâŸ©,
    refine mem_map.2 (mem_of_superset ht _),
    rintros âŸ¨a, bâŸ©,
    simpa [subset_def] using hts a b a },
  { assume s hs,
    rcases mem_uniformity_of_uniform_continuous_invariant uniform_continuous_mul hs
      with âŸ¨t, ht, htsâŸ©,
    refine âŸ¨_, ht, _âŸ©,
    rintros âŸ¨a, bâŸ©, simpa [subset_def] using hts 1 (b / a) a }
end

@[to_additive] lemma uniformity_eq_comap_nhds_one_swapped :
  ğ“¤ Î± = comap (Î»x:Î±Ã—Î±, x.1 / x.2) (ğ“ (1:Î±)) :=
by { rw [â† comap_swap_uniformity, uniformity_eq_comap_nhds_one, comap_comap, (âˆ˜)], refl }

@[to_additive] lemma uniform_group.ext {G : Type*} [group G] {u v : uniform_space G}
  (hu : @uniform_group G u _) (hv : @uniform_group G v _)
  (h : @nhds _ u.to_topological_space 1 = @nhds _ v.to_topological_space 1) :
  u = v :=
uniform_space_eq $
  by rw [@uniformity_eq_comap_nhds_one _ u _ hu, @uniformity_eq_comap_nhds_one _ v _ hv, h]

@[to_additive] lemma uniform_group.ext_iff {G : Type*} [group G] {u v : uniform_space G}
  (hu : @uniform_group G u _) (hv : @uniform_group G v _) :
  u = v â†” @nhds _ u.to_topological_space 1 = @nhds _ v.to_topological_space 1 :=
âŸ¨Î» h, h â–¸ rfl, hu.ext hvâŸ©

variables {Î±}

@[to_additive] theorem uniform_group.uniformity_countably_generated
  [(ğ“ (1 : Î±)).is_countably_generated] :
  (ğ“¤ Î±).is_countably_generated :=
by { rw uniformity_eq_comap_nhds_one, exact filter.comap.is_countably_generated _ _ }

open mul_opposite

@[to_additive]
lemma uniformity_eq_comap_inv_mul_nhds_one : ğ“¤ Î± = comap (Î»x:Î±Ã—Î±, x.1â»Â¹ * x.2) (ğ“ (1:Î±)) :=
begin
  rw [â† comap_uniformity_mul_opposite, uniformity_eq_comap_nhds_one, â† op_one, â† comap_unop_nhds,
    comap_comap, comap_comap],
  simp [(âˆ˜)]
end

@[to_additive] lemma uniformity_eq_comap_inv_mul_nhds_one_swapped :
  ğ“¤ Î± = comap (Î»x:Î±Ã—Î±, x.2â»Â¹ * x.1) (ğ“ (1:Î±)) :=
by { rw [â† comap_swap_uniformity, uniformity_eq_comap_inv_mul_nhds_one, comap_comap, (âˆ˜)], refl }

end

@[to_additive] lemma filter.has_basis.uniformity_of_nhds_one {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ set Î±}
  (h : (ğ“ (1 : Î±)).has_basis p U) :
  (ğ“¤ Î±).has_basis p (Î» i, {x : Î± Ã— Î± | x.2 / x.1 âˆˆ U i}) :=
by { rw uniformity_eq_comap_nhds_one, exact h.comap _ }

@[to_additive] lemma filter.has_basis.uniformity_of_nhds_one_inv_mul
  {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ set Î±} (h : (ğ“ (1 : Î±)).has_basis p U) :
  (ğ“¤ Î±).has_basis p (Î» i, {x : Î± Ã— Î± | x.1â»Â¹ * x.2 âˆˆ U i}) :=
by { rw uniformity_eq_comap_inv_mul_nhds_one, exact h.comap _ }

@[to_additive] lemma filter.has_basis.uniformity_of_nhds_one_swapped
  {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ set Î±} (h : (ğ“ (1 : Î±)).has_basis p U) :
  (ğ“¤ Î±).has_basis p (Î» i, {x : Î± Ã— Î± | x.1 / x.2 âˆˆ U i}) :=
by { rw uniformity_eq_comap_nhds_one_swapped, exact h.comap _ }

@[to_additive] lemma filter.has_basis.uniformity_of_nhds_one_inv_mul_swapped
  {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ set Î±} (h : (ğ“ (1 : Î±)).has_basis p U) :
  (ğ“¤ Î±).has_basis p (Î» i, {x : Î± Ã— Î± | x.2â»Â¹ * x.1 âˆˆ U i}) :=
by { rw uniformity_eq_comap_inv_mul_nhds_one_swapped, exact h.comap _ }

@[to_additive] lemma group_separation_rel (x y : Î±) :
  (x, y) âˆˆ separation_rel Î± â†” x / y âˆˆ closure ({1} : set Î±) :=
have embedding (Î»a, a * (y / x)), from (uniform_embedding_translate_mul (y / x)).embedding,
show (x, y) âˆˆ â‹‚â‚€ (ğ“¤ Î±).sets â†” x / y âˆˆ closure ({1} : set Î±),
begin
  rw [this.closure_eq_preimage_closure_image, uniformity_eq_comap_nhds_one Î±, sInter_comap_sets],
  simp [mem_closure_iff_nhds, inter_singleton_nonempty, sub_eq_add_neg, add_assoc]
end

@[to_additive] lemma uniform_continuous_of_tendsto_one {hom : Type*} [uniform_space Î²] [group Î²]
  [uniform_group Î²] [monoid_hom_class hom Î± Î²] {f : hom} (h : tendsto f (ğ“ 1) (ğ“ 1)) :
  uniform_continuous f :=
begin
  have : ((Î»x:Î²Ã—Î², x.2 / x.1) âˆ˜ (Î»x:Î±Ã—Î±, (f x.1, f x.2))) = (Î»x:Î±Ã—Î±, f (x.2 / x.1)),
  { simp only [map_div] },
  rw [uniform_continuous, uniformity_eq_comap_nhds_one Î±, uniformity_eq_comap_nhds_one Î²,
    tendsto_comap_iff, this],
  exact tendsto.comp h tendsto_comap
end

/-- A group homomorphism (a bundled morphism of a type that implements `monoid_hom_class`) between
two uniform groups is uniformly continuous provided that it is continuous at one. See also
`continuous_of_continuous_at_one`. -/
@[to_additive "An additive group homomorphism (a bundled morphism of a type that implements
`add_monoid_hom_class`) between two uniform additive groups is uniformly continuous provided that it
is continuous at zero. See also `continuous_of_continuous_at_zero`."]
lemma uniform_continuous_of_continuous_at_one {hom : Type*}
  [uniform_space Î²] [group Î²] [uniform_group Î²] [monoid_hom_class hom Î± Î²]
  (f : hom) (hf : continuous_at f 1) :
  uniform_continuous f :=
uniform_continuous_of_tendsto_one (by simpa using hf.tendsto)

@[to_additive] lemma monoid_hom.uniform_continuous_of_continuous_at_one
  [uniform_space Î²] [group Î²] [uniform_group Î²]
  (f : Î± â†’* Î²) (hf : continuous_at f 1) :
  uniform_continuous f :=
uniform_continuous_of_continuous_at_one f hf

/-- A homomorphism from a uniform group to a discrete uniform group is continuous if and only if
its kernel is open. -/
@[to_additive "A homomorphism from a uniform additive group to a discrete uniform additive group is
continuous if and only if its kernel is open."]
lemma uniform_group.uniform_continuous_iff_open_ker {hom : Type*} [uniform_space Î²]
  [discrete_topology Î²] [group Î²] [uniform_group Î²] [monoid_hom_class hom Î± Î²] {f : hom} :
  uniform_continuous f â†” is_open ((f : Î± â†’* Î²).ker : set Î±) :=
begin
  refine âŸ¨Î» hf, _, Î» hf, _âŸ©,
  { apply (is_open_discrete ({1} : set Î²)).preimage (uniform_continuous.continuous hf) },
  { apply uniform_continuous_of_continuous_at_one,
    rw [continuous_at, nhds_discrete Î², map_one, tendsto_pure],
    exact hf.mem_nhds (map_one f) }
end

@[to_additive] lemma uniform_continuous_monoid_hom_of_continuous {hom : Type*} [uniform_space Î²]
  [group Î²] [uniform_group Î²] [monoid_hom_class hom Î± Î²] {f : hom} (h : continuous f) :
  uniform_continuous f :=
uniform_continuous_of_tendsto_one $
  suffices tendsto f (ğ“ 1) (ğ“ (f 1)), by rwa map_one at this,
  h.tendsto 1

@[to_additive] lemma cauchy_seq.mul {Î¹ : Type*} [semilattice_sup Î¹] {u v : Î¹ â†’ Î±}
  (hu : cauchy_seq u) (hv : cauchy_seq v) : cauchy_seq (u * v) :=
uniform_continuous_mul.comp_cauchy_seq (hu.prod hv)

@[to_additive] lemma cauchy_seq.mul_const {Î¹ : Type*} [semilattice_sup Î¹]
  {u : Î¹ â†’ Î±} {x : Î±} (hu : cauchy_seq u) : cauchy_seq (Î» n, u n * x) :=
(uniform_continuous_id.mul uniform_continuous_const).comp_cauchy_seq hu

@[to_additive] lemma cauchy_seq.const_mul {Î¹ : Type*} [semilattice_sup Î¹]
  {u : Î¹ â†’ Î±} {x : Î±} (hu : cauchy_seq u) : cauchy_seq (Î» n, x * u n) :=
(uniform_continuous_const.mul uniform_continuous_id).comp_cauchy_seq hu

@[to_additive] lemma cauchy_seq.inv {Î¹ : Type*} [semilattice_sup Î¹]
  {u : Î¹ â†’ Î±} (h : cauchy_seq u) : cauchy_seq (uâ»Â¹) :=
uniform_continuous_inv.comp_cauchy_seq h

@[to_additive] lemma totally_bounded_iff_subset_finite_Union_nhds_one {s : set Î±} :
  totally_bounded s â†” âˆ€ U âˆˆ ğ“ (1 : Î±), âˆƒ (t : set Î±), t.finite âˆ§ s âŠ† â‹ƒ y âˆˆ t, y â€¢ U :=
(ğ“ (1 : Î±)).basis_sets.uniformity_of_nhds_one_inv_mul_swapped.totally_bounded_iff.trans $
  by simp [â† preimage_smul_inv, preimage]

section uniform_convergence
variables {Î¹ : Type*} {l : filter Î¹} {l' : filter Î²} {f f' : Î¹ â†’ Î² â†’ Î±} {g g' : Î² â†’ Î±} {s : set Î²}

@[to_additive] lemma tendsto_uniformly_on_filter.mul (hf : tendsto_uniformly_on_filter f g l l')
  (hf' : tendsto_uniformly_on_filter f' g' l l') :
  tendsto_uniformly_on_filter (f * f') (g * g') l l' :=
Î» u hu, ((uniform_continuous_mul.comp_tendsto_uniformly_on_filter
  (hf.prod hf')) u hu).diag_of_prod_left

@[to_additive] lemma tendsto_uniformly_on_filter.div (hf : tendsto_uniformly_on_filter f g l l')
  (hf' : tendsto_uniformly_on_filter f' g' l l') :
  tendsto_uniformly_on_filter (f / f') (g / g') l l' :=
Î» u hu, ((uniform_continuous_div.comp_tendsto_uniformly_on_filter
  (hf.prod hf')) u hu).diag_of_prod_left

@[to_additive] lemma tendsto_uniformly_on.mul (hf : tendsto_uniformly_on f g l s)
  (hf' : tendsto_uniformly_on f' g' l s) : tendsto_uniformly_on (f * f') (g * g') l s :=
Î» u hu, ((uniform_continuous_mul.comp_tendsto_uniformly_on (hf.prod hf')) u hu).diag_of_prod

@[to_additive] lemma tendsto_uniformly_on.div (hf : tendsto_uniformly_on f g l s)
  (hf' : tendsto_uniformly_on f' g' l s) : tendsto_uniformly_on (f / f') (g / g') l s :=
Î» u hu, ((uniform_continuous_div.comp_tendsto_uniformly_on (hf.prod hf')) u hu).diag_of_prod

@[to_additive] lemma tendsto_uniformly.mul (hf : tendsto_uniformly f g l)
  (hf' : tendsto_uniformly f' g' l) : tendsto_uniformly (f * f') (g * g') l :=
Î» u hu, ((uniform_continuous_mul.comp_tendsto_uniformly (hf.prod hf')) u hu).diag_of_prod

@[to_additive] lemma tendsto_uniformly.div (hf : tendsto_uniformly f g l)
  (hf' : tendsto_uniformly f' g' l) : tendsto_uniformly (f / f') (g / g') l :=
Î» u hu, ((uniform_continuous_div.comp_tendsto_uniformly (hf.prod hf')) u hu).diag_of_prod

@[to_additive] lemma uniform_cauchy_seq_on.mul (hf : uniform_cauchy_seq_on f l s)
  (hf' : uniform_cauchy_seq_on f' l s) : uniform_cauchy_seq_on (f * f') l s :=
Î» u hu, by simpa using ((uniform_continuous_mul.comp_uniform_cauchy_seq_on (hf.prod' hf')) u hu)

@[to_additive] lemma uniform_cauchy_seq_on.div (hf : uniform_cauchy_seq_on f l s)
  (hf' : uniform_cauchy_seq_on f' l s) : uniform_cauchy_seq_on (f / f') l s :=
Î» u hu, by simpa using ((uniform_continuous_div.comp_uniform_cauchy_seq_on (hf.prod' hf')) u hu)

end uniform_convergence
end uniform_group

section topological_group
open filter
variables (G : Type*) [group G] [topological_space G] [topological_group G]

/-- The right uniformity on a topological group (as opposed to the left uniformity).

Warning: in general the right and left uniformities do not coincide and so one does not obtain a
`uniform_group` structure. Two important special cases where they _do_ coincide are for
commutative groups (see `topological_comm_group_is_uniform`) and for compact groups (see
`topological_group_is_uniform_of_compact_space`). -/
@[to_additive "The right uniformity on a topological additive group (as opposed to the left
uniformity).

Warning: in general the right and left uniformities do not coincide and so one does not obtain a
`uniform_add_group` structure. Two important special cases where they _do_ coincide are for
commutative additive groups (see `topological_add_comm_group_is_uniform`) and for compact
additive groups (see `topological_add_comm_group_is_uniform_of_compact_space`)."]
def topological_group.to_uniform_space : uniform_space G :=
{ uniformity          := comap (Î»p:GÃ—G, p.2 / p.1) (ğ“ 1),
  refl                :=
    by refine map_le_iff_le_comap.1 (le_trans _ (pure_le_nhds 1));
      simp [set.subset_def] {contextual := tt},
  symm                :=
  begin
    suffices : tendsto (Î»p:GÃ—G, (p.2 / p.1)â»Â¹) (comap (Î»p:GÃ—G, p.2 / p.1) (ğ“ 1)) (ğ“ 1â»Â¹),
    { simpa [tendsto_comap_iff], },
    exact tendsto.comp (tendsto.inv tendsto_id) tendsto_comap
  end,
  comp                :=
  begin
    intros D H,
    rw mem_lift'_sets,
    { rcases H with âŸ¨U, U_nhds, U_subâŸ©,
      rcases exists_nhds_one_split U_nhds with âŸ¨V, âŸ¨V_nhds, V_sumâŸ©âŸ©,
      existsi ((Î»p:GÃ—G, p.2 / p.1) â»Â¹' V),
      have H : (Î»p:GÃ—G, p.2 / p.1) â»Â¹' V âˆˆ comap (Î»p:GÃ—G, p.2 / p.1) (ğ“ (1 : G)),
        by existsi [V, V_nhds] ; refl,
      existsi H,
      have comp_rel_sub :
        comp_rel ((Î»p:GÃ—G, p.2 / p.1) â»Â¹' V) ((Î»p, p.2 / p.1) â»Â¹' V) âŠ† (Î»p:GÃ—G, p.2 / p.1) â»Â¹' U,
      begin
        intros p p_comp_rel,
        rcases p_comp_rel with âŸ¨z, âŸ¨Hz1, Hz2âŸ©âŸ©,
        simpa using V_sum _ Hz2 _ Hz1,
      end,
      exact set.subset.trans comp_rel_sub U_sub },
    { exact monotone_id.comp_rel monotone_id }
  end,
  is_open_uniformity  :=
  begin
    intro S,
    let S' := Î» x, {p : G Ã— G | p.1 = x â†’ p.2 âˆˆ S},
    show is_open S â†” âˆ€ (x : G), x âˆˆ S â†’ S' x âˆˆ comap (Î»p:GÃ—G, p.2 / p.1) (ğ“ (1 : G)),
    rw [is_open_iff_mem_nhds],
    refine forallâ‚‚_congr (Î» a ha, _),
    rw [â† nhds_translation_div, mem_comap, mem_comap],
    refine existsâ‚‚_congr (Î» t ht, _),
    show (Î» (y : G), y / a) â»Â¹' t âŠ† S â†” (Î» (p : G Ã— G), p.snd / p.fst) â»Â¹' t âŠ† S' a,
    split,
    { rintros h âŸ¨x, yâŸ© hx rfl, exact h hx },
    { rintros h x hx, exact @h (a, x) hx rfl }
  end }

local attribute [instance] topological_group.to_uniform_space

@[to_additive] lemma uniformity_eq_comap_nhds_one' :
  ğ“¤ G = comap (Î»p:GÃ—G, p.2 / p.1) (ğ“ (1 : G)) := rfl

@[to_additive] lemma topological_group_is_uniform_of_compact_space
  [compact_space G] : uniform_group G :=
âŸ¨begin
  apply compact_space.uniform_continuous_of_continuous,
  exact continuous_div',
endâŸ©

variables {G}

@[to_additive] instance subgroup.is_closed_of_discrete [t2_space G]
  {H : subgroup G} [discrete_topology H] : is_closed (H : set G) :=
begin
  obtain âŸ¨V, V_in, VHâŸ© : âˆƒ (V : set G) (hV : V âˆˆ ğ“ (1 : G)), V âˆ© (H : set G) = {1},
    from nhds_inter_eq_singleton_of_mem_discrete H.one_mem,
  haveI : separated_space G := separated_iff_t2.mpr â€¹_â€º,
  have : (Î» p : G Ã— G, p.2 / p.1) â»Â¹' V âˆˆ ğ“¤ G, from preimage_mem_comap V_in,
  apply is_closed_of_spaced_out this,
  intros h h_in h' h'_in,
  contrapose!,
  rintro (hyp : h' / h âˆˆ V),
  have : h'/h âˆˆ ({1} : set G) := VH â–¸ set.mem_inter hyp (H.div_mem h'_in h_in),
  exact (eq_of_div_eq_one this).symm
end

@[to_additive] lemma topological_group.tendsto_uniformly_iff
  {Î¹ Î± : Type*} (F : Î¹ â†’ Î± â†’ G) (f : Î± â†’ G) (p : filter Î¹) :
  @tendsto_uniformly Î± G Î¹ (topological_group.to_uniform_space G) F f p
    â†” âˆ€ u âˆˆ ğ“ (1 : G), âˆ€á¶  i in p, âˆ€ a, F i a / f a âˆˆ u :=
âŸ¨Î» h u hu, h _ âŸ¨u, hu, Î» _, idâŸ©, Î» h v âŸ¨u, hu, hvâŸ©,
  mem_of_superset (h u hu) (Î» i hi a, hv (by exact hi a))âŸ©

@[to_additive] lemma topological_group.tendsto_uniformly_on_iff
  {Î¹ Î± : Type*} (F : Î¹ â†’ Î± â†’ G) (f : Î± â†’ G) (p : filter Î¹) (s : set Î±) :
  @tendsto_uniformly_on Î± G Î¹ (topological_group.to_uniform_space G) F f p s
    â†” âˆ€ u âˆˆ ğ“ (1 : G), âˆ€á¶  i in p, âˆ€ a âˆˆ s, F i a / f a âˆˆ u :=
âŸ¨Î» h u hu, h _ âŸ¨u, hu, Î» _, idâŸ©, Î» h v âŸ¨u, hu, hvâŸ©,
  mem_of_superset (h u hu) (Î» i hi a ha, hv (by exact hi a ha))âŸ©

@[to_additive] lemma topological_group.tendsto_locally_uniformly_iff
  {Î¹ Î± : Type*} [topological_space Î±] (F : Î¹ â†’ Î± â†’ G) (f : Î± â†’ G) (p : filter Î¹) :
  @tendsto_locally_uniformly Î± G Î¹ (topological_group.to_uniform_space G) _ F f p
    â†” âˆ€ (u âˆˆ ğ“ (1 : G)) (x : Î±), âˆƒ (t âˆˆ ğ“ x), âˆ€á¶  i in p, âˆ€ a âˆˆ t, F i a / f a âˆˆ u :=
âŸ¨Î» h u hu, h _ âŸ¨u, hu, Î» _, idâŸ©, Î» h v âŸ¨u, hu, hvâŸ© x, exists_imp_exists (by exact Î» a,
  exists_imp_exists (Î» ha hp, mem_of_superset hp (Î» i hi a ha, hv (by exact hi a ha)))) (h u hu x)âŸ©

@[to_additive] lemma topological_group.tendsto_locally_uniformly_on_iff
  {Î¹ Î± : Type*} [topological_space Î±] (F : Î¹ â†’ Î± â†’ G) (f : Î± â†’ G) (p : filter Î¹) (s : set Î±) :
  @tendsto_locally_uniformly_on Î± G Î¹ (topological_group.to_uniform_space G) _ F f p s
    â†” âˆ€ (u âˆˆ ğ“ (1 : G)) (x âˆˆ s), âˆƒ (t âˆˆ ğ“[s] x), âˆ€á¶  i in p, âˆ€ a âˆˆ t, F i a / f a âˆˆ u :=
âŸ¨Î» h u hu, h _ âŸ¨u, hu, Î» _, idâŸ©, Î» h v âŸ¨u, hu, hvâŸ© x, exists_imp_exists (by exact Î» a,
  exists_imp_exists (Î» ha hp, mem_of_superset hp (Î» i hi a ha, hv (by exact hi a ha)))) âˆ˜ h u hu xâŸ©

end topological_group

section topological_comm_group
universes u v w x
open filter

variables (G : Type*) [comm_group G] [topological_space G] [topological_group G]

section
local attribute [instance] topological_group.to_uniform_space

variable {G}
@[to_additive] lemma topological_comm_group_is_uniform : uniform_group G :=
have tendsto
    ((Î»p:(GÃ—G), p.1 / p.2) âˆ˜ (Î»p:(GÃ—G)Ã—(GÃ—G), (p.1.2 / p.1.1, p.2.2 / p.2.1)))
    (comap (Î»p:(GÃ—G)Ã—(GÃ—G), (p.1.2 / p.1.1, p.2.2 / p.2.1)) ((ğ“ 1).prod (ğ“ 1)))
    (ğ“ (1 / 1)) :=
  (tendsto_fst.div' tendsto_snd).comp tendsto_comap,
begin
  constructor,
  rw [uniform_continuous, uniformity_prod_eq_prod, tendsto_map'_iff,
    uniformity_eq_comap_nhds_one' G, tendsto_comap_iff, prod_comap_comap_eq],
  simpa [(âˆ˜), div_eq_mul_inv, mul_comm, mul_left_comm] using this
end

open set

@[to_additive] lemma topological_group.t2_space_iff_one_closed :
  t2_space G â†” is_closed ({1} : set G) :=
begin
  haveI : uniform_group G := topological_comm_group_is_uniform,
  rw [â† separated_iff_t2, separated_space_iff, â† closure_eq_iff_is_closed],
  split; intro h,
  { apply subset.antisymm,
    { intros x x_in,
      have := group_separation_rel x 1,
      rw div_one at this,
      rw [â† this, h] at x_in,
      change x = 1 at x_in,
      simp [x_in] },
    { exact subset_closure } },
  { ext p,
    cases p with x y,
    rw [group_separation_rel x, h, mem_singleton_iff, div_eq_one],
    refl }
end

@[to_additive] lemma topological_group.t2_space_of_one_sep
  (H : âˆ€ x : G, x â‰  1 â†’ âˆƒ U âˆˆ nhds (1 : G), x âˆ‰ U) : t2_space G :=
begin
  rw [topological_group.t2_space_iff_one_closed, â† is_open_compl_iff, is_open_iff_mem_nhds],
  intros x x_not,
  have : x â‰  1, from mem_compl_singleton_iff.mp x_not,
  rcases H x this with âŸ¨U, U_in, xUâŸ©,
  rw â† nhds_one_symm G at U_in,
  rcases U_in with âŸ¨W, W_in, UWâŸ©,
  rw â† nhds_translation_mul_inv,
  use [W, W_in],
  rw subset_compl_comm,
  suffices : xâ»Â¹ âˆ‰ W, by simpa,
  exact Î» h, xU (UW h)
end

end

@[to_additive] lemma uniform_group.to_uniform_space_eq {G : Type*} [u : uniform_space G]
  [group G] [uniform_group G] : topological_group.to_uniform_space G = u :=
begin
  ext : 1,
  rw [uniformity_eq_comap_nhds_one' G, uniformity_eq_comap_nhds_one G]
end

end topological_comm_group

open filter set function

section
variables {Î± : Type*} {Î² : Type*} {hom : Type*}
variables [topological_space Î±] [group Î±] [topological_group Î±]

-- Î² is a dense subgroup of Î±, inclusion is denoted by e
variables [topological_space Î²] [group Î²]
variables [monoid_hom_class hom Î² Î±] {e : hom} (de : dense_inducing e)
include de

@[to_additive] lemma tendsto_div_comap_self (xâ‚€ : Î±) :
  tendsto (Î»t:Î²Ã—Î², t.2 / t.1) (comap (Î»p:Î²Ã—Î², (e p.1, e p.2)) $ ğ“ (xâ‚€, xâ‚€)) (ğ“ 1) :=
begin
  have comm : (Î»x:Î±Ã—Î±, x.2/x.1) âˆ˜ (Î»t:Î²Ã—Î², (e t.1, e t.2)) = e âˆ˜ (Î»t:Î²Ã—Î², t.2 / t.1),
  { ext t,
    change e t.2 / e t.1 = e (t.2 / t.1),
    rwa â† map_div e t.2 t.1 },
  have lim : tendsto (Î» x : Î± Ã— Î±, x.2/x.1) (ğ“ (xâ‚€, xâ‚€)) (ğ“ (e 1)),
  { simpa using (continuous_div'.comp (@continuous_swap Î± Î± _ _)).tendsto (xâ‚€, xâ‚€) },
  simpa using de.tendsto_comap_nhds_nhds lim comm
end
end

namespace dense_inducing
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}
variables {G : Type*}

-- Î² is a dense subgroup of Î±, inclusion is denoted by e
-- Î´ is a dense subgroup of Î³, inclusion is denoted by f
variables [topological_space Î±] [add_comm_group Î±] [topological_add_group Î±]
variables [topological_space Î²] [add_comm_group Î²] [topological_add_group Î²]
variables [topological_space Î³] [add_comm_group Î³] [topological_add_group Î³]
variables [topological_space Î´] [add_comm_group Î´] [topological_add_group Î´]
variables [uniform_space G] [add_comm_group G] [uniform_add_group G] [separated_space G]
  [complete_space G]
variables {e : Î² â†’+ Î±} (de : dense_inducing e)
variables {f : Î´ â†’+ Î³} (df : dense_inducing f)

variables {Ï† : Î² â†’+ Î´ â†’+ G}
local notation `Î¦` := Î» p : Î² Ã— Î´, Ï† p.1 p.2

variables  (hÏ† : continuous Î¦)

include de df hÏ†

variables {W' : set G} (W'_nhd : W' âˆˆ ğ“ (0 : G))
include W'_nhd

private lemma extend_Z_bilin_aux (xâ‚€ : Î±) (yâ‚ : Î´) :
  âˆƒ Uâ‚‚ âˆˆ comap e (ğ“ xâ‚€), âˆ€ x x' âˆˆ Uâ‚‚, Î¦ (x' - x, yâ‚) âˆˆ W' :=
begin
  let Nx := ğ“ xâ‚€,
  let ee := Î» u : Î² Ã— Î², (e u.1, e u.2),

  have lim1 : tendsto (Î» a : Î² Ã— Î², (a.2 - a.1, yâ‚)) (comap e Nx Ã—á¶  comap e Nx) (ğ“ (0, yâ‚)),
  { have := tendsto.prod_mk (tendsto_sub_comap_self de xâ‚€)
      (tendsto_const_nhds : tendsto (Î» (p : Î² Ã— Î²), yâ‚) (comap ee $ ğ“ (xâ‚€, xâ‚€)) (ğ“ yâ‚)),
    rw [nhds_prod_eq, prod_comap_comap_eq, â†nhds_prod_eq],
    exact (this : _) },
  have lim2 : tendsto Î¦ (ğ“ (0, yâ‚)) (ğ“ 0), by simpa using hÏ†.tendsto (0, yâ‚),
  have lim := lim2.comp lim1,
  rw tendsto_prod_self_iff at lim,
  simp_rw ball_mem_comm,
  exact lim W' W'_nhd
end

private lemma extend_Z_bilin_key (xâ‚€ : Î±) (yâ‚€ : Î³) :
  âˆƒ U âˆˆ comap e (ğ“ xâ‚€), âˆƒ V âˆˆ comap f (ğ“ yâ‚€),
    âˆ€ x x' âˆˆ U, âˆ€ y y' âˆˆ V, Î¦ (x', y') - Î¦ (x, y) âˆˆ W' :=
begin
  let Nx := ğ“ xâ‚€,
  let Ny := ğ“ yâ‚€,
  let dp := dense_inducing.prod de df,
  let ee := Î» u : Î² Ã— Î², (e u.1, e u.2),
  let ff := Î» u : Î´ Ã— Î´, (f u.1, f u.2),

  have lim_Ï† : filter.tendsto Î¦ (ğ“ (0, 0)) (ğ“ 0),
  { simpa using hÏ†.tendsto (0, 0) },

  have lim_Ï†_sub_sub : tendsto (Î» (p : (Î² Ã— Î²) Ã— (Î´ Ã— Î´)), Î¦ (p.1.2 - p.1.1, p.2.2 - p.2.1))
    ((comap ee $ ğ“ (xâ‚€, xâ‚€)) Ã—á¶  (comap ff $ ğ“ (yâ‚€, yâ‚€))) (ğ“ 0),
  { have lim_sub_sub :  tendsto (Î» (p : (Î² Ã— Î²) Ã— Î´ Ã— Î´), (p.1.2 - p.1.1, p.2.2 - p.2.1))
      ((comap ee (ğ“ (xâ‚€, xâ‚€))) Ã—á¶  (comap ff (ğ“ (yâ‚€, yâ‚€)))) (ğ“ 0 Ã—á¶  ğ“ 0),
    { have := filter.prod_mono (tendsto_sub_comap_self de xâ‚€) (tendsto_sub_comap_self df yâ‚€),
      rwa prod_map_map_eq at this },
    rw â† nhds_prod_eq at lim_sub_sub,
    exact tendsto.comp lim_Ï† lim_sub_sub },

  rcases exists_nhds_zero_quarter W'_nhd with âŸ¨W, W_nhd, W4âŸ©,

  have : âˆƒ Uâ‚ âˆˆ comap e (ğ“ xâ‚€), âˆƒ Vâ‚ âˆˆ comap f (ğ“ yâ‚€),
    âˆ€ x x' âˆˆ Uâ‚, âˆ€ y y' âˆˆ Vâ‚,  Î¦ (x'-x, y'-y) âˆˆ W,
  { have := tendsto_prod_iff.1 lim_Ï†_sub_sub W W_nhd,
    repeat { rw [nhds_prod_eq, â†prod_comap_comap_eq] at this },
    rcases this with âŸ¨U, U_in, V, V_in, HâŸ©,
    rw [mem_prod_same_iff] at U_in V_in,
    rcases U_in with âŸ¨Uâ‚, Uâ‚_in, HUâ‚âŸ©,
    rcases V_in with âŸ¨Vâ‚, Vâ‚_in, HVâ‚âŸ©,
    existsi [Uâ‚, Uâ‚_in, Vâ‚, Vâ‚_in],
    intros x x_in x' x'_in y y_in y' y'_in,
    exact H _ _ (HUâ‚ (mk_mem_prod x_in x'_in)) (HVâ‚ (mk_mem_prod y_in y'_in)) },
  rcases this with âŸ¨Uâ‚, Uâ‚_nhd, Vâ‚, Vâ‚_nhd, HâŸ©,

  obtain âŸ¨xâ‚, xâ‚_inâŸ© : Uâ‚.nonempty :=
    ((de.comap_nhds_ne_bot _).nonempty_of_mem Uâ‚_nhd),
  obtain âŸ¨yâ‚, yâ‚_inâŸ© : Vâ‚.nonempty :=
    ((df.comap_nhds_ne_bot _).nonempty_of_mem Vâ‚_nhd),

  have cont_flip : continuous (Î» p : Î´ Ã— Î², Ï†.flip p.1 p.2),
  { show continuous (Î¦ âˆ˜ prod.swap), from hÏ†.comp continuous_swap },
  rcases (extend_Z_bilin_aux de df hÏ† W_nhd xâ‚€ yâ‚) with âŸ¨Uâ‚‚, Uâ‚‚_nhd, HUâŸ©,
  rcases (extend_Z_bilin_aux df de cont_flip W_nhd yâ‚€ xâ‚) with âŸ¨Vâ‚‚, Vâ‚‚_nhd, HVâŸ©,

  existsi [Uâ‚ âˆ© Uâ‚‚, inter_mem Uâ‚_nhd Uâ‚‚_nhd,
            Vâ‚ âˆ© Vâ‚‚, inter_mem Vâ‚_nhd Vâ‚‚_nhd],

  rintros x âŸ¨xUâ‚, xUâ‚‚âŸ© x' âŸ¨x'Uâ‚, x'Uâ‚‚âŸ© y âŸ¨yVâ‚, yVâ‚‚âŸ© y' âŸ¨y'Vâ‚, y'Vâ‚‚âŸ©,
  have key_formula : Ï† x' y' - Ï† x y =
    Ï†(x' - x) yâ‚ + Ï† (x' - x) (y' - yâ‚) + Ï† xâ‚ (y' - y) + Ï† (x - xâ‚) (y' - y),
  { simp, abel },
  rw key_formula,
  have hâ‚ := HU x xUâ‚‚ x' x'Uâ‚‚,
  have hâ‚‚ := H x xUâ‚ x' x'Uâ‚ yâ‚ yâ‚_in y' y'Vâ‚,
  have hâ‚ƒ := HV y yVâ‚‚ y' y'Vâ‚‚,
  have hâ‚„ := H xâ‚ xâ‚_in x xUâ‚ y yVâ‚ y' y'Vâ‚,
  exact W4 hâ‚ hâ‚‚ hâ‚ƒ hâ‚„
end

omit W'_nhd

open dense_inducing

/-- Bourbaki GT III.6.5 Theorem I:
â„¤-bilinear continuous maps from dense images into a complete Hausdorff group extend by continuity.
Note: Bourbaki assumes that Î± and Î² are also complete Hausdorff, but this is not necessary. -/
theorem extend_Z_bilin  : continuous (extend (de.prod df) Î¦) :=
begin
  refine continuous_extend_of_cauchy _ _,
  rintro âŸ¨xâ‚€, yâ‚€âŸ©,
  split,
  { apply ne_bot.map,
    apply comap_ne_bot,

    intros U h,
    rcases mem_closure_iff_nhds.1 ((de.prod df).dense (xâ‚€, yâ‚€)) U h with âŸ¨x, x_in, âŸ¨z, z_xâŸ©âŸ©,
    existsi z,
    cc },
  { suffices : map (Î» (p : (Î² Ã— Î´) Ã— (Î² Ã— Î´)), Î¦ p.2 - Î¦ p.1)
      (comap (Î» (p : (Î² Ã— Î´) Ã— Î² Ã— Î´), ((e p.1.1, f p.1.2), (e p.2.1, f p.2.2)))
         (ğ“ (xâ‚€, yâ‚€) Ã—á¶  ğ“ (xâ‚€, yâ‚€))) â‰¤ ğ“ 0,
    by rwa [uniformity_eq_comap_nhds_zero G, prod_map_map_eq, â†map_le_iff_le_comap, filter.map_map,
        prod_comap_comap_eq],

    intros W' W'_nhd,

    have key := extend_Z_bilin_key de df hÏ† W'_nhd xâ‚€ yâ‚€,
    rcases key with âŸ¨U, U_nhd, V, V_nhd, hâŸ©,
    rw mem_comap at U_nhd,
    rcases U_nhd with âŸ¨U', U'_nhd, U'_subâŸ©,
    rw mem_comap at V_nhd,
    rcases V_nhd with âŸ¨V', V'_nhd, V'_subâŸ©,

    rw [mem_map, mem_comap, nhds_prod_eq],
    existsi (U' Ã—Ë¢ V') Ã—Ë¢ (U' Ã—Ë¢ V'),
    rw mem_prod_same_iff,

    simp only [exists_prop],
    split,
    { change U' âˆˆ ğ“ xâ‚€ at U'_nhd,
      change V' âˆˆ ğ“ yâ‚€ at V'_nhd,
      have := prod_mem_prod U'_nhd V'_nhd,
      tauto },
    { intros p h',
      simp only [set.mem_preimage, set.prod_mk_mem_set_prod_eq] at h',
      rcases p with âŸ¨âŸ¨x, yâŸ©, âŸ¨x', y'âŸ©âŸ©,
      apply h ; tauto } }
end
end dense_inducing

section complete_quotient

universe u
open topological_space classical

/-- The quotient `G â§¸ N` of a complete first countable topological group `G` by a normal subgroup
is itself complete. [N. Bourbaki, *General Topology*, IX.3.1 Proposition 4][bourbaki1966b]

Because a topological group is not equipped with a `uniform_space` instance by default, we must
explicitly provide it in order to consider completeness. See `quotient_group.complete_space` for a
version in which `G` is already equipped with a uniform structure. -/
@[to_additive "The quotient `G â§¸ N` of a complete first countable topological additive group
`G` by a normal additive subgroup is itself complete. Consequently, quotients of Banach spaces by
subspaces are complete. [N. Bourbaki, *General Topology*, IX.3.1 Proposition 4][bourbaki1966b]

Because an additive topological group is not equipped with a `uniform_space` instance by default,
we must explicitly provide it in order to consider completeness. See
`quotient_add_group.complete_space` for a version in which `G` is already equipped with a uniform
structure."]
instance quotient_group.complete_space' (G : Type u) [group G] [topological_space G]
  [topological_group G] [first_countable_topology G] (N : subgroup G) [N.normal]
  [@complete_space G (topological_group.to_uniform_space G)] :
  @complete_space (G â§¸ N) (topological_group.to_uniform_space (G â§¸ N)) :=
begin
  /- Since `G â§¸ N` is a topological group it is a uniform space, and since `G` is first countable
  the uniformities of both `G` and `G â§¸ N` are countably generated. Moreover, we may choose a
  sequential antitone neighborhood basis `u` for `ğ“ (1 : G)` so that `(u (n + 1)) ^ 2 âŠ† u n`, and
  this descends to an antitone neighborhood basis `v` for `ğ“ (1 : G â§¸ N)`. Since `ğ“¤ (G â§¸ N)` is
  countably generated, it suffices to show any Cauchy sequence `x` converges. -/
  letI : uniform_space (G â§¸ N) := topological_group.to_uniform_space (G â§¸ N),
  letI : uniform_space G := topological_group.to_uniform_space G,
  haveI : (ğ“¤ (G â§¸ N)).is_countably_generated := comap.is_countably_generated _ _,
  obtain âŸ¨u, hu, u_mulâŸ© := topological_group.exists_antitone_basis_nhds_one G,
  obtain âŸ¨hv, v_antiâŸ© := @has_antitone_basis.map _ _ _ _ _ _ (coe : G â†’ G â§¸ N) hu,
  rw [â†quotient_group.nhds_eq N 1, quotient_group.coe_one] at hv,
  refine uniform_space.complete_of_cauchy_seq_tendsto (Î» x hx, _),
  /- Given `n : â„•`, for sufficiently large `a b : â„•`, given any lift of `x b`, we can find a lift
  of `x a` such that the quotient of the lifts lies in `u n`. -/
  have keyâ‚€ : âˆ€ i j : â„•, âˆƒ M : â„•,
    j < M âˆ§ âˆ€ a b : â„•, M â‰¤ a â†’ M â‰¤ b â†’ âˆ€ g : G, x b = g â†’ âˆƒ g' : G, g / g' âˆˆ u i âˆ§ x a = g',
  { have hğ“¤GN : (ğ“¤ (G â§¸ N)).has_basis (Î» _, true) (Î» i, {x | x.snd / x.fst âˆˆ coe '' u i}),
    { simpa [uniformity_eq_comap_nhds_one'] using hv.comap _ },
    simp only [hğ“¤GN.cauchy_seq_iff, ge_iff_le, mem_set_of_eq, forall_true_left, mem_image] at hx,
    intros i j,
    rcases hx i with âŸ¨M, hMâŸ©,
    refine âŸ¨max j M + 1, (le_max_left _ _).trans_lt (lt_add_one _), Î» a b ha hb g hg, _âŸ©,
    obtain âŸ¨y, y_mem, hyâŸ© := hM a (((le_max_right j _).trans (lt_add_one _).le).trans ha) b
      (((le_max_right j _).trans (lt_add_one _).le).trans hb),
    refine âŸ¨yâ»Â¹ * g,
      by simpa only [div_eq_mul_inv, mul_inv_rev, inv_inv, mul_inv_cancel_left] using y_mem, _âŸ©,
    rw [quotient_group.coe_mul, quotient_group.coe_inv, hy, hg, inv_div, div_mul_cancel'], },
  /- Inductively construct a subsequence `Ï† : â„• â†’ â„•` using `keyâ‚€` so that if `a b : â„•` exceed
  `Ï† (n + 1)`, then we may find lifts whose quotients lie within `u n`. -/
  set Ï† : â„• â†’ â„• := Î» n, nat.rec_on n (some $ keyâ‚€ 0 0) (Î» k yk, some $ keyâ‚€ (k + 1) yk),
  have hÏ† : âˆ€ n : â„•, Ï† n < Ï† (n + 1) âˆ§ âˆ€ a b : â„•, Ï† (n + 1) â‰¤ a â†’ Ï† (n + 1) â‰¤ b â†’
    (âˆ€ g : G, x b = g â†’ âˆƒ g' : G, g / g' âˆˆ u (n + 1) âˆ§ x a = g'),
    from Î» n, some_spec (keyâ‚€ (n + 1) (Ï† n)),
  /- Inductively construct a sequence `x' n : G` of lifts of `x (Ï† (n + 1))` such that quotients of
  successive terms lie in `x' n / x' (n + 1) âˆˆ u (n + 1)`. We actually need the proofs that each
  term is a lift to construct the next term, so we use a Î£-type. -/
  set x' : Î  n, psigma (Î» g : G, x (Ï† (n + 1)) = g) :=
    Î» n, nat.rec_on n
      âŸ¨some (quotient_group.mk_surjective (x (Ï† 1))),
       (some_spec (quotient_group.mk_surjective (x (Ï† 1)))).symmâŸ©
      (Î» k hk, âŸ¨some $ (hÏ† k).2 _ _ (hÏ† (k + 1)).1.le le_rfl hk.fst hk.snd,
          (some_spec $ (hÏ† k).2 _ _ (hÏ† (k + 1)).1.le le_rfl hk.fst hk.snd).2âŸ©),
  have hx' : âˆ€ n : â„•, (x' n).fst / (x' (n + 1)).fst âˆˆ u (n + 1) :=
    Î» n, (some_spec $ (hÏ† n).2 _ _ (hÏ† (n + 1)).1.le le_rfl (x' n).fst (x' n).snd).1,
  /- The sequence `x'` is Cauchy. This is where we exploit the condition on `u`. The key idea
  is to show by decreasing induction that `x' m / x' n âˆˆ u m` if `m â‰¤ n`. -/
  have x'_cauchy : cauchy_seq (Î» n, (x' n).fst),
  { have hğ“¤G : (ğ“¤ G).has_basis (Î» _, true) (Î» i, {x | x.snd / x.fst âˆˆ u i}),
    { simpa [uniformity_eq_comap_nhds_one'] using hu.to_has_basis.comap _ },
    simp only [hğ“¤G.cauchy_seq_iff', ge_iff_le, mem_set_of_eq, forall_true_left],
    exact Î» m, âŸ¨m, Î» n hmn, nat.decreasing_induction'
      (Î» k hkn hkm hk, u_mul k âŸ¨_, _, hx' k, hk, div_mul_div_cancel' _ _ _âŸ©)
      hmn (by simpa only [div_self'] using mem_of_mem_nhds (hu.mem _))âŸ© },
  /- Since `G` is complete, `x'` converges to some `xâ‚€`, and so the image of this sequence under
  the quotient map converges to `â†‘xâ‚€`. The image of `x'` is a convergent subsequence of `x`, and
  since `x` is Cauchy, this implies it converges. -/
  rcases cauchy_seq_tendsto_of_complete x'_cauchy with âŸ¨xâ‚€, hxâ‚€âŸ©,
  refine âŸ¨â†‘xâ‚€, tendsto_nhds_of_cauchy_seq_of_subseq hx
    (strict_mono_nat_of_lt_succ $ Î» n, (hÏ† (n + 1)).1).tendsto_at_top _âŸ©,
  convert ((continuous_coinduced_rng : continuous (coe : G â†’ G â§¸ N)).tendsto xâ‚€).comp hxâ‚€,
  exact funext (Î» n, (x' n).snd),
end

/-- The quotient `G â§¸ N` of a complete first countable uniform group `G` by a normal subgroup
is itself complete. In constrast to `quotient_group.complete_space'`, in this version `G` is
already equipped with a uniform structure.
[N. Bourbaki, *General Topology*, IX.3.1 Proposition 4][bourbaki1966b]

Even though `G` is equipped with a uniform structure, the quotient `G â§¸ N` does not inherit a
uniform structure, so it is still provided manually via `topological_group.to_uniform_space`.
In the most common use cases, this coincides (definitionally) with the uniform structure on the
quotient obtained via other means.  -/
@[to_additive "The quotient `G â§¸ N` of a complete first countable uniform additive group
`G` by a normal additive subgroup is itself complete. Consequently, quotients of Banach spaces by
subspaces are complete. In constrast to `quotient_add_group.complete_space'`, in this version
`G` is already equipped with a uniform structure.
[N. Bourbaki, *General Topology*, IX.3.1 Proposition 4][bourbaki1966b]

Even though `G` is equipped with a uniform structure, the quotient `G â§¸ N` does not inherit a
uniform structure, so it is still provided manually via `topological_add_group.to_uniform_space`.
In the most common use case â”€ quotients of normed additive commutative groups by subgroups â”€
significant care was taken so that the uniform structure inherent in that setting coincides
(definitionally) with the uniform structure provided here."]
instance quotient_group.complete_space (G : Type u) [group G] [us : uniform_space G]
  [uniform_group G] [first_countable_topology G] (N : subgroup G) [N.normal]
  [hG : complete_space G] : @complete_space (G â§¸ N) (topological_group.to_uniform_space (G â§¸ N)) :=
by { unfreezingI { rw â†@uniform_group.to_uniform_space_eq _ us _ _ at hG }, apply_instance }


end complete_quotient
