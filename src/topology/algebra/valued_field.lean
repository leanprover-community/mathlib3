/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/

import topology.algebra.valuation
import topology.algebra.with_zero_topology
import topology.algebra.uniform_field

/-!
# Valued fields and their completions

In this file we study the topology of a field `K` endowed with a valuation (in our application
to adic spaces, `K` will be the valuation field associated to some valuation on a ring, defined in
valuation.basic).

We already know from valuation.topology that one can build a topology on `K` which
makes it a topological ring.

The first goal is to show `K` is a topological *field*, ie inversion is continuous
at every non-zero element.

The next goal is to prove `K` is a *completable* topological field. This gives us
a completion `hat K` which is a topological field. We also prove that `K` is automatically
separated, so the map from `K` to `hat K` is injective.

Then we extend the valuation given on `K` to a valuation on `hat K`.
-/

open filter set
open_locale topological_space

section division_ring

variables {K : Type*} [division_ring K] {Î“â‚€ : Type*} [linear_ordered_comm_group_with_zero Î“â‚€]

section valuation_topological_division_ring

section inversion_estimate
variables (v : valuation K Î“â‚€)

-- The following is the main technical lemma ensuring that inversion is continuous
-- in the topology induced by a valuation on a division ring (ie the next instance)
-- and the fact that a valued field is completable
-- [BouAC, VI.5.1 Lemme 1]
lemma valuation.inversion_estimate {x y : K} {Î³ : Î“â‚€Ë£} (y_ne : y â‰  0)
  (h : v (x - y) < min (Î³ * ((v y) * (v y))) (v y)) :
  v (xâ»Â¹ - yâ»Â¹) < Î³ :=
begin
  have hyp1 : v (x - y) < Î³ * ((v y) * (v y)),
    from lt_of_lt_of_le h (min_le_left _ _),
  have hyp1' : v (x - y) * ((v y) * (v y))â»Â¹ < Î³,
    from mul_inv_lt_of_lt_mulâ‚€ hyp1,
  have hyp2 : v (x - y) < v y,
    from lt_of_lt_of_le h (min_le_right _ _),
  have key : v x = v y, from valuation.map_eq_of_sub_lt v hyp2,
  have x_ne : x â‰  0,
  { intro h,
    apply y_ne,
    rw [h, v.map_zero] at key,
    exact v.zero_iff.1 key.symm },
  have decomp : xâ»Â¹ - yâ»Â¹ = xâ»Â¹ * (y - x) * yâ»Â¹,
  by rw [mul_sub_left_distrib, sub_mul, mul_assoc,
        show y * yâ»Â¹ = 1, from mul_inv_cancel y_ne,
        show xâ»Â¹ * x = 1, from inv_mul_cancel x_ne, mul_one, one_mul],
  calc
    v (xâ»Â¹ - yâ»Â¹) = v (xâ»Â¹ * (y - x) * yâ»Â¹) : by rw decomp
    ... = (v xâ»Â¹) * (v $ y - x) * (v yâ»Â¹) : by repeat { rw valuation.map_mul }
    ... = (v x)â»Â¹ * (v $ y - x) * (v y)â»Â¹ : by rw [v.map_inv, v.map_inv]
    ... = (v $ y - x) * ((v y) * (v y))â»Â¹ : by
      { rw [mul_assoc, mul_comm, key, mul_assoc, mul_inv_rev] }
    ... = (v $ y - x) * ((v y) * (v y))â»Â¹ : rfl
    ... = (v $ x - y) * ((v y) * (v y))â»Â¹ : by rw valuation.map_sub_swap
    ... < Î³ : hyp1',
end
end inversion_estimate

open valued

/-- The topology coming from a valuation on a division ring makes it a topological division ring
    [BouAC, VI.5.1 middle of Proposition 1] -/
@[priority 100]
instance valued.topological_division_ring [valued K Î“â‚€] : topological_division_ring K :=
{ continuous_at_invâ‚€ :=
    begin
      intros x x_ne s s_in,
      cases valued.mem_nhds.mp s_in with Î³ hs, clear s_in,
      rw [mem_map, valued.mem_nhds],
      change âˆƒ (Î³ : Î“â‚€Ë£), {y : K | (v (y - x) : Î“â‚€) < Î³} âŠ† {x : K | xâ»Â¹ âˆˆ s},
      have vx_ne := (valuation.ne_zero_iff $ v).mpr x_ne,
      let Î³' := units.mk0 _ vx_ne,
      use min (Î³ * (Î³'*Î³')) Î³',
      intros y y_in,
      apply hs,
      simp only [mem_set_of_eq] at y_in,
      rw [units.min_coe, units.coe_mul, units.coe_mul] at y_in,
      exact valuation.inversion_estimate _ x_ne y_in
    end,
  ..(by apply_instance : topological_ring K) }

/-- A valued division ring is separated. -/
@[priority 100]
instance valued_ring.separated [valued K Î“â‚€] : separated_space K :=
begin
  rw separated_iff_t2,
  apply topological_add_group.t2_space_of_zero_sep,
  intros x x_ne,
  refine âŸ¨{k | v k < v x}, _, Î» h, lt_irrefl _ hâŸ©,
  rw valued.mem_nhds,
  have vx_ne := (valuation.ne_zero_iff $ v).mpr x_ne,
  let Î³' := units.mk0 _ vx_ne,
  exact âŸ¨Î³', Î» y hy, by simpa using hyâŸ©,
end

section
local attribute [instance] linear_ordered_comm_group_with_zero.topological_space

open valued

lemma valued.continuous_valuation [valued K Î“â‚€] : continuous (v : K â†’ Î“â‚€) :=
begin
  rw continuous_iff_continuous_at,
  intro x,
  classical,
  by_cases h : x = 0,
  { rw h,
    change tendsto _ _ (ð“ (v (0 : K))),
    erw valuation.map_zero,
    rw linear_ordered_comm_group_with_zero.tendsto_zero,
    intro Î³,
    rw valued.mem_nhds_zero,
    use [Î³, set.subset.refl _] },
  { change tendsto _ _ _,
    have v_ne : (v x : Î“â‚€) â‰  0, from (valuation.ne_zero_iff _).mpr h,
    rw linear_ordered_comm_group_with_zero.tendsto_of_ne_zero v_ne,
    apply valued.loc_const v_ne },
end
end

end valuation_topological_division_ring

end division_ring

namespace valued
open uniform_space

variables {K : Type*} [field K] {Î“â‚€ : Type*} [linear_ordered_comm_group_with_zero Î“â‚€]
  [hv: valued K Î“â‚€]

include hv

local notation `hat ` := completion

/-- A valued field is completable. -/
@[priority 100]
instance completable : completable_top_field K :=
{ nice := begin
    rintros F hF h0,
    have : âˆƒ (Î³â‚€ : Î“â‚€Ë£) (M âˆˆ F), âˆ€ x âˆˆ M, (Î³â‚€ : Î“â‚€) â‰¤ v x,
    { rcases filter.inf_eq_bot_iff.mp h0 with âŸ¨U, U_in, M, M_in, HâŸ©,
      rcases valued.mem_nhds_zero.mp U_in with âŸ¨Î³â‚€, hUâŸ©,
      existsi [Î³â‚€, M, M_in],
      intros x xM,
      apply le_of_not_lt _,
      intro hyp,
      have : x âˆˆ U âˆ© M := âŸ¨hU hyp, xMâŸ©,
      rwa H at this },
    rcases this with âŸ¨Î³â‚€, Mâ‚€, Mâ‚€_in, Hâ‚€âŸ©,
    rw valued.cauchy_iff at hF âŠ¢,
    refine âŸ¨hF.1.map _, _âŸ©,
    replace hF := hF.2,
    intros Î³,
    rcases hF (min (Î³ * Î³â‚€ * Î³â‚€) Î³â‚€) with âŸ¨Mâ‚, Mâ‚_in, Hâ‚âŸ©, clear hF,
    use (Î» x : K, xâ»Â¹) '' (Mâ‚€ âˆ© Mâ‚),
    split,
    { rw mem_map,
      apply mem_of_superset (filter.inter_mem Mâ‚€_in Mâ‚_in),
      exact subset_preimage_image _ _ },
    { rintros _ âŸ¨x, âŸ¨x_inâ‚€, x_inâ‚âŸ©, rflâŸ© _ âŸ¨y, âŸ¨y_inâ‚€, y_inâ‚âŸ©, rflâŸ©,
      simp only [mem_set_of_eq],
      specialize Hâ‚ x x_inâ‚ y y_inâ‚,
      replace x_inâ‚€ := Hâ‚€ x x_inâ‚€,
      replace y_inâ‚€ := Hâ‚€ y y_inâ‚€, clear Hâ‚€,
      apply valuation.inversion_estimate,
      { have : (v x : Î“â‚€) â‰  0,
        { intro h, rw h at x_inâ‚€, simpa using x_inâ‚€, },
        exact (valuation.ne_zero_iff _).mp this },
      { refine lt_of_lt_of_le Hâ‚ _,
        rw units.min_coe,
        apply min_le_min _ x_inâ‚€,
        rw mul_assoc,
        have : ((Î³â‚€ * Î³â‚€ : Î“â‚€Ë£) : Î“â‚€) â‰¤ v x * v x,
          from calc â†‘Î³â‚€ * â†‘Î³â‚€ â‰¤ â†‘Î³â‚€ * v x : mul_le_mul_left' x_inâ‚€ â†‘Î³â‚€
                          ... â‰¤ _ : mul_le_mul_right' x_inâ‚€ (v x),
        rw units.coe_mul,
        exact mul_le_mul_left' this Î³ } }
  end,
  ..valued_ring.separated }

local attribute [instance] linear_ordered_comm_group_with_zero.topological_space

/-- The extension of the valuation of a valued field to the completion of the field. -/
noncomputable def extension : hat K â†’ Î“â‚€ :=
completion.dense_inducing_coe.extend (v : K â†’ Î“â‚€)

lemma continuous_extension : continuous (valued.extension : hat K â†’ Î“â‚€) :=
 begin
  refine completion.dense_inducing_coe.continuous_extend _,
  intro xâ‚€,
  by_cases h : xâ‚€ = coe 0,
  { refine âŸ¨0, _âŸ©,
    erw [h, â† completion.dense_inducing_coe.to_inducing.nhds_eq_comap]; try { apply_instance },
    rw linear_ordered_comm_group_with_zero.tendsto_zero,
    intro Î³â‚€,
    rw valued.mem_nhds,
    exact âŸ¨Î³â‚€, by simpâŸ© },
  { have preimage_one : v â»Â¹' {(1 : Î“â‚€)} âˆˆ ð“ (1 : K),
    { have : (v (1 : K) : Î“â‚€) â‰  0, { rw valuation.map_one, exact zero_ne_one.symm },
      convert valued.loc_const this,
      ext x,
      rw [valuation.map_one, mem_preimage, mem_singleton_iff, mem_set_of_eq] },
    obtain âŸ¨V, V_in, hVâŸ© : âˆƒ V âˆˆ ð“ (1 : hat K), âˆ€ x : K, (x : hat K) âˆˆ V â†’ (v x : Î“â‚€) = 1,
    { rwa [completion.dense_inducing_coe.nhds_eq_comap, mem_comap] at preimage_one },

    have : âˆƒ V' âˆˆ (ð“ (1 : hat K)), (0 : hat K) âˆ‰ V' âˆ§ âˆ€ x y âˆˆ V', x*yâ»Â¹ âˆˆ V,
    { have : tendsto (Î» p : hat K Ã— hat K, p.1*p.2â»Â¹) ((ð“ 1).prod (ð“ 1)) (ð“ 1),
      { rw â† nhds_prod_eq,
        conv {congr, skip, skip, rw â† (one_mul (1 : hat K))},
        refine tendsto.mul continuous_fst.continuous_at
                           (tendsto.comp _ continuous_snd.continuous_at),
        convert continuous_at_invâ‚€ (zero_ne_one.symm : 1 â‰  (0 : hat K)),
        exact inv_one.symm },
      rcases tendsto_prod_self_iff.mp this V V_in with âŸ¨U, U_in, hUâŸ©,
      let hatKstar := ({0}á¶œ : set $ hat K),
      have : hatKstar âˆˆ ð“ (1 : hat K),
      from compl_singleton_mem_nhds zero_ne_one.symm,
      use  [U âˆ© hatKstar, filter.inter_mem U_in this],
      split,
      { rintro âŸ¨h, h'âŸ©,
        rw mem_compl_singleton_iff at h',
        exact h' rfl },
      { rintros x âŸ¨hx, _âŸ© y âŸ¨hy, _âŸ©,
        apply hU ; assumption } },
    rcases this with âŸ¨V', V'_in, zeroV', hV'âŸ©,
    have nhds_right : (Î» x, x*xâ‚€) '' V' âˆˆ ð“ xâ‚€,
    { have l : function.left_inverse (Î» x : hat K, x * xâ‚€â»Â¹) (Î» x : hat K, x * xâ‚€),
      { intro x,
        simp only [mul_assoc, mul_inv_cancel h, mul_one] },
      have r: function.right_inverse (Î» x : hat K, x * xâ‚€â»Â¹) (Î» x : hat K, x * xâ‚€),
      { intro x,
        simp only [mul_assoc, inv_mul_cancel h, mul_one] },
      have c : continuous  (Î» x : hat K, x * xâ‚€â»Â¹),
        from continuous_id.mul continuous_const,
      rw image_eq_preimage_of_inverse l r,
      rw â† mul_inv_cancel h at V'_in,
      exact c.continuous_at V'_in },
    have : âˆƒ (zâ‚€ : K) (yâ‚€ âˆˆ V'), coe zâ‚€ = yâ‚€*xâ‚€ âˆ§ zâ‚€ â‰  0,
    { rcases completion.dense_range_coe.mem_nhds nhds_right with âŸ¨zâ‚€, yâ‚€, yâ‚€_in, H : yâ‚€ * xâ‚€ = zâ‚€âŸ©,
      refine âŸ¨zâ‚€, yâ‚€, yâ‚€_in, âŸ¨H.symm, _âŸ©âŸ©,
      rintro rfl,
      exact mul_ne_zero (ne_of_mem_of_not_mem yâ‚€_in zeroV') h H },
    rcases this with âŸ¨zâ‚€, yâ‚€, yâ‚€_in, hzâ‚€, zâ‚€_neâŸ©,
    have vzâ‚€_ne: (v zâ‚€ : Î“â‚€) â‰  0 := by rwa valuation.ne_zero_iff,
    refine âŸ¨v zâ‚€, _âŸ©,
    rw [linear_ordered_comm_group_with_zero.tendsto_of_ne_zero vzâ‚€_ne, mem_comap],
    use [(Î» x, x*xâ‚€) '' V', nhds_right],
    intros x x_in,
    rcases mem_preimage.1 x_in with âŸ¨y, y_in, hyâŸ©, clear x_in,
    change y*xâ‚€ = coe x at hy,
    have : (v (x*zâ‚€â»Â¹) : Î“â‚€) = 1,
    { apply hV,
      have : ((zâ‚€â»Â¹ : K) : hat K) = zâ‚€â»Â¹,
      from ring_hom.map_inv (completion.coe_ring_hom : K â†’+* hat K) zâ‚€,
      rw [completion.coe_mul, this, â† hy, hzâ‚€, mul_inv, mul_comm yâ‚€â»Â¹, â† mul_assoc, mul_assoc y,
          mul_inv_cancel h, mul_one],
      solve_by_elim },
    calc v x = v (x*zâ‚€â»Â¹*zâ‚€) : by rw [mul_assoc, inv_mul_cancel zâ‚€_ne, mul_one]
         ... = v (x*zâ‚€â»Â¹)*v zâ‚€ : valuation.map_mul _ _ _
         ... = v zâ‚€ : by rw [this, one_mul]  },
end

@[simp, norm_cast]
lemma extension_extends (x : K) : extension (x : hat K) = v x :=
begin
  haveI : t2_space Î“â‚€ := regular_space.t2_space _,
  refine completion.dense_inducing_coe.extend_eq_of_tendsto _,
  rw â† completion.dense_inducing_coe.nhds_eq_comap,
  exact valued.continuous_valuation.continuous_at,
end

/-- the extension of a valuation on a division ring to its completion. -/
noncomputable def extension_valuation :
  valuation (hat K) Î“â‚€ :=
{ to_fun := valued.extension,
  map_zero' := by { rw [â† v.map_zero, â† valued.extension_extends (0 : K)], refl, },
  map_one' := by { rw [â† completion.coe_one, valued.extension_extends (1 : K)],
                   exact valuation.map_one _ },
  map_mul' := Î» x y, begin
    apply completion.induction_onâ‚‚ x y,
    { have c1 : continuous (Î» (x : hat K Ã— hat K), valued.extension (x.1 * x.2)),
        from valued.continuous_extension.comp (continuous_fst.mul continuous_snd),

      have c2 : continuous (Î» (x : hat K Ã— hat K), valued.extension x.1 * valued.extension x.2),
        from (valued.continuous_extension.comp continuous_fst).mul
              (valued.continuous_extension.comp continuous_snd),
      exact is_closed_eq c1 c2 },
    { intros x y,
      norm_cast,
      exact valuation.map_mul _ _ _ },
  end,
  map_add_le_max' := Î» x y, begin
    rw le_max_iff,
    apply completion.induction_onâ‚‚ x y,
    { have cont : continuous (valued.extension : hat K â†’ Î“â‚€) := valued.continuous_extension,
      exact  (is_closed_le (cont.comp continuous_add) $ cont.comp continuous_fst).union
        (is_closed_le (cont.comp continuous_add) $ cont.comp continuous_snd) },
    { intros x y,
      dsimp,
      norm_cast,
      rw â† le_max_iff,
      exact v.map_add x y, },
  end }

-- Bourbaki CA VI Â§5 no.3 Proposition 5 (d)
lemma closure_coe_completion_v_lt {Î³ : Î“â‚€Ë£} :
  closure (coe '' { x : K | v x < (Î³ : Î“â‚€) }) = { x : hat K | extension_valuation x < (Î³ : Î“â‚€) } :=
begin
  ext x,
  let Î³â‚€ := extension_valuation x,
  suffices : Î³â‚€ â‰  0 â†’ (x âˆˆ closure (coe '' { x : K | v x < (Î³ : Î“â‚€) }) â†” Î³â‚€ < (Î³ : Î“â‚€)),
  { cases eq_or_ne Î³â‚€ 0,
    { simp only [h, (valuation.zero_iff _).mp h, mem_set_of_eq, valuation.map_zero, units.zero_lt,
        iff_true],
      apply subset_closure,
      exact âŸ¨0, by simpa only [mem_set_of_eq, valuation.map_zero, units.zero_lt, true_and]âŸ©, },
    { exact this h, }, },
  intros h,
  have hÎ³â‚€ : extension â»Â¹' {Î³â‚€} âˆˆ ð“ x := continuous_extension.continuous_at.preimage_mem_nhds
    (linear_ordered_comm_group_with_zero.singleton_mem_nhds_of_ne_zero h),
  rw mem_closure_iff_nhds',
  refine âŸ¨Î» hx, _, Î» hx s hs, _âŸ©,
  { obtain âŸ¨âŸ¨-, y, hyâ‚ : v y < (Î³ : Î“â‚€), rflâŸ©, hyâ‚‚âŸ© := hx _ hÎ³â‚€,
    replace hyâ‚‚ : v y = Î³â‚€, { simpa using hyâ‚‚, },
    rwa â† hyâ‚‚, },
  { obtain âŸ¨y, hyâ‚, hyâ‚‚ : â†‘y âˆˆ sâŸ© := completion.dense_range_coe.mem_nhds (inter_mem hÎ³â‚€ hs),
    replace hyâ‚ : v y = Î³â‚€, { simpa using hyâ‚, },
    rw â† hyâ‚ at hx,
    exact âŸ¨âŸ¨y, âŸ¨y, hx, rflâŸ©âŸ©, hyâ‚‚âŸ©, },
end

noncomputable instance valued_completion : valued (hat K) Î“â‚€ :=
{ v := extension_valuation,
  is_topological_valuation := Î» s,
  begin
    suffices : has_basis (ð“ (0 : hat K)) (Î» _, true) (Î» Î³ : Î“â‚€Ë£, { x | extension_valuation x < Î³ }),
    { rw this.mem_iff,
      exact exists_congr (Î» Î³, by simp), },
    simp_rw â† closure_coe_completion_v_lt,
    exact (has_basis_nhds_zero K Î“â‚€).has_basis_of_dense_inducing completion.dense_inducing_coe,
  end }

end valued
