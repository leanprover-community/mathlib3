/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/

import topology.algebra.valuation
import topology.algebra.with_zero_topology
import topology.algebra.uniform_field

/-!
# Valued fields and their completions

In this file we study the topology of a field `K` endowed with a valuation (in our application
to adic spaces, `K` will be the valuation field associated to some valuation on a ring, defined in
valuation.basic).

We already know from valuation.topology that one can build a topology on `K` which
makes it a topological ring.

The first goal is to show `K` is a topological *field*, ie inversion is continuous
at every non-zero element.

The next goal is to prove `K` is a *completable* topological field. This gives us
a completion `hat K` which is a topological field. We also prove that `K` is automatically
separated, so the map from `K` to `hat K` is injective.

Then we extend the valuation given on `K` to a valuation on `hat K`.
-/

section move_to_correct_file

open uniform_space
open_locale topological_space

-- Bourbaki GT III ¬ß3 no.4 Proposition 7 [I hope]
lemma filter.has_basis.completion_nhds_zero {G Œπ : Type*}
  [add_group G] [uniform_space G] [uniform_add_group G] {s : Œπ ‚Üí set G} {p : Œπ ‚Üí Prop}
  (h : (ùìù (0 : G)).has_basis p s) :
  (ùìù (0 : completion G)).has_basis p $ Œª i, closure $ coe '' (s i) :=
sorry

end move_to_correct_file

open filter set
open_locale topological_space

section division_ring

variables {K : Type*} [division_ring K] {Œì‚ÇÄ : Type*} [linear_ordered_comm_group_with_zero Œì‚ÇÄ]

section valuation_topological_division_ring

section inversion_estimate
variables (v : valuation K Œì‚ÇÄ)

-- The following is the main technical lemma ensuring that inversion is continuous
-- in the topology induced by a valuation on a division ring (ie the next instance)
-- and the fact that a valued field is completable
-- [BouAC, VI.5.1 Lemme 1]
lemma valuation.inversion_estimate {x y : K} {Œ≥ : Œì‚ÇÄÀ£} (y_ne : y ‚â† 0)
  (h : v (x - y) < min (Œ≥ * ((v y) * (v y))) (v y)) :
  v (x‚Åª¬π - y‚Åª¬π) < Œ≥ :=
begin
  have hyp1 : v (x - y) < Œ≥ * ((v y) * (v y)),
    from lt_of_lt_of_le h (min_le_left _ _),
  have hyp1' : v (x - y) * ((v y) * (v y))‚Åª¬π < Œ≥,
    from mul_inv_lt_of_lt_mul‚ÇÄ hyp1,
  have hyp2 : v (x - y) < v y,
    from lt_of_lt_of_le h (min_le_right _ _),
  have key : v x = v y, from valuation.map_eq_of_sub_lt v hyp2,
  have x_ne : x ‚â† 0,
  { intro h,
    apply y_ne,
    rw [h, v.map_zero] at key,
    exact v.zero_iff.1 key.symm },
  have decomp : x‚Åª¬π - y‚Åª¬π = x‚Åª¬π * (y - x) * y‚Åª¬π,
  by rw [mul_sub_left_distrib, sub_mul, mul_assoc,
        show y * y‚Åª¬π = 1, from mul_inv_cancel y_ne,
        show x‚Åª¬π * x = 1, from inv_mul_cancel x_ne, mul_one, one_mul],
  calc
    v (x‚Åª¬π - y‚Åª¬π) = v (x‚Åª¬π * (y - x) * y‚Åª¬π) : by rw decomp
    ... = (v x‚Åª¬π) * (v $ y - x) * (v y‚Åª¬π) : by repeat { rw valuation.map_mul }
    ... = (v x)‚Åª¬π * (v $ y - x) * (v y)‚Åª¬π : by rw [v.map_inv, v.map_inv]
    ... = (v $ y - x) * ((v y) * (v y))‚Åª¬π : by
      { rw [mul_assoc, mul_comm, key, mul_assoc, mul_inv_rev‚ÇÄ] }
    ... = (v $ y - x) * ((v y) * (v y))‚Åª¬π : rfl
    ... = (v $ x - y) * ((v y) * (v y))‚Åª¬π : by rw valuation.map_sub_swap
    ... < Œ≥ : hyp1',
end
end inversion_estimate

open valued

/-- The topology coming from a valuation on a division ring makes it a topological division ring
    [BouAC, VI.5.1 middle of Proposition 1] -/
@[priority 100]
instance valued.topological_division_ring [valued K Œì‚ÇÄ] : topological_division_ring K :=
{ continuous_at_inv‚ÇÄ :=
    begin
      intros x x_ne s s_in,
      cases valued.mem_nhds.mp s_in with Œ≥ hs, clear s_in,
      rw [mem_map, valued.mem_nhds],
      change ‚àÉ (Œ≥ : Œì‚ÇÄÀ£), {y : K | (v (y - x) : Œì‚ÇÄ) < Œ≥} ‚äÜ {x : K | x‚Åª¬π ‚àà s},
      have vx_ne := (valuation.ne_zero_iff $ v).mpr x_ne,
      let Œ≥' := units.mk0 _ vx_ne,
      use min (Œ≥ * (Œ≥'*Œ≥')) Œ≥',
      intros y y_in,
      apply hs,
      simp only [mem_set_of_eq] at y_in,
      rw [units.min_coe, units.coe_mul, units.coe_mul] at y_in,
      exact valuation.inversion_estimate _ x_ne y_in
    end,
  ..(by apply_instance : topological_ring K) }

/-- A valued division ring is separated. -/
@[priority 100]
instance valued_ring.separated [valued K Œì‚ÇÄ] : separated_space K :=
begin
  apply valued.separated_of_zero_sep,
  intros x x_ne,
  refine ‚ü®{k | v k < v x}, _, Œª h, lt_irrefl _ h‚ü©,
  rw valued.mem_nhds,
  have vx_ne := (valuation.ne_zero_iff $ v).mpr x_ne,
  let Œ≥' := units.mk0 _ vx_ne,
  exact ‚ü®Œ≥', Œª y hy, by simpa using hy‚ü©,
end

section
local attribute [instance] linear_ordered_comm_group_with_zero.topological_space

open valued

lemma valued.continuous_valuation [valued K Œì‚ÇÄ] : continuous (v : K ‚Üí Œì‚ÇÄ) :=
begin
  rw continuous_iff_continuous_at,
  intro x,
  classical,
  by_cases h : x = 0,
  { rw h,
    change tendsto _ _ (ùìù (v (0 : K))),
    erw valuation.map_zero,
    rw linear_ordered_comm_group_with_zero.tendsto_zero,
    intro Œ≥,
    rw valued.mem_nhds_zero,
    use [Œ≥, set.subset.refl _] },
  { change tendsto _ _ _,
    have v_ne : (v x : Œì‚ÇÄ) ‚â† 0, from (valuation.ne_zero_iff _).mpr h,
    rw linear_ordered_comm_group_with_zero.tendsto_of_ne_zero v_ne,
    apply valued.loc_const v_ne },
end
end

end valuation_topological_division_ring

end division_ring

namespace valued
open uniform_space

variables {K : Type*} [field K] {Œì‚ÇÄ : Type*} [linear_ordered_comm_group_with_zero Œì‚ÇÄ]
  [hv: valued K Œì‚ÇÄ]

include hv

local notation `hat ` := completion

/-- A valued field is completable. -/
@[priority 100]
instance completable : completable_top_field K :=
{ nice := begin
    rintros F hF h0,
    have : ‚àÉ (Œ≥‚ÇÄ : Œì‚ÇÄÀ£) (M ‚àà F), ‚àÄ x ‚àà M, (Œ≥‚ÇÄ : Œì‚ÇÄ) ‚â§ v x,
    { rcases filter.inf_eq_bot_iff.mp h0 with ‚ü®U, U_in, M, M_in, H‚ü©,
      rcases valued.mem_nhds_zero.mp U_in with ‚ü®Œ≥‚ÇÄ, hU‚ü©,
      existsi [Œ≥‚ÇÄ, M, M_in],
      intros x xM,
      apply le_of_not_lt _,
      intro hyp,
      have : x ‚àà U ‚à© M := ‚ü®hU hyp, xM‚ü©,
      rwa H at this },
    rcases this with ‚ü®Œ≥‚ÇÄ, M‚ÇÄ, M‚ÇÄ_in, H‚ÇÄ‚ü©,
    rw valued.cauchy_iff at hF ‚ä¢,
    refine ‚ü®hF.1.map _, _‚ü©,
    replace hF := hF.2,
    intros Œ≥,
    rcases hF (min (Œ≥ * Œ≥‚ÇÄ * Œ≥‚ÇÄ) Œ≥‚ÇÄ) with ‚ü®M‚ÇÅ, M‚ÇÅ_in, H‚ÇÅ‚ü©, clear hF,
    use (Œª x : K, x‚Åª¬π) '' (M‚ÇÄ ‚à© M‚ÇÅ),
    split,
    { rw mem_map,
      apply mem_of_superset (filter.inter_mem M‚ÇÄ_in M‚ÇÅ_in),
      exact subset_preimage_image _ _ },
    { rintros _ ‚ü®x, ‚ü®x_in‚ÇÄ, x_in‚ÇÅ‚ü©, rfl‚ü© _ ‚ü®y, ‚ü®y_in‚ÇÄ, y_in‚ÇÅ‚ü©, rfl‚ü©,
      simp only [mem_set_of_eq],
      specialize H‚ÇÅ x x_in‚ÇÅ y y_in‚ÇÅ,
      replace x_in‚ÇÄ := H‚ÇÄ x x_in‚ÇÄ,
      replace y_in‚ÇÄ := H‚ÇÄ y y_in‚ÇÄ, clear H‚ÇÄ,
      apply valuation.inversion_estimate,
      { have : (v x : Œì‚ÇÄ) ‚â† 0,
        { intro h, rw h at x_in‚ÇÄ, simpa using x_in‚ÇÄ, },
        exact (valuation.ne_zero_iff _).mp this },
      { refine lt_of_lt_of_le H‚ÇÅ _,
        rw units.min_coe,
        apply min_le_min _ x_in‚ÇÄ,
        rw mul_assoc,
        have : ((Œ≥‚ÇÄ * Œ≥‚ÇÄ : Œì‚ÇÄÀ£) : Œì‚ÇÄ) ‚â§ v x * v x,
          from calc ‚ÜëŒ≥‚ÇÄ * ‚ÜëŒ≥‚ÇÄ ‚â§ ‚ÜëŒ≥‚ÇÄ * v x : mul_le_mul_left' x_in‚ÇÄ ‚ÜëŒ≥‚ÇÄ
                          ... ‚â§ _ : mul_le_mul_right' x_in‚ÇÄ (v x),
        rw units.coe_mul,
        exact mul_le_mul_left' this Œ≥ } }
  end,
  ..valued_ring.separated }

local attribute [instance] linear_ordered_comm_group_with_zero.topological_space

/-- The extension of the valuation of a valued field to the completion of the field. -/
noncomputable def extension : hat K ‚Üí Œì‚ÇÄ :=
completion.dense_inducing_coe.extend (v : K ‚Üí Œì‚ÇÄ)

lemma continuous_extension : continuous (valued.extension : hat K ‚Üí Œì‚ÇÄ) :=
 begin
  refine completion.dense_inducing_coe.continuous_extend _,
  intro x‚ÇÄ,
  by_cases h : x‚ÇÄ = coe 0,
  { refine ‚ü®0, _‚ü©,
    erw [h, ‚Üê completion.dense_inducing_coe.to_inducing.nhds_eq_comap]; try { apply_instance },
    rw linear_ordered_comm_group_with_zero.tendsto_zero,
    intro Œ≥‚ÇÄ,
    rw valued.mem_nhds,
    exact ‚ü®Œ≥‚ÇÄ, by simp‚ü© },
  { have preimage_one : v ‚Åª¬π' {(1 : Œì‚ÇÄ)} ‚àà ùìù (1 : K),
    { have : (v (1 : K) : Œì‚ÇÄ) ‚â† 0, { rw valuation.map_one, exact zero_ne_one.symm },
      convert valued.loc_const this,
      ext x,
      rw [valuation.map_one, mem_preimage, mem_singleton_iff, mem_set_of_eq] },
    obtain ‚ü®V, V_in, hV‚ü© : ‚àÉ V ‚àà ùìù (1 : hat K), ‚àÄ x : K, (x : hat K) ‚àà V ‚Üí (v x : Œì‚ÇÄ) = 1,
    { rwa [completion.dense_inducing_coe.nhds_eq_comap, mem_comap] at preimage_one },

    have : ‚àÉ V' ‚àà (ùìù (1 : hat K)), (0 : hat K) ‚àâ V' ‚àß ‚àÄ x y ‚àà V', x*y‚Åª¬π ‚àà V,
    { have : tendsto (Œª p : hat K √ó hat K, p.1*p.2‚Åª¬π) ((ùìù 1).prod (ùìù 1)) (ùìù 1),
      { rw ‚Üê nhds_prod_eq,
        conv {congr, skip, skip, rw ‚Üê (one_mul (1 : hat K))},
        refine tendsto.mul continuous_fst.continuous_at
                           (tendsto.comp _ continuous_snd.continuous_at),
        convert continuous_at_inv‚ÇÄ (zero_ne_one.symm : 1 ‚â† (0 : hat K)),
        exact inv_one.symm },
      rcases tendsto_prod_self_iff.mp this V V_in with ‚ü®U, U_in, hU‚ü©,
      let hatKstar := ({0}·∂ú : set $ hat K),
      have : hatKstar ‚àà ùìù (1 : hat K),
      from compl_singleton_mem_nhds zero_ne_one.symm,
      use  [U ‚à© hatKstar, filter.inter_mem U_in this],
      split,
      { rintro ‚ü®h, h'‚ü©,
        rw mem_compl_singleton_iff at h',
        exact h' rfl },
      { rintros x ‚ü®hx, _‚ü© y ‚ü®hy, _‚ü©,
        apply hU ; assumption } },
    rcases this with ‚ü®V', V'_in, zeroV', hV'‚ü©,
    have nhds_right : (Œª x, x*x‚ÇÄ) '' V' ‚àà ùìù x‚ÇÄ,
    { have l : function.left_inverse (Œª x : hat K, x * x‚ÇÄ‚Åª¬π) (Œª x : hat K, x * x‚ÇÄ),
      { intro x,
        simp only [mul_assoc, mul_inv_cancel h, mul_one] },
      have r: function.right_inverse (Œª x : hat K, x * x‚ÇÄ‚Åª¬π) (Œª x : hat K, x * x‚ÇÄ),
      { intro x,
        simp only [mul_assoc, inv_mul_cancel h, mul_one] },
      have c : continuous  (Œª x : hat K, x * x‚ÇÄ‚Åª¬π),
        from continuous_id.mul continuous_const,
      rw image_eq_preimage_of_inverse l r,
      rw ‚Üê mul_inv_cancel h at V'_in,
      exact c.continuous_at V'_in },
    have : ‚àÉ (z‚ÇÄ : K) (y‚ÇÄ ‚àà V'), coe z‚ÇÄ = y‚ÇÄ*x‚ÇÄ ‚àß z‚ÇÄ ‚â† 0,
    { rcases completion.dense_range_coe.mem_nhds nhds_right with ‚ü®z‚ÇÄ, y‚ÇÄ, y‚ÇÄ_in, H : y‚ÇÄ * x‚ÇÄ = z‚ÇÄ‚ü©,
      refine ‚ü®z‚ÇÄ, y‚ÇÄ, y‚ÇÄ_in, ‚ü®H.symm, _‚ü©‚ü©,
      rintro rfl,
      exact mul_ne_zero (ne_of_mem_of_not_mem y‚ÇÄ_in zeroV') h H },
    rcases this with ‚ü®z‚ÇÄ, y‚ÇÄ, y‚ÇÄ_in, hz‚ÇÄ, z‚ÇÄ_ne‚ü©,
    have vz‚ÇÄ_ne: (v z‚ÇÄ : Œì‚ÇÄ) ‚â† 0 := by rwa valuation.ne_zero_iff,
    refine ‚ü®v z‚ÇÄ, _‚ü©,
    rw [linear_ordered_comm_group_with_zero.tendsto_of_ne_zero vz‚ÇÄ_ne, mem_comap],
    use [(Œª x, x*x‚ÇÄ) '' V', nhds_right],
    intros x x_in,
    rcases mem_preimage.1 x_in with ‚ü®y, y_in, hy‚ü©, clear x_in,
    change y*x‚ÇÄ = coe x at hy,
    have : (v (x*z‚ÇÄ‚Åª¬π) : Œì‚ÇÄ) = 1,
    { apply hV,
      have : ((z‚ÇÄ‚Åª¬π : K) : hat K) = z‚ÇÄ‚Åª¬π,
      from ring_hom.map_inv (completion.coe_ring_hom : K ‚Üí+* hat K) z‚ÇÄ,
      rw [completion.coe_mul, this, ‚Üê hy, hz‚ÇÄ, mul_inv‚ÇÄ, mul_comm y‚ÇÄ‚Åª¬π, ‚Üê mul_assoc, mul_assoc y,
          mul_inv_cancel h, mul_one],
      solve_by_elim },
    calc v x = v (x*z‚ÇÄ‚Åª¬π*z‚ÇÄ) : by rw [mul_assoc, inv_mul_cancel z‚ÇÄ_ne, mul_one]
         ... = v (x*z‚ÇÄ‚Åª¬π)*v z‚ÇÄ : valuation.map_mul _ _ _
         ... = v z‚ÇÄ : by rw [this, one_mul]  },
end

@[simp, norm_cast]
lemma extension_extends (x : K) : extension (x : hat K) = v x :=
begin
  haveI : t2_space Œì‚ÇÄ := regular_space.t2_space _,
  refine completion.dense_inducing_coe.extend_eq_of_tendsto _,
  rw ‚Üê completion.dense_inducing_coe.nhds_eq_comap,
  exact valued.continuous_valuation.continuous_at,
end

/-- the extension of a valuation on a division ring to its completion. -/
noncomputable def extension_valuation :
  valuation (hat K) Œì‚ÇÄ :=
{ to_fun := valued.extension,
  map_zero' := by { rw [‚Üê v.map_zero, ‚Üê valued.extension_extends (0 : K)], refl, },
  map_one' := by { rw [‚Üê completion.coe_one, valued.extension_extends (1 : K)],
                   exact valuation.map_one _ },
  map_mul' := Œª x y, begin
    apply completion.induction_on‚ÇÇ x y,
    { have c1 : continuous (Œª (x : hat K √ó hat K), valued.extension (x.1 * x.2)),
        from valued.continuous_extension.comp (continuous_fst.mul continuous_snd),

      have c2 : continuous (Œª (x : hat K √ó hat K), valued.extension x.1 * valued.extension x.2),
        from (valued.continuous_extension.comp continuous_fst).mul
              (valued.continuous_extension.comp continuous_snd),
      exact is_closed_eq c1 c2 },
    { intros x y,
      norm_cast,
      exact valuation.map_mul _ _ _ },
  end,
  map_add_le_max' := Œª x y, begin
    rw le_max_iff,
    apply completion.induction_on‚ÇÇ x y,
    { have cont : continuous (valued.extension : hat K ‚Üí Œì‚ÇÄ) := valued.continuous_extension,
      exact  (is_closed_le (cont.comp continuous_add) $ cont.comp continuous_fst).union
        (is_closed_le (cont.comp continuous_add) $ cont.comp continuous_snd) },
    { intros x y,
      dsimp,
      norm_cast,
      rw ‚Üê le_max_iff,
      exact v.map_add x y, },
  end }

-- Bourbaki CA VI ¬ß5 no.3 Proposition 5 (d) [I hope]
lemma closure_v_lt {Œ≥ : Œì‚ÇÄÀ£} :
  closure (coe '' { x : K | v x < (Œ≥ : Œì‚ÇÄ) }) = { x : hat K | extension_valuation x < (Œ≥ : Œì‚ÇÄ) } :=
begin
  refine le_antisymm (Œª x hx, _) (Œª x hx, _),
  { -- TODO Golf this ridiculous proof!
    let Œ≥‚ÇÄ := extension_valuation x,
    change Œ≥‚ÇÄ < (Œ≥ : Œì‚ÇÄ),
    cases eq_or_ne Œ≥‚ÇÄ 0,
    { simp [h], },
    { have hŒ≥‚ÇÄ : is_open ({ Œ≥‚ÇÄ } : set Œì‚ÇÄ),
      { simp only [is_open_iff_mem_nhds, mem_singleton_iff, forall_eq],
        apply (linear_ordered_comm_group_with_zero.has_basis_nhds_of_ne_zero h).mem_of_mem
          true.intro,
        exact unit.star, },
      let u := (extension_valuation : hat K ‚Üí Œì‚ÇÄ)‚Åª¬π' { Œ≥‚ÇÄ },
      have hu : x ‚àà u, { simp, },
      obtain ‚ü®-, hy‚ÇÅ, y, hy‚ÇÇ, rfl‚ü© :=
        mem_closure_iff.mp hx u (continuous_extension.is_open_preimage _ hŒ≥‚ÇÄ) hu,
      replace hy‚ÇÅ : v y = Œ≥‚ÇÄ, { rw ‚Üê extension_extends y, simp at hy‚ÇÅ, exact hy‚ÇÅ, },
      rw ‚Üê hy‚ÇÅ,
      exact hy‚ÇÇ, }, },
  { -- Oh, it's basically the same argument. OK tidy up later.
    sorry, },
end

noncomputable instance valued_completion : valued (hat K) Œì‚ÇÄ :=
{ v := valued.extension_valuation,
  is_uniform_valuation := Œª s,
  begin
    suffices : has_basis (ùìù (0 : hat K)) (Œª _, true) (Œª (Œ≥ : Œì‚ÇÄÀ£),
      { x | valued.extension_valuation x < (Œ≥ : Œì‚ÇÄ) }),
    { simp only [uniformity_eq_comap_nhds_zero, mem_comap],
      split,
      { rintros ‚ü®n, hn, hns‚ü©,
        obtain ‚ü®Œ≥, -, hŒ≥‚ü© := this.mem_iff.mp hn,
        exact ‚ü®Œ≥, subset.trans (preimage_mono hŒ≥) hns‚ü©, },
      { rintros ‚ü®Œ≥, hŒ≥‚ü©,
        exact ‚ü®{ x | valued.extension_valuation x < (Œ≥ : Œì‚ÇÄ) },
          this.mem_iff.mpr ‚ü®Œ≥, trivial, subset.rfl‚ü©, subset.trans subset.rfl hŒ≥‚ü©, }, },
    simp_rw ‚Üê valued.closure_v_lt,
    exact (valued.has_basis_nhds_zero K Œì‚ÇÄ).completion_nhds_zero,
  end }

end valued
