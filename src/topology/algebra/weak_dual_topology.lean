/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import topology.algebra.module

/-!
# Weak dual topology

This file defines the weak-* topology on duals of suitable topological modules `E` over suitable
topological semirings `ğ•œ`. The (weak) dual consists of continuous linear functionals `E â†’L[ğ•œ] ğ•œ`
from `E` to scalars `ğ•œ`. The weak-* topology is the coarsest topology on this dual
`weak_dual ğ•œ E := (E â†’L[ğ•œ] ğ•œ)` w.r.t. which the evaluation maps at all `z : E` are continuous.

The weak dual is a module over `ğ•œ` if the semiring `ğ•œ` is commutative.

## Main definitions

The main definitions are the type `weak_dual ğ•œ E` and a topology instance on it.

* `weak_dual ğ•œ E` is a type synonym for `dual ğ•œ E` (when the latter is defined), both are equal to
  the type `E â†’L[ğ•œ] ğ•œ` of continuous linear maps from a module `E` over `ğ•œ` to the ring `ğ•œ`.
* the instance `topological_space (weak_dual ğ•œ E)` is the weak-* topology on `weak_dual ğ•œ E`, i.e.,
  the coarsest topology making the evaluation maps at all `z : E` are continuous.

## Main results

We establish that `weak_dual ğ•œ E` has the following structure:
* The addition in `weak_dual ğ•œ E` is continuous, i.e. we have `has_continuous_add (weak_dual ğ•œ E)`.
* If the scalars `ğ•œ` are a commutative semiring, then `weak_dual ğ•œ E` is a module over `ğ•œ`.
* If the scalars `ğ•œ` are a commutative semiring, then the scalar multiplication by `ğ•œ` in
  `weak_dual ğ•œ E` is continuous, i.e. we have `has_continuous_smul ğ•œ (weak_dual ğ•œ E)`.

We prove the following results characterizing the weak-* topology:
* `eval_continuous` shows that for any `z : E`, the evaluation mapping `weak_dual ğ•œ E â†’ ğ•œ`
  taking `x'`to `x' z` is continuous.
* `continuous_of_continuous_eval` shows that for a mapping to `weak_dual ğ•œ E â†’ ğ•œ` to be continuous,
  it is sufficient that its compositions with evaluations at all points `z : E` are continuous
* `tendsto_iff_forall_eval_tendsto` is a characterization of convergence in `weak_dual ğ•œ E` in
  terms of convergence of the evaluations at all points `z : E`

## Notations

No new notation is introduced.

## Implementation notes

The weak-* topology is defined as the induced topology under the mapping that associates to a dual
element `x'` the functional `E â†’ ğ•œ`, when the space `E â†’ ğ•œ` of functionals is equipped with the
topology of pointwise convergence (product topology).

Typically one might assumes that `ğ•œ` is a topological semiring in the sense of the typeclasses
 `topological_space ğ•œ`, `semiring ğ•œ`, `has_continuous_add ğ•œ`, `has_continuous_mul ğ•œ`,
and that the space `E` is a topological module over `ğ•œ` in the sense of the typeclasses
`topological_space E`, `add_comm_monoid E`, `has_continuous_add E`, `module ğ•œ E`,
`has_continuous_smul ğ•œ E`. The definitions and results are, however, given with suitable subsets
of these assumptions.

## References

* https://en.wikipedia.org/wiki/Weak_topology#Weak-*_topology

## Tags

weak-star, weak dual

-/

noncomputable theory
open filter
open_locale topological_space

section weak_star_topology
/-!
### Weak star topology on duals of topological modules
In this section, we define the weak-* topology on duals of suitable topological modules `E` over
suitable topological semirings `ğ•œ`. The (weak) dual `weak_dual ğ•œ E` consists of continuous linear
functionals `E â†’L[ğ•œ] ğ•œ` from `E` to scalars `ğ•œ`. The weak-* topology is the coarsest topology on
this dual `weak_dual ğ•œ E := (E â†’L[ğ•œ] ğ•œ)` w.r.t. which the evaluation maps at all `z : E` are
continuous.

The weak dual is a module over `ğ•œ` if the semiring `ğ•œ` is commutative.
-/

universe variables u v
variables (ğ•œ : Type*) [topological_space ğ•œ] [semiring ğ•œ]
variables (E : Type*) [topological_space E] [add_comm_monoid E] [module ğ•œ E]

/-- The (weak) dual of a topological module `E` over a topological semiring `ğ•œ` consists of
continuous linear functionals from `E` to scalars `ğ•œ`. It is a type synonym with the original
dual, but will be equipped with a different topology. -/
@[derive [inhabited, has_coe_to_fun]]
def weak_dual := E â†’L[ğ•œ] ğ•œ

instance [has_continuous_add ğ•œ] : add_comm_monoid (weak_dual ğ•œ E) :=
continuous_linear_map.add_comm_monoid

namespace weak_dual

/-- The weak-* topology instance `weak_dual_topology` on the dual of a topological module `E` over
a topological semiring `ğ•œ` is defined as the induced topology under the mapping that associates to
a dual element `x' : weak_dual ğ•œ E` the functional `E â†’ ğ•œ`, when the space `E â†’ ğ•œ` of functionals
is equipped with the topology of pointwise convergence (product topology). -/
instance : topological_space (weak_dual ğ•œ E) :=
topological_space.induced (Î» x' : weak_dual ğ•œ E, Î» z : E, x' z) Pi.topological_space

lemma coe_fn_continuous :
  continuous (Î» (x' : (weak_dual ğ•œ E)), (Î» (z : E), x' z)) :=
continuous_induced_dom

lemma eval_continuous (z : E) : continuous (Î» (x' : weak_dual ğ•œ E), x' z) :=
(continuous_pi_iff.mp (coe_fn_continuous ğ•œ E)) z

lemma continuous_of_continuous_eval {Î± : Type u} [topological_space Î±]
  {g : Î± â†’ weak_dual ğ•œ E} (h : âˆ€ z, continuous (Î» a, g a z)) : continuous g :=
continuous_induced_rng (continuous_pi_iff.mpr h)

theorem tendsto_iff_forall_eval_tendsto {Î³ : Type u} {F : filter Î³}
  {Ïˆs : Î³ â†’ weak_dual ğ•œ E} {Ïˆ : weak_dual ğ•œ E} :
  tendsto Ïˆs F (ğ“ Ïˆ) â†” âˆ€ z : E, tendsto (Î» i, Ïˆs i z) F (ğ“ (Ïˆ z)) :=
begin
  rw â†tendsto_pi,
  split,
  { intros weak_star_conv,
    exact (((coe_fn_continuous ğ•œ E).tendsto Ïˆ).comp weak_star_conv), },
  { intro h_lim_forall,
    rwa [nhds_induced, tendsto_comap_iff], },
end

/-- Addition in `weak_dual ğ•œ E` is continuous. -/
instance [has_continuous_add ğ•œ] : has_continuous_add (weak_dual ğ•œ E) :=
{ continuous_add := begin
    apply continuous_of_continuous_eval,
    intros z,
    have h : continuous (Î» p : ğ•œ Ã— ğ•œ, p.1 + p.2) := continuous_add,
    exact h.comp ((eval_continuous ğ•œ E z).prod_map (eval_continuous ğ•œ E z)),
  end, }

/-- If the scalars `ğ•œ` are a commutative semiring, then `weak_dual ğ•œ E` is a module over `ğ•œ`. -/
instance (ğ•œ : Type u) [topological_space ğ•œ] [comm_semiring ğ•œ]
  [has_continuous_add ğ•œ] [has_continuous_mul ğ•œ]
  (E : Type*) [topological_space E] [add_comm_group E] [module ğ•œ E] :
  module ğ•œ (weak_dual ğ•œ E) :=
continuous_linear_map.module

/-- Scalar multiplication in `weak_dual ğ•œ E` is continuous (when `ğ•œ` is a commutative
semiring). -/
instance (ğ•œ : Type u) [topological_space ğ•œ] [comm_semiring ğ•œ]
  [has_continuous_add ğ•œ] [has_continuous_mul ğ•œ]
  (E : Type*) [topological_space E] [add_comm_group E]
  [module ğ•œ E] :
  has_continuous_smul ğ•œ (weak_dual ğ•œ E) :=
{ continuous_smul := begin
    apply continuous_of_continuous_eval,
    intros z,
    have h : continuous (Î» p : ğ•œ Ã— ğ•œ, p.1 * p.2) := continuous_mul,
    exact h.comp ((continuous_id').prod_map (eval_continuous ğ•œ E z)),
  end, }

end weak_dual

end weak_star_topology
