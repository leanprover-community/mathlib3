/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import topology.algebra.module

/-!
# Weak dual topology

This file defines the weak-* topology on duals of suitable topological modules `E` over suitable
topological semirings `ğ•œ`. The (weak) dual consists of continuous linear functionals `E â†’L[ğ•œ] ğ•œ`
from `E` to scalars `ğ•œ`. The weak-* topology is the coarsest topology on this dual
`weak_dual ğ•œ E := (E â†’L[ğ•œ] ğ•œ)` w.r.t. which the evaluation maps at all `z : E` are continuous.

The weak dual is a module over `ğ•œ` if the semiring `ğ•œ` is commutative.

## Main definitions

The main definitions are the type `weak_dual ğ•œ E` and a topology instance on it.

* `weak_dual ğ•œ E` is a type synonym for `dual ğ•œ E` (when the latter is defined): both are equal to
  the type `E â†’L[ğ•œ] ğ•œ` of continuous linear maps from a module `E` over `ğ•œ` to the ring `ğ•œ`.
* The instance `weak_dual.topological_space` is the weak-* topology on `weak_dual ğ•œ E`, i.e., the
  coarsest topology making the evaluation maps at all `z : E` continuous.

## Main results

We establish that `weak_dual ğ•œ E` has the following structure:
* `weak_dual.has_continuous_add`: The addition in `weak_dual ğ•œ E` is continuous.
* `weak_dual.module`: If the scalars `ğ•œ` are a commutative semiring, then `weak_dual ğ•œ E` is a
  module over `ğ•œ`.
* `weak_dual.has_continuous_smul`: If the scalars `ğ•œ` are a commutative semiring, then the scalar
  multiplication by `ğ•œ` in `weak_dual ğ•œ E` is continuous.

We prove the following results characterizing the weak-* topology:
* `weak_dual.eval_continuous`: For any `z : E`, the evaluation mapping `weak_dual ğ•œ E â†’ ğ•œ` taking
  `x'`to `x' z` is continuous.
* `weak_dual.continuous_of_continuous_eval`: For a mapping to `weak_dual ğ•œ E` to be continuous,
  it suffices that its compositions with evaluations at all points `z : E` are continuous.
* `weak_dual.tendsto_iff_forall_eval_tendsto`: Convergence in `weak_dual ğ•œ E` can be characterized
  in terms of convergence of the evaluations at all points `z : E`.

## Notations

No new notation is introduced.

## Implementation notes

The weak-* topology is defined as the induced topology under the mapping that associates to a dual
element `x'` the functional `E â†’ ğ•œ`, when the space `E â†’ ğ•œ` of functionals is equipped with the
topology of pointwise convergence (product topology).

Typically one might assume that `ğ•œ` is a topological semiring in the sense of the typeclasses
`topological_space ğ•œ`, `semiring ğ•œ`, `has_continuous_add ğ•œ`, `has_continuous_mul ğ•œ`,
and that the space `E` is a topological module over `ğ•œ` in the sense of the typeclasses
`topological_space E`, `add_comm_monoid E`, `has_continuous_add E`, `module ğ•œ E`,
`has_continuous_smul ğ•œ E`. The definitions and results are, however, given with weaker assumptions
when possible.

## References

* https://en.wikipedia.org/wiki/Weak_topology#Weak-*_topology

## Tags

weak-star, weak dual

-/

noncomputable theory
open filter
open_locale topological_space

universes u v

section weak_star_topology
/-!
### Weak star topology on duals of topological modules
-/

variables (ğ•œ : Type*) [topological_space ğ•œ] [semiring ğ•œ]
variables (E : Type*) [topological_space E] [add_comm_monoid E] [module ğ•œ E]

/-- The weak dual of a topological module `E` over a topological semiring `ğ•œ` consists of
continuous linear functionals from `E` to scalars `ğ•œ`. It is a type synonym with the usual dual
(when the latter is defined), but will be equipped with a different topology. -/
@[derive [inhabited, Î» Î±, has_coe_to_fun Î± (Î» _, E â†’ ğ•œ)]]
def weak_dual := E â†’L[ğ•œ] ğ•œ

instance [has_continuous_add ğ•œ] : add_comm_monoid (weak_dual ğ•œ E) :=
continuous_linear_map.add_comm_monoid

namespace weak_dual

/-- The weak-* topology instance `weak_dual.topological_space` on the dual of a topological module
`E` over a topological semiring `ğ•œ` is defined as the induced topology under the mapping that
associates to a dual element `x' : weak_dual ğ•œ E` the functional `E â†’ ğ•œ`, when the space `E â†’ ğ•œ`
of functionals is equipped with the topology of pointwise convergence (product topology). -/
instance : topological_space (weak_dual ğ•œ E) :=
topological_space.induced (Î» x' : weak_dual ğ•œ E, Î» z : E, x' z) Pi.topological_space

/-- The coercion `coe_fn : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)` is an embedding. -/
lemma coe_fn_embedding :
  embedding (coe_fn : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)) :=
by convert continuous_linear_map.coe_fn_injective.embedding_induced

lemma coe_fn_continuous :
  continuous (Î» (x' : (weak_dual ğ•œ E)), (Î» (z : E), x' z)) :=
continuous_induced_dom

lemma eval_continuous (z : E) : continuous (Î» (x' : weak_dual ğ•œ E), x' z) :=
(continuous_pi_iff.mp (coe_fn_continuous ğ•œ E)) z

lemma continuous_of_continuous_eval {Î± : Type u} [topological_space Î±]
  {g : Î± â†’ weak_dual ğ•œ E} (h : âˆ€ z, continuous (Î» a, g a z)) : continuous g :=
continuous_induced_rng (continuous_pi_iff.mpr h)

theorem tendsto_iff_forall_eval_tendsto {Î³ : Type u} {F : filter Î³}
  {Ïˆs : Î³ â†’ weak_dual ğ•œ E} {Ïˆ : weak_dual ğ•œ E} :
  tendsto Ïˆs F (ğ“ Ïˆ) â†” âˆ€ z : E, tendsto (Î» i, Ïˆs i z) F (ğ“ (Ïˆ z)) :=
by rw [â† tendsto_pi_nhds, (coe_fn_embedding ğ•œ E).tendsto_nhds_iff]

/-- Addition in `weak_dual ğ•œ E` is continuous. -/
instance [has_continuous_add ğ•œ] : has_continuous_add (weak_dual ğ•œ E) :=
âŸ¨continuous_induced_rng $ ((coe_fn_continuous ğ•œ E).comp continuous_fst).add
  ((coe_fn_continuous ğ•œ E).comp continuous_snd)âŸ©

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it acts on `weak_dual ğ•œ E`. -/
instance (M : Type*) [monoid M] [distrib_mul_action M ğ•œ] [smul_comm_class ğ•œ M ğ•œ]
  [topological_space M] [has_continuous_smul M ğ•œ] :
  mul_action M (weak_dual ğ•œ E) :=
continuous_linear_map.mul_action

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it acts distributively on `weak_dual ğ•œ E`. -/
instance (M : Type*) [monoid M] [distrib_mul_action M ğ•œ] [smul_comm_class ğ•œ M ğ•œ]
  [topological_space M] [has_continuous_smul M ğ•œ] [has_continuous_add ğ•œ] :
  distrib_mul_action M (weak_dual ğ•œ E) :=
continuous_linear_map.distrib_mul_action

/-- If `ğ•œ` is a topological module over a semiring `R` and scalar multiplication commutes with the
multiplication on `ğ•œ`, then `weak_dual ğ•œ E` is a module over `R`. -/
instance (R : Type*) [semiring R] [module R ğ•œ] [smul_comm_class ğ•œ R ğ•œ]
  [topological_space R] [has_continuous_smul R ğ•œ] [has_continuous_add ğ•œ] :
  module R (weak_dual ğ•œ E) :=
continuous_linear_map.module

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it continuously acts on `weak_dual ğ•œ E`. -/
instance (M : Type*) [monoid M] [distrib_mul_action M ğ•œ] [smul_comm_class ğ•œ M ğ•œ]
  [topological_space M] [has_continuous_smul M ğ•œ] :
  has_continuous_smul M (weak_dual ğ•œ E) :=
âŸ¨continuous_induced_rng $ continuous_fst.smul ((coe_fn_continuous ğ•œ E).comp continuous_snd)âŸ©

end weak_dual

end weak_star_topology
