/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/

import topology.algebra.ordered.basic
import algebra.order.with_zero

/-!
# The topology on linearly ordered commutative groups with zero

Let `Î“â‚€` be a linearly ordered commutative group to which we have adjoined a zero element.
Then `Î“â‚€` may naturally be endowed with a topology that turns `Î“â‚€` into a topological monoid.
Neighborhoods of zero are sets containing `{Î³ | Î³ < Î³â‚€}` for some invertible element `Î³â‚€`
and every invertible element is open.
In particular the topology is the following:
"a subset `U âŠ† Î“â‚€` is open if `0 âˆ‰ U` or if there is an invertible
`Î³â‚€ âˆˆ Î“â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U`", but this fact is not proven here since the neighborhoods
description is what is actually useful.

We prove this topology is ordered and regular (in addition to be compatible with the monoid
structure).

All this is useful to extend a valuation to a completion. This is an abstract version of how the
absolute value (resp. `p`-adic absolute value) on `â„š` is extended to `â„` (resp. `â„šâ‚š`).

## Implementation notes

This topology is not defined as an instance since it may not be the desired topology on
a linearly ordered commutative group with zero. You can locally activate this topology using
`local attribute [instance] linear_ordered_comm_group_with_zero.topological_space`
All other instances will (`ordered_topology`, `regular_space`, `has_continuous_mul`) then follow.

-/

open_locale topological_space
open topological_space filter set

namespace linear_ordered_comm_group_with_zero

variables (Î“â‚€ : Type*) [linear_ordered_comm_group_with_zero Î“â‚€]

/-- The neighbourhoods around Î³ âˆˆ Î“â‚€, used in the definition of the topology on Î“â‚€.
These neighbourhoods are defined as follows:
A set s is a neighbourhood of 0 if there is an invertible Î³â‚€ âˆˆ Î“â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† s.
If Î³ â‰  0, then every set that contains Î³ is a neighbourhood of Î³. -/
def nhds_fun (x : Î“â‚€) : filter Î“â‚€ :=
if x = 0 then â¨… (Î³â‚€ : units Î“â‚€), principal {Î³ | Î³ < Î³â‚€} else pure x

/-- The topology on a linearly ordered commutative group with a zero element adjoined.
A subset U is open if 0 âˆ‰ U or if there is an invertible element Î³â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U. -/
protected def topological_space : topological_space Î“â‚€ :=
topological_space.mk_of_nhds (nhds_fun Î“â‚€)

local attribute [instance] linear_ordered_comm_group_with_zero.topological_space

/-- The neighbourhoods {Î³ | Î³ < Î³â‚€} of 0 form a directed set indexed by the invertible 
elements Î³â‚€. -/
lemma directed_lt : directed (â‰¥) (Î» Î³â‚€ : units Î“â‚€, principal {Î³ : Î“â‚€ | Î³ < Î³â‚€}) :=
begin
  intros Î³â‚ Î³â‚‚,
  use linear_order.min Î³â‚ Î³â‚‚ ; dsimp only,
  split ; rw [ge_iff_le, principal_mono] ; intros x x_in,
  { calc x < â†‘(linear_order.min Î³â‚ Î³â‚‚) : x_in
        ... â‰¤ Î³â‚ : min_le_left Î³â‚ Î³â‚‚ },
  { calc x < â†‘(linear_order.min Î³â‚ Î³â‚‚) : x_in
        ... â‰¤ Î³â‚‚ : min_le_right Î³â‚ Î³â‚‚ }
end

-- We need two auxilliary lemmas to show that nhds_fun accurately describes the neighbourhoods
-- coming from the topology (that is defined in terms of nhds_fun).

/-- At all points of a linearly ordered commutative group with a zero element adjoined,
the pure filter is smaller than the filter given by nhds_fun. -/
lemma pure_le_nhds_fun : pure â‰¤ nhds_fun Î“â‚€ :=
Î» x, by { by_cases hx : x = 0; simp [hx, nhds_fun] }

/-- For every point Î“â‚€, and every â€œneighbourhoodâ€ s of it (described by nhds_fun), there is a
smaller â€œneighbourhoodâ€ t âŠ† s, such that s is a â€œneighbourhoodâ€œ of all the points in t. -/
lemma nhds_fun_ok (x : Î“â‚€) {s} (s_in : s âˆˆ nhds_fun Î“â‚€ x) :
  (âˆƒ t âˆˆ nhds_fun Î“â‚€ x, t âŠ† s âˆ§ âˆ€ y âˆˆ t, s âˆˆ nhds_fun Î“â‚€ y) :=
begin
  by_cases hx : x = 0,
  { simp only [hx, nhds_fun, exists_prop, if_true, eq_self_iff_true] at s_in âŠ¢,
    cases (mem_infi_of_directed (directed_lt Î“â‚€) _).mp s_in with Î³â‚€ h,
    use {Î³ : Î“â‚€ | Î³ < Î³â‚€},
    rw mem_principal at h,
    split,
    { apply mem_infi_of_mem Î³â‚€,
      rw mem_principal },
    { refine âŸ¨h, Î» y y_in, _âŸ©,
      by_cases hy : y = 0,
      { simp only [hy, if_true, eq_self_iff_true],
        apply mem_infi_of_mem Î³â‚€,
        rwa mem_principal },
      { simp [hy, h y_in] } } },
  { simp only [hx, nhds_fun, exists_prop, if_false, mem_pure] at s_in âŠ¢,
    refine âŸ¨{x}, mem_singleton _, singleton_subset_iff.2 s_in, Î» y y_in, _âŸ©,
    simpa [mem_singleton_iff.mp y_in, hx] }
end

variables  {Î“â‚€}

/-- The neighbourhood filter of an invertible element consists of all sets containing that 
element. -/
lemma nhds_coe_units (Î³ : units Î“â‚€) : ð“ (Î³ : Î“â‚€) = pure (Î³ : Î“â‚€) :=
calc ð“ (Î³ : Î“â‚€) = nhds_fun Î“â‚€ Î³ : nhds_mk_of_nhds (nhds_fun Î“â‚€) Î³ (pure_le_nhds_fun Î“â‚€)
                                                   (nhds_fun_ok Î“â‚€)
              ... = pure (Î³ : Î“â‚€) : if_neg Î³.ne_zero

/-- The neighbourhood filter of a nonzero element consists of all sets containing that 
element. -/
@[simp] lemma nhds_of_ne_zero (Î³ : Î“â‚€) (h : Î³ â‰  0) :
  ð“ Î³ = pure Î³ :=
nhds_coe_units (units.mk0 _ h)

/-- If Î³ is an invertible element of a linearly ordered group with zero element adjoined,
then {Î³} is a neighbourhood of Î³. -/
lemma singleton_nhds_of_units (Î³ : units Î“â‚€) : ({Î³} : set Î“â‚€) âˆˆ ð“ (Î³ : Î“â‚€) :=
by simp

/-- If Î³ is a nonzero element of a linearly ordered group with zero element adjoined,
then {Î³} is a neighbourhood of Î³. -/
lemma singleton_nhds_of_ne_zero (Î³ : Î“â‚€) (h : Î³ â‰  0) : ({Î³} : set Î“â‚€) âˆˆ ð“ (Î³ : Î“â‚€) :=
by simp [h]

/-- If U is a neighbourhood of 0 in a linearly ordered group with zero element adjoined,
then there exists an invertible element Î³â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U. -/
lemma has_basis_nhds_zero :
  has_basis (ð“ (0 : Î“â‚€)) (Î» _, true) (Î» Î³â‚€ : units Î“â‚€, {Î³ : Î“â‚€ | Î³ < Î³â‚€}) :=
âŸ¨begin
  intro U,
  rw nhds_mk_of_nhds (nhds_fun Î“â‚€) 0 (pure_le_nhds_fun Î“â‚€) (nhds_fun_ok Î“â‚€),
  simp only [nhds_fun, if_true, eq_self_iff_true, exists_true_left],
  simp_rw [mem_infi_of_directed (directed_lt Î“â‚€), mem_principal]
endâŸ©

/-- If Î³ is an invertible element of a linearly ordered group with zero element adjoined,
then {x | x < Î³} is a neighbourhood of 0. -/
lemma nhds_zero_of_units (Î³ : units Î“â‚€) : {x : Î“â‚€ | x < Î³} âˆˆ ð“ (0 : Î“â‚€) :=
by { rw has_basis_nhds_zero.mem_iff, use Î³, simp }

lemma tendsto_zero {Î± : Type*} {F : filter Î±} {f : Î± â†’ Î“â‚€} :
  tendsto f F (ð“ (0 : Î“â‚€)) â†” âˆ€ Î³â‚€ : units Î“â‚€, { x : Î± | f x < Î³â‚€ } âˆˆ F :=
by simpa using has_basis_nhds_zero.tendsto_right_iff

/-- If Î³ is a nonzero element of a linearly ordered group with zero element adjoined,
then {x | x < Î³} is a neighbourhood of 0. -/
lemma nhds_zero_of_ne_zero (Î³ : Î“â‚€) (h : Î³ â‰  0) : {x : Î“â‚€ | x < Î³} âˆˆ ð“ (0 : Î“â‚€) :=
nhds_zero_of_units (units.mk0 _ h)

lemma has_basis_nhds_units (Î³ : units Î“â‚€) :
  has_basis (ð“ (Î³ : Î“â‚€)) (Î» i : unit, true) (Î» i, {Î³}) :=
begin
  rw nhds_of_ne_zero _ Î³.ne_zero,
  exact has_basis_pure Î³
end

lemma has_basis_nhds_of_ne_zero {x : Î“â‚€} (h : x â‰  0) :
  has_basis (ð“ x) (Î» i : unit, true) (Î» i, {x}) :=
has_basis_nhds_units (units.mk0 x h)

lemma tendsto_units {Î± : Type*} {F : filter Î±} {f : Î± â†’ Î“â‚€} {Î³â‚€ : units Î“â‚€} :
  tendsto f F (ð“ (Î³â‚€ : Î“â‚€)) â†” { x : Î± | f x = Î³â‚€ } âˆˆ F :=
begin
  rw (has_basis_nhds_units Î³â‚€).tendsto_right_iff,
  simpa
end

lemma tendsto_of_ne_zero {Î± : Type*} {F : filter Î±} {f : Î± â†’ Î“â‚€} {Î³ : Î“â‚€} (h : Î³ â‰  0) :
  tendsto f F (ð“ Î³) â†” { x : Î± | f x = Î³ } âˆˆ F :=
@tendsto_units _ _ _ F f (units.mk0 Î³ h)

variable (Î“â‚€)

/-- The topology on a linearly ordered group with zero element adjoined
is compatible with the order structure. -/
@[priority 100]
instance ordered_topology : order_closed_topology Î“â‚€ :=
{ is_closed_le' :=
  begin
    rw â† is_open_compl_iff,
    show is_open {p : Î“â‚€ Ã— Î“â‚€ | Â¬p.fst â‰¤ p.snd},
    simp only [not_le],
    rw is_open_iff_mem_nhds,
    rintros âŸ¨a,bâŸ© hab,
    change b < a at hab,
    have ha : a â‰  0 := ne_zero_of_lt hab,
    rw [nhds_prod_eq, mem_prod_iff],
    by_cases hb : b = 0,
    { subst b,
      use [{a}, singleton_nhds_of_ne_zero _ ha, {x : Î“â‚€ | x < a}, nhds_zero_of_ne_zero _ ha],
      intros p p_in,
      cases mem_prod.1 p_in with h1 h2,
      rw mem_singleton_iff at h1,
      change p.2 < p.1,
      rwa h1 },
    { use [{a}, singleton_nhds_of_ne_zero _ ha, {b}, singleton_nhds_of_ne_zero _ hb],
      intros p p_in,
      cases mem_prod.1 p_in with h1 h2,
      rw mem_singleton_iff at h1 h2,
      change p.2 < p.1,
      rwa [h1, h2] }
  end }

/-- The topology on a linearly ordered group with zero element adjoined is Tâ‚ƒ (aka regular). -/
@[priority 100]
instance regular_space : regular_space Î“â‚€ :=
begin
  haveI : t1_space Î“â‚€ := t2_space.t1_space,
  split,
  intros s x s_closed x_not_in_s,
  by_cases hx : x = 0,
  { refine âŸ¨s, _, subset.rfl, _âŸ©,
    { subst x,
      rw is_open_iff_mem_nhds,
      intros y hy,
      by_cases hy' : y = 0, { subst y, contradiction },
      simpa [hy'] },
    { erw inf_eq_bot_iff,
      use sá¶œ,
      simp only [exists_prop, mem_principal],
      exact âŸ¨s_closed.compl_mem_nhds x_not_in_s, âŸ¨s, subset.refl s, by simpâŸ©âŸ© } },
  { simp only [nhds_within, inf_eq_bot_iff, exists_prop, mem_principal],
    exact âŸ¨{x}á¶œ, is_open_compl_iff.mpr is_closed_singleton, by rwa subset_compl_singleton_iff,
           {x}, singleton_nhds_of_ne_zero x hx, {x}á¶œ, by simp [subset.refl]âŸ© }
end

/-- The topology on a linearly ordered group with zero element adjoined makes it a topological
monoid. -/
@[priority 100]
instance : has_continuous_mul Î“â‚€ :=
âŸ¨begin
  have common : âˆ€ y â‰  (0 : Î“â‚€), continuous_at (Î» (p : Î“â‚€ Ã— Î“â‚€), p.fst * p.snd) (0, y),
  { intros y hy,
    set Î³ := units.mk0 y hy,
    suffices : tendsto (Î» (p : Î“â‚€ Ã— Î“â‚€), p.fst * p.snd) ((ð“ 0).prod (ð“ Î³)) (ð“ 0),
    by simpa [continuous_at, nhds_prod_eq],
    suffices : âˆ€ (Î³' : units Î“â‚€), âˆƒ (Î³''  : units Î“â‚€), âˆ€ (a b : Î“â‚€), a < Î³'' â†’ b = y â†’ a * b < Î³',
    { rw (has_basis_nhds_zero.prod $ has_basis_nhds_units Î³).tendsto_iff has_basis_nhds_zero,
      simpa },
    intros Î³',
    use Î³â»Â¹*Î³',
    rintros a b ha hb,
    rw [hb, mul_comm],
    rw [units.coe_mul] at ha,
    simpa using inv_mul_lt_of_lt_mulâ‚€ ha },
  rw continuous_iff_continuous_at,
  rintros âŸ¨x, yâŸ©,
  by_cases hx : x = 0; by_cases hy : y = 0,
  { suffices : tendsto (Î» (p : Î“â‚€ Ã— Î“â‚€), p.fst * p.snd) (ð“ (0, 0)) (ð“ 0),
    by simpa [hx, hy, continuous_at],
    suffices : âˆ€ (Î³ : units Î“â‚€), âˆƒ (Î³' : units Î“â‚€), âˆ€ (a b : Î“â‚€), a < Î³' â†’ b < Î³' â†’ a * b < Î³,
    by simpa [nhds_prod_eq, has_basis_nhds_zero.prod_self.tendsto_iff has_basis_nhds_zero],
    intros Î³,
    rcases exists_square_le Î³ with âŸ¨Î³', hâŸ©,
    use Î³',
    intros a b ha hb,
    calc a*b < Î³'*Î³' : mul_lt_mulâ‚€ ha hb
    ... â‰¤ Î³ : by exact_mod_cast h },
  { rw hx,
    exact common y hy },
  { rw hy,
    have : (Î» (p : Î“â‚€ Ã— Î“â‚€), p.fst * p.snd) =
           (Î» (p : Î“â‚€ Ã— Î“â‚€), p.fst * p.snd) âˆ˜ (Î» p : Î“â‚€ Ã— Î“â‚€, (p.2, p.1)),
    by { ext, rw [mul_comm] },
    rw this,
    apply continuous_at.comp _ continuous_swap.continuous_at,
    exact common x hx },
  { change tendsto _ _ _,
    rw [nhds_prod_eq],
    rw ((has_basis_nhds_of_ne_zero hx).prod (has_basis_nhds_of_ne_zero hy)).tendsto_iff
       (has_basis_nhds_of_ne_zero $ mul_ne_zero hx hy),
    suffices : âˆ€ (a b : Î“â‚€), a = x â†’ b = y â†’ a * b = x * y, by simpa,
    rintros a b rfl rfl,
    refl },
endâŸ©

end linear_ordered_comm_group_with_zero
