/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro

Bases of topologies. Countability axioms.
-/

import topology.constructions data.set.countable

open set filter lattice classical
open_locale topological_space

namespace filter
universes u v
variables {Œ± : Type u} {Œ≤ : Type v}

/--
A filter has a countable basis iff it is generated by a countable collection
of subsets of Œ±. (A filter is a generated by a collection of sets iff it is
the infimum of the principal filters.)

Note: we do not require the collection to be closed under finite intersections.
-/
def has_countable_basis (f : filter Œ±) : Prop :=
‚àÉ s : set (set Œ±), countable s ‚àß f = ‚®Ö t ‚àà s, principal t

lemma has_countable_basis_of_seq (f : filter Œ±) (x : ‚Ñï ‚Üí set Œ±) (h : f = ‚®Ö i, principal (x i)) :
  f.has_countable_basis :=
‚ü®range x, countable_range _, by rwa infi_range‚ü©

lemma seq_of_has_countable_basis (f : filter Œ±) (cblb : f.has_countable_basis) :
    ‚àÉ x : ‚Ñï ‚Üí set Œ±, f = ‚®Ö i, principal (x i) :=
begin
  rcases cblb with ‚ü®B, Bcbl, gen‚ü©, subst gen,
  cases B.eq_empty_or_nonempty with hB Bnonempty, { use Œª n, set.univ, simp [principal_univ, *] },
  rw countable_iff_exists_surjective_to_subtype Bnonempty at Bcbl,
  rcases Bcbl with ‚ü®g, gsurj‚ü©,
  rw lattice.infi_subtype',
  use (Œª n, g n), apply le_antisymm; rw le_infi_iff,
  { intro i, apply infi_le_of_le (g i) _, apply le_refl _ },
  { intros a, rcases gsurj a with i, apply infi_le_of_le i _, subst h, apply le_refl _ }
end

/--
Different characterization of countable basis. A filter has a countable basis
iff it is generated by a sequence of sets.
-/
lemma has_countable_basis_iff_seq (f : filter Œ±) :
  f.has_countable_basis ‚Üî
    ‚àÉ x : ‚Ñï ‚Üí set Œ±, f = ‚®Ö i, principal (x i) :=
‚ü®seq_of_has_countable_basis _, Œª ‚ü®x, xgen‚ü©, has_countable_basis_of_seq _ x xgen‚ü©

lemma mono_seq_of_has_countable_basis (f : filter Œ±) (cblb : f.has_countable_basis) :
  ‚àÉ x : ‚Ñï ‚Üí set Œ±, (‚àÄ i j, i ‚â§ j ‚Üí x j ‚äÜ x i) ‚àß f = ‚®Ö i, principal (x i) :=
begin
  rcases (seq_of_has_countable_basis f cblb) with ‚ü®x', hx'‚ü©,
  let x := Œª n, ‚ãÇ m ‚â§ n, x' m,
  use x, split,
  { intros i j hij a, simp [x], intros h i' hi'i, apply h, transitivity; assumption },
  subst hx', apply le_antisymm; rw le_infi_iff; intro i,
  { rw le_principal_iff, apply Inter_mem_sets (finite_le_nat _),
    intros j hji, rw ‚Üê le_principal_iff, apply infi_le_of_le j _, apply le_refl _ },
  { apply infi_le_of_le i _, rw principal_mono, intro a, simp [x], intro h, apply h, refl },
end

/--
Different characterization of countable basis. A filter has a countable basis
iff it is generated by a monotonically decreasing sequence of sets.
-/
lemma has_countable_basis_iff_mono_seq (f : filter Œ±) :
  f.has_countable_basis ‚Üî
    ‚àÉ x : ‚Ñï ‚Üí set Œ±, (‚àÄ i j, i ‚â§ j ‚Üí x j ‚äÜ x i) ‚àß f = ‚®Ö i, principal (x i) :=
‚ü®mono_seq_of_has_countable_basis _, Œª ‚ü®x, _, xgen‚ü©, has_countable_basis_of_seq _ x xgen‚ü©

/--
Different characterization of countable basis. A filter has a countable basis
iff there exists a monotonically decreasing sequence of sets `x i`
such that `s ‚àà f ‚Üî ‚àÉ i, x i ‚äÜ s`. -/
lemma has_countable_basis_iff_mono_seq' (f : filter Œ±) :
  f.has_countable_basis ‚Üî
    ‚àÉ x : ‚Ñï ‚Üí set Œ±, (‚àÄ i j, i ‚â§ j ‚Üí x j ‚äÜ x i) ‚àß (‚àÄ {s}, s ‚àà f ‚Üî ‚àÉ i, x i ‚äÜ s) :=
begin
  refine (has_countable_basis_iff_mono_seq f).trans (exists_congr $ Œª x, and_congr_right _),
  intro hmono,
  have : directed (‚â•) (Œª i, principal (x i)),
    from directed_of_mono _ (Œª i j hij, principal_mono.2 (hmono _ _ hij)),
  simp only [filter.ext_iff, mem_infi this ‚ü®0‚ü©, mem_Union, mem_principal_sets]
end

lemma has_countable_basis.comap {l : filter Œ≤} (h : has_countable_basis l) (f : Œ± ‚Üí Œ≤) :
  has_countable_basis (l.comap f) :=
begin
  rcases h with ‚ü®S, h‚ÇÅ, h‚ÇÇ‚ü©,
  refine ‚ü®preimage f '' S, countable_image _ h‚ÇÅ, _‚ü©,
  calc comap f l = ‚®Ö s ‚àà S, principal (f ‚Åª¬π' s) : by simp [h‚ÇÇ]
  ... = ‚®Ö s ‚àà S, ‚®Ö (t : set Œ±) (H : f ‚Åª¬π' s = t), principal t : by simp
  ... = ‚®Ö (t : set Œ±) (s ‚àà S) (h‚ÇÇ : f ‚Åª¬π' s = t), principal t :
    by { rw [infi_comm], congr' 1, ext t, rw [infi_comm] }
  ... = _ : by simp [-infi_infi_eq_right, infi_and]
end

-- TODO : prove this for a encodable type
lemma has_countable_basis_at_top_finset_nat : has_countable_basis (@at_top (finset ‚Ñï) _) :=
begin
  refine has_countable_basis_of_seq _ (ŒªN, Ici (finset.range N)) (eq_infi_of_mem_sets_iff_exists_mem _),
  assume s,
  rw mem_at_top_sets,
  refine ‚ü®_, Œª ‚ü®N, hN‚ü©, ‚ü®finset.range N, hN‚ü©‚ü©,
  rintros ‚ü®t, ht‚ü©,
  rcases mem_at_top_sets.1 (tendsto_finset_range (mem_at_top t)) with ‚ü®N, hN‚ü©,
  simp only [preimage, mem_set_of_eq] at hN,
  exact ‚ü®N, mem_principal_sets.2 $ Œª t' ht', ht t' $ le_trans (hN _ $ le_refl N) ht'‚ü©
end

lemma has_countable_basis.tendsto_iff_seq_tendsto {f : Œ± ‚Üí Œ≤} {k : filter Œ±} {l : filter Œ≤}
  (hcb : k.has_countable_basis) :
  tendsto f k l ‚Üî (‚àÄ x : ‚Ñï ‚Üí Œ±, tendsto x at_top k ‚Üí tendsto (f ‚àò x) at_top l) :=
suffices (‚àÄ x : ‚Ñï ‚Üí Œ±, tendsto x at_top k ‚Üí tendsto (f ‚àò x) at_top l) ‚Üí tendsto f k l,
  from ‚ü®by intros; apply tendsto.comp; assumption, by assumption‚ü©,
begin
  rw filter.has_countable_basis_iff_mono_seq at hcb,
  rcases hcb with ‚ü®g, gmon, gbasis‚ü©,
  have gbasis : ‚àÄ A, A ‚àà k ‚Üî ‚àÉ i, g i ‚äÜ A,
  { intro A,
    subst gbasis,
    rw mem_infi,
    { simp only [set.mem_Union, iff_self, filter.mem_principal_sets] },
    { exact directed_of_mono _ (Œª i j h, principal_mono.mpr $ gmon _ _ h) },
    { apply_instance } },
  classical, contrapose,
  simp only [not_forall, not_imp, not_exists, subset_def, @tendsto_def _ _ f, gbasis],
  rintro ‚ü®B, hBl, hfBk‚ü©,
  choose x h using hfBk,
  use x, split,
  { simp only [tendsto_at_top', gbasis],
    rintros A ‚ü®i, hgiA‚ü©,
    use i,
    refine (Œª j hj, hgiA $ gmon _ _ hj _),
    simp only [h] },
  { simp only [tendsto_at_top', (‚àò), not_forall, not_exists],
    use [B, hBl],
    intro i, use [i, (le_refl _)],
    apply (h i).right },
end

lemma has_countable_basis.tendsto_of_seq_tendsto {f : Œ± ‚Üí Œ≤} {k : filter Œ±} {l : filter Œ≤}
  (hcb : k.has_countable_basis) :
  (‚àÄ x : ‚Ñï ‚Üí Œ±, tendsto x at_top k ‚Üí tendsto (f ‚àò x) at_top l) ‚Üí tendsto f k l :=
hcb.tendsto_iff_seq_tendsto.2

end filter

namespace topological_space
/- countability axioms

For our applications we are interested that there exists a countable basis, but we do not need the
concrete basis itself. This allows us to declare these type classes as `Prop` to use them as mixins.
-/
universe u
variables {Œ± : Type u} [t : topological_space Œ±]
include t

/-- A topological basis is one that satisfies the necessary conditions so that
  it suffices to take unions of the basis sets to get a topology (without taking
  finite intersections as well). -/
def is_topological_basis (s : set (set Œ±)) : Prop :=
(‚àÄt‚ÇÅ‚ààs, ‚àÄt‚ÇÇ‚ààs, ‚àÄ x ‚àà t‚ÇÅ ‚à© t‚ÇÇ, ‚àÉ t‚ÇÉ‚ààs, x ‚àà t‚ÇÉ ‚àß t‚ÇÉ ‚äÜ t‚ÇÅ ‚à© t‚ÇÇ) ‚àß
(‚ãÉ‚ÇÄ s) = univ ‚àß
t = generate_from s

lemma is_topological_basis_of_subbasis {s : set (set Œ±)} (hs : t = generate_from s) :
  is_topological_basis ((Œªf, ‚ãÇ‚ÇÄ f) '' {f:set (set Œ±) | finite f ‚àß f ‚äÜ s ‚àß (‚ãÇ‚ÇÄ f).nonempty}) :=
let b' := (Œªf, ‚ãÇ‚ÇÄ f) '' {f:set (set Œ±) | finite f ‚àß f ‚äÜ s ‚àß (‚ãÇ‚ÇÄ f).nonempty} in
‚ü®assume s‚ÇÅ ‚ü®t‚ÇÅ, ‚ü®hft‚ÇÅ, ht‚ÇÅb, ht‚ÇÅ‚ü©, eq‚ÇÅ‚ü© s‚ÇÇ ‚ü®t‚ÇÇ, ‚ü®hft‚ÇÇ, ht‚ÇÇb, ht‚ÇÇ‚ü©, eq‚ÇÇ‚ü©,
    have ie : ‚ãÇ‚ÇÄ(t‚ÇÅ ‚à™ t‚ÇÇ) = ‚ãÇ‚ÇÄ t‚ÇÅ ‚à© ‚ãÇ‚ÇÄ t‚ÇÇ, from Inf_union,
    eq‚ÇÅ ‚ñ∏ eq‚ÇÇ ‚ñ∏ assume x h,
      ‚ü®_, ‚ü®t‚ÇÅ ‚à™ t‚ÇÇ, ‚ü®finite_union hft‚ÇÅ hft‚ÇÇ, union_subset ht‚ÇÅb ht‚ÇÇb,
        ie.symm ‚ñ∏ ‚ü®_, h‚ü©‚ü©, ie‚ü©, h, subset.refl _‚ü©,
  eq_univ_iff_forall.2 $ assume a, ‚ü®univ, ‚ü®‚àÖ, ‚ü®finite_empty, empty_subset _,
    by rw sInter_empty; exact ‚ü®a, mem_univ a‚ü©‚ü©, sInter_empty‚ü©, mem_univ _‚ü©,
 have generate_from s = generate_from b',
    from le_antisymm
      (le_generate_from $ assume u ‚ü®t, ‚ü®hft, htb, ne‚ü©, eq‚ü©,
        eq ‚ñ∏ @is_open_sInter _ (generate_from s) _ hft (assume s hs, generate_open.basic _ $ htb hs))
      (le_generate_from $ assume s hs,
        s.eq_empty_or_nonempty.elim
          (assume : s = ‚àÖ, by rw [this]; apply @is_open_empty _ _)
          (assume : s.nonempty, generate_open.basic _ ‚ü®{s}, ‚ü®finite_singleton s, singleton_subset_iff.2 hs,
            by rwa sInter_singleton‚ü©, sInter_singleton s‚ü©)),
  this ‚ñ∏ hs‚ü©

lemma is_topological_basis_of_open_of_nhds {s : set (set Œ±)}
  (h_open : ‚àÄ u ‚àà s, _root_.is_open u)
  (h_nhds : ‚àÄ(a:Œ±) (u : set Œ±), a ‚àà u ‚Üí _root_.is_open u ‚Üí ‚àÉv ‚àà s, a ‚àà v ‚àß v ‚äÜ u) :
  is_topological_basis s :=
‚ü®assume t‚ÇÅ ht‚ÇÅ t‚ÇÇ ht‚ÇÇ x ‚ü®xt‚ÇÅ, xt‚ÇÇ‚ü©,
    h_nhds x (t‚ÇÅ ‚à© t‚ÇÇ) ‚ü®xt‚ÇÅ, xt‚ÇÇ‚ü©
      (is_open_inter _ _ _ (h_open _ ht‚ÇÅ) (h_open _ ht‚ÇÇ)),
  eq_univ_iff_forall.2 $ assume a,
    let ‚ü®u, h‚ÇÅ, h‚ÇÇ, _‚ü© := h_nhds a univ trivial (is_open_univ _) in
    ‚ü®u, h‚ÇÅ, h‚ÇÇ‚ü©,
  le_antisymm
    (le_generate_from h_open)
    (assume u hu,
      (@is_open_iff_nhds Œ± (generate_from _) _).mpr $ assume a hau,
        let ‚ü®v, hvs, hav, hvu‚ü© := h_nhds a u hau hu in
        by rw nhds_generate_from; exact infi_le_of_le v (infi_le_of_le ‚ü®hav, hvs‚ü© $ le_principal_iff.2 hvu))‚ü©

lemma mem_nhds_of_is_topological_basis {a : Œ±} {s : set Œ±} {b : set (set Œ±)}
  (hb : is_topological_basis b) : s ‚àà ùìù a ‚Üî ‚àÉt‚ààb, a ‚àà t ‚àß t ‚äÜ s :=
begin
  change s ‚àà (ùìù a).sets ‚Üî ‚àÉt‚ààb, a ‚àà t ‚àß t ‚äÜ s,
  rw [hb.2.2, nhds_generate_from, binfi_sets_eq],
  { simp only [mem_bUnion_iff, exists_prop, mem_set_of_eq, and_assoc, and.left_comm], refl },
  { exact assume s ‚ü®hs‚ÇÅ, hs‚ÇÇ‚ü© t ‚ü®ht‚ÇÅ, ht‚ÇÇ‚ü©,
      have a ‚àà s ‚à© t, from ‚ü®hs‚ÇÅ, ht‚ÇÅ‚ü©,
      let ‚ü®u, hu‚ÇÅ, hu‚ÇÇ, hu‚ÇÉ‚ü© := hb.1 _ hs‚ÇÇ _ ht‚ÇÇ _ this in
      ‚ü®u, ‚ü®hu‚ÇÇ, hu‚ÇÅ‚ü©, le_principal_iff.2 (subset.trans hu‚ÇÉ (inter_subset_left _ _)),
        le_principal_iff.2 (subset.trans hu‚ÇÉ (inter_subset_right _ _))‚ü© },
  { rcases eq_univ_iff_forall.1 hb.2.1 a with ‚ü®i, h1, h2‚ü©,
    exact ‚ü®i, h2, h1‚ü© }
end

lemma is_open_of_is_topological_basis {s : set Œ±} {b : set (set Œ±)}
  (hb : is_topological_basis b) (hs : s ‚àà b) : _root_.is_open s :=
is_open_iff_mem_nhds.2 $ Œª a as,
(mem_nhds_of_is_topological_basis hb).2 ‚ü®s, hs, as, subset.refl _‚ü©

lemma mem_basis_subset_of_mem_open {b : set (set Œ±)}
  (hb : is_topological_basis b) {a:Œ±} {u : set Œ±} (au : a ‚àà u)
  (ou : _root_.is_open u) : ‚àÉv ‚àà b, a ‚àà v ‚àß v ‚äÜ u :=
(mem_nhds_of_is_topological_basis hb).1 $ mem_nhds_sets ou au

lemma sUnion_basis_of_is_open {B : set (set Œ±)}
  (hB : is_topological_basis B) {u : set Œ±} (ou : _root_.is_open u) :
  ‚àÉ S ‚äÜ B, u = ‚ãÉ‚ÇÄ S :=
‚ü®{s ‚àà B | s ‚äÜ u}, Œª s h, h.1, set.ext $ Œª a,
  ‚ü®Œª ha, let ‚ü®b, hb, ab, bu‚ü© := mem_basis_subset_of_mem_open hB ha ou in
         ‚ü®b, ‚ü®hb, bu‚ü©, ab‚ü©,
   Œª ‚ü®b, ‚ü®hb, bu‚ü©, ab‚ü©, bu ab‚ü©‚ü©

lemma Union_basis_of_is_open {B : set (set Œ±)}
  (hB : is_topological_basis B) {u : set Œ±} (ou : _root_.is_open u) :
  ‚àÉ (Œ≤ : Type u) (f : Œ≤ ‚Üí set Œ±), u = (‚ãÉ i, f i) ‚àß ‚àÄ i, f i ‚àà B :=
let ‚ü®S, sb, su‚ü© := sUnion_basis_of_is_open hB ou in
‚ü®S, subtype.val, su.trans set.sUnion_eq_Union, Œª ‚ü®b, h‚ü©, sb h‚ü©

variables (Œ±)

/-- A separable space is one with a countable dense subset. -/
class separable_space : Prop :=
(exists_countable_closure_eq_univ : ‚àÉs:set Œ±, countable s ‚àß closure s = univ)

/-- A first-countable space is one in which every point has a
  countable neighborhood basis. -/
class first_countable_topology : Prop :=
(nhds_generated_countable : ‚àÄa:Œ±, (ùìù a).has_countable_basis)

/-- A second-countable space is one with a countable basis. -/
class second_countable_topology : Prop :=
(is_open_generated_countable : ‚àÉb:set (set Œ±), countable b ‚àß t = topological_space.generate_from b)

@[priority 100] -- see Note [lower instance priority]
instance second_countable_topology.to_first_countable_topology
  [second_countable_topology Œ±] : first_countable_topology Œ± :=
let ‚ü®b, hb, eq‚ü© := second_countable_topology.is_open_generated_countable Œ± in
‚ü®assume a, ‚ü®{s | a ‚àà s ‚àß s ‚àà b},
  countable_subset (assume x ‚ü®_, hx‚ü©, hx) hb, by rw [eq, nhds_generate_from]‚ü©‚ü©

lemma second_countable_topology_induced (Œ≤)
  [t : topological_space Œ≤] [second_countable_topology Œ≤] (f : Œ± ‚Üí Œ≤) :
  @second_countable_topology Œ± (t.induced f) :=
begin
  rcases second_countable_topology.is_open_generated_countable Œ≤ with ‚ü®b, hb, eq‚ü©,
  refine { is_open_generated_countable := ‚ü®preimage f '' b, countable_image _ hb, _‚ü© },
  rw [eq, induced_generate_from_eq]
end

instance subtype.second_countable_topology
  (s : set Œ±) [second_countable_topology Œ±] : second_countable_topology s :=
second_countable_topology_induced s Œ± coe

lemma is_open_generated_countable_inter [second_countable_topology Œ±] :
  ‚àÉb:set (set Œ±), countable b ‚àß ‚àÖ ‚àâ b ‚àß is_topological_basis b :=
let ‚ü®b, hb‚ÇÅ, hb‚ÇÇ‚ü© := second_countable_topology.is_open_generated_countable Œ± in
let b' := (Œªs, ‚ãÇ‚ÇÄ s) '' {s:set (set Œ±) | finite s ‚àß s ‚äÜ b ‚àß (‚ãÇ‚ÇÄ s).nonempty} in
‚ü®b',
  countable_image _ $ countable_subset
    (by simp only [(and_assoc _ _).symm]; exact inter_subset_left _ _)
    (countable_set_of_finite_subset hb‚ÇÅ),
  assume ‚ü®s, ‚ü®_, _, hn‚ü©, hp‚ü©, absurd hn (not_nonempty_iff_eq_empty.2 hp),
  is_topological_basis_of_subbasis hb‚ÇÇ‚ü©

/- TODO: more fine grained instances for first_countable_topology, separable_space, t2_space, ... -/
instance {Œ≤ : Type*} [topological_space Œ≤]
  [second_countable_topology Œ±] [second_countable_topology Œ≤] : second_countable_topology (Œ± √ó Œ≤) :=
‚ü®let ‚ü®a, ha‚ÇÅ, ha‚ÇÇ, ha‚ÇÉ, ha‚ÇÑ, ha‚ÇÖ‚ü© := is_open_generated_countable_inter Œ± in
  let ‚ü®b, hb‚ÇÅ, hb‚ÇÇ, hb‚ÇÉ, hb‚ÇÑ, hb‚ÇÖ‚ü© := is_open_generated_countable_inter Œ≤ in
  ‚ü®{g | ‚àÉu‚ààa, ‚àÉv‚ààb, g = set.prod u v},
    have {g | ‚àÉu‚ààa, ‚àÉv‚ààb, g = set.prod u v} = (‚ãÉu‚ààa, ‚ãÉv‚ààb, {set.prod u v}),
      by apply set.ext; simp,
    by rw [this]; exact (countable_bUnion ha‚ÇÅ $ assume u hu, countable_bUnion hb‚ÇÅ $ by simp),
    by rw [ha‚ÇÖ, hb‚ÇÖ, prod_generate_from_generate_from_eq ha‚ÇÑ hb‚ÇÑ]‚ü©‚ü©

instance second_countable_topology_fintype {Œπ : Type*} {œÄ : Œπ ‚Üí Type*}
  [fintype Œπ] [t : ‚àÄa, topological_space (œÄ a)] [sc : ‚àÄa, second_countable_topology (œÄ a)] :
  second_countable_topology (‚àÄa, œÄ a) :=
have ‚àÄi, ‚àÉb : set (set (œÄ i)), countable b ‚àß ‚àÖ ‚àâ b ‚àß is_topological_basis b, from
  assume a, @is_open_generated_countable_inter (œÄ a) _ (sc a),
let ‚ü®g, hg‚ü© := classical.axiom_of_choice this in
have t = (Œªa, generate_from (g a)), from funext $ assume a, (hg a).2.2.2.2,
begin
  constructor,
  refine ‚ü®pi univ '' pi univ g, countable_image _ _, _‚ü©,
  { suffices : countable {f : Œ†a, set (œÄ a) | ‚àÄa, f a ‚àà g a}, { simpa [pi] },
    exact countable_pi (assume i, (hg i).1), },
  rw [this, pi_generate_from_eq_fintype],
  { congr' 1, ext f, simp [pi, eq_comm] },
  exact assume a, (hg a).2.2.2.1
end

@[priority 100] -- see Note [lower instance priority]
instance second_countable_topology.to_separable_space
  [second_countable_topology Œ±] : separable_space Œ± :=
let ‚ü®b, hb‚ÇÅ, hb‚ÇÇ, hb‚ÇÉ, hb‚ÇÑ, eq‚ü© := is_open_generated_countable_inter Œ± in
have nhds_eq : ‚àÄa, ùìù a = (‚®Ö s : {s : set Œ± // a ‚àà s ‚àß s ‚àà b}, principal s.val),
  by intro a; rw [eq, nhds_generate_from, infi_subtype]; refl,
have ‚àÄs‚ààb, set.nonempty s,
  from assume s hs, ne_empty_iff_nonempty.1 $ Œª eq, absurd hs (eq.symm ‚ñ∏ hb‚ÇÇ),
have ‚àÉf:‚àÄs‚ààb, Œ±, ‚àÄs h, f s h ‚àà s, by simpa only [skolem, set.nonempty] using this,
let ‚ü®f, hf‚ü© := this in
‚ü®‚ü®(‚ãÉs‚ààb, ‚ãÉh:s‚ààb, {f s h}),
  countable_bUnion hb‚ÇÅ (Œª _ _, countable_Union_Prop $ Œª _, countable_singleton _),
  set.ext $ assume a,
  have a ‚àà (‚ãÉ‚ÇÄ b), by rw [hb‚ÇÑ]; exact trivial,
  let ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := this in
  have w : {s : set Œ± // a ‚àà s ‚àß s ‚àà b}, from ‚ü®t, ht‚ÇÇ, ht‚ÇÅ‚ü©,
  suffices (‚®Ö (x : {s // a ‚àà s ‚àß s ‚àà b}), principal (x.val ‚à© ‚ãÉs (h‚ÇÅ h‚ÇÇ : s ‚àà b), {f s h‚ÇÇ})) ‚â† ‚ä•,
    by simpa only [closure_eq_nhds, nhds_eq, infi_inf w, inf_principal, mem_set_of_eq, mem_univ, iff_true],
  infi_ne_bot_of_directed ‚ü®a‚ü©
    (assume ‚ü®s‚ÇÅ, has‚ÇÅ, hs‚ÇÅ‚ü© ‚ü®s‚ÇÇ, has‚ÇÇ, hs‚ÇÇ‚ü©,
      have a ‚àà s‚ÇÅ ‚à© s‚ÇÇ, from ‚ü®has‚ÇÅ, has‚ÇÇ‚ü©,
      let ‚ü®s‚ÇÉ, hs‚ÇÉ, has‚ÇÉ, hs‚ü© := hb‚ÇÉ _ hs‚ÇÅ _ hs‚ÇÇ _ this in
      ‚ü®‚ü®s‚ÇÉ, has‚ÇÉ, hs‚ÇÉ‚ü©, begin
        simp only [le_principal_iff, mem_principal_sets, (‚â•)],
        simp only [subset_inter_iff] at hs, split;
          apply inter_subset_inter_left; simp only [hs]
      end‚ü©)
    (assume ‚ü®s, has, hs‚ü©,
      have (s ‚à© (‚ãÉ (s : set Œ±) (H h : s ‚àà b), {f s h})).nonempty,
        from ‚ü®_, hf _ hs, mem_bUnion hs $ mem_Union.mpr ‚ü®hs, mem_singleton _‚ü©‚ü©,
      principal_ne_bot_iff.2 this) ‚ü©‚ü©

variables {Œ±}

lemma is_open_Union_countable [second_countable_topology Œ±]
  {Œπ} (s : Œπ ‚Üí set Œ±) (H : ‚àÄ i, _root_.is_open (s i)) :
  ‚àÉ T : set Œπ, countable T ‚àß (‚ãÉ i ‚àà T, s i) = ‚ãÉ i, s i :=
let ‚ü®B, cB, _, bB‚ü© := is_open_generated_countable_inter Œ± in
begin
  let B' := {b ‚àà B | ‚àÉ i, b ‚äÜ s i},
  choose f hf using Œª b:B', b.2.2,
  haveI : encodable B' := (countable_subset (sep_subset _ _) cB).to_encodable,
  refine ‚ü®_, countable_range f,
    subset.antisymm (bUnion_subset_Union _ _) (sUnion_subset _)‚ü©,
  rintro _ ‚ü®i, rfl‚ü© x xs,
  rcases mem_basis_subset_of_mem_open bB xs (H _) with ‚ü®b, hb, xb, bs‚ü©,
  exact ‚ü®_, ‚ü®_, rfl‚ü©, _, ‚ü®‚ü®‚ü®_, hb, _, bs‚ü©, rfl‚ü©, rfl‚ü©, hf _ (by exact xb)‚ü©
end

lemma is_open_sUnion_countable [second_countable_topology Œ±]
  (S : set (set Œ±)) (H : ‚àÄ s ‚àà S, _root_.is_open s) :
  ‚àÉ T : set (set Œ±), countable T ‚àß T ‚äÜ S ‚àß ‚ãÉ‚ÇÄ T = ‚ãÉ‚ÇÄ S :=
let ‚ü®T, cT, hT‚ü© := is_open_Union_countable (Œª s:S, s.1) (Œª s, H s.1 s.2) in
‚ü®subtype.val '' T, countable_image _ cT,
  image_subset_iff.2 $ Œª ‚ü®x, xs‚ü© xt, xs,
  by rwa [sUnion_image, sUnion_eq_Union]‚ü©

end topological_space
