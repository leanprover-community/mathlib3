/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/

import topology.continuous_on
import topology.constructions

/-!
# Bases of topologies. Countability axioms.

A topological basis on a topological space `t` is a collection of sets,
such that all open sets can be generated as unions of these sets, without the need to take
finite intersections of them. This file introduces a framework for dealing with these collections,
and also what more we can say under certain countability conditions on bases,
which are referred to as first- and second-countable.
We also briefly cover the theory of separable spaces, which are those with a countable, dense
subset. If a space is second-countable, and also has a countably generated uniformity filter
(for example, if `t` is a metric space), it will automatically be separable (and indeed, these
conditions are equivalent in this case).

## Main definitions

* `is_topological_basis s`: The topological space `t` has basis `s`.
* `separable_space Œ±`: The topological space `t` has a countable, dense subset.
* `first_countable_topology Œ±`: A topology in which `ùìù x` is countably generated for every `x`.
* `second_countable_topology Œ±`: A topology which has a topological basis which is countable.

## Main results

* `first_countable_topology.tendsto_subseq`: In a first-countable space,
  cluster points are limits of subsequences.
* `second_countable_topology.is_open_Union_countable`: In a second-countable space, the union of
  arbitrarily-many open sets is equal to a sub-union of only countably many of these sets.
* `second_countable_topology.countable_cover_nhds`: Consider `f : Œ± ‚Üí set Œ±` with the property that
  `f x ‚àà ùìù x` for all `x`. Then there is some countable set `s` whose image covers the space.

## Implementation Notes
For our applications we are interested that there exists a countable basis, but we do not need the
concrete basis itself. This allows us to declare these type classes as `Prop` to use them as mixins.

### TODO:
More fine grained instances for `first_countable_topology`, `separable_space`, `t2_space`, and more
(see the comment below `subtype.second_countable_topology`.)
-/

open set filter classical
open_locale topological_space filter
noncomputable theory

namespace topological_space

universe u
variables {Œ± : Type u} [t : topological_space Œ±]
include t

/-- A topological basis is one that satisfies the necessary conditions so that
  it suffices to take unions of the basis sets to get a topology (without taking
  finite intersections as well). -/
structure is_topological_basis (s : set (set Œ±)) : Prop :=
(exists_subset_inter : ‚àÄt‚ÇÅ‚ààs, ‚àÄt‚ÇÇ‚ààs, ‚àÄ x ‚àà t‚ÇÅ ‚à© t‚ÇÇ, ‚àÉ t‚ÇÉ‚ààs, x ‚àà t‚ÇÉ ‚àß t‚ÇÉ ‚äÜ t‚ÇÅ ‚à© t‚ÇÇ)
(sUnion_eq : (‚ãÉ‚ÇÄ s) = univ)
(eq_generate_from : t = generate_from s)

/-- If a family of sets `s` generates the topology, then nonempty intersections of finite
subcollections of `s` form a topological basis. -/
lemma is_topological_basis_of_subbasis {s : set (set Œ±)} (hs : t = generate_from s) :
  is_topological_basis ((Œª f, ‚ãÇ‚ÇÄ f) '' {f : set (set Œ±) | finite f ‚àß f ‚äÜ s ‚àß (‚ãÇ‚ÇÄ f).nonempty}) :=
begin
  refine ‚ü®_, _, _‚ü©,
  { rintro _ ‚ü®t‚ÇÅ, ‚ü®hft‚ÇÅ, ht‚ÇÅb, ht‚ÇÅ‚ü©, rfl‚ü© _ ‚ü®t‚ÇÇ, ‚ü®hft‚ÇÇ, ht‚ÇÇb, ht‚ÇÇ‚ü©, rfl‚ü© x h,
    have : ‚ãÇ‚ÇÄ (t‚ÇÅ ‚à™ t‚ÇÇ) = ‚ãÇ‚ÇÄ t‚ÇÅ ‚à© ‚ãÇ‚ÇÄ t‚ÇÇ := sInter_union t‚ÇÅ t‚ÇÇ,
    exact ‚ü®_, ‚ü®t‚ÇÅ ‚à™ t‚ÇÇ, ‚ü®hft‚ÇÅ.union hft‚ÇÇ, union_subset ht‚ÇÅb ht‚ÇÇb, this.symm ‚ñ∏ ‚ü®x, h‚ü©‚ü©, this‚ü©, h,
      subset.rfl‚ü© },
  { rw [sUnion_image, bUnion_eq_univ_iff],
    intro x, have : x ‚àà ‚ãÇ‚ÇÄ ‚àÖ, { rw sInter_empty, exact mem_univ x },
    exact ‚ü®‚àÖ, ‚ü®finite_empty, empty_subset _, x, this‚ü©, this‚ü© },
  { rw hs,
    apply le_antisymm; apply le_generate_from,
    { rintro _ ‚ü®t, ‚ü®hft, htb, ht‚ü©, rfl‚ü©,
      exact @is_open_sInter _ (generate_from s) _ hft (Œª s hs, generate_open.basic _ $ htb hs) },
    { intros t ht,
      rcases t.eq_empty_or_nonempty with rfl|hne, { apply @is_open_empty _ _ },
      rw ‚Üê sInter_singleton t at hne ‚ä¢,
      exact generate_open.basic _ ‚ü®{t}, ‚ü®finite_singleton t, singleton_subset_iff.2 ht, hne‚ü©,
        rfl‚ü© } }
end

/-- If a family of open sets `s` is such that every open neighbourhood contains some
member of `s`, then `s` is a topological basis. -/
lemma is_topological_basis_of_open_of_nhds {s : set (set Œ±)}
  (h_open : ‚àÄ u ‚àà s, is_open u)
  (h_nhds : ‚àÄ(a:Œ±) (u : set Œ±), a ‚àà u ‚Üí is_open u ‚Üí ‚àÉv ‚àà s, a ‚àà v ‚àß v ‚äÜ u) :
  is_topological_basis s :=
begin
  refine ‚ü®Œª t‚ÇÅ ht‚ÇÅ t‚ÇÇ ht‚ÇÇ x hx, h_nhds _ _ hx (is_open.inter (h_open _ ht‚ÇÅ) (h_open _ ht‚ÇÇ)), _, _‚ü©,
  { refine sUnion_eq_univ_iff.2 (Œª a, _),
    rcases h_nhds a univ trivial is_open_univ with ‚ü®u, h‚ÇÅ, h‚ÇÇ, -‚ü©,
    exact ‚ü®u, h‚ÇÅ, h‚ÇÇ‚ü© },
  { refine (le_generate_from h_open).antisymm (Œª u hu, _),
    refine (@is_open_iff_nhds Œ± (generate_from s) u).mpr (Œª a ha, _),
    rcases h_nhds a u ha hu with ‚ü®v, hvs, hav, hvu‚ü©,
    rw nhds_generate_from,
    exact binfi_le_of_le v ‚ü®hav, hvs‚ü© (le_principal_iff.2 hvu) }
end

/-- A set `s` is in the neighbourhood of `a` iff there is some basis set `t`, which
contains `a` and is itself contained in `s`. -/
lemma is_topological_basis.mem_nhds_iff {a : Œ±} {s : set Œ±} {b : set (set Œ±)}
  (hb : is_topological_basis b) : s ‚àà ùìù a ‚Üî ‚àÉt‚ààb, a ‚àà t ‚àß t ‚äÜ s :=
begin
  change s ‚àà (ùìù a).sets ‚Üî ‚àÉt‚ààb, a ‚àà t ‚àß t ‚äÜ s,
  rw [hb.eq_generate_from, nhds_generate_from, binfi_sets_eq],
  { simp only [mem_bUnion_iff, exists_prop, mem_set_of_eq, and_assoc, and.left_comm], refl },
  { exact assume s ‚ü®hs‚ÇÅ, hs‚ÇÇ‚ü© t ‚ü®ht‚ÇÅ, ht‚ÇÇ‚ü©,
      have a ‚àà s ‚à© t, from ‚ü®hs‚ÇÅ, ht‚ÇÅ‚ü©,
      let ‚ü®u, hu‚ÇÅ, hu‚ÇÇ, hu‚ÇÉ‚ü© := hb.1 _ hs‚ÇÇ _ ht‚ÇÇ _ this in
      ‚ü®u, ‚ü®hu‚ÇÇ, hu‚ÇÅ‚ü©, le_principal_iff.2 (subset.trans hu‚ÇÉ (inter_subset_left _ _)),
        le_principal_iff.2 (subset.trans hu‚ÇÉ (inter_subset_right _ _))‚ü© },
  { rcases eq_univ_iff_forall.1 hb.sUnion_eq a with ‚ü®i, h1, h2‚ü©,
    exact ‚ü®i, h2, h1‚ü© }
end

lemma is_topological_basis.nhds_has_basis {b : set (set Œ±)} (hb : is_topological_basis b) {a : Œ±} :
  (ùìù a).has_basis (Œª t : set Œ±, t ‚àà b ‚àß a ‚àà t) (Œª t, t) :=
‚ü®Œª s, hb.mem_nhds_iff.trans $ by simp only [exists_prop, and_assoc]‚ü©

protected lemma is_topological_basis.is_open {s : set Œ±} {b : set (set Œ±)}
  (hb : is_topological_basis b) (hs : s ‚àà b) : is_open s :=
by { rw hb.eq_generate_from, exact generate_open.basic s hs }

lemma is_topological_basis.exists_subset_of_mem_open {b : set (set Œ±)}
  (hb : is_topological_basis b) {a:Œ±} {u : set Œ±} (au : a ‚àà u)
  (ou : is_open u) : ‚àÉv ‚àà b, a ‚àà v ‚àß v ‚äÜ u :=
hb.mem_nhds_iff.1 $ is_open.mem_nhds ou au

 /-- Any open set is the union of the basis sets contained in it. -/
lemma is_topological_basis.open_eq_sUnion' {B : set (set Œ±)}
  (hB : is_topological_basis B) {u : set Œ±} (ou : is_open u) :
  u = ‚ãÉ‚ÇÄ {s ‚àà B | s ‚äÜ u} :=
ext $ Œª a,
‚ü®Œª ha, let ‚ü®b, hb, ab, bu‚ü© := hB.exists_subset_of_mem_open ha ou in ‚ü®b, ‚ü®hb, bu‚ü©, ab‚ü©,
  Œª ‚ü®b, ‚ü®hb, bu‚ü©, ab‚ü©, bu ab‚ü©

lemma is_topological_basis.open_eq_sUnion {B : set (set Œ±)}
  (hB : is_topological_basis B) {u : set Œ±} (ou : is_open u) :
  ‚àÉ S ‚äÜ B, u = ‚ãÉ‚ÇÄ S :=
‚ü®{s ‚àà B | s ‚äÜ u}, Œª s h, h.1, hB.open_eq_sUnion' ou‚ü©

lemma is_topological_basis.open_eq_Union {B : set (set Œ±)}
  (hB : is_topological_basis B) {u : set Œ±} (ou : is_open u) :
  ‚àÉ (Œ≤ : Type u) (f : Œ≤ ‚Üí set Œ±), u = (‚ãÉ i, f i) ‚àß ‚àÄ i, f i ‚àà B :=
‚ü®‚Ü•{s ‚àà B | s ‚äÜ u}, coe, by { rw ‚Üê sUnion_eq_Union, apply hB.open_eq_sUnion' ou }, Œª s, and.left s.2‚ü©

 /-- A point `a` is in the closure of `s` iff all basis sets containing `a` intersect `s`. -/
lemma is_topological_basis.mem_closure_iff {b : set (set Œ±)} (hb : is_topological_basis b)
  {s : set Œ±} {a : Œ±} :
  a ‚àà closure s ‚Üî ‚àÄ o ‚àà b, a ‚àà o ‚Üí (o ‚à© s).nonempty :=
(mem_closure_iff_nhds_basis' hb.nhds_has_basis).trans $ by simp only [and_imp]

 /-- A set is dense iff it has non-trivial intersection with all basis sets. -/
lemma is_topological_basis.dense_iff {b : set (set Œ±)} (hb : is_topological_basis b) {s : set Œ±} :
  dense s ‚Üî ‚àÄ o ‚àà b, set.nonempty o ‚Üí (o ‚à© s).nonempty :=
begin
  simp only [dense, hb.mem_closure_iff],
  exact ‚ü®Œª h o hb ‚ü®a, ha‚ü©, h a o hb ha, Œª h a o hb ha, h o hb ‚ü®a, ha‚ü©‚ü©
end

lemma is_topological_basis.is_open_map_iff {Œ≤} [topological_space Œ≤] {B : set (set Œ±)}
  (hB : is_topological_basis B) {f : Œ± ‚Üí Œ≤} :
  is_open_map f ‚Üî ‚àÄ s ‚àà B, is_open (f '' s) :=
begin
  refine ‚ü®Œª H o ho, H _ (hB.is_open ho), Œª hf o ho, _‚ü©,
  rw [hB.open_eq_sUnion' ho, sUnion_eq_Union, image_Union],
  exact is_open_Union (Œª s, hf s s.2.1)
end

lemma is_topological_basis.exists_nonempty_subset {B : set (set Œ±)}
  (hb : is_topological_basis B) {u : set Œ±} (hu : u.nonempty) (ou : is_open u) :
  ‚àÉ v ‚àà B, set.nonempty v ‚àß v ‚äÜ u :=
begin
  cases hu with x hx,
  rw [hb.open_eq_sUnion' ou, mem_sUnion] at hx,
  rcases hx with ‚ü®v, hv, hxv‚ü©,
  exact ‚ü®v, hv.1, ‚ü®x, hxv‚ü©, hv.2‚ü©
end

lemma is_topological_basis_opens : is_topological_basis { U : set Œ± | is_open U } :=
is_topological_basis_of_open_of_nhds (by tauto) (by tauto)

protected lemma is_topological_basis.prod {Œ≤} [topological_space Œ≤] {B‚ÇÅ : set (set Œ±)}
  {B‚ÇÇ : set (set Œ≤)} (h‚ÇÅ : is_topological_basis B‚ÇÅ) (h‚ÇÇ : is_topological_basis B‚ÇÇ) :
  is_topological_basis (image2 set.prod B‚ÇÅ B‚ÇÇ) :=
begin
  refine is_topological_basis_of_open_of_nhds _ _,
  { rintro _ ‚ü®u‚ÇÅ, u‚ÇÇ, hu‚ÇÅ, hu‚ÇÇ, rfl‚ü©,
    exact (h‚ÇÅ.is_open hu‚ÇÅ).prod (h‚ÇÇ.is_open hu‚ÇÇ) },
  { rintro ‚ü®a, b‚ü© u hu uo,
    rcases (h‚ÇÅ.nhds_has_basis.prod_nhds h‚ÇÇ.nhds_has_basis).mem_iff.1 (is_open.mem_nhds uo hu)
      with ‚ü®‚ü®s, t‚ü©, ‚ü®‚ü®hs, ha‚ü©, ht, hb‚ü©, hu‚ü©,
    exact ‚ü®s.prod t, mem_image2_of_mem hs ht, ‚ü®ha, hb‚ü©, hu‚ü© }
end

protected lemma is_topological_basis.inducing {Œ≤} [topological_space Œ≤]
  {f : Œ± ‚Üí Œ≤} {T : set (set Œ≤)} (hf : inducing f) (h : is_topological_basis T) :
  is_topological_basis (image (preimage f) T) :=
begin
  refine is_topological_basis_of_open_of_nhds _ _,
  { rintros _ ‚ü®V, hV, rfl‚ü©,
    rwa hf.is_open_iff,
    refine ‚ü®V, h.is_open hV, rfl‚ü© },
  { intros a U ha hU,
    rw hf.is_open_iff at hU,
    obtain ‚ü®V, hV, rfl‚ü© := hU,
    obtain ‚ü®S, hS, rfl‚ü© := h.open_eq_sUnion hV,
    obtain ‚ü®W, hW, ha‚ü© := ha,
    refine ‚ü®f ‚Åª¬π' W, ‚ü®_, hS hW, rfl‚ü©, ha, set.preimage_mono $ set.subset_sUnion_of_mem hW‚ü© }
end

lemma is_topological_basis_of_cover {Œπ} {U  : Œπ ‚Üí set Œ±} (Uo : ‚àÄ i, is_open (U i))
  (Uc : (‚ãÉ i, U i) = univ) {b : Œ† i, set (set (U i))} (hb : ‚àÄ i, is_topological_basis (b i)) :
  is_topological_basis (‚ãÉ i : Œπ, image (coe : U i ‚Üí Œ±) '' (b i)) :=
begin
  refine is_topological_basis_of_open_of_nhds (Œª u hu, _) _,
  { simp only [mem_Union, mem_image] at hu,
    rcases hu with ‚ü®i, s, sb, rfl‚ü©,
    exact (Uo i).is_open_map_subtype_coe _ ((hb i).is_open sb) },
  { intros a u ha uo,
    rcases Union_eq_univ_iff.1 Uc a with ‚ü®i, hi‚ü©,
    lift a to ‚Ü•(U i) using hi,
    rcases (hb i).exists_subset_of_mem_open (by exact ha) (uo.preimage continuous_subtype_coe)
      with ‚ü®v, hvb, hav, hvu‚ü©,
    exact ‚ü®coe '' v, mem_Union.2 ‚ü®i, mem_image_of_mem _ hvb‚ü©, mem_image_of_mem _ hav,
      image_subset_iff.2 hvu‚ü© }
end

protected lemma is_topological_basis.continuous {Œ≤ : Type*} [topological_space Œ≤]
  {B : set (set Œ≤)} (hB : is_topological_basis B) (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ s ‚àà B, is_open (f ‚Åª¬π' s)) :
  continuous f :=
begin rw hB.eq_generate_from, exact continuous_generated_from hf end

variables (Œ±)

/-- A separable space is one with a countable dense subset, available through
`topological_space.exists_countable_dense`. If `Œ±` is also known to be nonempty, then
`topological_space.dense_seq` provides a sequence `‚Ñï ‚Üí Œ±` with dense range, see
`topological_space.dense_range_dense_seq`.

If `Œ±` is a uniform space with countably generated uniformity filter (e.g., an `emetric_space`),
then this condition is equivalent to `topological_space.second_countable_topology Œ±`. In this case
the latter should be used as a typeclass argument in theorems because Lean can automatically deduce
`separable_space` from `second_countable_topology` but it can't deduce `second_countable_topology`
and `emetric_space`. -/
class separable_space : Prop :=
(exists_countable_dense : ‚àÉs:set Œ±, countable s ‚àß dense s)

lemma exists_countable_dense [separable_space Œ±] :
  ‚àÉ s : set Œ±, countable s ‚àß dense s :=
separable_space.exists_countable_dense

/-- A nonempty separable space admits a sequence with dense range. Instead of running `cases` on the
conclusion of this lemma, you might want to use `topological_space.dense_seq` and
`topological_space.dense_range_dense_seq`.

If `Œ±` might be empty, then `exists_countable_dense` is the main way to use separability of `Œ±`. -/
lemma exists_dense_seq [separable_space Œ±] [nonempty Œ±] : ‚àÉ u : ‚Ñï ‚Üí Œ±, dense_range u :=
begin
  obtain ‚ü®s : set Œ±, hs, s_dense‚ü© := exists_countable_dense Œ±,
  cases countable_iff_exists_surjective.mp hs with u hu,
  exact ‚ü®u, s_dense.mono hu‚ü©,
end

/-- A dense sequence in a non-empty separable topological space.

If `Œ±` might be empty, then `exists_countable_dense` is the main way to use separability of `Œ±`. -/
def dense_seq [separable_space Œ±] [nonempty Œ±] : ‚Ñï ‚Üí Œ± := classical.some (exists_dense_seq Œ±)

/-- The sequence `dense_seq Œ±` has dense range. -/
@[simp] lemma dense_range_dense_seq [separable_space Œ±] [nonempty Œ±] :
  dense_range (dense_seq Œ±) := classical.some_spec (exists_dense_seq Œ±)

variable {Œ±}

/-- In a separable space, a family of nonempty disjoint open sets is countable. -/
lemma countable_of_is_open_of_disjoint [separable_space Œ±] {Œ≤ : Type*}
  (s : Œ≤ ‚Üí set Œ±) {a : set Œ≤} (ha : ‚àÄ i ‚àà a, is_open (s i)) (h'a : ‚àÄ i ‚àà a, (s i).nonempty)
  (h : a.pairwise_on (disjoint on s)) :
  countable a :=
begin
  rcases eq_empty_or_nonempty a with rfl|H, { exact countable_empty },
  haveI : inhabited Œ±,
  { choose i ia using H,
    choose y hy using h'a i ia,
    exact ‚ü®y‚ü© },
  rcases exists_countable_dense Œ± with ‚ü®u, u_count, u_dense‚ü©,
  have : ‚àÄ i, i ‚àà a ‚Üí ‚àÉ y, y ‚àà s i ‚à© u :=
    Œª i hi, dense_iff_inter_open.1 u_dense (s i) (ha i hi) (h'a i hi),
  choose! f hf using this,
  have f_inj : inj_on f a,
  { assume i hi j hj hij,
    have : ¬¨disjoint (s i) (s j),
    { rw not_disjoint_iff_nonempty_inter,
      refine ‚ü®f i, (hf i hi).1, _‚ü©,
      rw hij,
      exact (hf j hj).1 },
    contrapose! this,
    exact h i hi j hj this },
  apply countable_of_injective_of_countable_image f_inj,
  apply u_count.mono _,
  exact image_subset_iff.2 (Œª i hi, (hf i hi).2)
end

/-- In a separable space, a family of disjoint sets with nonempty interiors is countable. -/
lemma countable_of_nonempty_interior_of_disjoint [separable_space Œ±] {Œ≤ : Type*} (s : Œ≤ ‚Üí set Œ±)
  {a : set Œ≤} (ha : ‚àÄ i ‚àà a, (interior (s i)).nonempty) (h : a.pairwise_on (disjoint on s)) :
  countable a :=
begin
  have : a.pairwise_on (disjoint on (Œª i, interior (s i))) :=
    pairwise_on_disjoint_on_mono h (Œª i hi, interior_subset),
  exact countable_of_is_open_of_disjoint (Œª i, interior (s i)) (Œª i hi, is_open_interior) ha this
end

end topological_space

open topological_space

lemma is_topological_basis_pi {Œπ : Type*} {X : Œπ ‚Üí Type*}
  [‚àÄ i, topological_space (X i)] {T : Œ† i, set (set (X i))}
  (cond : ‚àÄ i, is_topological_basis (T i)) :
  is_topological_basis {S : set (Œ† i, X i) | ‚àÉ (U : Œ† i, set (X i)) (F : finset Œπ),
    (‚àÄ i, i ‚àà F ‚Üí (U i) ‚àà T i) ‚àß S = (F : set Œπ).pi U } :=
begin
  classical,
  refine is_topological_basis_of_open_of_nhds _ _,
  { rintro _ ‚ü®U, F, h1, rfl‚ü©,
    apply is_open_set_pi F.finite_to_set,
    intros i hi,
    exact is_topological_basis.is_open (cond i) (h1 i hi) },
  { intros a U ha hU,
    have : U ‚àà nhds a := is_open.mem_nhds hU ha,
    rw [nhds_pi, filter.mem_infi] at this,
    obtain ‚ü®F, hF, V, hV1, rfl‚ü© := this,
    choose U' hU' using hV1,
    obtain ‚ü®hU1, hU2‚ü© := ‚ü®Œª i, (hU' i).1, Œª i, (hU' i).2‚ü©,
    have : ‚àÄ j : F, ‚àÉ (T' : set (X j)) (hT : T' ‚àà T j), a j ‚àà T' ‚àß T' ‚äÜ U' j,
    { intros i,
      specialize hU1 i,
      rwa (cond i).mem_nhds_iff at hU1 },
    choose U'' hU'' using this,
    let U : Œ† (i : Œπ), set (X i) := Œª i,
      if hi : i ‚àà F then U'' ‚ü®i, hi‚ü© else set.univ,
    refine ‚ü®F.pi U, ‚ü®U, hF.to_finset, Œª i hi, _, by simp‚ü©, _, _‚ü©,
    { dsimp only [U],
      rw [dif_pos],
      swap, { simpa using hi },
      exact (hU'' _).1 },
    { rw set.mem_pi,
      intros i hi,
      dsimp only [U],
      rw dif_pos hi,
      exact (hU'' _).2.1 },
    { intros x hx,
      rintros - ‚ü®i, rfl‚ü©,
      refine hU2 i ((hU'' i).2.2 _),
      convert hx i i.2,
      rcases i with ‚ü®i, p‚ü©,
      dsimp [U],
      rw dif_pos p, } },
end

lemma is_topological_basis_infi {Œ≤ : Type*} {Œπ : Type*} {X : Œπ ‚Üí Type*}
  [t : ‚àÄ i, topological_space (X i)] {T : Œ† i, set (set (X i))}
  (cond : ‚àÄ i, is_topological_basis (T i)) (f : Œ† i, Œ≤ ‚Üí X i) :
  @is_topological_basis Œ≤ (‚®Ö i, induced (f i) (t i))
  { S | ‚àÉ (U : Œ† i, set (X i)) (F : finset Œπ),
    (‚àÄ i, i ‚àà F ‚Üí U i ‚àà T i) ‚àß S = ‚ãÇ i (hi : i ‚àà F), (f i) ‚Åª¬π' (U i) } :=
begin
  convert (is_topological_basis_pi cond).inducing (inducing_infi_to_pi _),
  ext V,
  split,
  { rintros ‚ü®U, F, h1, h2‚ü©,
    have : (F : set Œπ).pi U = (‚ãÇ (i : Œπ) (hi : i ‚àà F),
        (Œª (z : Œ† j, X j), z i) ‚Åª¬π' (U i)), by { ext, simp },
    refine ‚ü®(F : set Œπ).pi U, ‚ü®U, F, h1, rfl‚ü©, _‚ü©,
    rw [this, h2, set.preimage_Inter],
    congr' 1,
    ext1,
    rw set.preimage_Inter,
    refl },
  { rintros ‚ü®U, ‚ü®U, F, h1, rfl‚ü©, h‚ü©,
    refine ‚ü®U, F, h1, _‚ü©,
    have : (F : set Œπ).pi U = (‚ãÇ (i : Œπ) (hi : i ‚àà F),
        (Œª (z : Œ† j, X j), z i) ‚Åª¬π' (U i)), by { ext, simp },
    rw [‚Üê h, this, set.preimage_Inter],
    congr' 1,
    ext1,
    rw set.preimage_Inter,
    refl }
end

/-- If `Œ±` is a separable space and `f : Œ± ‚Üí Œ≤` is a continuous map with dense range, then `Œ≤` is
a separable space as well. E.g., the completion of a separable uniform space is separable. -/
protected lemma dense_range.separable_space {Œ± Œ≤ : Type*} [topological_space Œ±] [separable_space Œ±]
  [topological_space Œ≤] {f : Œ± ‚Üí Œ≤} (h : dense_range f) (h' : continuous f) :
  separable_space Œ≤ :=
let ‚ü®s, s_cnt, s_dense‚ü© := exists_countable_dense Œ± in
‚ü®‚ü®f '' s, countable.image s_cnt f, h.dense_image h' s_dense‚ü©‚ü©

lemma dense.exists_countable_dense_subset {Œ± : Type*} [topological_space Œ±]
  {s : set Œ±} [separable_space s] (hs : dense s) :
  ‚àÉ t ‚äÜ s, countable t ‚àß dense t :=
let ‚ü®t, htc, htd‚ü© := exists_countable_dense s
in ‚ü®coe '' t, image_subset_iff.2 $ Œª x _, mem_preimage.2 $ subtype.coe_prop _, htc.image coe,
  hs.dense_range_coe.dense_image continuous_subtype_val htd‚ü©

namespace topological_space
universe u
variables (Œ± : Type u) [t : topological_space Œ±]
include t


/-- A first-countable space is one in which every point has a
  countable neighborhood basis. -/
class first_countable_topology : Prop :=
(nhds_generated_countable : ‚àÄa:Œ±, (ùìù a).is_countably_generated)

attribute [instance] first_countable_topology.nhds_generated_countable

namespace first_countable_topology
variable {Œ±}

/-- In a first-countable space, a cluster point `x` of a sequence
is the limit of some subsequence. -/
lemma tendsto_subseq [first_countable_topology Œ±] {u : ‚Ñï ‚Üí Œ±} {x : Œ±}
  (hx : map_cluster_pt x at_top u) :
  ‚àÉ (œà : ‚Ñï ‚Üí ‚Ñï), (strict_mono œà) ‚àß (tendsto (u ‚àò œà) at_top (ùìù x)) :=
subseq_tendsto_of_ne_bot hx

end first_countable_topology

variables {Œ±}

instance is_countably_generated_nhds_within (x : Œ±) [is_countably_generated (ùìù x)] (s : set Œ±) :
  is_countably_generated (ùìù[s] x) :=
inf.is_countably_generated _ _

variable (Œ±)

/-- A second-countable space is one with a countable basis. -/
class second_countable_topology : Prop :=
(is_open_generated_countable [] :
  ‚àÉ b : set (set Œ±), countable b ‚àß t = topological_space.generate_from b)

variable {Œ±}

protected lemma is_topological_basis.second_countable_topology
  {b : set (set Œ±)} (hb : is_topological_basis b) (hc : countable b) :
  second_countable_topology Œ± :=
‚ü®‚ü®b, hc, hb.eq_generate_from‚ü©‚ü©

variable (Œ±)

lemma exists_countable_basis [second_countable_topology Œ±] :
  ‚àÉb:set (set Œ±), countable b ‚àß ‚àÖ ‚àâ b ‚àß is_topological_basis b :=
let ‚ü®b, hb‚ÇÅ, hb‚ÇÇ‚ü© := second_countable_topology.is_open_generated_countable Œ± in
let b' := (Œªs, ‚ãÇ‚ÇÄ s) '' {s:set (set Œ±) | finite s ‚àß s ‚äÜ b ‚àß (‚ãÇ‚ÇÄ s).nonempty} in
‚ü®b',
  ((countable_set_of_finite_subset hb‚ÇÅ).mono
    (by { simp only [‚Üê and_assoc], apply inter_subset_left })).image _,
  assume ‚ü®s, ‚ü®_, _, hn‚ü©, hp‚ü©, absurd hn (not_nonempty_iff_eq_empty.2 hp),
  is_topological_basis_of_subbasis hb‚ÇÇ‚ü©

/-- A countable topological basis of `Œ±`. -/
def countable_basis [second_countable_topology Œ±] : set (set Œ±) :=
(exists_countable_basis Œ±).some

lemma countable_countable_basis [second_countable_topology Œ±] : countable (countable_basis Œ±) :=
(exists_countable_basis Œ±).some_spec.1

instance encodable_countable_basis [second_countable_topology Œ±] :
  encodable (countable_basis Œ±) :=
(countable_countable_basis Œ±).to_encodable

lemma empty_nmem_countable_basis [second_countable_topology Œ±] : ‚àÖ ‚àâ countable_basis Œ± :=
(exists_countable_basis Œ±).some_spec.2.1

lemma is_basis_countable_basis [second_countable_topology Œ±] :
  is_topological_basis (countable_basis Œ±) :=
(exists_countable_basis Œ±).some_spec.2.2

lemma eq_generate_from_countable_basis [second_countable_topology Œ±] :
  ‚Äπtopological_space Œ±‚Ä∫ = generate_from (countable_basis Œ±) :=
(is_basis_countable_basis Œ±).eq_generate_from

variable {Œ±}

lemma is_open_of_mem_countable_basis [second_countable_topology Œ±] {s : set Œ±}
  (hs : s ‚àà countable_basis Œ±) : is_open s :=
(is_basis_countable_basis Œ±).is_open hs

lemma nonempty_of_mem_countable_basis [second_countable_topology Œ±] {s : set Œ±}
  (hs : s ‚àà countable_basis Œ±) : s.nonempty :=
ne_empty_iff_nonempty.1 $ ne_of_mem_of_not_mem hs $ empty_nmem_countable_basis Œ±

variable (Œ±)

@[priority 100] -- see Note [lower instance priority]
instance second_countable_topology.to_first_countable_topology
  [second_countable_topology Œ±] : first_countable_topology Œ± :=
‚ü®Œª x, has_countable_basis.is_countably_generated $
  ‚ü®(is_basis_countable_basis Œ±).nhds_has_basis, (countable_countable_basis Œ±).mono $
    inter_subset_left _ _‚ü©‚ü©

/-- If `Œ≤` is a second-countable space, then its induced topology
via `f` on `Œ±` is also second-countable. -/
lemma second_countable_topology_induced (Œ≤)
  [t : topological_space Œ≤] [second_countable_topology Œ≤] (f : Œ± ‚Üí Œ≤) :
  @second_countable_topology Œ± (t.induced f) :=
begin
  rcases second_countable_topology.is_open_generated_countable Œ≤ with ‚ü®b, hb, eq‚ü©,
  refine { is_open_generated_countable := ‚ü®preimage f '' b, hb.image _, _‚ü© },
  rw [eq, induced_generate_from_eq]
end

instance subtype.second_countable_topology (s : set Œ±) [second_countable_topology Œ±] :
  second_countable_topology s :=
second_countable_topology_induced s Œ± coe

/- TODO: more fine grained instances for first_countable_topology, separable_space, t2_space, ... -/
instance {Œ≤ : Type*} [topological_space Œ≤]
  [second_countable_topology Œ±] [second_countable_topology Œ≤] : second_countable_topology (Œ± √ó Œ≤) :=
((is_basis_countable_basis Œ±).prod (is_basis_countable_basis Œ≤)).second_countable_topology $
  (countable_countable_basis Œ±).image2 (countable_countable_basis Œ≤) _

instance second_countable_topology_fintype {Œπ : Type*} {œÄ : Œπ ‚Üí Type*}
  [fintype Œπ] [t : ‚àÄa, topological_space (œÄ a)] [sc : ‚àÄa, second_countable_topology (œÄ a)] :
  second_countable_topology (‚àÄa, œÄ a) :=
begin
  have : t = (Œªa, generate_from (countable_basis (œÄ a))),
    from funext (assume a, (is_basis_countable_basis (œÄ a)).eq_generate_from),
  rw this,
  constructor,
  refine ‚ü®pi univ '' pi univ (Œª a, countable_basis (œÄ a)), countable.image _ _, _‚ü©,
  { suffices : countable {f : Œ†a, set (œÄ a) | ‚àÄa, f a ‚àà countable_basis (œÄ a)}, { simpa [pi] },
    exact countable_pi (assume i, (countable_countable_basis _)), },
  rw [pi_generate_from_eq_fintype],
  { congr' 1 with f, simp [pi, eq_comm] },
  exact assume a, (is_basis_countable_basis (œÄ a)).sUnion_eq
end

@[priority 100] -- see Note [lower instance priority]
instance second_countable_topology.to_separable_space
  [second_countable_topology Œ±] : separable_space Œ± :=
begin
  choose p hp using Œª s : countable_basis Œ±, nonempty_of_mem_countable_basis s.2,
  exact ‚ü®‚ü®range p, countable_range _,
    (is_basis_countable_basis Œ±).dense_iff.2 $ Œª o ho _, ‚ü®p ‚ü®o, ho‚ü©, hp _, mem_range_self _‚ü©‚ü©‚ü©
end

variables {Œ±}

/-- A countable open cover induces a second-countable topology if all open covers
are themselves second countable. -/
lemma second_countable_topology_of_countable_cover {Œπ} [encodable Œπ] {U : Œπ ‚Üí set Œ±}
  [‚àÄ i, second_countable_topology (U i)] (Uo : ‚àÄ i, is_open (U i))  (hc : (‚ãÉ i, U i) = univ) :
  second_countable_topology Œ± :=
begin
  have : is_topological_basis (‚ãÉ i, image (coe : U i ‚Üí Œ±) '' (countable_basis (U i))),
    from is_topological_basis_of_cover Uo hc (Œª i, is_basis_countable_basis (U i)),
  exact this.second_countable_topology
    (countable_Union $ Œª i, (countable_countable_basis _).image _)
end

/-- In a second-countable space, an open set, given as a union of open sets,
is equal to the union of countably many of those sets. -/
lemma is_open_Union_countable [second_countable_topology Œ±]
  {Œπ} (s : Œπ ‚Üí set Œ±) (H : ‚àÄ i, is_open (s i)) :
  ‚àÉ T : set Œπ, countable T ‚àß (‚ãÉ i ‚àà T, s i) = ‚ãÉ i, s i :=
begin
  let B := {b ‚àà countable_basis Œ± | ‚àÉ i, b ‚äÜ s i},
  choose f hf using Œª b : B, b.2.2,
  haveI : encodable B := ((countable_countable_basis Œ±).mono (sep_subset _ _)).to_encodable,
  refine ‚ü®_, countable_range f,
    subset.antisymm (bUnion_subset_Union _ _) (sUnion_subset _)‚ü©,
  rintro _ ‚ü®i, rfl‚ü© x xs,
  rcases (is_basis_countable_basis Œ±).exists_subset_of_mem_open xs (H _) with ‚ü®b, hb, xb, bs‚ü©,
  exact ‚ü®_, ‚ü®_, rfl‚ü©, _, ‚ü®‚ü®‚ü®_, hb, _, bs‚ü©, rfl‚ü©, rfl‚ü©, hf _ (by exact xb)‚ü©
end

lemma is_open_sUnion_countable [second_countable_topology Œ±]
  (S : set (set Œ±)) (H : ‚àÄ s ‚àà S, is_open s) :
  ‚àÉ T : set (set Œ±), countable T ‚àß T ‚äÜ S ‚àß ‚ãÉ‚ÇÄ T = ‚ãÉ‚ÇÄ S :=
let ‚ü®T, cT, hT‚ü© := is_open_Union_countable (Œª s:S, s.1) (Œª s, H s.1 s.2) in
‚ü®subtype.val '' T, cT.image _,
  image_subset_iff.2 $ Œª ‚ü®x, xs‚ü© xt, xs,
  by rwa [sUnion_image, sUnion_eq_Union]‚ü©

/-- In a topological space with second countable topology, if `f` is a function that sends each
point `x` to a neighborhood of `x`, then for some countable set `s`, the neighborhoods `f x`,
`x ‚àà s`, cover the whole space. -/
lemma countable_cover_nhds [second_countable_topology Œ±] {f : Œ± ‚Üí set Œ±}
  (hf : ‚àÄ x, f x ‚àà ùìù x) : ‚àÉ s : set Œ±, countable s ‚àß (‚ãÉ x ‚àà s, f x) = univ :=
begin
  rcases is_open_Union_countable (Œª x, interior (f x)) (Œª x, is_open_interior) with ‚ü®s, hsc, hsU‚ü©,
  suffices : (‚ãÉ x ‚àà s, interior (f x)) = univ,
    from ‚ü®s, hsc, flip eq_univ_of_subset this (bUnion_mono $ Œª _ _, interior_subset)‚ü©,
  simp only [hsU, eq_univ_iff_forall, mem_Union],
  exact Œª x, ‚ü®x, mem_interior_iff_mem_nhds.2 (hf x)‚ü©
end

lemma countable_cover_nhds_within [second_countable_topology Œ±] {f : Œ± ‚Üí set Œ±} {s : set Œ±}
  (hf : ‚àÄ x ‚àà s, f x ‚àà ùìù[s] x) : ‚àÉ t ‚äÜ s, countable t ‚àß s ‚äÜ (‚ãÉ x ‚àà t, f x) :=
begin
  have : ‚àÄ x : s, coe ‚Åª¬π' (f x) ‚àà ùìù x, from Œª x, preimage_coe_mem_nhds_subtype.2 (hf x x.2),
  rcases countable_cover_nhds this with ‚ü®t, htc, htU‚ü©,
  refine ‚ü®coe '' t, subtype.coe_image_subset _ _, htc.image _, Œª x hx, _‚ü©,
  simp only [bUnion_image, eq_univ_iff_forall, ‚Üê preimage_Union, mem_preimage] at htU ‚ä¢,
  exact htU ‚ü®x, hx‚ü©
end

end topological_space

open topological_space

variables {Œ± Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤] {f : Œ± ‚Üí Œ≤}

protected lemma inducing.second_countable_topology [second_countable_topology Œ≤]
  (hf : inducing f) : second_countable_topology Œ± :=
by { rw hf.1, exact second_countable_topology_induced Œ± Œ≤ f }

protected lemma embedding.second_countable_topology [second_countable_topology Œ≤]
  (hf : embedding f) : second_countable_topology Œ± :=
hf.1.second_countable_topology
