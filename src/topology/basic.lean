/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Jeremy Avigad
-/
import order.filter.ultrafilter
import algebra.support
import order.filter.lift

/-!
# Basic theory of topological spaces.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

The main definition is the type class `topological_space Î±` which endows a type `Î±` with a topology.
Then `set Î±` gets predicates `is_open`, `is_closed` and functions `interior`, `closure` and
`frontier`. Each point `x` of `Î±` gets a neighborhood filter `ğ“ x`. A filter `F` on `Î±` has
`x` as a cluster point if `cluster_pt x F : ğ“ x âŠ“ F â‰  âŠ¥`. A map `f : Î¹ â†’ Î±` clusters at `x`
along `F : filter Î¹` if `map_cluster_pt x F f : cluster_pt x (map f F)`. In particular
the notion of cluster point of a sequence `u` is `map_cluster_pt x at_top u`.

For topological spaces `Î±` and `Î²`, a function `f : Î± â†’ Î²` and a point `a : Î±`,
`continuous_at f a` means `f` is continuous at `a`, and global continuity is
`continuous f`. There is also a version of continuity `pcontinuous` for
partially defined functions.

## Notation

* `ğ“ x`: the filter `nhds x` of neighborhoods of a point `x`;
* `ğ“Ÿ s`: the principal filter of a set `s`;
* `ğ“[s] x`: the filter `nhds_within x s` of neighborhoods of a point `x` within a set `s`;
* `ğ“[â‰¤] x`: the filter `nhds_within x (set.Iic x)` of left-neighborhoods of `x`;
* `ğ“[â‰¥] x`: the filter `nhds_within x (set.Ici x)` of right-neighborhoods of `x`;
* `ğ“[<] x`: the filter `nhds_within x (set.Iio x)` of punctured left-neighborhoods of `x`;
* `ğ“[>] x`: the filter `nhds_within x (set.Ioi x)` of punctured right-neighborhoods of `x`;
* `ğ“[â‰ ] x`: the filter `nhds_within x {x}á¶œ` of punctured neighborhoods of `x`.

## Implementation notes

Topology in mathlib heavily uses filters (even more than in Bourbaki). See explanations in
<https://leanprover-community.github.io/theories/topology.html>.

## References

*  [N. Bourbaki, *General Topology*][bourbaki1966]
*  [I. M. James, *Topologies and Uniformities*][james1999]

## Tags

topological space, interior, closure, frontier, neighborhood, continuity, continuous function
-/

noncomputable theory
open set filter classical
open_locale classical filter

universes u v w

/-!
### Topological spaces
-/

/-- A topology on `Î±`. -/
@[protect_proj] class topological_space (Î± : Type u) :=
(is_open        : set Î± â†’ Prop)
(is_open_univ   : is_open univ)
(is_open_inter  : âˆ€s t, is_open s â†’ is_open t â†’ is_open (s âˆ© t))
(is_open_sUnion : âˆ€s, (âˆ€tâˆˆs, is_open t) â†’ is_open (â‹ƒâ‚€ s))

/-- A constructor for topologies by specifying the closed sets,
and showing that they satisfy the appropriate conditions. -/
def topological_space.of_closed {Î± : Type u} (T : set (set Î±))
  (empty_mem : âˆ… âˆˆ T) (sInter_mem : âˆ€ A âŠ† T, â‹‚â‚€ A âˆˆ T) (union_mem : âˆ€ A B âˆˆ T, A âˆª B âˆˆ T) :
  topological_space Î± :=
{ is_open := Î» X, Xá¶œ âˆˆ T,
  is_open_univ := by simp [empty_mem],
  is_open_inter := Î» s t hs ht, by simpa only [compl_inter] using union_mem sá¶œ hs tá¶œ ht,
  is_open_sUnion := Î» s hs,
    by rw set.compl_sUnion; exact sInter_mem (compl '' s)
    (Î» z âŸ¨y, hy, hzâŸ©, by simpa [hz.symm] using hs y hy) }

section topological_space

variables {Î± : Type u} {Î² : Type v} {Î¹ : Sort w} {a : Î±} {s sâ‚ sâ‚‚ t : set Î±} {p pâ‚ pâ‚‚ : Î± â†’ Prop}

/-- `is_open s` means that `s` is open in the ambient topological space on `Î±` -/
def is_open [topological_space Î±] (s : set Î±) : Prop := @topological_space.is_open _ â€¹_â€º s

localized "notation (name := is_open_of) `is_open[` t `]` := @is_open hole! t" in topology

lemma is_open_mk {p hâ‚ hâ‚‚ hâ‚ƒ} {s : set Î±} : is_open[âŸ¨p, hâ‚, hâ‚‚, hâ‚ƒâŸ©] s â†” p s := iff.rfl

@[ext]
lemma topological_space_eq {f g : topological_space Î±} (h : is_open[f] = is_open[g]) : f = g :=
by unfreezingI { cases f, cases g, congr, exact h }

section
variables [topological_space Î±]

@[simp]
lemma is_open_univ : is_open (univ : set Î±) := topological_space.is_open_univ

lemma is_open.inter (hâ‚ : is_open sâ‚) (hâ‚‚ : is_open sâ‚‚) : is_open (sâ‚ âˆ© sâ‚‚) :=
topological_space.is_open_inter sâ‚ sâ‚‚ hâ‚ hâ‚‚

lemma is_open_sUnion {s : set (set Î±)} (h : âˆ€t âˆˆ s, is_open t) : is_open (â‹ƒâ‚€ s) :=
topological_space.is_open_sUnion s h

end

lemma topological_space_eq_iff {t t' : topological_space Î±} :
  t = t' â†” âˆ€ s, is_open[t] s â†” is_open[t'] s :=
âŸ¨Î» h s, h â–¸ iff.rfl, Î» h, by { ext, exact h _ }âŸ©

lemma is_open_fold {s : set Î±} {t : topological_space Î±} : t.is_open s = is_open[t] s :=
rfl

variables [topological_space Î±]

lemma is_open_Union {f : Î¹ â†’ set Î±} (h : âˆ€i, is_open (f i)) : is_open (â‹ƒi, f i) :=
is_open_sUnion $ by rintro _ âŸ¨i, rflâŸ©; exact h i

lemma is_open_bUnion {s : set Î²} {f : Î² â†’ set Î±} (h : âˆ€iâˆˆs, is_open (f i)) :
  is_open (â‹ƒiâˆˆs, f i) :=
is_open_Union $ assume i, is_open_Union $ assume hi, h i hi

lemma is_open.union (hâ‚ : is_open sâ‚) (hâ‚‚ : is_open sâ‚‚) : is_open (sâ‚ âˆª sâ‚‚) :=
by rw union_eq_Union; exact is_open_Union (bool.forall_bool.2 âŸ¨hâ‚‚, hâ‚âŸ©)

@[simp] lemma is_open_empty : is_open (âˆ… : set Î±) :=
by rw â† sUnion_empty; exact is_open_sUnion (assume a, false.elim)

lemma is_open_sInter {s : set (set Î±)} (hs : s.finite) : (âˆ€t âˆˆ s, is_open t) â†’ is_open (â‹‚â‚€ s) :=
finite.induction_on hs (Î» _, by rw sInter_empty; exact is_open_univ) $
Î» a s has hs ih h, by rw sInter_insert; exact
is_open.inter (h _ $ mem_insert _ _) (ih $ Î» t, h t âˆ˜ mem_insert_of_mem _)

lemma is_open_bInter {s : set Î²} {f : Î² â†’ set Î±} (hs : s.finite) :
  (âˆ€iâˆˆs, is_open (f i)) â†’ is_open (â‹‚iâˆˆs, f i) :=
finite.induction_on hs
  (Î» _, by rw bInter_empty; exact is_open_univ)
  (Î» a s has hs ih h, by rw bInter_insert; exact
    is_open.inter (h a (mem_insert _ _)) (ih (Î» i hi, h i (mem_insert_of_mem _ hi))))

lemma is_open_Inter [finite Î²] {s : Î² â†’ set Î±} (h : âˆ€ i, is_open (s i)) : is_open (â‹‚ i, s i) :=
suffices is_open (â‹‚ (i : Î²) (hi : i âˆˆ @univ Î²), s i), by simpa,
is_open_bInter finite_univ (Î» i _, h i)

lemma is_open_Inter_prop {p : Prop} {s : p â†’ set Î±}
  (h : âˆ€ h : p, is_open (s h)) : is_open (Inter s) :=
by by_cases p; simp *

lemma is_open_bInter_finset {s : finset Î²} {f : Î² â†’ set Î±} (h : âˆ€ i âˆˆ s, is_open (f i)) :
  is_open (â‹‚ i âˆˆ s, f i) :=
is_open_bInter (to_finite _) h

lemma is_open_const {p : Prop} : is_open {a : Î± | p} :=
by_cases
  (assume : p, begin simp only [this]; exact is_open_univ end)
  (assume : Â¬ p, begin simp only [this]; exact is_open_empty end)

lemma is_open.and : is_open {a | pâ‚ a} â†’ is_open {a | pâ‚‚ a} â†’ is_open {a | pâ‚ a âˆ§ pâ‚‚ a} :=
is_open.inter

/-- A set is closed if its complement is open -/
class is_closed (s : set Î±) : Prop :=
(is_open_compl : is_open sá¶œ)

localized "notation (name := is_closed_of) `is_closed[` t `]` := @is_closed hole! t" in topology

@[simp] lemma is_open_compl_iff {s : set Î±} : is_open sá¶œ â†” is_closed s :=
âŸ¨Î» h, âŸ¨hâŸ©, Î» h, h.is_open_complâŸ©

@[simp] lemma is_closed_empty : is_closed (âˆ… : set Î±) :=
by { rw [â† is_open_compl_iff, compl_empty], exact is_open_univ }

@[simp] lemma is_closed_univ : is_closed (univ : set Î±) :=
by { rw [â† is_open_compl_iff, compl_univ], exact is_open_empty }

lemma is_closed.union : is_closed sâ‚ â†’ is_closed sâ‚‚ â†’ is_closed (sâ‚ âˆª sâ‚‚) :=
Î» hâ‚ hâ‚‚, by { rw [â† is_open_compl_iff] at *, rw compl_union, exact is_open.inter hâ‚ hâ‚‚ }

lemma is_closed_sInter {s : set (set Î±)} : (âˆ€t âˆˆ s, is_closed t) â†’ is_closed (â‹‚â‚€ s) :=
by simpa only [â† is_open_compl_iff, compl_sInter, sUnion_image] using is_open_bUnion

lemma is_closed_Inter {f : Î¹ â†’ set Î±} (h : âˆ€i, is_closed (f i)) : is_closed (â‹‚i, f i ) :=
is_closed_sInter $ assume t âŸ¨i, (heq : f i = t)âŸ©, heq â–¸ h i

lemma is_closed_bInter {s : set Î²} {f : Î² â†’ set Î±} (h : âˆ€ i âˆˆ s, is_closed (f i)) :
  is_closed (â‹‚ i âˆˆ s, f i) :=
is_closed_Inter $ Î» i, is_closed_Inter $ h i

@[simp] lemma is_closed_compl_iff {s : set Î±} : is_closed sá¶œ â†” is_open s :=
by rw [â†is_open_compl_iff, compl_compl]

lemma is_open.is_closed_compl {s : set Î±} (hs : is_open s) : is_closed sá¶œ :=
is_closed_compl_iff.2 hs

lemma is_open.sdiff {s t : set Î±} (hâ‚ : is_open s) (hâ‚‚ : is_closed t) : is_open (s \ t) :=
is_open.inter hâ‚ $ is_open_compl_iff.mpr hâ‚‚

lemma is_closed.inter (hâ‚ : is_closed sâ‚) (hâ‚‚ : is_closed sâ‚‚) : is_closed (sâ‚ âˆ© sâ‚‚) :=
by { rw [â† is_open_compl_iff] at *, rw compl_inter, exact is_open.union hâ‚ hâ‚‚ }

lemma is_closed.sdiff {s t : set Î±} (hâ‚ : is_closed s) (hâ‚‚ : is_open t) : is_closed (s \ t) :=
is_closed.inter hâ‚ (is_closed_compl_iff.mpr hâ‚‚)

lemma is_closed_bUnion {s : set Î²} {f : Î² â†’ set Î±} (hs : s.finite) :
  (âˆ€iâˆˆs, is_closed (f i)) â†’ is_closed (â‹ƒiâˆˆs, f i) :=
finite.induction_on hs
  (Î» _, by rw bUnion_empty; exact is_closed_empty)
  (Î» a s has hs ih h, by rw bUnion_insert; exact
    is_closed.union (h a (mem_insert _ _)) (ih (Î» i hi, h i (mem_insert_of_mem _ hi))))

lemma is_closed_Union [finite Î²] {s : Î² â†’ set Î±} (h : âˆ€ i, is_closed (s i)) :
  is_closed (â‹ƒ i, s i) :=
suffices is_closed (â‹ƒ (i : Î²) (hi : i âˆˆ @univ Î²), s i),
  by convert this; simp [set.ext_iff],
is_closed_bUnion finite_univ (Î» i _, h i)

lemma is_closed_Union_prop {p : Prop} {s : p â†’ set Î±}
  (h : âˆ€ h : p, is_closed (s h)) : is_closed (Union s) :=
by by_cases p; simp *

lemma is_closed_imp {p q : Î± â†’ Prop} (hp : is_open {x | p x})
  (hq : is_closed {x | q x}) : is_closed {x | p x â†’ q x} :=
have {x | p x â†’ q x} = {x | p x}á¶œ âˆª {x | q x}, from set.ext $ Î» x, imp_iff_not_or,
by rw [this]; exact is_closed.union (is_closed_compl_iff.mpr hp) hq

lemma is_closed.not : is_closed {a | p a} â†’ is_open {a | Â¬ p a} :=
is_open_compl_iff.mpr

/-!
### Interior of a set
-/

/-- The interior of a set `s` is the largest open subset of `s`. -/
def interior (s : set Î±) : set Î± := â‹ƒâ‚€ {t | is_open t âˆ§ t âŠ† s}

lemma mem_interior {s : set Î±} {x : Î±} :
  x âˆˆ interior s â†” âˆƒ t âŠ† s, is_open t âˆ§ x âˆˆ t :=
by simp only [interior, mem_sUnion, mem_set_of_eq, exists_prop, and_assoc, and.left_comm]

@[simp] lemma is_open_interior {s : set Î±} : is_open (interior s) :=
is_open_sUnion $ assume t âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚

lemma interior_subset {s : set Î±} : interior s âŠ† s :=
sUnion_subset $ assume t âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚‚

lemma interior_maximal {s t : set Î±} (hâ‚ : t âŠ† s) (hâ‚‚ : is_open t) : t âŠ† interior s :=
subset_sUnion_of_mem âŸ¨hâ‚‚, hâ‚âŸ©

lemma is_open.interior_eq {s : set Î±} (h : is_open s) : interior s = s :=
subset.antisymm interior_subset (interior_maximal (subset.refl s) h)

lemma interior_eq_iff_is_open {s : set Î±} : interior s = s â†” is_open s :=
âŸ¨assume h, h â–¸ is_open_interior, is_open.interior_eqâŸ©

lemma subset_interior_iff_is_open {s : set Î±} : s âŠ† interior s â†” is_open s :=
by simp only [interior_eq_iff_is_open.symm, subset.antisymm_iff, interior_subset, true_and]

lemma is_open.subset_interior_iff {s t : set Î±} (hâ‚ : is_open s) :
  s âŠ† interior t â†” s âŠ† t :=
âŸ¨assume h, subset.trans h interior_subset, assume hâ‚‚, interior_maximal hâ‚‚ hâ‚âŸ©

lemma subset_interior_iff {s t : set Î±} : t âŠ† interior s â†” âˆƒ U, is_open U âˆ§ t âŠ† U âˆ§ U âŠ† s :=
âŸ¨Î» h, âŸ¨interior s, is_open_interior, h, interior_subsetâŸ©,
  Î» âŸ¨U, hU, htU, hUsâŸ©, htU.trans (interior_maximal hUs hU)âŸ©

@[mono] lemma interior_mono {s t : set Î±} (h : s âŠ† t) : interior s âŠ† interior t :=
interior_maximal (subset.trans interior_subset h) is_open_interior

@[simp] lemma interior_empty : interior (âˆ… : set Î±) = âˆ… :=
is_open_empty.interior_eq

@[simp] lemma interior_univ : interior (univ : set Î±) = univ :=
is_open_univ.interior_eq

@[simp] lemma interior_eq_univ {s : set Î±} : interior s = univ â†” s = univ :=
âŸ¨Î» h, univ_subset_iff.mp $ h.symm.trans_le interior_subset, Î» h, h.symm â–¸ interior_univâŸ©

@[simp] lemma interior_interior {s : set Î±} : interior (interior s) = interior s :=
is_open_interior.interior_eq

@[simp] lemma interior_inter {s t : set Î±} : interior (s âˆ© t) = interior s âˆ© interior t :=
subset.antisymm
  (subset_inter (interior_mono $ inter_subset_left s t) (interior_mono $ inter_subset_right s t))
  (interior_maximal (inter_subset_inter interior_subset interior_subset) $
    is_open.inter is_open_interior is_open_interior)

@[simp] lemma finset.interior_Inter {Î¹ : Type*} (s : finset Î¹) (f : Î¹ â†’ set Î±) :
  interior (â‹‚ i âˆˆ s, f i) = â‹‚ i âˆˆ s, interior (f i) :=
begin
  classical,
  refine s.induction_on (by simp) _,
  intros i s hâ‚ hâ‚‚,
  simp [hâ‚‚],
end

@[simp] lemma interior_Inter {Î¹ : Type*} [finite Î¹] (f : Î¹ â†’ set Î±) :
  interior (â‹‚ i, f i) = â‹‚ i, interior (f i) :=
by { casesI nonempty_fintype Î¹, convert finset.univ.interior_Inter f; simp }

lemma interior_union_is_closed_of_interior_empty {s t : set Î±} (hâ‚ : is_closed s)
  (hâ‚‚ : interior t = âˆ…) :
  interior (s âˆª t) = interior s :=
have interior (s âˆª t) âŠ† s, from
  assume x âŸ¨u, âŸ¨(huâ‚ : is_open u), (huâ‚‚ : u âŠ† s âˆª t)âŸ©, (hxâ‚ : x âˆˆ u)âŸ©,
  classical.by_contradiction $ assume hxâ‚‚ : x âˆ‰ s,
    have u \ s âŠ† t,
      from assume x âŸ¨hâ‚, hâ‚‚âŸ©, or.resolve_left (huâ‚‚ hâ‚) hâ‚‚,
    have u \ s âŠ† interior t,
      by rwa (is_open.sdiff huâ‚ hâ‚).subset_interior_iff,
    have u \ s âŠ† âˆ…,
      by rwa hâ‚‚ at this,
    this âŸ¨hxâ‚, hxâ‚‚âŸ©,
subset.antisymm
  (interior_maximal this is_open_interior)
  (interior_mono $ subset_union_left _ _)

lemma is_open_iff_forall_mem_open : is_open s â†” âˆ€ x âˆˆ s, âˆƒ t âŠ† s, is_open t âˆ§ x âˆˆ t :=
by rw â† subset_interior_iff_is_open; simp only [subset_def, mem_interior]

lemma interior_Inter_subset (s : Î¹ â†’ set Î±) : interior (â‹‚ i, s i) âŠ† â‹‚ i, interior (s i) :=
subset_Inter $ Î» i, interior_mono $ Inter_subset _ _

lemma interior_Interâ‚‚_subset (p : Î¹ â†’ Sort*) (s : Î  i, p i â†’ set Î±) :
  interior (â‹‚ i j, s i j) âŠ† â‹‚ i j, interior (s i j) :=
(interior_Inter_subset _).trans $ Inter_mono $ Î» i, interior_Inter_subset _

lemma interior_sInter_subset (S : set (set Î±)) : interior (â‹‚â‚€ S) âŠ† â‹‚ s âˆˆ S, interior s :=
calc interior (â‹‚â‚€ S) = interior (â‹‚ s âˆˆ S, s) : by rw sInter_eq_bInter
                 ... âŠ† â‹‚ s âˆˆ S, interior s  : interior_Interâ‚‚_subset _ _

/-!
### Closure of a set
-/

/-- The closure of `s` is the smallest closed set containing `s`. -/
def closure (s : set Î±) : set Î± := â‹‚â‚€ {t | is_closed t âˆ§ s âŠ† t}

@[simp] lemma is_closed_closure {s : set Î±} : is_closed (closure s) :=
is_closed_sInter $ assume t âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚

lemma subset_closure {s : set Î±} : s âŠ† closure s :=
subset_sInter $ assume t âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚‚

lemma not_mem_of_not_mem_closure {s : set Î±} {P : Î±} (hP : P âˆ‰ closure s) : P âˆ‰ s :=
Î» h, hP (subset_closure h)

lemma closure_minimal {s t : set Î±} (hâ‚ : s âŠ† t) (hâ‚‚ : is_closed t) : closure s âŠ† t :=
sInter_subset_of_mem âŸ¨hâ‚‚, hâ‚âŸ©

lemma disjoint.closure_left {s t : set Î±} (hd : disjoint s t) (ht : is_open t) :
  disjoint (closure s) t :=
disjoint_compl_left.mono_left $ closure_minimal hd.subset_compl_right ht.is_closed_compl

lemma disjoint.closure_right {s t : set Î±} (hd : disjoint s t) (hs : is_open s) :
  disjoint s (closure t) :=
(hd.symm.closure_left hs).symm

lemma is_closed.closure_eq {s : set Î±} (h : is_closed s) : closure s = s :=
subset.antisymm (closure_minimal (subset.refl s) h) subset_closure

lemma is_closed.closure_subset {s : set Î±} (hs : is_closed s) : closure s âŠ† s :=
closure_minimal (subset.refl _) hs

lemma is_closed.closure_subset_iff {s t : set Î±} (hâ‚ : is_closed t) :
  closure s âŠ† t â†” s âŠ† t :=
âŸ¨subset.trans subset_closure, assume h, closure_minimal h hâ‚âŸ©

lemma is_closed.mem_iff_closure_subset {s : set Î±} (hs : is_closed s) {x : Î±} :
  x âˆˆ s â†” closure ({x} : set Î±) âŠ† s :=
(hs.closure_subset_iff.trans set.singleton_subset_iff).symm

@[mono] lemma closure_mono {s t : set Î±} (h : s âŠ† t) : closure s âŠ† closure t :=
closure_minimal (subset.trans h subset_closure) is_closed_closure

lemma monotone_closure (Î± : Type*) [topological_space Î±] : monotone (@closure Î± _) :=
Î» _ _, closure_mono

lemma diff_subset_closure_iff {s t : set Î±} :
  s \ t âŠ† closure t â†” s âŠ† closure t :=
by rw [diff_subset_iff, union_eq_self_of_subset_left subset_closure]

lemma closure_inter_subset_inter_closure (s t : set Î±) :
  closure (s âˆ© t) âŠ† closure s âˆ© closure t :=
(monotone_closure Î±).map_inf_le s t

lemma is_closed_of_closure_subset {s : set Î±} (h : closure s âŠ† s) : is_closed s :=
by rw subset.antisymm subset_closure h; exact is_closed_closure

lemma closure_eq_iff_is_closed {s : set Î±} : closure s = s â†” is_closed s :=
âŸ¨assume h, h â–¸ is_closed_closure, is_closed.closure_eqâŸ©

lemma closure_subset_iff_is_closed {s : set Î±} : closure s âŠ† s â†” is_closed s :=
âŸ¨is_closed_of_closure_subset, is_closed.closure_subsetâŸ©

@[simp] lemma closure_empty : closure (âˆ… : set Î±) = âˆ… :=
is_closed_empty.closure_eq

@[simp] lemma closure_empty_iff (s : set Î±) : closure s = âˆ… â†” s = âˆ… :=
âŸ¨subset_eq_empty subset_closure, Î» h, h.symm â–¸ closure_emptyâŸ©

@[simp] lemma closure_nonempty_iff {s : set Î±} : (closure s).nonempty â†” s.nonempty :=
by simp only [nonempty_iff_ne_empty, ne.def, closure_empty_iff]

alias closure_nonempty_iff â†” set.nonempty.of_closure set.nonempty.closure

@[simp] lemma closure_univ : closure (univ : set Î±) = univ :=
is_closed_univ.closure_eq

@[simp] lemma closure_closure {s : set Î±} : closure (closure s) = closure s :=
is_closed_closure.closure_eq

@[simp] lemma closure_union {s t : set Î±} : closure (s âˆª t) = closure s âˆª closure t :=
subset.antisymm
  (closure_minimal (union_subset_union subset_closure subset_closure) $
    is_closed.union is_closed_closure is_closed_closure)
  ((monotone_closure Î±).le_map_sup s t)

@[simp] lemma finset.closure_bUnion {Î¹ : Type*} (s : finset Î¹) (f : Î¹ â†’ set Î±) :
  closure (â‹ƒ i âˆˆ s, f i) = â‹ƒ i âˆˆ s, closure (f i) :=
begin
  classical,
  refine s.induction_on (by simp) _,
  intros i s hâ‚ hâ‚‚,
  simp [hâ‚‚],
end

@[simp] lemma closure_Union {Î¹ : Type*} [finite Î¹] (f : Î¹ â†’ set Î±) :
  closure (â‹ƒ i, f i) = â‹ƒ i, closure (f i) :=
by { casesI nonempty_fintype Î¹, convert finset.univ.closure_bUnion f; simp }

lemma interior_subset_closure {s : set Î±} : interior s âŠ† closure s :=
subset.trans interior_subset subset_closure

lemma closure_eq_compl_interior_compl {s : set Î±} : closure s = (interior sá¶œ)á¶œ :=
begin
  rw [interior, closure, compl_sUnion, compl_image_set_of],
  simp only [compl_subset_compl, is_open_compl_iff],
end

@[simp] lemma interior_compl {s : set Î±} : interior sá¶œ = (closure s)á¶œ :=
by simp [closure_eq_compl_interior_compl]

@[simp] lemma closure_compl {s : set Î±} : closure sá¶œ = (interior s)á¶œ :=
by simp [closure_eq_compl_interior_compl]

theorem mem_closure_iff {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆ€ o, is_open o â†’ a âˆˆ o â†’ (o âˆ© s).nonempty :=
âŸ¨Î» h o oo ao, classical.by_contradiction $ Î» os,
  have s âŠ† oá¶œ, from Î» x xs xo, os âŸ¨x, xo, xsâŸ©,
  closure_minimal this (is_closed_compl_iff.2 oo) h ao,
Î» H c âŸ¨hâ‚, hâ‚‚âŸ©, classical.by_contradiction $ Î» nc,
  let âŸ¨x, hc, hsâŸ© := (H _ hâ‚.is_open_compl nc) in hc (hâ‚‚ hs)âŸ©

lemma closure_inter_open_nonempty_iff {s t : set Î±} (h : is_open t) :
  (closure s âˆ© t).nonempty â†” (s âˆ© t).nonempty :=
âŸ¨Î» âŸ¨x, hxcs, hxtâŸ©, inter_comm t s â–¸ mem_closure_iff.1 hxcs t h hxt,
  Î» h, h.mono $ inf_le_inf_right t subset_closureâŸ©

lemma filter.le_lift'_closure (l : filter Î±) : l â‰¤ l.lift' closure :=
le_lift'.2 $ Î» s hs, mem_of_superset hs subset_closure

lemma filter.has_basis.lift'_closure {l : filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±}
  (h : l.has_basis p s) :
  (l.lift' closure).has_basis p (Î» i, closure (s i)) :=
h.lift' (monotone_closure Î±)

lemma filter.has_basis.lift'_closure_eq_self {l : filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±}
  (h : l.has_basis p s) (hc : âˆ€ i, p i â†’ is_closed (s i)) :
  l.lift' closure = l :=
le_antisymm (h.ge_iff.2 $ Î» i hi, (hc i hi).closure_eq â–¸ mem_lift' (h.mem_of_mem hi))
  l.le_lift'_closure

@[simp] lemma filter.lift'_closure_eq_bot {l : filter Î±} : l.lift' closure = âŠ¥ â†” l = âŠ¥ :=
âŸ¨Î» h, bot_unique $ h â–¸ l.le_lift'_closure,
  Î» h, h.symm â–¸ by rw [lift'_bot (monotone_closure _), closure_empty, principal_empty]âŸ©

/-- A set is dense in a topological space if every point belongs to its closure. -/
def dense (s : set Î±) : Prop := âˆ€ x, x âˆˆ closure s

lemma dense_iff_closure_eq {s : set Î±} : dense s â†” closure s = univ :=
eq_univ_iff_forall.symm

lemma dense.closure_eq {s : set Î±} (h : dense s) : closure s = univ :=
dense_iff_closure_eq.mp h

lemma interior_eq_empty_iff_dense_compl {s : set Î±} : interior s = âˆ… â†” dense sá¶œ :=
by rw [dense_iff_closure_eq, closure_compl, compl_univ_iff]

lemma dense.interior_compl {s : set Î±} (h : dense s) : interior sá¶œ = âˆ… :=
interior_eq_empty_iff_dense_compl.2 $ by rwa compl_compl

/-- The closure of a set `s` is dense if and only if `s` is dense. -/
@[simp] lemma dense_closure {s : set Î±} : dense (closure s) â†” dense s :=
by rw [dense, dense, closure_closure]

alias dense_closure â†” dense.of_closure dense.closure

@[simp] lemma dense_univ : dense (univ : set Î±) := Î» x, subset_closure trivial

/-- A set is dense if and only if it has a nonempty intersection with each nonempty open set. -/
lemma dense_iff_inter_open {s : set Î±} :
  dense s â†” âˆ€ U, is_open U â†’ U.nonempty â†’ (U âˆ© s).nonempty :=
begin
  split ; intro h,
  { rintros U U_op âŸ¨x, x_inâŸ©,
    exact mem_closure_iff.1 (by simp only [h.closure_eq]) U U_op x_in },
  { intro x,
    rw mem_closure_iff,
    intros U U_op x_in,
    exact h U U_op âŸ¨_, x_inâŸ© },
end

alias dense_iff_inter_open â†” dense.inter_open_nonempty _

lemma dense.exists_mem_open {s : set Î±} (hs : dense s) {U : set Î±} (ho : is_open U)
  (hne : U.nonempty) :
  âˆƒ x âˆˆ s, x âˆˆ U :=
let âŸ¨x, hxâŸ© := hs.inter_open_nonempty U ho hne in âŸ¨x, hx.2, hx.1âŸ©

lemma dense.nonempty_iff {s : set Î±} (hs : dense s) :
  s.nonempty â†” nonempty Î± :=
âŸ¨Î» âŸ¨x, hxâŸ©, âŸ¨xâŸ©, Î» âŸ¨xâŸ©,
  let âŸ¨y, hyâŸ© := hs.inter_open_nonempty _ is_open_univ âŸ¨x, trivialâŸ© in âŸ¨y, hy.2âŸ©âŸ©

lemma dense.nonempty [h : nonempty Î±] {s : set Î±} (hs : dense s) : s.nonempty :=
hs.nonempty_iff.2 h

@[mono]
lemma dense.mono {sâ‚ sâ‚‚ : set Î±} (h : sâ‚ âŠ† sâ‚‚) (hd : dense sâ‚) : dense sâ‚‚ :=
Î» x, closure_mono h (hd x)

/-- Complement to a singleton is dense if and only if the singleton is not an open set. -/
lemma dense_compl_singleton_iff_not_open {x : Î±} : dense ({x}á¶œ : set Î±) â†” Â¬is_open ({x} : set Î±) :=
begin
  fsplit,
  { intros hd ho,
    exact (hd.inter_open_nonempty _ ho (singleton_nonempty _)).ne_empty (inter_compl_self _) },
  { refine Î» ho, dense_iff_inter_open.2 (Î» U hU hne, inter_compl_nonempty_iff.2 $ Î» hUx, _),
    obtain rfl : U = {x}, from eq_singleton_iff_nonempty_unique_mem.2 âŸ¨hne, hUxâŸ©,
    exact ho hU }
end

/-!
### Frontier of a set
-/

/-- The frontier of a set is the set of points between the closure and interior. -/
def frontier (s : set Î±) : set Î± := closure s \ interior s

@[simp] lemma closure_diff_interior (s : set Î±) : closure s \ interior s = frontier s := rfl

@[simp] lemma closure_diff_frontier (s : set Î±) : closure s \ frontier s = interior s :=
by rw [frontier, diff_diff_right_self, inter_eq_self_of_subset_right interior_subset_closure]

@[simp] lemma self_diff_frontier (s : set Î±) : s \ frontier s = interior s :=
by rw [frontier, diff_diff_right, diff_eq_empty.2 subset_closure,
  inter_eq_self_of_subset_right interior_subset, empty_union]

lemma frontier_eq_closure_inter_closure {s : set Î±} :
  frontier s = closure s âˆ© closure sá¶œ :=
by rw [closure_compl, frontier, diff_eq]

lemma frontier_subset_closure {s : set Î±} : frontier s âŠ† closure s := diff_subset _ _

lemma is_closed.frontier_subset (hs : is_closed s) : frontier s âŠ† s :=
frontier_subset_closure.trans hs.closure_eq.subset

lemma frontier_closure_subset {s : set Î±} : frontier (closure s) âŠ† frontier s :=
diff_subset_diff closure_closure.subset $ interior_mono subset_closure

lemma frontier_interior_subset {s : set Î±} : frontier (interior s) âŠ† frontier s :=
diff_subset_diff (closure_mono interior_subset) interior_interior.symm.subset

/-- The complement of a set has the same frontier as the original set. -/
@[simp] lemma frontier_compl (s : set Î±) : frontier sá¶œ = frontier s :=
by simp only [frontier_eq_closure_inter_closure, compl_compl, inter_comm]

@[simp] lemma frontier_univ : frontier (univ : set Î±) = âˆ… := by simp [frontier]

@[simp] lemma frontier_empty : frontier (âˆ… : set Î±) = âˆ… := by simp [frontier]

lemma frontier_inter_subset (s t : set Î±) :
  frontier (s âˆ© t) âŠ† (frontier s âˆ© closure t) âˆª (closure s âˆ© frontier t) :=
begin
  simp only [frontier_eq_closure_inter_closure, compl_inter, closure_union],
  convert inter_subset_inter_left _ (closure_inter_subset_inter_closure s t),
  simp only [inter_distrib_left, inter_distrib_right, inter_assoc],
  congr' 2,
  apply inter_comm
end

lemma frontier_union_subset (s t : set Î±) :
  frontier (s âˆª t) âŠ† (frontier s âˆ© closure tá¶œ) âˆª (closure sá¶œ âˆ© frontier t) :=
by simpa only [frontier_compl, â† compl_union]
  using frontier_inter_subset sá¶œ tá¶œ

lemma is_closed.frontier_eq {s : set Î±} (hs : is_closed s) : frontier s = s \ interior s :=
by rw [frontier, hs.closure_eq]

lemma is_open.frontier_eq {s : set Î±} (hs : is_open s) : frontier s = closure s \ s :=
by rw [frontier, hs.interior_eq]

lemma is_open.inter_frontier_eq {s : set Î±} (hs : is_open s) : s âˆ© frontier s = âˆ… :=
by rw [hs.frontier_eq, inter_diff_self]

/-- The frontier of a set is closed. -/
lemma is_closed_frontier {s : set Î±} : is_closed (frontier s) :=
by rw frontier_eq_closure_inter_closure; exact is_closed.inter is_closed_closure is_closed_closure

/-- The frontier of a closed set has no interior point. -/
lemma interior_frontier {s : set Î±} (h : is_closed s) : interior (frontier s) = âˆ… :=
begin
  have A : frontier s = s \ interior s, from h.frontier_eq,
  have B : interior (frontier s) âŠ† interior s, by rw A; exact interior_mono (diff_subset _ _),
  have C : interior (frontier s) âŠ† frontier s := interior_subset,
  have : interior (frontier s) âŠ† (interior s) âˆ© (s \ interior s) :=
    subset_inter B (by simpa [A] using C),
  rwa [inter_diff_self, subset_empty_iff] at this,
end

lemma closure_eq_interior_union_frontier (s : set Î±) : closure s = interior s âˆª frontier s :=
(union_diff_cancel interior_subset_closure).symm

lemma closure_eq_self_union_frontier (s : set Î±) : closure s = s âˆª frontier s :=
(union_diff_cancel' interior_subset subset_closure).symm

lemma disjoint.frontier_left (ht : is_open t) (hd : disjoint s t) : disjoint (frontier s) t :=
subset_compl_iff_disjoint_right.1 $ frontier_subset_closure.trans $ closure_minimal
  (disjoint_left.1 hd) $ is_closed_compl_iff.2 ht

lemma disjoint.frontier_right (hs : is_open s) (hd : disjoint s t) : disjoint s (frontier t) :=
(hd.symm.frontier_left hs).symm

lemma frontier_eq_inter_compl_interior {s : set Î±} :
  frontier s = (interior s)á¶œ âˆ© (interior (sá¶œ))á¶œ :=
by { rw [â†frontier_compl, â†closure_compl], refl }

lemma compl_frontier_eq_union_interior {s : set Î±} :
  (frontier s)á¶œ = interior s âˆª interior sá¶œ :=
begin
  rw frontier_eq_inter_compl_interior,
  simp only [compl_inter, compl_compl],
end

/-!
### Neighborhoods
-/

/-- A set is called a neighborhood of `a` if it contains an open set around `a`. The set of all
neighborhoods of `a` forms a filter, the neighborhood filter at `a`, is here defined as the
infimum over the principal filters of all open sets containing `a`. -/
@[irreducible] def nhds (a : Î±) : filter Î± := (â¨… s âˆˆ {s : set Î± | a âˆˆ s âˆ§ is_open s}, ğ“Ÿ s)

localized "notation (name := nhds) `ğ“` := nhds" in topology

/-- The "neighborhood within" filter. Elements of `ğ“[s] a` are sets containing the
intersection of `s` and a neighborhood of `a`. -/
def nhds_within (a : Î±) (s : set Î±) : filter Î± := ğ“ a âŠ“ ğ“Ÿ s

localized "notation (name := nhds_within) `ğ“[` s `] ` x:100 := nhds_within x s" in topology
localized "notation (name := nhds_within.ne) `ğ“[â‰ ] ` x:100 := nhds_within x {x}á¶œ" in topology
localized "notation (name := nhds_within.ge) `ğ“[â‰¥] ` x:100 := nhds_within x (set.Ici x)" in topology
localized "notation (name := nhds_within.le) `ğ“[â‰¤] ` x:100 := nhds_within x (set.Iic x)" in topology
localized "notation (name := nhds_within.gt) `ğ“[>] ` x:100 := nhds_within x (set.Ioi x)" in topology
localized "notation (name := nhds_within.lt) `ğ“[<] ` x:100 := nhds_within x (set.Iio x)" in topology

lemma nhds_def (a : Î±) : ğ“ a = (â¨… s âˆˆ {s : set Î± | a âˆˆ s âˆ§ is_open s}, ğ“Ÿ s) := by rw nhds

lemma nhds_def' (a : Î±) : ğ“ a = â¨… (s : set Î±) (hs : is_open s) (ha : a âˆˆ s), ğ“Ÿ s :=
by simp only [nhds_def, mem_set_of_eq, and_comm (a âˆˆ _), infi_and]

/-- The open sets containing `a` are a basis for the neighborhood filter. See `nhds_basis_opens'`
for a variant using open neighborhoods instead. -/
lemma nhds_basis_opens (a : Î±) : (ğ“ a).has_basis (Î» s : set Î±, a âˆˆ s âˆ§ is_open s) (Î» s, s) :=
begin
  rw nhds_def,
  exact has_basis_binfi_principal
    (Î» s âŸ¨has, hsâŸ© t âŸ¨hat, htâŸ©, âŸ¨s âˆ© t, âŸ¨âŸ¨has, hatâŸ©, is_open.inter hs htâŸ©,
      âŸ¨inter_subset_left _ _, inter_subset_right _ _âŸ©âŸ©)
    âŸ¨univ, âŸ¨mem_univ a, is_open_univâŸ©âŸ©
end

lemma nhds_basis_closeds (a : Î±) : (ğ“ a).has_basis (Î» s : set Î±, a âˆ‰ s âˆ§ is_closed s) compl :=
âŸ¨Î» t, (nhds_basis_opens a).mem_iff.trans $ compl_surjective.exists.trans $
  by simp only [is_open_compl_iff, mem_compl_iff]âŸ©

/-- A filter lies below the neighborhood filter at `a` iff it contains every open set around `a`. -/
lemma le_nhds_iff {f a} : f â‰¤ ğ“ a â†” âˆ€ s : set Î±, a âˆˆ s â†’ is_open s â†’ s âˆˆ f :=
by simp [nhds_def]

/-- To show a filter is above the neighborhood filter at `a`, it suffices to show that it is above
the principal filter of some open set `s` containing `a`. -/
lemma nhds_le_of_le {f a} {s : set Î±} (h : a âˆˆ s) (o : is_open s) (sf : ğ“Ÿ s â‰¤ f) : ğ“ a â‰¤ f :=
by rw nhds_def; exact infi_le_of_le s (infi_le_of_le âŸ¨h, oâŸ© sf)

lemma mem_nhds_iff {a : Î±} {s : set Î±} :
  s âˆˆ ğ“ a â†” âˆƒ t âŠ† s, is_open t âˆ§ a âˆˆ t :=
(nhds_basis_opens a).mem_iff.trans
  âŸ¨Î» âŸ¨t, âŸ¨hat, htâŸ©, htsâŸ©, âŸ¨t, hts, ht, hatâŸ©, Î» âŸ¨t, hts, ht, hatâŸ©, âŸ¨t, âŸ¨hat, htâŸ©, htsâŸ©âŸ©

/-- A predicate is true in a neighborhood of `a` iff it is true for all the points in an open set
containing `a`. -/
lemma eventually_nhds_iff {a : Î±} {p : Î± â†’ Prop} :
  (âˆ€á¶  x in ğ“ a, p x) â†” âˆƒ (t : set Î±), (âˆ€ x âˆˆ t, p x) âˆ§ is_open t âˆ§ a âˆˆ t :=
mem_nhds_iff.trans $ by simp only [subset_def, exists_prop, mem_set_of_eq]

lemma map_nhds {a : Î±} {f : Î± â†’ Î²} :
  map f (ğ“ a) = (â¨… s âˆˆ {s : set Î± | a âˆˆ s âˆ§ is_open s}, ğ“Ÿ (image f s)) :=
((nhds_basis_opens a).map f).eq_binfi

lemma mem_of_mem_nhds {a : Î±} {s : set Î±} : s âˆˆ ğ“ a â†’ a âˆˆ s :=
Î» H, let âŸ¨t, ht, _, hsâŸ© := mem_nhds_iff.1 H in ht hs

/-- If a predicate is true in a neighborhood of `a`, then it is true for `a`. -/
lemma filter.eventually.self_of_nhds {p : Î± â†’ Prop} {a : Î±}
  (h : âˆ€á¶  y in ğ“ a, p y) : p a :=
mem_of_mem_nhds h

lemma is_open.mem_nhds {a : Î±} {s : set Î±} (hs : is_open s) (ha : a âˆˆ s) :
  s âˆˆ ğ“ a :=
mem_nhds_iff.2 âŸ¨s, subset.refl _, hs, haâŸ©

lemma is_open.mem_nhds_iff {a : Î±} {s : set Î±} (hs : is_open s) : s âˆˆ ğ“ a â†” a âˆˆ s :=
âŸ¨mem_of_mem_nhds, Î» ha, mem_nhds_iff.2 âŸ¨s, subset.refl _, hs, haâŸ©âŸ©

lemma is_closed.compl_mem_nhds {a : Î±} {s : set Î±} (hs : is_closed s) (ha : a âˆ‰ s) : sá¶œ âˆˆ ğ“ a :=
hs.is_open_compl.mem_nhds (mem_compl ha)

lemma is_open.eventually_mem {a : Î±} {s : set Î±} (hs : is_open s) (ha : a âˆˆ s) :
  âˆ€á¶  x in ğ“ a, x âˆˆ s :=
is_open.mem_nhds hs ha

/-- The open neighborhoods of `a` are a basis for the neighborhood filter. See `nhds_basis_opens`
for a variant using open sets around `a` instead. -/
lemma nhds_basis_opens' (a : Î±) : (ğ“ a).has_basis (Î» s : set Î±, s âˆˆ ğ“ a âˆ§ is_open s) (Î» x, x) :=
begin
  convert nhds_basis_opens a,
  ext s,
  exact and.congr_left_iff.2 is_open.mem_nhds_iff
end

/-- If `U` is a neighborhood of each point of a set `s` then it is a neighborhood of `s`:
it contains an open set containing `s`. -/
lemma exists_open_set_nhds {s U : set Î±} (h : âˆ€ x âˆˆ s, U âˆˆ ğ“ x) :
  âˆƒ V : set Î±, s âŠ† V âˆ§ is_open V âˆ§ V âŠ† U :=
begin
  have := Î» x hx, (nhds_basis_opens x).mem_iff.1 (h x hx),
  choose! Z hZ hZU using this, choose hZmem hZo using hZ,
  exact âŸ¨â‹ƒ x âˆˆ s, Z x, Î» x hx, mem_bUnion hx (hZmem x hx), is_open_bUnion hZo, Unionâ‚‚_subset hZUâŸ©
end

/-- If `U` is a neighborhood of each point of a set `s` then it is a neighborhood of s:
it contains an open set containing `s`. -/
lemma exists_open_set_nhds' {s U : set Î±} (h : U âˆˆ â¨† x âˆˆ s, ğ“ x) :
  âˆƒ V : set Î±, s âŠ† V âˆ§ is_open V âˆ§ V âŠ† U :=
exists_open_set_nhds (by simpa using h)

/-- If a predicate is true in a neighbourhood of `a`, then for `y` sufficiently close
to `a` this predicate is true in a neighbourhood of `y`. -/
lemma filter.eventually.eventually_nhds {p : Î± â†’ Prop} {a : Î±} (h : âˆ€á¶  y in ğ“ a, p y) :
  âˆ€á¶  y in ğ“ a, âˆ€á¶  x in ğ“ y, p x :=
let âŸ¨t, htp, hto, haâŸ© := eventually_nhds_iff.1 h in
eventually_nhds_iff.2 âŸ¨t, Î» x hx, eventually_nhds_iff.2 âŸ¨t, htp, hto, hxâŸ©, hto, haâŸ©

@[simp] lemma eventually_eventually_nhds {p : Î± â†’ Prop} {a : Î±} :
  (âˆ€á¶  y in ğ“ a, âˆ€á¶  x in ğ“ y, p x) â†” âˆ€á¶  x in ğ“ a, p x :=
âŸ¨Î» h, h.self_of_nhds, Î» h, h.eventually_nhdsâŸ©

@[simp] lemma frequently_frequently_nhds {p : Î± â†’ Prop} {a : Î±} :
  (âˆƒá¶  y in ğ“ a, âˆƒá¶  x in ğ“ y, p x) â†” (âˆƒá¶  x in ğ“ a, p x) :=
begin
  rw â† not_iff_not,
  simp_rw not_frequently,
  exact eventually_eventually_nhds,
end

@[simp] lemma eventually_mem_nhds {s : set Î±} {a : Î±} :
  (âˆ€á¶  x in ğ“ a, s âˆˆ ğ“ x) â†” s âˆˆ ğ“ a :=
eventually_eventually_nhds

@[simp] lemma nhds_bind_nhds : (ğ“ a).bind ğ“ = ğ“ a := filter.ext $ Î» s, eventually_eventually_nhds

@[simp] lemma eventually_eventually_eq_nhds {f g : Î± â†’ Î²} {a : Î±} :
  (âˆ€á¶  y in ğ“ a, f =á¶ [ğ“ y] g) â†” f =á¶ [ğ“ a] g :=
eventually_eventually_nhds

lemma filter.eventually_eq.eq_of_nhds {f g : Î± â†’ Î²} {a : Î±} (h : f =á¶ [ğ“ a] g) : f a = g a :=
h.self_of_nhds

@[simp] lemma eventually_eventually_le_nhds [has_le Î²] {f g : Î± â†’ Î²} {a : Î±} :
  (âˆ€á¶  y in ğ“ a, f â‰¤á¶ [ğ“ y] g) â†” f â‰¤á¶ [ğ“ a] g :=
eventually_eventually_nhds

/-- If two functions are equal in a neighbourhood of `a`, then for `y` sufficiently close
to `a` these functions are equal in a neighbourhood of `y`. -/
lemma filter.eventually_eq.eventually_eq_nhds {f g : Î± â†’ Î²} {a : Î±} (h : f =á¶ [ğ“ a] g) :
  âˆ€á¶  y in ğ“ a, f =á¶ [ğ“ y] g :=
h.eventually_nhds

/-- If `f x â‰¤ g x` in a neighbourhood of `a`, then for `y` sufficiently close to `a` we have
`f x â‰¤ g x` in a neighbourhood of `y`. -/
lemma filter.eventually_le.eventually_le_nhds [has_le Î²] {f g : Î± â†’ Î²} {a : Î±} (h : f â‰¤á¶ [ğ“ a] g) :
  âˆ€á¶  y in ğ“ a, f â‰¤á¶ [ğ“ y] g :=
h.eventually_nhds

theorem all_mem_nhds (x : Î±) (P : set Î± â†’ Prop) (hP : âˆ€ s t, s âŠ† t â†’ P s â†’ P t) :
  (âˆ€ s âˆˆ ğ“ x, P s) â†” (âˆ€ s, is_open s â†’ x âˆˆ s â†’ P s) :=
((nhds_basis_opens x).forall_iff hP).trans $ by simp only [and_comm (x âˆˆ _), and_imp]

theorem all_mem_nhds_filter (x : Î±) (f : set Î± â†’ set Î²) (hf : âˆ€ s t, s âŠ† t â†’ f s âŠ† f t)
    (l : filter Î²) :
  (âˆ€ s âˆˆ ğ“ x, f s âˆˆ l) â†” (âˆ€ s, is_open s â†’ x âˆˆ s â†’ f s âˆˆ l) :=
all_mem_nhds _ _ (Î» s t ssubt h, mem_of_superset h (hf s t ssubt))

theorem tendsto_nhds {f : Î² â†’ Î±} {l : filter Î²} {a : Î±} :
  tendsto f l (ğ“ a) â†” (âˆ€ s, is_open s â†’ a âˆˆ s â†’ f â»Â¹' s âˆˆ l) :=
all_mem_nhds_filter _ _ (Î» s t h, preimage_mono h) _

lemma tendsto_at_top_nhds [nonempty Î²] [semilattice_sup Î²] {f : Î² â†’ Î±} {a : Î±} :
  (tendsto f at_top (ğ“ a)) â†” âˆ€ U : set Î±, a âˆˆ U â†’ is_open U â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ f n âˆˆ U :=
(at_top_basis.tendsto_iff (nhds_basis_opens a)).trans $
  by simp only [and_imp, exists_prop, true_and, mem_Ici, ge_iff_le]

lemma tendsto_const_nhds {a : Î±} {f : filter Î²} : tendsto (Î»b:Î², a) f (ğ“ a) :=
tendsto_nhds.mpr $ assume s hs ha, univ_mem' $ assume _, ha

lemma tendsto_at_top_of_eventually_const {Î¹ : Type*} [semilattice_sup Î¹] [nonempty Î¹]
  {x : Î±} {u : Î¹ â†’ Î±} {iâ‚€ : Î¹} (h : âˆ€ i â‰¥ iâ‚€, u i = x) : tendsto u at_top (ğ“ x) :=
tendsto.congr' (eventually_eq.symm (eventually_at_top.mpr âŸ¨iâ‚€, hâŸ©)) tendsto_const_nhds

lemma tendsto_at_bot_of_eventually_const {Î¹ : Type*} [semilattice_inf Î¹] [nonempty Î¹]
  {x : Î±} {u : Î¹ â†’ Î±} {iâ‚€ : Î¹} (h : âˆ€ i â‰¤ iâ‚€, u i = x) : tendsto u at_bot (ğ“ x) :=
tendsto.congr' (eventually_eq.symm (eventually_at_bot.mpr âŸ¨iâ‚€, hâŸ©)) tendsto_const_nhds

lemma pure_le_nhds : pure â‰¤ (ğ“ : Î± â†’ filter Î±) :=
assume a s hs, mem_pure.2 $ mem_of_mem_nhds hs

lemma tendsto_pure_nhds {Î± : Type*} [topological_space Î²] (f : Î± â†’ Î²) (a : Î±) :
  tendsto f (pure a) (ğ“ (f a)) :=
(tendsto_pure_pure f a).mono_right (pure_le_nhds _)

lemma order_top.tendsto_at_top_nhds {Î± : Type*} [partial_order Î±] [order_top Î±]
  [topological_space Î²] (f : Î± â†’ Î²) : tendsto f at_top (ğ“ $ f âŠ¤) :=
(tendsto_at_top_pure f).mono_right (pure_le_nhds _)

@[simp] instance nhds_ne_bot {a : Î±} : ne_bot (ğ“ a) :=
ne_bot_of_le (pure_le_nhds a)

/-!
### Cluster points

In this section we define [cluster points](https://en.wikipedia.org/wiki/Limit_point)
(also known as limit points and accumulation points) of a filter and of a sequence.
-/

/-- A point `x` is a cluster point of a filter `F` if `ğ“ x âŠ“ F â‰  âŠ¥`. Also known as
an accumulation point or a limit point, but beware that terminology varies. This
is *not* the same as asking `ğ“[â‰ ] x âŠ“ F â‰  âŠ¥`. See `mem_closure_iff_cluster_pt` in particular. -/
def cluster_pt (x : Î±) (F : filter Î±) : Prop := ne_bot (ğ“ x âŠ“ F)

lemma cluster_pt.ne_bot {x : Î±} {F : filter Î±} (h : cluster_pt x F) : ne_bot (ğ“ x âŠ“ F) := h

lemma filter.has_basis.cluster_pt_iff {Î¹a Î¹F} {pa : Î¹a â†’ Prop} {sa : Î¹a â†’ set Î±}
  {pF : Î¹F â†’ Prop} {sF : Î¹F â†’ set Î±} {F : filter Î±}
  (ha : (ğ“ a).has_basis pa sa) (hF : F.has_basis pF sF) :
  cluster_pt a F â†” âˆ€ â¦ƒiâ¦„ (hi : pa i) â¦ƒjâ¦„ (hj : pF j), (sa i âˆ© sF j).nonempty :=
ha.inf_basis_ne_bot_iff hF

lemma cluster_pt_iff {x : Î±} {F : filter Î±} :
  cluster_pt x F â†” âˆ€ â¦ƒU : set Î±â¦„ (hU : U âˆˆ ğ“ x) â¦ƒVâ¦„ (hV : V âˆˆ F), (U âˆ© V).nonempty :=
inf_ne_bot_iff

/-- `x` is a cluster point of a set `s` if every neighbourhood of `x` meets `s` on a nonempty
set. See also `mem_closure_iff_cluster_pt`. -/
lemma cluster_pt_principal_iff {x : Î±} {s : set Î±} :
  cluster_pt x (ğ“Ÿ s) â†” âˆ€ U âˆˆ ğ“ x, (U âˆ© s).nonempty :=
inf_principal_ne_bot_iff

lemma cluster_pt_principal_iff_frequently {x : Î±} {s : set Î±} :
  cluster_pt x (ğ“Ÿ s) â†” âˆƒá¶  y in ğ“ x, y âˆˆ s :=
by simp only [cluster_pt_principal_iff, frequently_iff, set.nonempty, exists_prop, mem_inter_iff]

lemma cluster_pt.of_le_nhds {x : Î±} {f : filter Î±} (H : f â‰¤ ğ“ x) [ne_bot f] : cluster_pt x f :=
by rwa [cluster_pt, inf_eq_right.mpr H]

lemma cluster_pt.of_le_nhds' {x : Î±} {f : filter Î±} (H : f â‰¤ ğ“ x) (hf : ne_bot f) :
  cluster_pt x f :=
cluster_pt.of_le_nhds H

lemma cluster_pt.of_nhds_le {x : Î±} {f : filter Î±} (H : ğ“ x â‰¤ f) : cluster_pt x f :=
by simp only [cluster_pt, inf_eq_left.mpr H, nhds_ne_bot]

lemma cluster_pt.mono {x : Î±} {f g : filter Î±} (H : cluster_pt x f) (h : f â‰¤ g) :
  cluster_pt x g :=
âŸ¨ne_bot_of_le_ne_bot H.ne $ inf_le_inf_left _ hâŸ©

lemma cluster_pt.of_inf_left {x : Î±} {f g : filter Î±} (H : cluster_pt x $ f âŠ“ g) :
  cluster_pt x f :=
H.mono inf_le_left

lemma cluster_pt.of_inf_right {x : Î±} {f g : filter Î±} (H : cluster_pt x $ f âŠ“ g) :
  cluster_pt x g :=
H.mono inf_le_right

lemma ultrafilter.cluster_pt_iff {x : Î±} {f : ultrafilter Î±} : cluster_pt x f â†” â†‘f â‰¤ ğ“ x :=
âŸ¨f.le_of_inf_ne_bot', Î» h, cluster_pt.of_le_nhds hâŸ©

/-- A point `x` is a cluster point of a sequence `u` along a filter `F` if it is a cluster point
of `map u F`. -/
def map_cluster_pt {Î¹ :Type*} (x : Î±) (F : filter Î¹) (u : Î¹ â†’ Î±) : Prop := cluster_pt x (map u F)

lemma map_cluster_pt_iff {Î¹ :Type*} (x : Î±) (F : filter Î¹) (u : Î¹ â†’ Î±) :
  map_cluster_pt x F u â†” âˆ€ s âˆˆ ğ“ x, âˆƒá¶  a in F, u a âˆˆ s :=
by { simp_rw [map_cluster_pt, cluster_pt, inf_ne_bot_iff_frequently_left, frequently_map], refl }

lemma map_cluster_pt_of_comp {Î¹ Î´ :Type*} {F : filter Î¹} {Ï† : Î´ â†’ Î¹} {p : filter Î´}
  {x : Î±} {u : Î¹ â†’ Î±} [ne_bot p] (h : tendsto Ï† p F) (H : tendsto (u âˆ˜ Ï†) p (ğ“ x)) :
  map_cluster_pt x F u :=
begin
  have := calc
  map (u âˆ˜ Ï†) p = map u (map Ï† p) : map_map
  ... â‰¤ map u F : map_mono h,
  have : map (u âˆ˜ Ï†) p â‰¤ ğ“ x âŠ“ map u F,
    from le_inf H this,
  exact ne_bot_of_le this
end

/--A point `x` is an accumulation point of a filter `F` if `ğ“[â‰ ] x âŠ“ F â‰  âŠ¥`.-/
def acc_pt (x : Î±) (F : filter Î±) : Prop := ne_bot (ğ“[â‰ ] x âŠ“ F)

lemma acc_iff_cluster (x : Î±) (F : filter Î±) : acc_pt x F â†” cluster_pt x (ğ“Ÿ {x}á¶œ âŠ“ F) :=
by rw [acc_pt, nhds_within, cluster_pt, inf_assoc]

/-- `x` is an accumulation point of a set `C` iff it is a cluster point of `C âˆ– {x}`.-/
lemma acc_principal_iff_cluster (x : Î±) (C : set Î±) :
  acc_pt x (ğ“Ÿ C) â†” cluster_pt x (ğ“Ÿ(C \ {x})) :=
by rw [acc_iff_cluster, inf_principal, inter_comm]; refl

/-- `x` is an accumulation point of a set `C` iff every neighborhood
of `x` contains a point of `C` other than `x`. -/
lemma acc_pt_iff_nhds (x : Î±) (C : set Î±) : acc_pt x (ğ“Ÿ C) â†” âˆ€ U âˆˆ ğ“ x, âˆƒ y âˆˆ U âˆ© C, y â‰  x :=
by simp [acc_principal_iff_cluster, cluster_pt_principal_iff, set.nonempty, exists_prop,
  and_assoc, and_comm (Â¬ _ = x)]

/-- `x` is an accumulation point of a set `C` iff
there are points near `x` in `C` and different from `x`.-/
lemma acc_pt_iff_frequently (x : Î±) (C : set Î±) : acc_pt x (ğ“Ÿ C) â†” âˆƒá¶  y in ğ“ x, y â‰  x âˆ§ y âˆˆ C :=
by simp [acc_principal_iff_cluster, cluster_pt_principal_iff_frequently, and_comm]

/-- If `x` is an accumulation point of `F` and `F â‰¤ G`, then
`x` is an accumulation point of `D. -/
lemma acc_pt.mono {x : Î±} {F G : filter Î±} (h : acc_pt x F) (hFG : F â‰¤ G) : acc_pt x G :=
âŸ¨ne_bot_of_le_ne_bot h.ne (inf_le_inf_left _ hFG)âŸ©

/-!
### Interior, closure and frontier in terms of neighborhoods
-/

lemma interior_eq_nhds' {s : set Î±} : interior s = {a | s âˆˆ ğ“ a} :=
set.ext $ Î» x, by simp only [mem_interior, mem_nhds_iff, mem_set_of_eq]

lemma interior_eq_nhds {s : set Î±} : interior s = {a | ğ“ a â‰¤ ğ“Ÿ s} :=
interior_eq_nhds'.trans $ by simp only [le_principal_iff]

lemma mem_interior_iff_mem_nhds {s : set Î±} {a : Î±} :
  a âˆˆ interior s â†” s âˆˆ ğ“ a :=
by rw [interior_eq_nhds', mem_set_of_eq]

@[simp] lemma interior_mem_nhds {s : set Î±} {a : Î±} :
  interior s âˆˆ ğ“ a â†” s âˆˆ ğ“ a :=
âŸ¨Î» h, mem_of_superset h interior_subset,
  Î» h, is_open.mem_nhds is_open_interior (mem_interior_iff_mem_nhds.2 h)âŸ©

lemma interior_set_of_eq {p : Î± â†’ Prop} :
  interior {x | p x} = {x | âˆ€á¶  y in ğ“ x, p y} :=
interior_eq_nhds'

lemma is_open_set_of_eventually_nhds {p : Î± â†’ Prop} :
  is_open {x | âˆ€á¶  y in ğ“ x, p y} :=
by simp only [â† interior_set_of_eq, is_open_interior]

lemma subset_interior_iff_nhds {s V : set Î±} : s âŠ† interior V â†” âˆ€ x âˆˆ s, V âˆˆ ğ“ x :=
show (âˆ€ x, x âˆˆ s â†’  x âˆˆ _) â†” _, by simp_rw mem_interior_iff_mem_nhds

lemma is_open_iff_nhds {s : set Î±} : is_open s â†” âˆ€aâˆˆs, ğ“ a â‰¤ ğ“Ÿ s :=
calc is_open s â†” s âŠ† interior s : subset_interior_iff_is_open.symm
  ... â†” (âˆ€aâˆˆs, ğ“ a â‰¤ ğ“Ÿ s) : by rw [interior_eq_nhds]; refl

lemma is_open_iff_mem_nhds {s : set Î±} : is_open s â†” âˆ€aâˆˆs, s âˆˆ ğ“ a :=
is_open_iff_nhds.trans $ forall_congr $ Î» _, imp_congr_right $ Î» _, le_principal_iff

/-- A set `s` is open iff for every point `x` in `s` and every `y` close to `x`, `y` is in `s`. -/
lemma is_open_iff_eventually {s : set Î±} : is_open s â†” âˆ€ x, x âˆˆ s â†’ âˆ€á¶  y in ğ“ x, y âˆˆ s :=
is_open_iff_mem_nhds

theorem is_open_iff_ultrafilter {s : set Î±} :
  is_open s â†” (âˆ€ (x âˆˆ s) (l : ultrafilter Î±), â†‘l â‰¤ ğ“ x â†’ s âˆˆ l) :=
by simp_rw [is_open_iff_mem_nhds, â† mem_iff_ultrafilter]

lemma is_open_singleton_iff_nhds_eq_pure (a : Î±) :
  is_open ({a} : set Î±) â†” ğ“ a = pure a :=
begin
  split,
  { intros h,
    apply le_antisymm _ (pure_le_nhds a),
    rw le_pure_iff,
    exact h.mem_nhds (mem_singleton a) },
  { intros h,
    simp [is_open_iff_nhds, h] }
end

lemma is_open_singleton_iff_punctured_nhds {Î± : Type*} [topological_space Î±] (a : Î±) :
  is_open ({a} : set Î±) â†” (ğ“[â‰ ] a) = âŠ¥ :=
by rw [is_open_singleton_iff_nhds_eq_pure, nhds_within, â† mem_iff_inf_principal_compl,
        â† le_pure_iff, nhds_ne_bot.le_pure_iff]

lemma mem_closure_iff_frequently {s : set Î±} {a : Î±} : a âˆˆ closure s â†” âˆƒá¶  x in ğ“ a, x âˆˆ s :=
by rw [filter.frequently, filter.eventually, â† mem_interior_iff_mem_nhds,
  closure_eq_compl_interior_compl]; refl

alias mem_closure_iff_frequently â†” _ filter.frequently.mem_closure

/-- A set `s` is closed iff for every point `x`, if there is a point `y` close to `x` that belongs
to `s` then `x` is in `s`. -/
lemma is_closed_iff_frequently {s : set Î±} : is_closed s â†” âˆ€ x, (âˆƒá¶  y in ğ“ x, y âˆˆ s) â†’ x âˆˆ s :=
begin
  rw â† closure_subset_iff_is_closed,
  apply forall_congr (Î» x, _),
  rw mem_closure_iff_frequently
end

/-- The set of cluster points of a filter is closed. In particular, the set of limit points
of a sequence is closed. -/
lemma is_closed_set_of_cluster_pt {f : filter Î±} : is_closed {x | cluster_pt x f} :=
begin
  simp only [cluster_pt, inf_ne_bot_iff_frequently_left, set_of_forall, imp_iff_not_or],
  refine is_closed_Inter (Î» p, is_closed.union _ _); apply is_closed_compl_iff.2,
  exacts [is_open_set_of_eventually_nhds, is_open_const]
end

theorem mem_closure_iff_cluster_pt {s : set Î±} {a : Î±} : a âˆˆ closure s â†” cluster_pt a (ğ“Ÿ s) :=
mem_closure_iff_frequently.trans cluster_pt_principal_iff_frequently.symm

lemma mem_closure_iff_nhds_ne_bot {s : set Î±} : a âˆˆ closure s â†” ğ“ a âŠ“ ğ“Ÿ s â‰  âŠ¥ :=
mem_closure_iff_cluster_pt.trans ne_bot_iff

lemma mem_closure_iff_nhds_within_ne_bot {s : set Î±} {x : Î±} :
  x âˆˆ closure s â†” ne_bot (ğ“[s] x) :=
mem_closure_iff_cluster_pt

/-- If `x` is not an isolated point of a topological space, then `{x}á¶œ` is dense in the whole
space. -/
lemma dense_compl_singleton (x : Î±) [ne_bot (ğ“[â‰ ] x)] : dense ({x}á¶œ : set Î±) :=
begin
  intro y,
  unfreezingI { rcases eq_or_ne y x with rfl|hne },
  { rwa mem_closure_iff_nhds_within_ne_bot },
  { exact subset_closure hne }
end

/-- If `x` is not an isolated point of a topological space, then the closure of `{x}á¶œ` is the whole
space. -/
@[simp] lemma closure_compl_singleton (x : Î±) [ne_bot (ğ“[â‰ ] x)] :
  closure {x}á¶œ = (univ : set Î±) :=
(dense_compl_singleton x).closure_eq

/-- If `x` is not an isolated point of a topological space, then the interior of `{x}` is empty. -/
@[simp] lemma interior_singleton (x : Î±) [ne_bot (ğ“[â‰ ] x)] :
  interior {x} = (âˆ… : set Î±) :=
interior_eq_empty_iff_dense_compl.2 (dense_compl_singleton x)

lemma not_is_open_singleton (x : Î±) [ne_bot (ğ“[â‰ ] x)] : Â¬ is_open ({x} : set Î±) :=
dense_compl_singleton_iff_not_open.1 (dense_compl_singleton x)

lemma closure_eq_cluster_pts {s : set Î±} : closure s = {a | cluster_pt a (ğ“Ÿ s)} :=
set.ext $ Î» x, mem_closure_iff_cluster_pt

theorem mem_closure_iff_nhds {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆ€ t âˆˆ ğ“ a, (t âˆ© s).nonempty :=
mem_closure_iff_cluster_pt.trans cluster_pt_principal_iff

theorem mem_closure_iff_nhds' {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆ€ t âˆˆ ğ“ a, âˆƒ y : s, â†‘y âˆˆ t :=
by simp only [mem_closure_iff_nhds, set.inter_nonempty_iff_exists_right,
              set_coe.exists, subtype.coe_mk]

theorem mem_closure_iff_comap_ne_bot {A : set Î±} {x : Î±} :
  x âˆˆ closure A â†” ne_bot (comap (coe : A â†’ Î±) (ğ“ x)) :=
by simp_rw [mem_closure_iff_nhds, comap_ne_bot_iff, set.inter_nonempty_iff_exists_right,
            set_coe.exists, subtype.coe_mk]

theorem mem_closure_iff_nhds_basis' {a : Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} (h : (ğ“ a).has_basis p s)
  {t : set Î±} :
  a âˆˆ closure t â†” âˆ€ i, p i â†’ (s i âˆ© t).nonempty :=
mem_closure_iff_cluster_pt.trans $ (h.cluster_pt_iff (has_basis_principal _)).trans $
  by simp only [exists_prop, forall_const]

theorem mem_closure_iff_nhds_basis {a : Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} (h : (ğ“ a).has_basis p s)
  {t : set Î±} :
  a âˆˆ closure t â†” âˆ€ i, p i â†’ âˆƒ y âˆˆ t, y âˆˆ s i :=
(mem_closure_iff_nhds_basis' h).trans $
  by simp only [set.nonempty, mem_inter_iff, exists_prop, and_comm]

/-- `x` belongs to the closure of `s` if and only if some ultrafilter
  supported on `s` converges to `x`. -/
lemma mem_closure_iff_ultrafilter {s : set Î±} {x : Î±} :
  x âˆˆ closure s â†” âˆƒ (u : ultrafilter Î±), s âˆˆ u âˆ§ â†‘u â‰¤ ğ“ x :=
by simp [closure_eq_cluster_pts, cluster_pt, â† exists_ultrafilter_iff, and.comm]

lemma is_closed_iff_cluster_pt {s : set Î±} : is_closed s â†” âˆ€a, cluster_pt a (ğ“Ÿ s) â†’ a âˆˆ s :=
calc is_closed s â†” closure s âŠ† s : closure_subset_iff_is_closed.symm
  ... â†” (âˆ€a, cluster_pt a (ğ“Ÿ s) â†’ a âˆˆ s) : by simp only [subset_def, mem_closure_iff_cluster_pt]

lemma is_closed_iff_nhds {s : set Î±} : is_closed s â†” âˆ€ x, (âˆ€ U âˆˆ ğ“ x, (U âˆ© s).nonempty) â†’ x âˆˆ s :=
by simp_rw [is_closed_iff_cluster_pt, cluster_pt, inf_principal_ne_bot_iff]

lemma is_closed.interior_union_left {s t : set Î±} (h : is_closed s) :
  interior (s âˆª t) âŠ† s âˆª interior t :=
Î» a âŸ¨u, âŸ¨âŸ¨huâ‚, huâ‚‚âŸ©, haâŸ©âŸ©, (classical.em (a âˆˆ s)).imp_right $ Î» h, mem_interior.mpr
  âŸ¨u âˆ© sá¶œ, Î» x hx, (huâ‚‚ hx.1).resolve_left hx.2, is_open.inter huâ‚ is_closed.is_open_compl, âŸ¨ha, hâŸ©âŸ©

lemma is_closed.interior_union_right {s t : set Î±} (h : is_closed t) :
  interior (s âˆª t) âŠ† interior s âˆª t :=
by simpa only [union_comm] using h.interior_union_left

lemma is_open.inter_closure {s t : set Î±} (h : is_open s) : s âˆ© closure t âŠ† closure (s âˆ© t) :=
compl_subset_compl.mp $ by simpa only [â† interior_compl, compl_inter]
  using is_closed.interior_union_left h.is_closed_compl

lemma is_open.closure_inter {s t : set Î±} (h : is_open t) : closure s âˆ© t âŠ† closure (s âˆ© t) :=
by simpa only [inter_comm] using h.inter_closure

lemma dense.open_subset_closure_inter {s t : set Î±} (hs : dense s) (ht : is_open t) :
  t âŠ† closure (t âˆ© s) :=
calc t = t âˆ© closure s   : by rw [hs.closure_eq, inter_univ]
   ... âŠ† closure (t âˆ© s) : ht.inter_closure

lemma mem_closure_of_mem_closure_union {sâ‚ sâ‚‚ : set Î±} {x : Î±} (h : x âˆˆ closure (sâ‚ âˆª sâ‚‚))
  (hâ‚ : sâ‚á¶œ âˆˆ ğ“ x) : x âˆˆ closure sâ‚‚ :=
begin
  rw mem_closure_iff_nhds_ne_bot at *,
  rwa â† calc
    ğ“ x âŠ“ principal (sâ‚ âˆª sâ‚‚) = ğ“ x âŠ“ (principal sâ‚ âŠ” principal sâ‚‚) : by rw sup_principal
    ... = (ğ“ x âŠ“ principal sâ‚) âŠ” (ğ“ x âŠ“ principal sâ‚‚) : inf_sup_left
    ... = âŠ¥ âŠ” ğ“ x âŠ“ principal sâ‚‚ : by rw inf_principal_eq_bot.mpr hâ‚
    ... = ğ“ x âŠ“ principal sâ‚‚ : bot_sup_eq
end

/-- The intersection of an open dense set with a dense set is a dense set. -/
lemma dense.inter_of_open_left {s t : set Î±} (hs : dense s) (ht : dense t) (hso : is_open s) :
  dense (s âˆ© t) :=
Î» x, (closure_minimal hso.inter_closure is_closed_closure) $
  by simp [hs.closure_eq, ht.closure_eq]

/-- The intersection of a dense set with an open dense set is a dense set. -/
lemma dense.inter_of_open_right {s t : set Î±} (hs : dense s) (ht : dense t) (hto : is_open t) :
  dense (s âˆ© t) :=
inter_comm t s â–¸ ht.inter_of_open_left hs hto

lemma dense.inter_nhds_nonempty {s t : set Î±} (hs : dense s) {x : Î±} (ht : t âˆˆ ğ“ x) :
  (s âˆ© t).nonempty :=
let âŸ¨U, hsub, ho, hxâŸ© := mem_nhds_iff.1 ht in
  (hs.inter_open_nonempty U ho âŸ¨x, hxâŸ©).mono $ Î» y hy, âŸ¨hy.2, hsub hy.1âŸ©

lemma closure_diff {s t : set Î±} : closure s \ closure t âŠ† closure (s \ t) :=
calc closure s \ closure t = (closure t)á¶œ âˆ© closure s : by simp only [diff_eq, inter_comm]
  ... âŠ† closure ((closure t)á¶œ âˆ© s) : (is_open_compl_iff.mpr $ is_closed_closure).inter_closure
  ... = closure (s \ closure t) : by simp only [diff_eq, inter_comm]
  ... âŠ† closure (s \ t) : closure_mono $ diff_subset_diff (subset.refl s) subset_closure

lemma filter.frequently.mem_of_closed {a : Î±} {s : set Î±} (h : âˆƒá¶  x in ğ“ a, x âˆˆ s)
  (hs : is_closed s) : a âˆˆ s :=
hs.closure_subset h.mem_closure

lemma is_closed.mem_of_frequently_of_tendsto {f : Î² â†’ Î±} {b : filter Î²} {a : Î±} {s : set Î±}
  (hs : is_closed s) (h : âˆƒá¶  x in b, f x âˆˆ s) (hf : tendsto f b (ğ“ a)) : a âˆˆ s :=
(hf.frequently $ show âˆƒá¶  x in b, (Î» y, y âˆˆ s) (f x), from h).mem_of_closed hs

lemma is_closed.mem_of_tendsto {f : Î² â†’ Î±} {b : filter Î²} {a : Î±} {s : set Î±}
  [ne_bot b] (hs : is_closed s) (hf : tendsto f b (ğ“ a)) (h : âˆ€á¶  x in b, f x âˆˆ s) : a âˆˆ s :=
hs.mem_of_frequently_of_tendsto h.frequently hf

lemma mem_closure_of_frequently_of_tendsto {f : Î² â†’ Î±} {b : filter Î²} {a : Î±} {s : set Î±}
  (h : âˆƒá¶  x in b, f x âˆˆ s) (hf : tendsto f b (ğ“ a)) : a âˆˆ closure s :=
filter.frequently.mem_closure $ hf.frequently h

lemma mem_closure_of_tendsto {f : Î² â†’ Î±} {b : filter Î²} {a : Î±} {s : set Î±}
  [ne_bot b] (hf : tendsto f b (ğ“ a)) (h : âˆ€á¶  x in b, f x âˆˆ s) : a âˆˆ closure s :=
mem_closure_of_frequently_of_tendsto h.frequently hf

/-- Suppose that `f` sends the complement to `s` to a single point `a`, and `l` is some filter.
Then `f` tends to `a` along `l` restricted to `s` if and only if it tends to `a` along `l`. -/
lemma tendsto_inf_principal_nhds_iff_of_forall_eq {f : Î² â†’ Î±} {l : filter Î²} {s : set Î²}
  {a : Î±} (h : âˆ€ x âˆ‰ s, f x = a) :
  tendsto f (l âŠ“ ğ“Ÿ s) (ğ“ a) â†” tendsto f l (ğ“ a) :=
begin
  rw [tendsto_iff_comap, tendsto_iff_comap],
  replace h : ğ“Ÿ sá¶œ â‰¤ comap f (ğ“ a),
  { rintros U âŸ¨t, ht, htUâŸ© x hx,
    have : f x âˆˆ t, from (h x hx).symm â–¸ mem_of_mem_nhds ht,
    exact htU this },
  refine âŸ¨Î» h', _, le_trans inf_le_leftâŸ©,
  have := sup_le h' h,
  rw [sup_inf_right, sup_principal, union_compl_self, principal_univ,
    inf_top_eq, sup_le_iff] at this,
  exact this.1
end

/-!
### Limits of filters in topological spaces
-/

section lim

/-- If `f` is a filter, then `Lim f` is a limit of the filter, if it exists. -/
noncomputable def Lim [nonempty Î±] (f : filter Î±) : Î± := epsilon $ Î»a, f â‰¤ ğ“ a

/--
If `f` is a filter satisfying `ne_bot f`, then `Lim' f` is a limit of the filter, if it exists.
-/
def Lim' (f : filter Î±) [ne_bot f] : Î± := @Lim _ _ (nonempty_of_ne_bot f) f

/--
If `F` is an ultrafilter, then `filter.ultrafilter.Lim F` is a limit of the filter, if it exists.
Note that dot notation `F.Lim` can be used for `F : ultrafilter Î±`.
-/
def ultrafilter.Lim : ultrafilter Î± â†’ Î± := Î» F, Lim' F

/-- If `f` is a filter in `Î²` and `g : Î² â†’ Î±` is a function, then `lim f` is a limit of `g` at `f`,
if it exists. -/
noncomputable def lim [nonempty Î±] (f : filter Î²) (g : Î² â†’ Î±) : Î± :=
Lim (f.map g)

/-- If a filter `f` is majorated by some `ğ“ a`, then it is majorated by `ğ“ (Lim f)`. We formulate
this lemma with a `[nonempty Î±]` argument of `Lim` derived from `h` to make it useful for types
without a `[nonempty Î±]` instance. Because of the built-in proof irrelevance, Lean will unify
this instance with any other instance. -/
lemma le_nhds_Lim {f : filter Î±} (h : âˆƒa, f â‰¤ ğ“ a) : f â‰¤ ğ“ (@Lim _ _ (nonempty_of_exists h) f) :=
epsilon_spec h

/-- If `g` tends to some `ğ“ a` along `f`, then it tends to `ğ“ (lim f g)`. We formulate
this lemma with a `[nonempty Î±]` argument of `lim` derived from `h` to make it useful for types
without a `[nonempty Î±]` instance. Because of the built-in proof irrelevance, Lean will unify
this instance with any other instance. -/
lemma tendsto_nhds_lim {f : filter Î²} {g : Î² â†’ Î±} (h : âˆƒ a, tendsto g f (ğ“ a)) :
  tendsto g f (ğ“ $ @lim _ _ _ (nonempty_of_exists h) f g) :=
le_nhds_Lim h

end lim

end topological_space

/-!
### Continuity
-/

section continuous
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}
variables [topological_space Î±] [topological_space Î²] [topological_space Î³]
open_locale topology

/-- A function between topological spaces is continuous if the preimage
  of every open set is open. Registered as a structure to make sure it is not unfolded by Lean. -/
structure continuous (f : Î± â†’ Î²) : Prop :=
(is_open_preimage : âˆ€s, is_open s â†’ is_open (f â»Â¹' s))

lemma continuous_def {f : Î± â†’ Î²} : continuous f â†” (âˆ€s, is_open s â†’ is_open (f â»Â¹' s)) :=
âŸ¨Î» hf s hs, hf.is_open_preimage s hs, Î» h, âŸ¨hâŸ©âŸ©

lemma is_open.preimage {f : Î± â†’ Î²} (hf : continuous f) {s : set Î²} (h : is_open s) :
  is_open (f â»Â¹' s) :=
hf.is_open_preimage s h

lemma continuous.congr {f g : Î± â†’ Î²} (h : continuous f) (h' : âˆ€ x, f x = g x) : continuous g :=
by { convert h, ext, rw h' }

/-- A function between topological spaces is continuous at a point `xâ‚€`
if `f x` tends to `f xâ‚€` when `x` tends to `xâ‚€`. -/
def continuous_at (f : Î± â†’ Î²) (x : Î±) := tendsto f (ğ“ x) (ğ“ (f x))

lemma continuous_at.tendsto {f : Î± â†’ Î²} {x : Î±} (h : continuous_at f x) :
  tendsto f (ğ“ x) (ğ“ (f x)) :=
h

lemma continuous_at_def {f : Î± â†’ Î²} {x : Î±} : continuous_at f x â†” âˆ€ A âˆˆ ğ“ (f x), f â»Â¹' A âˆˆ ğ“ x :=
iff.rfl

lemma continuous_at_congr {f g : Î± â†’ Î²} {x : Î±} (h : f =á¶ [ğ“ x] g) :
  continuous_at f x â†” continuous_at g x :=
by simp only [continuous_at, tendsto_congr' h, h.eq_of_nhds]

lemma continuous_at.congr {f g : Î± â†’ Î²} {x : Î±} (hf : continuous_at f x) (h : f =á¶ [ğ“ x] g) :
  continuous_at g x :=
(continuous_at_congr h).1 hf

lemma continuous_at.preimage_mem_nhds {f : Î± â†’ Î²} {x : Î±} {t : set Î²} (h : continuous_at f x)
  (ht : t âˆˆ ğ“ (f x)) : f â»Â¹' t âˆˆ ğ“ x :=
h ht

lemma eventually_eq_zero_nhds {Mâ‚€} [has_zero Mâ‚€] {a : Î±} {f : Î± â†’ Mâ‚€} :
  f =á¶ [ğ“ a] 0 â†” a âˆ‰ closure (function.support f) :=
by rw [â† mem_compl_iff, â† interior_compl, mem_interior_iff_mem_nhds, function.compl_support]; refl

lemma cluster_pt.map {x : Î±} {la : filter Î±} {lb : filter Î²} (H : cluster_pt x la)
  {f : Î± â†’ Î²} (hfc : continuous_at f x) (hf : tendsto f la lb) :
  cluster_pt (f x) lb :=
âŸ¨ne_bot_of_le_ne_bot ((map_ne_bot_iff f).2 H).ne $ hfc.tendsto.inf hfâŸ©

/-- See also `interior_preimage_subset_preimage_interior`. -/
lemma preimage_interior_subset_interior_preimage {f : Î± â†’ Î²} {s : set Î²}
  (hf : continuous f) : fâ»Â¹' (interior s) âŠ† interior (fâ»Â¹' s) :=
interior_maximal (preimage_mono interior_subset) (is_open_interior.preimage hf)

lemma continuous_id : continuous (id : Î± â†’ Î±) :=
continuous_def.2 $ assume s h, h

lemma continuous.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : continuous g) (hf : continuous f) :
  continuous (g âˆ˜ f) :=
continuous_def.2 $ assume s h, (h.preimage hg).preimage hf

lemma continuous.iterate {f : Î± â†’ Î±} (h : continuous f) (n : â„•) : continuous (f^[n]) :=
nat.rec_on n continuous_id (Î» n ihn, ihn.comp h)

lemma continuous_at.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} {x : Î±}
  (hg : continuous_at g (f x)) (hf : continuous_at f x) :
  continuous_at (g âˆ˜ f) x :=
hg.comp hf

lemma continuous.tendsto {f : Î± â†’ Î²} (hf : continuous f) (x) :
  tendsto f (ğ“ x) (ğ“ (f x)) :=
((nhds_basis_opens x).tendsto_iff $ nhds_basis_opens $ f x).2 $
  Î» t âŸ¨hxt, htâŸ©, âŸ¨f â»Â¹' t, âŸ¨hxt, ht.preimage hfâŸ©, subset.refl _âŸ©

/-- A version of `continuous.tendsto` that allows one to specify a simpler form of the limit.
E.g., one can write `continuous_exp.tendsto' 0 1 exp_zero`. -/
lemma continuous.tendsto' {f : Î± â†’ Î²} (hf : continuous f) (x : Î±) (y : Î²) (h : f x = y) :
  tendsto f (ğ“ x) (ğ“ y) :=
h â–¸ hf.tendsto x

lemma continuous.continuous_at {f : Î± â†’ Î²} {x : Î±} (h : continuous f) :
  continuous_at f x :=
h.tendsto x

lemma continuous_iff_continuous_at {f : Î± â†’ Î²} : continuous f â†” âˆ€ x, continuous_at f x :=
âŸ¨continuous.tendsto,
  assume hf : âˆ€x, tendsto f (ğ“ x) (ğ“ (f x)),
  continuous_def.2 $
  assume s, assume hs : is_open s,
  have âˆ€a, f a âˆˆ s â†’ s âˆˆ ğ“ (f a),
    from Î» a ha, is_open.mem_nhds hs ha,
  show is_open (f â»Â¹' s),
    from is_open_iff_nhds.2 $ Î» a ha, le_principal_iff.2 $ hf _ (this a ha)âŸ©

lemma continuous_at_const {x : Î±} {b : Î²} : continuous_at (Î» a:Î±, b) x :=
tendsto_const_nhds

lemma continuous_const {b : Î²} : continuous (Î»a:Î±, b) :=
continuous_iff_continuous_at.mpr $ assume a, continuous_at_const

lemma filter.eventually_eq.continuous_at {x : Î±} {f : Î± â†’ Î²} {y : Î²} (h : f =á¶ [ğ“ x] (Î» _, y)) :
  continuous_at f x :=
(continuous_at_congr h).2 tendsto_const_nhds

lemma continuous_of_const {f : Î± â†’ Î²} (h : âˆ€ x y, f x = f y) : continuous f :=
continuous_iff_continuous_at.mpr $ Î» x, filter.eventually_eq.continuous_at $
  eventually_of_forall (Î» y, h y x)

lemma continuous_at_id {x : Î±} : continuous_at id x :=
continuous_id.continuous_at

lemma continuous_at.iterate {f : Î± â†’ Î±} {x : Î±} (hf : continuous_at f x) (hx : f x = x) (n : â„•) :
  continuous_at (f^[n]) x :=
nat.rec_on n continuous_at_id $ Î» n ihn,
show continuous_at (f^[n] âˆ˜ f) x,
from continuous_at.comp (hx.symm â–¸ ihn) hf

lemma continuous_iff_is_closed {f : Î± â†’ Î²} :
  continuous f â†” (âˆ€s, is_closed s â†’ is_closed (f â»Â¹' s)) :=
âŸ¨assume hf s hs, by simpa using (continuous_def.1 hf sá¶œ hs.is_open_compl).is_closed_compl,
  assume hf, continuous_def.2 $ assume s,
    by rw [â†is_closed_compl_iff, â†is_closed_compl_iff]; exact hf _âŸ©

lemma is_closed.preimage {f : Î± â†’ Î²} (hf : continuous f) {s : set Î²} (h : is_closed s) :
  is_closed (f â»Â¹' s) :=
continuous_iff_is_closed.mp hf s h

lemma mem_closure_image {f : Î± â†’ Î²} {x : Î±} {s : set Î±} (hf : continuous_at f x)
  (hx : x âˆˆ closure s) : f x âˆˆ closure (f '' s) :=
mem_closure_of_frequently_of_tendsto
  ((mem_closure_iff_frequently.1 hx).mono (Î» x, mem_image_of_mem _)) hf

lemma continuous_at_iff_ultrafilter {f : Î± â†’ Î²} {x} : continuous_at f x â†”
  âˆ€ g : ultrafilter Î±, â†‘g â‰¤ ğ“ x â†’ tendsto f g (ğ“ (f x)) :=
tendsto_iff_ultrafilter f (ğ“ x) (ğ“ (f x))

lemma continuous_iff_ultrafilter {f : Î± â†’ Î²} :
  continuous f â†” âˆ€ x (g : ultrafilter Î±), â†‘g â‰¤ ğ“ x â†’ tendsto f g (ğ“ (f x)) :=
by simp only [continuous_iff_continuous_at, continuous_at_iff_ultrafilter]

lemma continuous.closure_preimage_subset {f : Î± â†’ Î²}
  (hf : continuous f) (t : set Î²) :
  closure (f â»Â¹' t) âŠ† f â»Â¹' (closure t) :=
begin
  rw â† (is_closed_closure.preimage hf).closure_eq,
  exact closure_mono (preimage_mono subset_closure),
end

lemma continuous.frontier_preimage_subset
  {f : Î± â†’ Î²} (hf : continuous f) (t : set Î²) :
  frontier (f â»Â¹' t) âŠ† f â»Â¹' (frontier t) :=
diff_subset_diff (hf.closure_preimage_subset t) (preimage_interior_subset_interior_preimage hf)

/-- If a continuous map `f` maps `s` to `t`, then it maps `closure s` to `closure t`. -/
lemma set.maps_to.closure {s : set Î±} {t : set Î²} {f : Î± â†’ Î²} (h : maps_to f s t)
  (hc : continuous f) : maps_to f (closure s) (closure t) :=
begin
  simp only [maps_to, mem_closure_iff_cluster_pt],
  exact Î» x hx, hx.map hc.continuous_at (tendsto_principal_principal.2 h)
end

lemma image_closure_subset_closure_image {f : Î± â†’ Î²} {s : set Î±} (h : continuous f) :
  f '' closure s âŠ† closure (f '' s) :=
((maps_to_image f s).closure h).image_subset

lemma closure_subset_preimage_closure_image {f : Î± â†’ Î²} {s : set Î±} (h : continuous f) :
  closure s âŠ† f â»Â¹' (closure (f '' s)) :=
by { rw â† set.image_subset_iff, exact image_closure_subset_closure_image h }

lemma map_mem_closure {s : set Î±} {t : set Î²} {f : Î± â†’ Î²} {a : Î±}
  (hf : continuous f) (ha : a âˆˆ closure s) (ht : maps_to f s t) : f a âˆˆ closure t :=
ht.closure hf ha

/-- If a continuous map `f` maps `s` to a closed set `t`, then it maps `closure s` to `t`. -/
lemma set.maps_to.closure_left {s : set Î±} {t : set Î²} {f : Î± â†’ Î²} (h : maps_to f s t)
  (hc : continuous f) (ht : is_closed t) : maps_to f (closure s) t :=
ht.closure_eq â–¸ h.closure hc

/-!
### Function with dense range
-/

section dense_range
variables {Îº Î¹ : Type*} (f : Îº â†’ Î²) (g : Î² â†’ Î³)

/-- `f : Î¹ â†’ Î²` has dense range if its range (image) is a dense subset of Î². -/
def dense_range := dense (range f)

variables {f}

/-- A surjective map has dense range. -/
lemma function.surjective.dense_range (hf : function.surjective f) : dense_range f :=
Î» x, by simp [hf.range_eq]

lemma dense_range_id : dense_range (id : Î± â†’ Î±) :=
function.surjective.dense_range function.surjective_id

lemma dense_range_iff_closure_range : dense_range f â†” closure (range f) = univ :=
dense_iff_closure_eq

lemma dense_range.closure_range (h : dense_range f) : closure (range f) = univ :=
h.closure_eq

lemma dense.dense_range_coe {s : set Î±} (h : dense s) : dense_range (coe : s â†’ Î±) :=
by simpa only [dense_range, subtype.range_coe_subtype]

lemma continuous.range_subset_closure_image_dense {f : Î± â†’ Î²} (hf : continuous f)
  {s : set Î±} (hs : dense s) :
  range f âŠ† closure (f '' s) :=
by { rw [â† image_univ, â† hs.closure_eq], exact image_closure_subset_closure_image hf }

/-- The image of a dense set under a continuous map with dense range is a dense set. -/
lemma dense_range.dense_image {f : Î± â†’ Î²} (hf' : dense_range f) (hf : continuous f)
  {s : set Î±} (hs : dense s) :
  dense (f '' s)  :=
(hf'.mono $ hf.range_subset_closure_image_dense hs).of_closure

/-- If `f` has dense range and `s` is an open set in the codomain of `f`, then the image of the
preimage of `s` under `f` is dense in `s`. -/
lemma dense_range.subset_closure_image_preimage_of_is_open (hf : dense_range f) {s : set Î²}
  (hs : is_open s) : s âŠ† closure (f '' (f â»Â¹' s)) :=
by { rw image_preimage_eq_inter_range, exact hf.open_subset_closure_inter hs }

/-- If a continuous map with dense range maps a dense set to a subset of `t`, then `t` is a dense
set. -/
lemma dense_range.dense_of_maps_to {f : Î± â†’ Î²} (hf' : dense_range f) (hf : continuous f)
  {s : set Î±} (hs : dense s) {t : set Î²} (ht : maps_to f s t) :
  dense t :=
(hf'.dense_image hf hs).mono ht.image_subset

/-- Composition of a continuous map with dense range and a function with dense range has dense
range. -/
lemma dense_range.comp {g : Î² â†’ Î³} {f : Îº â†’ Î²} (hg : dense_range g) (hf : dense_range f)
  (cg : continuous g) :
  dense_range (g âˆ˜ f) :=
by { rw [dense_range, range_comp], exact hg.dense_image cg hf }

lemma dense_range.nonempty_iff (hf : dense_range f) : nonempty Îº â†” nonempty Î² :=
range_nonempty_iff_nonempty.symm.trans hf.nonempty_iff

lemma dense_range.nonempty [h : nonempty Î²] (hf : dense_range f) : nonempty Îº :=
hf.nonempty_iff.mpr h

/-- Given a function `f : Î± â†’ Î²` with dense range and `b : Î²`, returns some `a : Î±`. -/
def dense_range.some (hf : dense_range f) (b : Î²) : Îº :=
classical.choice $ hf.nonempty_iff.mpr âŸ¨bâŸ©

lemma dense_range.exists_mem_open (hf : dense_range f) {s : set Î²} (ho : is_open s)
  (hs : s.nonempty) :
  âˆƒ a, f a âˆˆ s :=
exists_range_iff.1 $ hf.exists_mem_open ho hs

lemma dense_range.mem_nhds {f : Îº â†’ Î²} (h : dense_range f) {b : Î²} {U : set Î²}
  (U_in : U âˆˆ ğ“ b) : âˆƒ a, f a âˆˆ U :=
let âŸ¨a, haâŸ© := h.exists_mem_open is_open_interior âŸ¨b, mem_interior_iff_mem_nhds.2 U_inâŸ©
in âŸ¨a, interior_subset haâŸ©

end dense_range

end continuous

/--
The library contains many lemmas stating that functions/operations are continuous. There are many
ways to formulate the continuity of operations. Some are more convenient than others.
Note: for the most part this note also applies to other properties
(`measurable`, `differentiable`, `continuous_on`, ...).

### The traditional way
As an example, let's look at addition `(+) : M â†’ M â†’ M`. We can state that this is continuous
in different definitionally equal ways (omitting some typing information)
* `continuous (Î» p, p.1 + p.2)`;
* `continuous (function.uncurry (+))`;
* `continuous â†¿(+)`. (`â†¿` is notation for recursively uncurrying a function)

However, lemmas with this conclusion are not nice to use in practice because
1. They confuse the elaborator. The following two examples fail, because of limitations in the
  elaboration process.
  ```
  variables {M : Type*} [has_add M] [topological_space M] [has_continuous_add M]
  example : continuous (Î» x : M, x + x) :=
  continuous_add.comp _

  example : continuous (Î» x : M, x + x) :=
  continuous_add.comp (continuous_id.prod_mk continuous_id)
  ```
  The second is a valid proof, which is accepted if you write it as
  `continuous_add.comp (continuous_id.prod_mk continuous_id : _)`

2. If the operation has more than 2 arguments, they are impractical to use, because in your
  application the arguments in the domain might be in a different order or associated differently.

### The convenient way
A much more convenient way to write continuity lemmas is like `continuous.add`:
```
continuous.add {f g : X â†’ M} (hf : continuous f) (hg : continuous g) : continuous (Î» x, f x + g x)
```
The conclusion can be `continuous (f + g)`, which is definitionally equal.
This has the following advantages
* It supports projection notation, so is shorter to write.
* `continuous.add _ _` is recognized correctly by the elaborator and gives useful new goals.
* It works generally, since the domain is a variable.

As an example for an unary operation, we have `continuous.neg`.
```
continuous.neg {f : Î± â†’ G} (hf : continuous f) : continuous (Î» x, -f x)
```
For unary functions, the elaborator is not confused when applying the traditional lemma
(like `continuous_neg`), but it's still convenient to have the short version available (compare
`hf.neg.neg.neg` with `continuous_neg.comp $ continuous_neg.comp $ continuous_neg.comp hf`).

As a harder example, consider an operation of the following type:
```
def strans {x : F} (Î³ Î³' : path x x) (tâ‚€ : I) : path x x
```
The precise definition is not important, only its type.
The correct continuity principle for this operation is something like this:
```
{f : X â†’ F} {Î³ Î³' : âˆ€ x, path (f x) (f x)} {tâ‚€ s : X â†’ I}
  (hÎ³ : continuous â†¿Î³) (hÎ³' : continuous â†¿Î³')
  (ht : continuous tâ‚€) (hs : continuous s) :
  continuous (Î» x, strans (Î³ x) (Î³' x) (t x) (s x))
```
Note that *all* arguments of `strans` are indexed over `X`, even the basepoint `x`, and the last
argument `s` that arises since `path x x` has a coercion to `I â†’ F`. The paths `Î³` and `Î³'` (which
are unary functions from `I`) become binary functions in the continuity lemma.

### Summary
* Make sure that your continuity lemmas are stated in the most general way, and in a convenient
  form. That means that:
  - The conclusion has a variable `X` as domain (not something like `Y Ã— Z`);
  - Wherever possible, all point arguments `c : Y` are replaced by functions `c : X â†’ Y`;
  - All `n`-ary function arguments are replaced by `n+1`-ary functions
    (`f : Y â†’ Z` becomes `f : X â†’ Y â†’ Z`);
  - All (relevant) arguments have continuity assumptions, and perhaps there are additional
    assumptions needed to make the operation continuous;
  - The function in the conclusion is fully applied.
* These remarks are mostly about the format of the *conclusion* of a continuity lemma.
  In assumptions it's fine to state that a function with more than 1 argument is continuous using
  `â†¿` or `function.uncurry`.

### Functions with discontinuities

In some cases, you want to work with discontinuous functions, and in certain expressions they are
still continuous. For example, consider the fractional part of a number, `fract : â„ â†’ â„`.
In this case, you want to add conditions to when a function involving `fract` is continuous, so you
get something like this: (assumption `hf` could be weakened, but the important thing is the shape
of the conclusion)
```
lemma continuous_on.comp_fract {X Y : Type*} [topological_space X] [topological_space Y]
  {f : X â†’ â„ â†’ Y} {g : X â†’ â„} (hf : continuous â†¿f) (hg : continuous g) (h : âˆ€ s, f s 0 = f s 1) :
  continuous (Î» x, f x (fract (g x)))
```
With `continuous_at` you can be even more precise about what to prove in case of discontinuities,
see e.g. `continuous_at.comp_div_cases`.
-/
library_note "continuity lemma statement"
