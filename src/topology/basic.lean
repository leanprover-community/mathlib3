/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Jeremy Avigad
-/
import order.filter.ultrafilter
import order.filter.partial
import order.filter.bases

/-!
# Basic theory of topological spaces.

The main definition is the type class `topological space Î±` which endows a type `Î±` with a topology.
Then `set Î±` gets predicates `is_open`, `is_closed` and functions `interior`, `closure` and
`frontier`. Each point `x` of `Î±` gets a neighborhood filter `ğ“ x`. A filter `F` on `Î±`Â has
`x` as a cluster point if `is_cluster_pt x F : ğ“ x âŠ“ F â‰  âŠ¥`. A map `f : Î¹ â†’ Î±` clusters at `x`
along `F : filter Î¹` if `map_cluster_pt x F f : cluster_pt x (map f F)`. In particular
the notion of cluster point of a sequence `u` is `map_cluster_pt x at_top u`.

This file also defines locally finite families of subsets of `Î±`.

For topological spaces `Î±` and `Î²`, a function `f : Î± â†’ Î²` and a point `a : Î±`,
`continuous_at f a` means `f` is continuous at `a`, and global continuity is
`continuous f`. There is also a version of continuity `pcontinuous` for
partially defined functions.

## Implementation notes

Topology in mathlib heavily uses filters (even more than in Bourbaki). See explanations in
<https://leanprover-community.github.io/theories/topology.html>.

## References

*  [N. Bourbaki, *General Topology*][bourbaki1966]
*  [I. M. James, *Topologies and Uniformities*][james1999]

## Tags

topological space, interior, closure, frontier, neighborhood, continuity, continuous function
-/

open set filter classical
open_locale classical filter

universes u v w

/-!
###Â Topological spaces
-/

/-- A topology on `Î±`. -/
@[protect_proj] structure topological_space (Î± : Type u) :=
(is_open        : set Î± â†’ Prop)
(is_open_univ   : is_open univ)
(is_open_inter  : âˆ€s t, is_open s â†’ is_open t â†’ is_open (s âˆ© t))
(is_open_sUnion : âˆ€s, (âˆ€tâˆˆs, is_open t) â†’ is_open (â‹ƒâ‚€ s))

attribute [class] topological_space

/-- A constructor for topologies by specifying the closed sets,
and showing that they satisfy the appropriate conditions. -/
def topological_space.of_closed {Î± : Type u} (T : set (set Î±))
  (empty_mem : âˆ… âˆˆ T) (sInter_mem : âˆ€ A âŠ† T, â‹‚â‚€ A âˆˆ T) (union_mem : âˆ€ A B âˆˆ T, A âˆª B âˆˆ T) :
  topological_space Î± :=
{ is_open := Î» X, Xá¶œ âˆˆ T,
  is_open_univ := by simp [empty_mem],
  is_open_inter := Î» s t hs ht, by simpa [set.compl_inter] using union_mem sá¶œ tá¶œ hs ht,
  is_open_sUnion := Î» s hs,
    by rw set.compl_sUnion; exact sInter_mem (set.compl '' s)
    (Î» z âŸ¨y, hy, hzâŸ©, by simpa [hz.symm] using hs y hy) }

section topological_space

variables {Î± : Type u} {Î² : Type v} {Î¹ : Sort w} {a : Î±} {s sâ‚ sâ‚‚ : set Î±} {p pâ‚ pâ‚‚ : Î± â†’ Prop}

@[ext]
lemma topological_space_eq : âˆ€ {f g : topological_space Î±}, f.is_open = g.is_open â†’ f = g
| âŸ¨a, _, _, _âŸ© âŸ¨b, _, _, _âŸ© rfl := rfl

section
variables [t : topological_space Î±]
include t

/-- `is_open s` means that `s` is open in the ambient topological space on `Î±` -/
def is_open (s : set Î±) : Prop := topological_space.is_open t s

@[simp]
lemma is_open_univ : is_open (univ : set Î±) := topological_space.is_open_univ t

lemma is_open_inter (hâ‚ : is_open sâ‚) (hâ‚‚ : is_open sâ‚‚) : is_open (sâ‚ âˆ© sâ‚‚) :=
topological_space.is_open_inter t sâ‚ sâ‚‚ hâ‚ hâ‚‚

lemma is_open_sUnion {s : set (set Î±)} (h : âˆ€t âˆˆ s, is_open t) : is_open (â‹ƒâ‚€ s) :=
topological_space.is_open_sUnion t s h

end

lemma is_open_fold {s : set Î±} {t : topological_space Î±} : t.is_open s = @is_open Î± t s :=
rfl

variables [topological_space Î±]

lemma is_open_Union {f : Î¹ â†’ set Î±} (h : âˆ€i, is_open (f i)) : is_open (â‹ƒi, f i) :=
is_open_sUnion $ by rintro _ âŸ¨i, rflâŸ©; exact h i

lemma is_open_bUnion {s : set Î²} {f : Î² â†’ set Î±} (h : âˆ€iâˆˆs, is_open (f i)) :
  is_open (â‹ƒiâˆˆs, f i) :=
is_open_Union $ assume i, is_open_Union $ assume hi, h i hi

lemma is_open_union (hâ‚ : is_open sâ‚) (hâ‚‚ : is_open sâ‚‚) : is_open (sâ‚ âˆª sâ‚‚) :=
by rw union_eq_Union; exact is_open_Union (bool.forall_bool.2 âŸ¨hâ‚‚, hâ‚âŸ©)

@[simp] lemma is_open_empty : is_open (âˆ… : set Î±) :=
by rw â† sUnion_empty; exact is_open_sUnion (assume a, false.elim)

lemma is_open_sInter {s : set (set Î±)} (hs : finite s) : (âˆ€t âˆˆ s, is_open t) â†’ is_open (â‹‚â‚€ s) :=
finite.induction_on hs (Î» _, by rw sInter_empty; exact is_open_univ) $
Î» a s has hs ih h, by rw sInter_insert; exact
is_open_inter (h _ $ mem_insert _ _) (ih $ Î» t, h t âˆ˜ mem_insert_of_mem _)

lemma is_open_bInter {s : set Î²} {f : Î² â†’ set Î±} (hs : finite s) :
  (âˆ€iâˆˆs, is_open (f i)) â†’ is_open (â‹‚iâˆˆs, f i) :=
finite.induction_on hs
  (Î» _, by rw bInter_empty; exact is_open_univ)
  (Î» a s has hs ih h, by rw bInter_insert; exact
    is_open_inter (h a (mem_insert _ _)) (ih (Î» i hi, h i (mem_insert_of_mem _ hi))))

lemma is_open_Inter [fintype Î²] {s : Î² â†’ set Î±}
  (h : âˆ€ i, is_open (s i)) : is_open (â‹‚ i, s i) :=
suffices is_open (â‹‚ (i : Î²) (hi : i âˆˆ @univ Î²), s i), by simpa,
is_open_bInter finite_univ (Î» i _, h i)

lemma is_open_Inter_prop {p : Prop} {s : p â†’ set Î±}
  (h : âˆ€ h : p, is_open (s h)) : is_open (Inter s) :=
by by_cases p; simp *

lemma is_open_const {p : Prop} : is_open {a : Î± | p} :=
by_cases
  (assume : p, begin simp only [this]; exact is_open_univ end)
  (assume : Â¬ p, begin simp only [this]; exact is_open_empty end)

lemma is_open_and : is_open {a | pâ‚ a} â†’ is_open {a | pâ‚‚ a} â†’ is_open {a | pâ‚ a âˆ§ pâ‚‚ a} :=
is_open_inter

/-- A set is closed if its complement is open -/
def is_closed (s : set Î±) : Prop := is_open sá¶œ

@[simp] lemma is_closed_empty : is_closed (âˆ… : set Î±) :=
by unfold is_closed; rw compl_empty; exact is_open_univ

@[simp] lemma is_closed_univ : is_closed (univ : set Î±) :=
by unfold is_closed; rw compl_univ; exact is_open_empty

lemma is_closed_union : is_closed sâ‚ â†’ is_closed sâ‚‚ â†’ is_closed (sâ‚ âˆª sâ‚‚) :=
Î» hâ‚ hâ‚‚, by unfold is_closed; rw compl_union; exact is_open_inter hâ‚ hâ‚‚

lemma is_closed_sInter {s : set (set Î±)} : (âˆ€t âˆˆ s, is_closed t) â†’ is_closed (â‹‚â‚€ s) :=
by simp only [is_closed, compl_sInter, sUnion_image]; exact assume h, is_open_Union $ assume t, is_open_Union $ assume ht, h t ht

lemma is_closed_Inter {f : Î¹ â†’ set Î±} (h : âˆ€i, is_closed (f i)) : is_closed (â‹‚i, f i ) :=
is_closed_sInter $ assume t âŸ¨i, (heq : f i = t)âŸ©, heq â–¸ h i

@[simp] lemma is_open_compl_iff {s : set Î±} : is_open sá¶œ â†” is_closed s := iff.rfl

@[simp] lemma is_closed_compl_iff {s : set Î±} : is_closed sá¶œ â†” is_open s :=
by rw [â†is_open_compl_iff, compl_compl]

lemma is_open_diff {s t : set Î±} (hâ‚ : is_open s) (hâ‚‚ : is_closed t) : is_open (s \ t) :=
is_open_inter hâ‚ $ is_open_compl_iff.mpr hâ‚‚

lemma is_closed_inter (hâ‚ : is_closed sâ‚) (hâ‚‚ : is_closed sâ‚‚) : is_closed (sâ‚ âˆ© sâ‚‚) :=
by rw [is_closed, compl_inter]; exact is_open_union hâ‚ hâ‚‚

lemma is_closed_bUnion {s : set Î²} {f : Î² â†’ set Î±} (hs : finite s) :
  (âˆ€iâˆˆs, is_closed (f i)) â†’ is_closed (â‹ƒiâˆˆs, f i) :=
finite.induction_on hs
  (Î» _, by rw bUnion_empty; exact is_closed_empty)
  (Î» a s has hs ih h, by rw bUnion_insert; exact
    is_closed_union (h a (mem_insert _ _)) (ih (Î» i hi, h i (mem_insert_of_mem _ hi))))

lemma is_closed_Union [fintype Î²] {s : Î² â†’ set Î±}
  (h : âˆ€ i, is_closed (s i)) : is_closed (Union s) :=
suffices is_closed (â‹ƒ (i : Î²) (hi : i âˆˆ @univ Î²), s i),
  by convert this; simp [set.ext_iff],
is_closed_bUnion finite_univ (Î» i _, h i)

lemma is_closed_Union_prop {p : Prop} {s : p â†’ set Î±}
  (h : âˆ€ h : p, is_closed (s h)) : is_closed (Union s) :=
by by_cases p; simp *

lemma is_closed_imp {p q : Î± â†’ Prop} (hp : is_open {x | p x})
  (hq : is_closed {x | q x}) : is_closed {x | p x â†’ q x} :=
have {x | p x â†’ q x} = {x | p x}á¶œ âˆª {x | q x}, from set.ext $ Î» x, imp_iff_not_or,
by rw [this]; exact is_closed_union (is_closed_compl_iff.mpr hp) hq

lemma is_open_neg : is_closed {a | p a} â†’ is_open {a | Â¬ p a} :=
is_open_compl_iff.mpr

/-!
### Interior of a set
-/

/-- The interior of a set `s` is the largest open subset of `s`. -/
def interior (s : set Î±) : set Î± := â‹ƒâ‚€ {t | is_open t âˆ§ t âŠ† s}

lemma mem_interior {s : set Î±} {x : Î±} :
  x âˆˆ interior s â†” âˆƒ t âŠ† s, is_open t âˆ§ x âˆˆ t :=
by simp only [interior, mem_set_of_eq, exists_prop, and_assoc, and.left_comm]

@[simp] lemma is_open_interior {s : set Î±} : is_open (interior s) :=
is_open_sUnion $ assume t âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚

lemma interior_subset {s : set Î±} : interior s âŠ† s :=
sUnion_subset $ assume t âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚‚

lemma interior_maximal {s t : set Î±} (hâ‚ : t âŠ† s) (hâ‚‚ : is_open t) : t âŠ† interior s :=
subset_sUnion_of_mem âŸ¨hâ‚‚, hâ‚âŸ©

lemma interior_eq_of_open {s : set Î±} (h : is_open s) : interior s = s :=
subset.antisymm interior_subset (interior_maximal (subset.refl s) h)

lemma interior_eq_iff_open {s : set Î±} : interior s = s â†” is_open s :=
âŸ¨assume h, h â–¸ is_open_interior, interior_eq_of_openâŸ©

lemma subset_interior_iff_open {s : set Î±} : s âŠ† interior s â†” is_open s :=
by simp only [interior_eq_iff_open.symm, subset.antisymm_iff, interior_subset, true_and]

lemma subset_interior_iff_subset_of_open {s t : set Î±} (hâ‚ : is_open s) :
  s âŠ† interior t â†” s âŠ† t :=
âŸ¨assume h, subset.trans h interior_subset, assume hâ‚‚, interior_maximal hâ‚‚ hâ‚âŸ©

lemma interior_mono {s t : set Î±} (h : s âŠ† t) : interior s âŠ† interior t :=
interior_maximal (subset.trans interior_subset h) is_open_interior

@[simp] lemma interior_empty : interior (âˆ… : set Î±) = âˆ… :=
interior_eq_of_open is_open_empty

@[simp] lemma interior_univ : interior (univ : set Î±) = univ :=
interior_eq_of_open is_open_univ

@[simp] lemma interior_interior {s : set Î±} : interior (interior s) = interior s :=
interior_eq_of_open is_open_interior

@[simp] lemma interior_inter {s t : set Î±} : interior (s âˆ© t) = interior s âˆ© interior t :=
subset.antisymm
  (subset_inter (interior_mono $ inter_subset_left s t) (interior_mono $ inter_subset_right s t))
  (interior_maximal (inter_subset_inter interior_subset interior_subset) $ is_open_inter is_open_interior is_open_interior)

lemma interior_union_is_closed_of_interior_empty {s t : set Î±} (hâ‚ : is_closed s) (hâ‚‚ : interior t = âˆ…) :
  interior (s âˆª t) = interior s :=
have interior (s âˆª t) âŠ† s, from
  assume x âŸ¨u, âŸ¨(huâ‚ : is_open u), (huâ‚‚ : u âŠ† s âˆª t)âŸ©, (hxâ‚ : x âˆˆ u)âŸ©,
  classical.by_contradiction $ assume hxâ‚‚ : x âˆ‰ s,
    have u \ s âŠ† t,
      from assume x âŸ¨hâ‚, hâ‚‚âŸ©, or.resolve_left (huâ‚‚ hâ‚) hâ‚‚,
    have u \ s âŠ† interior t,
      by rwa subset_interior_iff_subset_of_open (is_open_diff huâ‚ hâ‚),
    have u \ s âŠ† âˆ…,
      by rwa hâ‚‚ at this,
    this âŸ¨hxâ‚, hxâ‚‚âŸ©,
subset.antisymm
  (interior_maximal this is_open_interior)
  (interior_mono $ subset_union_left _ _)

lemma is_open_iff_forall_mem_open : is_open s â†” âˆ€ x âˆˆ s, âˆƒ t âŠ† s, is_open t âˆ§ x âˆˆ t :=
by rw â† subset_interior_iff_open; simp only [subset_def, mem_interior]

/-!
###Â Closure of a set
-/

/-- The closure of `s` is the smallest closed set containing `s`. -/
def closure (s : set Î±) : set Î± := â‹‚â‚€ {t | is_closed t âˆ§ s âŠ† t}

@[simp] lemma is_closed_closure {s : set Î±} : is_closed (closure s) :=
is_closed_sInter $ assume t âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚

lemma subset_closure {s : set Î±} : s âŠ† closure s :=
subset_sInter $ assume t âŸ¨hâ‚, hâ‚‚âŸ©, hâ‚‚

lemma closure_minimal {s t : set Î±} (hâ‚ : s âŠ† t) (hâ‚‚ : is_closed t) : closure s âŠ† t :=
sInter_subset_of_mem âŸ¨hâ‚‚, hâ‚âŸ©

lemma is_closed.closure_eq {s : set Î±} (h : is_closed s) : closure s = s :=
subset.antisymm (closure_minimal (subset.refl s) h) subset_closure

lemma is_closed.closure_subset {s : set Î±} (hs : is_closed s) : closure s âŠ† s :=
closure_minimal (subset.refl _) hs

lemma is_closed.closure_subset_iff {s t : set Î±} (hâ‚ : is_closed t) :
  closure s âŠ† t â†” s âŠ† t :=
âŸ¨subset.trans subset_closure, assume h, closure_minimal h hâ‚âŸ©

lemma closure_mono {s t : set Î±} (h : s âŠ† t) : closure s âŠ† closure t :=
closure_minimal (subset.trans h subset_closure) is_closed_closure

lemma monotone_closure (Î± : Type*) [topological_space Î±] : monotone (@closure Î± _) :=
Î» _ _, closure_mono

lemma closure_inter_subset_inter_closure (s t : set Î±) :
  closure (s âˆ© t) âŠ† closure s âˆ© closure t :=
(monotone_closure Î±).map_inf_le s t

lemma is_closed_of_closure_subset {s : set Î±} (h : closure s âŠ† s) : is_closed s :=
by rw subset.antisymm subset_closure h; exact is_closed_closure

lemma closure_eq_iff_is_closed {s : set Î±} : closure s = s â†” is_closed s :=
âŸ¨assume h, h â–¸ is_closed_closure, is_closed.closure_eqâŸ©

lemma closure_subset_iff_is_closed {s : set Î±} : closure s âŠ† s â†” is_closed s :=
âŸ¨is_closed_of_closure_subset, is_closed.closure_subsetâŸ©

@[simp] lemma closure_empty : closure (âˆ… : set Î±) = âˆ… :=
is_closed_empty.closure_eq

@[simp] lemma closure_empty_iff (s : set Î±) : closure s = âˆ… â†” s = âˆ… :=
âŸ¨subset_eq_empty subset_closure, Î» h, h.symm â–¸ closure_emptyâŸ©

lemma set.nonempty.closure {s : set Î±} (h : s.nonempty) :
  set.nonempty (closure s) :=
let âŸ¨x, hxâŸ© := h in âŸ¨x, subset_closure hxâŸ©

@[simp] lemma closure_univ : closure (univ : set Î±) = univ :=
is_closed_univ.closure_eq

@[simp] lemma closure_closure {s : set Î±} : closure (closure s) = closure s :=
is_closed_closure.closure_eq

@[simp] lemma closure_union {s t : set Î±} : closure (s âˆª t) = closure s âˆª closure t :=
subset.antisymm
  (closure_minimal (union_subset_union subset_closure subset_closure) $ is_closed_union is_closed_closure is_closed_closure)
  ((monotone_closure Î±).le_map_sup s t)

lemma interior_subset_closure {s : set Î±} : interior s âŠ† closure s :=
subset.trans interior_subset subset_closure

lemma closure_eq_compl_interior_compl {s : set Î±} : closure s = (interior sá¶œ)á¶œ :=
begin
  unfold interior closure is_closed,
  rw [compl_sUnion, compl_image_set_of],
  simp only [compl_subset_compl]
end

@[simp] lemma interior_compl {s : set Î±} : interior sá¶œ = (closure s)á¶œ :=
by simp [closure_eq_compl_interior_compl]

@[simp] lemma closure_compl {s : set Î±} : closure sá¶œ = (interior s)á¶œ :=
by simp [closure_eq_compl_interior_compl]

theorem mem_closure_iff {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆ€ o, is_open o â†’ a âˆˆ o â†’ (o âˆ© s).nonempty :=
âŸ¨Î» h o oo ao, classical.by_contradiction $ Î» os,
  have s âŠ† oá¶œ, from Î» x xs xo, os âŸ¨x, xo, xsâŸ©,
  closure_minimal this (is_closed_compl_iff.2 oo) h ao,
Î» H c âŸ¨hâ‚, hâ‚‚âŸ©, classical.by_contradiction $ Î» nc,
  let âŸ¨x, hc, hsâŸ© := (H _ hâ‚ nc) in hc (hâ‚‚ hs)âŸ©

lemma dense_iff_inter_open {s : set Î±} :
  closure s = univ â†” âˆ€ U, is_open U â†’ U.nonempty â†’ (U âˆ© s).nonempty :=
begin
  split ; intro h,
  { rintros U U_op âŸ¨x, x_inâŸ©,
    exact mem_closure_iff.1 (by simp only [h]) U U_op x_in },
  { apply eq_univ_of_forall, intro x,
    rw mem_closure_iff,
    intros U U_op x_in,
    exact h U U_op âŸ¨_, x_inâŸ© },
end

lemma dense_of_subset_dense {sâ‚ sâ‚‚ : set Î±} (h : sâ‚ âŠ† sâ‚‚) (hd : closure sâ‚ = univ) :
  closure sâ‚‚ = univ :=
by { rw [â† univ_subset_iff, â† hd], exact closure_mono h }

/-!
### Frontier of a set
-/

/-- The frontier of a set is the set of points between the closure and interior. -/
def frontier (s : set Î±) : set Î± := closure s \ interior s

lemma frontier_eq_closure_inter_closure {s : set Î±} :
  frontier s = closure s âˆ© closure sá¶œ :=
by rw [closure_compl, frontier, diff_eq]

/-- The complement of a set has the same frontier as the original set. -/
@[simp] lemma frontier_compl (s : set Î±) : frontier sá¶œ = frontier s :=
by simp only [frontier_eq_closure_inter_closure, compl_compl, inter_comm]

lemma frontier_inter_subset (s t : set Î±) :
  frontier (s âˆ© t) âŠ† (frontier s âˆ© closure t) âˆª (closure s âˆ© frontier t) :=
begin
  simp only [frontier_eq_closure_inter_closure, compl_inter, closure_union],
  convert inter_subset_inter_left _ (closure_inter_subset_inter_closure s t),
  simp only [inter_distrib_left, inter_distrib_right, inter_assoc],
  congr' 2,
  apply inter_comm
end

lemma frontier_union_subset (s t : set Î±) :
  frontier (s âˆª t) âŠ† (frontier s âˆ© closure tá¶œ) âˆª (closure sá¶œ âˆ© frontier t) :=
by simpa only [frontier_compl, â† compl_union]
  using frontier_inter_subset sá¶œ tá¶œ

lemma is_closed.frontier_eq {s : set Î±} (hs : is_closed s) : frontier s = s \ interior s :=
by rw [frontier, hs.closure_eq]

lemma is_open.frontier_eq {s : set Î±} (hs : is_open s) : frontier s = closure s \ s :=
by rw [frontier, interior_eq_of_open hs]

/-- The frontier of a set is closed. -/
lemma is_closed_frontier {s : set Î±} : is_closed (frontier s) :=
by rw frontier_eq_closure_inter_closure; exact is_closed_inter is_closed_closure is_closed_closure

/-- The frontier of a closed set has no interior point. -/
lemma interior_frontier {s : set Î±} (h : is_closed s) : interior (frontier s) = âˆ… :=
begin
  have A : frontier s = s \ interior s, from h.frontier_eq,
  have B : interior (frontier s) âŠ† interior s, by rw A; exact interior_mono (diff_subset _ _),
  have C : interior (frontier s) âŠ† frontier s := interior_subset,
  have : interior (frontier s) âŠ† (interior s) âˆ© (s \ interior s) :=
    subset_inter B (by simpa [A] using C),
  rwa [inter_diff_self, subset_empty_iff] at this,
end

/-!
###Â Neighborhoods
-/

/-- neighbourhood filter -/
def nhds (a : Î±) : filter Î± := (â¨… s âˆˆ {s : set Î± | a âˆˆ s âˆ§ is_open s}, ğ“Ÿ s)

localized "notation `ğ“` := nhds" in topological_space

lemma nhds_def (a : Î±) : ğ“ a = (â¨… s âˆˆ {s : set Î± | a âˆˆ s âˆ§ is_open s}, ğ“Ÿ s) := rfl

lemma nhds_basis_opens (a : Î±) : (ğ“ a).has_basis (Î» s : set Î±, a âˆˆ s âˆ§ is_open s) (Î» x, x) :=
has_basis_binfi_principal
  (Î» s âŸ¨has, hsâŸ© t âŸ¨hat, htâŸ©, âŸ¨s âˆ© t, âŸ¨âŸ¨has, hatâŸ©, is_open_inter hs htâŸ©,
    âŸ¨inter_subset_left _ _, inter_subset_right _ _âŸ©âŸ©)
  âŸ¨univ, âŸ¨mem_univ a, is_open_univâŸ©âŸ©

lemma le_nhds_iff {f a} : f â‰¤ ğ“ a â†” âˆ€ s : set Î±, a âˆˆ s â†’ is_open s â†’ s âˆˆ f :=
by simp [nhds_def]

lemma nhds_le_of_le {f a} {s : set Î±} (h : a âˆˆ s) (o : is_open s) (sf : ğ“Ÿ s â‰¤ f) : ğ“ a â‰¤ f :=
by rw nhds_def; exact infi_le_of_le s (infi_le_of_le âŸ¨h, oâŸ© sf)

lemma mem_nhds_sets_iff {a : Î±} {s : set Î±} :
  s âˆˆ ğ“ a â†” âˆƒtâŠ†s, is_open t âˆ§ a âˆˆ t :=
(nhds_basis_opens a).mem_iff.trans
  âŸ¨Î» âŸ¨t, âŸ¨hat, htâŸ©, htsâŸ©, âŸ¨t, hts, ht, hatâŸ©, Î» âŸ¨t, hts, ht, hatâŸ©, âŸ¨t, âŸ¨hat, htâŸ©, htsâŸ©âŸ©

lemma eventually_nhds_iff {a : Î±} {p : Î± â†’ Prop} :
  (âˆ€á¶  x in ğ“ a, p x) â†” âˆƒ (t : set Î±), (âˆ€ x âˆˆ t, p x) âˆ§ is_open t âˆ§ a âˆˆ t :=
mem_nhds_sets_iff.trans $ by simp only [subset_def, exists_prop, mem_set_of_eq]

lemma map_nhds {a : Î±} {f : Î± â†’ Î²} :
  map f (ğ“ a) = (â¨… s âˆˆ {s : set Î± | a âˆˆ s âˆ§ is_open s}, ğ“Ÿ (image f s)) :=
((nhds_basis_opens a).map f).eq_binfi

attribute [irreducible] nhds

lemma mem_of_nhds {a : Î±} {s : set Î±} : s âˆˆ ğ“ a â†’ a âˆˆ s :=
Î» H, let âŸ¨t, ht, _, hsâŸ© := mem_nhds_sets_iff.1 H in ht hs

lemma filter.eventually.self_of_nhds {p : Î± â†’ Prop} {a : Î±}
  (h : âˆ€á¶  y in ğ“ a, p y) : p a :=
mem_of_nhds h

lemma mem_nhds_sets {a : Î±} {s : set Î±} (hs : is_open s) (ha : a âˆˆ s) :
  s âˆˆ ğ“ a :=
mem_nhds_sets_iff.2 âŸ¨s, subset.refl _, hs, haâŸ©

lemma nhds_basis_opens' (a : Î±) : (ğ“ a).has_basis (Î» s : set Î±, s âˆˆ ğ“ a âˆ§ is_open s) (Î» x, x) :=
begin
  convert nhds_basis_opens a,
  ext s,
  split,
  { rintros âŸ¨s_in, s_opâŸ©,
    exact âŸ¨mem_of_nhds s_in, s_opâŸ© },
  { rintros âŸ¨a_in, s_opâŸ©,
    exact âŸ¨mem_nhds_sets s_op a_in, s_opâŸ© },
end

/-- If a predicate is true in a neighbourhood of `a`, then for `y` sufficiently close
to `a` this predicate is true in a neighbourhood of `y`. -/
lemma filter.eventually.eventually_nhds {p : Î± â†’ Prop} {a : Î±} (h : âˆ€á¶  y in ğ“ a, p y) :
  âˆ€á¶  y in ğ“ a, âˆ€á¶  x in ğ“ y, p x :=
let âŸ¨t, htp, hto, haâŸ© := eventually_nhds_iff.1 h in
eventually_nhds_iff.2 âŸ¨t, Î» x hx, eventually_nhds_iff.2 âŸ¨t, htp, hto, hxâŸ©, hto, haâŸ©

@[simp] lemma eventually_eventually_nhds {p : Î± â†’ Prop} {a : Î±} :
  (âˆ€á¶  y in ğ“ a, âˆ€á¶  x in ğ“ y, p x) â†” âˆ€á¶  x in ğ“ a, p x :=
âŸ¨Î» h, h.self_of_nhds, Î» h, h.eventually_nhdsâŸ©

@[simp] lemma nhds_bind_nhds : (ğ“ a).bind ğ“ = ğ“ a := filter.ext $ Î» s, eventually_eventually_nhds

@[simp] lemma eventually_eventually_eq_nhds {f g : Î± â†’ Î²} {a : Î±} :
  (âˆ€á¶  y in ğ“ a, f =á¶ [ğ“ y] g) â†” f =á¶ [ğ“ a] g :=
eventually_eventually_nhds

lemma filter.eventually_eq.eq_of_nhds {f g : Î± â†’ Î²} {a : Î±} (h : f =á¶ [ğ“ a] g) : f a = g a :=
let âŸ¨u, hu, HâŸ© := h.exists_mem in H _ (mem_of_nhds hu)

@[simp] lemma eventually_eventually_le_nhds [has_le Î²] {f g : Î± â†’ Î²} {a : Î±} :
  (âˆ€á¶  y in ğ“ a, f â‰¤á¶ [ğ“ y] g) â†” f â‰¤á¶ [ğ“ a] g :=
eventually_eventually_nhds

/-- If two functions are equal in a neighbourhood of `a`, then for `y` sufficiently close
to `a` these functions are equal in a neighbourhood of `y`. -/
lemma filter.eventually_eq.eventually_eq_nhds {f g : Î± â†’ Î²} {a : Î±} (h : f =á¶ [ğ“ a] g) :
  âˆ€á¶  y in ğ“ a, f =á¶ [ğ“ y] g :=
h.eventually_nhds

/-- If `f x â‰¤ g x` in a neighbourhood of `a`, then for `y` sufficiently close to `a` we have
`f x â‰¤ g x` in a neighbourhood of `y`. -/
lemma filter.eventually_le.eventually_le_nhds [has_le Î²] {f g : Î± â†’ Î²} {a : Î±} (h : f â‰¤á¶ [ğ“ a] g) :
  âˆ€á¶  y in ğ“ a, f â‰¤á¶ [ğ“ y] g :=
h.eventually_nhds

theorem all_mem_nhds (x : Î±) (P : set Î± â†’ Prop) (hP : âˆ€ s t, s âŠ† t â†’ P s â†’ P t) :
  (âˆ€ s âˆˆ ğ“ x, P s) â†” (âˆ€ s, is_open s â†’ x âˆˆ s â†’ P s) :=
((nhds_basis_opens x).forall_iff hP).trans $ by simp only [and_comm (x âˆˆ _), and_imp]

theorem all_mem_nhds_filter (x : Î±) (f : set Î± â†’ set Î²) (hf : âˆ€ s t, s âŠ† t â†’ f s âŠ† f t)
    (l : filter Î²) :
  (âˆ€ s âˆˆ ğ“ x, f s âˆˆ l) â†” (âˆ€ s, is_open s â†’ x âˆˆ s â†’ f s âˆˆ l) :=
all_mem_nhds _ _ (Î» s t ssubt h, mem_sets_of_superset h (hf s t ssubt))

theorem rtendsto_nhds {r : rel Î² Î±} {l : filter Î²} {a : Î±} :
  rtendsto r l (ğ“ a) â†” (âˆ€ s, is_open s â†’ a âˆˆ s â†’ r.core s âˆˆ l) :=
all_mem_nhds_filter _ _ (Î» s t, id) _

theorem rtendsto'_nhds {r : rel Î² Î±} {l : filter Î²} {a : Î±} :
  rtendsto' r l (ğ“ a) â†” (âˆ€ s, is_open s â†’ a âˆˆ s â†’ r.preimage s âˆˆ l) :=
by { rw [rtendsto'_def], apply all_mem_nhds_filter, apply rel.preimage_mono }

theorem ptendsto_nhds {f : Î² â†’. Î±} {l : filter Î²} {a : Î±} :
  ptendsto f l (ğ“ a) â†” (âˆ€ s, is_open s â†’ a âˆˆ s â†’ f.core s âˆˆ l) :=
rtendsto_nhds

theorem ptendsto'_nhds {f : Î² â†’. Î±} {l : filter Î²} {a : Î±} :
  ptendsto' f l (ğ“ a) â†” (âˆ€ s, is_open s â†’ a âˆˆ s â†’ f.preimage s âˆˆ l) :=
rtendsto'_nhds

theorem tendsto_nhds {f : Î² â†’ Î±} {l : filter Î²} {a : Î±} :
  tendsto f l (ğ“ a) â†” (âˆ€ s, is_open s â†’ a âˆˆ s â†’ f â»Â¹' s âˆˆ l) :=
all_mem_nhds_filter _ _ (Î» s t h, preimage_mono h) _

lemma tendsto_const_nhds {a : Î±} {f : filter Î²} : tendsto (Î»b:Î², a) f (ğ“ a) :=
tendsto_nhds.mpr $ assume s hs ha, univ_mem_sets' $ assume _, ha

lemma pure_le_nhds : pure â‰¤ (ğ“ : Î± â†’ filter Î±) :=
assume a s hs, mem_pure_sets.2 $ mem_of_nhds hs

lemma tendsto_pure_nhds {Î± : Type*} [topological_space Î²] (f : Î± â†’ Î²) (a : Î±) :
  tendsto f (pure a) (ğ“ (f a)) :=
tendsto_le_right (pure_le_nhds _) (tendsto_pure_pure f a)

lemma order_top.tendsto_at_top {Î± : Type*} [order_top Î±] [topological_space Î²] (f : Î± â†’ Î²) :
  tendsto f at_top (ğ“ $ f âŠ¤) :=
tendsto_le_right (pure_le_nhds _) $ tendsto_at_top_pure f

@[simp] instance nhds_ne_bot {a : Î±} : ne_bot (ğ“ a) :=
ne_bot_of_le (pure_le_nhds a)

/-!
### Cluster points

In this section we define [cluster points](https://en.wikipedia.org/wiki/Limit_point)
(also known as limit points and accumulation points) of a filter and of a sequence.
-/

/-- A point `x` is a cluster point of a filter `F` if ğ“ x âŠ“ F â‰  âŠ¥. Also known as
an accumulation point or a limit point. -/
def cluster_pt (x : Î±) (F : filter Î±) : Prop := ne_bot (ğ“ x âŠ“ F)

lemma cluster_pt.ne_bot {x : Î±} {F : filter Î±} (h : cluster_pt x F) : ne_bot (ğ“ x âŠ“ F) := h

lemma cluster_pt_iff {x : Î±} {F : filter Î±} :
  cluster_pt x F â†” âˆ€ {U V : set Î±}, U âˆˆ ğ“ x â†’ V âˆˆ F â†’ (U âˆ© V).nonempty :=
inf_ne_bot_iff

/-- `x` is a cluster point of a set `s` if every neighbourhood of `x` meets `s` on a nonempty
set. -/
lemma cluster_pt_principal_iff {x : Î±} {s : set Î±} :
  cluster_pt x (ğ“Ÿ s) â†” âˆ€ U âˆˆ ğ“ x, (U âˆ© s).nonempty :=
inf_principal_ne_bot_iff

lemma cluster_pt.of_le_nhds {x : Î±} {f : filter Î±} (H : f â‰¤ ğ“ x) [ne_bot f] : cluster_pt x f :=
by rwa [cluster_pt, inf_eq_right.mpr H]

lemma cluster_pt.of_le_nhds' {x : Î±} {f : filter Î±} (H : f â‰¤ ğ“ x) (hf : ne_bot f) :
  cluster_pt x f :=
cluster_pt.of_le_nhds H

lemma cluster_pt.of_nhds_le {x : Î±} {f : filter Î±} (H : ğ“ x â‰¤ f) : cluster_pt x f :=
by simp only [cluster_pt, inf_eq_left.mpr H, nhds_ne_bot]

lemma cluster_pt.mono {x : Î±} {f g : filter Î±} (H : cluster_pt x f) (h : f â‰¤ g) :
  cluster_pt x g :=
ne_bot_of_le_ne_bot H $ inf_le_inf_left _ h

lemma cluster_pt.of_inf_left {x : Î±} {f g : filter Î±} (H : cluster_pt x $ f âŠ“ g) :
  cluster_pt x f :=
H.mono inf_le_left

lemma cluster_pt.of_inf_right {x : Î±} {f g : filter Î±} (H : cluster_pt x $ f âŠ“ g) :
  cluster_pt x g :=
H.mono inf_le_right

/-- A point `x` is a cluster point of a sequence `u` along a filter `F` if it is a cluster point
of `map u F`. -/
def map_cluster_pt {Î¹ :Type*} (x : Î±) (F : filter Î¹) (u : Î¹ â†’ Î±) : Prop := cluster_pt x (map u F)

lemma map_cluster_pt_iff {Î¹ :Type*} (x : Î±) (F : filter Î¹) (u : Î¹ â†’ Î±) :
  map_cluster_pt x F u â†” âˆ€ s âˆˆ ğ“ x, âˆƒá¶  a in F, u a âˆˆ s :=
by { simp_rw [map_cluster_pt, cluster_pt, inf_ne_bot_iff_frequently_left, frequently_map], refl }

lemma map_cluster_pt_of_comp {Î¹ Î´ :Type*} {F : filter Î¹} {Ï† : Î´ â†’ Î¹} {p : filter Î´}
  {x : Î±} {u : Î¹ â†’ Î±} [ne_bot p] (h : tendsto Ï† p F) (H : tendsto (u âˆ˜ Ï†) p (ğ“ x)) :
  map_cluster_pt x F u :=
begin
  have := calc
  map (u âˆ˜ Ï†) p = map u (map Ï† p) : map_map
  ... â‰¤ map u F : map_mono h,
  have : map (u âˆ˜ Ï†) p â‰¤ ğ“ x âŠ“ map u F,
    from le_inf H this,
  exact ne_bot_of_le this
end

/-!
### Interior, closure and frontier in terms of neighborhoods
-/

lemma interior_eq_nhds {s : set Î±} : interior s = {a | ğ“ a â‰¤ ğ“Ÿ s} :=
set.ext $ Î» x, by simp only [mem_interior, le_principal_iff, mem_nhds_sets_iff]; refl

lemma mem_interior_iff_mem_nhds {s : set Î±} {a : Î±} :
  a âˆˆ interior s â†” s âˆˆ ğ“ a :=
by simp only [interior_eq_nhds, le_principal_iff]; refl

lemma subset_interior_iff_nhds {s V : set Î±} : s âŠ† interior V â†” âˆ€ x âˆˆ s, V âˆˆ ğ“ x :=
show (âˆ€ x, x âˆˆ s â†’  x âˆˆ _) â†” _, by simp_rw mem_interior_iff_mem_nhds

lemma is_open_iff_nhds {s : set Î±} : is_open s â†” âˆ€aâˆˆs, ğ“ a â‰¤ ğ“Ÿ s :=
calc is_open s â†” s âŠ† interior s : subset_interior_iff_open.symm
  ... â†” (âˆ€aâˆˆs, ğ“ a â‰¤ ğ“Ÿ s) : by rw [interior_eq_nhds]; refl

lemma is_open_iff_mem_nhds {s : set Î±} : is_open s â†” âˆ€aâˆˆs, s âˆˆ ğ“ a :=
is_open_iff_nhds.trans $ forall_congr $ Î» _, imp_congr_right $ Î» _, le_principal_iff

lemma closure_eq_cluster_pts {s : set Î±} : closure s = {a | cluster_pt a (ğ“Ÿ s)} :=
calc closure s = (interior sá¶œ)á¶œ : closure_eq_compl_interior_compl
  ... = {a | Â¬ ğ“ a â‰¤ ğ“Ÿ sá¶œ} : by rw [interior_eq_nhds]; refl
  ... = {a | cluster_pt a (ğ“Ÿ s)} : set.ext $ assume a, not_congr
    (is_compl_principal s).inf_left_eq_bot_iff.symm

theorem mem_closure_iff_cluster_pt {s : set Î±} {a : Î±} : a âˆˆ closure s â†” cluster_pt a (ğ“Ÿ s) :=
by simp only [closure_eq_cluster_pts, mem_set_of_eq]

theorem mem_closure_iff_nhds {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆ€ t âˆˆ ğ“ a, (t âˆ© s).nonempty :=
mem_closure_iff_cluster_pt.trans cluster_pt_principal_iff

theorem mem_closure_iff_nhds' {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆ€ t âˆˆ ğ“ a, âˆƒ y : s, â†‘y âˆˆ t :=
by simp only [mem_closure_iff_nhds, set.nonempty_inter_iff_exists_right]

theorem mem_closure_iff_comap_ne_bot {A : set Î±} {x : Î±} :
  x âˆˆ closure A â†” ne_bot (comap (coe : A â†’ Î±) (ğ“ x)) :=
by simp_rw [mem_closure_iff_nhds, comap_ne_bot_iff, set.nonempty_inter_iff_exists_right]

theorem mem_closure_iff_nhds_basis {a : Î±} {p : Î² â†’ Prop} {s : Î² â†’ set Î±} (h : (ğ“ a).has_basis p s)
  {t : set Î±} :
  a âˆˆ closure t â†” âˆ€ i, p i â†’ âˆƒ y âˆˆ t, y âˆˆ s i :=
mem_closure_iff_nhds.trans
  âŸ¨Î» H i hi, let âŸ¨x, hxâŸ© := (H _ $ h.mem_of_mem hi) in âŸ¨x, hx.2, hx.1âŸ©,
    Î» H t' ht', let âŸ¨i, hi, hitâŸ© := h.mem_iff.1 ht', âŸ¨x, xt, hxâŸ© := H i hi in
    âŸ¨x, hit hx, xtâŸ©âŸ©

/-- `x` belongs to the closure of `s` if and only if some ultrafilter
  supported on `s` converges to `x`. -/
lemma mem_closure_iff_ultrafilter {s : set Î±} {x : Î±} :
  x âˆˆ closure s â†” âˆƒ (u : ultrafilter Î±), s âˆˆ u.val âˆ§ u.val â‰¤ ğ“ x :=
begin
  rw closure_eq_cluster_pts, change cluster_pt x (ğ“Ÿ s) â†” _, symmetry,
  convert exists_ultrafilter_iff _, ext u,
  rw [â†le_principal_iff, inf_comm, le_inf_iff]
end

lemma is_closed_iff_cluster_pt {s : set Î±} : is_closed s â†” âˆ€a, cluster_pt a (ğ“Ÿ s) â†’ a âˆˆ s :=
calc is_closed s â†” closure s âŠ† s : closure_subset_iff_is_closed.symm
  ... â†” (âˆ€a, cluster_pt a (ğ“Ÿ s) â†’ a âˆˆ s) : by simp only [subset_def, mem_closure_iff_cluster_pt]

lemma closure_inter_open {s t : set Î±} (h : is_open s) : s âˆ© closure t âŠ† closure (s âˆ© t) :=
assume a âŸ¨hs, htâŸ©,
have s âˆˆ ğ“ a, from mem_nhds_sets h hs,
have ğ“ a âŠ“ ğ“Ÿ s = ğ“ a, by rwa [inf_eq_left, le_principal_iff],
have cluster_pt a (ğ“Ÿ (s âˆ© t)),
  from calc ğ“ a âŠ“ ğ“Ÿ (s âˆ© t) = ğ“ a âŠ“ (ğ“Ÿ s âŠ“ ğ“Ÿ t) : by rw inf_principal
    ... = ğ“ a âŠ“ ğ“Ÿ t : by rw [â†inf_assoc, this]
    ... â‰  âŠ¥ : by rw [closure_eq_cluster_pts] at ht; assumption,
by rwa [closure_eq_cluster_pts]

lemma dense_inter_of_open_left {s t : set Î±} (hs : closure s = univ) (ht : closure t = univ)
  (hso : is_open s) :
  closure (s âˆ© t) = univ :=
eq_univ_of_subset (closure_minimal (closure_inter_open hso) is_closed_closure) $
  by simp only [*, inter_univ]

lemma dense_inter_of_open_right {s t : set Î±} (hs : closure s = univ) (ht : closure t = univ)
  (hto : is_open t) :
  closure (s âˆ© t) = univ :=
inter_comm t s â–¸ dense_inter_of_open_left ht hs hto

lemma closure_diff {s t : set Î±} : closure s \ closure t âŠ† closure (s \ t) :=
calc closure s \ closure t = (closure t)á¶œ âˆ© closure s : by simp only [diff_eq, inter_comm]
  ... âŠ† closure ((closure t)á¶œ âˆ© s) : closure_inter_open $ is_open_compl_iff.mpr $ is_closed_closure
  ... = closure (s \ closure t) : by simp only [diff_eq, inter_comm]
  ... âŠ† closure (s \ t) : closure_mono $ diff_subset_diff (subset.refl s) subset_closure

lemma mem_of_closed_of_tendsto {f : Î² â†’ Î±} {b : filter Î²} {a : Î±} {s : set Î±}
  [ne_bot b] (hf : tendsto f b (ğ“ a)) (hs : is_closed s) (h : f â»Â¹' s âˆˆ b) : a âˆˆ s :=
is_closed_iff_cluster_pt.mp hs a $ ne_bot_of_le $ le_inf hf (le_principal_iff.mpr h)

lemma mem_of_closed_of_tendsto' {f : Î² â†’ Î±} {x : filter Î²} {a : Î±} {s : set Î±}
  (hf : tendsto f x (ğ“ a)) (hs : is_closed s) [ne_bot (x âŠ“ ğ“Ÿ (f â»Â¹' s))] : a âˆˆ s :=
have tendsto f (x âŠ“ ğ“Ÿ (f â»Â¹' s)) (ğ“ a) := tendsto_inf_left hf,
mem_of_closed_of_tendsto this hs $ mem_inf_sets_of_right $ mem_principal_self _

lemma mem_closure_of_tendsto {f : Î² â†’ Î±} {b : filter Î²} {a : Î±} {s : set Î±}
  [ne_bot b] (hf : tendsto f b (ğ“ a)) (h : âˆ€á¶  x in b, f x âˆˆ s) : a âˆˆ closure s :=
mem_of_closed_of_tendsto hf is_closed_closure $
  filter.mem_sets_of_superset h (preimage_mono subset_closure)

/-- Suppose that `f` sends the complement to `s` to a single point `a`, and `l` is some filter.
Then `f` tends to `a` along `l` restricted to `s` if and only it tends to `a` along `l`. -/
lemma tendsto_inf_principal_nhds_iff_of_forall_eq {f : Î² â†’ Î±} {l : filter Î²} {s : set Î²}
  {a : Î±} (h : âˆ€ x âˆ‰ s, f x = a) :
  tendsto f (l âŠ“ ğ“Ÿ s) (ğ“ a) â†” tendsto f l (ğ“ a) :=
begin
  rw [tendsto_iff_comap, tendsto_iff_comap],
  replace h : ğ“Ÿ sá¶œ â‰¤ comap f (ğ“ a),
  { rintros U âŸ¨t, ht, htUâŸ© x hx,
    have : f x âˆˆ t, from (h x hx).symm â–¸ mem_of_nhds ht,
    exact htU this },
  refine âŸ¨Î» h', _, le_trans inf_le_leftâŸ©,
  have := sup_le h' h,
  rw [sup_inf_right, sup_principal, union_compl_self, principal_univ,
    inf_top_eq, sup_le_iff] at this,
  exact this.1
end

/-!
### Limits of filters in topological spaces
-/

section lim

/-- If `f` is a filter, then `Lim f` is a limit of the filter, if it exists. -/
noncomputable def Lim [nonempty Î±] (f : filter Î±) : Î± := epsilon $ Î»a, f â‰¤ ğ“ a

/-- If `f` is a filter in `Î²` and `g : Î² â†’ Î±` is a function, then `lim f` is a limit of `g` at `f`,
if it exists. -/
noncomputable def lim [nonempty Î±] (f : filter Î²) (g : Î² â†’ Î±) : Î± :=
Lim (f.map g)

/-- If a filter `f` is majorated by some `ğ“ a`, then it is majorated by `ğ“ (Lim f)`. We formulate
this lemma with a `[nonempty Î±]` argument of `Lim` derived from `h` to make it useful for types
without a `[nonempty Î±]` instance. Because of the built-in proof irrelevance, Lean will unify
this instance with any other instance. -/
lemma Lim_spec {f : filter Î±} (h : âˆƒa, f â‰¤ ğ“ a) : f â‰¤ ğ“ (@Lim _ _ (nonempty_of_exists h) f) :=
epsilon_spec h

/-- If `g` tends to some `ğ“ a` along `f`, then it tends to `ğ“ (lim f g)`. We formulate
this lemma with a `[nonempty Î±]` argument of `lim` derived from `h` to make it useful for types
without a `[nonempty Î±]` instance. Because of the built-in proof irrelevance, Lean will unify
this instance with any other instance. -/
lemma lim_spec {f : filter Î²} {g : Î² â†’ Î±} (h : âˆƒ a, tendsto g f (ğ“ a)) :
  tendsto g f (ğ“ $ @lim _ _ _ (nonempty_of_exists h) f g) :=
Lim_spec h

end lim

/-!
###Â Locally finite families
-/

/- locally finite family [General Topology (Bourbaki, 1995)] -/
section locally_finite

/-- A family of sets in `set Î±` is locally finite if at every point `x:Î±`,
  there is a neighborhood of `x` which meets only finitely many sets in the family -/
def locally_finite (f : Î² â†’ set Î±) :=
âˆ€x:Î±, âˆƒt âˆˆ ğ“ x, finite {i | (f i âˆ© t).nonempty }

lemma locally_finite_of_finite {f : Î² â†’ set Î±} (h : finite (univ : set Î²)) : locally_finite f :=
assume x, âŸ¨univ, univ_mem_sets, h.subset $ subset_univ _âŸ©

lemma locally_finite_subset
  {fâ‚ fâ‚‚ : Î² â†’ set Î±} (hfâ‚‚ : locally_finite fâ‚‚) (hf : âˆ€b, fâ‚ b âŠ† fâ‚‚ b) : locally_finite fâ‚ :=
assume a,
let âŸ¨t, htâ‚, htâ‚‚âŸ© := hfâ‚‚ a in
âŸ¨t, htâ‚, htâ‚‚.subset $ assume i hi, hi.mono $ inter_subset_inter (hf i) $ subset.refl _âŸ©

lemma is_closed_Union_of_locally_finite {f : Î² â†’ set Î±}
  (hâ‚ : locally_finite f) (hâ‚‚ : âˆ€i, is_closed (f i)) : is_closed (â‹ƒi, f i) :=
is_open_iff_nhds.mpr $ assume a, assume h : a âˆ‰ (â‹ƒi, f i),
  have âˆ€i, a âˆˆ (f i)á¶œ,
    from assume i hi, h $ mem_Union.2 âŸ¨i, hiâŸ©,
  have âˆ€i, (f i)á¶œ âˆˆ (ğ“ a),
    by simp only [mem_nhds_sets_iff]; exact assume i, âŸ¨(f i)á¶œ, subset.refl _, hâ‚‚ i, this iâŸ©,
  let âŸ¨t, h_sets, (h_fin : finite {i | (f i âˆ© t).nonempty })âŸ© := hâ‚ a in

  calc ğ“ a â‰¤ ğ“Ÿ (t âˆ© (â‹‚ iâˆˆ{i | (f i âˆ© t).nonempty }, (f i)á¶œ)) :
  begin
    rw [le_principal_iff],
    apply @filter.inter_mem_sets _ (ğ“ a) _ _ h_sets,
    apply @filter.Inter_mem_sets _ (ğ“ a) _ _ _ h_fin,
    exact assume i h, this i
  end
  ... â‰¤ ğ“Ÿ (â‹ƒi, f i)á¶œ :
  begin
    simp only [principal_mono, subset_def, mem_compl_eq, mem_inter_eq,
      mem_Inter, mem_set_of_eq, mem_Union, and_imp, not_exists,
      exists_imp_distrib, ne_empty_iff_nonempty, set.nonempty],
    exact assume x xt ht i xfi, ht i x xfi xt xfi
  end

end locally_finite

end topological_space

/-!
### Continuity
-/

section continuous
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}
variables [topological_space Î±] [topological_space Î²] [topological_space Î³]
open_locale topological_space

/-- A function between topological spaces is continuous if the preimage
  of every open set is open. -/
def continuous (f : Î± â†’ Î²) := âˆ€s, is_open s â†’ is_open (f â»Â¹' s)

lemma is_open.preimage {f : Î± â†’ Î²} (hf : continuous f) {s : set Î²} (h : is_open s) :
  is_open (f â»Â¹' s) :=
hf s h

/-- A function between topological spaces is continuous at a point `xâ‚€`
if `f x` tends to `f xâ‚€` when `x` tends to `xâ‚€`. -/
def continuous_at (f : Î± â†’ Î²) (x : Î±) := tendsto f (ğ“ x) (ğ“ (f x))

lemma continuous_at.tendsto {f : Î± â†’ Î²} {x : Î±} (h : continuous_at f x) :
  tendsto f (ğ“ x) (ğ“ (f x)) :=
h

lemma continuous_at.preimage_mem_nhds {f : Î± â†’ Î²} {x : Î±} {t : set Î²} (h : continuous_at f x)
  (ht : t âˆˆ ğ“ (f x)) : f â»Â¹' t âˆˆ ğ“ x :=
h ht

lemma preimage_interior_subset_interior_preimage {f : Î± â†’ Î²} {s : set Î²}
  (hf : continuous f) : fâ»Â¹' (interior s) âŠ† interior (fâ»Â¹' s) :=
interior_maximal (preimage_mono interior_subset) (hf _ is_open_interior)

lemma continuous_id : continuous (id : Î± â†’ Î±) :=
assume s h, h

lemma continuous.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : continuous g) (hf : continuous f) :
  continuous (g âˆ˜ f) :=
assume s h, hf _ (hg s h)

lemma continuous.iterate {f : Î± â†’ Î±} (h : continuous f) (n : â„•) : continuous (f^[n]) :=
nat.rec_on n continuous_id (Î» n ihn, ihn.comp h)

lemma continuous_at.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} {x : Î±}
  (hg : continuous_at g (f x)) (hf : continuous_at f x) :
  continuous_at (g âˆ˜ f) x :=
hg.comp hf

lemma continuous.tendsto {f : Î± â†’ Î²} (hf : continuous f) (x) :
  tendsto f (ğ“ x) (ğ“ (f x)) :=
((nhds_basis_opens x).tendsto_iff $ nhds_basis_opens $ f x).2 $
  Î» t âŸ¨hxt, htâŸ©, âŸ¨f â»Â¹' t, âŸ¨hxt, hf _ htâŸ©, subset.refl _âŸ©

lemma continuous.continuous_at {f : Î± â†’ Î²} {x : Î±} (h : continuous f) :
  continuous_at f x :=
h.tendsto x

lemma continuous_iff_continuous_at {f : Î± â†’ Î²} : continuous f â†” âˆ€ x, continuous_at f x :=
âŸ¨continuous.tendsto,
  assume hf : âˆ€x, tendsto f (ğ“ x) (ğ“ (f x)),
  assume s, assume hs : is_open s,
  have âˆ€a, f a âˆˆ s â†’ s âˆˆ ğ“ (f a),
    from Î» a ha, mem_nhds_sets hs ha,
  show is_open (f â»Â¹' s),
    from is_open_iff_nhds.2 $ Î» a ha, le_principal_iff.2 $ hf _ (this a ha)âŸ©

lemma continuous_const {b : Î²} : continuous (Î»a:Î±, b) :=
continuous_iff_continuous_at.mpr $ assume a, tendsto_const_nhds

lemma continuous_at_const {x : Î±} {b : Î²} : continuous_at (Î» a:Î±, b) x :=
continuous_const.continuous_at

lemma continuous_at_id {x : Î±} : continuous_at id x :=
continuous_id.continuous_at

lemma continuous_at.iterate {f : Î± â†’ Î±} {x : Î±} (hf : continuous_at f x) (hx : f x = x) (n : â„•) :
  continuous_at (f^[n]) x :=
nat.rec_on n continuous_at_id $ Î» n ihn,
show continuous_at (f^[n] âˆ˜ f) x,
from continuous_at.comp (hx.symm â–¸ ihn) hf

lemma continuous_iff_is_closed {f : Î± â†’ Î²} :
  continuous f â†” (âˆ€s, is_closed s â†’ is_closed (f â»Â¹' s)) :=
âŸ¨assume hf s hs, hf sá¶œ hs,
  assume hf s, by rw [â†is_closed_compl_iff, â†is_closed_compl_iff]; exact hf _âŸ©

lemma is_closed.preimage {f : Î± â†’ Î²} (hf : continuous f) {s : set Î²} (h : is_closed s) :
  is_closed (f â»Â¹' s) :=
continuous_iff_is_closed.mp hf s h

lemma continuous_at_iff_ultrafilter {f : Î± â†’ Î²} (x) : continuous_at f x â†”
  âˆ€ g, is_ultrafilter g â†’ g â‰¤ ğ“ x â†’ g.map f â‰¤ ğ“ (f x) :=
tendsto_iff_ultrafilter f (ğ“ x) (ğ“ (f x))

lemma continuous_iff_ultrafilter {f : Î± â†’ Î²} :
  continuous f â†” âˆ€ x g, is_ultrafilter g â†’ g â‰¤ ğ“ x â†’ g.map f â‰¤ ğ“ (f x) :=
by simp only [continuous_iff_continuous_at, continuous_at_iff_ultrafilter]

/-- A piecewise defined function `if p then f else g` is continuous, if both `f` and `g`
are continuous, and they coincide on the frontier (boundary) of the set `{a | p a}`. -/
lemma continuous_if {p : Î± â†’ Prop} {f g : Î± â†’ Î²} {h : âˆ€a, decidable (p a)}
  (hp : âˆ€aâˆˆfrontier {a | p a}, f a = g a) (hf : continuous f) (hg : continuous g) :
  continuous (Î»a, @ite (p a) (h a) Î² (f a) (g a)) :=
continuous_iff_is_closed.mpr $
assume s hs,
have (Î»a, ite (p a) (f a) (g a)) â»Â¹' s =
    (closure {a | p a} âˆ©  f â»Â¹' s) âˆª (closure {a | Â¬ p a} âˆ© g â»Â¹' s),
  from set.ext $ assume a,
  classical.by_cases
    (assume : a âˆˆ frontier {a | p a},
      have hac : a âˆˆ closure {a | p a}, from this.left,
      have hai : a âˆˆ closure {a | Â¬ p a},
        from have a âˆˆ (interior {a | p a})á¶œ, from this.right, by rwa [â†closure_compl] at this,
      by by_cases p a; simp [h, hp a this, hac, hai, iff_def] {contextual := tt})
    (assume hf : a âˆˆ (frontier {a | p a})á¶œ,
      classical.by_cases
        (assume : p a,
          have hc : a âˆˆ closure {a | p a}, from subset_closure this,
          have hnc : a âˆ‰ closure {a | Â¬ p a},
            by show a âˆ‰ closure {a | p a}á¶œ; rw [closure_compl]; simpa [frontier, hc] using hf,
          by simp [this, hc, hnc])
        (assume : Â¬ p a,
          have hc : a âˆˆ closure {a | Â¬ p a}, from subset_closure this,
          have hnc : a âˆ‰ closure {a | p a},
            begin
              have hc : a âˆˆ closure {a | p a}á¶œ, from hc,
              simp [closure_compl] at hc,
              simpa [frontier, hc] using hf
            end,
          by simp [this, hc, hnc])),
by rw [this]; exact is_closed_union
  (is_closed_inter is_closed_closure $ continuous_iff_is_closed.mp hf s hs)
  (is_closed_inter is_closed_closure $ continuous_iff_is_closed.mp hg s hs)


/- Continuity and partial functions -/

/-- Continuity of a partial function -/
def pcontinuous (f : Î± â†’. Î²) := âˆ€ s, is_open s â†’ is_open (f.preimage s)

lemma open_dom_of_pcontinuous {f : Î± â†’. Î²} (h : pcontinuous f) : is_open f.dom :=
by rw [â†pfun.preimage_univ]; exact h _ is_open_univ

lemma pcontinuous_iff' {f : Î± â†’. Î²} :
  pcontinuous f â†” âˆ€ {x y} (h : y âˆˆ f x), ptendsto' f (ğ“ x) (ğ“ y) :=
begin
  split,
  { intros h x y h',
    simp only [ptendsto'_def, mem_nhds_sets_iff],
    rintros s âŸ¨t, tsubs, opent, ytâŸ©,
    exact âŸ¨f.preimage t, pfun.preimage_mono _ tsubs, h _ opent, âŸ¨y, yt, h'âŸ©âŸ©
  },
  intros hf s os,
  rw is_open_iff_nhds,
  rintros x âŸ¨y, ys, fxyâŸ© t,
  rw [mem_principal_sets],
  assume h : f.preimage s âŠ† t,
  change t âˆˆ ğ“ x,
  apply mem_sets_of_superset _ h,
  have h' : âˆ€ s âˆˆ ğ“ y, f.preimage s âˆˆ ğ“ x,
  { intros s hs,
     have : ptendsto' f (ğ“ x) (ğ“ y) := hf fxy,
     rw ptendsto'_def at this,
     exact this s hs },
  show f.preimage s âˆˆ ğ“ x,
  apply h', rw mem_nhds_sets_iff, exact âŸ¨s, set.subset.refl _, os, ysâŸ©
end

lemma image_closure_subset_closure_image {f : Î± â†’ Î²} {s : set Î±} (h : continuous f) :
  f '' closure s âŠ† closure (f '' s) :=
have âˆ€ (a : Î±), cluster_pt a (ğ“Ÿ s) â†’ cluster_pt (f a) (ğ“Ÿ (f '' s)),
  from assume a ha,
  have hâ‚ : Â¬ map f (ğ“ a âŠ“ ğ“Ÿ s) = âŠ¥,
    by rwa[map_eq_bot_iff],
  have hâ‚‚ : map f (ğ“ a âŠ“ ğ“Ÿ s) â‰¤ ğ“ (f a) âŠ“ ğ“Ÿ (f '' s),
    from le_inf
      (le_trans (map_mono inf_le_left) $ by rw [continuous_iff_continuous_at] at h; exact h a)
      (le_trans (map_mono inf_le_right) $ by simp [subset_preimage_image] ),
  ne_bot_of_le_ne_bot hâ‚ hâ‚‚,
by simp [image_subset_iff, closure_eq_cluster_pts]; assumption

lemma mem_closure {s : set Î±} {t : set Î²} {f : Î± â†’ Î²} {a : Î±}
  (hf : continuous f) (ha : a âˆˆ closure s) (ht : âˆ€aâˆˆs, f a âˆˆ t) : f a âˆˆ closure t :=
subset.trans (image_closure_subset_closure_image hf) (closure_mono $ image_subset_iff.2 ht) $
  (mem_image_of_mem f ha)

end continuous
