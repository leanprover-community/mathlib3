/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import order.filter.cofinite

/-!
# Basic theory of bornology

We develop the basic theory of bornologies. Instead of axiomatizing bounded sets and defining
bornologies in terms of those, we recognize that the cobounded sets form a filter and define a
bornology as a filter of cobounded sets which contains the cofinite filter.  This allows us to make
use of the extensive library for filters, but we also provide the relevant connecting results for
bounded sets.

The specification of a bornology in terms of the cobounded filter is equivalent to the standard
one (e.g., see [Bourbaki, *Topological Vector Spaces*][bourbaki1987], **covering bornology**, now
often called simply **bornology**) in terms of bounded sets (see `bornology.of_bounded`,
`is_bounded.union`, `is_bounded.subset`), except that we do not allow the empty bornology (that is,
we require that *some* set must be bounded; equivalently, `âˆ…` is bounded). In the literature the
cobounded filter is generally referred to as the *filter at infinity*.

## Main definitions

- `bornology Î±`: a class consisting of `cobounded : filter Î±` and a proof that this filter
  contains the `cofinite` filter.
- `bornology.is_cobounded`: the predicate that a set is a member of the `cobounded Î±` filter. For
  `s : set Î±`, one should prefer `bornology.is_cobounded s` over `s âˆˆ cobounded Î±`.
- `bornology.is_bounded`: the predicate that states a set is bounded (i.e., the complement of a
  cobounded set). One should prefer `bornology.is_bounded s` over `sá¶œ âˆˆ cobounded Î±`.
- `bounded_space Î±`: a class extending `bornology Î±` with the condition
  `bornology.is_bounded (set.univ : set Î±)`

Although use of `cobounded Î±` is discouraged for indicating the (co)boundedness of individual sets,
it is intended for regular use as a filter on `Î±`.
-/

open set filter
open_locale filter

variables {Î¹ Î± Î² : Type*}

/-- A **bornology** on a type `Î±` is a filter of cobounded sets which contains the cofinite filter.
Such spaces are equivalently specified by their bounded sets, see `bornology.of_bounded`
and `bornology.ext_iff_is_bounded`-/
@[ext]
class bornology (Î± : Type*) :=
(cobounded [] : filter Î±)
(le_cofinite [] : cobounded â‰¤ cofinite)

/-- A constructor for bornologies by specifying the bounded sets,
and showing that they satisfy the appropriate conditions. -/
@[simps]
def bornology.of_bounded {Î± : Type*} (B : set (set Î±))
  (empty_mem : âˆ… âˆˆ B) (subset_mem : âˆ€ sâ‚ âˆˆ B, âˆ€ sâ‚‚ : set Î±, sâ‚‚ âŠ† sâ‚ â†’ sâ‚‚ âˆˆ B)
  (union_mem : âˆ€ sâ‚ sâ‚‚ âˆˆ B, sâ‚ âˆª sâ‚‚ âˆˆ B) (singleton_mem : âˆ€ x, {x} âˆˆ B) :
  bornology Î± :=
{ cobounded :=
  { sets := {s : set Î± | sá¶œ âˆˆ B},
    univ_sets := by rwa â†compl_univ at empty_mem,
    sets_of_superset := Î» x y hx hy, subset_mem xá¶œ hx yá¶œ (compl_subset_compl.mpr hy),
    inter_sets := Î» x y hx hy, by simpa [compl_inter] using union_mem xá¶œ hx yá¶œ hy, },
  le_cofinite :=
  begin
    rw le_cofinite_iff_compl_singleton_mem,
    intros x,
    change {x}á¶œá¶œ âˆˆ B,
    rw compl_compl,
    exact singleton_mem x
  end }

/-- A constructor for bornologies by specifying the bounded sets,
and showing that they satisfy the appropriate conditions. -/
@[simps]
def bornology.of_bounded' {Î± : Type*} (B : set (set Î±))
  (empty_mem : âˆ… âˆˆ B) (subset_mem : âˆ€ sâ‚ âˆˆ B, âˆ€ sâ‚‚ : set Î±, sâ‚‚ âŠ† sâ‚ â†’ sâ‚‚ âˆˆ B)
  (union_mem : âˆ€ sâ‚ sâ‚‚ âˆˆ B, sâ‚ âˆª sâ‚‚ âˆˆ B) (sUnion_univ : â‹ƒâ‚€ B = univ) :
  bornology Î± :=
bornology.of_bounded B empty_mem subset_mem union_mem $ Î» x,
  begin
    rw sUnion_eq_univ_iff at sUnion_univ,
    rcases sUnion_univ x with âŸ¨s, hs, hxsâŸ©,
    exact subset_mem s hs {x} (singleton_subset_iff.mpr hxs)
  end

namespace bornology

section
variables [bornology Î±] {s t : set Î±} {x : Î±}

/-- `is_cobounded` is the predicate that `s` is in the filter of cobounded sets in the ambient
bornology on `Î±` -/
def is_cobounded (s : set Î±) : Prop := s âˆˆ cobounded Î±

/-- `is_bounded` is the predicate that `s` is bounded relative to the ambient bornology on `Î±`. -/
def is_bounded (s : set Î±) : Prop := is_cobounded sá¶œ

lemma is_cobounded_def {s : set Î±} : is_cobounded s â†” s âˆˆ cobounded Î± := iff.rfl

lemma is_bounded_def {s : set Î±} : is_bounded s â†” sá¶œ âˆˆ cobounded Î± := iff.rfl

@[simp] lemma is_bounded_compl_iff : is_bounded sá¶œ â†” is_cobounded s :=
by rw [is_bounded_def, is_cobounded_def, compl_compl]

@[simp] lemma is_cobounded_compl_iff : is_cobounded sá¶œ â†” is_bounded s := iff.rfl

alias is_bounded_compl_iff â†” bornology.is_bounded.of_compl bornology.is_cobounded.compl
alias is_cobounded_compl_iff â†” bornology.is_cobounded.of_compl bornology.is_bounded.compl

lemma is_bounded_iff_disjoint_principal : is_bounded s â†” disjoint (ğ“Ÿ s) (cobounded Î±) :=
by rw [is_bounded_def, disjoint_principal_left]

@[simp] lemma is_bounded_empty : is_bounded (âˆ… : set Î±) :=
by { rw [is_bounded_def, compl_empty], exact univ_mem}

lemma is_bounded_iff_mem : is_bounded s â†” âˆ€ x âˆˆ s, is_bounded s :=
âŸ¨Î» h x hx, h,
  Î» h, s.eq_empty_or_nonempty.elim (Î» hs, hs.symm â–¸ is_bounded_empty) $ Î» âŸ¨x, hxâŸ©, h x hxâŸ©

@[simp] lemma is_bounded_singleton : is_bounded ({x} : set Î±) :=
by {rw [is_bounded_def], exact le_cofinite _ (finite_singleton x).compl_mem_cofinite}

@[simp] lemma is_cobounded_univ : is_cobounded (univ : set Î±) := univ_mem

@[simp] lemma is_cobounded_inter : is_cobounded (s âˆ© t) â†” is_cobounded s âˆ§ is_cobounded t :=
inter_mem_iff

lemma is_cobounded.inter (hs : is_cobounded s) (ht : is_cobounded t) : is_cobounded (s âˆ© t) :=
is_cobounded_inter.2 âŸ¨hs, htâŸ©

@[simp] lemma is_bounded_union : is_bounded (s âˆª t) â†” is_bounded s âˆ§ is_bounded t :=
by simp only [â† is_cobounded_compl_iff, compl_union, is_cobounded_inter]

lemma is_bounded.union (hs : is_bounded s) (ht : is_bounded t) : is_bounded (s âˆª t) :=
is_bounded_union.2 âŸ¨hs, htâŸ©

lemma is_cobounded.superset (hs : is_cobounded s) (ht : s âŠ† t) : is_cobounded t :=
mem_of_superset hs ht

lemma is_bounded.subset (ht : is_bounded t) (hs : s âŠ† t) : is_bounded s :=
ht.superset (compl_subset_compl.mpr hs)

@[simp]
lemma sUnion_bounded_univ : (â‹ƒâ‚€ {s : set Î± | is_bounded s}) = univ :=
sUnion_eq_univ_iff.2 $ Î» a, âŸ¨{a}, is_bounded_singleton, mem_singleton aâŸ©

lemma comap_cobounded_le_iff [bornology Î²] {f : Î± â†’ Î²} :
  (cobounded Î²).comap f â‰¤ cobounded Î± â†” âˆ€ â¦ƒsâ¦„, is_bounded s â†’ is_bounded (f '' s) :=
begin
  refine âŸ¨Î» h s hs, _, Î» h t ht,
    âŸ¨(f '' tá¶œ)á¶œ, h $ is_cobounded.compl ht, compl_subset_comm.1 $ subset_preimage_image _ _âŸ©âŸ©,
  obtain âŸ¨t, ht, htsâŸ© := h hs.compl,
  rw [subset_compl_comm, â†preimage_compl] at hts,
  exact (is_cobounded.compl ht).subset ((image_subset f hts).trans $ image_preimage_subset _ _),
end

end

lemma ext_iff' {t t' : bornology Î±} :
  t = t' â†” âˆ€ s, (@cobounded Î± t).sets s â†” (@cobounded Î± t').sets s :=
(ext_iff _ _).trans filter.ext_iff

lemma ext_iff_is_bounded {t t' : bornology Î±} :
  t = t' â†” âˆ€ s, @is_bounded Î± t s â†” @is_bounded Î± t' s :=
âŸ¨Î» h s, h â–¸ iff.rfl, Î» h, by { ext, simpa only [is_bounded_def, compl_compl] using h sá¶œ, }âŸ©

variables {s : set Î±}

lemma is_cobounded_of_bounded_iff (B : set (set Î±)) {empty_mem subset_mem union_mem sUnion_univ} :
  @is_cobounded _ (of_bounded B empty_mem subset_mem union_mem sUnion_univ) s â†” sá¶œ âˆˆ B := iff.rfl

lemma is_bounded_of_bounded_iff (B : set (set Î±)) {empty_mem subset_mem union_mem sUnion_univ} :
  @is_bounded _ (of_bounded B empty_mem subset_mem union_mem sUnion_univ) s â†” s âˆˆ B :=
by rw [is_bounded_def, â†filter.mem_sets, of_bounded_cobounded_sets, set.mem_set_of_eq, compl_compl]

variables [bornology Î±]

lemma is_cobounded_bInter {s : set Î¹} {f : Î¹ â†’ set Î±} (hs : finite s) :
  is_cobounded (â‹‚ i âˆˆ s, f i) â†” âˆ€ i âˆˆ s, is_cobounded (f i) :=
bInter_mem hs

@[simp] lemma is_cobounded_bInter_finset (s : finset Î¹) {f : Î¹ â†’ set Î±} :
  is_cobounded (â‹‚ i âˆˆ s, f i) â†” âˆ€ i âˆˆ s, is_cobounded (f i) :=
bInter_finset_mem s

@[simp] lemma is_cobounded_Inter [fintype Î¹] {f : Î¹ â†’ set Î±} :
  is_cobounded (â‹‚ i, f i) â†” âˆ€ i, is_cobounded (f i) :=
Inter_mem

lemma is_cobounded_sInter {S : set (set Î±)} (hs : finite S) :
  is_cobounded (â‹‚â‚€ S) â†” âˆ€ s âˆˆ S, is_cobounded s :=
sInter_mem hs

lemma is_bounded_bUnion {s : set Î¹} {f : Î¹ â†’ set Î±} (hs : finite s) :
  is_bounded (â‹ƒ i âˆˆ s, f i) â†” âˆ€ i âˆˆ s, is_bounded (f i) :=
by simp only [â† is_cobounded_compl_iff, compl_Union, is_cobounded_bInter hs]

lemma is_bounded_bUnion_finset (s : finset Î¹) {f : Î¹ â†’ set Î±} :
  is_bounded (â‹ƒ i âˆˆ s, f i) â†” âˆ€ i âˆˆ s, is_bounded (f i) :=
is_bounded_bUnion s.finite_to_set

lemma is_bounded_sUnion {S : set (set Î±)} (hs : finite S) :
  is_bounded (â‹ƒâ‚€ S) â†” (âˆ€ s âˆˆ S, is_bounded s) :=
by rw [sUnion_eq_bUnion, is_bounded_bUnion hs]

@[simp] lemma is_bounded_Union [fintype Î¹] {s : Î¹ â†’ set Î±} :
  is_bounded (â‹ƒ i, s i) â†” âˆ€ i, is_bounded (s i) :=
by rw [â† sUnion_range, is_bounded_sUnion (finite_range s), forall_range_iff]

end bornology

open bornology

lemma set.finite.is_bounded [bornology Î±] {s : set Î±} (hs : s.finite) : is_bounded s :=
bornology.le_cofinite Î± hs.compl_mem_cofinite

instance : bornology punit := âŸ¨âŠ¥, bot_leâŸ©

/-- The cofinite filter as a bornology -/
@[reducible] def bornology.cofinite : bornology Î± :=
{ cobounded := cofinite,
  le_cofinite := le_rfl }

/-- A space with a `bornology` is a **bounded space** if `set.univ : set Î±` is bounded. -/
class bounded_space (Î± : Type*) [bornology Î±] : Prop :=
(bounded_univ : bornology.is_bounded (univ : set Î±))

namespace bornology

variables [bornology Î±]

lemma is_bounded_univ : is_bounded (univ : set Î±) â†” bounded_space Î± :=
âŸ¨Î» h, âŸ¨hâŸ©, Î» h, h.1âŸ©

lemma cobounded_eq_bot_iff : cobounded Î± = âŠ¥ â†” bounded_space Î± :=
by rw [â† is_bounded_univ, is_bounded_def, compl_univ, empty_mem_iff_bot]

variables [bounded_space Î±]

lemma is_bounded.all (s : set Î±) : is_bounded s := bounded_space.bounded_univ.subset s.subset_univ
lemma is_cobounded.all (s : set Î±) : is_cobounded s := compl_compl s â–¸ is_bounded.all sá¶œ

variable (Î±)

@[simp] lemma cobounded_eq_bot : cobounded Î± = âŠ¥ := cobounded_eq_bot_iff.2 â€¹_â€º

end bornology
