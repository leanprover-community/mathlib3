/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Mario Carneiro

Type of bounded continuous functions taking values in a metric space, with
the uniform distance.
 -/

import analysis.normed_space.basic topology.metric_space.lipschitz

noncomputable theory
local attribute [instance] classical.decidable_inhabited classical.prop_decidable
open_locale topological_space

open set lattice filter metric

universes u v w
variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

/-- A locally uniform limit of continuous functions is continuous -/
lemma continuous_of_locally_uniform_limit_of_continuous [topological_space Œ±] [metric_space Œ≤]
  {F : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} {f : Œ± ‚Üí Œ≤}
  (L : ‚àÄx:Œ±, ‚àÉs ‚àà ùìù x, ‚àÄŒµ>(0:‚Ñù), ‚àÉn, ‚àÄy‚ààs, dist (F n y) (f y) ‚â§ Œµ)
  (C : ‚àÄ n, continuous (F n)) : continuous f :=
continuous_iff'.2 $ Œª x Œµ Œµ0, begin
  rcases L x with ‚ü®r, rx, hr‚ü©,
  rcases hr (Œµ/2/2) (half_pos $ half_pos Œµ0) with ‚ü®n, hn‚ü©,
  filter_upwards [rx, continuous_iff'.1 (C n) x (Œµ/2) (half_pos Œµ0)],
  simp only [mem_set_of_eq],
  rintro y yr ys,
  calc dist (f y) (f x)
        ‚â§ dist (F n y) (F n x) + (dist (F n y) (f y) + dist (F n x) (f x)) : dist_triangle4_left _ _ _ _
    ... < Œµ/2 + (Œµ/2/2 + Œµ/2/2) :
      add_lt_add_of_lt_of_le ys (add_le_add (hn _ yr) (hn _ (mem_of_nhds rx)))
    ... = Œµ : by rw [add_halves, add_halves]
end

/-- A uniform limit of continuous functions is continuous -/
lemma continuous_of_uniform_limit_of_continuous [topological_space Œ±] {Œ≤ : Type v} [metric_space Œ≤]
  {F : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} {f : Œ± ‚Üí Œ≤} (L : ‚àÄŒµ>(0:‚Ñù), ‚àÉN, ‚àÄy, dist (F N y) (f y) ‚â§ Œµ) :
  (‚àÄ n, continuous (F n)) ‚Üí continuous f :=
continuous_of_locally_uniform_limit_of_continuous $ Œªx,
  ‚ü®univ, by simpa [filter.univ_mem_sets] using L‚ü©

/-- The type of bounded continuous functions from a topological space to a metric space -/
def bounded_continuous_function (Œ± : Type u) (Œ≤ : Type v) [topological_space Œ±] [metric_space Œ≤] : Type (max u v) :=
{f : Œ± ‚Üí Œ≤ // continuous f ‚àß ‚àÉC, ‚àÄx y:Œ±, dist (f x) (f y) ‚â§ C}

local infixr ` ‚Üí·µá `:25 := bounded_continuous_function

namespace bounded_continuous_function
section basics
variables [topological_space Œ±] [metric_space Œ≤] [metric_space Œ≥]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance : has_coe_to_fun (Œ± ‚Üí·µá Œ≤) :=  ‚ü®_, subtype.val‚ü©

lemma bounded_range : bounded (range f) :=
bounded_range_iff.2 f.2.2

/-- If a function is continuous on a compact space, it is automatically bounded,
and therefore gives rise to an element of the type of bounded continuous functions -/
def mk_of_compact [compact_space Œ±] (f : Œ± ‚Üí Œ≤) (hf : continuous f) : Œ± ‚Üí·µá Œ≤ :=
‚ü®f, hf, bounded_range_iff.1 $ bounded_of_compact $ compact_range hf‚ü©

/-- If a function is bounded on a discrete space, it is automatically continuous,
and therefore gives rise to an element of the type of bounded continuous functions -/
def mk_of_discrete [discrete_topology Œ±] (f : Œ± ‚Üí Œ≤) (hf : ‚àÉC, ‚àÄx y, dist (f x) (f y) ‚â§ C) :
  Œ± ‚Üí·µá Œ≤ :=
‚ü®f, continuous_of_discrete_topology, hf‚ü©

/-- The uniform distance between two bounded continuous functions -/
instance : has_dist (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf g, Inf {C | C ‚â• 0 ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C}‚ü©

lemma dist_eq : dist f g = Inf {C | C ‚â• 0 ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C} := rfl

lemma dist_set_exists : ‚àÉ C, C ‚â• 0 ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C :=
begin
  refine if h : nonempty Œ± then _ else ‚ü®0, le_refl _, Œª x, h.elim ‚ü®x‚ü©‚ü©,
  cases h with x,
  rcases f.2 with ‚ü®_, Cf, hCf‚ü©, /- hCf : ‚àÄ (x y : Œ±), dist (f.val x) (f.val y) ‚â§ Cf -/
  rcases g.2 with ‚ü®_, Cg, hCg‚ü©, /- hCg : ‚àÄ (x y : Œ±), dist (g.val x) (g.val y) ‚â§ Cg -/
  let C := max 0 (dist (f x) (g x) + (Cf + Cg)),
  exact ‚ü®C, le_max_left _ _, Œª y, calc
    dist (f y) (g y) ‚â§ dist (f x) (g x) + (dist (f x) (f y) + dist (g x) (g y)) : dist_triangle4_left _ _ _ _
                ... ‚â§ dist (f x) (g x) + (Cf + Cg) : add_le_add_left (add_le_add (hCf _ _) (hCg _ _)) _
                ... ‚â§ C : le_max_right _ _‚ü©
end

/-- The pointwise distance is controlled by the distance between functions, by definition -/
lemma dist_coe_le_dist (x : Œ±) : dist (f x) (g x) ‚â§ dist f g :=
le_cInf dist_set_exists $ Œªb hb, hb.2 x

@[ext] lemma ext (H : ‚àÄx, f x = g x) : f = g :=
subtype.eq $ by ext; apply H

/- This lemma will be needed in the proof of the metric space instance, but it will become
useless afterwards as it will be superceded by the general result that the distance is nonnegative
is metric spaces. -/
private lemma dist_nonneg' : 0 ‚â§ dist f g :=
le_cInf dist_set_exists (Œª C, and.left)

/-- The distance between two functions is controlled by the supremum of the pointwise distances -/
lemma dist_le (C0 : (0 : ‚Ñù) ‚â§ C) : dist f g ‚â§ C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) ‚â§ C :=
‚ü®Œª h x, le_trans (dist_coe_le_dist x) h, Œª H, cInf_le ‚ü®0, Œª C, and.left‚ü© ‚ü®C0, H‚ü©‚ü©

/-- On an empty space, bounded continuous functions are at distance 0 -/
lemma dist_zero_of_empty (e : ¬¨ nonempty Œ±) : dist f g = 0 :=
le_antisymm ((dist_le (le_refl _)).2 $ Œª x, e.elim ‚ü®x‚ü©) dist_nonneg'

/-- The type of bounded continuous functions, with the uniform distance, is a metric space. -/
instance : metric_space (Œ± ‚Üí·µá Œ≤) :=
{ dist_self := Œª f, le_antisymm ((dist_le (le_refl _)).2 $ Œª x, by simp) dist_nonneg',
  eq_of_dist_eq_zero := Œª f g hfg, by ext x; exact
    eq_of_dist_eq_zero (le_antisymm (hfg ‚ñ∏ dist_coe_le_dist _) dist_nonneg),
  dist_comm := Œª f g, by simp [dist_eq, dist_comm],
  dist_triangle := Œª f g h,
    (dist_le (add_nonneg dist_nonneg' dist_nonneg')).2 $ Œª x,
      le_trans (dist_triangle _ _ _) (add_le_add (dist_coe_le_dist _) (dist_coe_le_dist _)) }

/-- Constant as a continuous bounded function. -/
def const (b : Œ≤) : Œ± ‚Üí·µá Œ≤ := ‚ü®Œªx, b, continuous_const, 0, by simp [le_refl]‚ü©

/-- If the target space is inhabited, so is the space of bounded continuous functions -/
instance [inhabited Œ≤] : inhabited (Œ± ‚Üí·µá Œ≤) := ‚ü®const (default Œ≤)‚ü©

/-- The evaluation map is continuous, as a joint function of `u` and `x` -/
theorem continuous_eval : continuous (Œª p : (Œ± ‚Üí·µá Œ≤) √ó Œ±, p.1 p.2) :=
continuous_iff'.2 $ Œª ‚ü®f, x‚ü© Œµ Œµ0,
/- use the continuity of `f` to find a neighborhood of `x` where it varies at most by Œµ/2 -/
have Hs : _ := continuous_iff'.1 f.2.1 x (Œµ/2) (half_pos Œµ0),
mem_sets_of_superset (prod_mem_nhds_sets (ball_mem_nhds _ (half_pos Œµ0)) Hs) $
Œª ‚ü®g, y‚ü© ‚ü®hg, hy‚ü©, calc dist (g y) (f x)
      ‚â§ dist (g y) (f y) + dist (f y) (f x) : dist_triangle _ _ _
  ... < Œµ/2 + Œµ/2 : add_lt_add (lt_of_le_of_lt (dist_coe_le_dist _) hg) hy
  ... = Œµ : add_halves _

/-- In particular, when `x` is fixed, `f ‚Üí f x` is continuous -/
theorem continuous_evalx {x : Œ±} : continuous (Œª f : Œ± ‚Üí·µá Œ≤, f x) :=
continuous_eval.comp (continuous_id.prod_mk continuous_const)

/-- When `f` is fixed, `x ‚Üí f x` is also continuous, by definition -/
theorem continuous_evalf {f : Œ± ‚Üí·µá Œ≤} : continuous f := f.2.1

/-- Bounded continuous functions taking values in a complete space form a complete space. -/
instance [complete_space Œ≤] : complete_space (Œ± ‚Üí·µá Œ≤) :=
complete_of_cauchy_seq_tendsto $ Œª (f : ‚Ñï ‚Üí Œ± ‚Üí·µá Œ≤) (hf : cauchy_seq f),
begin
  /- We have to show that `f n` converges to a bounded continuous function.
  For this, we prove pointwise convergence to define the limit, then check
  it is a continuous bounded function, and then check the norm convergence. -/
  rcases cauchy_seq_iff_le_tendsto_0.1 hf with ‚ü®b, b0, b_bound, b_lim‚ü©,
  have f_bdd := Œªx n m N hn hm, le_trans (dist_coe_le_dist x) (b_bound n m N hn hm),
  have fx_cau : ‚àÄx, cauchy_seq (Œªn, f n x) :=
    Œªx, cauchy_seq_iff_le_tendsto_0.2 ‚ü®b, b0, f_bdd x, b_lim‚ü©,
  choose F hF using Œªx, cauchy_seq_tendsto_of_complete (fx_cau x),
  /- F : Œ± ‚Üí Œ≤,  hF : ‚àÄ (x : Œ±), tendsto (Œª (n : ‚Ñï), f n x) at_top (ùìù (F x))
  `F` is the desired limit function. Check that it is uniformly approximated by `f N` -/
  have fF_bdd : ‚àÄx N, dist (f N x) (F x) ‚â§ b N :=
    Œª x N, le_of_tendsto (by simp)
      (tendsto_dist tendsto_const_nhds (hF x))
      (filter.mem_at_top_sets.2 ‚ü®N, Œªn hn, f_bdd x N n N (le_refl N) hn‚ü©),
  refine ‚ü®‚ü®F, _, _‚ü©, _‚ü©,
  { /- Check that `F` is continuous -/
    refine continuous_of_uniform_limit_of_continuous (Œª Œµ Œµ0, _) (ŒªN, (f N).2.1),
    rcases metric.tendsto_at_top.1 b_lim Œµ Œµ0 with ‚ü®N, hN‚ü©,
    exact ‚ü®N, Œªy, calc
      dist (f N y) (F y) ‚â§ b N : fF_bdd y N
      ... ‚â§ dist (b N) 0 : begin simp, show b N ‚â§ abs(b N), from le_abs_self _ end
      ... ‚â§ Œµ : le_of_lt (hN N (le_refl N))‚ü© },
  { /- Check that `F` is bounded -/
    rcases (f 0).2.2 with ‚ü®C, hC‚ü©,
    exact ‚ü®C + (b 0 + b 0), Œª x y, calc
      dist (F x) (F y) ‚â§ dist (f 0 x) (f 0 y) + (dist (f 0 x) (F x) + dist (f 0 y) (F y)) : dist_triangle4_left _ _ _ _
         ... ‚â§ C + (b 0 + b 0) : add_le_add (hC x y) (add_le_add (fF_bdd x 0) (fF_bdd y 0))‚ü© },
  { /- Check that `F` is close to `f N` in distance terms -/
    refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero (Œª _, dist_nonneg) _ b_lim),
    exact Œª N, (dist_le (b0 _)).2 (Œªx, fF_bdd x N) }
end

/-- Composition (in the target) of a bounded continuous function with a Lipschitz map again
gives a bounded continuous function -/
def comp (G : Œ≤ ‚Üí Œ≥) {C : nnreal} (H : lipschitz_with C G)
  (f : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí·µá Œ≥ :=
‚ü®Œªx, G (f x), H.to_continuous.comp f.2.1,
  let ‚ü®D, hD‚ü© := f.2.2 in
  ‚ü®max C 0 * D, Œª x y, calc
    dist (G (f x)) (G (f y)) ‚â§ C * dist (f x) (f y) : H _ _
    ... ‚â§ max C 0 * dist (f x) (f y) : mul_le_mul_of_nonneg_right (le_max_left C 0) dist_nonneg
    ... ‚â§ max C 0 * D : mul_le_mul_of_nonneg_left (hD _ _) (le_max_right C 0)‚ü©‚ü©

/-- The composition operator (in the target) with a Lipschitz map is Lipschitz -/
lemma lipschitz_comp {G : Œ≤ ‚Üí Œ≥} {C : nnreal} (H : lipschitz_with C G) :
  lipschitz_with C (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
Œª f g,
(dist_le (mul_nonneg C.2 dist_nonneg)).2 $ Œª x,
calc dist (G (f x)) (G (g x)) ‚â§ C * dist (f x) (g x) : H _ _
  ... ‚â§ C * dist f g : mul_le_mul_of_nonneg_left (dist_coe_le_dist _) C.2

/-- The composition operator (in the target) with a Lipschitz map is uniformly continuous -/
lemma uniform_continuous_comp {G : Œ≤ ‚Üí Œ≥} {C : nnreal} (H : lipschitz_with C G) :
  uniform_continuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
(lipschitz_comp H).to_uniform_continuous

/-- The composition operator (in the target) with a Lipschitz map is continuous -/
lemma continuous_comp {G : Œ≤ ‚Üí Œ≥} {C : nnreal} (H : lipschitz_with C G) :
  continuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
(lipschitz_comp H).to_continuous

/-- Restriction (in the target) of a bounded continuous function taking values in a subset -/
def cod_restrict (s : set Œ≤) (f : Œ± ‚Üí·µá Œ≤) (H : ‚àÄx, f x ‚àà s) : Œ± ‚Üí·µá s :=
‚ü®Œªx, ‚ü®f x, H x‚ü©, continuous_subtype_mk _ f.2.1, f.2.2‚ü©

end basics

section arzela_ascoli
variables [topological_space Œ±] [compact_space Œ±] [metric_space Œ≤]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

/- Arzela-Ascoli theorem asserts that, on a compact space, a set of functions sharing
a common modulus of continuity and taking values in a compact set forms a compact
subset for the topology of uniform convergence. In this section, we prove this theorem
and several useful variations around it. -/

/-- First version, with pointwise equicontinuity and range in a compact space -/
theorem arzela_ascoli‚ÇÅ [compact_space Œ≤]
  (A : set (Œ± ‚Üí·µá Œ≤))
  (closed : is_closed A)
  (H : ‚àÄ (x:Œ±) (Œµ > 0), ‚àÉU ‚àà ùìù x, ‚àÄ (y z ‚àà U) (f : Œ± ‚Üí·µá Œ≤),
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ) :
  compact A :=
begin
  refine compact_of_totally_bounded_is_closed _ closed,
  refine totally_bounded_of_finite_discretization (Œª Œµ Œµ0, _),
  rcases dense Œµ0 with ‚ü®Œµ‚ÇÅ, Œµ‚ÇÅ0, ŒµŒµ‚ÇÅ‚ü©,
  let Œµ‚ÇÇ := Œµ‚ÇÅ/2/2,
  /- We have to find a finite discretization of `u`, i.e., finite information
  that is sufficient to reconstruct `u` up to Œµ. This information will be
  provided by the values of `u` on a sufficiently dense set tŒ±,
  slightly translated to fit in a finite Œµ‚ÇÇ-dense set tŒ≤ in the image. Such
  sets exist by compactness of the source and range. Then, to check that these
  data determine the function up to Œµ, one uses the control on the modulus of
  continuity to extend the closeness on tŒ± to closeness everywhere. -/
  have Œµ‚ÇÇ0 : Œµ‚ÇÇ > 0 := half_pos (half_pos Œµ‚ÇÅ0),
  have : ‚àÄx:Œ±, ‚àÉU, x ‚àà U ‚àß is_open U ‚àß ‚àÄ (y z ‚àà U) {f : Œ± ‚Üí·µá Œ≤},
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ‚ÇÇ := Œª x,
      let ‚ü®U, nhdsU, hU‚ü© := H x _ Œµ‚ÇÇ0,
          ‚ü®V, VU, openV, xV‚ü© := mem_nhds_sets_iff.1 nhdsU in
      ‚ü®V, xV, openV, Œªy z hy hz f hf, hU y z (VU hy) (VU hz) f hf‚ü©,
  choose U hU using this,
  /- For all x, the set hU x is an open set containing x on which the elements of A
  fluctuate by at most Œµ‚ÇÇ.
  We extract finitely many of these sets that cover the whole space, by compactness -/
  rcases compact_univ.elim_finite_subcover_image
    (Œªx _, (hU x).2.1) (Œªx hx, mem_bUnion (mem_univ _) (hU x).1)
    with ‚ü®tŒ±, _, ‚ü®_‚ü©, htŒ±‚ü©,
  /- tŒ± : set Œ±, htŒ± : univ ‚äÜ ‚ãÉx ‚àà tŒ±, U x -/
  rcases @finite_cover_balls_of_compact Œ≤ _ _ compact_univ _ Œµ‚ÇÇ0
    with ‚ü®tŒ≤, _, ‚ü®_‚ü©, htŒ≤‚ü©, resetI,
  /- tŒ≤ : set Œ≤, htŒ≤ : univ ‚äÜ ‚ãÉy ‚àà tŒ≤, ball y Œµ‚ÇÇ -/
  /- Associate to every point `y` in the space a nearby point `F y` in tŒ≤ -/
  choose F hF using Œªy, show ‚àÉz‚ààtŒ≤, dist y z < Œµ‚ÇÇ, by simpa using htŒ≤ (mem_univ y),
  /- F : Œ≤ ‚Üí Œ≤, hF : ‚àÄ (y : Œ≤), F y ‚àà tŒ≤ ‚àß dist y (F y) < Œµ‚ÇÇ -/

  /- Associate to every function a discrete approximation, mapping each point in `tŒ±`
  to a point in `tŒ≤` close to its true image by the function. -/
  refine ‚ü®tŒ± ‚Üí tŒ≤, by apply_instance, Œª f a, ‚ü®F (f a), (hF (f a)).1‚ü©, _‚ü©,
  rintro ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© f_eq_g,
  /- If two functions have the same approximation, then they are within distance Œµ -/
  refine lt_of_le_of_lt ((dist_le $ le_of_lt Œµ‚ÇÅ0).2 (Œª x, _)) ŒµŒµ‚ÇÅ,
  obtain ‚ü®x', x'tŒ±, hx'‚ü© : ‚àÉx' ‚àà tŒ±, x ‚àà U x' := mem_bUnion_iff.1 (htŒ± (mem_univ x)),
  refine calc dist (f x) (g x)
      ‚â§ dist (f x) (f x') + dist (g x) (g x') + dist (f x') (g x') : dist_triangle4_right _ _ _ _
  ... ‚â§ Œµ‚ÇÇ + Œµ‚ÇÇ + Œµ‚ÇÅ/2 : le_of_lt (add_lt_add (add_lt_add _ _) _)
  ... = Œµ‚ÇÅ : by rw [add_halves, add_halves],
  { exact (hU x').2.2 _ _ hx' ((hU x').1) hf },
  { exact (hU x').2.2 _ _ hx' ((hU x').1) hg },
  { have F_f_g : F (f x') = F (g x') :=
      (congr_arg (Œª f:tŒ± ‚Üí tŒ≤, (f ‚ü®x', x'tŒ±‚ü© : Œ≤)) f_eq_g : _),
    calc dist (f x') (g x')
          ‚â§ dist (f x') (F (f x')) + dist (g x') (F (f x')) : dist_triangle_right _ _ _
      ... = dist (f x') (F (f x')) + dist (g x') (F (g x')) : by rw F_f_g
      ... < Œµ‚ÇÇ + Œµ‚ÇÇ : add_lt_add (hF (f x')).2 (hF (g x')).2
      ... = Œµ‚ÇÅ/2 : add_halves _ }
end

/-- Second version, with pointwise equicontinuity and range in a compact subset -/
theorem arzela_ascoli‚ÇÇ
  (s : set Œ≤) (hs : compact s)
  (A : set (Œ± ‚Üí·µá Œ≤))
  (closed : is_closed A)
  (in_s : ‚àÄ(f : Œ± ‚Üí·µá Œ≤) (x : Œ±), f ‚àà A ‚Üí f x ‚àà s)
  (H : ‚àÄ(x:Œ±) (Œµ > 0), ‚àÉU ‚àà ùìù x, ‚àÄ (y z ‚àà U) (f : Œ± ‚Üí·µá Œ≤),
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ) :
  compact A :=
/- This version is deduced from the previous one by restricting to the compact type in the target,
using compactness there and then lifting everything to the original space. -/
begin
  have M : lipschitz_with 1 coe := lipschitz_with.subtype_coe s,
  let F : (Œ± ‚Üí·µá s) ‚Üí Œ± ‚Üí·µá Œ≤ := comp coe M,
  refine compact_of_is_closed_subset
    ((_ : compact (F ‚Åª¬π' A)).image (continuous_comp M)) closed (Œª f hf, _),
  { haveI : compact_space s := compact_iff_compact_space.1 hs,
    refine arzela_ascoli‚ÇÅ _ (continuous_iff_is_closed.1 (continuous_comp M) _ closed)
      (Œª x Œµ Œµ0, bex.imp_right (Œª U U_nhds hU y z hy hz f hf, _) (H x Œµ Œµ0)),
    calc dist (f y) (f z) = dist (F f y) (F f z) : rfl
                        ... < Œµ : hU y z hy hz (F f) hf },
  { let g := cod_restrict s f (Œªx, in_s f x hf),
    rw [show f = F g, by ext; refl] at hf ‚ä¢,
    exact ‚ü®g, hf, rfl‚ü© }
end

/-- Third (main) version, with pointwise equicontinuity and range in a compact subset, but
without closedness. The closure is then compact -/
theorem arzela_ascoli
  (s : set Œ≤) (hs : compact s)
  (A : set (Œ± ‚Üí·µá Œ≤))
  (in_s : ‚àÄ(f : Œ± ‚Üí·µá Œ≤) (x : Œ±), f ‚àà A ‚Üí f x ‚àà s)
  (H : ‚àÄ(x:Œ±) (Œµ > 0), ‚àÉU ‚àà ùìù x, ‚àÄ (y z ‚àà U) (f : Œ± ‚Üí·µá Œ≤),
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ) :
  compact (closure A) :=
/- This version is deduced from the previous one by checking that the closure of A, in
addition to being closed, still satisfies the properties of compact range and equicontinuity -/
arzela_ascoli‚ÇÇ s hs (closure A) is_closed_closure
  (Œª f x hf, (mem_of_closed' (closed_of_compact _ hs)).2 $ Œª Œµ Œµ0,
    let ‚ü®g, gA, dist_fg‚ü© := metric.mem_closure_iff.1 hf Œµ Œµ0 in
    ‚ü®g x, in_s g x gA, lt_of_le_of_lt (dist_coe_le_dist _) dist_fg‚ü©)
  (Œª x Œµ Œµ0, show ‚àÉ U ‚àà ùìù x,
      ‚àÄ y z ‚àà U, ‚àÄ (f : Œ± ‚Üí·µá Œ≤), f ‚àà closure A ‚Üí dist (f y) (f z) < Œµ,
    begin
      refine bex.imp_right (Œª U U_set hU y z hy hz f hf, _) (H x (Œµ/2) (half_pos Œµ0)),
      rcases metric.mem_closure_iff.1 hf (Œµ/2/2) (half_pos (half_pos Œµ0)) with ‚ü®g, gA, dist_fg‚ü©,
      replace dist_fg := Œª x, lt_of_le_of_lt (dist_coe_le_dist x) dist_fg,
      calc dist (f y) (f z) ‚â§ dist (f y) (g y) + dist (f z) (g z) + dist (g y) (g z) : dist_triangle4_right _ _ _ _
          ... < Œµ/2/2 + Œµ/2/2 + Œµ/2 :
            add_lt_add (add_lt_add (dist_fg y) (dist_fg z)) (hU y z hy hz g gA)
          ... = Œµ : by rw [add_halves, add_halves]
    end)

/- To apply the previous theorems, one needs to check the equicontinuity. An important
instance is when the source space is a metric space, and there is a fixed modulus of continuity
for all the functions in the set A -/

lemma equicontinuous_of_continuity_modulus {Œ± : Type u} [metric_space Œ±]
  (b : ‚Ñù ‚Üí ‚Ñù) (b_lim : tendsto b (ùìù 0) (ùìù 0))
  (A : set (Œ± ‚Üí·µá Œ≤))
  (H : ‚àÄ(x y:Œ±) (f : Œ± ‚Üí·µá Œ≤), f ‚àà A ‚Üí dist (f x) (f y) ‚â§ b (dist x y))
  (x:Œ±) (Œµ : ‚Ñù) (Œµ0 : Œµ > 0) : ‚àÉU ‚àà ùìù x, ‚àÄ (y z ‚àà U) (f : Œ± ‚Üí·µá Œ≤),
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ :=
begin
  rcases tendsto_nhds_nhds.1 b_lim Œµ Œµ0 with ‚ü®Œ¥, Œ¥0, hŒ¥‚ü©,
  refine ‚ü®ball x (Œ¥/2), ball_mem_nhds x (half_pos Œ¥0), Œª y z hy hz f hf, _‚ü©,
  have : dist y z < Œ¥ := calc
    dist y z ‚â§ dist y x + dist z x : dist_triangle_right _ _ _
    ... < Œ¥/2 + Œ¥/2 : add_lt_add hy hz
    ... = Œ¥ : add_halves _,
  calc
    dist (f y) (f z) ‚â§ b (dist y z) : H y z f hf
    ... ‚â§ abs (b (dist y z)) : le_abs_self _
    ... = dist (b (dist y z)) 0 : by simp [real.dist_eq]
    ... < Œµ : hŒ¥ (by simpa [real.dist_eq] using this),
end

end arzela_ascoli

section normed_group
/- In this section, if Œ≤ is a normed group, then we show that the space of bounded
continuous functions from Œ± to Œ≤ inherits a normed group structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Œ±] [normed_group Œ≤]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance : has_zero (Œ± ‚Üí·µá Œ≤) := ‚ü®const 0‚ü©

@[simp] lemma coe_zero : (0 : Œ± ‚Üí·µá Œ≤) x = 0 := rfl

instance : has_norm (Œ± ‚Üí·µá Œ≤) := ‚ü®Œªu, dist u 0‚ü©

lemma norm_def : ‚à•f‚à• = dist f 0 := rfl

lemma norm_coe_le_norm (x : Œ±) : ‚à•f x‚à• ‚â§ ‚à•f‚à• := calc
  ‚à•f x‚à• = dist (f x) ((0 : Œ± ‚Üí·µá Œ≤) x) : by simp [dist_zero_right]
  ... ‚â§ ‚à•f‚à• : dist_coe_le_dist _

/-- Distance between the images of any two points is at most twice the norm of the function. -/
lemma dist_le_two_norm (x y : Œ±) : dist (f x) (f y) ‚â§ 2 * ‚à•f‚à• := calc
  dist (f x) (f y) ‚â§ ‚à•f x‚à• + ‚à•f y‚à• : dist_le_norm_add_norm _ _
               ... ‚â§ ‚à•f‚à• + ‚à•f‚à•     : add_le_add (norm_coe_le_norm x) (norm_coe_le_norm y)
               ... = 2 * ‚à•f‚à•      : (two_mul _).symm

/-- The norm of a function is controlled by the supremum of the pointwise norms -/
lemma norm_le (C0 : (0 : ‚Ñù) ‚â§ C) : ‚à•f‚à• ‚â§ C ‚Üî ‚àÄx:Œ±, ‚à•f x‚à• ‚â§ C :=
by simpa only [coe_zero, dist_zero_right] using @dist_le _ _ _ _ f 0 _ C0

/-- The pointwise sum of two bounded continuous functions is again bounded continuous. -/
instance : has_add (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf g, ‚ü®Œªx, f x + g x, f.2.1.add g.2.1,
  let ‚ü®_, fM, hf‚ü© := f.2 in let ‚ü®_, gM, hg‚ü© := g.2 in
  ‚ü®fM + gM, Œª x y, dist_add_add_le_of_le (hf _ _) (hg _ _)‚ü©‚ü©‚ü©

/-- The pointwise opposite of a bounded continuous function is again bounded continuous. -/
instance : has_neg (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf, ‚ü®Œªx, -f x, f.2.1.neg, by simpa only [dist_neg_neg] using f.2.2‚ü©‚ü©

@[simp] lemma coe_add : (f + g) x = f x + g x := rfl
@[simp] lemma coe_neg : (-f) x = - (f x) := rfl
lemma forall_coe_zero_iff_zero : (‚àÄx, f x = 0) ‚Üî f = 0 :=
‚ü®@ext _ _ _ _ f 0, by rintro rfl _; refl‚ü©

instance : add_comm_group (Œ± ‚Üí·µá Œ≤) :=
{ add_assoc    := assume f g h, by ext; simp,
  zero_add     := assume f, by ext; simp,
  add_zero     := assume f, by ext; simp,
  add_left_neg := assume f, by ext; simp,
  add_comm     := assume f g, by ext; simp,
  ..bounded_continuous_function.has_add,
  ..bounded_continuous_function.has_neg,
  ..bounded_continuous_function.has_zero }

@[simp] lemma coe_diff : (f - g) x = f x - g x := rfl

instance : normed_group (Œ± ‚Üí·µá Œ≤) :=
normed_group.of_add_dist (Œª _, rfl) $ Œª f g h,
(dist_le dist_nonneg).2 $ Œª x,
le_trans (by rw [dist_eq_norm, dist_eq_norm, coe_add, coe_add,
  add_sub_add_right_eq_sub]) (dist_coe_le_dist x)

lemma abs_diff_coe_le_dist : norm (f x - g x) ‚â§ dist f g :=
by rw normed_group.dist_eq; exact @norm_coe_le_norm _ _ _ _ (f-g) x

lemma coe_le_coe_add_dist {f g : Œ± ‚Üí·µá ‚Ñù} : f x ‚â§ g x + dist f g :=
sub_le_iff_le_add'.1 $ (abs_le.1 $ @dist_coe_le_dist _ _ _ _ f g x).2

/-- Constructing a bounded continuous function from a uniformly bounded continuous
function taking values in a normed group. -/
def of_normed_group {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±] [normed_group Œ≤]
  (f : Œ±  ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄx, norm (f x) ‚â§ C) (Hf : continuous f) : Œ± ‚Üí·µá Œ≤ :=
‚ü®Œªn, f n, ‚ü®Hf, ‚ü®C + C, Œª m n,
  calc dist (f m) (f n) ‚â§ dist (f m) 0 + dist (f n) 0 : dist_triangle_right _ _ _
       ... = norm (f m) + norm (f n) : by simp
       ... ‚â§ C + C : add_le_add (H m) (H n)‚ü©‚ü©‚ü©

/-- Constructing a bounded continuous function from a uniformly bounded
function on a discrete space, taking values in a normed group -/
def of_normed_group_discrete {Œ± : Type u} {Œ≤ : Type v}
  [topological_space Œ±] [discrete_topology Œ±] [normed_group Œ≤]
  (f : Œ±  ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄx, norm (f x) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
of_normed_group f C H continuous_of_discrete_topology

end normed_group
end bounded_continuous_function
