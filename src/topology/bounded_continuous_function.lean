/-
Copyright (c) 2018 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Mario Carneiro, Yury Kudryashov, Heather Macbeth
-/
import analysis.normed_space.linear_isometry
import topology.algebra.continuous_functions
import tactic.equiv_rw

/-!
# Bounded continuous functions

The type of bounded continuous functions taking values in a metric space, with
the uniform distance.

-/

noncomputable theory
open_locale topological_space classical nnreal

open set filter metric

universes u v w
variables {Î± : Type u} {Î² : Type v} {Î³ : Type w}

/-- The type of bounded continuous functions from a topological space to a metric space -/
structure bounded_continuous_function
  (Î± : Type u) (Î² : Type v) [topological_space Î±] [metric_space Î²] extends continuous_map Î± Î² :
  Type (max u v) :=
(bounded' : âˆƒC, âˆ€x y:Î±, dist (to_fun x) (to_fun y) â‰¤ C)

localized "infixr ` â†’áµ‡ `:25 := bounded_continuous_function" in bounded_continuous_function

namespace bounded_continuous_function
section basics
variables [topological_space Î±] [metric_space Î²] [metric_space Î³]
variables {f g : Î± â†’áµ‡ Î²} {x : Î±} {C : â„}

instance : has_coe_to_fun (Î± â†’áµ‡ Î²) :=  âŸ¨_, Î» f, f.to_funâŸ©

protected lemma bounded (f : Î± â†’áµ‡ Î²) : âˆƒC, âˆ€ x y : Î±, dist (f x) (f y) â‰¤ C := f.bounded'
@[continuity]
protected lemma continuous (f : Î± â†’áµ‡ Î²) : continuous f := f.to_continuous_map.continuous

@[ext] lemma ext (H : âˆ€x, f x = g x) : f = g :=
by { cases f, cases g, congr, ext, exact H x, }

lemma ext_iff : f = g â†” âˆ€ x, f x = g x :=
âŸ¨Î» h, Î» x, h â–¸ rfl, extâŸ©

lemma bounded_range : bounded (range f) :=
bounded_range_iff.2 f.bounded

/-- A continuous function with an explicit bound is a bounded continuous function. -/
def mk_of_bound (f : C(Î±, Î²)) (C : â„) (h : âˆ€ x y : Î±, dist (f x) (f y) â‰¤ C) : Î± â†’áµ‡ Î² :=
âŸ¨f, âŸ¨C, hâŸ©âŸ©

@[simp] lemma mk_of_bound_coe {f} {C} {h} : (mk_of_bound f C h : Î± â†’ Î²) = (f : Î± â†’ Î²) :=
rfl

/-- A continuous function on a compact space is automatically a bounded continuous function. -/
def mk_of_compact [compact_space Î±] (f : C(Î±, Î²)) : Î± â†’áµ‡ Î² :=
âŸ¨f, bounded_range_iff.1 $ bounded_of_compact $ compact_range f.continuousâŸ©

@[simp] lemma mk_of_compact_apply [compact_space Î±] (f : C(Î±, Î²)) (a : Î±) :
  mk_of_compact f a = f a :=
rfl

/-- If a function is bounded on a discrete space, it is automatically continuous,
and therefore gives rise to an element of the type of bounded continuous functions -/
def mk_of_discrete [discrete_topology Î±] (f : Î± â†’ Î²)
  (C : â„) (h :  âˆ€ x y : Î±, dist (f x) (f y) â‰¤ C) : Î± â†’áµ‡ Î² :=
âŸ¨âŸ¨f, continuous_of_discrete_topologyâŸ©, âŸ¨C, hâŸ©âŸ©

@[simp] lemma mk_of_discrete_apply
  [discrete_topology Î±] (f : Î± â†’ Î²) (C) (h) (a : Î±) :
  mk_of_discrete f C h a = f a :=
rfl

section
variables (Î± Î²)
/--
The map forgetting that a bounded continuous function is bounded.
-/
def forget_boundedness : (Î± â†’áµ‡ Î²) â†’ C(Î±, Î²) :=
Î» f, f.1

@[simp] lemma forget_boundedness_coe (f : Î± â†’áµ‡ Î²) : (forget_boundedness Î± Î² f : Î± â†’ Î²) = f :=
rfl

/--
When `Î±` is compact, the bounded continuous maps `Î± â†’áµ‡ ğ•œ` are
equivalent to `C(Î±, ğ•œ)`.
-/
@[simps]
def equiv_continuous_map_of_compact [compact_space Î±] : (Î± â†’áµ‡ Î²) â‰ƒ C(Î±, Î²) :=
âŸ¨forget_boundedness Î± Î², mk_of_compact, Î» f, by { ext, refl, }, Î» f, by { ext, refl, }âŸ©

end

/-- The uniform distance between two bounded continuous functions -/
instance : has_dist (Î± â†’áµ‡ Î²) :=
âŸ¨Î»f g, Inf {C | 0 â‰¤ C âˆ§ âˆ€ x : Î±, dist (f x) (g x) â‰¤ C}âŸ©

lemma dist_eq : dist f g = Inf {C | 0 â‰¤ C âˆ§ âˆ€ x : Î±, dist (f x) (g x) â‰¤ C} := rfl

lemma dist_set_exists : âˆƒ C, 0 â‰¤ C âˆ§ âˆ€ x : Î±, dist (f x) (g x) â‰¤ C :=
begin
  refine if h : nonempty Î± then _ else âŸ¨0, le_refl _, Î» x, h.elim âŸ¨xâŸ©âŸ©,
  cases h with x,
  rcases f.bounded with âŸ¨Cf, hCf : âˆ€ x y, dist (f x) (f y) â‰¤ CfâŸ©,
  rcases g.bounded with âŸ¨Cg, hCg : âˆ€ x y, dist (g x) (g y) â‰¤ CgâŸ©,
  let C := max 0 (dist (f x) (g x) + (Cf + Cg)),
  refine âŸ¨C, le_max_left _ _, Î» y, _âŸ©,
  calc dist (f y) (g y) â‰¤ dist (f x) (g x) + (dist (f x) (f y) + dist (g x) (g y)) :
    dist_triangle4_left _ _ _ _
                    ... â‰¤ dist (f x) (g x) + (Cf + Cg) : by mono*
                    ... â‰¤ C : le_max_right _ _
end

/-- The pointwise distance is controlled by the distance between functions, by definition. -/
lemma dist_coe_le_dist (x : Î±) : dist (f x) (g x) â‰¤ dist f g :=
le_cInf dist_set_exists $ Î»b hb, hb.2 x

/- This lemma will be needed in the proof of the metric space instance, but it will become
useless afterwards as it will be superseded by the general result that the distance is nonnegative
in metric spaces. -/
private lemma dist_nonneg' : 0 â‰¤ dist f g :=
le_cInf dist_set_exists (Î» C, and.left)

/-- The distance between two functions is controlled by the supremum of the pointwise distances -/
lemma dist_le (C0 : (0 : â„) â‰¤ C) : dist f g â‰¤ C â†” âˆ€x:Î±, dist (f x) (g x) â‰¤ C :=
âŸ¨Î» h x, le_trans (dist_coe_le_dist x) h, Î» H, cInf_le âŸ¨0, Î» C, and.leftâŸ© âŸ¨C0, HâŸ©âŸ©

lemma dist_le_of_nonempty [nonempty Î±] :
  dist f g â‰¤ C â†” âˆ€ x, dist (f x) (g x) â‰¤ C :=
âŸ¨Î» h x, le_trans (dist_coe_le_dist x) h,
 Î» w, (dist_le (le_trans dist_nonneg (w (nonempty.some â€¹_â€º)))).mpr wâŸ©

lemma dist_lt_of_compact_aux [nonempty Î±] [compact_space Î±]
  (w : âˆ€x:Î±, dist (f x) (g x) < C) : dist f g < C :=
begin
  have c : continuous (Î» x, dist (f x) (g x)), { continuity, },
  obtain âŸ¨x, -, leâŸ© :=
    is_compact.exists_forall_ge compact_univ set.univ_nonempty (continuous.continuous_on c),
  exact lt_of_le_of_lt (dist_le_of_nonempty.mpr (Î» y, le y trivial)) (w x),
end

lemma dist_lt_of_compact [compact_space Î±] (C0 : (0 : â„) < C) :
  dist f g < C â†” âˆ€x:Î±, dist (f x) (g x) < C :=
begin
  fsplit,
  { intros w x,
    exact lt_of_le_of_lt (dist_coe_le_dist x) w, },
  { by_cases h : nonempty Î±,
    { resetI,
      exact dist_lt_of_compact_aux, },
    { rintro -,
      convert C0,
      apply le_antisymm _ dist_nonneg',
      rw [dist_eq],
      exact cInf_le âŸ¨0, Î» C, and.leftâŸ© âŸ¨le_refl _, Î» x, false.elim (h (nonempty.intro x))âŸ©, }, },
end

lemma dist_lt_of_nonempty_compact [nonempty Î±] [compact_space Î±] :
  dist f g < C â†” âˆ€x:Î±, dist (f x) (g x) < C :=
âŸ¨Î» w x, lt_of_le_of_lt (dist_coe_le_dist x) w, dist_lt_of_compact_auxâŸ©

/-- On an empty space, bounded continuous functions are at distance 0 -/
lemma dist_zero_of_empty (e : Â¬ nonempty Î±) : dist f g = 0 :=
le_antisymm ((dist_le (le_refl _)).2 $ Î» x, e.elim âŸ¨xâŸ©) dist_nonneg'

/-- The type of bounded continuous functions, with the uniform distance, is a metric space. -/
instance : metric_space (Î± â†’áµ‡ Î²) :=
{ dist_self := Î» f, le_antisymm ((dist_le (le_refl _)).2 $ Î» x, by simp) dist_nonneg',
  eq_of_dist_eq_zero := Î» f g hfg, by ext x; exact
    eq_of_dist_eq_zero (le_antisymm (hfg â–¸ dist_coe_le_dist _) dist_nonneg),
  dist_comm := Î» f g, by simp [dist_eq, dist_comm],
  dist_triangle := Î» f g h,
    (dist_le (add_nonneg dist_nonneg' dist_nonneg')).2 $ Î» x,
      le_trans (dist_triangle _ _ _) (add_le_add (dist_coe_le_dist _) (dist_coe_le_dist _)) }

instance [compact_space Î±] : metric_space C(Î±,Î²) :=
metric_space.induced
  (equiv_continuous_map_of_compact Î± Î²).symm
  (equiv_continuous_map_of_compact Î± Î²).symm.injective
  (by apply_instance)

variables (Î± Î²)
/--
When `Î±` is compact, and `Î²` is a metric space, the bounded continuous maps `Î± â†’áµ‡ Î²` are
isometric to `C(Î±, Î²)`.
-/
@[simps]
def isometric_continuous_map_of_compact [compact_space Î±] :
  (Î± â†’áµ‡ Î²) â‰ƒáµ¢ C(Î±, Î²) :=
{ isometry_to_fun := Î» x y, rfl,
  to_equiv := equiv_continuous_map_of_compact Î± Î² }

variable {Î²}

/-- Constant as a continuous bounded function. -/
def const (b : Î²) : Î± â†’áµ‡ Î² := âŸ¨continuous_map.const b, 0, by simp [le_refl]âŸ©

variable {Î±}

@[simp] lemma coe_const (b : Î²) : â‡‘(const Î± b) = function.const Î± b := rfl
lemma const_apply (a : Î±) (b : Î²) : (const Î± b : Î± â†’ Î²) a = b := rfl

/-- If the target space is inhabited, so is the space of bounded continuous functions -/
instance [inhabited Î²] : inhabited (Î± â†’áµ‡ Î²) := âŸ¨const Î± (default Î²)âŸ©

/-- The evaluation map is continuous, as a joint function of `u` and `x` -/
theorem continuous_eval : continuous (Î» p : (Î± â†’áµ‡ Î²) Ã— Î±, p.1 p.2) :=
continuous_iff'.2 $ Î» âŸ¨f, xâŸ© Îµ Îµ0,
/- use the continuity of `f` to find a neighborhood of `x` where it varies at most by Îµ/2 -/
have Hs : _ := continuous_iff'.1 f.continuous x (Îµ/2) (half_pos Îµ0),
mem_sets_of_superset (prod_mem_nhds_sets (ball_mem_nhds _ (half_pos Îµ0)) Hs) $
Î» âŸ¨g, yâŸ© âŸ¨hg, hyâŸ©, calc dist (g y) (f x)
      â‰¤ dist (g y) (f y) + dist (f y) (f x) : dist_triangle _ _ _
  ... < Îµ/2 + Îµ/2 : add_lt_add (lt_of_le_of_lt (dist_coe_le_dist _) hg) hy
  ... = Îµ : add_halves _

/-- In particular, when `x` is fixed, `f â†’ f x` is continuous -/
theorem continuous_evalx {x : Î±} : continuous (Î» f : Î± â†’áµ‡ Î², f x) :=
continuous_eval.comp (continuous_id.prod_mk continuous_const)

/-- Bounded continuous functions taking values in a complete space form a complete space. -/
instance [complete_space Î²] : complete_space (Î± â†’áµ‡ Î²) :=
complete_of_cauchy_seq_tendsto $ Î» (f : â„• â†’ Î± â†’áµ‡ Î²) (hf : cauchy_seq f),
begin
  /- We have to show that `f n` converges to a bounded continuous function.
  For this, we prove pointwise convergence to define the limit, then check
  it is a continuous bounded function, and then check the norm convergence. -/
  rcases cauchy_seq_iff_le_tendsto_0.1 hf with âŸ¨b, b0, b_bound, b_limâŸ©,
  have f_bdd := Î»x n m N hn hm, le_trans (dist_coe_le_dist x) (b_bound n m N hn hm),
  have fx_cau : âˆ€x, cauchy_seq (Î»n, f n x) :=
    Î»x, cauchy_seq_iff_le_tendsto_0.2 âŸ¨b, b0, f_bdd x, b_limâŸ©,
  choose F hF using Î»x, cauchy_seq_tendsto_of_complete (fx_cau x),
  /- F : Î± â†’ Î²,  hF : âˆ€ (x : Î±), tendsto (Î» (n : â„•), f n x) at_top (ğ“ (F x))
  `F` is the desired limit function. Check that it is uniformly approximated by `f N` -/
  have fF_bdd : âˆ€x N, dist (f N x) (F x) â‰¤ b N :=
    Î» x N, le_of_tendsto (tendsto_const_nhds.dist (hF x))
      (filter.eventually_at_top.2 âŸ¨N, Î»n hn, f_bdd x N n N (le_refl N) hnâŸ©),
  refine âŸ¨âŸ¨âŸ¨F, _âŸ©, _âŸ©, _âŸ©,
  { /- Check that `F` is continuous, as a uniform limit of continuous functions -/
    have : tendsto_uniformly (Î»n x, f n x) F at_top,
    { refine metric.tendsto_uniformly_iff.2 (Î» Îµ Îµ0, _),
      refine ((tendsto_order.1 b_lim).2 Îµ Îµ0).mono (Î» n hn x, _),
      rw dist_comm,
      exact lt_of_le_of_lt (fF_bdd x n) hn },
    exact this.continuous (Î»N, (f N).continuous) },
  { /- Check that `F` is bounded -/
    rcases (f 0).bounded with âŸ¨C, hCâŸ©,
    refine âŸ¨C + (b 0 + b 0), Î» x y, _âŸ©,
    calc dist (F x) (F y) â‰¤ dist (f 0 x) (f 0 y) + (dist (f 0 x) (F x) + dist (f 0 y) (F y)) :
      dist_triangle4_left _ _ _ _
                      ... â‰¤ C + (b 0 + b 0) : by mono* },
  { /- Check that `F` is close to `f N` in distance terms -/
    refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero (Î» _, dist_nonneg) _ b_lim),
    exact Î» N, (dist_le (b0 _)).2 (Î»x, fF_bdd x N) }
end

/-- Composition (in the target) of a bounded continuous function with a Lipschitz map again
gives a bounded continuous function -/
def comp (G : Î² â†’ Î³) {C : â„â‰¥0} (H : lipschitz_with C G)
  (f : Î± â†’áµ‡ Î²) : Î± â†’áµ‡ Î³ :=
âŸ¨âŸ¨Î»x, G (f x), H.continuous.comp f.continuousâŸ©,
  let âŸ¨D, hDâŸ© := f.bounded in
  âŸ¨max C 0 * D, Î» x y, calc
    dist (G (f x)) (G (f y)) â‰¤ C * dist (f x) (f y) : H.dist_le_mul _ _
    ... â‰¤ max C 0 * dist (f x) (f y) : mul_le_mul_of_nonneg_right (le_max_left C 0) dist_nonneg
    ... â‰¤ max C 0 * D : mul_le_mul_of_nonneg_left (hD _ _) (le_max_right C 0)âŸ©âŸ©

/-- The composition operator (in the target) with a Lipschitz map is Lipschitz -/
lemma lipschitz_comp {G : Î² â†’ Î³} {C : â„â‰¥0} (H : lipschitz_with C G) :
  lipschitz_with C (comp G H : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ‡ Î³) :=
lipschitz_with.of_dist_le_mul $ Î» f g,
(dist_le (mul_nonneg C.2 dist_nonneg)).2 $ Î» x,
calc dist (G (f x)) (G (g x)) â‰¤ C * dist (f x) (g x) : H.dist_le_mul _ _
  ... â‰¤ C * dist f g : mul_le_mul_of_nonneg_left (dist_coe_le_dist _) C.2

/-- The composition operator (in the target) with a Lipschitz map is uniformly continuous -/
lemma uniform_continuous_comp {G : Î² â†’ Î³} {C : â„â‰¥0} (H : lipschitz_with C G) :
  uniform_continuous (comp G H : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ‡ Î³) :=
(lipschitz_comp H).uniform_continuous

/-- The composition operator (in the target) with a Lipschitz map is continuous -/
lemma continuous_comp {G : Î² â†’ Î³} {C : â„â‰¥0} (H : lipschitz_with C G) :
  continuous (comp G H : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ‡ Î³) :=
(lipschitz_comp H).continuous

/-- Restriction (in the target) of a bounded continuous function taking values in a subset -/
def cod_restrict (s : set Î²) (f : Î± â†’áµ‡ Î²) (H : âˆ€x, f x âˆˆ s) : Î± â†’áµ‡ s :=
âŸ¨âŸ¨s.cod_restrict f H, continuous_subtype_mk _ f.continuousâŸ©, f.boundedâŸ©

end basics

section arzela_ascoli
variables [topological_space Î±] [compact_space Î±] [metric_space Î²]
variables {f g : Î± â†’áµ‡ Î²} {x : Î±} {C : â„}

/- Arzela-Ascoli theorem asserts that, on a compact space, a set of functions sharing
a common modulus of continuity and taking values in a compact set forms a compact
subset for the topology of uniform convergence. In this section, we prove this theorem
and several useful variations around it. -/

/-- First version, with pointwise equicontinuity and range in a compact space -/
theorem arzela_ascoliâ‚ [compact_space Î²]
  (A : set (Î± â†’áµ‡ Î²))
  (closed : is_closed A)
  (H : âˆ€ (x:Î±) (Îµ > 0), âˆƒU âˆˆ ğ“ x, âˆ€ (y z âˆˆ U) (f : Î± â†’áµ‡ Î²),
    f âˆˆ A â†’ dist (f y) (f z) < Îµ) :
  is_compact A :=
begin
  refine compact_of_totally_bounded_is_closed _ closed,
  refine totally_bounded_of_finite_discretization (Î» Îµ Îµ0, _),
  rcases exists_between Îµ0 with âŸ¨Îµâ‚, Îµâ‚0, ÎµÎµâ‚âŸ©,
  let Îµâ‚‚ := Îµâ‚/2/2,
  /- We have to find a finite discretization of `u`, i.e., finite information
  that is sufficient to reconstruct `u` up to Îµ. This information will be
  provided by the values of `u` on a sufficiently dense set tÎ±,
  slightly translated to fit in a finite Îµâ‚‚-dense set tÎ² in the image. Such
  sets exist by compactness of the source and range. Then, to check that these
  data determine the function up to Îµ, one uses the control on the modulus of
  continuity to extend the closeness on tÎ± to closeness everywhere. -/
  have Îµâ‚‚0 : Îµâ‚‚ > 0 := half_pos (half_pos Îµâ‚0),
  have : âˆ€x:Î±, âˆƒU, x âˆˆ U âˆ§ is_open U âˆ§ âˆ€ (y z âˆˆ U) {f : Î± â†’áµ‡ Î²},
    f âˆˆ A â†’ dist (f y) (f z) < Îµâ‚‚ := Î» x,
      let âŸ¨U, nhdsU, hUâŸ© := H x _ Îµâ‚‚0,
          âŸ¨V, VU, openV, xVâŸ© := mem_nhds_sets_iff.1 nhdsU in
      âŸ¨V, xV, openV, Î»y z hy hz f hf, hU y z (VU hy) (VU hz) f hfâŸ©,
  choose U hU using this,
  /- For all x, the set hU x is an open set containing x on which the elements of A
  fluctuate by at most Îµâ‚‚.
  We extract finitely many of these sets that cover the whole space, by compactness -/
  rcases compact_univ.elim_finite_subcover_image
    (Î»x _, (hU x).2.1) (Î»x hx, mem_bUnion (mem_univ _) (hU x).1)
    with âŸ¨tÎ±, _, âŸ¨_âŸ©, htÎ±âŸ©,
  /- tÎ± : set Î±, htÎ± : univ âŠ† â‹ƒx âˆˆ tÎ±, U x -/
  rcases @finite_cover_balls_of_compact Î² _ _ compact_univ _ Îµâ‚‚0
    with âŸ¨tÎ², _, âŸ¨_âŸ©, htÎ²âŸ©, resetI,
  /- tÎ² : set Î², htÎ² : univ âŠ† â‹ƒy âˆˆ tÎ², ball y Îµâ‚‚ -/
  /- Associate to every point `y` in the space a nearby point `F y` in tÎ² -/
  choose F hF using Î»y, show âˆƒzâˆˆtÎ², dist y z < Îµâ‚‚, by simpa using htÎ² (mem_univ y),
  /- F : Î² â†’ Î², hF : âˆ€ (y : Î²), F y âˆˆ tÎ² âˆ§ dist y (F y) < Îµâ‚‚ -/

  /- Associate to every function a discrete approximation, mapping each point in `tÎ±`
  to a point in `tÎ²` close to its true image by the function. -/
  refine âŸ¨tÎ± â†’ tÎ², by apply_instance, Î» f a, âŸ¨F (f a), (hF (f a)).1âŸ©, _âŸ©,
  rintro âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© f_eq_g,
  /- If two functions have the same approximation, then they are within distance Îµ -/
  refine lt_of_le_of_lt ((dist_le $ le_of_lt Îµâ‚0).2 (Î» x, _)) ÎµÎµâ‚,
  obtain âŸ¨x', x'tÎ±, hx'âŸ© : âˆƒx' âˆˆ tÎ±, x âˆˆ U x' := mem_bUnion_iff.1 (htÎ± (mem_univ x)),
  refine calc dist (f x) (g x)
      â‰¤ dist (f x) (f x') + dist (g x) (g x') + dist (f x') (g x') : dist_triangle4_right _ _ _ _
  ... â‰¤ Îµâ‚‚ + Îµâ‚‚ + Îµâ‚/2 : le_of_lt (add_lt_add (add_lt_add _ _) _)
  ... = Îµâ‚ : by rw [add_halves, add_halves],
  { exact (hU x').2.2 _ _ hx' ((hU x').1) hf },
  { exact (hU x').2.2 _ _ hx' ((hU x').1) hg },
  { have F_f_g : F (f x') = F (g x') :=
      (congr_arg (Î» f:tÎ± â†’ tÎ², (f âŸ¨x', x'tÎ±âŸ© : Î²)) f_eq_g : _),
    calc dist (f x') (g x')
          â‰¤ dist (f x') (F (f x')) + dist (g x') (F (f x')) : dist_triangle_right _ _ _
      ... = dist (f x') (F (f x')) + dist (g x') (F (g x')) : by rw F_f_g
      ... < Îµâ‚‚ + Îµâ‚‚ : add_lt_add (hF (f x')).2 (hF (g x')).2
      ... = Îµâ‚/2 : add_halves _ }
end

/-- Second version, with pointwise equicontinuity and range in a compact subset -/
theorem arzela_ascoliâ‚‚
  (s : set Î²) (hs : is_compact s)
  (A : set (Î± â†’áµ‡ Î²))
  (closed : is_closed A)
  (in_s : âˆ€(f : Î± â†’áµ‡ Î²) (x : Î±), f âˆˆ A â†’ f x âˆˆ s)
  (H : âˆ€(x:Î±) (Îµ > 0), âˆƒU âˆˆ ğ“ x, âˆ€ (y z âˆˆ U) (f : Î± â†’áµ‡ Î²),
    f âˆˆ A â†’ dist (f y) (f z) < Îµ) :
  is_compact A :=
/- This version is deduced from the previous one by restricting to the compact type in the target,
using compactness there and then lifting everything to the original space. -/
begin
  have M : lipschitz_with 1 coe := lipschitz_with.subtype_coe s,
  let F : (Î± â†’áµ‡ s) â†’ Î± â†’áµ‡ Î² := comp coe M,
  refine compact_of_is_closed_subset
    ((_ : is_compact (F â»Â¹' A)).image (continuous_comp M)) closed (Î» f hf, _),
  { haveI : compact_space s := compact_iff_compact_space.1 hs,
    refine arzela_ascoliâ‚ _ (continuous_iff_is_closed.1 (continuous_comp M) _ closed)
      (Î» x Îµ Îµ0, bex.imp_right (Î» U U_nhds hU y z hy hz f hf, _) (H x Îµ Îµ0)),
    calc dist (f y) (f z) = dist (F f y) (F f z) : rfl
                        ... < Îµ : hU y z hy hz (F f) hf },
  { let g := cod_restrict s f (Î»x, in_s f x hf),
    rw [show f = F g, by ext; refl] at hf âŠ¢,
    exact âŸ¨g, hf, rflâŸ© }
end

/-- Third (main) version, with pointwise equicontinuity and range in a compact subset, but
without closedness. The closure is then compact -/
theorem arzela_ascoli
  (s : set Î²) (hs : is_compact s)
  (A : set (Î± â†’áµ‡ Î²))
  (in_s : âˆ€(f : Î± â†’áµ‡ Î²) (x : Î±), f âˆˆ A â†’ f x âˆˆ s)
  (H : âˆ€(x:Î±) (Îµ > 0), âˆƒU âˆˆ ğ“ x, âˆ€ (y z âˆˆ U) (f : Î± â†’áµ‡ Î²),
    f âˆˆ A â†’ dist (f y) (f z) < Îµ) :
  is_compact (closure A) :=
/- This version is deduced from the previous one by checking that the closure of A, in
addition to being closed, still satisfies the properties of compact range and equicontinuity -/
arzela_ascoliâ‚‚ s hs (closure A) is_closed_closure
  (Î» f x hf, (mem_of_closed' hs.is_closed).2 $ Î» Îµ Îµ0,
    let âŸ¨g, gA, dist_fgâŸ© := metric.mem_closure_iff.1 hf Îµ Îµ0 in
    âŸ¨g x, in_s g x gA, lt_of_le_of_lt (dist_coe_le_dist _) dist_fgâŸ©)
  (Î» x Îµ Îµ0, show âˆƒ U âˆˆ ğ“ x,
      âˆ€ y z âˆˆ U, âˆ€ (f : Î± â†’áµ‡ Î²), f âˆˆ closure A â†’ dist (f y) (f z) < Îµ,
    begin
      refine bex.imp_right (Î» U U_set hU y z hy hz f hf, _) (H x (Îµ/2) (half_pos Îµ0)),
      rcases metric.mem_closure_iff.1 hf (Îµ/2/2) (half_pos (half_pos Îµ0)) with âŸ¨g, gA, dist_fgâŸ©,
      replace dist_fg := Î» x, lt_of_le_of_lt (dist_coe_le_dist x) dist_fg,
      calc dist (f y) (f z) â‰¤ dist (f y) (g y) + dist (f z) (g z) + dist (g y) (g z) :
        dist_triangle4_right _ _ _ _
          ... < Îµ/2/2 + Îµ/2/2 + Îµ/2 :
            add_lt_add (add_lt_add (dist_fg y) (dist_fg z)) (hU y z hy hz g gA)
          ... = Îµ : by rw [add_halves, add_halves]
    end)

/- To apply the previous theorems, one needs to check the equicontinuity. An important
instance is when the source space is a metric space, and there is a fixed modulus of continuity
for all the functions in the set A -/

lemma equicontinuous_of_continuity_modulus {Î± : Type u} [metric_space Î±]
  (b : â„ â†’ â„) (b_lim : tendsto b (ğ“ 0) (ğ“ 0))
  (A : set (Î± â†’áµ‡ Î²))
  (H : âˆ€(x y:Î±) (f : Î± â†’áµ‡ Î²), f âˆˆ A â†’ dist (f x) (f y) â‰¤ b (dist x y))
  (x:Î±) (Îµ : â„) (Îµ0 : 0 < Îµ) : âˆƒU âˆˆ ğ“ x, âˆ€ (y z âˆˆ U) (f : Î± â†’áµ‡ Î²),
    f âˆˆ A â†’ dist (f y) (f z) < Îµ :=
begin
  rcases tendsto_nhds_nhds.1 b_lim Îµ Îµ0 with âŸ¨Î´, Î´0, hÎ´âŸ©,
  refine âŸ¨ball x (Î´/2), ball_mem_nhds x (half_pos Î´0), Î» y z hy hz f hf, _âŸ©,
  have : dist y z < Î´ := calc
    dist y z â‰¤ dist y x + dist z x : dist_triangle_right _ _ _
    ... < Î´/2 + Î´/2 : add_lt_add hy hz
    ... = Î´ : add_halves _,
  calc
    dist (f y) (f z) â‰¤ b (dist y z) : H y z f hf
    ... â‰¤ abs (b (dist y z)) : le_abs_self _
    ... = dist (b (dist y z)) 0 : by simp [real.dist_eq]
    ... < Îµ : hÎ´ (by simpa [real.dist_eq] using this),
end

end arzela_ascoli

section normed_group
/- In this section, if Î² is a normed group, then we show that the space of bounded
continuous functions from Î± to Î² inherits a normed group structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Î±] [normed_group Î²]
variables (f g : Î± â†’áµ‡ Î²) {x : Î±} {C : â„}

instance : has_zero (Î± â†’áµ‡ Î²) := âŸ¨const Î± 0âŸ©

@[simp] lemma coe_zero : ((0 : Î± â†’áµ‡ Î²) : Î± â†’ Î²) = 0 := rfl

instance : has_norm (Î± â†’áµ‡ Î²) := âŸ¨Î»u, dist u 0âŸ©

lemma norm_def : âˆ¥fâˆ¥ = dist f 0 := rfl

/-- The norm of a bounded continuous function is the supremum of `âˆ¥f xâˆ¥`.
We use `Inf` to ensure that the definition works if `Î±` has no elements. -/
lemma norm_eq (f : Î± â†’áµ‡ Î²) :
  âˆ¥fâˆ¥ = Inf {C : â„ | 0 â‰¤ C âˆ§ âˆ€ (x : Î±), âˆ¥f xâˆ¥ â‰¤ C} :=
by simp [norm_def, bounded_continuous_function.dist_eq]

lemma norm_coe_le_norm (x : Î±) : âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ := calc
  âˆ¥f xâˆ¥ = dist (f x) ((0 : Î± â†’áµ‡ Î²) x) : by simp [dist_zero_right]
  ... â‰¤ âˆ¥fâˆ¥ : dist_coe_le_dist _

lemma dist_le_two_norm' {f : Î³ â†’ Î²} {C : â„} (hC : âˆ€ x, âˆ¥f xâˆ¥ â‰¤ C) (x y : Î³) :
  dist (f x) (f y) â‰¤ 2 * C :=
calc dist (f x) (f y) â‰¤ âˆ¥f xâˆ¥ + âˆ¥f yâˆ¥ : dist_le_norm_add_norm _ _
                  ... â‰¤ C + C         : add_le_add (hC x) (hC y)
                  ... = 2 * C         : (two_mul _).symm

/-- Distance between the images of any two points is at most twice the norm of the function. -/
lemma dist_le_two_norm (x y : Î±) : dist (f x) (f y) â‰¤ 2 * âˆ¥fâˆ¥ :=
dist_le_two_norm' f.norm_coe_le_norm x y

variable {f}

/-- The norm of a function is controlled by the supremum of the pointwise norms -/
lemma norm_le (C0 : (0 : â„) â‰¤ C) : âˆ¥fâˆ¥ â‰¤ C â†” âˆ€x:Î±, âˆ¥f xâˆ¥ â‰¤ C :=
by simpa using @dist_le _ _ _ _ f 0 _ C0

lemma norm_le_of_nonempty [nonempty Î±]
  {f : Î± â†’áµ‡ Î²} {M : â„} : âˆ¥fâˆ¥ â‰¤ M â†” âˆ€ x, âˆ¥f xâˆ¥ â‰¤ M :=
âŸ¨Î» w x, le_trans (norm_coe_le_norm f x) w,
  Î» w, (norm_le (le_trans (norm_nonneg _) (w (nonempty.some â€¹_â€º)))).mpr wâŸ©

lemma norm_lt_of_compact [nonempty Î±] [compact_space Î±]
  {f : Î± â†’áµ‡ Î²} {M : â„} : âˆ¥fâˆ¥ < M â†” âˆ€ x, âˆ¥f xâˆ¥ < M :=
begin
  fsplit,
  { intros w x,
    exact lt_of_le_of_lt (norm_coe_le_norm f x) w, },
  { intro w,
    have c : continuous (Î» x, âˆ¥f xâˆ¥), { continuity, },
    obtain âŸ¨x, -, leâŸ© :=
      is_compact.exists_forall_ge compact_univ set.univ_nonempty (continuous.continuous_on c),
    exact lt_of_le_of_lt (norm_le_of_nonempty.mpr (Î» y, le y trivial)) (w x), },
end

variable (f)

/-- Norm of `const Î± b` is less than or equal to `âˆ¥bâˆ¥`. If `Î±` is nonempty,
then it is equal to `âˆ¥bâˆ¥`. -/
lemma norm_const_le (b : Î²) : âˆ¥const Î± bâˆ¥ â‰¤ âˆ¥bâˆ¥ :=
(norm_le (norm_nonneg b)).2 $ Î» x, le_refl _

@[simp] lemma norm_const_eq [h : nonempty Î±] (b : Î²) : âˆ¥const Î± bâˆ¥ = âˆ¥bâˆ¥ :=
le_antisymm (norm_const_le b) $ h.elim $ Î» x, (const Î± b).norm_coe_le_norm x

/-- Constructing a bounded continuous function from a uniformly bounded continuous
function taking values in a normed group. -/
def of_normed_group {Î± : Type u} {Î² : Type v} [topological_space Î±] [normed_group Î²]
  (f : Î±  â†’ Î²) (Hf : continuous f) (C : â„) (H : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C) : Î± â†’áµ‡ Î² :=
âŸ¨âŸ¨Î»n, f n, HfâŸ©, âŸ¨_, dist_le_two_norm' HâŸ©âŸ©

lemma norm_of_normed_group_le {f : Î± â†’ Î²} (hfc : continuous f) {C : â„} (hC : 0 â‰¤ C)
  (hfC : âˆ€ x, âˆ¥f xâˆ¥ â‰¤ C) : âˆ¥of_normed_group f hfc C hfCâˆ¥ â‰¤ C :=
(norm_le hC).2 hfC

/-- Constructing a bounded continuous function from a uniformly bounded
function on a discrete space, taking values in a normed group -/
def of_normed_group_discrete {Î± : Type u} {Î² : Type v}
  [topological_space Î±] [discrete_topology Î±] [normed_group Î²]
  (f : Î±  â†’ Î²) (C : â„) (H : âˆ€x, norm (f x) â‰¤ C) : Î± â†’áµ‡ Î² :=
of_normed_group f continuous_of_discrete_topology C H

/-- The pointwise sum of two bounded continuous functions is again bounded continuous. -/
instance : has_add (Î± â†’áµ‡ Î²) :=
âŸ¨Î»f g, of_normed_group (f + g) (f.continuous.add g.continuous) (âˆ¥fâˆ¥ + âˆ¥gâˆ¥) $ Î» x,
  le_trans (norm_add_le _ _) (add_le_add (f.norm_coe_le_norm x) (g.norm_coe_le_norm x))âŸ©

/-- The pointwise opposite of a bounded continuous function is again bounded continuous. -/
instance : has_neg (Î± â†’áµ‡ Î²) :=
âŸ¨Î»f, of_normed_group (-f) f.continuous.neg âˆ¥fâˆ¥ $ Î» x,
  trans_rel_right _ (norm_neg _) (f.norm_coe_le_norm x)âŸ©

/-- The pointwise difference of two bounded continuous functions is again bounded continuous. -/
instance : has_sub (Î± â†’áµ‡ Î²) :=
âŸ¨Î»f g, of_normed_group (f - g) (f.continuous.sub g.continuous) (âˆ¥fâˆ¥ + âˆ¥gâˆ¥) $ Î» x,
  by { simp only [sub_eq_add_neg],
       exact le_trans (norm_add_le _ _) (add_le_add (f.norm_coe_le_norm x) $
         trans_rel_right _ (norm_neg _) (g.norm_coe_le_norm x)) }âŸ©

@[simp] lemma coe_add : â‡‘(f + g) = Î» x, f x + g x := rfl
lemma add_apply : (f + g) x = f x + g x := rfl
@[simp] lemma coe_neg : â‡‘(-f) = Î» x, - f x := rfl
lemma neg_apply : (-f) x = -f x := rfl

lemma forall_coe_zero_iff_zero : (âˆ€x, f x = 0) â†” f = 0 :=
(@ext_iff _ _ _ _ f 0).symm

instance : add_comm_group (Î± â†’áµ‡ Î²) :=
{ add_assoc      := assume f g h, by ext; simp [add_assoc],
  zero_add       := assume f, by ext; simp,
  add_zero       := assume f, by ext; simp,
  add_left_neg   := assume f, by ext; simp,
  add_comm       := assume f g, by ext; simp [add_comm],
  sub_eq_add_neg := assume f g, by { ext, apply sub_eq_add_neg },
  ..bounded_continuous_function.has_add,
  ..bounded_continuous_function.has_neg,
  ..bounded_continuous_function.has_sub,
  ..bounded_continuous_function.has_zero }

@[simp] lemma coe_sub : â‡‘(f - g) = Î» x, f x - g x := rfl
lemma sub_apply : (f - g) x = f x - g x := rfl

/-- Coercion of a `normed_group_hom` is an `add_monoid_hom`. Similar to `add_monoid_hom.coe_fn` -/
@[simps]
def coe_fn_add_hom : (Î± â†’áµ‡ Î²) â†’+ (Î± â†’ Î²) :=
{ to_fun := coe_fn, map_zero' := coe_zero, map_add' := coe_add}

open_locale big_operators
@[simp] lemma coe_sum {Î¹ : Type*} (s : finset Î¹) (f : Î¹ â†’ (Î± â†’áµ‡ Î²)) :
  â‡‘(âˆ‘ i in s, f i) = (âˆ‘ i in s, (f i : Î± â†’ Î²)) :=
(@coe_fn_add_hom Î± Î² _ _).map_sum f s

lemma sum_apply {Î¹ : Type*} (s : finset Î¹) (f : Î¹ â†’ (Î± â†’áµ‡ Î²)) (a : Î±) :
  (âˆ‘ i in s, f i) a = (âˆ‘ i in s, f i a) :=
by simp

instance : normed_group (Î± â†’áµ‡ Î²) :=
{ dist_eq := Î» f g, by simp only [norm_eq, dist_eq, dist_eq_norm, sub_apply] }

lemma abs_diff_coe_le_dist : âˆ¥f x - g xâˆ¥ â‰¤ dist f g :=
by { rw dist_eq_norm, exact (f - g).norm_coe_le_norm x }

lemma coe_le_coe_add_dist {f g : Î± â†’áµ‡ â„} : f x â‰¤ g x + dist f g :=
sub_le_iff_le_add'.1 $ (abs_le.1 $ @dist_coe_le_dist _ _ _ _ f g x).2

variables (Î± Î²)

/--
The additive map forgetting that a bounded continuous function is bounded.
-/
@[simps]
def forget_boundedness_add_hom : (Î± â†’áµ‡ Î²) â†’+ C(Î±, Î²) :=
{ to_fun := forget_boundedness Î± Î²,
  map_zero' := by { ext, simp, },
  map_add' := by { intros, ext, simp, }, }

section compact_space
variables [compact_space Î±]

section

/--
When `Î±` is compact, the bounded continuous maps `Î± â†’áµ‡ ğ•œ` are
additively equivalent to `C(Î±, ğ•œ)`.
-/
@[simps]
def add_equiv_continuous_map_of_compact : (Î± â†’áµ‡ Î²) â‰ƒ+ C(Î±, Î²) :=
{ ..forget_boundedness_add_hom Î± Î²,
  ..equiv_continuous_map_of_compact Î± Î², }

@[simp]
lemma add_equiv_continuous_map_of_compact_to_equiv :
  (add_equiv_continuous_map_of_compact Î± Î²).to_equiv = equiv_continuous_map_of_compact Î± Î² :=
rfl

end

-- TODO at some point we will need lemmas characterising this norm!
-- At the moment the only way to reason about it is to transfer `f : C(Î±,Î²)` back to `Î± â†’áµ‡ Î²`.
instance : has_norm C(Î±,Î²) :=
{ norm := Î» x, dist x 0 }

instance : normed_group C(Î±,Î²) :=
{ dist_eq := Î» x y,
  begin
    change dist x y = dist (x-y) 0,
     -- it would be nice if `equiv_rw` could rewrite in multiple places at once
    equiv_rw (equiv_continuous_map_of_compact Î± Î²).symm at x,
    equiv_rw (equiv_continuous_map_of_compact Î± Î²).symm at y,
    have p : dist x y = dist (x-y) 0, { rw dist_eq_norm, rw dist_zero_right, },
    convert p,
    exact ((add_equiv_continuous_map_of_compact Î± Î²).map_sub _ _).symm,
  end, }

end compact_space

end normed_group

section normed_space
/-!
### Normed space structure

In this section, if `Î²` is a normed space, then we show that the space of bounded
continuous functions from `Î±` to `Î²` inherits a normed space structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables {ğ•œ : Type*} [normed_field ğ•œ]
variables [topological_space Î±] [normed_group Î²] [normed_space ğ•œ Î²]
variables {f g : Î± â†’áµ‡ Î²} {x : Î±} {C : â„}

instance : has_scalar ğ•œ (Î± â†’áµ‡ Î²) :=
âŸ¨Î» c f, of_normed_group (c â€¢ f) (continuous_const.smul f.continuous) (âˆ¥câˆ¥ * âˆ¥fâˆ¥) $ Î» x,
  trans_rel_right _ (norm_smul _ _)
    (mul_le_mul_of_nonneg_left (f.norm_coe_le_norm _) (norm_nonneg _))âŸ©

@[simp] lemma coe_smul (c : ğ•œ) (f : Î± â†’áµ‡ Î²) : â‡‘(c â€¢ f) = Î» x, c â€¢ (f x) := rfl
lemma smul_apply (c : ğ•œ) (f : Î± â†’áµ‡ Î²) (x : Î±) : (c â€¢ f) x = c â€¢ f x := rfl

instance : semimodule ğ•œ (Î± â†’áµ‡ Î²) :=
semimodule.of_core $
{ smul     := (â€¢),
  smul_add := Î» c f g, ext $ Î» x, smul_add c (f x) (g x),
  add_smul := Î» câ‚ câ‚‚ f, ext $ Î» x, add_smul câ‚ câ‚‚ (f x),
  mul_smul := Î» câ‚ câ‚‚ f, ext $ Î» x, mul_smul câ‚ câ‚‚ (f x),
  one_smul := Î» f, ext $ Î» x, one_smul ğ•œ (f x) }

instance : normed_space ğ•œ (Î± â†’áµ‡ Î²) := âŸ¨Î» c f, norm_of_normed_group_le _
  (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _âŸ©

instance [compact_space Î±] : normed_space ğ•œ C(Î±,Î²) :=
{ norm_smul_le := Î» c f,
  begin
    equiv_rw (equiv_continuous_map_of_compact Î± Î²).symm at f,
    exact le_of_eq (norm_smul c f),
  end }

end normed_space

section normed_ring
/-!
### Normed ring structure

In this section, if `R` is a normed ring, then we show that the space of bounded
continuous functions from `Î±` to `R` inherits a normed ring structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Î±] {R : Type*} [normed_ring R]

instance : ring (Î± â†’áµ‡ R) :=
{ one := const Î± 1,
  mul := Î» f g, of_normed_group (f * g) (f.continuous.mul g.continuous) (âˆ¥fâˆ¥ * âˆ¥gâˆ¥) $ Î» x,
    le_trans (normed_ring.norm_mul (f x) (g x)) $
      mul_le_mul (f.norm_coe_le_norm x) (g.norm_coe_le_norm x) (norm_nonneg _) (norm_nonneg _),
  one_mul := Î» f, ext $ Î» x, one_mul (f x),
  mul_one := Î» f, ext $ Î» x, mul_one (f x),
  mul_assoc := Î» fâ‚ fâ‚‚ fâ‚ƒ, ext $ Î» x, mul_assoc _ _ _,
  left_distrib := Î» fâ‚ fâ‚‚ fâ‚ƒ, ext $ Î» x, left_distrib _ _ _,
  right_distrib := Î» fâ‚ fâ‚‚ fâ‚ƒ, ext $ Î» x, right_distrib _ _ _,
  .. bounded_continuous_function.add_comm_group }

instance : normed_ring (Î± â†’áµ‡ R) :=
{ norm_mul := Î» f g, norm_of_normed_group_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _,
  .. bounded_continuous_function.normed_group }

instance [compact_space Î±] : normed_ring C(Î±,R) :=
{ norm_mul := Î» f g,
  begin
    equiv_rw (equiv_continuous_map_of_compact Î± R).symm at f,
    equiv_rw (equiv_continuous_map_of_compact Î± R).symm at g,
    exact norm_mul_le f g,
  end,
  ..(infer_instance : normed_group C(Î±,R)) }

end normed_ring

section normed_comm_ring
/-!
### Normed commutative ring structure

In this section, if `R` is a normed commutative ring, then we show that the space of bounded
continuous functions from `Î±` to `R` inherits a normed commutative ring structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Î±] {R : Type*} [normed_comm_ring R]

instance : comm_ring (Î± â†’áµ‡ R) :=
{ mul_comm := Î» fâ‚ fâ‚‚, ext $ Î» x, mul_comm _ _,
  .. bounded_continuous_function.ring }

instance : normed_comm_ring (Î± â†’áµ‡ R) :=
{ .. bounded_continuous_function.comm_ring, .. bounded_continuous_function.normed_group }

end normed_comm_ring

section normed_algebra
/-!
### Normed algebra structure

In this section, if `Î³` is a normed algebra, then we show that the space of bounded
continuous functions from `Î±` to `Î³` inherits a normed algebra structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables {ğ•œ : Type*} [normed_field ğ•œ]
variables [topological_space Î±] [normed_group Î²] [normed_space ğ•œ Î²]
variables [normed_ring Î³] [normed_algebra ğ•œ Î³]
variables {f g : Î± â†’áµ‡ Î³} {x : Î±} {c : ğ•œ}

/-- `bounded_continuous_function.const` as a `ring_hom`. -/
def C : ğ•œ â†’+* (Î± â†’áµ‡ Î³) :=
{ to_fun    := Î» (c : ğ•œ), const Î± ((algebra_map ğ•œ Î³) c),
  map_one'  := ext $ Î» x, (algebra_map ğ•œ Î³).map_one,
  map_mul'  := Î» câ‚ câ‚‚, ext $ Î» x, (algebra_map ğ•œ Î³).map_mul _ _,
  map_zero' := ext $ Î» x, (algebra_map ğ•œ Î³).map_zero,
  map_add'  := Î» câ‚ câ‚‚, ext $ Î» x, (algebra_map ğ•œ Î³).map_add _ _ }

instance : algebra ğ•œ (Î± â†’áµ‡ Î³) :=
{ to_ring_hom := C,
  commutes' := Î» c f, ext $ Î» x, algebra.commutes' _ _,
  smul_def' := Î» c f, ext $ Î» x, algebra.smul_def' _ _,
  ..bounded_continuous_function.semimodule,
  ..bounded_continuous_function.ring }

@[simp] lemma algebra_map_apply (k : ğ•œ) (a : Î±) :
  algebra_map ğ•œ (Î± â†’áµ‡ Î³) k a = k â€¢ 1 :=
by { rw algebra.algebra_map_eq_smul_one, refl, }

instance [nonempty Î±] : normed_algebra ğ•œ (Î± â†’áµ‡ Î³) :=
{ norm_algebra_map_eq := Î» c, begin
    calc âˆ¥ (algebra_map ğ•œ (Î± â†’áµ‡ Î³)).to_fun câˆ¥ = âˆ¥(algebra_map ğ•œ Î³) câˆ¥ : _
    ... = âˆ¥câˆ¥ : norm_algebra_map_eq _ _,
    apply norm_const_eq ((algebra_map ğ•œ Î³) c), assumption,
  end,
  ..bounded_continuous_function.algebra }

instance [nonempty Î±] [compact_space Î±] : normed_algebra ğ•œ C(Î±, Î³) :=
{ norm_algebra_map_eq := Î» c, (norm_algebra_map_eq (Î± â†’áµ‡ Î³) c : _), }

/-!
### Structure as normed module over scalar functions

If `Î²` is a normed `ğ•œ`-space, then we show that the space of bounded continuous
functions from `Î±` to `Î²` is naturally a module over the algebra of bounded continuous
functions from `Î±` to `ğ•œ`. -/

instance has_scalar' : has_scalar (Î± â†’áµ‡ ğ•œ) (Î± â†’áµ‡ Î²) :=
âŸ¨Î» (f : Î± â†’áµ‡ ğ•œ) (g : Î± â†’áµ‡ Î²), of_normed_group (Î» x, (f x) â€¢ (g x))
(f.continuous.smul g.continuous) (âˆ¥fâˆ¥ * âˆ¥gâˆ¥) (Î» x, calc
  âˆ¥f x â€¢ g xâˆ¥ â‰¤ âˆ¥f xâˆ¥ * âˆ¥g xâˆ¥ : normed_space.norm_smul_le _ _
  ... â‰¤ âˆ¥fâˆ¥ * âˆ¥gâˆ¥ : mul_le_mul (f.norm_coe_le_norm _) (g.norm_coe_le_norm _) (norm_nonneg _)
    (norm_nonneg _)) âŸ©

instance module' : module (Î± â†’áµ‡ ğ•œ) (Î± â†’áµ‡ Î²) :=
semimodule.of_core $
{ smul     := (â€¢),
  smul_add := Î» c fâ‚ fâ‚‚, ext $ Î» x, smul_add _ _ _,
  add_smul := Î» câ‚ câ‚‚ f, ext $ Î» x, add_smul _ _ _,
  mul_smul := Î» câ‚ câ‚‚ f, ext $ Î» x, mul_smul _ _ _,
  one_smul := Î» f, ext $ Î» x, one_smul ğ•œ (f x) }

lemma norm_smul_le (f : Î± â†’áµ‡ ğ•œ) (g : Î± â†’áµ‡ Î²) : âˆ¥f â€¢ gâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥gâˆ¥ :=
norm_of_normed_group_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _

/- TODO: When `normed_module` has been added to `normed_space.basic`, the above facts
show that the space of bounded continuous functions from `Î±` to `Î²` is naturally a normed
module over the algebra of bounded continuous functions from `Î±` to `ğ•œ`. -/

variables (Î± ğ•œ)

/--
When `Î±` is compact and `ğ•œ` is a normed field,
the `ğ•œ`-algebra of bounded continuous maps `Î± â†’áµ‡ ğ•œ` is
`ğ•œ`-linearly isometric to `C(Î±, ğ•œ)`.
-/
def linear_isometry_continuous_map_of_compact [compact_space Î±] :
  (Î± â†’áµ‡ ğ•œ) â‰ƒâ‚—áµ¢[ğ•œ] C(Î±, ğ•œ) :=
{ map_smul' := Î» c f, by { ext, simp, },
  norm_map' := Î» f, rfl,
  ..add_equiv_continuous_map_of_compact Î± ğ•œ }

@[simp]
lemma isometric_continuous_map_of_compact_to_isometric [compact_space Î±] :
  (linear_isometry_continuous_map_of_compact Î± ğ•œ).to_isometric =
    isometric_continuous_map_of_compact Î± ğ•œ :=
rfl

@[simp]
lemma linear_isometry_continuous_map_of_compact_to_add_equiv [compact_space Î±] :
  (linear_isometry_continuous_map_of_compact Î± ğ•œ).to_linear_equiv.to_add_equiv =
    add_equiv_continuous_map_of_compact Î± ğ•œ :=
rfl

@[simp]
lemma linear_isometry_continuous_map_of_compact_to_equiv [compact_space Î±] :
  (linear_isometry_continuous_map_of_compact Î± ğ•œ).to_linear_equiv.to_equiv =
    equiv_continuous_map_of_compact Î± ğ•œ :=
rfl

end normed_algebra

end bounded_continuous_function
