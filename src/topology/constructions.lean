/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import topology.maps

/-!
# Constructions of new topological spaces from old ones

This file constructs products, sums, subtypes and quotients of topological spaces
and sets up their basic theory, such as criteria for maps into or out of these
constructions to be continuous; descriptions of the open sets, neighborhood filters,
and generators of these constructions; and their behavior with respect to embeddings
and other specific classes of maps.

## Implementation note

The constructed topologies are defined using induced and coinduced topologies
along with the complete lattice structure on topologies. Their universal properties
(for example, a map `X â†’ Y Ã— Z` is continuous if and only if both projections
`X â†’ Y`, `X â†’ Z` are) follow easily using order-theoretic descriptions of
continuity. With more work we can also extract descriptions of the open sets,
neighborhood filters and so on.

## Tags

product, sum, disjoint union, subspace, quotient space

-/

noncomputable theory

open topological_space set filter
open_locale classical topological_space filter

universes u v w x
variables {Î± : Type u} {Î² : Type v} {Î³ : Type w} {Î´ : Type x}

section constructions

instance {p : Î± â†’ Prop} [t : topological_space Î±] : topological_space (subtype p) :=
induced coe t

instance {r : Î± â†’ Î± â†’ Prop} [t : topological_space Î±] : topological_space (quot r) :=
coinduced (quot.mk r) t

instance {s : setoid Î±} [t : topological_space Î±] : topological_space (quotient s) :=
coinduced quotient.mk t

instance [tâ‚ : topological_space Î±] [tâ‚‚ : topological_space Î²] : topological_space (Î± Ã— Î²) :=
induced prod.fst tâ‚ âŠ“ induced prod.snd tâ‚‚

instance [tâ‚ : topological_space Î±] [tâ‚‚ : topological_space Î²] : topological_space (Î± âŠ• Î²) :=
coinduced sum.inl tâ‚ âŠ” coinduced sum.inr tâ‚‚

instance {Î² : Î± â†’ Type v} [tâ‚‚ : Î a, topological_space (Î² a)] : topological_space (sigma Î²) :=
â¨†a, coinduced (sigma.mk a) (tâ‚‚ a)

instance Pi.topological_space {Î² : Î± â†’ Type v} [tâ‚‚ : Î a, topological_space (Î² a)] :
  topological_space (Î a, Î² a) :=
â¨…a, induced (Î»f, f a) (tâ‚‚ a)

instance ulift.topological_space [t : topological_space Î±] : topological_space (ulift.{v u} Î±) :=
t.induced ulift.down

lemma quotient.preimage_mem_nhds [topological_space Î±] [s : setoid Î±]
  {V : set $ quotient s} {a : Î±} (hs : V âˆˆ ğ“ (quotient.mk a)) : quotient.mk â»Â¹' V âˆˆ ğ“ a :=
preimage_nhds_coinduced hs

/-- The image of a dense set under `quotient.mk` is a dense set. -/
lemma dense.quotient [setoid Î±] [topological_space Î±] {s : set Î±} (H : dense s) :
  dense (quotient.mk '' s) :=
(surjective_quotient_mk Î±).dense_range.dense_image continuous_coinduced_rng H

/-- The composition of `quotient.mk` and a function with dense range has dense range. -/
lemma dense_range.quotient [setoid Î±] [topological_space Î±] {f : Î² â†’ Î±} (hf : dense_range f) :
  dense_range (quotient.mk âˆ˜ f) :=
(surjective_quotient_mk Î±).dense_range.comp hf continuous_coinduced_rng

instance {p : Î± â†’ Prop} [topological_space Î±] [discrete_topology Î±] :
  discrete_topology (subtype p) :=
âŸ¨bot_unique $ assume s hs,
  âŸ¨coe '' s, is_open_discrete _, (set.preimage_image_eq _ subtype.coe_injective)âŸ©âŸ©

instance sum.discrete_topology [topological_space Î±] [topological_space Î²]
  [hÎ± : discrete_topology Î±] [hÎ² : discrete_topology Î²] : discrete_topology (Î± âŠ• Î²) :=
âŸ¨by unfold sum.topological_space; simp [hÎ±.eq_bot, hÎ².eq_bot]âŸ©

instance sigma.discrete_topology {Î² : Î± â†’ Type v} [Î a, topological_space (Î² a)]
  [h : Î a, discrete_topology (Î² a)] : discrete_topology (sigma Î²) :=
âŸ¨by { unfold sigma.topological_space, simp [Î» a, (h a).eq_bot] }âŸ©

section topÎ±

variable [topological_space Î±]

/-
The ğ“ filter and the subspace topology.
-/

theorem mem_nhds_subtype (s : set Î±) (a : {x // x âˆˆ s}) (t : set {x // x âˆˆ s}) :
  t âˆˆ ğ“ a â†” âˆƒ u âˆˆ ğ“ (a : Î±), coe â»Â¹' u âŠ† t :=
mem_nhds_induced coe a t

theorem nhds_subtype (s : set Î±) (a : {x // x âˆˆ s}) :
  ğ“ a = comap coe (ğ“ (a : Î±)) :=
nhds_induced coe a

end topÎ±

end constructions


section prod
variables [topological_space Î±] [topological_space Î²] [topological_space Î³] [topological_space Î´]

@[continuity] lemma continuous_fst : continuous (@prod.fst Î± Î²) :=
continuous_inf_dom_left continuous_induced_dom

lemma continuous_at_fst {p : Î± Ã— Î²} : continuous_at prod.fst p :=
continuous_fst.continuous_at

@[continuity] lemma continuous_snd : continuous (@prod.snd Î± Î²) :=
continuous_inf_dom_right continuous_induced_dom

lemma continuous_at_snd {p : Î± Ã— Î²} : continuous_at prod.snd p :=
continuous_snd.continuous_at

@[continuity] lemma continuous.prod_mk {f : Î³ â†’ Î±} {g : Î³ â†’ Î²}
  (hf : continuous f) (hg : continuous g) : continuous (Î»x, (f x, g x)) :=
continuous_inf_rng (continuous_induced_rng hf) (continuous_induced_rng hg)

@[continuity] lemma continuous.prod.mk (a : Î±) : continuous (prod.mk a : Î² â†’ Î± Ã— Î²) :=
continuous_const.prod_mk continuous_id'

lemma continuous.prod_map {f : Î³ â†’ Î±} {g : Î´ â†’ Î²} (hf : continuous f) (hg : continuous g) :
  continuous (Î» x : Î³ Ã— Î´, (f x.1, g x.2)) :=
(hf.comp continuous_fst).prod_mk (hg.comp continuous_snd)

lemma filter.eventually.prod_inl_nhds {p : Î± â†’ Prop} {a : Î±}  (h : âˆ€á¶  x in ğ“ a, p x) (b : Î²) :
  âˆ€á¶  x in ğ“ (a, b), p (x : Î± Ã— Î²).1 :=
continuous_at_fst h

lemma filter.eventually.prod_inr_nhds {p : Î² â†’ Prop} {b : Î²} (h : âˆ€á¶  x in ğ“ b, p x) (a : Î±) :
  âˆ€á¶  x in ğ“ (a, b), p (x : Î± Ã— Î²).2 :=
continuous_at_snd h

lemma filter.eventually.prod_mk_nhds {pa : Î± â†’ Prop} {a} (ha : âˆ€á¶  x in ğ“ a, pa x)
  {pb : Î² â†’ Prop} {b} (hb : âˆ€á¶  y in ğ“ b, pb y) :
  âˆ€á¶  p in ğ“ (a, b), pa (p : Î± Ã— Î²).1 âˆ§ pb p.2 :=
(ha.prod_inl_nhds b).and (hb.prod_inr_nhds a)

lemma continuous_swap : continuous (prod.swap : Î± Ã— Î² â†’ Î² Ã— Î±) :=
continuous.prod_mk continuous_snd continuous_fst

lemma continuous_uncurry_left {f : Î± â†’ Î² â†’ Î³} (a : Î±)
  (h : continuous (function.uncurry f)) : continuous (f a) :=
show continuous (function.uncurry f âˆ˜ (Î» b, (a, b))), from h.comp (by continuity)

lemma continuous_uncurry_right {f : Î± â†’ Î² â†’ Î³} (b : Î²)
  (h : continuous (function.uncurry f)) : continuous (Î» a, f a b) :=
show continuous (function.uncurry f âˆ˜ (Î» a, (a, b))), from h.comp (by continuity)

lemma continuous_curry {g : Î± Ã— Î² â†’ Î³} (a : Î±)
  (h : continuous g) : continuous (function.curry g a) :=
show continuous (g âˆ˜ (Î» b, (a, b))), from h.comp (by continuity)

lemma is_open.prod {s : set Î±} {t : set Î²} (hs : is_open s) (ht : is_open t) :
  is_open (set.prod s t) :=
is_open.inter (hs.preimage continuous_fst) (ht.preimage continuous_snd)

lemma nhds_prod_eq {a : Î±} {b : Î²} : ğ“ (a, b) = ğ“ a Ã—á¶  ğ“ b :=
by rw [filter.prod, prod.topological_space, nhds_inf, nhds_induced, nhds_induced]

lemma mem_nhds_prod_iff {a : Î±} {b : Î²} {s : set (Î± Ã— Î²)} :
  s âˆˆ ğ“ (a, b) â†” âˆƒ (u âˆˆ ğ“ a) (v âˆˆ ğ“ b), set.prod u v âŠ† s :=
by rw [nhds_prod_eq, mem_prod_iff]

lemma mem_nhds_prod_iff' {a : Î±} {b : Î²} {s : set (Î± Ã— Î²)} :
  s âˆˆ ğ“ (a, b) â†” âˆƒ u v, is_open u âˆ§ a âˆˆ u âˆ§ is_open v âˆ§ b âˆˆ v âˆ§ set.prod u v âŠ† s :=
begin
  rw mem_nhds_prod_iff,
  split,
  { rintros âŸ¨u, Hu, v, Hv, hâŸ©,
    rcases mem_nhds_iff.1 Hu with âŸ¨u', u'u, u'_open, Hu'âŸ©,
    rcases mem_nhds_iff.1 Hv with âŸ¨v', v'v, v'_open, Hv'âŸ©,
    exact âŸ¨u', v', u'_open, Hu', v'_open, Hv', (set.prod_mono u'u v'v).trans hâŸ© },
  { rintros âŸ¨u, v, u_open, au, v_open, bv, huvâŸ©,
    exact âŸ¨u, u_open.mem_nhds au, v, v_open.mem_nhds bv, huvâŸ© }
end

lemma filter.has_basis.prod_nhds {Î¹a Î¹b : Type*} {pa : Î¹a â†’ Prop} {pb : Î¹b â†’ Prop}
  {sa : Î¹a â†’ set Î±} {sb : Î¹b â†’ set Î²} {a : Î±} {b : Î²} (ha : (ğ“ a).has_basis pa sa)
  (hb : (ğ“ b).has_basis pb sb) :
  (ğ“ (a, b)).has_basis (Î» i : Î¹a Ã— Î¹b, pa i.1 âˆ§ pb i.2) (Î» i, (sa i.1).prod (sb i.2)) :=
by { rw nhds_prod_eq, exact ha.prod hb }

lemma filter.has_basis.prod_nhds' {Î¹a Î¹b : Type*} {pa : Î¹a â†’ Prop} {pb : Î¹b â†’ Prop}
  {sa : Î¹a â†’ set Î±} {sb : Î¹b â†’ set Î²} {ab : Î± Ã— Î²} (ha : (ğ“ ab.1).has_basis pa sa)
  (hb : (ğ“ ab.2).has_basis pb sb) :
  (ğ“ ab).has_basis (Î» i : Î¹a Ã— Î¹b, pa i.1 âˆ§ pb i.2) (Î» i, (sa i.1).prod (sb i.2)) :=
by { cases ab, exact ha.prod_nhds hb }

instance [discrete_topology Î±] [discrete_topology Î²] : discrete_topology (Î± Ã— Î²) :=
âŸ¨eq_of_nhds_eq_nhds $ assume âŸ¨a, bâŸ©,
  by rw [nhds_prod_eq, nhds_discrete Î±, nhds_discrete Î², nhds_bot, filter.prod_pure_pure]âŸ©

lemma prod_mem_nhds_iff {s : set Î±} {t : set Î²} {a : Î±} {b : Î²} :
  s.prod t âˆˆ ğ“ (a, b) â†” s âˆˆ ğ“ a âˆ§ t âˆˆ ğ“ b :=
by rw [nhds_prod_eq, prod_mem_prod_iff]

lemma prod_is_open.mem_nhds {s : set Î±} {t : set Î²} {a : Î±} {b : Î²}
  (ha : s âˆˆ ğ“ a) (hb : t âˆˆ ğ“ b) : set.prod s t âˆˆ ğ“ (a, b) :=
prod_mem_nhds_iff.2 âŸ¨ha, hbâŸ©

lemma nhds_swap (a : Î±) (b : Î²) : ğ“ (a, b) = (ğ“ (b, a)).map prod.swap :=
by rw [nhds_prod_eq, filter.prod_comm, nhds_prod_eq]; refl

lemma filter.tendsto.prod_mk_nhds {Î³} {a : Î±} {b : Î²} {f : filter Î³} {ma : Î³ â†’ Î±} {mb : Î³ â†’ Î²}
  (ha : tendsto ma f (ğ“ a)) (hb : tendsto mb f (ğ“ b)) :
  tendsto (Î»c, (ma c, mb c)) f (ğ“ (a, b)) :=
by rw [nhds_prod_eq]; exact filter.tendsto.prod_mk ha hb

lemma filter.eventually.curry_nhds {p : Î± Ã— Î² â†’ Prop} {x : Î±} {y : Î²} (h : âˆ€á¶  x in ğ“ (x, y), p x) :
  âˆ€á¶  x' in ğ“ x, âˆ€á¶  y' in ğ“ y, p (x', y') :=
by { rw [nhds_prod_eq] at h, exact h.curry }

lemma continuous_at.prod {f : Î± â†’ Î²} {g : Î± â†’ Î³} {x : Î±}
  (hf : continuous_at f x) (hg : continuous_at g x) : continuous_at (Î»x, (f x, g x)) x :=
hf.prod_mk_nhds hg

lemma continuous_at.prod_map {f : Î± â†’ Î³} {g : Î² â†’ Î´} {p : Î± Ã— Î²}
  (hf : continuous_at f p.fst) (hg : continuous_at g p.snd) :
  continuous_at (Î» p : Î± Ã— Î², (f p.1, g p.2)) p :=
(hf.comp continuous_at_fst).prod (hg.comp continuous_at_snd)

lemma continuous_at.prod_map' {f : Î± â†’ Î³} {g : Î² â†’ Î´} {x : Î±} {y : Î²}
  (hf : continuous_at f x) (hg : continuous_at g y) :
  continuous_at (Î» p : Î± Ã— Î², (f p.1, g p.2)) (x, y) :=
have hf : continuous_at f (x, y).fst, from hf,
have hg : continuous_at g (x, y).snd, from hg,
hf.prod_map hg

lemma prod_generate_from_generate_from_eq {Î± Î² : Type*} {s : set (set Î±)} {t : set (set Î²)}
  (hs : â‹ƒâ‚€ s = univ) (ht : â‹ƒâ‚€ t = univ) :
  @prod.topological_space Î± Î² (generate_from s) (generate_from t) =
  generate_from {g | âˆƒuâˆˆs, âˆƒvâˆˆt, g = set.prod u v} :=
let G := generate_from {g | âˆƒuâˆˆs, âˆƒvâˆˆt, g = set.prod u v} in
le_antisymm
  (le_generate_from $ assume g âŸ¨u, hu, v, hv, g_eqâŸ©, g_eq.symm â–¸
    @is_open.prod _ _ (generate_from s) (generate_from t) _ _
      (generate_open.basic _ hu) (generate_open.basic _ hv))
  (le_inf
    (coinduced_le_iff_le_induced.mp $ le_generate_from $ assume u hu,
      have (â‹ƒvâˆˆt, set.prod u v) = prod.fst â»Â¹' u,
        from calc (â‹ƒvâˆˆt, set.prod u v) = set.prod u univ :
            set.ext $ assume âŸ¨a, bâŸ©, by rw â† ht; simp [and.left_comm] {contextual:=tt}
          ... = prod.fst â»Â¹' u : by simp [set.prod, preimage],
      show G.is_open (prod.fst â»Â¹' u),
        from this â–¸ @is_open_Union _ _ G _ $ assume v, @is_open_Union _ _ G _ $ assume hv,
          generate_open.basic _ âŸ¨_, hu, _, hv, rflâŸ©)
    (coinduced_le_iff_le_induced.mp $ le_generate_from $ assume v hv,
      have (â‹ƒuâˆˆs, set.prod u v) = prod.snd â»Â¹' v,
        from calc (â‹ƒuâˆˆs, set.prod u v) = set.prod univ v:
            set.ext $ assume âŸ¨a, bâŸ©, by rw [â†hs]; by_cases b âˆˆ v; simp [h] {contextual:=tt}
          ... = prod.snd â»Â¹' v : by simp [set.prod, preimage],
      show G.is_open (prod.snd â»Â¹' v),
        from this â–¸ @is_open_Union _ _ G _ $ assume u, @is_open_Union _ _ G _ $ assume hu,
          generate_open.basic _ âŸ¨_, hu, _, hv, rflâŸ©))

lemma prod_eq_generate_from :
  prod.topological_space =
  generate_from {g | âˆƒ(s:set Î±) (t:set Î²), is_open s âˆ§ is_open t âˆ§ g = set.prod s t} :=
le_antisymm
  (le_generate_from $ assume g âŸ¨s, t, hs, ht, g_eqâŸ©, g_eq.symm â–¸ hs.prod ht)
  (le_inf
    (ball_image_of_ball $ Î»t ht, generate_open.basic _ âŸ¨t, univ, by simpa [set.prod_eq] using htâŸ©)
    (ball_image_of_ball $ Î»t ht, generate_open.basic _ âŸ¨univ, t, by simpa [set.prod_eq] using htâŸ©))

lemma is_open_prod_iff {s : set (Î±Ã—Î²)} : is_open s â†”
  (âˆ€a b, (a, b) âˆˆ s â†’ âˆƒu v, is_open u âˆ§ is_open v âˆ§ a âˆˆ u âˆ§ b âˆˆ v âˆ§ set.prod u v âŠ† s) :=
begin
  rw [is_open_iff_nhds],
  simp_rw [le_principal_iff, prod.forall,
    ((nhds_basis_opens _).prod_nhds (nhds_basis_opens _)).mem_iff, prod.exists, exists_prop],
  simp only [and_assoc, and.left_comm]
end

/-- A product of induced topologies is induced by the product map -/
lemma prod_induced_induced {Î± Î³ : Type*} (f : Î± â†’ Î²) (g : Î³ â†’ Î´) :
  @prod.topological_space Î± Î³ (induced f â€¹_â€º) (induced g â€¹_â€º) =
  induced (Î» p, (f p.1, g p.2)) prod.topological_space :=
begin
  set fxg := (Î» p : Î± Ã— Î³, (f p.1, g p.2)),
  have key1 : f âˆ˜ (prod.fst : Î± Ã— Î³ â†’ Î±) = (prod.fst : Î² Ã— Î´ â†’ Î²) âˆ˜ fxg, from rfl,
  have key2 : g âˆ˜ (prod.snd : Î± Ã— Î³ â†’ Î³) = (prod.snd : Î² Ã— Î´ â†’ Î´) âˆ˜ fxg, from rfl,
  unfold prod.topological_space,
  conv_lhs {
    rw [induced_compose, induced_compose, key1, key2],
    congr, rw â† induced_compose, skip, rw â† induced_compose, },
  rw induced_inf
end

lemma continuous_uncurry_of_discrete_topology_left [discrete_topology Î±]
  {f : Î± â†’ Î² â†’ Î³} (h : âˆ€ a, continuous (f a)) : continuous (function.uncurry f) :=
continuous_iff_continuous_at.2 $ Î» âŸ¨a, bâŸ©,
  by simp only [continuous_at, nhds_prod_eq, nhds_discrete Î±, pure_prod, tendsto_map'_iff, (âˆ˜),
    function.uncurry, (h a).tendsto]

/-- Given a neighborhood `s` of `(x, x)`, then `(x, x)` has a square open neighborhood
  that is a subset of `s`. -/
lemma exists_nhds_square {s : set (Î± Ã— Î±)} {x : Î±} (hx : s âˆˆ ğ“ (x, x)) :
  âˆƒU, is_open U âˆ§ x âˆˆ U âˆ§ set.prod U U âŠ† s :=
by simpa [nhds_prod_eq, (nhds_basis_opens x).prod_self.mem_iff, and.assoc, and.left_comm] using hx

/-- `prod.fst` maps neighborhood of `x : Î± Ã— Î²` within the section `prod.snd â»Â¹' {x.2}`
to `ğ“ x.1`. -/
lemma map_fst_nhds_within (x : Î± Ã— Î²) : map prod.fst (ğ“[prod.snd â»Â¹' {x.2}] x) = ğ“ x.1 :=
begin
  refine le_antisymm (continuous_at_fst.mono_left inf_le_left) (Î» s hs, _),
  rcases x with âŸ¨x, yâŸ©,
  rw [mem_map, nhds_within, mem_inf_principal, mem_nhds_prod_iff] at hs,
  rcases hs with âŸ¨u, hu, v, hv, HâŸ©,
  simp only [prod_subset_iff, mem_singleton_iff, mem_set_of_eq, mem_preimage] at H,
  exact mem_of_superset hu (Î» z hz, H _ hz _ (mem_of_mem_nhds hv) rfl)
end

@[simp] lemma map_fst_nhds (x : Î± Ã— Î²) : map prod.fst (ğ“ x) = ğ“ x.1 :=
le_antisymm continuous_at_fst $ (map_fst_nhds_within x).symm.trans_le (map_mono inf_le_left)

/-- The first projection in a product of topological spaces sends open sets to open sets. -/
lemma is_open_map_fst : is_open_map (@prod.fst Î± Î²) :=
is_open_map_iff_nhds_le.2 $ Î» x, (map_fst_nhds x).ge

/-- `prod.snd` maps neighborhood of `x : Î± Ã— Î²` within the section `prod.fst â»Â¹' {x.1}`
to `ğ“ x.2`. -/
lemma map_snd_nhds_within (x : Î± Ã— Î²) : map prod.snd (ğ“[prod.fst â»Â¹' {x.1}] x) = ğ“ x.2 :=
begin
  refine le_antisymm (continuous_at_snd.mono_left inf_le_left) (Î» s hs, _),
  rcases x with âŸ¨x, yâŸ©,
  rw [mem_map, nhds_within, mem_inf_principal, mem_nhds_prod_iff] at hs,
  rcases hs with âŸ¨u, hu, v, hv, HâŸ©,
  simp only [prod_subset_iff, mem_singleton_iff, mem_set_of_eq, mem_preimage] at H,
  exact mem_of_superset hv (Î» z hz, H _ (mem_of_mem_nhds hu) _ hz rfl)
end

@[simp] lemma map_snd_nhds (x : Î± Ã— Î²) : map prod.snd (ğ“ x) = ğ“ x.2 :=
le_antisymm continuous_at_snd $ (map_snd_nhds_within x).symm.trans_le (map_mono inf_le_left)

/-- The second projection in a product of topological spaces sends open sets to open sets. -/
lemma is_open_map_snd : is_open_map (@prod.snd Î± Î²) :=
is_open_map_iff_nhds_le.2 $ Î» x, (map_snd_nhds x).ge

/-- A product set is open in a product space if and only if each factor is open, or one of them is
empty -/
lemma is_open_prod_iff' {s : set Î±} {t : set Î²} :
  is_open (set.prod s t) â†” (is_open s âˆ§ is_open t) âˆ¨ (s = âˆ…) âˆ¨ (t = âˆ…) :=
begin
  cases (set.prod s t).eq_empty_or_nonempty with h h,
  { simp [h, prod_eq_empty_iff.1 h] },
  { have st : s.nonempty âˆ§ t.nonempty, from prod_nonempty_iff.1 h,
    split,
    { assume H : is_open (set.prod s t),
      refine or.inl âŸ¨_, _âŸ©,
      show is_open s,
      { rw â† fst_image_prod s st.2,
        exact is_open_map_fst _ H },
      show is_open t,
      { rw â† snd_image_prod st.1 t,
        exact is_open_map_snd _ H } },
    { assume H,
      simp only [st.1.ne_empty, st.2.ne_empty, not_false_iff, or_false] at H,
      exact H.1.prod H.2 } }
end

lemma closure_prod_eq {s : set Î±} {t : set Î²} :
  closure (set.prod s t) = set.prod (closure s) (closure t) :=
set.ext $ assume âŸ¨a, bâŸ©,
have (ğ“ a Ã—á¶  ğ“ b) âŠ“ ğ“Ÿ (set.prod s t) = (ğ“ a âŠ“ ğ“Ÿ s) Ã—á¶  (ğ“ b âŠ“ ğ“Ÿ t),
  by rw [â†prod_inf_prod, prod_principal_principal],
by simp [closure_eq_cluster_pts, cluster_pt, nhds_prod_eq, this]; exact prod_ne_bot

lemma interior_prod_eq (s : set Î±) (t : set Î²) :
  interior (s.prod t) = (interior s).prod (interior t) :=
set.ext $ Î» âŸ¨a, bâŸ©, by simp only [mem_interior_iff_mem_nhds, mem_prod, prod_mem_nhds_iff]

lemma frontier_prod_eq (s : set Î±) (t : set Î²) :
  frontier (s.prod t) = (closure s).prod (frontier t) âˆª (frontier s).prod (closure t) :=
by simp only [frontier, closure_prod_eq, interior_prod_eq, prod_diff_prod]

@[simp] lemma frontier_prod_univ_eq (s : set Î±) :
  frontier (s.prod (univ : set Î²)) = (frontier s).prod univ :=
by simp [frontier_prod_eq]

@[simp] lemma frontier_univ_prod_eq (s : set Î²) :
  frontier ((univ : set Î±).prod s) = (univ : set Î±).prod (frontier s) :=
by simp [frontier_prod_eq]

lemma map_mem_closure2 {s : set Î±} {t : set Î²} {u : set Î³} {f : Î± â†’ Î² â†’ Î³} {a : Î±} {b : Î²}
  (hf : continuous (Î»p:Î±Ã—Î², f p.1 p.2)) (ha : a âˆˆ closure s) (hb : b âˆˆ closure t)
  (hu : âˆ€a b, a âˆˆ s â†’ b âˆˆ t â†’ f a b âˆˆ u) :
  f a b âˆˆ closure u :=
have (a, b) âˆˆ closure (set.prod s t), by rw [closure_prod_eq]; from âŸ¨ha, hbâŸ©,
show (Î»p:Î±Ã—Î², f p.1 p.2) (a, b) âˆˆ closure u, from
  map_mem_closure hf this $ assume âŸ¨a, bâŸ© âŸ¨ha, hbâŸ©, hu a b ha hb

lemma is_closed.prod {sâ‚ : set Î±} {sâ‚‚ : set Î²} (hâ‚ : is_closed sâ‚) (hâ‚‚ : is_closed sâ‚‚) :
  is_closed (set.prod sâ‚ sâ‚‚) :=
closure_eq_iff_is_closed.mp $ by simp only [hâ‚.closure_eq, hâ‚‚.closure_eq, closure_prod_eq]

/-- The product of two dense sets is a dense set. -/
lemma dense.prod {s : set Î±} {t : set Î²} (hs : dense s) (ht : dense t) :
  dense (s.prod t) :=
Î» x, by { rw closure_prod_eq, exact âŸ¨hs x.1, ht x.2âŸ© }

/-- If `f` and `g` are maps with dense range, then `prod.map f g` has dense range. -/
lemma dense_range.prod_map {Î¹ : Type*} {Îº : Type*} {f : Î¹ â†’ Î²} {g : Îº â†’ Î³}
  (hf : dense_range f) (hg : dense_range g) : dense_range (prod.map f g) :=
by simpa only [dense_range, prod_range_range_eq] using hf.prod hg

lemma inducing.prod_mk {f : Î± â†’ Î²} {g : Î³ â†’ Î´} (hf : inducing f) (hg : inducing g) :
  inducing (Î»x:Î±Ã—Î³, (f x.1, g x.2)) :=
âŸ¨by rw [prod.topological_space, prod.topological_space, hf.induced, hg.induced,
         induced_compose, induced_compose, induced_inf, induced_compose, induced_compose]âŸ©

lemma embedding.prod_mk {f : Î± â†’ Î²} {g : Î³ â†’ Î´} (hf : embedding f) (hg : embedding g) :
  embedding (Î»x:Î±Ã—Î³, (f x.1, g x.2)) :=
{ inj := assume âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ©, by simp; exact assume hâ‚ hâ‚‚, âŸ¨hf.inj hâ‚, hg.inj hâ‚‚âŸ©,
  ..hf.to_inducing.prod_mk hg.to_inducing }

protected lemma is_open_map.prod {f : Î± â†’ Î²} {g : Î³ â†’ Î´} (hf : is_open_map f) (hg : is_open_map g) :
  is_open_map (Î» p : Î± Ã— Î³, (f p.1, g p.2)) :=
begin
  rw [is_open_map_iff_nhds_le],
  rintros âŸ¨a, bâŸ©,
  rw [nhds_prod_eq, nhds_prod_eq, â† filter.prod_map_map_eq],
  exact filter.prod_mono (is_open_map_iff_nhds_le.1 hf a) (is_open_map_iff_nhds_le.1 hg b)
end

protected lemma open_embedding.prod {f : Î± â†’ Î²} {g : Î³ â†’ Î´}
  (hf : open_embedding f) (hg : open_embedding g) : open_embedding (Î»x:Î±Ã—Î³, (f x.1, g x.2)) :=
open_embedding_of_embedding_open (hf.1.prod_mk hg.1)
  (hf.is_open_map.prod hg.is_open_map)

lemma embedding_graph {f : Î± â†’ Î²} (hf : continuous f) : embedding (Î»x, (x, f x)) :=
embedding_of_embedding_compose (continuous_id.prod_mk hf) continuous_fst embedding_id

end prod

section sum
open sum
variables [topological_space Î±] [topological_space Î²] [topological_space Î³]

@[continuity] lemma continuous_inl : continuous (@inl Î± Î²) :=
continuous_sup_rng_left continuous_coinduced_rng

@[continuity] lemma continuous_inr : continuous (@inr Î± Î²) :=
continuous_sup_rng_right continuous_coinduced_rng

@[continuity] lemma continuous_sum_rec {f : Î± â†’ Î³} {g : Î² â†’ Î³}
  (hf : continuous f) (hg : continuous g) : @continuous (Î± âŠ• Î²) Î³ _ _ (@sum.rec Î± Î² (Î»_, Î³) f g) :=
begin
  apply continuous_sup_dom;
  rw continuous_def at hf hg âŠ¢;
  assumption
end

lemma is_open_sum_iff {s : set (Î± âŠ• Î²)} :
  is_open s â†” is_open (inl â»Â¹' s) âˆ§ is_open (inr â»Â¹' s) :=
iff.rfl

lemma is_open_map_sum {f : Î± âŠ• Î² â†’ Î³}
  (hâ‚ : is_open_map (Î» a, f (inl a))) (hâ‚‚ : is_open_map (Î» b, f (inr b))) :
  is_open_map f :=
begin
  intros u hu,
  rw is_open_sum_iff at hu,
  cases hu with huâ‚ huâ‚‚,
  have : u = inl '' (inl â»Â¹' u) âˆª inr '' (inr â»Â¹' u),
  { ext (_|_); simp },
  rw [this, set.image_union, set.image_image, set.image_image],
  exact is_open.union (hâ‚ _ huâ‚) (hâ‚‚ _ huâ‚‚)
end

lemma embedding_inl : embedding (@inl Î± Î²) :=
{ induced := begin
    unfold sum.topological_space,
    apply le_antisymm,
    { rw â† coinduced_le_iff_le_induced, exact le_sup_left },
    { intros u hu, existsi (inl '' u),
      change
        (is_open (inl â»Â¹' (@inl Î± Î² '' u)) âˆ§
         is_open (inr â»Â¹' (@inl Î± Î² '' u))) âˆ§
        inl â»Â¹' (inl '' u) = u,
      have : inl â»Â¹' (@inl Î± Î² '' u) = u :=
        preimage_image_eq u (Î» _ _, inl.inj_iff.mp), rw this,
      have : inr â»Â¹' (@inl Î± Î² '' u) = âˆ… :=
        eq_empty_iff_forall_not_mem.mpr (assume a âŸ¨b, _, hâŸ©, inl_ne_inr h), rw this,
      exact âŸ¨âŸ¨hu, is_open_emptyâŸ©, rflâŸ© }
  end,
  inj := Î» _ _, inl.inj_iff.mp }

lemma embedding_inr : embedding (@inr Î± Î²) :=
{ induced := begin
    unfold sum.topological_space,
    apply le_antisymm,
    { rw â† coinduced_le_iff_le_induced, exact le_sup_right },
    { intros u hu, existsi (inr '' u),
      change
        (is_open (inl â»Â¹' (@inr Î± Î² '' u)) âˆ§
         is_open (inr â»Â¹' (@inr Î± Î² '' u))) âˆ§
        inr â»Â¹' (inr '' u) = u,
      have : inl â»Â¹' (@inr Î± Î² '' u) = âˆ… :=
        eq_empty_iff_forall_not_mem.mpr (assume b âŸ¨a, _, hâŸ©, inr_ne_inl h), rw this,
      have : inr â»Â¹' (@inr Î± Î² '' u) = u :=
        preimage_image_eq u (Î» _ _, inr.inj_iff.mp), rw this,
      exact âŸ¨âŸ¨is_open_empty, huâŸ©, rflâŸ© }
  end,
  inj := Î» _ _, inr.inj_iff.mp }

lemma is_open_range_inl : is_open (range (inl : Î± â†’ Î± âŠ• Î²)) :=
is_open_sum_iff.2 $ by simp

lemma is_open_range_inr : is_open (range (inr : Î² â†’ Î± âŠ• Î²)) :=
is_open_sum_iff.2 $ by simp

lemma open_embedding_inl : open_embedding (inl : Î± â†’ Î± âŠ• Î²) :=
{ open_range := is_open_range_inl,
  .. embedding_inl }

lemma open_embedding_inr : open_embedding (inr : Î² â†’ Î± âŠ• Î²) :=
{ open_range := is_open_range_inr,
  .. embedding_inr }

end sum

section subtype
variables [topological_space Î±] [topological_space Î²] [topological_space Î³] {p : Î± â†’ Prop}

lemma embedding_subtype_coe : embedding (coe : subtype p â†’ Î±) :=
âŸ¨âŸ¨rflâŸ©, subtype.coe_injectiveâŸ©

lemma closed_embedding_subtype_coe (h : is_closed {a | p a}) :
  closed_embedding (coe : subtype p â†’ Î±) :=
âŸ¨embedding_subtype_coe, by rwa [subtype.range_coe_subtype]âŸ©

@[continuity] lemma continuous_subtype_val : continuous (@subtype.val Î± p) :=
continuous_induced_dom

lemma continuous_subtype_coe : continuous (coe : subtype p â†’ Î±) :=
continuous_subtype_val

lemma is_open.open_embedding_subtype_coe {s : set Î±} (hs : is_open s) :
  open_embedding (coe : s â†’ Î±) :=
{ induced := rfl,
  inj := subtype.coe_injective,
  open_range := (subtype.range_coe : range coe = s).symm â–¸  hs }

lemma is_open.is_open_map_subtype_coe {s : set Î±} (hs : is_open s) :
  is_open_map (coe : s â†’ Î±) :=
hs.open_embedding_subtype_coe.is_open_map

lemma is_open_map.restrict {f : Î± â†’ Î²} (hf : is_open_map f) {s : set Î±} (hs : is_open s) :
  is_open_map (s.restrict f) :=
hf.comp hs.is_open_map_subtype_coe

lemma is_closed.closed_embedding_subtype_coe {s : set Î±} (hs : is_closed s) :
  closed_embedding (coe : {x // x âˆˆ s} â†’ Î±) :=
{ induced := rfl,
  inj := subtype.coe_injective,
  closed_range := (subtype.range_coe : range coe = s).symm â–¸ hs }

@[continuity] lemma continuous_subtype_mk {f : Î² â†’ Î±}
  (hp : âˆ€x, p (f x)) (h : continuous f) : continuous (Î»x, (âŸ¨f x, hp xâŸ© : subtype p)) :=
continuous_induced_rng h

lemma continuous_inclusion {s t : set Î±} (h : s âŠ† t) : continuous (inclusion h) :=
continuous_subtype_mk _ continuous_subtype_coe

lemma continuous_at_subtype_coe {p : Î± â†’ Prop} {a : subtype p} :
  continuous_at (coe : subtype p â†’ Î±) a :=
continuous_iff_continuous_at.mp continuous_subtype_coe _

lemma map_nhds_subtype_coe_eq {a : Î±} (ha : p a) (h : {a | p a} âˆˆ ğ“ a) :
  map (coe : subtype p â†’ Î±) (ğ“ âŸ¨a, haâŸ©) = ğ“ a :=
map_nhds_induced_of_mem $ by simpa only [subtype.coe_mk, subtype.range_coe] using h

lemma nhds_subtype_eq_comap {a : Î±} {h : p a} :
  ğ“ (âŸ¨a, hâŸ© : subtype p) = comap coe (ğ“ a) :=
nhds_induced _ _

lemma tendsto_subtype_rng {Î² : Type*} {p : Î± â†’ Prop} {b : filter Î²} {f : Î² â†’ subtype p} :
  âˆ€{a:subtype p}, tendsto f b (ğ“ a) â†” tendsto (Î»x, (f x : Î±)) b (ğ“ (a : Î±))
| âŸ¨a, haâŸ© := by rw [nhds_subtype_eq_comap, tendsto_comap_iff, subtype.coe_mk]

lemma continuous_subtype_nhds_cover {Î¹ : Sort*} {f : Î± â†’ Î²} {c : Î¹ â†’ Î± â†’ Prop}
  (c_cover : âˆ€x:Î±, âˆƒi, {x | c i x} âˆˆ ğ“ x)
  (f_cont  : âˆ€i, continuous (Î»(x : subtype (c i)), f x)) :
  continuous f :=
continuous_iff_continuous_at.mpr $ assume x,
  let âŸ¨i, (c_sets : {x | c i x} âˆˆ ğ“ x)âŸ© := c_cover x in
  let x' : subtype (c i) := âŸ¨x, mem_of_mem_nhds c_setsâŸ© in
  calc map f (ğ“ x) = map f (map coe (ğ“ x')) :
      congr_arg (map f) (map_nhds_subtype_coe_eq _ $ c_sets).symm
    ... = map (Î»x:subtype (c i), f x) (ğ“ x') : rfl
    ... â‰¤ ğ“ (f x) : continuous_iff_continuous_at.mp (f_cont i) x'

lemma continuous_subtype_is_closed_cover {Î¹ : Sort*} {f : Î± â†’ Î²} (c : Î¹ â†’ Î± â†’ Prop)
  (h_lf : locally_finite (Î»i, {x | c i x}))
  (h_is_closed : âˆ€i, is_closed {x | c i x})
  (h_cover : âˆ€x, âˆƒi, c i x)
  (f_cont  : âˆ€i, continuous (Î»(x : subtype (c i)), f x)) :
  continuous f :=
continuous_iff_is_closed.mpr $
  assume s hs,
  have âˆ€i, is_closed ((coe : {x | c i x} â†’ Î±) '' (f âˆ˜ coe â»Â¹' s)),
    from assume i,
    (closed_embedding_subtype_coe (h_is_closed _)).is_closed_map _ (hs.preimage (f_cont i)),
  have is_closed (â‹ƒi, (coe : {x | c i x} â†’ Î±) '' (f âˆ˜ coe â»Â¹' s)),
    from locally_finite.is_closed_Union
      (h_lf.subset $ assume i x âŸ¨âŸ¨x', hx'âŸ©, _, heqâŸ©, heq â–¸ hx')
      this,
  have f â»Â¹' s = (â‹ƒi, (coe : {x | c i x} â†’ Î±) '' (f âˆ˜ coe â»Â¹' s)),
  begin
    apply set.ext,
    have : âˆ€ (x : Î±), f x âˆˆ s â†” âˆƒ (i : Î¹), c i x âˆ§ f x âˆˆ s :=
      Î» x, âŸ¨Î» hx, let âŸ¨i, hiâŸ© := h_cover x in âŸ¨i, hi, hxâŸ©,
            Î» âŸ¨i, hi, hxâŸ©, hxâŸ©,
    simpa [and.comm, @and.left_comm (c _ _), â† exists_and_distrib_right],
  end,
  by rwa [this]

lemma closure_subtype {x : {a // p a}} {s : set {a // p a}}:
  x âˆˆ closure s â†” (x : Î±) âˆˆ closure ((coe : _ â†’ Î±) '' s) :=
closure_induced

end subtype

section quotient
variables [topological_space Î±] [topological_space Î²] [topological_space Î³]
variables {r : Î± â†’ Î± â†’ Prop} {s : setoid Î±}

lemma quotient_map_quot_mk : quotient_map (@quot.mk Î± r) :=
âŸ¨quot.exists_rep, rflâŸ©

@[continuity] lemma continuous_quot_mk : continuous (@quot.mk Î± r) :=
continuous_coinduced_rng

@[continuity] lemma continuous_quot_lift {f : Î± â†’ Î²} (hr : âˆ€ a b, r a b â†’ f a = f b)
  (h : continuous f) : continuous (quot.lift f hr : quot r â†’ Î²) :=
continuous_coinduced_dom h

lemma quotient_map_quotient_mk : quotient_map (@quotient.mk Î± s) :=
quotient_map_quot_mk

lemma continuous_quotient_mk : continuous (@quotient.mk Î± s) :=
continuous_coinduced_rng

lemma continuous_quotient_lift {f : Î± â†’ Î²} (hs : âˆ€ a b, a â‰ˆ b â†’ f a = f b)
  (h : continuous f) : continuous (quotient.lift f hs : quotient s â†’ Î²) :=
continuous_coinduced_dom h

end quotient

section pi
variables {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*}

@[continuity]
lemma continuous_pi [topological_space Î±] [âˆ€i, topological_space (Ï€ i)] {f : Î± â†’ Î i:Î¹, Ï€ i}
  (h : âˆ€i, continuous (Î»a, f a i)) : continuous f :=
continuous_infi_rng $ assume i, continuous_induced_rng $ h i

@[continuity]
lemma continuous_apply [âˆ€i, topological_space (Ï€ i)] (i : Î¹) :
  continuous (Î»p:Î i, Ï€ i, p i) :=
continuous_infi_dom continuous_induced_dom

lemma continuous_at_apply [âˆ€i, topological_space (Ï€ i)] (i : Î¹) (x : Î  i, Ï€ i) :
  continuous_at (Î» p : Î  i, Ï€ i, p i) x :=
(continuous_apply i).continuous_at

lemma filter.tendsto.apply [âˆ€i, topological_space (Ï€ i)] {l : filter Î±} {f : Î± â†’ Î  i, Ï€ i}
  {x : Î  i, Ï€ i} (h : tendsto f l (ğ“ x)) (i : Î¹) :
  tendsto (Î» a, f a i) l (ğ“ $ x i) :=
(continuous_at_apply i _).tendsto.comp h

lemma continuous_pi_iff [topological_space Î±] [âˆ€ i, topological_space (Ï€ i)] {f : Î± â†’ Î  i, Ï€ i} :
  continuous f â†” âˆ€ i, continuous (Î» y, f y i) :=
iff.intro (Î» h i, (continuous_apply i).comp h) continuous_pi

lemma nhds_pi [t : âˆ€i, topological_space (Ï€ i)] {a : Î i, Ï€ i} :
  ğ“ a = (â¨…i, comap (Î»x, x i) (ğ“ (a i))) :=
calc ğ“ a = (â¨…i, @nhds _ (@topological_space.induced _ _ (Î»x:Î i, Ï€ i, x i) (t i)) a) : nhds_infi
  ... = (â¨…i, comap (Î»x, x i) (ğ“ (a i))) : by simp [nhds_induced]

lemma tendsto_pi [t : âˆ€i, topological_space (Ï€ i)] {f : Î± â†’ Î i, Ï€ i} {g : Î i, Ï€ i} {u : filter Î±} :
  tendsto f u (ğ“ g) â†” âˆ€ x, tendsto (Î» i, f i x) u (ğ“ (g x)) :=
by simp [nhds_pi, filter.tendsto_comap_iff]

lemma continuous_at_pi [âˆ€ i, topological_space (Ï€ i)] [topological_space Î±] {f : Î± â†’ Î  i, Ï€ i}
  {x : Î±} :
  continuous_at f x â†” âˆ€ i, continuous_at (Î» y, f y i) x :=
tendsto_pi

lemma filter.tendsto.update [âˆ€i, topological_space (Ï€ i)] [decidable_eq Î¹]
  {l : filter Î±} {f : Î± â†’ Î  i, Ï€ i} {x : Î  i, Ï€ i} (hf : tendsto f l (ğ“ x)) (i : Î¹)
  {g : Î± â†’ Ï€ i} {xi : Ï€ i} (hg : tendsto g l (ğ“ xi)) :
  tendsto (Î» a, function.update (f a) i (g a)) l (ğ“ $ function.update x i xi) :=
tendsto_pi.2 $ Î» j, by { rcases em (j = i) with rfl|hj; simp [*, hf.apply] }

lemma continuous_at.update [âˆ€i, topological_space (Ï€ i)] [topological_space Î±] [decidable_eq Î¹]
  {f : Î± â†’ Î  i, Ï€ i} {a : Î±} (hf : continuous_at f a) (i : Î¹) {g : Î± â†’ Ï€ i}
  (hg : continuous_at g a) :
  continuous_at (Î» a, function.update (f a) i (g a)) a :=
hf.update i hg

lemma continuous.update [âˆ€i, topological_space (Ï€ i)] [topological_space Î±] [decidable_eq Î¹]
  {f : Î± â†’ Î  i, Ï€ i} (hf : continuous f) (i : Î¹) {g : Î± â†’ Ï€ i} (hg : continuous g) :
  continuous (Î» a, function.update (f a) i (g a)) :=
continuous_iff_continuous_at.2 $ Î» x, hf.continuous_at.update i hg.continuous_at

/-- `function.update f i x` is continuous in `(f, x)`. -/
@[continuity] lemma continuous_update [âˆ€i, topological_space (Ï€ i)] [decidable_eq Î¹] (i : Î¹) :
  continuous (Î» f : (Î  j, Ï€ j) Ã— Ï€ i, function.update f.1 i f.2) :=
continuous_fst.update i continuous_snd

lemma is_open_set_pi [âˆ€a, topological_space (Ï€ a)] {i : set Î¹} {s : Î a, set (Ï€ a)}
  (hi : finite i) (hs : âˆ€aâˆˆi, is_open (s a)) : is_open (pi i s) :=
by rw [pi_def]; exact (is_open_bInter hi $ assume a ha, (hs _ ha).preimage (continuous_apply _))

lemma is_closed_set_pi [âˆ€a, topological_space (Ï€ a)] {i : set Î¹} {s : Î a, set (Ï€ a)}
  (hs : âˆ€aâˆˆi, is_closed (s a)) : is_closed (pi i s) :=
by rw [pi_def];
  exact (is_closed_Inter $ Î» a, is_closed_Inter $ Î» ha, (hs _ ha).preimage (continuous_apply _))

lemma set_pi_mem_nhds [Î  a, topological_space (Ï€ a)] {i : set Î¹} {s : Î  a, set (Ï€ a)}
  {x : Î  a, Ï€ a} (hi : finite i) (hs : âˆ€ a âˆˆ i, s a âˆˆ ğ“ (x a)) :
  pi i s âˆˆ ğ“ x :=
by { rw [pi_def, bInter_mem hi], exact Î» a ha, (continuous_apply a).continuous_at (hs a ha) }

lemma pi_eq_generate_from [âˆ€a, topological_space (Ï€ a)] :
  Pi.topological_space =
  generate_from {g | âˆƒ(s:Î a, set (Ï€ a)) (i : finset Î¹), (âˆ€aâˆˆi, is_open (s a)) âˆ§ g = pi â†‘i s} :=
le_antisymm
  (le_generate_from $ assume g âŸ¨s, i, hi, eqâŸ©, eq.symm â–¸ is_open_set_pi (finset.finite_to_set _) hi)
  (le_infi $ assume a s âŸ¨t, ht, s_eqâŸ©, generate_open.basic _ $
    âŸ¨function.update (Î»a, univ) a t, {a}, by simpa using ht, by ext f; simp [s_eq.symm, pi]âŸ©)

lemma pi_generate_from_eq {g : Î a, set (set (Ï€ a))} :
  @Pi.topological_space Î¹ Ï€ (Î»a, generate_from (g a)) =
  generate_from {t | âˆƒ(s:Î a, set (Ï€ a)) (i : finset Î¹), (âˆ€aâˆˆi, s a âˆˆ g a) âˆ§ t = pi â†‘i s} :=
let G := {t | âˆƒ(s:Î a, set (Ï€ a)) (i : finset Î¹), (âˆ€aâˆˆi, s a âˆˆ g a) âˆ§ t = pi â†‘i s} in
begin
  rw [pi_eq_generate_from],
  refine le_antisymm (generate_from_mono _) (le_generate_from _),
  exact assume s âŸ¨t, i, ht, eqâŸ©, âŸ¨t, i, assume a ha, generate_open.basic _ (ht a ha), eqâŸ©,
  { rintros s âŸ¨t, i, hi, rflâŸ©,
    rw [pi_def],
    apply is_open_bInter (finset.finite_to_set _),
    assume a ha, show ((generate_from G).coinduced (Î»f:Î a, Ï€ a, f a)).is_open (t a),
    refine le_generate_from _ _ (hi a ha),
    exact assume s hs, generate_open.basic _ âŸ¨function.update (Î»a, univ) a s, {a}, by simp [hs]âŸ© }
end

lemma pi_generate_from_eq_fintype {g : Î a, set (set (Ï€ a))} [fintype Î¹] (hg : âˆ€a, â‹ƒâ‚€ g a = univ) :
  @Pi.topological_space Î¹ Ï€ (Î»a, generate_from (g a)) =
  generate_from {t | âˆƒ(s:Î a, set (Ï€ a)), (âˆ€a, s a âˆˆ g a) âˆ§ t = pi univ s} :=
begin
  rw [pi_generate_from_eq],
  refine le_antisymm (generate_from_mono _) (le_generate_from _),
  exact assume s âŸ¨t, ht, eqâŸ©, âŸ¨t, finset.univ, by simp [ht, eq]âŸ©,
  { rintros s âŸ¨t, i, ht, rflâŸ©,
    apply is_open_iff_forall_mem_open.2 _,
    assume f hf,
    choose c hc using show âˆ€a, âˆƒs, s âˆˆ g a âˆ§ f a âˆˆ s,
    { assume a, have : f a âˆˆ â‹ƒâ‚€ g a, { rw [hg], apply mem_univ }, simpa },
    refine âŸ¨pi univ (Î»a, if a âˆˆ i then t a else (c : Î a, set (Ï€ a)) a), _, _, _âŸ©,
    { simp [pi_if] },
    { refine generate_open.basic _ âŸ¨_, assume a, _, rflâŸ©,
      by_cases a âˆˆ i; simp [*, pi] at * },
    { have : f âˆˆ pi {a | a âˆ‰ i} c, { simp [*, pi] at * },
      simpa [pi_if, hf] } }
end

/-- Suppose `Ï€ i` is a family of topological spaces indexed by `i : Î¹`, and `X` is a type
endowed with a family of maps `f i : X â†’ Ï€ i` for every `i : Î¹`, hence inducing a
map `g : X â†’ Î  i, Ï€ i`. This lemma shows that infimum of the topologies on `X` induced by
the `f i` as `i : Î¹` varies is simply the topology on `X` induced by `g : X â†’ Î  i, Ï€ i`
where `Î  i, Ï€ i` is endowed with the usual product topology. -/
lemma inducing_infi_to_pi {X : Type*} [âˆ€ i, topological_space (Ï€ i)] (f : Î  i, X â†’ Ï€ i) :
  @inducing X (Î  i, Ï€ i) (â¨… i, induced (f i) infer_instance) _ (Î» x i, f i x) :=
begin
  constructor,
  erw induced_infi,
  congr' 1,
  funext,
  erw induced_compose,
end

variables [fintype Î¹] [âˆ€ i, topological_space (Ï€ i)] [âˆ€ i, discrete_topology (Ï€ i)]

/-- A finite product of discrete spaces is discrete. -/
instance Pi.discrete_topology : discrete_topology (Î  i, Ï€ i) :=
singletons_open_iff_discrete.mp (Î» x,
begin
  rw show {x} = â‹‚ i, {y : Î  i, Ï€ i | y i = x i},
  { ext, simp only [function.funext_iff, set.mem_singleton_iff, set.mem_Inter, set.mem_set_of_eq] },
  exact is_open_Inter (Î» i, (continuous_apply i).is_open_preimage {x i} (is_open_discrete {x i}))
end)

end pi

section sigma
variables {Î¹ : Type*} {Ïƒ : Î¹ â†’ Type*} [Î  i, topological_space (Ïƒ i)]

@[continuity]
lemma continuous_sigma_mk {i : Î¹} : continuous (@sigma.mk Î¹ Ïƒ i) :=
continuous_supr_rng continuous_coinduced_rng

lemma is_open_sigma_iff {s : set (sigma Ïƒ)} : is_open s â†” âˆ€ i, is_open (sigma.mk i â»Â¹' s) :=
by simp only [is_open_supr_iff, is_open_coinduced]

lemma is_closed_sigma_iff {s : set (sigma Ïƒ)} : is_closed s â†” âˆ€ i, is_closed (sigma.mk i â»Â¹' s) :=
by simp [â† is_open_compl_iff, is_open_sigma_iff]

lemma is_open_map_sigma_mk {i : Î¹} : is_open_map (@sigma.mk Î¹ Ïƒ i) :=
begin
  intros s hs,
  rw is_open_sigma_iff,
  intro j,
  classical,
  by_cases h : i = j,
  { subst j,
    convert hs,
    exact set.preimage_image_eq _ sigma_mk_injective },
  { convert is_open_empty,
    apply set.eq_empty_of_subset_empty,
    rintro x âŸ¨y, _, hyâŸ©,
    have : i = j, by cc,
    contradiction }
end

lemma is_open_range_sigma_mk {i : Î¹} : is_open (set.range (@sigma.mk Î¹ Ïƒ i)) :=
by { rw â†set.image_univ, exact is_open_map_sigma_mk _ is_open_univ }

lemma is_closed_map_sigma_mk {i : Î¹} : is_closed_map (@sigma.mk Î¹ Ïƒ i) :=
begin
  intros s hs,
  rw is_closed_sigma_iff,
  intro j,
  classical,
  by_cases h : i = j,
  { subst j,
    convert hs,
    exact set.preimage_image_eq _ sigma_mk_injective },
  { convert is_closed_empty,
    apply set.eq_empty_of_subset_empty,
    rintro x âŸ¨y, _, hyâŸ©,
    have : i = j, by cc,
    contradiction }
end

lemma is_closed_sigma_mk {i : Î¹} : is_closed (set.range (@sigma.mk Î¹ Ïƒ i)) :=
by { rw â†set.image_univ, exact is_closed_map_sigma_mk _ is_closed_univ }

lemma open_embedding_sigma_mk {i : Î¹} : open_embedding (@sigma.mk Î¹ Ïƒ i) :=
open_embedding_of_continuous_injective_open
  continuous_sigma_mk sigma_mk_injective is_open_map_sigma_mk

lemma closed_embedding_sigma_mk {i : Î¹} : closed_embedding (@sigma.mk Î¹ Ïƒ i) :=
closed_embedding_of_continuous_injective_closed
  continuous_sigma_mk sigma_mk_injective is_closed_map_sigma_mk

lemma embedding_sigma_mk {i : Î¹} : embedding (@sigma.mk Î¹ Ïƒ i) :=
closed_embedding_sigma_mk.1

/-- A map out of a sum type is continuous if its restriction to each summand is. -/
@[continuity]
lemma continuous_sigma [topological_space Î²] {f : sigma Ïƒ â†’ Î²}
  (h : âˆ€ i, continuous (Î» a, f âŸ¨i, aâŸ©)) : continuous f :=
continuous_supr_dom (Î» i, continuous_coinduced_dom (h i))

@[continuity]
lemma continuous_sigma_map {Îº : Type*} {Ï„ : Îº â†’ Type*} [Î  k, topological_space (Ï„ k)]
  {fâ‚ : Î¹ â†’ Îº} {fâ‚‚ : Î  i, Ïƒ i â†’ Ï„ (fâ‚ i)} (hf : âˆ€ i, continuous (fâ‚‚ i)) :
  continuous (sigma.map fâ‚ fâ‚‚) :=
continuous_sigma $ Î» i,
  show continuous (Î» a, sigma.mk (fâ‚ i) (fâ‚‚ i a)),
  from continuous_sigma_mk.comp (hf i)

lemma is_open_map_sigma [topological_space Î²] {f : sigma Ïƒ â†’ Î²}
  (h : âˆ€ i, is_open_map (Î» a, f âŸ¨i, aâŸ©)) : is_open_map f :=
begin
  intros s hs,
  rw is_open_sigma_iff at hs,
  have : s = â‹ƒ i, sigma.mk i '' (sigma.mk i â»Â¹' s),
  { rw Union_image_preimage_sigma_mk_eq_self },
  rw this,
  rw [image_Union],
  apply is_open_Union,
  intro i,
  rw [image_image],
  exact h i _ (hs i)
end

/-- The sum of embeddings is an embedding. -/
lemma embedding_sigma_map {Ï„ : Î¹ â†’ Type*} [Î  i, topological_space (Ï„ i)]
  {f : Î  i, Ïƒ i â†’ Ï„ i} (hf : âˆ€ i, embedding (f i)) : embedding (sigma.map id f) :=
begin
  refine âŸ¨âŸ¨_âŸ©, function.injective_id.sigma_map (Î» i, (hf i).inj)âŸ©,
  refine le_antisymm
    (continuous_iff_le_induced.mp (continuous_sigma_map (Î» i, (hf i).continuous))) _,
  intros s hs,
  replace hs := is_open_sigma_iff.mp hs,
  have : âˆ€ i, âˆƒ t, is_open t âˆ§ f i â»Â¹' t = sigma.mk i â»Â¹' s,
  { intro i,
    apply is_open_induced_iff.mp,
    convert hs i,
    exact (hf i).induced.symm },
  choose t ht using this,
  apply is_open_induced_iff.mpr,
  refine âŸ¨â‹ƒ i, sigma.mk i '' t i, is_open_Union (Î» i, is_open_map_sigma_mk _ (ht i).1), _âŸ©,
  ext âŸ¨i, xâŸ©,
  change (sigma.mk i (f i x) âˆˆ â‹ƒ (i : Î¹), sigma.mk i '' t i) â†” x âˆˆ sigma.mk i â»Â¹' s,
  rw [â†(ht i).2, mem_Union],
  split,
  { rintro âŸ¨j, hjâŸ©,
    rw mem_image at hj,
    rcases hj with âŸ¨y, hyâ‚, hyâ‚‚âŸ©,
    rcases sigma.mk.inj_iff.mp hyâ‚‚ with âŸ¨rfl, hyâŸ©,
    replace hy := eq_of_heq hy,
    subst y,
    exact hyâ‚ },
  { intro hx,
    use i,
    rw mem_image,
    exact âŸ¨f i x, hx, rflâŸ© }
end

end sigma

section ulift

@[continuity] lemma continuous_ulift_down [topological_space Î±] :
  continuous (ulift.down : ulift.{v u} Î± â†’ Î±) :=
continuous_induced_dom

@[continuity] lemma continuous_ulift_up [topological_space Î±] :
  continuous (ulift.up : Î± â†’ ulift.{v u} Î±) :=
continuous_induced_rng continuous_id

end ulift

lemma mem_closure_of_continuous [topological_space Î±] [topological_space Î²]
  {f : Î± â†’ Î²} {a : Î±} {s : set Î±} {t : set Î²}
  (hf : continuous f) (ha : a âˆˆ closure s) (h : maps_to f s (closure t)) :
  f a âˆˆ closure t :=
calc f a âˆˆ f '' closure s : mem_image_of_mem _ ha
  ... âŠ† closure (f '' s) : image_closure_subset_closure_image hf
  ... âŠ† closure t : closure_minimal h.image_subset is_closed_closure

lemma mem_closure_of_continuous2 [topological_space Î±] [topological_space Î²] [topological_space Î³]
  {f : Î± â†’ Î² â†’ Î³} {a : Î±} {b : Î²} {s : set Î±} {t : set Î²} {u : set Î³}
  (hf : continuous (Î»p:Î±Ã—Î², f p.1 p.2)) (ha : a âˆˆ closure s) (hb : b âˆˆ closure t)
  (h : âˆ€aâˆˆs, âˆ€bâˆˆt, f a b âˆˆ closure u) :
  f a b âˆˆ closure u :=
have (a,b) âˆˆ closure (set.prod s t),
  by simp [closure_prod_eq, ha, hb],
show f (a, b).1 (a, b).2 âˆˆ closure u,
  from @mem_closure_of_continuous (Î±Ã—Î²) _ _ _ (Î»p:Î±Ã—Î², f p.1 p.2) (a,b) _ u hf this $
    assume âŸ¨pâ‚, pâ‚‚âŸ© âŸ¨hâ‚, hâ‚‚âŸ©, h pâ‚ hâ‚ pâ‚‚ hâ‚‚
