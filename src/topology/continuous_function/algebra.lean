/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Nicol√≤ Cavalleri
-/
import topology.algebra.module
import topology.continuous_function.basic
import algebra.algebra.subalgebra

/-!
# Algebraic structures over continuous functions

In this file we define instances of algebraic structures over the type `continuous_map Œ± Œ≤`
(denoted `C(Œ±, Œ≤)`) of **bundled** continuous maps from `Œ±` to `Œ≤`. For example, `C(Œ±, Œ≤)`
is a group when `Œ≤` is a group, a ring when `Œ≤` is a ring, etc.

For each type of algebraic structure, we also define an appropriate subobject of `Œ± ‚Üí Œ≤`
with carrier `{ f : Œ± ‚Üí Œ≤ | continuous f }`. For example, when `Œ≤` is a group, a subgroup
`continuous_subgroup Œ± Œ≤` of `Œ± ‚Üí Œ≤` is constructed with carrier `{ f : Œ± ‚Üí Œ≤ | continuous f }`.

Note that, rather than using the derived algebraic structures on these subobjects
(for example, when `Œ≤` is a group, the derived group structure on `continuous_subgroup Œ± Œ≤`),
one should use `C(Œ±, Œ≤)` with the appropriate instance of the structure.
-/

local attribute [elab_simple] continuous.comp

namespace continuous_functions

variables {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
variables {f g : {f : Œ± ‚Üí Œ≤ | continuous f }}

instance : has_coe_to_fun {f : Œ± ‚Üí Œ≤ | continuous f} :=  ‚ü®_, subtype.val‚ü©

end continuous_functions

namespace continuous_map
variables {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]

@[to_additive]
instance has_mul [has_mul Œ≤] [has_continuous_mul Œ≤] : has_mul C(Œ±, Œ≤) :=
‚ü®Œª f g, ‚ü®f * g, continuous_mul.comp (f.continuous.prod_mk g.continuous : _)‚ü©‚ü©

@[simp, norm_cast, to_additive]
lemma coe_mul [has_mul Œ≤] [has_continuous_mul Œ≤] (f g : C(Œ±, Œ≤)) :
  ((f * g : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤) * (g : Œ± ‚Üí Œ≤) := rfl

@[to_additive]
instance [has_one Œ≤] : has_one C(Œ±, Œ≤) := ‚ü®const (1 : Œ≤)‚ü©

@[simp, norm_cast, to_additive]
lemma coe_one [has_one Œ≤]  :
  ((1 : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (1 : Œ± ‚Üí Œ≤) := rfl

@[simp, to_additive] lemma mul_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [semigroup Œ≥] [has_continuous_mul Œ≥] (f‚ÇÅ f‚ÇÇ : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) :
  (f‚ÇÅ * f‚ÇÇ).comp g = f‚ÇÅ.comp g * f‚ÇÇ.comp g :=
by { ext, simp, }

@[simp, to_additive] lemma one_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥] [has_one Œ≥] (g : C(Œ±, Œ≤)) :
  (1 : C(Œ≤, Œ≥)).comp g = 1 :=
by { ext, simp, }

end continuous_map

section group_structure

/-!
### Group stucture

In this section we show that continuous functions valued in a topological group inherit
the structure of a group.
-/

section subtype

/-- The `submonoid` of continuous maps `Œ± ‚Üí Œ≤`. -/
@[to_additive "The `add_submonoid` of continuous maps `Œ± ‚Üí Œ≤`. "]
def continuous_submonoid (Œ± : Type*) (Œ≤ : Type*) [topological_space Œ±] [topological_space Œ≤]
  [monoid Œ≤] [has_continuous_mul Œ≤] : submonoid (Œ± ‚Üí Œ≤) :=
{ carrier := { f : Œ± ‚Üí Œ≤ | continuous f },
  one_mem' := @continuous_const _ _ _ _ 1,
  mul_mem' := Œª f g fc gc, continuous.comp
    has_continuous_mul.continuous_mul (continuous.prod_mk fc gc : _) }

/-- The subgroup of continuous maps `Œ± ‚Üí Œ≤`. -/
@[to_additive "The `add_subgroup` of continuous maps `Œ± ‚Üí Œ≤`. "]
def continuous_subgroup (Œ± : Type*) (Œ≤ : Type*) [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] : subgroup (Œ± ‚Üí Œ≤) :=
{ inv_mem' := Œª f fc, continuous.comp (@topological_group.continuous_inv Œ≤ _ _ _) fc,
  ..continuous_submonoid Œ± Œ≤, }.

end subtype

namespace continuous_map

@[to_additive]
instance {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
  [topological_space Œ≤] [semigroup Œ≤] [has_continuous_mul Œ≤] : semigroup C(Œ±, Œ≤) :=
{ mul_assoc := Œª a b c, by ext; exact mul_assoc _ _ _,
  ..continuous_map.has_mul}

@[to_additive]
instance {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [monoid Œ≤] [has_continuous_mul Œ≤] : monoid C(Œ±, Œ≤) :=
{ one_mul := Œª a, by ext; exact one_mul _,
  mul_one := Œª a, by ext; exact mul_one _,
  ..continuous_map.semigroup,
  ..continuous_map.has_one }

/-- Coercion to a function as an `monoid_hom`. Similar to `monoid_hom.coe_fn`. -/
@[to_additive "Coercion to a function as an `add_monoid_hom`. Similar to `add_monoid_hom.coe_fn`.",
  simps]
def coe_fn_monoid_hom {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [monoid Œ≤] [has_continuous_mul Œ≤] : C(Œ±, Œ≤) ‚Üí* (Œ± ‚Üí Œ≤) :=
{ to_fun := coe_fn, map_one' := coe_one, map_mul' := coe_mul }

/-- Composition on the left by a (continuous) homomorphism of topological monoids, as a
`monoid_hom`. Similar to `monoid_hom.comp_left`. -/
@[to_additive "Composition on the left by a (continuous) homomorphism of topological `add_monoid`s,
as an `add_monoid_hom`. Similar to `add_monoid_hom.comp_left`.", simps]
protected def _root_.monoid_hom.comp_left_continuous (Œ± : Type*) {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [monoid Œ≤] [has_continuous_mul Œ≤]
  [topological_space Œ≥] [monoid Œ≥] [has_continuous_mul Œ≥] (g : Œ≤ ‚Üí* Œ≥) (hg : continuous g)  :
  C(Œ±, Œ≤) ‚Üí* C(Œ±, Œ≥) :=
{ to_fun := Œª f, (‚ü®g, hg‚ü© : C(Œ≤, Œ≥)).comp f,
  map_one' := ext $ Œª x, g.map_one,
  map_mul' := Œª f‚ÇÅ f‚ÇÇ, ext $ Œª x, g.map_mul _ _ }

/-- Composition on the right as a `monoid_hom`. Similar to `monoid_hom.comp_hom'`. -/
@[to_additive "Composition on the right as an `add_monoid_hom`. Similar to
`add_monoid_hom.comp_hom'`.", simps]
def comp_monoid_hom' {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [monoid Œ≥] [has_continuous_mul Œ≥] (g : C(Œ±, Œ≤)) : C(Œ≤, Œ≥) ‚Üí* C(Œ±, Œ≥) :=
{ to_fun := Œª f, f.comp g, map_one' := one_comp g, map_mul' := Œª f‚ÇÅ f‚ÇÇ, mul_comp f‚ÇÅ f‚ÇÇ g }

@[simp, norm_cast]
lemma coe_pow {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [monoid Œ≤] [has_continuous_mul Œ≤] (f : C(Œ±, Œ≤)) (n : ‚Ñï) :
  ((f^n : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤)^n :=
(coe_fn_monoid_hom : C(Œ±, Œ≤) ‚Üí* _).map_pow f n

@[simp] lemma pow_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [monoid Œ≥] [has_continuous_mul Œ≥] (f : C(Œ≤, Œ≥)) (n : ‚Ñï) (g : C(Œ±, Œ≤)) :
  (f^n).comp g = (f.comp g)^n :=
(comp_monoid_hom' g).map_pow f n

@[to_additive]
instance {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
[topological_space Œ≤] [comm_monoid Œ≤] [has_continuous_mul Œ≤] : comm_monoid C(Œ±, Œ≤) :=
{ one_mul := Œª a, by ext; exact one_mul _,
  mul_one := Œª a, by ext; exact mul_one _,
  mul_comm := Œª a b, by ext; exact mul_comm _ _,
  ..continuous_map.semigroup,
  ..continuous_map.has_one }

open_locale big_operators
@[simp, to_additive] lemma coe_prod {Œ± : Type*} {Œ≤ : Type*} [comm_monoid Œ≤]
  [topological_space Œ±] [topological_space Œ≤] [has_continuous_mul Œ≤]
  {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí C(Œ±, Œ≤)) :
  ‚áë(‚àè i in s, f i) = (‚àè i in s, (f i : Œ± ‚Üí Œ≤)) :=
(coe_fn_monoid_hom : C(Œ±, Œ≤) ‚Üí* _).map_prod f s

@[to_additive]
lemma prod_apply {Œ± : Type*} {Œ≤ : Type*} [comm_monoid Œ≤]
  [topological_space Œ±] [topological_space Œ≤] [has_continuous_mul Œ≤]
  {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí C(Œ±, Œ≤)) (a : Œ±) :
  (‚àè i in s, f i) a = (‚àè i in s, f i a) :=
by simp

@[to_additive]
instance {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] : group C(Œ±, Œ≤) :=
{ inv := Œª f, ‚ü®Œª x, (f x)‚Åª¬π, continuous_inv.comp f.continuous‚ü©,
  mul_left_inv := Œª a, by ext; exact mul_left_inv _,
  ..continuous_map.monoid }

@[simp, norm_cast, to_additive]
lemma coe_inv {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] (f : C(Œ±, Œ≤)) :
  ((f‚Åª¬π : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (f‚Åª¬π : Œ± ‚Üí Œ≤) :=
rfl

@[simp, norm_cast, to_additive]
lemma coe_div {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [group Œ≤] [topological_group Œ≤] (f g : C(Œ±, Œ≤)) :
  ((f / g : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤) / (g : Œ± ‚Üí Œ≤) :=
by { simp only [div_eq_mul_inv], refl, }

@[simp, to_additive] lemma inv_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [group Œ≥] [topological_group Œ≥] (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) :
  (f‚Åª¬π).comp g = (f.comp g)‚Åª¬π :=
by { ext, simp, }

@[simp, to_additive] lemma div_comp {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]
  [group Œ≥] [topological_group Œ≥] (f g : C(Œ≤, Œ≥)) (h : C(Œ±, Œ≤)) :
  (f / g).comp h = (f.comp h) / (g.comp h) :=
by { ext, simp, }

@[to_additive]
instance {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
  [topological_space Œ≤] [comm_group Œ≤] [topological_group Œ≤] : comm_group C(Œ±, Œ≤) :=
{ ..continuous_map.group,
  ..continuous_map.comm_monoid }

end continuous_map

end group_structure

section ring_structure

/-!
### Ring stucture

In this section we show that continuous functions valued in a topological ring `R` inherit
the structure of a ring.
-/

section subtype

/-- The subsemiring of continuous maps `Œ± ‚Üí Œ≤`. -/
def continuous_subsemiring (Œ± : Type*) (R : Type*) [topological_space Œ±] [topological_space R]
  [semiring R] [topological_ring R] : subsemiring (Œ± ‚Üí R) :=
{ ..continuous_add_submonoid Œ± R,
  ..continuous_submonoid Œ± R }.

/-- The subring of continuous maps `Œ± ‚Üí Œ≤`. -/
def continuous_subring (Œ± : Type*) (R : Type*) [topological_space Œ±] [topological_space R]
  [ring R] [topological_ring R] : subring (Œ± ‚Üí R) :=
{ ..continuous_subsemiring Œ± R,
  ..continuous_add_subgroup Œ± R }.

end subtype

namespace continuous_map

instance {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [semiring Œ≤] [topological_ring Œ≤] : semiring C(Œ±, Œ≤) :=
{ left_distrib := Œª a b c, by ext; exact left_distrib _ _ _,
  right_distrib := Œª a b c, by ext; exact right_distrib _ _ _,
  zero_mul := Œª a, by ext; exact zero_mul _,
  mul_zero := Œª a, by ext; exact mul_zero _,
  ..continuous_map.add_comm_monoid,
  ..continuous_map.monoid }

instance {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [ring Œ≤] [topological_ring Œ≤] : ring C(Œ±, Œ≤) :=
{ ..continuous_map.semiring,
  ..continuous_map.add_comm_group, }

instance {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
  [topological_space Œ≤] [comm_ring Œ≤] [topological_ring Œ≤] : comm_ring C(Œ±, Œ≤) :=
{ ..continuous_map.semiring,
  ..continuous_map.add_comm_group,
  ..continuous_map.comm_monoid,}

/-- Composition on the left by a (continuous) homomorphism of topological rings, as a `ring_hom`.
Similar to `ring_hom.comp_left`. -/
@[simps] protected def _root_.ring_hom.comp_left_continuous (Œ± : Type*) {Œ≤ : Type*} {Œ≥ : Type*}
  [topological_space Œ±] [topological_space Œ≤] [semiring Œ≤] [topological_ring Œ≤]
  [topological_space Œ≥] [semiring Œ≥] [topological_ring Œ≥] (g : Œ≤ ‚Üí+* Œ≥) (hg : continuous g) :
  C(Œ±, Œ≤) ‚Üí+* C(Œ±, Œ≥) :=
{ .. g.to_monoid_hom.comp_left_continuous Œ± hg,
  .. g.to_add_monoid_hom.comp_left_continuous Œ± hg }

/-- Coercion to a function as a `ring_hom`. -/
@[simps]
def coe_fn_ring_hom {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  [ring Œ≤] [topological_ring Œ≤] : C(Œ±, Œ≤) ‚Üí+* (Œ± ‚Üí Œ≤) :=
{ to_fun := coe_fn,
  ..(coe_fn_monoid_hom : C(Œ±, Œ≤) ‚Üí* _),
  ..(coe_fn_add_monoid_hom : C(Œ±, Œ≤) ‚Üí+ _) }

end continuous_map

end ring_structure

local attribute [ext] subtype.eq

section module_structure

/-!
### Semiodule stucture

In this section we show that continuous functions valued in a topological module `M` over a
topological semiring `R` inherit the structure of a module.
-/

section subtype

variables (Œ± : Type*) [topological_space Œ±]
variables (R : Type*) [semiring R] [topological_space R]
variables (M : Type*) [topological_space M] [add_comm_group M]
variables [module R M] [has_continuous_smul R M] [topological_add_group M]

/-- The `R`-submodule of continuous maps `Œ± ‚Üí M`. -/
def continuous_submodule : submodule R (Œ± ‚Üí M) :=
{ carrier := { f : Œ± ‚Üí M | continuous f },
  smul_mem' := Œª c f hf, continuous_smul.comp
    (continuous.prod_mk (continuous_const : continuous (Œª x, c)) hf),
  ..continuous_add_subgroup Œ± M }

end subtype

namespace continuous_map
variables {Œ± : Type*} [topological_space Œ±]
  {R : Type*} [semiring R] [topological_space R]
  {M : Type*} [topological_space M] [add_comm_monoid M]
  {M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_monoid M‚ÇÇ]

instance
  [module R M] [has_continuous_smul R M] :
  has_scalar R C(Œ±, M) :=
‚ü®Œª r f, ‚ü®r ‚Ä¢ f, f.continuous.const_smul r‚ü©‚ü©

@[simp, norm_cast]
lemma coe_smul [module R M] [has_continuous_smul R M]
  (c : R) (f : C(Œ±, M)) : ‚áë(c ‚Ä¢ f) = c ‚Ä¢ f := rfl

lemma smul_apply [module R M] [has_continuous_smul R M]
  (c : R) (f : C(Œ±, M)) (a : Œ±) : (c ‚Ä¢ f) a = c ‚Ä¢ (f a) :=
by simp

@[simp] lemma smul_comp {Œ± : Type*} {Œ≤ : Type*}
  [topological_space Œ±] [topological_space Œ≤]
   [module R M] [has_continuous_smul R M] (r : R) (f : C(Œ≤, M)) (g : C(Œ±, Œ≤)) :
  (r ‚Ä¢ f).comp g = r ‚Ä¢ (f.comp g) :=
by { ext, simp, }

variables [has_continuous_add M] [module R M] [has_continuous_smul R M]
variables [has_continuous_add M‚ÇÇ] [module R M‚ÇÇ] [has_continuous_smul R M‚ÇÇ]

instance module : module R C(Œ±, M) :=
{ smul     := (‚Ä¢),
  smul_add := Œª c f g, by { ext, exact smul_add c (f x) (g x) },
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, by { ext, exact add_smul c‚ÇÅ c‚ÇÇ (f x) },
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, by { ext, exact mul_smul c‚ÇÅ c‚ÇÇ (f x) },
  one_smul := Œª f, by { ext, exact one_smul R (f x) },
  zero_smul := Œª f, by { ext, exact zero_smul _ _ },
  smul_zero := Œª r, by { ext, exact smul_zero _ } }

variables (R)

/-- Composition on the left by a continuous linear map, as a `linear_map`.
Similar to `linear_map.comp_left`. -/
@[simps] protected def _root_.continuous_linear_map.comp_left_continuous (Œ± : Type*)
  [topological_space Œ±] (g : M ‚ÜíL[R] M‚ÇÇ) :
  C(Œ±, M) ‚Üí‚Çó[R] C(Œ±, M‚ÇÇ) :=
{ map_smul' := Œª c f, ext $ Œª x, g.map_smul' c _,
  .. g.to_linear_map.to_add_monoid_hom.comp_left_continuous Œ± g.continuous }

/-- Coercion to a function as a `linear_map`. -/
@[simps]
def coe_fn_linear_map : C(Œ±, M) ‚Üí‚Çó[R] (Œ± ‚Üí M) :=
{ to_fun := coe_fn,
  map_smul' := coe_smul,
  ..(coe_fn_add_monoid_hom : C(Œ±, M) ‚Üí+ _) }

end continuous_map

end module_structure

section algebra_structure

/-!
### Algebra structure

In this section we show that continuous functions valued in a topological algebra `A` over a ring
`R` inherit the structure of an algebra. Note that the hypothesis that `A` is a topological algebra
is obtained by requiring that `A` be both a `has_continuous_smul` and a `topological_ring`.-/

section subtype

variables {Œ± : Type*} [topological_space Œ±]
{R : Type*} [comm_semiring R]
{A : Type*} [topological_space A] [semiring A]
[algebra R A] [topological_ring A]

/-- The `R`-subalgebra of continuous maps `Œ± ‚Üí A`. -/
def continuous_subalgebra : subalgebra R (Œ± ‚Üí A) :=
{ carrier := { f : Œ± ‚Üí A | continuous f },
  algebra_map_mem' := Œª r, (continuous_const : continuous $ Œª (x : Œ±), algebra_map R A r),
  ..continuous_subsemiring Œ± A }

end subtype

section continuous_map

variables {Œ± : Type*} [topological_space Œ±]
{R : Type*} [comm_semiring R]
{A : Type*} [topological_space A] [semiring A]
[algebra R A] [topological_ring A]
{A‚ÇÇ : Type*} [topological_space A‚ÇÇ] [semiring A‚ÇÇ]
[algebra R A‚ÇÇ] [topological_ring A‚ÇÇ]

/-- Continuous constant functions as a `ring_hom`. -/
def continuous_map.C : R ‚Üí+* C(Œ±, A) :=
{ to_fun    := Œª c : R, ‚ü®Œª x: Œ±, ((algebra_map R A) c), continuous_const‚ü©,
  map_one'  := by ext x; exact (algebra_map R A).map_one,
  map_mul'  := Œª c‚ÇÅ c‚ÇÇ, by ext x; exact (algebra_map R A).map_mul _ _,
  map_zero' := by ext x; exact (algebra_map R A).map_zero,
  map_add'  := Œª c‚ÇÅ c‚ÇÇ, by ext x; exact (algebra_map R A).map_add _ _ }

@[simp] lemma continuous_map.C_apply (r : R) (a : Œ±) : continuous_map.C r a = algebra_map R A r :=
rfl

variables [topological_space R] [has_continuous_smul R A] [has_continuous_smul R A‚ÇÇ]

instance continuous_map.algebra : algebra R C(Œ±, A) :=
{ to_ring_hom := continuous_map.C,
  commutes' := Œª c f, by ext x; exact algebra.commutes' _ _,
  smul_def' := Œª c f, by ext x; exact algebra.smul_def' _ _, }

variables (R)

/-- Composition on the left by a (continuous) homomorphism of topological `R`-algebras, as an
`alg_hom`. Similar to `alg_hom.comp_left`. -/
@[simps] protected def alg_hom.comp_left_continuous {Œ± : Type*} [topological_space Œ±]
  (g : A ‚Üí‚Çê[R] A‚ÇÇ) (hg : continuous g) :
  C(Œ±, A) ‚Üí‚Çê[R] C(Œ±, A‚ÇÇ) :=
{ commutes' := Œª c, continuous_map.ext $ Œª _, g.commutes' _,
  .. g.to_ring_hom.comp_left_continuous Œ± hg }

/-- Coercion to a function as an `alg_hom`. -/
@[simps]
def continuous_map.coe_fn_alg_hom : C(Œ±, A) ‚Üí‚Çê[R] (Œ± ‚Üí A) :=
{ to_fun := coe_fn,
  commutes' := Œª r, rfl,
  -- `..(continuous_map.coe_fn_ring_hom : C(Œ±, A) ‚Üí+* _)` times out for some reason
  map_zero' := continuous_map.coe_zero,
  map_one' := continuous_map.coe_one,
  map_add' := continuous_map.coe_add,
  map_mul' := continuous_map.coe_mul }

instance: is_scalar_tower R A C(Œ±, A) :=
{ smul_assoc := Œª _ _ _, by { ext, simp } }

variables {R}

/--
A version of `separates_points` for subalgebras of the continuous functions,
used for stating the Stone-Weierstrass theorem.
-/
abbreviation subalgebra.separates_points (s : subalgebra R C(Œ±, A)) : Prop :=
set.separates_points ((Œª f : C(Œ±, A), (f : Œ± ‚Üí A)) '' (s : set C(Œ±, A)))

lemma subalgebra.separates_points_monotone :
  monotone (Œª s : subalgebra R C(Œ±, A), s.separates_points) :=
Œª s s' r h x y n,
begin
  obtain ‚ü®f, m, w‚ü© := h n,
  rcases m with ‚ü®f, ‚ü®m, rfl‚ü©‚ü©,
  exact ‚ü®_, ‚ü®f, ‚ü®r m, rfl‚ü©‚ü©, w‚ü©,
end

@[simp] lemma algebra_map_apply (k : R) (a : Œ±) :
  algebra_map R C(Œ±, A) k a = k ‚Ä¢ 1 :=
by { rw algebra.algebra_map_eq_smul_one, refl, }

variables {ùïú : Type*} [topological_space ùïú]

/--
A set of continuous maps "separates points strongly"
if for each pair of distinct points there is a function with specified values on them.

We give a slightly unusual formulation, where the specified values are given by some
function `v`, and we ask `f x = v x ‚àß f y = v y`. This avoids needing a hypothesis `x ‚â† y`.

In fact, this definition would work perfectly well for a set of non-continuous functions,
but as the only current use case is in the Stone-Weierstrass theorem,
writing it this way avoids having to deal with casts inside the set.
(This may need to change if we do Stone-Weierstrass on non-compact spaces,
where the functions would be continuous functions vanishing at infinity.)
-/
def set.separates_points_strongly (s : set C(Œ±, ùïú)) : Prop :=
‚àÄ (v : Œ± ‚Üí ùïú) (x y : Œ±), ‚àÉ f : s, (f x : ùïú) = v x ‚àß f y = v y

variables [field ùïú] [topological_ring ùïú]

/--
Working in continuous functions into a topological field,
a subalgebra of functions that separates points also separates points strongly.

By the hypothesis, we can find a function `f` so `f x ‚â† f y`.
By an affine transformation in the field we can arrange so that `f x = a` and `f x = b`.
-/
lemma subalgebra.separates_points.strongly {s : subalgebra ùïú C(Œ±, ùïú)} (h : s.separates_points) :
  (s : set C(Œ±, ùïú)).separates_points_strongly :=
Œª v x y,
begin
  by_cases n : x = y,
  { subst n,
    use ((v x) ‚Ä¢ 1 : C(Œ±, ùïú)),
    { apply s.smul_mem,
      apply s.one_mem, },
    { simp, }, },
  obtain ‚ü®f, ‚ü®f, ‚ü®m, rfl‚ü©‚ü©, w‚ü© := h n,
  replace w : f x - f y ‚â† 0 := sub_ne_zero_of_ne w,
  let a := v x,
  let b := v y,
  let f' := ((b - a) * (f x - f y)‚Åª¬π) ‚Ä¢ (continuous_map.C (f x) - f) + continuous_map.C a,
  refine ‚ü®‚ü®f', _‚ü©, _, _‚ü©,
  { simp only [f', set_like.mem_coe, subalgebra.mem_to_submodule],
    -- TODO should there be a tactic for this?
    -- We could add an attribute `@[subobject_mem]`, and a tactic
    -- ``def subobject_mem := `[solve_by_elim with subobject_mem { max_depth := 10 }]``
    solve_by_elim
      [subalgebra.add_mem, subalgebra.smul_mem, subalgebra.sub_mem, subalgebra.algebra_map_mem]
      { max_depth := 6 }, },
  { simp [f'], },
  { simp [f', inv_mul_cancel_right' w], },
end

end continuous_map

-- TODO[gh-6025]: make this an instance once safe to do so
lemma continuous_map.subsingleton_subalgebra (Œ± : Type*) [topological_space Œ±]
  (R : Type*) [comm_semiring R] [topological_space R] [topological_ring R]
  [subsingleton Œ±] : subsingleton (subalgebra R C(Œ±, R)) :=
begin
  fsplit,
  intros s‚ÇÅ s‚ÇÇ,
  by_cases n : nonempty Œ±,
  { obtain ‚ü®x‚ü© := n,
    ext f,
    have h : f = algebra_map R C(Œ±, R) (f x),
    { ext x', simp only [mul_one, algebra.id.smul_eq_mul, algebra_map_apply], congr, },
    rw h,
    simp only [subalgebra.algebra_map_mem], },
  { ext f,
    have h : f = 0,
    { ext x', exact false.elim (n ‚ü®x'‚ü©), },
    subst h,
    simp only [subalgebra.zero_mem], },
end

end algebra_structure

section module_over_continuous_functions

/-!
### Structure as module over scalar functions

If `M` is a module over `R`, then we show that the space of continuous functions from `Œ±` to `M`
is naturally a module over the ring of continuous functions from `Œ±` to `R`. -/

namespace continuous_map

instance has_scalar' {Œ± : Type*} [topological_space Œ±]
  {R : Type*} [semiring R] [topological_space R]
  {M : Type*} [topological_space M] [add_comm_monoid M]
  [module R M] [has_continuous_smul R M] :
  has_scalar C(Œ±, R) C(Œ±, M) :=
‚ü®Œª f g, ‚ü®Œª x, (f x) ‚Ä¢ (g x), (continuous.smul f.2 g.2)‚ü©‚ü©

instance module' {Œ± : Type*} [topological_space Œ±]
  (R : Type*) [ring R] [topological_space R] [topological_ring R]
  (M : Type*) [topological_space M] [add_comm_monoid M] [has_continuous_add M]
  [module R M] [has_continuous_smul R M] :
  module C(Œ±, R) C(Œ±, M) :=
{ smul     := (‚Ä¢),
  smul_add := Œª c f g, by ext x; exact smul_add (c x) (f x) (g x),
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, by ext x; exact add_smul (c‚ÇÅ x) (c‚ÇÇ x) (f x),
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, by ext x; exact mul_smul (c‚ÇÅ x) (c‚ÇÇ x) (f x),
  one_smul := Œª f, by ext x; exact one_smul R (f x),
  zero_smul := Œª f, by ext x; exact zero_smul _ _,
  smul_zero := Œª r, by ext x; exact smul_zero _, }

end continuous_map

end module_over_continuous_functions

/-!
We now provide formulas for `f ‚äì g` and `f ‚äî g`, where `f g : C(Œ±, Œ≤)`,
in terms of `continuous_map.abs`.
-/

section
variables {R : Type*} [linear_ordered_field R]

-- TODO:
-- This lemma (and the next) could go all the way back in `algebra.ordered_field`,
-- except that it is tedious to prove without tactics.
-- Rather than stranding it at some intermediate location,
-- it's here, immediately prior to the point of use.
lemma min_eq_half_add_sub_abs_sub {x y : R} : min x y = 2‚Åª¬π * (x + y - abs (x - y)) :=
begin
  dsimp [min, max, abs],
  simp only [neg_le_self_iff, if_congr, sub_nonneg, neg_sub],
  split_ifs; ring_nf; linarith,
end

lemma max_eq_half_add_add_abs_sub {x y : R} : max x y = 2‚Åª¬π * (x + y + abs (x - y)) :=
begin
  dsimp [min, max, abs],
  simp only [neg_le_self_iff, if_congr, sub_nonneg, neg_sub],
  split_ifs; ring_nf; linarith,
end
end

namespace continuous_map

section lattice
variables {Œ± : Type*} [topological_space Œ±]
variables {Œ≤ : Type*} [linear_ordered_field Œ≤] [topological_space Œ≤]
  [order_topology Œ≤] [topological_ring Œ≤]

lemma inf_eq (f g : C(Œ±, Œ≤)) : f ‚äì g = (2‚Åª¬π : Œ≤) ‚Ä¢ (f + g - (f - g).abs) :=
ext (Œª x, by simpa using min_eq_half_add_sub_abs_sub)

-- Not sure why this is grosser than `inf_eq`:
lemma sup_eq (f g : C(Œ±, Œ≤)) : f ‚äî g = (2‚Åª¬π : Œ≤) ‚Ä¢ (f + g + (f - g).abs) :=
ext (Œª x, by simpa [mul_add] using @max_eq_half_add_add_abs_sub _ _ (f x) (g x))

end lattice

end continuous_map
