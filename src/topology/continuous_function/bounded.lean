/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Mario Carneiro, Yury Kudryashov, Heather Macbeth
-/
import analysis.normed.order.lattice
import analysis.normed_space.operator_norm
import analysis.normed_space.star.basic
import data.real.sqrt
import topology.continuous_function.algebra
import topology.metric_space.equicontinuity

/-!
# Bounded continuous functions

The type of bounded continuous functions taking values in a metric space, with
the uniform distance.

-/

noncomputable theory
open_locale topology classical nnreal uniformity uniform_convergence

open set filter metric function

universes u v w
variables {F : Type*} {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

/-- `Œ± ‚Üí·µá Œ≤` is the type of bounded continuous functions `Œ± ‚Üí Œ≤` from a topological space to a
metric space.

When possible, instead of parametrizing results over `(f : Œ± ‚Üí·µá Œ≤)`,
you should parametrize over `(F : Type*) [bounded_continuous_map_class F Œ± Œ≤] (f : F)`.

When you extend this structure, make sure to extend `bounded_continuous_map_class`. -/
structure bounded_continuous_function (Œ± : Type u) (Œ≤ : Type v)
  [topological_space Œ±] [pseudo_metric_space Œ≤] extends continuous_map Œ± Œ≤ :
  Type (max u v) :=
(map_bounded' : ‚àÉ C, ‚àÄ x y, dist (to_fun x) (to_fun y) ‚â§ C)

localized "infixr (name := bounded_continuous_function)
  ` ‚Üí·µá `:25 := bounded_continuous_function" in bounded_continuous_function

section
set_option old_structure_cmd true

/-- `bounded_continuous_map_class F Œ± Œ≤` states that `F` is a type of bounded continuous maps.

You should also extend this typeclass when you extend `bounded_continuous_function`. -/
class bounded_continuous_map_class (F Œ± Œ≤ : Type*) [topological_space Œ±] [pseudo_metric_space Œ≤]
  extends continuous_map_class F Œ± Œ≤ :=
(map_bounded (f : F) : ‚àÉ C, ‚àÄ x y, dist (f x) (f y) ‚â§ C)

end

export bounded_continuous_map_class (map_bounded)

namespace bounded_continuous_function
section basics
variables [topological_space Œ±] [pseudo_metric_space Œ≤] [pseudo_metric_space Œ≥]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance : bounded_continuous_map_class (Œ± ‚Üí·µá Œ≤) Œ± Œ≤ :=
{ coe := Œª f, f.to_fun,
  coe_injective' := Œª f g h, by { obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := f, obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := g, congr' },
  map_continuous := Œª f, f.continuous_to_fun,
  map_bounded := Œª f, f.map_bounded' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`
directly. -/
instance : has_coe_to_fun (Œ± ‚Üí·µá Œ≤) (Œª _, Œ± ‚Üí Œ≤) := fun_like.has_coe_to_fun

instance [bounded_continuous_map_class F Œ± Œ≤] : has_coe_t F (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œª f, { to_fun := f, continuous_to_fun := map_continuous f, map_bounded' := map_bounded f }‚ü©

@[simp] lemma coe_to_continuous_fun (f : Œ± ‚Üí·µá Œ≤) : (f.to_continuous_map : Œ± ‚Üí Œ≤) = f := rfl

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def simps.apply (h : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí Œ≤ := h

initialize_simps_projections bounded_continuous_function (to_continuous_map_to_fun ‚Üí apply)

protected lemma bounded (f : Œ± ‚Üí·µá Œ≤) : ‚àÉC, ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C := f.map_bounded'
protected lemma continuous (f : Œ± ‚Üí·µá Œ≤) : continuous f := f.to_continuous_map.continuous

@[ext] lemma ext (h : ‚àÄ x, f x = g x) : f = g := fun_like.ext _ _ h

lemma bounded_range (f : Œ± ‚Üí·µá Œ≤) : bounded (range f) :=
bounded_range_iff.2 f.bounded

lemma bounded_image (f : Œ± ‚Üí·µá Œ≤) (s : set Œ±) : bounded (f '' s) :=
f.bounded_range.mono $ image_subset_range _ _

lemma eq_of_empty [is_empty Œ±] (f g : Œ± ‚Üí·µá Œ≤) : f = g :=
ext $ is_empty.elim ‚Äπ_‚Ä∫

/-- A continuous function with an explicit bound is a bounded continuous function. -/
def mk_of_bound (f : C(Œ±, Œ≤)) (C : ‚Ñù) (h : ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
‚ü®f, ‚ü®C, h‚ü©‚ü©

@[simp] lemma mk_of_bound_coe {f} {C} {h} : (mk_of_bound f C h : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤) :=
rfl

/-- A continuous function on a compact space is automatically a bounded continuous function. -/
def mk_of_compact [compact_space Œ±] (f : C(Œ±, Œ≤)) : Œ± ‚Üí·µá Œ≤ :=
‚ü®f, bounded_range_iff.1 (is_compact_range f.continuous).bounded‚ü©

@[simp] lemma mk_of_compact_apply [compact_space Œ±] (f : C(Œ±, Œ≤)) (a : Œ±) :
  mk_of_compact f a = f a :=
rfl

/-- If a function is bounded on a discrete space, it is automatically continuous,
and therefore gives rise to an element of the type of bounded continuous functions -/
@[simps] def mk_of_discrete [discrete_topology Œ±] (f : Œ± ‚Üí Œ≤)
  (C : ‚Ñù) (h : ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
‚ü®‚ü®f, continuous_of_discrete_topology‚ü©, ‚ü®C, h‚ü©‚ü©

/-- The uniform distance between two bounded continuous functions -/
instance : has_dist (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf g, Inf {C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C}‚ü©

lemma dist_eq : dist f g = Inf {C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C} := rfl

lemma dist_set_exists : ‚àÉ C, 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C :=
begin
  rcases f.bounded_range.union g.bounded_range with ‚ü®C, hC‚ü©,
  refine ‚ü®max 0 C, le_max_left _ _, Œª x, (hC _ _ _ _).trans (le_max_right _ _)‚ü©;
    [left, right]; apply mem_range_self
end

/-- The pointwise distance is controlled by the distance between functions, by definition. -/
lemma dist_coe_le_dist (x : Œ±) : dist (f x) (g x) ‚â§ dist f g :=
le_cInf dist_set_exists $ Œªb hb, hb.2 x

/- This lemma will be needed in the proof of the metric space instance, but it will become
useless afterwards as it will be superseded by the general result that the distance is nonnegative
in metric spaces. -/
private lemma dist_nonneg' : 0 ‚â§ dist f g :=
le_cInf dist_set_exists (Œª C, and.left)

/-- The distance between two functions is controlled by the supremum of the pointwise distances -/
lemma dist_le (C0 : (0 : ‚Ñù) ‚â§ C) : dist f g ‚â§ C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) ‚â§ C :=
‚ü®Œª h x, le_trans (dist_coe_le_dist x) h, Œª H, cInf_le ‚ü®0, Œª C, and.left‚ü© ‚ü®C0, H‚ü©‚ü©

lemma dist_le_iff_of_nonempty [nonempty Œ±] :
  dist f g ‚â§ C ‚Üî ‚àÄ x, dist (f x) (g x) ‚â§ C :=
‚ü®Œª h x, le_trans (dist_coe_le_dist x) h,
 Œª w, (dist_le (le_trans dist_nonneg (w (nonempty.some ‚Äπ_‚Ä∫)))).mpr w‚ü©

lemma dist_lt_of_nonempty_compact [nonempty Œ±] [compact_space Œ±]
  (w : ‚àÄx:Œ±, dist (f x) (g x) < C) : dist f g < C :=
begin
  have c : continuous (Œª x, dist (f x) (g x)), { continuity, },
  obtain ‚ü®x, -, le‚ü© :=
    is_compact.exists_forall_ge is_compact_univ set.univ_nonempty (continuous.continuous_on c),
  exact lt_of_le_of_lt (dist_le_iff_of_nonempty.mpr (Œª y, le y trivial)) (w x),
end

lemma dist_lt_iff_of_compact [compact_space Œ±] (C0 : (0 : ‚Ñù) < C) :
  dist f g < C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) < C :=
begin
  fsplit,
  { intros w x,
    exact lt_of_le_of_lt (dist_coe_le_dist x) w, },
  { by_cases h : nonempty Œ±,
    { resetI,
      exact dist_lt_of_nonempty_compact, },
    { rintro -,
      convert C0,
      apply le_antisymm _ dist_nonneg',
      rw [dist_eq],
      exact cInf_le ‚ü®0, Œª C, and.left‚ü© ‚ü®le_rfl, Œª x, false.elim (h (nonempty.intro x))‚ü©, }, },
end

lemma dist_lt_iff_of_nonempty_compact [nonempty Œ±] [compact_space Œ±] :
  dist f g < C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) < C :=
‚ü®Œª w x, lt_of_le_of_lt (dist_coe_le_dist x) w, dist_lt_of_nonempty_compact‚ü©

/-- The type of bounded continuous functions, with the uniform distance, is a pseudometric space. -/
instance : pseudo_metric_space (Œ± ‚Üí·µá Œ≤) :=
{ dist_self := Œª f, le_antisymm ((dist_le le_rfl).2 $ Œª x, by simp) dist_nonneg',
  dist_comm := Œª f g, by simp [dist_eq, dist_comm],
  dist_triangle := Œª f g h,
    (dist_le (add_nonneg dist_nonneg' dist_nonneg')).2 $ Œª x,
      le_trans (dist_triangle _ _ _) (add_le_add (dist_coe_le_dist _) (dist_coe_le_dist _)) }

/-- The type of bounded continuous functions, with the uniform distance, is a metric space. -/
instance {Œ± Œ≤} [topological_space Œ±] [metric_space Œ≤] : metric_space (Œ± ‚Üí·µá Œ≤) :=
{ eq_of_dist_eq_zero := Œª f g hfg, by ext x; exact
    eq_of_dist_eq_zero (le_antisymm (hfg ‚ñ∏ dist_coe_le_dist _) dist_nonneg) }

lemma nndist_eq : nndist f g = Inf {C | ‚àÄ x : Œ±, nndist (f x) (g x) ‚â§ C} :=
subtype.ext $ dist_eq.trans $ begin
  rw [nnreal.coe_Inf, nnreal.coe_image],
  simp_rw [mem_set_of_eq, ‚Üênnreal.coe_le_coe, subtype.coe_mk, exists_prop, coe_nndist],
end

lemma nndist_set_exists : ‚àÉ C, ‚àÄ x : Œ±, nndist (f x) (g x) ‚â§ C :=
subtype.exists.mpr $ dist_set_exists.imp $ Œª a ‚ü®ha, h‚ü©, ‚ü®ha, h‚ü©

lemma nndist_coe_le_nndist (x : Œ±) : nndist (f x) (g x) ‚â§ nndist f g :=
dist_coe_le_dist x

/-- On an empty space, bounded continuous functions are at distance 0 -/
lemma dist_zero_of_empty [is_empty Œ±] : dist f g = 0 :=
by rw [(ext is_empty_elim : f = g), dist_self]

lemma dist_eq_supr : dist f g = ‚®Ü x : Œ±, dist (f x) (g x) :=
begin
  casesI is_empty_or_nonempty Œ±, { rw [supr_of_empty', real.Sup_empty, dist_zero_of_empty] },
  refine (dist_le_iff_of_nonempty.mpr $ le_csupr _).antisymm (csupr_le dist_coe_le_dist),
  exact dist_set_exists.imp (Œª C hC, forall_range_iff.2 hC.2)
end

lemma nndist_eq_supr : nndist f g = ‚®Ü x : Œ±, nndist (f x) (g x) :=
subtype.ext $ dist_eq_supr.trans $ by simp_rw [nnreal.coe_supr, coe_nndist]

lemma tendsto_iff_tendsto_uniformly {Œπ : Type*} {F : Œπ ‚Üí (Œ± ‚Üí·µá Œ≤)} {f : Œ± ‚Üí·µá Œ≤} {l : filter Œπ} :
  tendsto F l (ùìù f) ‚Üî tendsto_uniformly (Œª i, F i) f l :=
iff.intro
  (Œª h, tendsto_uniformly_iff.2
    (Œª Œµ Œµ0, (metric.tendsto_nhds.mp h Œµ Œµ0).mp (eventually_of_forall $
    Œª n hn x, lt_of_le_of_lt (dist_coe_le_dist x) (dist_comm (F n) f ‚ñ∏ hn))))
  (Œª h, metric.tendsto_nhds.mpr $ Œª Œµ Œµ_pos,
    (h _ (dist_mem_uniformity $ half_pos Œµ_pos)).mp (eventually_of_forall $
    Œª n hn, lt_of_le_of_lt ((dist_le (half_pos Œµ_pos).le).mpr $
    Œª x, dist_comm (f x) (F n x) ‚ñ∏ le_of_lt (hn x)) (half_lt_self Œµ_pos)))

/-- The topology on `Œ± ‚Üí·µá Œ≤` is exactly the topology induced by the natural map to `Œ± ‚Üí·µ§ Œ≤`. -/
lemma inducing_coe_fn : inducing (uniform_fun.of_fun ‚àò coe_fn : (Œ± ‚Üí·µá Œ≤) ‚Üí (Œ± ‚Üí·µ§ Œ≤)) :=
begin
  rw inducing_iff_nhds,
  refine Œª f, eq_of_forall_le_iff (Œª l, _),
  rw [‚Üê tendsto_iff_comap, ‚Üê tendsto_id', tendsto_iff_tendsto_uniformly,
      uniform_fun.tendsto_iff_tendsto_uniformly],
  refl
end

-- TODO: upgrade to a `uniform_embedding`
lemma embedding_coe_fn : embedding (uniform_fun.of_fun ‚àò coe_fn : (Œ± ‚Üí·µá Œ≤) ‚Üí (Œ± ‚Üí·µ§ Œ≤)) :=
‚ü®inducing_coe_fn, Œª f g h, ext $ Œª x, congr_fun h x‚ü©

variables (Œ±) {Œ≤}

/-- Constant as a continuous bounded function. -/
@[simps {fully_applied := ff}] def const (b : Œ≤) : Œ± ‚Üí·µá Œ≤ :=
‚ü®continuous_map.const Œ± b, 0, by simp [le_rfl]‚ü©

variable {Œ±}

lemma const_apply' (a : Œ±) (b : Œ≤) : (const Œ± b : Œ± ‚Üí Œ≤) a = b := rfl

/-- If the target space is inhabited, so is the space of bounded continuous functions -/
instance [inhabited Œ≤] : inhabited (Œ± ‚Üí·µá Œ≤) := ‚ü®const Œ± default‚ü©

lemma lipschitz_evalx (x : Œ±) : lipschitz_with 1 (Œª f : Œ± ‚Üí·µá Œ≤, f x) :=
lipschitz_with.mk_one $ Œª f g, dist_coe_le_dist x

theorem uniform_continuous_coe : @uniform_continuous (Œ± ‚Üí·µá Œ≤) (Œ± ‚Üí Œ≤) _ _ coe_fn :=
uniform_continuous_pi.2 $ Œª x, (lipschitz_evalx x).uniform_continuous

lemma continuous_coe : continuous (Œª (f : Œ± ‚Üí·µá Œ≤) x, f x) :=
uniform_continuous.continuous uniform_continuous_coe

/-- When `x` is fixed, `(f : Œ± ‚Üí·µá Œ≤) ‚Ü¶ f x` is continuous -/
@[continuity] theorem continuous_eval_const {x : Œ±} : continuous (Œª f : Œ± ‚Üí·µá Œ≤, f x) :=
(continuous_apply x).comp continuous_coe

/-- The evaluation map is continuous, as a joint function of `u` and `x` -/
@[continuity] theorem continuous_eval : continuous (Œª p : (Œ± ‚Üí·µá Œ≤) √ó Œ±, p.1 p.2) :=
continuous_prod_of_continuous_lipschitz _ 1 (Œª f, f.continuous) $ lipschitz_evalx

/-- Bounded continuous functions taking values in a complete space form a complete space. -/
instance [complete_space Œ≤] : complete_space (Œ± ‚Üí·µá Œ≤) :=
complete_of_cauchy_seq_tendsto $ Œª (f : ‚Ñï ‚Üí Œ± ‚Üí·µá Œ≤) (hf : cauchy_seq f),
begin
  /- We have to show that `f n` converges to a bounded continuous function.
  For this, we prove pointwise convergence to define the limit, then check
  it is a continuous bounded function, and then check the norm convergence. -/
  rcases cauchy_seq_iff_le_tendsto_0.1 hf with ‚ü®b, b0, b_bound, b_lim‚ü©,
  have f_bdd := Œªx n m N hn hm, le_trans (dist_coe_le_dist x) (b_bound n m N hn hm),
  have fx_cau : ‚àÄx, cauchy_seq (Œªn, f n x) :=
    Œªx, cauchy_seq_iff_le_tendsto_0.2 ‚ü®b, b0, f_bdd x, b_lim‚ü©,
  choose F hF using Œªx, cauchy_seq_tendsto_of_complete (fx_cau x),
  /- F : Œ± ‚Üí Œ≤,  hF : ‚àÄ (x : Œ±), tendsto (Œª (n : ‚Ñï), f n x) at_top (ùìù (F x))
  `F` is the desired limit function. Check that it is uniformly approximated by `f N` -/
  have fF_bdd : ‚àÄx N, dist (f N x) (F x) ‚â§ b N :=
    Œª x N, le_of_tendsto (tendsto_const_nhds.dist (hF x))
      (filter.eventually_at_top.2 ‚ü®N, Œªn hn, f_bdd x N n N (le_refl N) hn‚ü©),
  refine ‚ü®‚ü®‚ü®F, _‚ü©, _‚ü©, _‚ü©,
  { /- Check that `F` is continuous, as a uniform limit of continuous functions -/
    have : tendsto_uniformly (Œªn x, f n x) F at_top,
    { refine metric.tendsto_uniformly_iff.2 (Œª Œµ Œµ0, _),
      refine ((tendsto_order.1 b_lim).2 Œµ Œµ0).mono (Œª n hn x, _),
      rw dist_comm,
      exact lt_of_le_of_lt (fF_bdd x n) hn },
    exact this.continuous (eventually_of_forall $ Œª N, (f N).continuous) },
  { /- Check that `F` is bounded -/
    rcases (f 0).bounded with ‚ü®C, hC‚ü©,
    refine ‚ü®C + (b 0 + b 0), Œª x y, _‚ü©,
    calc dist (F x) (F y) ‚â§ dist (f 0 x) (f 0 y) + (dist (f 0 x) (F x) + dist (f 0 y) (F y)) :
      dist_triangle4_left _ _ _ _
                      ... ‚â§ C + (b 0 + b 0) : by mono* },
  { /- Check that `F` is close to `f N` in distance terms -/
    refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero (Œª _, dist_nonneg) _ b_lim),
    exact Œª N, (dist_le (b0 _)).2 (Œªx, fF_bdd x N) }
end

/-- Composition of a bounded continuous function and a continuous function. -/
def comp_continuous {Œ¥ : Type*} [topological_space Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) : Œ¥ ‚Üí·µá Œ≤ :=
{ to_continuous_map := f.1.comp g,
  map_bounded' := f.map_bounded'.imp (Œª C hC x y, hC _ _) }

@[simp] lemma coe_comp_continuous {Œ¥ : Type*} [topological_space Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) :
  coe_fn (f.comp_continuous g) = f ‚àò g := rfl

@[simp] lemma comp_continuous_apply {Œ¥ : Type*} [topological_space Œ¥]
  (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) (x : Œ¥) : f.comp_continuous g x = f (g x) :=
rfl

lemma lipschitz_comp_continuous {Œ¥ : Type*} [topological_space Œ¥] (g : C(Œ¥, Œ±)) :
  lipschitz_with 1 (Œª f : Œ± ‚Üí·µá Œ≤, f.comp_continuous g) :=
lipschitz_with.mk_one $ Œª f‚ÇÅ f‚ÇÇ, (dist_le dist_nonneg).2 $ Œª x, dist_coe_le_dist (g x)

lemma continuous_comp_continuous {Œ¥ : Type*} [topological_space Œ¥] (g : C(Œ¥, Œ±)) :
  continuous (Œª f : Œ± ‚Üí·µá Œ≤, f.comp_continuous g) :=
(lipschitz_comp_continuous g).continuous

/-- Restrict a bounded continuous function to a set. -/
def restrict (f : Œ± ‚Üí·µá Œ≤) (s : set Œ±) : s ‚Üí·µá Œ≤ :=
f.comp_continuous $ (continuous_map.id _).restrict s

@[simp] lemma coe_restrict (f : Œ± ‚Üí·µá Œ≤) (s : set Œ±) : coe_fn (f.restrict s) = f ‚àò coe := rfl

@[simp] lemma restrict_apply (f : Œ± ‚Üí·µá Œ≤) (s : set Œ±) (x : s) : f.restrict s x = f x := rfl

/-- Composition (in the target) of a bounded continuous function with a Lipschitz map again
gives a bounded continuous function -/
def comp (G : Œ≤ ‚Üí Œ≥) {C : ‚Ñù‚â•0} (H : lipschitz_with C G)
  (f : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí·µá Œ≥ :=
‚ü®‚ü®Œªx, G (f x), H.continuous.comp f.continuous‚ü©,
  let ‚ü®D, hD‚ü© := f.bounded in
  ‚ü®max C 0 * D, Œª x y, calc
    dist (G (f x)) (G (f y)) ‚â§ C * dist (f x) (f y) : H.dist_le_mul _ _
    ... ‚â§ max C 0 * dist (f x) (f y) : mul_le_mul_of_nonneg_right (le_max_left C 0) dist_nonneg
    ... ‚â§ max C 0 * D : mul_le_mul_of_nonneg_left (hD _ _) (le_max_right C 0)‚ü©‚ü©

/-- The composition operator (in the target) with a Lipschitz map is Lipschitz -/
lemma lipschitz_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : lipschitz_with C G) :
  lipschitz_with C (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
lipschitz_with.of_dist_le_mul $ Œª f g,
(dist_le (mul_nonneg C.2 dist_nonneg)).2 $ Œª x,
calc dist (G (f x)) (G (g x)) ‚â§ C * dist (f x) (g x) : H.dist_le_mul _ _
  ... ‚â§ C * dist f g : mul_le_mul_of_nonneg_left (dist_coe_le_dist _) C.2

/-- The composition operator (in the target) with a Lipschitz map is uniformly continuous -/
lemma uniform_continuous_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : lipschitz_with C G) :
  uniform_continuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
(lipschitz_comp H).uniform_continuous

/-- The composition operator (in the target) with a Lipschitz map is continuous -/
lemma continuous_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : lipschitz_with C G) :
  continuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
(lipschitz_comp H).continuous

/-- Restriction (in the target) of a bounded continuous function taking values in a subset -/
def cod_restrict (s : set Œ≤) (f : Œ± ‚Üí·µá Œ≤) (H : ‚àÄx, f x ‚àà s) : Œ± ‚Üí·µá s :=
‚ü®‚ü®s.cod_restrict f H, f.continuous.subtype_mk _‚ü©, f.bounded‚ü©

section extend

variables {Œ¥ : Type*} [topological_space Œ¥] [discrete_topology Œ¥]

/-- A version of `function.extend` for bounded continuous maps. We assume that the domain has
discrete topology, so we only need to verify boundedness. -/
def extend (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) : Œ¥ ‚Üí·µá Œ≤ :=
{ to_fun := extend f g h,
  continuous_to_fun := continuous_of_discrete_topology,
  map_bounded' :=
    begin
      rw [‚Üê bounded_range_iff, range_extend f.injective, metric.bounded_union],
      exact ‚ü®g.bounded_range, h.bounded_image _‚ü©
    end }

@[simp] lemma extend_apply (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) (x : Œ±) :
  extend f g h (f x) = g x :=
f.injective.extend_apply _ _ _

@[simp] lemma extend_comp (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) : extend f g h ‚àò f = g :=
extend_comp f.injective _ _

lemma extend_apply' {f : Œ± ‚Ü™ Œ¥} {x : Œ¥} (hx : x ‚àâ range f) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) :
  extend f g h x = h x :=
extend_apply' _ _ _ hx

lemma extend_of_empty [is_empty Œ±] (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) :
  extend f g h = h :=
fun_like.coe_injective $ function.extend_of_empty f g h

@[simp] lemma dist_extend_extend (f : Œ± ‚Ü™ Œ¥) (g‚ÇÅ g‚ÇÇ : Œ± ‚Üí·µá Œ≤) (h‚ÇÅ h‚ÇÇ : Œ¥ ‚Üí·µá Œ≤) :
  dist (g‚ÇÅ.extend f h‚ÇÅ) (g‚ÇÇ.extend f h‚ÇÇ) =
    max (dist g‚ÇÅ g‚ÇÇ) (dist (h‚ÇÅ.restrict (range f)·∂ú) (h‚ÇÇ.restrict (range f)·∂ú)) :=
begin
  refine le_antisymm ((dist_le $ le_max_iff.2 $ or.inl dist_nonneg).2 $ Œª x, _) (max_le _ _),
  { rcases em (‚àÉ y, f y = x) with (‚ü®x, rfl‚ü©|hx),
    { simp only [extend_apply],
      exact (dist_coe_le_dist x).trans (le_max_left _ _) },
    { simp only [extend_apply' hx],
      lift x to ((range f)·∂ú : set Œ¥) using hx,
      calc dist (h‚ÇÅ x) (h‚ÇÇ x) = dist (h‚ÇÅ.restrict (range f)·∂ú x) (h‚ÇÇ.restrict (range f)·∂ú x) : rfl
      ... ‚â§ dist (h‚ÇÅ.restrict (range f)·∂ú) (h‚ÇÇ.restrict (range f)·∂ú) : dist_coe_le_dist x
      ... ‚â§ _ : le_max_right _ _ } },
  { refine (dist_le dist_nonneg).2 (Œª x, _),
    rw [‚Üê extend_apply f g‚ÇÅ h‚ÇÅ, ‚Üê extend_apply f g‚ÇÇ h‚ÇÇ],
    exact dist_coe_le_dist _ },
  { refine (dist_le dist_nonneg).2 (Œª x, _),
    calc dist (h‚ÇÅ x) (h‚ÇÇ x) = dist (extend f g‚ÇÅ h‚ÇÅ x) (extend f g‚ÇÇ h‚ÇÇ x) :
      by rw [extend_apply' x.coe_prop, extend_apply' x.coe_prop]
    ... ‚â§ _ : dist_coe_le_dist _ }
end

lemma isometry_extend (f : Œ± ‚Ü™ Œ¥) (h : Œ¥ ‚Üí·µá Œ≤) :
  isometry (Œª g : Œ± ‚Üí·µá Œ≤, extend f g h) :=
isometry.of_dist_eq $ Œª g‚ÇÅ g‚ÇÇ, by simp [dist_nonneg]

end extend

end basics

section arzela_ascoli
variables [topological_space Œ±] [compact_space Œ±] [pseudo_metric_space Œ≤]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

/- Arzela-Ascoli theorem asserts that, on a compact space, a set of functions sharing
a common modulus of continuity and taking values in a compact set forms a compact
subset for the topology of uniform convergence. In this section, we prove this theorem
and several useful variations around it. -/

/-- First version, with pointwise equicontinuity and range in a compact space -/
theorem arzela_ascoli‚ÇÅ [compact_space Œ≤]
  (A : set (Œ± ‚Üí·µá Œ≤))
  (closed : is_closed A)
  (H : equicontinuous (coe_fn : A ‚Üí Œ± ‚Üí Œ≤)) :
  is_compact A :=
begin
  simp_rw [equicontinuous, metric.equicontinuous_at_iff_pair] at H,
  refine is_compact_of_totally_bounded_is_closed _ closed,
  refine totally_bounded_of_finite_discretization (Œª Œµ Œµ0, _),
  rcases exists_between Œµ0 with ‚ü®Œµ‚ÇÅ, Œµ‚ÇÅ0, ŒµŒµ‚ÇÅ‚ü©,
  let Œµ‚ÇÇ := Œµ‚ÇÅ/2/2,
  /- We have to find a finite discretization of `u`, i.e., finite information
  that is sufficient to reconstruct `u` up to Œµ. This information will be
  provided by the values of `u` on a sufficiently dense set tŒ±,
  slightly translated to fit in a finite Œµ‚ÇÇ-dense set tŒ≤ in the image. Such
  sets exist by compactness of the source and range. Then, to check that these
  data determine the function up to Œµ, one uses the control on the modulus of
  continuity to extend the closeness on tŒ± to closeness everywhere. -/
  have Œµ‚ÇÇ0 : Œµ‚ÇÇ > 0 := half_pos (half_pos Œµ‚ÇÅ0),
  have : ‚àÄx:Œ±, ‚àÉU, x ‚àà U ‚àß is_open U ‚àß ‚àÄ (y z ‚àà U) {f : Œ± ‚Üí·µá Œ≤},
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ‚ÇÇ := Œª x,
      let ‚ü®U, nhdsU, hU‚ü© := H x _ Œµ‚ÇÇ0,
          ‚ü®V, VU, openV, xV‚ü© := _root_.mem_nhds_iff.1 nhdsU in
      ‚ü®V, xV, openV, Œªy hy z hz f hf, hU y (VU hy) z (VU hz) ‚ü®f, hf‚ü©‚ü©,
  choose U hU using this,
  /- For all x, the set hU x is an open set containing x on which the elements of A
  fluctuate by at most Œµ‚ÇÇ.
  We extract finitely many of these sets that cover the whole space, by compactness -/
  rcases is_compact_univ.elim_finite_subcover_image
    (Œªx _, (hU x).2.1) (Œªx hx, mem_bUnion (mem_univ _) (hU x).1)
    with ‚ü®tŒ±, _, ‚ü®_‚ü©, htŒ±‚ü©,
  /- tŒ± : set Œ±, htŒ± : univ ‚äÜ ‚ãÉx ‚àà tŒ±, U x -/
  rcases @finite_cover_balls_of_compact Œ≤ _ _ is_compact_univ _ Œµ‚ÇÇ0
    with ‚ü®tŒ≤, _, ‚ü®_‚ü©, htŒ≤‚ü©, resetI,
  /- tŒ≤ : set Œ≤, htŒ≤ : univ ‚äÜ ‚ãÉy ‚àà tŒ≤, ball y Œµ‚ÇÇ -/
  /- Associate to every point `y` in the space a nearby point `F y` in tŒ≤ -/
  choose F hF using Œªy, show ‚àÉz‚ààtŒ≤, dist y z < Œµ‚ÇÇ, by simpa using htŒ≤ (mem_univ y),
  /- F : Œ≤ ‚Üí Œ≤, hF : ‚àÄ (y : Œ≤), F y ‚àà tŒ≤ ‚àß dist y (F y) < Œµ‚ÇÇ -/

  /- Associate to every function a discrete approximation, mapping each point in `tŒ±`
  to a point in `tŒ≤` close to its true image by the function. -/
  refine ‚ü®tŒ± ‚Üí tŒ≤, by apply_instance, Œª f a, ‚ü®F (f a), (hF (f a)).1‚ü©, _‚ü©,
  rintro ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© f_eq_g,
  /- If two functions have the same approximation, then they are within distance Œµ -/
  refine lt_of_le_of_lt ((dist_le $ le_of_lt Œµ‚ÇÅ0).2 (Œª x, _)) ŒµŒµ‚ÇÅ,
  obtain ‚ü®x', x'tŒ±, hx'‚ü© : ‚àÉx' ‚àà tŒ±, x ‚àà U x' := mem_Union‚ÇÇ.1 (htŒ± (mem_univ x)),
  calc dist (f x) (g x)
      ‚â§ dist (f x) (f x') + dist (g x) (g x') + dist (f x') (g x') : dist_triangle4_right _ _ _ _
  ... ‚â§ Œµ‚ÇÇ + Œµ‚ÇÇ + Œµ‚ÇÅ/2 : le_of_lt (add_lt_add (add_lt_add _ _) _)
  ... = Œµ‚ÇÅ : by rw [add_halves, add_halves],
  { exact (hU x').2.2 _ hx' _ ((hU x').1) hf },
  { exact (hU x').2.2 _ hx' _ ((hU x').1) hg },
  { have F_f_g : F (f x') = F (g x') :=
      (congr_arg (Œª f:tŒ± ‚Üí tŒ≤, (f ‚ü®x', x'tŒ±‚ü© : Œ≤)) f_eq_g : _),
    calc dist (f x') (g x')
          ‚â§ dist (f x') (F (f x')) + dist (g x') (F (f x')) : dist_triangle_right _ _ _
      ... = dist (f x') (F (f x')) + dist (g x') (F (g x')) : by rw F_f_g
      ... < Œµ‚ÇÇ + Œµ‚ÇÇ : add_lt_add (hF (f x')).2 (hF (g x')).2
      ... = Œµ‚ÇÅ/2 : add_halves _ }
end

/-- Second version, with pointwise equicontinuity and range in a compact subset -/
theorem arzela_ascoli‚ÇÇ
  (s : set Œ≤) (hs : is_compact s)
  (A : set (Œ± ‚Üí·µá Œ≤))
  (closed : is_closed A)
  (in_s : ‚àÄ(f : Œ± ‚Üí·µá Œ≤) (x : Œ±), f ‚àà A ‚Üí f x ‚àà s)
  (H : equicontinuous (coe_fn : A ‚Üí Œ± ‚Üí Œ≤)) :
  is_compact A :=
/- This version is deduced from the previous one by restricting to the compact type in the target,
using compactness there and then lifting everything to the original space. -/
begin
  have M : lipschitz_with 1 coe := lipschitz_with.subtype_coe s,
  let F : (Œ± ‚Üí·µá s) ‚Üí Œ± ‚Üí·µá Œ≤ := comp coe M,
  refine is_compact_of_is_closed_subset
    ((_ : is_compact (F ‚Åª¬π' A)).image (continuous_comp M)) closed (Œª f hf, _),
  { haveI : compact_space s := is_compact_iff_compact_space.1 hs,
    refine arzela_ascoli‚ÇÅ _ (continuous_iff_is_closed.1 (continuous_comp M) _ closed) _,
    rw uniform_embedding_subtype_coe.to_uniform_inducing.equicontinuous_iff,
    exact H.comp (A.restrict_preimage F) },
  { let g := cod_restrict s f (Œªx, in_s f x hf),
    rw [show f = F g, by ext; refl] at hf ‚ä¢,
    exact ‚ü®g, hf, rfl‚ü© }
end

/-- Third (main) version, with pointwise equicontinuity and range in a compact subset, but
without closedness. The closure is then compact -/
theorem arzela_ascoli [t2_space Œ≤]
  (s : set Œ≤) (hs : is_compact s)
  (A : set (Œ± ‚Üí·µá Œ≤))
  (in_s : ‚àÄ(f : Œ± ‚Üí·µá Œ≤) (x : Œ±), f ‚àà A ‚Üí f x ‚àà s)
  (H : equicontinuous (coe_fn : A ‚Üí Œ± ‚Üí Œ≤)) :
  is_compact (closure A) :=
/- This version is deduced from the previous one by checking that the closure of A, in
addition to being closed, still satisfies the properties of compact range and equicontinuity -/
arzela_ascoli‚ÇÇ s hs (closure A) is_closed_closure
  (Œª f x hf, (mem_of_closed' hs.is_closed).2 $ Œª Œµ Œµ0,
    let ‚ü®g, gA, dist_fg‚ü© := metric.mem_closure_iff.1 hf Œµ Œµ0 in
    ‚ü®g x, in_s g x gA, lt_of_le_of_lt (dist_coe_le_dist _) dist_fg‚ü©)
  (H.closure' continuous_coe)

end arzela_ascoli

section has_one

variables [topological_space Œ±] [pseudo_metric_space Œ≤] [has_one Œ≤]

@[to_additive] instance : has_one (Œ± ‚Üí·µá Œ≤) := ‚ü®const Œ± 1‚ü©

@[simp, to_additive] lemma coe_one : ((1 : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí Œ≤) = 1 := rfl

@[simp, to_additive]
lemma mk_of_compact_one [compact_space Œ±] : mk_of_compact (1 : C(Œ±, Œ≤)) = 1 := rfl

@[to_additive] lemma forall_coe_one_iff_one (f : Œ± ‚Üí·µá Œ≤) : (‚àÄ x, f x = 1) ‚Üî f = 1 :=
(@fun_like.ext_iff _ _ _ _ f 1).symm

@[simp, to_additive] lemma one_comp_continuous [topological_space Œ≥] (f : C(Œ≥, Œ±)) :
  (1 : Œ± ‚Üí·µá Œ≤).comp_continuous f = 1 := rfl

end has_one

section has_lipschitz_add
/- In this section, if `Œ≤` is an `add_monoid` whose addition operation is Lipschitz, then we show
that the space of bounded continuous functions from `Œ±` to `Œ≤` inherits a topological `add_monoid`
structure, by using pointwise operations and checking that they are compatible with the uniform
distance.

Implementation note: The material in this section could have been written for `has_lipschitz_mul`
and transported by `@[to_additive]`.  We choose not to do this because this causes a few lemma
names (for example, `coe_mul`) to conflict with later lemma names for normed rings; this is only a
trivial inconvenience, but in any case there are no obvious applications of the multiplicative
version. -/

variables [topological_space Œ±] [pseudo_metric_space Œ≤] [add_monoid Œ≤]
variables [has_lipschitz_add Œ≤]
variables (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

/-- The pointwise sum of two bounded continuous functions is again bounded continuous. -/
instance : has_add (Œ± ‚Üí·µá Œ≤) :=
{ add := Œª f g,
  bounded_continuous_function.mk_of_bound (f.to_continuous_map + g.to_continuous_map)
    (‚Üë(has_lipschitz_add.C Œ≤) * max (classical.some f.bounded) (classical.some g.bounded))
    begin
      intros x y,
      refine le_trans (lipschitz_with_lipschitz_const_add ‚ü®f x, g x‚ü© ‚ü®f y, g y‚ü©) _,
      rw prod.dist_eq,
      refine mul_le_mul_of_nonneg_left _ (has_lipschitz_add.C Œ≤).coe_nonneg,
      apply max_le_max,
      exact classical.some_spec f.bounded x y,
      exact classical.some_spec g.bounded x y,
    end }

@[simp] lemma coe_add : ‚áë(f + g) = f + g := rfl
lemma add_apply : (f + g) x = f x + g x := rfl

@[simp] lemma mk_of_compact_add [compact_space Œ±] (f g : C(Œ±, Œ≤)) :
  mk_of_compact (f + g) = mk_of_compact f + mk_of_compact g := rfl

lemma add_comp_continuous [topological_space Œ≥] (h : C(Œ≥, Œ±)) :
  (g + f).comp_continuous h = g.comp_continuous h + f.comp_continuous h := rfl

@[simp] lemma coe_nsmul_rec : ‚àÄ n, ‚áë(nsmul_rec n f) = n ‚Ä¢ f
| 0 := by rw [nsmul_rec, zero_smul, coe_zero]
| (n + 1) := by rw [nsmul_rec, succ_nsmul, coe_add, coe_nsmul_rec]

instance has_nat_scalar : has_smul ‚Ñï (Œ± ‚Üí·µá Œ≤) :=
{ smul := Œª n f,
  { to_continuous_map := n ‚Ä¢ f.to_continuous_map,
    map_bounded' := by simpa [coe_nsmul_rec] using (nsmul_rec n f).map_bounded' } }

@[simp] lemma coe_nsmul (r : ‚Ñï) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ f := rfl
@[simp] lemma nsmul_apply (r : ‚Ñï) (f : Œ± ‚Üí·µá Œ≤) (v : Œ±) : (r ‚Ä¢ f) v = r ‚Ä¢ f v := rfl

instance : add_monoid (Œ± ‚Üí·µá Œ≤) :=
fun_like.coe_injective.add_monoid _ coe_zero coe_add (Œª _ _, coe_nsmul _ _)

instance : has_lipschitz_add (Œ± ‚Üí·µá Œ≤) :=
{ lipschitz_add := ‚ü®has_lipschitz_add.C Œ≤, begin
    have C_nonneg := (has_lipschitz_add.C Œ≤).coe_nonneg,
    rw lipschitz_with_iff_dist_le_mul,
    rintros ‚ü®f‚ÇÅ, g‚ÇÅ‚ü© ‚ü®f‚ÇÇ, g‚ÇÇ‚ü©,
    rw dist_le (mul_nonneg C_nonneg dist_nonneg),
    intros x,
    refine le_trans (lipschitz_with_lipschitz_const_add ‚ü®f‚ÇÅ x, g‚ÇÅ x‚ü© ‚ü®f‚ÇÇ x, g‚ÇÇ x‚ü©) _,
    refine mul_le_mul_of_nonneg_left _ C_nonneg,
    apply max_le_max; exact dist_coe_le_dist x,
  end‚ü© }

/-- Coercion of a `normed_add_group_hom` is an `add_monoid_hom`. Similar to
`add_monoid_hom.coe_fn`. -/
@[simps] def coe_fn_add_hom : (Œ± ‚Üí·µá Œ≤) ‚Üí+ (Œ± ‚Üí Œ≤) :=
{ to_fun := coe_fn, map_zero' := coe_zero, map_add' := coe_add }

variables (Œ± Œ≤)

/-- The additive map forgetting that a bounded continuous function is bounded.
-/
@[simps] def to_continuous_map_add_hom : (Œ± ‚Üí·µá Œ≤) ‚Üí+ C(Œ±, Œ≤) :=
{ to_fun := to_continuous_map,
  map_zero' := by { ext, simp, },
  map_add' := by { intros, ext, simp, }, }

end has_lipschitz_add

section comm_has_lipschitz_add

variables [topological_space Œ±] [pseudo_metric_space Œ≤] [add_comm_monoid Œ≤] [has_lipschitz_add Œ≤]

@[to_additive] instance : add_comm_monoid (Œ± ‚Üí·µá Œ≤) :=
{ add_comm      := assume f g, by ext; simp [add_comm],
  .. bounded_continuous_function.add_monoid }

open_locale big_operators

@[simp] lemma coe_sum {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí (Œ± ‚Üí·µá Œ≤)) :
  ‚áë(‚àë i in s, f i) = (‚àë i in s, (f i : Œ± ‚Üí Œ≤)) :=
(@coe_fn_add_hom Œ± Œ≤ _ _ _ _).map_sum f s

lemma sum_apply {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí (Œ± ‚Üí·µá Œ≤)) (a : Œ±) :
  (‚àë i in s, f i) a = (‚àë i in s, f i a) :=
by simp

end comm_has_lipschitz_add

section normed_add_comm_group
/- In this section, if Œ≤ is a normed group, then we show that the space of bounded
continuous functions from Œ± to Œ≤ inherits a normed group structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Œ±] [seminormed_add_comm_group Œ≤]
variables (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

instance : has_norm (Œ± ‚Üí·µá Œ≤) := ‚ü®Œªu, dist u 0‚ü©

lemma norm_def : ‚Äñf‚Äñ = dist f 0 := rfl

/-- The norm of a bounded continuous function is the supremum of `‚Äñf x‚Äñ`.
We use `Inf` to ensure that the definition works if `Œ±` has no elements. -/
lemma norm_eq (f : Œ± ‚Üí·µá Œ≤) :
  ‚Äñf‚Äñ = Inf {C : ‚Ñù | 0 ‚â§ C ‚àß ‚àÄ (x : Œ±), ‚Äñf x‚Äñ ‚â§ C} :=
by simp [norm_def, bounded_continuous_function.dist_eq]

/-- When the domain is non-empty, we do not need the `0 ‚â§ C` condition in the formula for ‚Äñf‚Äñ as an
`Inf`. -/
lemma norm_eq_of_nonempty [h : nonempty Œ±] : ‚Äñf‚Äñ = Inf {C : ‚Ñù | ‚àÄ (x : Œ±), ‚Äñf x‚Äñ ‚â§ C} :=
begin
  unfreezingI { obtain ‚ü®a‚ü© := h, },
  rw norm_eq,
  congr,
  ext,
  simp only [and_iff_right_iff_imp],
  exact Œª h', le_trans (norm_nonneg (f a)) (h' a),
end

@[simp] lemma norm_eq_zero_of_empty [h : is_empty Œ±] : ‚Äñf‚Äñ = 0 :=
dist_zero_of_empty

lemma norm_coe_le_norm (x : Œ±) : ‚Äñf x‚Äñ ‚â§ ‚Äñf‚Äñ := calc
  ‚Äñf x‚Äñ = dist (f x) ((0 : Œ± ‚Üí·µá Œ≤) x) : by simp [dist_zero_right]
  ... ‚â§ ‚Äñf‚Äñ : dist_coe_le_dist _

lemma dist_le_two_norm' {f : Œ≥ ‚Üí Œ≤} {C : ‚Ñù} (hC : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) (x y : Œ≥) :
  dist (f x) (f y) ‚â§ 2 * C :=
calc dist (f x) (f y) ‚â§ ‚Äñf x‚Äñ + ‚Äñf y‚Äñ : dist_le_norm_add_norm _ _
                  ... ‚â§ C + C         : add_le_add (hC x) (hC y)
                  ... = 2 * C         : (two_mul _).symm

/-- Distance between the images of any two points is at most twice the norm of the function. -/
lemma dist_le_two_norm (x y : Œ±) : dist (f x) (f y) ‚â§ 2 * ‚Äñf‚Äñ :=
dist_le_two_norm' f.norm_coe_le_norm x y

variable {f}

/-- The norm of a function is controlled by the supremum of the pointwise norms -/
lemma norm_le (C0 : (0 : ‚Ñù) ‚â§ C) : ‚Äñf‚Äñ ‚â§ C ‚Üî ‚àÄx:Œ±, ‚Äñf x‚Äñ ‚â§ C :=
by simpa using @dist_le _ _ _ _ f 0 _ C0

lemma norm_le_of_nonempty [nonempty Œ±]
  {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} : ‚Äñf‚Äñ ‚â§ M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ ‚â§ M :=
begin
  simp_rw [norm_def, ‚Üêdist_zero_right],
  exact dist_le_iff_of_nonempty,
end

lemma norm_lt_iff_of_compact [compact_space Œ±]
  {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} (M0 : 0 < M) : ‚Äñf‚Äñ < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ < M :=
begin
  simp_rw [norm_def, ‚Üêdist_zero_right],
  exact dist_lt_iff_of_compact M0,
end

lemma norm_lt_iff_of_nonempty_compact [nonempty Œ±] [compact_space Œ±]
  {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} : ‚Äñf‚Äñ < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ < M :=
begin
  simp_rw [norm_def, ‚Üêdist_zero_right],
  exact dist_lt_iff_of_nonempty_compact,
end

variable (f)

/-- Norm of `const Œ± b` is less than or equal to `‚Äñb‚Äñ`. If `Œ±` is nonempty,
then it is equal to `‚Äñb‚Äñ`. -/
lemma norm_const_le (b : Œ≤) : ‚Äñconst Œ± b‚Äñ ‚â§ ‚Äñb‚Äñ :=
(norm_le (norm_nonneg b)).2 $ Œª x, le_rfl

@[simp] lemma norm_const_eq [h : nonempty Œ±] (b : Œ≤) : ‚Äñconst Œ± b‚Äñ = ‚Äñb‚Äñ :=
le_antisymm (norm_const_le b) $ h.elim $ Œª x, (const Œ± b).norm_coe_le_norm x

/-- Constructing a bounded continuous function from a uniformly bounded continuous
function taking values in a normed group. -/
def of_normed_add_comm_group {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±]
  [seminormed_add_comm_group Œ≤] (f : Œ± ‚Üí Œ≤) (Hf : continuous f) (C : ‚Ñù) (H : ‚àÄx, ‚Äñf x‚Äñ ‚â§ C) :
  Œ± ‚Üí·µá Œ≤ :=
‚ü®‚ü®Œªn, f n, Hf‚ü©, ‚ü®_, dist_le_two_norm' H‚ü©‚ü©

@[simp] lemma coe_of_normed_add_comm_group
  {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±] [seminormed_add_comm_group Œ≤]
  (f : Œ± ‚Üí Œ≤) (Hf : continuous f) (C : ‚Ñù) (H : ‚àÄx, ‚Äñf x‚Äñ ‚â§ C) :
  (of_normed_add_comm_group f Hf C H : Œ± ‚Üí Œ≤) = f := rfl

lemma norm_of_normed_add_comm_group_le {f : Œ± ‚Üí Œ≤} (hfc : continuous f) {C : ‚Ñù} (hC : 0 ‚â§ C)
  (hfC : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) : ‚Äñof_normed_add_comm_group f hfc C hfC‚Äñ ‚â§ C :=
(norm_le hC).2 hfC

/-- Constructing a bounded continuous function from a uniformly bounded
function on a discrete space, taking values in a normed group -/
def of_normed_add_comm_group_discrete {Œ± : Type u} {Œ≤ : Type v}
  [topological_space Œ±] [discrete_topology Œ±] [seminormed_add_comm_group Œ≤]
  (f : Œ±  ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄx, norm (f x) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
of_normed_add_comm_group f continuous_of_discrete_topology C H

@[simp] lemma coe_of_normed_add_comm_group_discrete {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±]
  [discrete_topology Œ±] [seminormed_add_comm_group Œ≤] (f : Œ± ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄx, ‚Äñf x‚Äñ ‚â§ C) :
  (of_normed_add_comm_group_discrete f C H : Œ± ‚Üí Œ≤) = f := rfl

/-- Taking the pointwise norm of a bounded continuous function with values in a
`seminormed_add_comm_group` yields a bounded continuous function with values in ‚Ñù. -/
def norm_comp : Œ± ‚Üí·µá ‚Ñù :=
f.comp norm lipschitz_with_one_norm

@[simp] lemma coe_norm_comp : (f.norm_comp : Œ± ‚Üí ‚Ñù) = norm ‚àò f := rfl

@[simp] lemma norm_norm_comp : ‚Äñf.norm_comp‚Äñ = ‚Äñf‚Äñ :=
by simp only [norm_eq, coe_norm_comp, norm_norm]

lemma bdd_above_range_norm_comp : bdd_above $ set.range $ norm ‚àò f :=
(real.bounded_iff_bdd_below_bdd_above.mp $ @bounded_range _ _ _ _ f.norm_comp).2

lemma norm_eq_supr_norm : ‚Äñf‚Äñ = ‚®Ü x : Œ±, ‚Äñf x‚Äñ :=
by simp_rw [norm_def, dist_eq_supr, coe_zero, pi.zero_apply, dist_zero_right]

/-- If `‚Äñ(1 : Œ≤)‚Äñ = 1`, then `‚Äñ(1 : Œ± ‚Üí·µá Œ≤)‚Äñ = 1` if `Œ±` is nonempty. -/
instance [nonempty Œ±] [has_one Œ≤] [norm_one_class Œ≤] : norm_one_class (Œ± ‚Üí·µá Œ≤) :=
{ norm_one := by simp only [norm_eq_supr_norm, coe_one, pi.one_apply, norm_one, csupr_const] }

/-- The pointwise opposite of a bounded continuous function is again bounded continuous. -/
instance : has_neg (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf, of_normed_add_comm_group (-f) f.continuous.neg ‚Äñf‚Äñ $ Œª x,
  trans_rel_right _ (norm_neg _) (f.norm_coe_le_norm x)‚ü©

/-- The pointwise difference of two bounded continuous functions is again bounded continuous. -/
instance : has_sub (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf g, of_normed_add_comm_group (f - g) (f.continuous.sub g.continuous) (‚Äñf‚Äñ + ‚Äñg‚Äñ) $ Œª x,
  by { simp only [sub_eq_add_neg],
       exact le_trans (norm_add_le _ _) (add_le_add (f.norm_coe_le_norm x) $
         trans_rel_right _ (norm_neg _) (g.norm_coe_le_norm x)) }‚ü©

@[simp] lemma coe_neg : ‚áë(-f) = -f := rfl
lemma neg_apply : (-f) x = -f x := rfl

@[simp] lemma coe_sub : ‚áë(f - g) = f - g := rfl
lemma sub_apply : (f - g) x = f x - g x := rfl

@[simp] lemma mk_of_compact_neg [compact_space Œ±] (f : C(Œ±, Œ≤)) :
  mk_of_compact (-f) = -mk_of_compact f := rfl

@[simp] lemma mk_of_compact_sub [compact_space Œ±] (f g : C(Œ±, Œ≤)) :
  mk_of_compact (f - g) = mk_of_compact f - mk_of_compact g := rfl

@[simp] lemma coe_zsmul_rec : ‚àÄ z, ‚áë(zsmul_rec z f) = z ‚Ä¢ f
| (int.of_nat n) := by rw [zsmul_rec, int.of_nat_eq_coe, coe_nsmul_rec, coe_nat_zsmul]
| -[1+ n] := by rw [zsmul_rec, zsmul_neg_succ_of_nat, coe_neg, coe_nsmul_rec]

instance has_int_scalar : has_smul ‚Ñ§ (Œ± ‚Üí·µá Œ≤) :=
{ smul := Œª n f,
  { to_continuous_map := n ‚Ä¢ f.to_continuous_map,
    map_bounded' := by simpa using (zsmul_rec n f).map_bounded' } }

@[simp] lemma coe_zsmul (r : ‚Ñ§) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ f := rfl
@[simp] lemma zsmul_apply (r : ‚Ñ§) (f : Œ± ‚Üí·µá Œ≤) (v : Œ±) : (r ‚Ä¢ f) v = r ‚Ä¢ f v := rfl

instance : add_comm_group (Œ± ‚Üí·µá Œ≤) :=
fun_like.coe_injective.add_comm_group _ coe_zero coe_add coe_neg coe_sub (Œª _ _, coe_nsmul _ _)
  (Œª _ _, coe_zsmul _ _)

instance : seminormed_add_comm_group (Œ± ‚Üí·µá Œ≤) :=
{ dist_eq := Œª f g, by simp only [norm_eq, dist_eq, dist_eq_norm, sub_apply] }

instance {Œ± Œ≤} [topological_space Œ±] [normed_add_comm_group Œ≤] : normed_add_comm_group (Œ± ‚Üí·µá Œ≤) :=
{ ..bounded_continuous_function.seminormed_add_comm_group }

lemma nnnorm_def : ‚Äñf‚Äñ‚Çä = nndist f 0 := rfl

lemma nnnorm_coe_le_nnnorm (x : Œ±) : ‚Äñf x‚Äñ‚Çä ‚â§ ‚Äñf‚Äñ‚Çä := norm_coe_le_norm _ _

lemma nndist_le_two_nnnorm (x y : Œ±) : nndist (f x) (f y) ‚â§ 2 * ‚Äñf‚Äñ‚Çä := dist_le_two_norm _ _ _

/-- The nnnorm of a function is controlled by the supremum of the pointwise nnnorms -/
lemma nnnorm_le (C : ‚Ñù‚â•0) : ‚Äñf‚Äñ‚Çä ‚â§ C ‚Üî ‚àÄx:Œ±, ‚Äñf x‚Äñ‚Çä ‚â§ C :=
norm_le C.prop

lemma nnnorm_const_le (b : Œ≤) : ‚Äñconst Œ± b‚Äñ‚Çä ‚â§ ‚Äñb‚Äñ‚Çä :=
norm_const_le _

@[simp] lemma nnnorm_const_eq [h : nonempty Œ±] (b : Œ≤) : ‚Äñconst Œ± b‚Äñ‚Çä = ‚Äñb‚Äñ‚Çä :=
subtype.ext $ norm_const_eq _

lemma nnnorm_eq_supr_nnnorm : ‚Äñf‚Äñ‚Çä = ‚®Ü x : Œ±, ‚Äñf x‚Äñ‚Çä :=
subtype.ext $ (norm_eq_supr_norm f).trans $ by simp_rw [nnreal.coe_supr, coe_nnnorm]

lemma abs_diff_coe_le_dist : ‚Äñf x - g x‚Äñ ‚â§ dist f g :=
by { rw dist_eq_norm, exact (f - g).norm_coe_le_norm x }

lemma coe_le_coe_add_dist {f g : Œ± ‚Üí·µá ‚Ñù} : f x ‚â§ g x + dist f g :=
sub_le_iff_le_add'.1 $ (abs_le.1 $ @dist_coe_le_dist _ _ _ _ f g x).2

lemma norm_comp_continuous_le [topological_space Œ≥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ≥, Œ±)) :
  ‚Äñf.comp_continuous g‚Äñ ‚â§ ‚Äñf‚Äñ :=
((lipschitz_comp_continuous g).dist_le_mul f 0).trans $
  by rw [nnreal.coe_one, one_mul, dist_zero_right]

end normed_add_comm_group

section has_bounded_smul
/-!
### `has_bounded_smul` (in particular, topological module) structure

In this section, if `Œ≤` is a metric space and a `ùïú`-module whose addition and scalar multiplication
are compatible with the metric structure, then we show that the space of bounded continuous
functions from `Œ±` to `Œ≤` inherits a so-called `has_bounded_smul` structure (in particular, a
`has_continuous_mul` structure, which is the mathlib formulation of being a topological module), by
using pointwise operations and checking that they are compatible with the uniform distance. -/

variables {ùïú : Type*} [pseudo_metric_space ùïú] [topological_space Œ±] [pseudo_metric_space Œ≤]

section has_smul
variables [has_zero ùïú] [has_zero Œ≤] [has_smul ùïú Œ≤] [has_bounded_smul ùïú Œ≤]

instance : has_smul ùïú (Œ± ‚Üí·µá Œ≤) :=
{ smul := Œª c f,
  { to_continuous_map := c ‚Ä¢ f.to_continuous_map,
    map_bounded' := let ‚ü®b, hb‚ü© := f.bounded in ‚ü®dist c 0 * b, Œª x y, begin
      refine (dist_smul_pair c (f x) (f y)).trans _,
      refine mul_le_mul_of_nonneg_left _ dist_nonneg,
      exact hb x y
    end‚ü© } }

@[simp] lemma coe_smul (c : ùïú) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(c ‚Ä¢ f) = Œª x, c ‚Ä¢ (f x) := rfl
lemma smul_apply (c : ùïú) (f : Œ± ‚Üí·µá Œ≤) (x : Œ±) : (c ‚Ä¢ f) x = c ‚Ä¢ f x := rfl

instance [has_smul ùïú·µê·µí·µñ Œ≤] [is_central_scalar ùïú Œ≤] : is_central_scalar ùïú (Œ± ‚Üí·µá Œ≤) :=
{ op_smul_eq_smul := Œª _ _, ext $ Œª _, op_smul_eq_smul _ _ }

instance : has_bounded_smul ùïú (Œ± ‚Üí·µá Œ≤) :=
{ dist_smul_pair' := Œª c f‚ÇÅ f‚ÇÇ, begin
    rw dist_le (mul_nonneg dist_nonneg dist_nonneg),
    intros x,
    refine (dist_smul_pair c (f‚ÇÅ x) (f‚ÇÇ x)).trans _,
    exact mul_le_mul_of_nonneg_left (dist_coe_le_dist x) dist_nonneg
  end,
  dist_pair_smul' := Œª c‚ÇÅ c‚ÇÇ f, begin
    rw dist_le (mul_nonneg dist_nonneg dist_nonneg),
    intros x,
    refine (dist_pair_smul c‚ÇÅ c‚ÇÇ (f x)).trans _,
    convert mul_le_mul_of_nonneg_left (dist_coe_le_dist x) dist_nonneg,
    simp
  end }

end has_smul

section mul_action
variables [monoid_with_zero ùïú] [has_zero Œ≤] [mul_action ùïú Œ≤] [has_bounded_smul ùïú Œ≤]

instance : mul_action ùïú (Œ± ‚Üí·µá Œ≤) := fun_like.coe_injective.mul_action _ coe_smul

end mul_action

section distrib_mul_action
variables [monoid_with_zero ùïú] [add_monoid Œ≤] [distrib_mul_action ùïú Œ≤] [has_bounded_smul ùïú Œ≤]
variables [has_lipschitz_add Œ≤]

instance : distrib_mul_action ùïú (Œ± ‚Üí·µá Œ≤) :=
function.injective.distrib_mul_action ‚ü®_, coe_zero, coe_add‚ü© fun_like.coe_injective coe_smul

end distrib_mul_action

section module
variables [semiring ùïú] [add_comm_monoid Œ≤] [module ùïú Œ≤] [has_bounded_smul ùïú Œ≤]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

variables [has_lipschitz_add Œ≤]

instance : module ùïú (Œ± ‚Üí·µá Œ≤) :=
function.injective.module _ ‚ü®_, coe_zero, coe_add‚ü© fun_like.coe_injective coe_smul

variables (ùïú)
/-- The evaluation at a point, as a continuous linear map from `Œ± ‚Üí·µá Œ≤` to `Œ≤`. -/
def eval_clm (x : Œ±) : (Œ± ‚Üí·µá Œ≤) ‚ÜíL[ùïú] Œ≤ :=
{ to_fun := Œª f, f x,
  map_add' := Œª f g, add_apply _ _,
  map_smul' := Œª c f, smul_apply _ _ _ }

@[simp] lemma eval_clm_apply (x : Œ±) (f : Œ± ‚Üí·µá Œ≤) :
  eval_clm ùïú x f = f x := rfl

variables (Œ± Œ≤)

/-- The linear map forgetting that a bounded continuous function is bounded. -/
@[simps]
def to_continuous_map_linear_map : (Œ± ‚Üí·µá Œ≤) ‚Üí‚Çó[ùïú] C(Œ±, Œ≤) :=
{ to_fun := to_continuous_map,
  map_smul' := Œª f g, rfl,
  map_add' := Œª c f, rfl }

end module

end has_bounded_smul

section normed_space
/-!
### Normed space structure

In this section, if `Œ≤` is a normed space, then we show that the space of bounded
continuous functions from `Œ±` to `Œ≤` inherits a normed space structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables {ùïú : Type*}
variables [topological_space Œ±] [seminormed_add_comm_group Œ≤]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance [normed_field ùïú] [normed_space ùïú Œ≤] : normed_space ùïú (Œ± ‚Üí·µá Œ≤) := ‚ü®Œª c f, begin
  refine norm_of_normed_add_comm_group_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _,
  exact (Œª x, trans_rel_right _ (norm_smul _ _)
    (mul_le_mul_of_nonneg_left (f.norm_coe_le_norm _) (norm_nonneg _))) end‚ü©

variables [nontrivially_normed_field ùïú] [normed_space ùïú Œ≤]
variables [seminormed_add_comm_group Œ≥] [normed_space ùïú Œ≥]

variables (Œ±)
-- TODO does this work in the `has_bounded_smul` setting, too?
/--
Postcomposition of bounded continuous functions into a normed module by a continuous linear map is
a continuous linear map.
Upgraded version of `continuous_linear_map.comp_left_continuous`, similar to
`linear_map.comp_left`. -/
protected def _root_.continuous_linear_map.comp_left_continuous_bounded (g : Œ≤ ‚ÜíL[ùïú] Œ≥) :
  (Œ± ‚Üí·µá Œ≤) ‚ÜíL[ùïú] (Œ± ‚Üí·µá Œ≥) :=
linear_map.mk_continuous
  { to_fun := Œª f, of_normed_add_comm_group
      (g ‚àò f)
      (g.continuous.comp f.continuous)
      (‚Äñg‚Äñ * ‚Äñf‚Äñ)
      (Œª x, (g.le_op_norm_of_le (f.norm_coe_le_norm x))),
    map_add' := Œª f g, by ext; simp,
    map_smul' := Œª c f, by ext; simp }
  ‚Äñg‚Äñ
  (Œª f, norm_of_normed_add_comm_group_le _ (mul_nonneg (norm_nonneg g) (norm_nonneg f)) _)

@[simp] lemma _root_.continuous_linear_map.comp_left_continuous_bounded_apply (g : Œ≤ ‚ÜíL[ùïú] Œ≥)
  (f : Œ± ‚Üí·µá Œ≤) (x : Œ±) :
  (g.comp_left_continuous_bounded Œ± f) x = g (f x) :=
rfl

end normed_space

section normed_ring
/-!
### Normed ring structure

In this section, if `R` is a normed ring, then we show that the space of bounded
continuous functions from `Œ±` to `R` inherits a normed ring structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Œ±] {R : Type*}

section non_unital

section semi_normed
variables [non_unital_semi_normed_ring R]

instance : has_mul (Œ± ‚Üí·µá R) :=
{ mul := Œª f g, of_normed_add_comm_group (f * g) (f.continuous.mul g.continuous) (‚Äñf‚Äñ * ‚Äñg‚Äñ) $ Œª x,
    le_trans (norm_mul_le (f x) (g x)) $
      mul_le_mul (f.norm_coe_le_norm x) (g.norm_coe_le_norm x) (norm_nonneg _) (norm_nonneg _) }

@[simp] lemma coe_mul (f g : Œ± ‚Üí·µá R) : ‚áë(f * g) = f * g := rfl
lemma mul_apply (f g : Œ± ‚Üí·µá R) (x : Œ±) : (f * g) x = f x * g x := rfl

instance : non_unital_ring (Œ± ‚Üí·µá R) :=
fun_like.coe_injective.non_unital_ring _ coe_zero coe_add coe_mul coe_neg coe_sub
  (Œª _ _, coe_nsmul _ _) (Œª _ _, coe_zsmul _ _)

instance : non_unital_semi_normed_ring (Œ± ‚Üí·µá R) :=
{ norm_mul := Œª f g, norm_of_normed_add_comm_group_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _))
    _,
  .. bounded_continuous_function.seminormed_add_comm_group }

end semi_normed

instance [non_unital_normed_ring R] : non_unital_normed_ring (Œ± ‚Üí·µá R) :=
{ .. bounded_continuous_function.non_unital_semi_normed_ring,
  .. bounded_continuous_function.normed_add_comm_group }

end non_unital

section semi_normed

variables [semi_normed_ring R]

@[simp] lemma coe_npow_rec (f : Œ± ‚Üí·µá R) : ‚àÄ n, ‚áë(npow_rec n f) = f ^ n
| 0 := by rw [npow_rec, pow_zero, coe_one]
| (n + 1) := by rw [npow_rec, pow_succ, coe_mul, coe_npow_rec]

instance has_nat_pow : has_pow (Œ± ‚Üí·µá R) ‚Ñï :=
{ pow := Œª f n,
  { to_continuous_map := f.to_continuous_map ^ n,
    map_bounded' := by simpa [coe_npow_rec] using (npow_rec n f).map_bounded' } }

@[simp] lemma coe_pow (n : ‚Ñï) (f : Œ± ‚Üí·µá R) : ‚áë(f ^ n) = f ^ n := rfl
@[simp] lemma pow_apply (n : ‚Ñï) (f : Œ± ‚Üí·µá R) (v : Œ±) : (f ^ n) v = f v ^ n := rfl

instance : has_nat_cast (Œ± ‚Üí·µá R) :=
‚ü®Œª n, bounded_continuous_function.const _ n‚ü©

@[simp, norm_cast] lemma coe_nat_cast (n : ‚Ñï) : ((n : Œ± ‚Üí·µá R) : Œ± ‚Üí R) = n := rfl

instance : has_int_cast (Œ± ‚Üí·µá R) :=
‚ü®Œª n, bounded_continuous_function.const _ n‚ü©

@[simp, norm_cast] lemma coe_int_cast (n : ‚Ñ§) : ((n : Œ± ‚Üí·µá R) : Œ± ‚Üí R) = n := rfl

instance : ring (Œ± ‚Üí·µá R) :=
fun_like.coe_injective.ring _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub
  (Œª _ _, coe_nsmul _ _)
  (Œª _ _, coe_zsmul _ _)
  (Œª _ _, coe_pow _ _)
  coe_nat_cast
  coe_int_cast

instance : semi_normed_ring (Œ± ‚Üí·µá R) :=
{ ..bounded_continuous_function.non_unital_semi_normed_ring }

end semi_normed

instance [normed_ring R] : normed_ring (Œ± ‚Üí·µá R) :=
{ ..bounded_continuous_function.non_unital_normed_ring }

end normed_ring

section normed_comm_ring
/-!
### Normed commutative ring structure

In this section, if `R` is a normed commutative ring, then we show that the space of bounded
continuous functions from `Œ±` to `R` inherits a normed commutative ring structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Œ±] {R : Type*}

instance [semi_normed_comm_ring R] : comm_ring (Œ± ‚Üí·µá R) :=
{ mul_comm := Œª f‚ÇÅ f‚ÇÇ, ext $ Œª x, mul_comm _ _,
  .. bounded_continuous_function.ring }

instance [semi_normed_comm_ring R] : semi_normed_comm_ring (Œ± ‚Üí·µá R) :=
{ ..bounded_continuous_function.comm_ring, ..bounded_continuous_function.seminormed_add_comm_group }

instance [normed_comm_ring R] : normed_comm_ring (Œ± ‚Üí·µá R) :=
{ .. bounded_continuous_function.comm_ring, .. bounded_continuous_function.normed_add_comm_group }

end normed_comm_ring

section normed_algebra
/-!
### Normed algebra structure

In this section, if `Œ≥` is a normed algebra, then we show that the space of bounded
continuous functions from `Œ±` to `Œ≥` inherits a normed algebra structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables {ùïú : Type*} [normed_field ùïú]
variables [topological_space Œ±] [seminormed_add_comm_group Œ≤] [normed_space ùïú Œ≤]
variables [normed_ring Œ≥] [normed_algebra ùïú Œ≥]
variables {f g : Œ± ‚Üí·µá Œ≥} {x : Œ±} {c : ùïú}

/-- `bounded_continuous_function.const` as a `ring_hom`. -/
def C : ùïú ‚Üí+* (Œ± ‚Üí·µá Œ≥) :=
{ to_fun    := Œª (c : ùïú), const Œ± ((algebra_map ùïú Œ≥) c),
  map_one'  := ext $ Œª x, (algebra_map ùïú Œ≥).map_one,
  map_mul'  := Œª c‚ÇÅ c‚ÇÇ, ext $ Œª x, (algebra_map ùïú Œ≥).map_mul _ _,
  map_zero' := ext $ Œª x, (algebra_map ùïú Œ≥).map_zero,
  map_add'  := Œª c‚ÇÅ c‚ÇÇ, ext $ Œª x, (algebra_map ùïú Œ≥).map_add _ _ }

instance : algebra ùïú (Œ± ‚Üí·µá Œ≥) :=
{ to_ring_hom := C,
  commutes' := Œª c f, ext $ Œª x, algebra.commutes' _ _,
  smul_def' := Œª c f, ext $ Œª x, algebra.smul_def' _ _,
  ..bounded_continuous_function.module,
  ..bounded_continuous_function.ring }

@[simp] lemma algebra_map_apply (k : ùïú) (a : Œ±) :
  algebra_map ùïú (Œ± ‚Üí·µá Œ≥) k a = k ‚Ä¢ 1 :=
by { rw algebra.algebra_map_eq_smul_one, refl, }

instance : normed_algebra ùïú (Œ± ‚Üí·µá Œ≥) :=
{ ..bounded_continuous_function.normed_space }

/-!
### Structure as normed module over scalar functions

If `Œ≤` is a normed `ùïú`-space, then we show that the space of bounded continuous
functions from `Œ±` to `Œ≤` is naturally a module over the algebra of bounded continuous
functions from `Œ±` to `ùïú`. -/

instance has_smul' : has_smul (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œª (f : Œ± ‚Üí·µá ùïú) (g : Œ± ‚Üí·µá Œ≤), of_normed_add_comm_group (Œª x, (f x) ‚Ä¢ (g x))
(f.continuous.smul g.continuous) (‚Äñf‚Äñ * ‚Äñg‚Äñ) (Œª x, calc
  ‚Äñf x ‚Ä¢ g x‚Äñ ‚â§ ‚Äñf x‚Äñ * ‚Äñg x‚Äñ : normed_space.norm_smul_le _ _
  ... ‚â§ ‚Äñf‚Äñ * ‚Äñg‚Äñ : mul_le_mul (f.norm_coe_le_norm _) (g.norm_coe_le_norm _) (norm_nonneg _)
    (norm_nonneg _)) ‚ü©

instance module' : module (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) :=
module.of_core $
{ smul     := (‚Ä¢),
  smul_add := Œª c f‚ÇÅ f‚ÇÇ, ext $ Œª x, smul_add _ _ _,
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, ext $ Œª x, add_smul _ _ _,
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, ext $ Œª x, mul_smul _ _ _,
  one_smul := Œª f, ext $ Œª x, one_smul ùïú (f x) }

lemma norm_smul_le (f : Œ± ‚Üí·µá ùïú) (g : Œ± ‚Üí·µá Œ≤) : ‚Äñf ‚Ä¢ g‚Äñ ‚â§ ‚Äñf‚Äñ * ‚Äñg‚Äñ :=
norm_of_normed_add_comm_group_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _

/- TODO: When `normed_module` has been added to `normed_space.basic`, the above facts
show that the space of bounded continuous functions from `Œ±` to `Œ≤` is naturally a normed
module over the algebra of bounded continuous functions from `Œ±` to `ùïú`. -/

end normed_algebra

lemma nnreal.upper_bound {Œ± : Type*} [topological_space Œ±]
  (f : Œ± ‚Üí·µá ‚Ñù‚â•0) (x : Œ±) : f x ‚â§ nndist f 0 :=
begin
  have key : nndist (f x) ((0 : Œ± ‚Üí·µá ‚Ñù‚â•0) x) ‚â§ nndist f 0,
  { exact @dist_coe_le_dist Œ± ‚Ñù‚â•0 _ _ f 0 x, },
  simp only [coe_zero, pi.zero_apply] at key,
  rwa nnreal.nndist_zero_eq_val' (f x) at key,
end

/-!
### Star structures

In this section, if `Œ≤` is a normed ‚ãÜ-group, then so is the space of bounded
continuous functions from `Œ±` to `Œ≤`, by using the star operation pointwise.

If `ùïú` is normed field and a ‚ãÜ-ring over which `Œ≤` is a normed algebra and a
star module, then the space of bounded continuous functions from `Œ±` to `Œ≤`
is a star module.

If `Œ≤` is a ‚ãÜ-ring in addition to being a normed ‚ãÜ-group, then `Œ± ‚Üí·µá Œ≤`
inherits a ‚ãÜ-ring structure.

In summary, if `Œ≤` is a C‚ãÜ-algebra over `ùïú`, then so is  `Œ± ‚Üí·µá Œ≤`; note that
completeness is guaranteed when `Œ≤` is complete (see
`bounded_continuous_function.complete`). -/

section normed_add_comm_group

variables {ùïú : Type*} [normed_field ùïú] [star_ring ùïú] [topological_space Œ±]
  [seminormed_add_comm_group Œ≤] [star_add_monoid Œ≤] [normed_star_group Œ≤]
variables [normed_space ùïú Œ≤] [star_module ùïú Œ≤]

instance : star_add_monoid (Œ± ‚Üí·µá Œ≤) :=
{ star            := Œª f, f.comp star star_normed_add_group_hom.lipschitz,
  star_involutive := Œª f, ext $ Œª x, star_star (f x),
  star_add        := Œª f g, ext $ Œª x, star_add (f x) (g x) }

/-- The right-hand side of this equality can be parsed `star ‚àò ‚áëf` because of the
instance `pi.has_star`. Upon inspecting the goal, one sees `‚ä¢ ‚áë(star f) = star ‚áëf`.-/
@[simp] lemma coe_star (f : Œ± ‚Üí·µá Œ≤) : ‚áë(star f) = star f := rfl

@[simp] lemma star_apply (f : Œ± ‚Üí·µá Œ≤) (x : Œ±) : star f x = star (f x) := rfl

instance : normed_star_group (Œ± ‚Üí·µá Œ≤) :=
{ norm_star := Œª f, by simp only [norm_eq, star_apply, norm_star] }

instance : star_module ùïú (Œ± ‚Üí·µá Œ≤) :=
{ star_smul := Œª k f, ext $ Œª x, star_smul k (f x) }

end normed_add_comm_group

section cstar_ring

variables [topological_space Œ±]
variables [non_unital_normed_ring Œ≤] [star_ring Œ≤]

instance [normed_star_group Œ≤] : star_ring (Œ± ‚Üí·µá Œ≤) :=
{ star_mul := Œª f g, ext $ Œª x, star_mul (f x) (g x),
  ..bounded_continuous_function.star_add_monoid }

variable [cstar_ring Œ≤]

instance : cstar_ring (Œ± ‚Üí·µá Œ≤) :=
{ norm_star_mul_self :=
  begin
    intro f,
    refine le_antisymm _ _,
    { rw [‚Üêsq, norm_le (sq_nonneg _)],
      dsimp [star_apply],
      intro x,
      rw [cstar_ring.norm_star_mul_self, ‚Üêsq],
      refine sq_le_sq' _ _,
      { linarith [norm_nonneg (f x), norm_nonneg f] },
      { exact norm_coe_le_norm f x }, },
    { rw [‚Üêsq, ‚Üêreal.le_sqrt (norm_nonneg _) (norm_nonneg _), norm_le (real.sqrt_nonneg _)],
      intro x,
      rw [real.le_sqrt (norm_nonneg _) (norm_nonneg _), sq, ‚Üêcstar_ring.norm_star_mul_self],
      exact norm_coe_le_norm (star f * f) x }
  end }

end cstar_ring

section normed_lattice_ordered_group

variables [topological_space Œ±] [normed_lattice_add_comm_group Œ≤]

instance : partial_order (Œ± ‚Üí·µá Œ≤) := partial_order.lift (Œª f, f.to_fun) (by tidy)

/--
Continuous normed lattice group valued functions form a meet-semilattice
-/
instance : semilattice_inf (Œ± ‚Üí·µá Œ≤) :=
{ inf := Œª f g,
  { to_fun := Œª t, f t ‚äì g t,
    continuous_to_fun := f.continuous.inf g.continuous,
    map_bounded' := begin
      obtain ‚ü®C‚ÇÅ, hf‚ü© := f.bounded,
      obtain ‚ü®C‚ÇÇ, hg‚ü© := g.bounded,
      refine ‚ü®C‚ÇÅ + C‚ÇÇ, Œª x y, _‚ü©,
      simp_rw normed_add_comm_group.dist_eq at hf hg ‚ä¢,
      exact (norm_inf_sub_inf_le_add_norm _ _ _ _).trans (add_le_add (hf _ _) (hg _ _)),
    end },
  inf_le_left := Œª f g, continuous_map.le_def.mpr (Œª _, inf_le_left),
  inf_le_right := Œª f g, continuous_map.le_def.mpr (Œª _, inf_le_right),
  le_inf := Œª f g‚ÇÅ g‚ÇÇ w‚ÇÅ w‚ÇÇ, continuous_map.le_def.mpr (Œª _, le_inf (continuous_map.le_def.mp w‚ÇÅ _)
    (continuous_map.le_def.mp w‚ÇÇ _)),
  ..bounded_continuous_function.partial_order }

instance : semilattice_sup (Œ± ‚Üí·µá Œ≤) :=
{ sup := Œª f g,
  { to_fun := Œª t, f t ‚äî g t,
    continuous_to_fun := f.continuous.sup g.continuous,
    map_bounded' := begin
      obtain ‚ü®C‚ÇÅ, hf‚ü© := f.bounded,
      obtain ‚ü®C‚ÇÇ, hg‚ü© := g.bounded,
      refine ‚ü®C‚ÇÅ + C‚ÇÇ, Œª x y, _‚ü©,
      simp_rw normed_add_comm_group.dist_eq at hf hg ‚ä¢,
      exact (norm_sup_sub_sup_le_add_norm _ _ _ _).trans (add_le_add (hf _ _) (hg _ _)),
    end },
  le_sup_left := Œª f g, continuous_map.le_def.mpr (Œª _, le_sup_left),
  le_sup_right := Œª f g, continuous_map.le_def.mpr (Œª _, le_sup_right),
  sup_le := Œª f g‚ÇÅ g‚ÇÇ w‚ÇÅ w‚ÇÇ, continuous_map.le_def.mpr (Œª _, sup_le (continuous_map.le_def.mp w‚ÇÅ _)
    (continuous_map.le_def.mp w‚ÇÇ _)),
  ..bounded_continuous_function.partial_order }

instance  : lattice (Œ± ‚Üí·µá Œ≤) :=
{ .. bounded_continuous_function.semilattice_sup, .. bounded_continuous_function.semilattice_inf }

@[simp] lemma coe_fn_sup (f g : Œ± ‚Üí·µá Œ≤) : ‚áë(f ‚äî g) = f ‚äî g := rfl

@[simp] lemma coe_fn_abs (f : Œ± ‚Üí·µá Œ≤) : ‚áë|f| = |f| := rfl

instance : normed_lattice_add_comm_group (Œ± ‚Üí·µá Œ≤) :=
{ add_le_add_left := begin
    intros f g h‚ÇÅ h t,
    simp only [coe_to_continuous_fun, pi.add_apply, add_le_add_iff_left, coe_add,
      continuous_map.to_fun_eq_coe],
    exact h‚ÇÅ _,
  end,
  solid :=
  begin
    intros f g h,
    have i1: ‚àÄ t, ‚Äñf t‚Äñ ‚â§ ‚Äñg t‚Äñ := Œª t, solid (h t),
    rw norm_le (norm_nonneg _),
    exact Œª t, (i1 t).trans (norm_coe_le_norm g t),
  end,
  ..bounded_continuous_function.lattice, ..bounded_continuous_function.seminormed_add_comm_group }

end normed_lattice_ordered_group

section nonnegative_part

variables [topological_space Œ±]

/-- The nonnegative part of a bounded continuous `‚Ñù`-valued function as a bounded
continuous `‚Ñù‚â•0`-valued function. -/
def nnreal_part (f : Œ± ‚Üí·µá ‚Ñù) : Œ± ‚Üí·µá ‚Ñù‚â•0 :=
bounded_continuous_function.comp _
  (show lipschitz_with 1 real.to_nnreal, from lipschitz_with_pos) f

@[simp] lemma nnreal_part_coe_fun_eq (f : Œ± ‚Üí·µá ‚Ñù) : ‚áë(f.nnreal_part) = real.to_nnreal ‚àò ‚áëf := rfl

/-- The absolute value of a bounded continuous `‚Ñù`-valued function as a bounded
continuous `‚Ñù‚â•0`-valued function. -/
def nnnorm (f : Œ± ‚Üí·µá ‚Ñù) : Œ± ‚Üí·µá ‚Ñù‚â•0 :=
bounded_continuous_function.comp _
  (show lipschitz_with 1 (Œª (x : ‚Ñù), ‚Äñx‚Äñ‚Çä), from lipschitz_with_one_norm) f

@[simp] lemma nnnorm_coe_fun_eq (f : Œ± ‚Üí·µá ‚Ñù) : ‚áë(f.nnnorm) = has_nnnorm.nnnorm ‚àò ‚áëf := rfl

/-- Decompose a bounded continuous function to its positive and negative parts. -/
lemma self_eq_nnreal_part_sub_nnreal_part_neg (f : Œ± ‚Üí·µá ‚Ñù) :
  ‚áëf = coe ‚àò f.nnreal_part - coe ‚àò (-f).nnreal_part :=
by { funext x, dsimp, simp only [max_zero_sub_max_neg_zero_eq_self], }

/-- Express the absolute value of a bounded continuous function in terms of its
positive and negative parts. -/
lemma abs_self_eq_nnreal_part_add_nnreal_part_neg (f : Œ± ‚Üí·µá ‚Ñù) :
  abs ‚àò ‚áëf = coe ‚àò f.nnreal_part + coe ‚àò (-f).nnreal_part :=
by { funext x, dsimp, simp only [max_zero_add_max_neg_zero_eq_abs_self], }

end nonnegative_part

end bounded_continuous_function
