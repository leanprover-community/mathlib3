/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Mario Carneiro, Yury Kudryashov, Heather Macbeth
-/
import analysis.normed_space.operator_norm
import topology.continuous_function.algebra

/-!
# Bounded continuous functions

The type of bounded continuous functions taking values in a metric space, with
the uniform distance.

-/

noncomputable theory
open_locale topological_space classical nnreal

open set filter metric

universes u v w
variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

/-- The type of bounded continuous functions from a topological space to a metric space -/
structure bounded_continuous_function
  (Œ± : Type u) (Œ≤ : Type v) [topological_space Œ±] [metric_space Œ≤] extends continuous_map Œ± Œ≤ :
  Type (max u v) :=
(bounded' : ‚àÉC, ‚àÄx y:Œ±, dist (to_fun x) (to_fun y) ‚â§ C)

localized "infixr ` ‚Üí·µá `:25 := bounded_continuous_function" in bounded_continuous_function

namespace bounded_continuous_function
section basics
variables [topological_space Œ±] [metric_space Œ≤] [metric_space Œ≥]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance : has_coe_to_fun (Œ± ‚Üí·µá Œ≤) (Œª _, Œ± ‚Üí Œ≤) :=  ‚ü®Œª f, f.to_fun‚ü©

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def simps.apply (h : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí Œ≤ := h

initialize_simps_projections bounded_continuous_function (to_continuous_map_to_fun ‚Üí apply)

protected lemma bounded (f : Œ± ‚Üí·µá Œ≤) : ‚àÉC, ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C := f.bounded'
@[continuity]
protected lemma continuous (f : Œ± ‚Üí·µá Œ≤) : continuous f := f.to_continuous_map.continuous

@[ext] lemma ext (H : ‚àÄx, f x = g x) : f = g :=
by { cases f, cases g, congr, ext, exact H x, }

lemma ext_iff : f = g ‚Üî ‚àÄ x, f x = g x :=
‚ü®Œª h, Œª x, h ‚ñ∏ rfl, ext‚ü©

lemma bounded_range : bounded (range f) :=
bounded_range_iff.2 f.bounded

/-- A continuous function with an explicit bound is a bounded continuous function. -/
def mk_of_bound (f : C(Œ±, Œ≤)) (C : ‚Ñù) (h : ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
‚ü®f, ‚ü®C, h‚ü©‚ü©

@[simp] lemma mk_of_bound_coe {f} {C} {h} : (mk_of_bound f C h : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤) :=
rfl

/-- A continuous function on a compact space is automatically a bounded continuous function. -/
def mk_of_compact [compact_space Œ±] (f : C(Œ±, Œ≤)) : Œ± ‚Üí·µá Œ≤ :=
‚ü®f, bounded_range_iff.1 (is_compact_range f.continuous).bounded‚ü©

@[simp] lemma mk_of_compact_apply [compact_space Œ±] (f : C(Œ±, Œ≤)) (a : Œ±) :
  mk_of_compact f a = f a :=
rfl

/-- If a function is bounded on a discrete space, it is automatically continuous,
and therefore gives rise to an element of the type of bounded continuous functions -/
@[simps] def mk_of_discrete [discrete_topology Œ±] (f : Œ± ‚Üí Œ≤)
  (C : ‚Ñù) (h : ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
‚ü®‚ü®f, continuous_of_discrete_topology‚ü©, ‚ü®C, h‚ü©‚ü©

section
variables (Œ± Œ≤)
/--
The map forgetting that a bounded continuous function is bounded.
-/
def forget_boundedness : (Œ± ‚Üí·µá Œ≤) ‚Üí C(Œ±, Œ≤) :=
Œª f, f.1

@[simp] lemma forget_boundedness_coe (f : Œ± ‚Üí·µá Œ≤) : (forget_boundedness Œ± Œ≤ f : Œ± ‚Üí Œ≤) = f :=
rfl

end

/-- The uniform distance between two bounded continuous functions -/
instance : has_dist (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf g, Inf {C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C}‚ü©

lemma dist_eq : dist f g = Inf {C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C} := rfl

lemma dist_set_exists : ‚àÉ C, 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C :=
begin
  refine if h : nonempty Œ± then _ else ‚ü®0, le_refl _, Œª x, h.elim ‚ü®x‚ü©‚ü©,
  cases h with x,
  rcases f.bounded with ‚ü®Cf, hCf : ‚àÄ x y, dist (f x) (f y) ‚â§ Cf‚ü©,
  rcases g.bounded with ‚ü®Cg, hCg : ‚àÄ x y, dist (g x) (g y) ‚â§ Cg‚ü©,
  let C := max 0 (dist (f x) (g x) + (Cf + Cg)),
  refine ‚ü®C, le_max_left _ _, Œª y, _‚ü©,
  calc dist (f y) (g y) ‚â§ dist (f x) (g x) + (dist (f x) (f y) + dist (g x) (g y)) :
    dist_triangle4_left _ _ _ _
                    ... ‚â§ dist (f x) (g x) + (Cf + Cg) : by mono*
                    ... ‚â§ C : le_max_right _ _
end

/-- The pointwise distance is controlled by the distance between functions, by definition. -/
lemma dist_coe_le_dist (x : Œ±) : dist (f x) (g x) ‚â§ dist f g :=
le_cInf dist_set_exists $ Œªb hb, hb.2 x

/- This lemma will be needed in the proof of the metric space instance, but it will become
useless afterwards as it will be superseded by the general result that the distance is nonnegative
in metric spaces. -/
private lemma dist_nonneg' : 0 ‚â§ dist f g :=
le_cInf dist_set_exists (Œª C, and.left)

/-- The distance between two functions is controlled by the supremum of the pointwise distances -/
lemma dist_le (C0 : (0 : ‚Ñù) ‚â§ C) : dist f g ‚â§ C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) ‚â§ C :=
‚ü®Œª h x, le_trans (dist_coe_le_dist x) h, Œª H, cInf_le ‚ü®0, Œª C, and.left‚ü© ‚ü®C0, H‚ü©‚ü©

lemma dist_le_iff_of_nonempty [nonempty Œ±] :
  dist f g ‚â§ C ‚Üî ‚àÄ x, dist (f x) (g x) ‚â§ C :=
‚ü®Œª h x, le_trans (dist_coe_le_dist x) h,
 Œª w, (dist_le (le_trans dist_nonneg (w (nonempty.some ‚Äπ_‚Ä∫)))).mpr w‚ü©

lemma dist_lt_of_nonempty_compact [nonempty Œ±] [compact_space Œ±]
  (w : ‚àÄx:Œ±, dist (f x) (g x) < C) : dist f g < C :=
begin
  have c : continuous (Œª x, dist (f x) (g x)), { continuity, },
  obtain ‚ü®x, -, le‚ü© :=
    is_compact.exists_forall_ge compact_univ set.univ_nonempty (continuous.continuous_on c),
  exact lt_of_le_of_lt (dist_le_iff_of_nonempty.mpr (Œª y, le y trivial)) (w x),
end

lemma dist_lt_iff_of_compact [compact_space Œ±] (C0 : (0 : ‚Ñù) < C) :
  dist f g < C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) < C :=
begin
  fsplit,
  { intros w x,
    exact lt_of_le_of_lt (dist_coe_le_dist x) w, },
  { by_cases h : nonempty Œ±,
    { resetI,
      exact dist_lt_of_nonempty_compact, },
    { rintro -,
      convert C0,
      apply le_antisymm _ dist_nonneg',
      rw [dist_eq],
      exact cInf_le ‚ü®0, Œª C, and.left‚ü© ‚ü®le_refl _, Œª x, false.elim (h (nonempty.intro x))‚ü©, }, },
end

lemma dist_lt_iff_of_nonempty_compact [nonempty Œ±] [compact_space Œ±] :
  dist f g < C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) < C :=
‚ü®Œª w x, lt_of_le_of_lt (dist_coe_le_dist x) w, dist_lt_of_nonempty_compact‚ü©

/-- On an empty space, bounded continuous functions are at distance 0 -/
lemma dist_zero_of_empty [is_empty Œ±] : dist f g = 0 :=
le_antisymm ((dist_le (le_refl _)).2 is_empty_elim) dist_nonneg'

/-- The type of bounded continuous functions, with the uniform distance, is a metric space. -/
instance : metric_space (Œ± ‚Üí·µá Œ≤) :=
{ dist_self := Œª f, le_antisymm ((dist_le (le_refl _)).2 $ Œª x, by simp) dist_nonneg',
  eq_of_dist_eq_zero := Œª f g hfg, by ext x; exact
    eq_of_dist_eq_zero (le_antisymm (hfg ‚ñ∏ dist_coe_le_dist _) dist_nonneg),
  dist_comm := Œª f g, by simp [dist_eq, dist_comm],
  dist_triangle := Œª f g h,
    (dist_le (add_nonneg dist_nonneg' dist_nonneg')).2 $ Œª x,
      le_trans (dist_triangle _ _ _) (add_le_add (dist_coe_le_dist _) (dist_coe_le_dist _)) }

variables (Œ±) {Œ≤}

/-- Constant as a continuous bounded function. -/
@[simps {fully_applied := ff}] def const (b : Œ≤) : Œ± ‚Üí·µá Œ≤ :=
‚ü®continuous_map.const b, 0, by simp [le_refl]‚ü©

variable {Œ±}

lemma const_apply' (a : Œ±) (b : Œ≤) : (const Œ± b : Œ± ‚Üí Œ≤) a = b := rfl

/-- If the target space is inhabited, so is the space of bounded continuous functions -/
instance [inhabited Œ≤] : inhabited (Œ± ‚Üí·µá Œ≤) := ‚ü®const Œ± (default Œ≤)‚ü©

lemma lipschitz_evalx (x : Œ±) : lipschitz_with 1 (Œª f : Œ± ‚Üí·µá Œ≤, f x) :=
lipschitz_with.mk_one $ Œª f g, dist_coe_le_dist x

theorem uniform_continuous_coe : @uniform_continuous (Œ± ‚Üí·µá Œ≤) (Œ± ‚Üí Œ≤) _ _ coe_fn :=
uniform_continuous_pi.2 $ Œª x, (lipschitz_evalx x).uniform_continuous

lemma continuous_coe : continuous (Œª (f : Œ± ‚Üí·µá Œ≤) x, f x) :=
uniform_continuous.continuous uniform_continuous_coe

/-- When `x` is fixed, `(f : Œ± ‚Üí·µá Œ≤) ‚Ü¶ f x` is continuous -/
@[continuity] theorem continuous_evalx {x : Œ±} : continuous (Œª f : Œ± ‚Üí·µá Œ≤, f x) :=
(continuous_apply x).comp continuous_coe

/-- The evaluation map is continuous, as a joint function of `u` and `x` -/
@[continuity] theorem continuous_eval : continuous (Œª p : (Œ± ‚Üí·µá Œ≤) √ó Œ±, p.1 p.2) :=
continuous_prod_of_continuous_lipschitz _ 1 (Œª f, f.continuous) $ lipschitz_evalx

/-- Bounded continuous functions taking values in a complete space form a complete space. -/
instance [complete_space Œ≤] : complete_space (Œ± ‚Üí·µá Œ≤) :=
complete_of_cauchy_seq_tendsto $ Œª (f : ‚Ñï ‚Üí Œ± ‚Üí·µá Œ≤) (hf : cauchy_seq f),
begin
  /- We have to show that `f n` converges to a bounded continuous function.
  For this, we prove pointwise convergence to define the limit, then check
  it is a continuous bounded function, and then check the norm convergence. -/
  rcases cauchy_seq_iff_le_tendsto_0.1 hf with ‚ü®b, b0, b_bound, b_lim‚ü©,
  have f_bdd := Œªx n m N hn hm, le_trans (dist_coe_le_dist x) (b_bound n m N hn hm),
  have fx_cau : ‚àÄx, cauchy_seq (Œªn, f n x) :=
    Œªx, cauchy_seq_iff_le_tendsto_0.2 ‚ü®b, b0, f_bdd x, b_lim‚ü©,
  choose F hF using Œªx, cauchy_seq_tendsto_of_complete (fx_cau x),
  /- F : Œ± ‚Üí Œ≤,  hF : ‚àÄ (x : Œ±), tendsto (Œª (n : ‚Ñï), f n x) at_top (ùìù (F x))
  `F` is the desired limit function. Check that it is uniformly approximated by `f N` -/
  have fF_bdd : ‚àÄx N, dist (f N x) (F x) ‚â§ b N :=
    Œª x N, le_of_tendsto (tendsto_const_nhds.dist (hF x))
      (filter.eventually_at_top.2 ‚ü®N, Œªn hn, f_bdd x N n N (le_refl N) hn‚ü©),
  refine ‚ü®‚ü®‚ü®F, _‚ü©, _‚ü©, _‚ü©,
  { /- Check that `F` is continuous, as a uniform limit of continuous functions -/
    have : tendsto_uniformly (Œªn x, f n x) F at_top,
    { refine metric.tendsto_uniformly_iff.2 (Œª Œµ Œµ0, _),
      refine ((tendsto_order.1 b_lim).2 Œµ Œµ0).mono (Œª n hn x, _),
      rw dist_comm,
      exact lt_of_le_of_lt (fF_bdd x n) hn },
    exact this.continuous (ŒªN, (f N).continuous) },
  { /- Check that `F` is bounded -/
    rcases (f 0).bounded with ‚ü®C, hC‚ü©,
    refine ‚ü®C + (b 0 + b 0), Œª x y, _‚ü©,
    calc dist (F x) (F y) ‚â§ dist (f 0 x) (f 0 y) + (dist (f 0 x) (F x) + dist (f 0 y) (F y)) :
      dist_triangle4_left _ _ _ _
                      ... ‚â§ C + (b 0 + b 0) : by mono* },
  { /- Check that `F` is close to `f N` in distance terms -/
    refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero (Œª _, dist_nonneg) _ b_lim),
    exact Œª N, (dist_le (b0 _)).2 (Œªx, fF_bdd x N) }
end

/-- Composition of a bounded continuous function and a continuous function. -/
@[simps { fully_applied := ff }]
def comp_continuous {Œ¥ : Type*} [topological_space Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) : Œ¥ ‚Üí·µá Œ≤ :=
{ to_continuous_map := f.1.comp g,
  bounded' := f.bounded'.imp (Œª C hC x y, hC _ _) }

/-- Restrict a bounded continuous function to a set. -/
@[simps apply { fully_applied := ff }]
def restrict (f : Œ± ‚Üí·µá Œ≤) (s : set Œ±) : s ‚Üí·µá Œ≤ := f.comp_continuous (continuous_map.id.restrict s)

/-- Composition (in the target) of a bounded continuous function with a Lipschitz map again
gives a bounded continuous function -/
def comp (G : Œ≤ ‚Üí Œ≥) {C : ‚Ñù‚â•0} (H : lipschitz_with C G)
  (f : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí·µá Œ≥ :=
‚ü®‚ü®Œªx, G (f x), H.continuous.comp f.continuous‚ü©,
  let ‚ü®D, hD‚ü© := f.bounded in
  ‚ü®max C 0 * D, Œª x y, calc
    dist (G (f x)) (G (f y)) ‚â§ C * dist (f x) (f y) : H.dist_le_mul _ _
    ... ‚â§ max C 0 * dist (f x) (f y) : mul_le_mul_of_nonneg_right (le_max_left C 0) dist_nonneg
    ... ‚â§ max C 0 * D : mul_le_mul_of_nonneg_left (hD _ _) (le_max_right C 0)‚ü©‚ü©

/-- The composition operator (in the target) with a Lipschitz map is Lipschitz -/
lemma lipschitz_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : lipschitz_with C G) :
  lipschitz_with C (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
lipschitz_with.of_dist_le_mul $ Œª f g,
(dist_le (mul_nonneg C.2 dist_nonneg)).2 $ Œª x,
calc dist (G (f x)) (G (g x)) ‚â§ C * dist (f x) (g x) : H.dist_le_mul _ _
  ... ‚â§ C * dist f g : mul_le_mul_of_nonneg_left (dist_coe_le_dist _) C.2

/-- The composition operator (in the target) with a Lipschitz map is uniformly continuous -/
lemma uniform_continuous_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : lipschitz_with C G) :
  uniform_continuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
(lipschitz_comp H).uniform_continuous

/-- The composition operator (in the target) with a Lipschitz map is continuous -/
lemma continuous_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : lipschitz_with C G) :
  continuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
(lipschitz_comp H).continuous

/-- Restriction (in the target) of a bounded continuous function taking values in a subset -/
def cod_restrict (s : set Œ≤) (f : Œ± ‚Üí·µá Œ≤) (H : ‚àÄx, f x ‚àà s) : Œ± ‚Üí·µá s :=
‚ü®‚ü®s.cod_restrict f H, continuous_subtype_mk _ f.continuous‚ü©, f.bounded‚ü©

end basics

section arzela_ascoli
variables [topological_space Œ±] [compact_space Œ±] [metric_space Œ≤]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

/- Arzela-Ascoli theorem asserts that, on a compact space, a set of functions sharing
a common modulus of continuity and taking values in a compact set forms a compact
subset for the topology of uniform convergence. In this section, we prove this theorem
and several useful variations around it. -/

/-- First version, with pointwise equicontinuity and range in a compact space -/
theorem arzela_ascoli‚ÇÅ [compact_space Œ≤]
  (A : set (Œ± ‚Üí·µá Œ≤))
  (closed : is_closed A)
  (H : ‚àÄ (x:Œ±) (Œµ > 0), ‚àÉU ‚àà ùìù x, ‚àÄ (y z ‚àà U) (f : Œ± ‚Üí·µá Œ≤),
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ) :
  is_compact A :=
begin
  refine compact_of_totally_bounded_is_closed _ closed,
  refine totally_bounded_of_finite_discretization (Œª Œµ Œµ0, _),
  rcases exists_between Œµ0 with ‚ü®Œµ‚ÇÅ, Œµ‚ÇÅ0, ŒµŒµ‚ÇÅ‚ü©,
  let Œµ‚ÇÇ := Œµ‚ÇÅ/2/2,
  /- We have to find a finite discretization of `u`, i.e., finite information
  that is sufficient to reconstruct `u` up to Œµ. This information will be
  provided by the values of `u` on a sufficiently dense set tŒ±,
  slightly translated to fit in a finite Œµ‚ÇÇ-dense set tŒ≤ in the image. Such
  sets exist by compactness of the source and range. Then, to check that these
  data determine the function up to Œµ, one uses the control on the modulus of
  continuity to extend the closeness on tŒ± to closeness everywhere. -/
  have Œµ‚ÇÇ0 : Œµ‚ÇÇ > 0 := half_pos (half_pos Œµ‚ÇÅ0),
  have : ‚àÄx:Œ±, ‚àÉU, x ‚àà U ‚àß is_open U ‚àß ‚àÄ (y z ‚àà U) {f : Œ± ‚Üí·µá Œ≤},
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ‚ÇÇ := Œª x,
      let ‚ü®U, nhdsU, hU‚ü© := H x _ Œµ‚ÇÇ0,
          ‚ü®V, VU, openV, xV‚ü© := _root_.mem_nhds_iff.1 nhdsU in
      ‚ü®V, xV, openV, Œªy z hy hz f hf, hU y z (VU hy) (VU hz) f hf‚ü©,
  choose U hU using this,
  /- For all x, the set hU x is an open set containing x on which the elements of A
  fluctuate by at most Œµ‚ÇÇ.
  We extract finitely many of these sets that cover the whole space, by compactness -/
  rcases compact_univ.elim_finite_subcover_image
    (Œªx _, (hU x).2.1) (Œªx hx, mem_bUnion (mem_univ _) (hU x).1)
    with ‚ü®tŒ±, _, ‚ü®_‚ü©, htŒ±‚ü©,
  /- tŒ± : set Œ±, htŒ± : univ ‚äÜ ‚ãÉx ‚àà tŒ±, U x -/
  rcases @finite_cover_balls_of_compact Œ≤ _ _ compact_univ _ Œµ‚ÇÇ0
    with ‚ü®tŒ≤, _, ‚ü®_‚ü©, htŒ≤‚ü©, resetI,
  /- tŒ≤ : set Œ≤, htŒ≤ : univ ‚äÜ ‚ãÉy ‚àà tŒ≤, ball y Œµ‚ÇÇ -/
  /- Associate to every point `y` in the space a nearby point `F y` in tŒ≤ -/
  choose F hF using Œªy, show ‚àÉz‚ààtŒ≤, dist y z < Œµ‚ÇÇ, by simpa using htŒ≤ (mem_univ y),
  /- F : Œ≤ ‚Üí Œ≤, hF : ‚àÄ (y : Œ≤), F y ‚àà tŒ≤ ‚àß dist y (F y) < Œµ‚ÇÇ -/

  /- Associate to every function a discrete approximation, mapping each point in `tŒ±`
  to a point in `tŒ≤` close to its true image by the function. -/
  refine ‚ü®tŒ± ‚Üí tŒ≤, by apply_instance, Œª f a, ‚ü®F (f a), (hF (f a)).1‚ü©, _‚ü©,
  rintro ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© f_eq_g,
  /- If two functions have the same approximation, then they are within distance Œµ -/
  refine lt_of_le_of_lt ((dist_le $ le_of_lt Œµ‚ÇÅ0).2 (Œª x, _)) ŒµŒµ‚ÇÅ,
  obtain ‚ü®x', x'tŒ±, hx'‚ü© : ‚àÉx' ‚àà tŒ±, x ‚àà U x' := mem_bUnion_iff.1 (htŒ± (mem_univ x)),
  calc dist (f x) (g x)
      ‚â§ dist (f x) (f x') + dist (g x) (g x') + dist (f x') (g x') : dist_triangle4_right _ _ _ _
  ... ‚â§ Œµ‚ÇÇ + Œµ‚ÇÇ + Œµ‚ÇÅ/2 : le_of_lt (add_lt_add (add_lt_add _ _) _)
  ... = Œµ‚ÇÅ : by rw [add_halves, add_halves],
  { exact (hU x').2.2 _ _ hx' ((hU x').1) hf },
  { exact (hU x').2.2 _ _ hx' ((hU x').1) hg },
  { have F_f_g : F (f x') = F (g x') :=
      (congr_arg (Œª f:tŒ± ‚Üí tŒ≤, (f ‚ü®x', x'tŒ±‚ü© : Œ≤)) f_eq_g : _),
    calc dist (f x') (g x')
          ‚â§ dist (f x') (F (f x')) + dist (g x') (F (f x')) : dist_triangle_right _ _ _
      ... = dist (f x') (F (f x')) + dist (g x') (F (g x')) : by rw F_f_g
      ... < Œµ‚ÇÇ + Œµ‚ÇÇ : add_lt_add (hF (f x')).2 (hF (g x')).2
      ... = Œµ‚ÇÅ/2 : add_halves _ }
end

/-- Second version, with pointwise equicontinuity and range in a compact subset -/
theorem arzela_ascoli‚ÇÇ
  (s : set Œ≤) (hs : is_compact s)
  (A : set (Œ± ‚Üí·µá Œ≤))
  (closed : is_closed A)
  (in_s : ‚àÄ(f : Œ± ‚Üí·µá Œ≤) (x : Œ±), f ‚àà A ‚Üí f x ‚àà s)
  (H : ‚àÄ(x:Œ±) (Œµ > 0), ‚àÉU ‚àà ùìù x, ‚àÄ (y z ‚àà U) (f : Œ± ‚Üí·µá Œ≤),
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ) :
  is_compact A :=
/- This version is deduced from the previous one by restricting to the compact type in the target,
using compactness there and then lifting everything to the original space. -/
begin
  have M : lipschitz_with 1 coe := lipschitz_with.subtype_coe s,
  let F : (Œ± ‚Üí·µá s) ‚Üí Œ± ‚Üí·µá Œ≤ := comp coe M,
  refine compact_of_is_closed_subset
    ((_ : is_compact (F ‚Åª¬π' A)).image (continuous_comp M)) closed (Œª f hf, _),
  { haveI : compact_space s := is_compact_iff_compact_space.1 hs,
    refine arzela_ascoli‚ÇÅ _ (continuous_iff_is_closed.1 (continuous_comp M) _ closed)
      (Œª x Œµ Œµ0, bex.imp_right (Œª U U_nhds hU y z hy hz f hf, _) (H x Œµ Œµ0)),
    calc dist (f y) (f z) = dist (F f y) (F f z) : rfl
                        ... < Œµ : hU y z hy hz (F f) hf },
  { let g := cod_restrict s f (Œªx, in_s f x hf),
    rw [show f = F g, by ext; refl] at hf ‚ä¢,
    exact ‚ü®g, hf, rfl‚ü© }
end

/-- Third (main) version, with pointwise equicontinuity and range in a compact subset, but
without closedness. The closure is then compact -/
theorem arzela_ascoli
  (s : set Œ≤) (hs : is_compact s)
  (A : set (Œ± ‚Üí·µá Œ≤))
  (in_s : ‚àÄ(f : Œ± ‚Üí·µá Œ≤) (x : Œ±), f ‚àà A ‚Üí f x ‚àà s)
  (H : ‚àÄ(x:Œ±) (Œµ > 0), ‚àÉU ‚àà ùìù x, ‚àÄ (y z ‚àà U) (f : Œ± ‚Üí·µá Œ≤),
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ) :
  is_compact (closure A) :=
/- This version is deduced from the previous one by checking that the closure of A, in
addition to being closed, still satisfies the properties of compact range and equicontinuity -/
arzela_ascoli‚ÇÇ s hs (closure A) is_closed_closure
  (Œª f x hf, (mem_of_closed' hs.is_closed).2 $ Œª Œµ Œµ0,
    let ‚ü®g, gA, dist_fg‚ü© := metric.mem_closure_iff.1 hf Œµ Œµ0 in
    ‚ü®g x, in_s g x gA, lt_of_le_of_lt (dist_coe_le_dist _) dist_fg‚ü©)
  (Œª x Œµ Œµ0, show ‚àÉ U ‚àà ùìù x,
      ‚àÄ y z ‚àà U, ‚àÄ (f : Œ± ‚Üí·µá Œ≤), f ‚àà closure A ‚Üí dist (f y) (f z) < Œµ,
    begin
      refine bex.imp_right (Œª U U_set hU y z hy hz f hf, _) (H x (Œµ/2) (half_pos Œµ0)),
      rcases metric.mem_closure_iff.1 hf (Œµ/2/2) (half_pos (half_pos Œµ0)) with ‚ü®g, gA, dist_fg‚ü©,
      replace dist_fg := Œª x, lt_of_le_of_lt (dist_coe_le_dist x) dist_fg,
      calc dist (f y) (f z) ‚â§ dist (f y) (g y) + dist (f z) (g z) + dist (g y) (g z) :
        dist_triangle4_right _ _ _ _
          ... < Œµ/2/2 + Œµ/2/2 + Œµ/2 :
            add_lt_add (add_lt_add (dist_fg y) (dist_fg z)) (hU y z hy hz g gA)
          ... = Œµ : by rw [add_halves, add_halves]
    end)

/- To apply the previous theorems, one needs to check the equicontinuity. An important
instance is when the source space is a metric space, and there is a fixed modulus of continuity
for all the functions in the set A -/

lemma equicontinuous_of_continuity_modulus {Œ± : Type u} [metric_space Œ±]
  (b : ‚Ñù ‚Üí ‚Ñù) (b_lim : tendsto b (ùìù 0) (ùìù 0))
  (A : set (Œ± ‚Üí·µá Œ≤))
  (H : ‚àÄ(x y:Œ±) (f : Œ± ‚Üí·µá Œ≤), f ‚àà A ‚Üí dist (f x) (f y) ‚â§ b (dist x y))
  (x:Œ±) (Œµ : ‚Ñù) (Œµ0 : 0 < Œµ) : ‚àÉU ‚àà ùìù x, ‚àÄ (y z ‚àà U) (f : Œ± ‚Üí·µá Œ≤),
    f ‚àà A ‚Üí dist (f y) (f z) < Œµ :=
begin
  rcases tendsto_nhds_nhds.1 b_lim Œµ Œµ0 with ‚ü®Œ¥, Œ¥0, hŒ¥‚ü©,
  refine ‚ü®ball x (Œ¥/2), ball_mem_nhds x (half_pos Œ¥0), Œª y z hy hz f hf, _‚ü©,
  have : dist y z < Œ¥ := calc
    dist y z ‚â§ dist y x + dist z x : dist_triangle_right _ _ _
    ... < Œ¥/2 + Œ¥/2 : add_lt_add hy hz
    ... = Œ¥ : add_halves _,
  calc
    dist (f y) (f z) ‚â§ b (dist y z) : H y z f hf
    ... ‚â§ |b (dist y z)| : le_abs_self _
    ... = dist (b (dist y z)) 0 : by simp [real.dist_eq]
    ... < Œµ : hŒ¥ (by simpa [real.dist_eq] using this),
end

end arzela_ascoli

section has_lipschitz_add
/- In this section, if `Œ≤` is an `add_monoid` whose addition operation is Lipschitz, then we show
that the space of bounded continuous functions from `Œ±` to `Œ≤` inherits a topological `add_monoid`
structure, by using pointwise operations and checking that they are compatible with the uniform
distance.

Implementation note: The material in this section could have been written for `has_lipschitz_mul`
and transported by `@[to_additive]`.  We choose not to do this because this causes a few lemma
names (for example, `coe_mul`) to conflict with later lemma names for normed rings; this is only a
trivial inconvenience, but in any case there are no obvious applications of the multiplicative
version. -/

variables [topological_space Œ±] [metric_space Œ≤] [add_monoid Œ≤]

instance : has_zero (Œ± ‚Üí·µá Œ≤) := ‚ü®const Œ± 0‚ü©

@[simp] lemma coe_zero : ((0 : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí Œ≤) = 0 := rfl

lemma forall_coe_zero_iff_zero (f : Œ± ‚Üí·µá Œ≤) : (‚àÄx, f x = 0) ‚Üî f = 0 := (@ext_iff _ _ _ _ f 0).symm

variables [has_lipschitz_add Œ≤]
variables (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

/-- The pointwise sum of two bounded continuous functions is again bounded continuous. -/
instance : has_add (Œ± ‚Üí·µá Œ≤) :=
{ add := Œª f g,
    bounded_continuous_function.mk_of_bound (f.to_continuous_map + g.to_continuous_map)
    (‚Üë(has_lipschitz_add.C Œ≤) * max (classical.some f.bounded) (classical.some g.bounded))
    begin
      intros x y,
      refine le_trans (lipschitz_with_lipschitz_const_add ‚ü®f x, g x‚ü© ‚ü®f y, g y‚ü©) _,
      rw prod.dist_eq,
      refine mul_le_mul_of_nonneg_left _ (has_lipschitz_add.C Œ≤).coe_nonneg,
      apply max_le_max,
      exact classical.some_spec f.bounded x y,
      exact classical.some_spec g.bounded x y,
    end }

@[simp] lemma coe_add : ‚áë(f + g) = f + g := rfl
lemma add_apply : (f + g) x = f x + g x := rfl

instance : add_monoid (Œ± ‚Üí·µá Œ≤) :=
{ add_assoc      := assume f g h, by ext; simp [add_assoc],
  zero_add       := assume f, by ext; simp,
  add_zero       := assume f, by ext; simp,
  .. bounded_continuous_function.has_add,
  .. bounded_continuous_function.has_zero }

instance : has_lipschitz_add (Œ± ‚Üí·µá Œ≤) :=
{ lipschitz_add := ‚ü®has_lipschitz_add.C Œ≤, begin
    have C_nonneg := (has_lipschitz_add.C Œ≤).coe_nonneg,
    rw lipschitz_with_iff_dist_le_mul,
    rintros ‚ü®f‚ÇÅ, g‚ÇÅ‚ü© ‚ü®f‚ÇÇ, g‚ÇÇ‚ü©,
    rw dist_le (mul_nonneg C_nonneg dist_nonneg),
    intros x,
    refine le_trans (lipschitz_with_lipschitz_const_add ‚ü®f‚ÇÅ x, g‚ÇÅ x‚ü© ‚ü®f‚ÇÇ x, g‚ÇÇ x‚ü©) _,
    refine mul_le_mul_of_nonneg_left _ C_nonneg,
    apply max_le_max; exact dist_coe_le_dist x,
  end‚ü© }

/-- Coercion of a `normed_group_hom` is an `add_monoid_hom`. Similar to `add_monoid_hom.coe_fn` -/
@[simps] def coe_fn_add_hom : (Œ± ‚Üí·µá Œ≤) ‚Üí+ (Œ± ‚Üí Œ≤) :=
{ to_fun := coe_fn, map_zero' := coe_zero, map_add' := coe_add }

variables (Œ± Œ≤)

/-- The additive map forgetting that a bounded continuous function is bounded.
-/
@[simps] def forget_boundedness_add_hom : (Œ± ‚Üí·µá Œ≤) ‚Üí+ C(Œ±, Œ≤) :=
{ to_fun := forget_boundedness Œ± Œ≤,
  map_zero' := by { ext, simp, },
  map_add' := by { intros, ext, simp, }, }

end has_lipschitz_add

section comm_has_lipschitz_add

variables [topological_space Œ±] [metric_space Œ≤] [add_comm_monoid Œ≤] [has_lipschitz_add Œ≤]

@[to_additive] instance : add_comm_monoid (Œ± ‚Üí·µá Œ≤) :=
{ add_comm      := assume f g, by ext; simp [add_comm],
  .. bounded_continuous_function.add_monoid }

open_locale big_operators

@[simp] lemma coe_sum {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí (Œ± ‚Üí·µá Œ≤)) :
  ‚áë(‚àë i in s, f i) = (‚àë i in s, (f i : Œ± ‚Üí Œ≤)) :=
(@coe_fn_add_hom Œ± Œ≤ _ _ _ _).map_sum f s

lemma sum_apply {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí (Œ± ‚Üí·µá Œ≤)) (a : Œ±) :
  (‚àë i in s, f i) a = (‚àë i in s, f i a) :=
by simp

end comm_has_lipschitz_add

section normed_group
/- In this section, if Œ≤ is a normed group, then we show that the space of bounded
continuous functions from Œ± to Œ≤ inherits a normed group structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Œ±] [normed_group Œ≤]
variables (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

instance : has_norm (Œ± ‚Üí·µá Œ≤) := ‚ü®Œªu, dist u 0‚ü©

lemma norm_def : ‚à•f‚à• = dist f 0 := rfl

/-- The norm of a bounded continuous function is the supremum of `‚à•f x‚à•`.
We use `Inf` to ensure that the definition works if `Œ±` has no elements. -/
lemma norm_eq (f : Œ± ‚Üí·µá Œ≤) :
  ‚à•f‚à• = Inf {C : ‚Ñù | 0 ‚â§ C ‚àß ‚àÄ (x : Œ±), ‚à•f x‚à• ‚â§ C} :=
by simp [norm_def, bounded_continuous_function.dist_eq]

/-- When the domain is non-empty, we do not need the `0 ‚â§ C` condition in the formula for ‚à•f‚à• as an
`Inf`. -/
lemma norm_eq_of_nonempty [h : nonempty Œ±] : ‚à•f‚à• = Inf {C : ‚Ñù | ‚àÄ (x : Œ±), ‚à•f x‚à• ‚â§ C} :=
begin
  unfreezingI { obtain ‚ü®a‚ü© := h, },
  rw norm_eq,
  congr,
  ext,
  simp only [and_iff_right_iff_imp],
  exact Œª h', le_trans (norm_nonneg (f a)) (h' a),
end

@[simp] lemma norm_eq_zero_of_empty [h : is_empty Œ±] : ‚à•f‚à• = 0 :=
begin
  have h' : ‚àÄ (C : ‚Ñù) (x : Œ±), ‚à•f x‚à• ‚â§ C, { intros, exfalso, apply h.false, use x, },
  simp only [norm_eq, h', and_true, implies_true_iff],
  exact cInf_Ici,
end

lemma norm_coe_le_norm (x : Œ±) : ‚à•f x‚à• ‚â§ ‚à•f‚à• := calc
  ‚à•f x‚à• = dist (f x) ((0 : Œ± ‚Üí·µá Œ≤) x) : by simp [dist_zero_right]
  ... ‚â§ ‚à•f‚à• : dist_coe_le_dist _

lemma dist_le_two_norm' {f : Œ≥ ‚Üí Œ≤} {C : ‚Ñù} (hC : ‚àÄ x, ‚à•f x‚à• ‚â§ C) (x y : Œ≥) :
  dist (f x) (f y) ‚â§ 2 * C :=
calc dist (f x) (f y) ‚â§ ‚à•f x‚à• + ‚à•f y‚à• : dist_le_norm_add_norm _ _
                  ... ‚â§ C + C         : add_le_add (hC x) (hC y)
                  ... = 2 * C         : (two_mul _).symm

/-- Distance between the images of any two points is at most twice the norm of the function. -/
lemma dist_le_two_norm (x y : Œ±) : dist (f x) (f y) ‚â§ 2 * ‚à•f‚à• :=
dist_le_two_norm' f.norm_coe_le_norm x y

variable {f}

/-- The norm of a function is controlled by the supremum of the pointwise norms -/
lemma norm_le (C0 : (0 : ‚Ñù) ‚â§ C) : ‚à•f‚à• ‚â§ C ‚Üî ‚àÄx:Œ±, ‚à•f x‚à• ‚â§ C :=
by simpa using @dist_le _ _ _ _ f 0 _ C0

lemma norm_le_of_nonempty [nonempty Œ±]
  {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} : ‚à•f‚à• ‚â§ M ‚Üî ‚àÄ x, ‚à•f x‚à• ‚â§ M :=
begin
  simp_rw [norm_def, ‚Üêdist_zero_right],
  exact dist_le_iff_of_nonempty,
end

lemma norm_lt_iff_of_compact [compact_space Œ±]
  {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} (M0 : 0 < M) : ‚à•f‚à• < M ‚Üî ‚àÄ x, ‚à•f x‚à• < M :=
begin
  simp_rw [norm_def, ‚Üêdist_zero_right],
  exact dist_lt_iff_of_compact M0,
end

lemma norm_lt_iff_of_nonempty_compact [nonempty Œ±] [compact_space Œ±]
  {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} : ‚à•f‚à• < M ‚Üî ‚àÄ x, ‚à•f x‚à• < M :=
begin
  simp_rw [norm_def, ‚Üêdist_zero_right],
  exact dist_lt_iff_of_nonempty_compact,
end

variable (f)

/-- Norm of `const Œ± b` is less than or equal to `‚à•b‚à•`. If `Œ±` is nonempty,
then it is equal to `‚à•b‚à•`. -/
lemma norm_const_le (b : Œ≤) : ‚à•const Œ± b‚à• ‚â§ ‚à•b‚à• :=
(norm_le (norm_nonneg b)).2 $ Œª x, le_refl _

@[simp] lemma norm_const_eq [h : nonempty Œ±] (b : Œ≤) : ‚à•const Œ± b‚à• = ‚à•b‚à• :=
le_antisymm (norm_const_le b) $ h.elim $ Œª x, (const Œ± b).norm_coe_le_norm x

/-- Constructing a bounded continuous function from a uniformly bounded continuous
function taking values in a normed group. -/
def of_normed_group {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±] [normed_group Œ≤]
  (f : Œ± ‚Üí Œ≤) (Hf : continuous f) (C : ‚Ñù) (H : ‚àÄx, ‚à•f x‚à• ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
‚ü®‚ü®Œªn, f n, Hf‚ü©, ‚ü®_, dist_le_two_norm' H‚ü©‚ü©

@[simp] lemma coe_of_normed_group
  {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±] [normed_group Œ≤]
  (f : Œ± ‚Üí Œ≤) (Hf : continuous f) (C : ‚Ñù) (H : ‚àÄx, ‚à•f x‚à• ‚â§ C) :
  (of_normed_group f Hf C H : Œ± ‚Üí Œ≤) = f := rfl

lemma norm_of_normed_group_le {f : Œ± ‚Üí Œ≤} (hfc : continuous f) {C : ‚Ñù} (hC : 0 ‚â§ C)
  (hfC : ‚àÄ x, ‚à•f x‚à• ‚â§ C) : ‚à•of_normed_group f hfc C hfC‚à• ‚â§ C :=
(norm_le hC).2 hfC

/-- Constructing a bounded continuous function from a uniformly bounded
function on a discrete space, taking values in a normed group -/
def of_normed_group_discrete {Œ± : Type u} {Œ≤ : Type v}
  [topological_space Œ±] [discrete_topology Œ±] [normed_group Œ≤]
  (f : Œ±  ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄx, norm (f x) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
of_normed_group f continuous_of_discrete_topology C H

@[simp] lemma coe_of_normed_group_discrete
  {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±] [discrete_topology Œ±] [normed_group Œ≤]
  (f : Œ± ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄx, ‚à•f x‚à• ‚â§ C) :
  (of_normed_group_discrete f C H : Œ± ‚Üí Œ≤) = f := rfl

/-- Taking the pointwise norm of a bounded continuous function with values in a `normed_group`,
yields a bounded continuous function with values in ‚Ñù. -/
def norm_comp : Œ± ‚Üí·µá ‚Ñù :=
of_normed_group (norm ‚àò f) (by continuity) ‚à•f‚à• (Œª x, by simp only [f.norm_coe_le_norm, norm_norm])

@[simp] lemma coe_norm_comp : (f.norm_comp : Œ± ‚Üí ‚Ñù) = norm ‚àò f := rfl

@[simp] lemma norm_norm_comp : ‚à•f.norm_comp‚à• = ‚à•f‚à• :=
by simp only [norm_eq, coe_norm_comp, norm_norm]

lemma bdd_above_range_norm_comp : bdd_above $ set.range $ norm ‚àò f :=
(real.bounded_iff_bdd_below_bdd_above.mp $ @bounded_range _ _ _ _ f.norm_comp).2

lemma norm_eq_supr_norm : ‚à•f‚à• = ‚®Ü x : Œ±, ‚à•f x‚à• :=
begin
  casesI is_empty_or_nonempty Œ± with hŒ± _,
  { suffices : range (norm ‚àò f) = ‚àÖ, { rw [f.norm_eq_zero_of_empty, supr, this, real.Sup_empty], },
    simp only [hŒ±, range_eq_empty, not_nonempty_iff], },
  { rw [norm_eq_of_nonempty, supr,
      ‚Üê cInf_upper_bounds_eq_cSup f.bdd_above_range_norm_comp (range_nonempty _)],
    congr,
    ext,
    simp only [forall_apply_eq_imp_iff', mem_range, exists_imp_distrib], },
end

/-- The pointwise opposite of a bounded continuous function is again bounded continuous. -/
instance : has_neg (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf, of_normed_group (-f) f.continuous.neg ‚à•f‚à• $ Œª x,
  trans_rel_right _ (norm_neg _) (f.norm_coe_le_norm x)‚ü©

/-- The pointwise difference of two bounded continuous functions is again bounded continuous. -/
instance : has_sub (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œªf g, of_normed_group (f - g) (f.continuous.sub g.continuous) (‚à•f‚à• + ‚à•g‚à•) $ Œª x,
  by { simp only [sub_eq_add_neg],
       exact le_trans (norm_add_le _ _) (add_le_add (f.norm_coe_le_norm x) $
         trans_rel_right _ (norm_neg _) (g.norm_coe_le_norm x)) }‚ü©

@[simp] lemma coe_neg : ‚áë(-f) = -f := rfl
lemma neg_apply : (-f) x = -f x := rfl

instance : add_comm_group (Œ± ‚Üí·µá Œ≤) :=
{ add_left_neg   := assume f, by ext; simp,
  add_comm       := assume f g, by ext; simp [add_comm],
  sub_eq_add_neg := assume f g, by { ext, apply sub_eq_add_neg },
  ..bounded_continuous_function.add_monoid,
  ..bounded_continuous_function.has_neg,
  ..bounded_continuous_function.has_sub }

@[simp] lemma coe_sub : ‚áë(f - g) = f - g := rfl
lemma sub_apply : (f - g) x = f x - g x := rfl

instance : normed_group (Œ± ‚Üí·µá Œ≤) :=
{ dist_eq := Œª f g, by simp only [norm_eq, dist_eq, dist_eq_norm, sub_apply] }

lemma abs_diff_coe_le_dist : ‚à•f x - g x‚à• ‚â§ dist f g :=
by { rw dist_eq_norm, exact (f - g).norm_coe_le_norm x }

lemma coe_le_coe_add_dist {f g : Œ± ‚Üí·µá ‚Ñù} : f x ‚â§ g x + dist f g :=
sub_le_iff_le_add'.1 $ (abs_le.1 $ @dist_coe_le_dist _ _ _ _ f g x).2

end normed_group

section has_bounded_smul
/-!
### `has_bounded_smul` (in particular, topological module) structure

In this section, if `Œ≤` is a metric space and a `ùïú`-module whose addition and scalar multiplication
are compatible with the metric structure, then we show that the space of bounded continuous
functions from `Œ±` to `Œ≤` inherits a so-called `has_bounded_smul` structure (in particular, a
`has_continuous_mul` structure, which is the mathlib formulation of being a topological module), by
using pointwise operations and checking that they are compatible with the uniform distance. -/

variables {ùïú : Type*} [metric_space ùïú] [semiring ùïú]
variables [topological_space Œ±] [metric_space Œ≤] [add_comm_monoid Œ≤]
  [module ùïú Œ≤] [has_bounded_smul ùïú Œ≤]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance : has_scalar ùïú (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œª c f,
  bounded_continuous_function.mk_of_bound
    (c ‚Ä¢ f.to_continuous_map)
    (dist c 0 * (classical.some f.bounded))
    begin
      intros x y,
      refine (dist_smul_pair c (f x) (f y)).trans _,
      refine mul_le_mul_of_nonneg_left _ dist_nonneg,
      exact classical.some_spec f.bounded x y
    end ‚ü©

@[simp] lemma coe_smul (c : ùïú) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(c ‚Ä¢ f) = Œª x, c ‚Ä¢ (f x) := rfl
lemma smul_apply (c : ùïú) (f : Œ± ‚Üí·µá Œ≤) (x : Œ±) : (c ‚Ä¢ f) x = c ‚Ä¢ f x := rfl

instance : has_bounded_smul ùïú (Œ± ‚Üí·µá Œ≤) :=
{ dist_smul_pair' := Œª c f‚ÇÅ f‚ÇÇ, begin
    rw dist_le (mul_nonneg dist_nonneg dist_nonneg),
    intros x,
    refine (dist_smul_pair c (f‚ÇÅ x) (f‚ÇÇ x)).trans _,
    exact mul_le_mul_of_nonneg_left (dist_coe_le_dist x) dist_nonneg
  end,
  dist_pair_smul' := Œª c‚ÇÅ c‚ÇÇ f, begin
    rw dist_le (mul_nonneg dist_nonneg dist_nonneg),
    intros x,
    refine (dist_pair_smul c‚ÇÅ c‚ÇÇ (f x)).trans _,
    convert mul_le_mul_of_nonneg_left (dist_coe_le_dist x) dist_nonneg,
    simp
  end }

variables [has_lipschitz_add Œ≤]

instance : module ùïú (Œ± ‚Üí·µá Œ≤) :=
{ smul     := (‚Ä¢),
  smul_add := Œª c f g, ext $ Œª x, smul_add c (f x) (g x),
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, ext $ Œª x, add_smul c‚ÇÅ c‚ÇÇ (f x),
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, ext $ Œª x, mul_smul c‚ÇÅ c‚ÇÇ (f x),
  one_smul := Œª f, ext $ Œª x, one_smul ùïú (f x),
  smul_zero := Œª c, ext $ Œª x, smul_zero c,
  zero_smul := Œª f, ext $ Œª x, zero_smul ùïú (f x),
  .. bounded_continuous_function.add_comm_monoid }

variables (ùïú)
/-- The evaluation at a point, as a continuous linear map from `Œ± ‚Üí·µá Œ≤` to `Œ≤`. -/
def eval_clm (x : Œ±) : (Œ± ‚Üí·µá Œ≤) ‚ÜíL[ùïú] Œ≤ :=
{ to_fun := Œª f, f x,
  map_add' := Œª f g, by simp only [pi.add_apply, coe_add],
  map_smul' := Œª c f, by simp only [coe_smul, ring_hom.id_apply] }

@[simp] lemma eval_clm_apply (x : Œ±) (f : Œ± ‚Üí·µá Œ≤) :
  eval_clm ùïú x f = f x := rfl

variables (Œ± Œ≤)

/-- The linear map forgetting that a bounded continuous function is bounded. -/
@[simps]
def forget_boundedness_linear_map : (Œ± ‚Üí·µá Œ≤) ‚Üí‚Çó[ùïú] C(Œ±, Œ≤) :=
{ to_fun := forget_boundedness Œ± Œ≤,
  map_smul' := by { intros, ext, simp, },
  map_add' := by { intros, ext, simp, }, }

end has_bounded_smul

section normed_space
/-!
### Normed space structure

In this section, if `Œ≤` is a normed space, then we show that the space of bounded
continuous functions from `Œ±` to `Œ≤` inherits a normed space structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables {ùïú : Type*}
variables [topological_space Œ±] [normed_group Œ≤]
variables {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance [normed_field ùïú] [normed_space ùïú Œ≤] : normed_space ùïú (Œ± ‚Üí·µá Œ≤) := ‚ü®Œª c f, begin
  refine norm_of_normed_group_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _,
  exact (Œª x, trans_rel_right _ (norm_smul _ _)
    (mul_le_mul_of_nonneg_left (f.norm_coe_le_norm _) (norm_nonneg _))) end‚ü©

variables [nondiscrete_normed_field ùïú] [normed_space ùïú Œ≤]
variables [normed_group Œ≥] [normed_space ùïú Œ≥]

variables (Œ±)
-- TODO does this work in the `has_bounded_smul` setting, too?
/--
Postcomposition of bounded continuous functions into a normed module by a continuous linear map is
a continuous linear map.
Upgraded version of `continuous_linear_map.comp_left_continuous`, similar to
`linear_map.comp_left`. -/
protected def _root_.continuous_linear_map.comp_left_continuous_bounded (g : Œ≤ ‚ÜíL[ùïú] Œ≥) :
  (Œ± ‚Üí·µá Œ≤) ‚ÜíL[ùïú] (Œ± ‚Üí·µá Œ≥) :=
linear_map.mk_continuous
  { to_fun := Œª f, of_normed_group
      (g ‚àò f)
      (g.continuous.comp f.continuous)
      (‚à•g‚à• * ‚à•f‚à•)
      (Œª x, (g.le_op_norm_of_le (f.norm_coe_le_norm x))),
    map_add' := Œª f g, by ext; simp,
    map_smul' := Œª c f, by ext; simp }
  ‚à•g‚à•
  (Œª f, norm_of_normed_group_le _ (mul_nonneg (norm_nonneg g) (norm_nonneg f)) _)

@[simp] lemma _root_.continuous_linear_map.comp_left_continuous_bounded_apply (g : Œ≤ ‚ÜíL[ùïú] Œ≥)
  (f : Œ± ‚Üí·µá Œ≤) (x : Œ±) :
  (g.comp_left_continuous_bounded Œ± f) x = g (f x) :=
rfl

end normed_space

section normed_ring
/-!
### Normed ring structure

In this section, if `R` is a normed ring, then we show that the space of bounded
continuous functions from `Œ±` to `R` inherits a normed ring structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Œ±] {R : Type*} [normed_ring R]

instance : ring (Œ± ‚Üí·µá R) :=
{ one := const Œ± 1,
  mul := Œª f g, of_normed_group (f * g) (f.continuous.mul g.continuous) (‚à•f‚à• * ‚à•g‚à•) $ Œª x,
    le_trans (normed_ring.norm_mul (f x) (g x)) $
      mul_le_mul (f.norm_coe_le_norm x) (g.norm_coe_le_norm x) (norm_nonneg _) (norm_nonneg _),
  one_mul := Œª f, ext $ Œª x, one_mul (f x),
  mul_one := Œª f, ext $ Œª x, mul_one (f x),
  mul_assoc := Œª f‚ÇÅ f‚ÇÇ f‚ÇÉ, ext $ Œª x, mul_assoc _ _ _,
  left_distrib := Œª f‚ÇÅ f‚ÇÇ f‚ÇÉ, ext $ Œª x, left_distrib _ _ _,
  right_distrib := Œª f‚ÇÅ f‚ÇÇ f‚ÇÉ, ext $ Œª x, right_distrib _ _ _,
  .. bounded_continuous_function.add_comm_group }

@[simp] lemma coe_mul (f g : Œ± ‚Üí·µá R) : ‚áë(f * g) = f * g := rfl
lemma mul_apply (f g : Œ± ‚Üí·µá R) (x : Œ±) : (f * g) x = f x * g x := rfl

instance : normed_ring (Œ± ‚Üí·µá R) :=
{ norm_mul := Œª f g, norm_of_normed_group_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _,
  .. bounded_continuous_function.normed_group }

end normed_ring

section normed_comm_ring
/-!
### Normed commutative ring structure

In this section, if `R` is a normed commutative ring, then we show that the space of bounded
continuous functions from `Œ±` to `R` inherits a normed commutative ring structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables [topological_space Œ±] {R : Type*} [normed_comm_ring R]

instance : comm_ring (Œ± ‚Üí·µá R) :=
{ mul_comm := Œª f‚ÇÅ f‚ÇÇ, ext $ Œª x, mul_comm _ _,
  .. bounded_continuous_function.ring }

instance : normed_comm_ring (Œ± ‚Üí·µá R) :=
{ .. bounded_continuous_function.comm_ring, .. bounded_continuous_function.normed_group }

end normed_comm_ring

section normed_algebra
/-!
### Normed algebra structure

In this section, if `Œ≥` is a normed algebra, then we show that the space of bounded
continuous functions from `Œ±` to `Œ≥` inherits a normed algebra structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/

variables {ùïú : Type*} [normed_field ùïú]
variables [topological_space Œ±] [normed_group Œ≤] [normed_space ùïú Œ≤]
variables [normed_ring Œ≥] [normed_algebra ùïú Œ≥]
variables {f g : Œ± ‚Üí·µá Œ≥} {x : Œ±} {c : ùïú}

/-- `bounded_continuous_function.const` as a `ring_hom`. -/
def C : ùïú ‚Üí+* (Œ± ‚Üí·µá Œ≥) :=
{ to_fun    := Œª (c : ùïú), const Œ± ((algebra_map ùïú Œ≥) c),
  map_one'  := ext $ Œª x, (algebra_map ùïú Œ≥).map_one,
  map_mul'  := Œª c‚ÇÅ c‚ÇÇ, ext $ Œª x, (algebra_map ùïú Œ≥).map_mul _ _,
  map_zero' := ext $ Œª x, (algebra_map ùïú Œ≥).map_zero,
  map_add'  := Œª c‚ÇÅ c‚ÇÇ, ext $ Œª x, (algebra_map ùïú Œ≥).map_add _ _ }

instance : algebra ùïú (Œ± ‚Üí·µá Œ≥) :=
{ to_ring_hom := C,
  commutes' := Œª c f, ext $ Œª x, algebra.commutes' _ _,
  smul_def' := Œª c f, ext $ Œª x, algebra.smul_def' _ _,
  ..bounded_continuous_function.module,
  ..bounded_continuous_function.ring }

@[simp] lemma algebra_map_apply (k : ùïú) (a : Œ±) :
  algebra_map ùïú (Œ± ‚Üí·µá Œ≥) k a = k ‚Ä¢ 1 :=
by { rw algebra.algebra_map_eq_smul_one, refl, }

instance [nonempty Œ±] : normed_algebra ùïú (Œ± ‚Üí·µá Œ≥) :=
{ norm_algebra_map_eq := Œª c, begin
    calc ‚à• (algebra_map ùïú (Œ± ‚Üí·µá Œ≥)).to_fun c‚à• = ‚à•(algebra_map ùïú Œ≥) c‚à• : _
    ... = ‚à•c‚à• : norm_algebra_map_eq _ _,
    apply norm_const_eq ((algebra_map ùïú Œ≥) c), assumption,
  end,
  ..bounded_continuous_function.algebra }

/-!
### Structure as normed module over scalar functions

If `Œ≤` is a normed `ùïú`-space, then we show that the space of bounded continuous
functions from `Œ±` to `Œ≤` is naturally a module over the algebra of bounded continuous
functions from `Œ±` to `ùïú`. -/

instance has_scalar' : has_scalar (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) :=
‚ü®Œª (f : Œ± ‚Üí·µá ùïú) (g : Œ± ‚Üí·µá Œ≤), of_normed_group (Œª x, (f x) ‚Ä¢ (g x))
(f.continuous.smul g.continuous) (‚à•f‚à• * ‚à•g‚à•) (Œª x, calc
  ‚à•f x ‚Ä¢ g x‚à• ‚â§ ‚à•f x‚à• * ‚à•g x‚à• : normed_space.norm_smul_le _ _
  ... ‚â§ ‚à•f‚à• * ‚à•g‚à• : mul_le_mul (f.norm_coe_le_norm _) (g.norm_coe_le_norm _) (norm_nonneg _)
    (norm_nonneg _)) ‚ü©

instance module' : module (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) :=
module.of_core $
{ smul     := (‚Ä¢),
  smul_add := Œª c f‚ÇÅ f‚ÇÇ, ext $ Œª x, smul_add _ _ _,
  add_smul := Œª c‚ÇÅ c‚ÇÇ f, ext $ Œª x, add_smul _ _ _,
  mul_smul := Œª c‚ÇÅ c‚ÇÇ f, ext $ Œª x, mul_smul _ _ _,
  one_smul := Œª f, ext $ Œª x, one_smul ùïú (f x) }

lemma norm_smul_le (f : Œ± ‚Üí·µá ùïú) (g : Œ± ‚Üí·µá Œ≤) : ‚à•f ‚Ä¢ g‚à• ‚â§ ‚à•f‚à• * ‚à•g‚à• :=
norm_of_normed_group_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _

/- TODO: When `normed_module` has been added to `normed_space.basic`, the above facts
show that the space of bounded continuous functions from `Œ±` to `Œ≤` is naturally a normed
module over the algebra of bounded continuous functions from `Œ±` to `ùïú`. -/

end normed_algebra

end bounded_continuous_function
