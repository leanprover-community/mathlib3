/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import topology.continuous_function.bounded
import analysis.normed_space.linear_isometry
import topology.uniform_space.compact_separated
import tactic.equiv_rw

/-!
# Continuous functions on a compact space

Continuous functions `CâŸ®Î±, Î²âŸ¯` from a compact space `Î±` to a metric space `Î²`
are automatically bounded, and so acquire various structures inherited from `Î± â†’áµ‡ Î²`.

This file transfers these structures, and restates some lemmas
characterising these structures.

If you need a lemma which is proved about `Î± â†’áµ‡ Î²` but not for `CâŸ®Î±, Î²âŸ¯` when `Î±` is compact,
you should restate it here. You can also use
`bounded_continuous_function.equiv_continuous_map_of_compact` to move functions back and forth.

-/

noncomputable theory
open_locale topological_space classical nnreal bounded_continuous_function

open set filter metric

open bounded_continuous_function

namespace continuous_map

variables (Î± : Type*) (Î² : Type*) [topological_space Î±] [compact_space Î±] [normed_group Î²]

/--
When `Î±` is compact, the bounded continuous maps `Î± â†’áµ‡ ğ•œ` are
equivalent to `CâŸ®Î±, ğ•œâŸ¯`.
-/
@[simps]
def equiv_bounded_of_compact : CâŸ®Î±, Î²âŸ¯ â‰ƒ (Î± â†’áµ‡ Î²) :=
âŸ¨mk_of_compact, forget_boundedness Î± Î², Î» f, by { ext, refl, }, Î» f, by { ext, refl, }âŸ©

/--
When `Î±` is compact, the bounded continuous maps `Î± â†’áµ‡ ğ•œ` are
additively equivalent to `CâŸ®Î±, ğ•œâŸ¯`.
-/
@[simps]
def add_equiv_bounded_of_compact : CâŸ®Î±, Î²âŸ¯ â‰ƒ+ (Î± â†’áµ‡ Î²) :=
({ ..forget_boundedness_add_hom Î± Î²,
  ..(equiv_bounded_of_compact Î± Î²).symm, } : (Î± â†’áµ‡ Î²) â‰ƒ+ CâŸ®Î±, Î²âŸ¯).symm

-- It would be nice if `@[simps]` produced this directly,
-- instead of the unhelpful `add_equiv_bounded_of_compact_apply_to_continuous_map`.
@[simp]
lemma add_equiv_bounded_of_compact_apply_apply (f : CâŸ®Î±, Î²âŸ¯) (a : Î±) :
  add_equiv_bounded_of_compact Î± Î² f a = f a :=
rfl

@[simp]
lemma add_equiv_bounded_of_compact_to_equiv :
  (add_equiv_bounded_of_compact Î± Î²).to_equiv = equiv_bounded_of_compact Î± Î² :=
rfl

instance : metric_space CâŸ®Î±,Î²âŸ¯ :=
metric_space.induced
  (equiv_bounded_of_compact Î± Î²)
  (equiv_bounded_of_compact Î± Î²).injective
  (by apply_instance)

section
variables {Î± Î²} (f g : CâŸ®Î±, Î²âŸ¯) {C : â„}

/-- The distance between two functions is controlled by the supremum of the pointwise distances -/
lemma dist_le (C0 : (0 : â„) â‰¤ C) : dist f g â‰¤ C â†” âˆ€x:Î±, dist (f x) (g x) â‰¤ C :=
@bounded_continuous_function.dist_le  _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) ((equiv_bounded_of_compact Î± Î²) g) _ C0

lemma dist_le_iff_of_nonempty [nonempty Î±] :
  dist f g â‰¤ C â†” âˆ€ x, dist (f x) (g x) â‰¤ C :=
@bounded_continuous_function.dist_le_iff_of_nonempty  _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) ((equiv_bounded_of_compact Î± Î²) g) _ _

lemma dist_lt_of_nonempty [nonempty Î±]
  (w : âˆ€x:Î±, dist (f x) (g x) < C) : dist f g < C :=
@bounded_continuous_function.dist_lt_of_nonempty_compact  _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) ((equiv_bounded_of_compact Î± Î²) g) _ _ _ w

lemma dist_lt_iff (C0 : (0 : â„) < C) :
  dist f g < C â†” âˆ€x:Î±, dist (f x) (g x) < C :=
@bounded_continuous_function.dist_lt_iff_of_compact  _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) ((equiv_bounded_of_compact Î± Î²) g) _ _ C0

lemma dist_lt_iff_of_nonempty [nonempty Î±] :
  dist f g < C â†” âˆ€x:Î±, dist (f x) (g x) < C :=
@bounded_continuous_function.dist_lt_iff_of_nonempty_compact  _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) ((equiv_bounded_of_compact Î± Î²) g) _ _ _

end

variables (Î± Î²)

/--
When `Î±` is compact, and `Î²` is a metric space, the bounded continuous maps `Î± â†’áµ‡ Î²` are
isometric to `CâŸ®Î±, Î²âŸ¯`.
-/
@[simps]
def isometric_bounded_of_compact :
  CâŸ®Î±, Î²âŸ¯ â‰ƒáµ¢ (Î± â†’áµ‡ Î²) :=
{ isometry_to_fun := Î» x y, rfl,
  to_equiv := equiv_bounded_of_compact Î± Î² }

-- TODO at some point we will need lemmas characterising this norm!
-- At the moment the only way to reason about it is to transfer `f : CâŸ®Î±,Î²âŸ¯` back to `Î± â†’áµ‡ Î²`.
instance : has_norm CâŸ®Î±,Î²âŸ¯ :=
{ norm := Î» x, dist x 0 }

instance : normed_group CâŸ®Î±,Î²âŸ¯ :=
{ dist_eq := Î» x y,
  begin
    change dist x y = dist (x-y) 0,
     -- it would be nice if `equiv_rw` could rewrite in multiple places at once
    equiv_rw (equiv_bounded_of_compact Î± Î²) at x,
    equiv_rw (equiv_bounded_of_compact Î± Î²) at y,
    have p : dist x y = dist (x-y) 0, { rw dist_eq_norm, rw dist_zero_right, },
    convert p,
    exact ((add_equiv_bounded_of_compact Î± Î²).symm.map_sub _ _).symm,
  end, }

section
variables {Î± Î²} (f : CâŸ®Î±, Î²âŸ¯)
-- The corresponding lemmas for `bounded_continuous_function` are stated with `{f}`,
-- and so can not be used in dot notation.

lemma norm_coe_le_norm (x : Î±) : âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
((equiv_bounded_of_compact Î± Î²) f).norm_coe_le_norm x

/-- Distance between the images of any two points is at most twice the norm of the function. -/
lemma dist_le_two_norm (x y : Î±) : dist (f x) (f y) â‰¤ 2 * âˆ¥fâˆ¥ :=
((equiv_bounded_of_compact Î± Î²) f).dist_le_two_norm x y

/-- The norm of a function is controlled by the supremum of the pointwise norms -/
lemma norm_le {C : â„} (C0 : (0 : â„) â‰¤ C) : âˆ¥fâˆ¥ â‰¤ C â†” âˆ€x:Î±, âˆ¥f xâˆ¥ â‰¤ C :=
@bounded_continuous_function.norm_le _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) _ C0

lemma norm_le_of_nonempty [nonempty Î±] {M : â„} : âˆ¥fâˆ¥ â‰¤ M â†” âˆ€ x, âˆ¥f xâˆ¥ â‰¤ M :=
@bounded_continuous_function.norm_le_of_nonempty _ _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) _

lemma norm_lt_iff {M : â„} (M0 : 0 < M) : âˆ¥fâˆ¥ < M â†” âˆ€ x, âˆ¥f xâˆ¥ < M :=
@bounded_continuous_function.norm_lt_iff_of_compact _ _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) _ M0

lemma norm_lt_iff_of_nonempty [nonempty Î±] {M : â„} :
  âˆ¥fâˆ¥ < M â†” âˆ€ x, âˆ¥f xâˆ¥ < M :=
@bounded_continuous_function.norm_lt_iff_of_nonempty_compact _ _ _ _ _ _
  ((equiv_bounded_of_compact Î± Î²) f) _

lemma apply_le_norm (f : CâŸ®Î±, â„âŸ¯) (x : Î±) : f x â‰¤ âˆ¥fâˆ¥ :=
le_trans (le_abs.mpr (or.inl (le_refl (f x)))) (f.norm_coe_le_norm x)

lemma neg_norm_le_apply (f : CâŸ®Î±, â„âŸ¯) (x : Î±) : -âˆ¥fâˆ¥ â‰¤ f x :=
le_trans (neg_le_neg (f.norm_coe_le_norm x)) (neg_le.mp (neg_le_abs_self (f x)))

end

section
variables {R : Type*} [normed_ring R]

instance : normed_ring CâŸ®Î±,RâŸ¯ :=
{ norm_mul := Î» f g,
  begin
    equiv_rw (equiv_bounded_of_compact Î± R) at f,
    equiv_rw (equiv_bounded_of_compact Î± R) at g,
    exact norm_mul_le f g,
  end,
  ..(infer_instance : normed_group CâŸ®Î±,RâŸ¯) }

end

section
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

instance : normed_space ğ•œ CâŸ®Î±,Î²âŸ¯ :=
{ norm_smul_le := Î» c f,
  begin
    equiv_rw (equiv_bounded_of_compact Î± Î²) at f,
    exact le_of_eq (norm_smul c f),
  end }

variables (Î± ğ•œ)

/--
When `Î±` is compact and `ğ•œ` is a normed field,
the `ğ•œ`-algebra of bounded continuous maps `Î± â†’áµ‡ Î²` is
`ğ•œ`-linearly isometric to `CâŸ®Î±, Î²âŸ¯`.
-/
def linear_isometry_bounded_of_compact :
  CâŸ®Î±, Î²âŸ¯ â‰ƒâ‚—áµ¢[ğ•œ] (Î± â†’áµ‡ Î²) :=
{ map_smul' := Î» c f, by { ext, simp, },
  norm_map' := Î» f, rfl,
  ..add_equiv_bounded_of_compact Î± Î² }

@[simp]
lemma linear_isometry_bounded_of_compact_to_isometric :
  (linear_isometry_bounded_of_compact Î± Î² ğ•œ).to_isometric =
    isometric_bounded_of_compact Î± Î² :=
rfl

@[simp]
lemma linear_isometry_bounded_of_compact_to_add_equiv :
  (linear_isometry_bounded_of_compact Î± Î² ğ•œ).to_linear_equiv.to_add_equiv =
    add_equiv_bounded_of_compact Î± Î² :=
rfl

@[simp]
lemma linear_isometry_bounded_of_compact_of_compact_to_equiv :
  (linear_isometry_bounded_of_compact Î± Î² ğ•œ).to_linear_equiv.to_equiv =
    equiv_bounded_of_compact Î± Î² :=
rfl

end

section
variables {ğ•œ : Type*} {Î³ : Type*} [normed_field ğ•œ] [normed_ring Î³] [normed_algebra ğ•œ Î³]

instance [nonempty Î±] : normed_algebra ğ•œ CâŸ®Î±, Î³âŸ¯ :=
{ norm_algebra_map_eq := Î» c, (norm_algebra_map_eq (Î± â†’áµ‡ Î³) c : _), }

end

end continuous_map

namespace continuous_map

section uniform_continuity
variables {Î± Î² : Type*}
variables [metric_space Î±] [compact_space Î±] [metric_space Î²]

/-!
We now set up some declarations making it convenient to use uniform continuity.
-/

lemma uniform_continuity
  (f : CâŸ®Î±, Î²âŸ¯) (Îµ : â„) (h : 0 < Îµ) :
  âˆƒ Î´ > 0, âˆ€ {x y}, dist x y < Î´ â†’ dist (f x) (f y) < Îµ :=
metric.uniform_continuous_iff.mp
  (compact_space.uniform_continuous_of_continuous f.continuous) Îµ h

/--
An arbitrarily chosen modulus of uniform continuity for a given function `f` and `Îµ > 0`.
-/
-- This definition allows us to separate the choice of some `Î´`,
-- and the corresponding use of `dist a b < Î´ â†’ dist (f a) (f b) < Îµ`,
-- even across different declarations.
def modulus (f : CâŸ®Î±, Î²âŸ¯) (Îµ : â„) (h : 0 < Îµ) : â„ :=
classical.some (uniform_continuity f Îµ h)

lemma modulus_pos (f : CâŸ®Î±, Î²âŸ¯) {Îµ : â„} {h : 0 < Îµ} : 0 < f.modulus Îµ h :=
classical.some (classical.some_spec (uniform_continuity f Îµ h))

lemma dist_lt_of_dist_lt_modulus
  (f : CâŸ®Î±, Î²âŸ¯) (Îµ : â„) (h : 0 < Îµ) {a b : Î±} (w : dist a b < f.modulus Îµ h) :
  dist (f a) (f b) < Îµ :=
classical.some_spec (classical.some_spec (uniform_continuity f Îµ h)) w

end uniform_continuity

/-!
We now setup variations on `comp_right_* f`, where `f : CâŸ®X, YâŸ¯`
(that is, precomposition by a continuous map),
as a morphism `CâŸ®Y, TâŸ¯ â†’ CâŸ®X, TâŸ¯`, respecting various types of structure.

In particular:
* `comp_right_continuous_map`, the bundled continuous map (for this we need `X Y` compact).
* `comp_right_homeomorph`, when we precompose by a homeomorphism.
* `comp_right_alg_hom`, when `T = R` is a topological ring.
-/
section comp_right

/--
Precomposition by a continuous map is itself a continuous map between spaces of continuous maps.
-/
def comp_right_continuous_map {X Y : Type*} (T : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y] [normed_group T]
  (f : CâŸ®X, YâŸ¯) : CâŸ®CâŸ®Y, TâŸ¯, CâŸ®X, TâŸ¯âŸ¯ :=
{ to_fun := Î» g, g.comp f,
  continuous_to_fun :=
  begin
    refine metric.continuous_iff.mpr _,
    intros g Îµ Îµ_pos,
    refine âŸ¨Îµ, Îµ_pos, Î» g' h, _âŸ©,
    rw continuous_map.dist_lt_iff _ _ Îµ_pos at h âŠ¢,
    { exact Î» x, h (f x), },
  end }

@[simp] lemma comp_right_continuous_map_apply {X Y : Type*} (T : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y] [normed_group T]
  (f : CâŸ®X, YâŸ¯) (g : CâŸ®Y, TâŸ¯) :
  (comp_right_continuous_map T f) g = g.comp f :=
rfl

/--
Precomposition by a homeomorphism is itself a homeomorphism between spaces of continuous maps.
-/
def comp_right_homeomorph {X Y : Type*} (T : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y] [normed_group T]
  (f : X â‰ƒâ‚œ Y) : CâŸ®Y, TâŸ¯ â‰ƒâ‚œ CâŸ®X, TâŸ¯ :=
{ to_fun := comp_right_continuous_map T f.to_continuous_map,
  inv_fun := comp_right_continuous_map T f.symm.to_continuous_map,
  left_inv := by tidy,
  right_inv := by tidy, }

/--
Precomposition of functions into a normed ring by continuous map is an algebra homomorphism.
-/
def comp_right_alg_hom {X Y : Type*} (R : Type*)
  [topological_space X] [topological_space Y] [normed_comm_ring R] (f : CâŸ®X, YâŸ¯) :
  CâŸ®Y, RâŸ¯ â†’â‚[R] CâŸ®X, RâŸ¯ :=
{ to_fun := Î» g, g.comp f,
  map_zero' := by { ext, simp, },
  map_add' := Î» gâ‚ gâ‚‚, by { ext, simp, },
  map_one' := by { ext, simp, },
  map_mul' := Î» gâ‚ gâ‚‚, by { ext, simp, },
  commutes' := Î» r, by { ext, simp, }, }

@[simp] lemma comp_right_alg_hom_apply {X Y : Type*} (R : Type*)
  [topological_space X] [topological_space Y] [normed_comm_ring R] (f : CâŸ®X, YâŸ¯) (g : CâŸ®Y, RâŸ¯) :
  (comp_right_alg_hom R f) g = g.comp f :=
rfl

lemma comp_right_alg_hom_continuous {X Y : Type*} (R : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y]
  [normed_comm_ring R] (f : CâŸ®X, YâŸ¯) :
  continuous (comp_right_alg_hom R f) :=
begin
  change continuous (comp_right_continuous_map R f),
  continuity,
end

end comp_right

end continuous_map
