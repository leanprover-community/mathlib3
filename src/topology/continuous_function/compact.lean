/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import topology.continuous_function.bounded
import topology.uniform_space.compact
import topology.compact_open
import topology.sets.compacts

/-!
# Continuous functions on a compact space

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Continuous functions `C(Œ±, Œ≤)` from a compact space `Œ±` to a metric space `Œ≤`
are automatically bounded, and so acquire various structures inherited from `Œ± ‚Üí·µá Œ≤`.

This file transfers these structures, and restates some lemmas
characterising these structures.

If you need a lemma which is proved about `Œ± ‚Üí·µá Œ≤` but not for `C(Œ±, Œ≤)` when `Œ±` is compact,
you should restate it here. You can also use
`bounded_continuous_function.equiv_continuous_map_of_compact` to move functions back and forth.

-/

noncomputable theory
open_locale topology classical nnreal bounded_continuous_function big_operators

open set filter metric

open bounded_continuous_function

namespace continuous_map

variables {Œ± Œ≤ E : Type*} [topological_space Œ±] [compact_space Œ±] [metric_space Œ≤]
  [normed_add_comm_group E]

section

variables (Œ± Œ≤)

/--
When `Œ±` is compact, the bounded continuous maps `Œ± ‚Üí·µá Œ≤` are
equivalent to `C(Œ±, Œ≤)`.
-/
@[simps { fully_applied := ff }]
def equiv_bounded_of_compact : C(Œ±, Œ≤) ‚âÉ (Œ± ‚Üí·µá Œ≤) :=
‚ü®mk_of_compact, bounded_continuous_function.to_continuous_map,
 Œª f, by { ext, refl, }, Œª f, by { ext, refl, }‚ü©

lemma uniform_inducing_equiv_bounded_of_compact :
  uniform_inducing (equiv_bounded_of_compact Œ± Œ≤) :=
uniform_inducing.mk'
begin
  simp only [has_basis_compact_convergence_uniformity.mem_iff, uniformity_basis_dist_le.mem_iff],
  exact Œª s, ‚ü®Œª ‚ü®‚ü®a, b‚ü©, ‚ü®ha, ‚ü®Œµ, hŒµ, hb‚ü©‚ü©, hs‚ü©, ‚ü®{p | ‚àÄ x, (p.1 x, p.2 x) ‚àà b},
    ‚ü®Œµ, hŒµ, Œª _ h x, hb (by exact (dist_le hŒµ.le).mp h x)‚ü©, Œª f g h, hs (by exact Œª x hx, h x)‚ü©,
    Œª ‚ü®t, ‚ü®Œµ, hŒµ, ht‚ü©, hs‚ü©, ‚ü®‚ü®set.univ, {p | dist p.1 p.2 ‚â§ Œµ}‚ü©,
      ‚ü®is_compact_univ, ‚ü®Œµ, hŒµ, Œª _ h, h‚ü©‚ü©,
      Œª ‚ü®f, g‚ü© h, hs _ _ (ht (by exact (dist_le hŒµ.le).mpr (Œª x, h x (mem_univ x))))‚ü©‚ü©,
end

lemma uniform_embedding_equiv_bounded_of_compact :
  uniform_embedding (equiv_bounded_of_compact Œ± Œ≤) :=
{ inj := (equiv_bounded_of_compact Œ± Œ≤).injective,
  .. uniform_inducing_equiv_bounded_of_compact Œ± Œ≤ }

/--
When `Œ±` is compact, the bounded continuous maps `Œ± ‚Üí·µá ùïú` are
additively equivalent to `C(Œ±, ùïú)`.
-/
@[simps apply symm_apply { fully_applied := ff }]
def add_equiv_bounded_of_compact [add_monoid Œ≤] [has_lipschitz_add Œ≤] :
  C(Œ±, Œ≤) ‚âÉ+ (Œ± ‚Üí·µá Œ≤) :=
({ .. to_continuous_map_add_hom Œ± Œ≤,
   .. (equiv_bounded_of_compact Œ± Œ≤).symm, } : (Œ± ‚Üí·µá Œ≤) ‚âÉ+ C(Œ±, Œ≤)).symm

instance : metric_space C(Œ±, Œ≤) :=
(uniform_embedding_equiv_bounded_of_compact Œ± Œ≤).comap_metric_space _

/--
When `Œ±` is compact, and `Œ≤` is a metric space, the bounded continuous maps `Œ± ‚Üí·µá Œ≤` are
isometric to `C(Œ±, Œ≤)`.
-/
@[simps to_equiv apply symm_apply { fully_applied := ff }]
def isometry_equiv_bounded_of_compact :
  C(Œ±, Œ≤) ‚âÉ·µ¢ (Œ± ‚Üí·µá Œ≤) :=
{ isometry_to_fun := Œª x y, rfl,
  to_equiv := equiv_bounded_of_compact Œ± Œ≤ }

end

@[simp] lemma _root_.bounded_continuous_function.dist_mk_of_compact (f g : C(Œ±, Œ≤)) :
  dist (mk_of_compact f) (mk_of_compact g) = dist f g := rfl

@[simp] lemma _root_.bounded_continuous_function.dist_to_continuous_map (f g : Œ± ‚Üí·µá Œ≤) :
  dist (f.to_continuous_map) (g.to_continuous_map) = dist f g := rfl

open bounded_continuous_function

section
variables {Œ± Œ≤} {f g : C(Œ±, Œ≤)} {C : ‚Ñù}

/-- The pointwise distance is controlled by the distance between functions, by definition. -/
lemma dist_apply_le_dist (x : Œ±) : dist (f x) (g x) ‚â§ dist f g :=
by simp only [‚Üê dist_mk_of_compact, dist_coe_le_dist, ‚Üê mk_of_compact_apply]

/-- The distance between two functions is controlled by the supremum of the pointwise distances -/
lemma dist_le (C0 : (0 : ‚Ñù) ‚â§ C) : dist f g ‚â§ C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) ‚â§ C :=
by simp only [‚Üê dist_mk_of_compact, dist_le C0, mk_of_compact_apply]

lemma dist_le_iff_of_nonempty [nonempty Œ±] :
  dist f g ‚â§ C ‚Üî ‚àÄ x, dist (f x) (g x) ‚â§ C :=
by simp only [‚Üê dist_mk_of_compact, dist_le_iff_of_nonempty, mk_of_compact_apply]

lemma dist_lt_iff_of_nonempty [nonempty Œ±] :
  dist f g < C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) < C :=
by simp only [‚Üê dist_mk_of_compact, dist_lt_iff_of_nonempty_compact, mk_of_compact_apply]

lemma dist_lt_of_nonempty [nonempty Œ±] (w : ‚àÄx:Œ±, dist (f x) (g x) < C) : dist f g < C :=
(dist_lt_iff_of_nonempty).2 w

lemma dist_lt_iff (C0 : (0 : ‚Ñù) < C) :
  dist f g < C ‚Üî ‚àÄx:Œ±, dist (f x) (g x) < C :=
by simp only [‚Üê dist_mk_of_compact, dist_lt_iff_of_compact C0, mk_of_compact_apply]

end

instance [complete_space Œ≤] : complete_space (C(Œ±, Œ≤)) :=
(isometry_equiv_bounded_of_compact Œ± Œ≤).complete_space

/-- See also `continuous_map.continuous_eval'` -/
@[continuity] lemma continuous_eval : continuous (Œª p : C(Œ±, Œ≤) √ó Œ±, p.1 p.2) :=
continuous_eval.comp ((isometry_equiv_bounded_of_compact Œ± Œ≤).continuous.prod_map continuous_id)

/-- See also `continuous_map.continuous_eval_const` -/
@[continuity] lemma continuous_eval_const (x : Œ±) : continuous (Œª f : C(Œ±, Œ≤), f x) :=
continuous_eval.comp (continuous_id.prod_mk continuous_const)

/-- See also `continuous_map.continuous_coe'` -/
lemma continuous_coe : @continuous (C(Œ±, Œ≤)) (Œ± ‚Üí Œ≤) _ _ coe_fn :=
continuous_pi continuous_eval_const

-- TODO at some point we will need lemmas characterising this norm!
-- At the moment the only way to reason about it is to transfer `f : C(Œ±,E)` back to `Œ± ‚Üí·µá E`.
instance : has_norm C(Œ±, E) :=
{ norm := Œª x, dist x 0 }

@[simp] lemma _root_.bounded_continuous_function.norm_mk_of_compact (f : C(Œ±, E)) :
  ‚Äñmk_of_compact f‚Äñ = ‚Äñf‚Äñ := rfl

@[simp] lemma _root_.bounded_continuous_function.norm_to_continuous_map_eq (f : Œ± ‚Üí·µá E) :
  ‚Äñf.to_continuous_map‚Äñ = ‚Äñf‚Äñ :=
rfl

open bounded_continuous_function

instance : normed_add_comm_group C(Œ±, E) :=
{ dist_eq := Œª x y, by
    rw [‚Üê norm_mk_of_compact, ‚Üê dist_mk_of_compact, dist_eq_norm, mk_of_compact_sub],
  dist := dist, norm := norm, .. continuous_map.metric_space _ _, .. continuous_map.add_comm_group }

instance [nonempty Œ±] [has_one E] [norm_one_class E] : norm_one_class C(Œ±, E) :=
{ norm_one := by simp only [‚Üênorm_mk_of_compact, mk_of_compact_one, norm_one] }

section
variables (f : C(Œ±, E))
-- The corresponding lemmas for `bounded_continuous_function` are stated with `{f}`,
-- and so can not be used in dot notation.

lemma norm_coe_le_norm (x : Œ±) : ‚Äñf x‚Äñ ‚â§ ‚Äñf‚Äñ :=
(mk_of_compact f).norm_coe_le_norm x

/-- Distance between the images of any two points is at most twice the norm of the function. -/
lemma dist_le_two_norm (x y : Œ±) : dist (f x) (f y) ‚â§ 2 * ‚Äñf‚Äñ :=
(mk_of_compact f).dist_le_two_norm x y

/-- The norm of a function is controlled by the supremum of the pointwise norms -/
lemma norm_le {C : ‚Ñù} (C0 : (0 : ‚Ñù) ‚â§ C) : ‚Äñf‚Äñ ‚â§ C ‚Üî ‚àÄx:Œ±, ‚Äñf x‚Äñ ‚â§ C :=
@bounded_continuous_function.norm_le _ _ _ _
  (mk_of_compact f) _ C0

lemma norm_le_of_nonempty [nonempty Œ±] {M : ‚Ñù} : ‚Äñf‚Äñ ‚â§ M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ ‚â§ M :=
@bounded_continuous_function.norm_le_of_nonempty _ _ _ _ _ (mk_of_compact f) _

lemma norm_lt_iff {M : ‚Ñù} (M0 : 0 < M) : ‚Äñf‚Äñ < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ < M :=
@bounded_continuous_function.norm_lt_iff_of_compact _ _ _ _ _ (mk_of_compact f) _ M0

theorem nnnorm_lt_iff {M : ‚Ñù‚â•0} (M0 : 0 < M) : ‚Äñf‚Äñ‚Çä < M ‚Üî ‚àÄ (x : Œ±), ‚Äñf x‚Äñ‚Çä < M :=
f.norm_lt_iff M0

lemma norm_lt_iff_of_nonempty [nonempty Œ±] {M : ‚Ñù} :
  ‚Äñf‚Äñ < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ < M :=
@bounded_continuous_function.norm_lt_iff_of_nonempty_compact _ _ _ _ _ _ (mk_of_compact f) _

lemma nnnorm_lt_iff_of_nonempty [nonempty Œ±] {M : ‚Ñù‚â•0} :
  ‚Äñf‚Äñ‚Çä < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ‚Çä < M :=
f.norm_lt_iff_of_nonempty

lemma apply_le_norm (f : C(Œ±, ‚Ñù)) (x : Œ±) : f x ‚â§ ‚Äñf‚Äñ :=
le_trans (le_abs.mpr (or.inl (le_refl (f x)))) (f.norm_coe_le_norm x)

lemma neg_norm_le_apply (f : C(Œ±, ‚Ñù)) (x : Œ±) : -‚Äñf‚Äñ ‚â§ f x :=
le_trans (neg_le_neg (f.norm_coe_le_norm x)) (neg_le.mp (neg_le_abs_self (f x)))

lemma norm_eq_supr_norm : ‚Äñf‚Äñ = ‚®Ü x : Œ±, ‚Äñf x‚Äñ :=
(mk_of_compact f).norm_eq_supr_norm

lemma norm_restrict_mono_set {X : Type*} [topological_space X]
  (f : C(X, E)) {K L : topological_space.compacts X} (hKL : K ‚â§ L) :
  ‚Äñf.restrict K‚Äñ ‚â§ ‚Äñf.restrict L‚Äñ :=
(norm_le _ (norm_nonneg _)).mpr (Œª x, norm_coe_le_norm (f.restrict L) $ set.inclusion hKL x)

end

section
variables {R : Type*} [normed_ring R]

instance : normed_ring C(Œ±,R) :=
{ norm_mul := Œª f g, norm_mul_le (mk_of_compact f) (mk_of_compact g),
  ..(infer_instance : normed_add_comm_group C(Œ±,R)),
  .. continuous_map.ring }

end

section
variables {ùïú : Type*} [normed_field ùïú] [normed_space ùïú E]

instance : normed_space ùïú C(Œ±,E) :=
{ norm_smul_le := Œª c f, (norm_smul_le c (mk_of_compact f) : _) }

section
variables (Œ± ùïú E)

/--
When `Œ±` is compact and `ùïú` is a normed field,
the `ùïú`-algebra of bounded continuous maps `Œ± ‚Üí·µá Œ≤` is
`ùïú`-linearly isometric to `C(Œ±, Œ≤)`.
-/
def linear_isometry_bounded_of_compact :
  C(Œ±, E) ‚âÉ‚Çó·µ¢[ùïú] (Œ± ‚Üí·µá E) :=
{ map_smul' := Œª c f, by { ext, simp, },
  norm_map' := Œª f, rfl,
  .. add_equiv_bounded_of_compact Œ± E }

variables {Œ± E} -- to match bounded_continuous_function.eval_clm

/-- The evaluation at a point, as a continuous linear map from `C(Œ±, ùïú)` to `ùïú`. -/
def eval_clm (x : Œ±) : C(Œ±, E) ‚ÜíL[ùïú] E :=
  (eval_clm ùïú x).comp
  ((linear_isometry_bounded_of_compact Œ± E ùïú).to_linear_isometry).to_continuous_linear_map

end

-- this lemma and the next are the analogues of those autogenerated by `@[simps]` for
-- `equiv_bounded_of_compact`, `add_equiv_bounded_of_compact`
@[simp] lemma linear_isometry_bounded_of_compact_symm_apply (f : Œ± ‚Üí·µá E) :
  (linear_isometry_bounded_of_compact Œ± E ùïú).symm f = f.to_continuous_map :=
rfl

@[simp] lemma linear_isometry_bounded_of_compact_apply_apply (f : C(Œ±, E)) (a : Œ±) :
  (linear_isometry_bounded_of_compact Œ± E ùïú f) a = f a :=
rfl


@[simp]
lemma linear_isometry_bounded_of_compact_to_isometry_equiv :
  (linear_isometry_bounded_of_compact Œ± E ùïú).to_isometry_equiv =
    (isometry_equiv_bounded_of_compact Œ± E) :=
rfl

@[simp]
lemma linear_isometry_bounded_of_compact_to_add_equiv :
  (linear_isometry_bounded_of_compact Œ± E ùïú).to_linear_equiv.to_add_equiv =
    (add_equiv_bounded_of_compact Œ± E) :=
rfl

@[simp]
lemma linear_isometry_bounded_of_compact_of_compact_to_equiv :
  (linear_isometry_bounded_of_compact Œ± E ùïú).to_linear_equiv.to_equiv =
    (equiv_bounded_of_compact Œ± E) :=
rfl

end

section
variables {ùïú : Type*} {Œ≥ : Type*} [normed_field ùïú] [normed_ring Œ≥] [normed_algebra ùïú Œ≥]

instance : normed_algebra ùïú C(Œ±, Œ≥) :=
{ ..continuous_map.normed_space }

end

end continuous_map

namespace continuous_map

section uniform_continuity
variables {Œ± Œ≤ : Type*}
variables [metric_space Œ±] [compact_space Œ±] [metric_space Œ≤]

/-!
We now set up some declarations making it convenient to use uniform continuity.
-/

lemma uniform_continuity
  (f : C(Œ±, Œ≤)) (Œµ : ‚Ñù) (h : 0 < Œµ) :
  ‚àÉ Œ¥ > 0, ‚àÄ {x y}, dist x y < Œ¥ ‚Üí dist (f x) (f y) < Œµ :=
metric.uniform_continuous_iff.mp
  (compact_space.uniform_continuous_of_continuous f.continuous) Œµ h

/--
An arbitrarily chosen modulus of uniform continuity for a given function `f` and `Œµ > 0`.
-/
-- This definition allows us to separate the choice of some `Œ¥`,
-- and the corresponding use of `dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ`,
-- even across different declarations.
def modulus (f : C(Œ±, Œ≤)) (Œµ : ‚Ñù) (h : 0 < Œµ) : ‚Ñù :=
classical.some (uniform_continuity f Œµ h)

lemma modulus_pos (f : C(Œ±, Œ≤)) {Œµ : ‚Ñù} {h : 0 < Œµ} : 0 < f.modulus Œµ h :=
(classical.some_spec (uniform_continuity f Œµ h)).fst

lemma dist_lt_of_dist_lt_modulus
  (f : C(Œ±, Œ≤)) (Œµ : ‚Ñù) (h : 0 < Œµ) {a b : Œ±} (w : dist a b < f.modulus Œµ h) :
  dist (f a) (f b) < Œµ :=
(classical.some_spec (uniform_continuity f Œµ h)).snd w

end uniform_continuity

end continuous_map

section comp_left
variables (X : Type*) {ùïú Œ≤ Œ≥ : Type*} [topological_space X] [compact_space X]
  [nontrivially_normed_field ùïú]
variables [normed_add_comm_group Œ≤] [normed_space ùïú Œ≤] [normed_add_comm_group Œ≥] [normed_space ùïú Œ≥]

open continuous_map

/--
Postcomposition of continuous functions into a normed module by a continuous linear map is a
continuous linear map.
Transferred version of `continuous_linear_map.comp_left_continuous_bounded`,
upgraded version of `continuous_linear_map.comp_left_continuous`,
similar to `linear_map.comp_left`. -/
protected def continuous_linear_map.comp_left_continuous_compact (g : Œ≤ ‚ÜíL[ùïú] Œ≥) :
  C(X, Œ≤) ‚ÜíL[ùïú] C(X, Œ≥) :=
(linear_isometry_bounded_of_compact X Œ≥ ùïú).symm.to_linear_isometry.to_continuous_linear_map.comp $
(g.comp_left_continuous_bounded X).comp $
(linear_isometry_bounded_of_compact X Œ≤ ùïú).to_linear_isometry.to_continuous_linear_map

@[simp] lemma continuous_linear_map.to_linear_comp_left_continuous_compact (g : Œ≤ ‚ÜíL[ùïú] Œ≥) :
  (g.comp_left_continuous_compact X : C(X, Œ≤) ‚Üí‚Çó[ùïú] C(X, Œ≥)) = g.comp_left_continuous ùïú X :=
by { ext f, refl }

@[simp] lemma continuous_linear_map.comp_left_continuous_compact_apply (g : Œ≤ ‚ÜíL[ùïú] Œ≥)
  (f : C(X, Œ≤)) (x : X) :
  g.comp_left_continuous_compact X f x = g (f x) :=
rfl

end comp_left

namespace continuous_map
/-!
We now setup variations on `comp_right_* f`, where `f : C(X, Y)`
(that is, precomposition by a continuous map),
as a morphism `C(Y, T) ‚Üí C(X, T)`, respecting various types of structure.

In particular:
* `comp_right_continuous_map`, the bundled continuous map (for this we need `X Y` compact).
* `comp_right_homeomorph`, when we precompose by a homeomorphism.
* `comp_right_alg_hom`, when `T = R` is a topological ring.
-/
section comp_right

/--
Precomposition by a continuous map is itself a continuous map between spaces of continuous maps.
-/
def comp_right_continuous_map {X Y : Type*} (T : Type*) [topological_space X] [compact_space X]
  [topological_space Y] [compact_space Y] [metric_space T]
  (f : C(X, Y)) : C(C(Y, T), C(X, T)) :=
{ to_fun := Œª g, g.comp f,
  continuous_to_fun :=
  begin
    refine metric.continuous_iff.mpr _,
    intros g Œµ Œµ_pos,
    refine ‚ü®Œµ, Œµ_pos, Œª g' h, _‚ü©,
    rw continuous_map.dist_lt_iff Œµ_pos at h ‚ä¢,
    { exact Œª x, h (f x), },
  end }

@[simp] lemma comp_right_continuous_map_apply {X Y : Type*} (T : Type*) [topological_space X]
  [compact_space X] [topological_space Y] [compact_space Y] [metric_space T]
  (f : C(X, Y)) (g : C(Y, T)) :
  (comp_right_continuous_map T f) g = g.comp f :=
rfl

/--
Precomposition by a homeomorphism is itself a homeomorphism between spaces of continuous maps.
-/
def comp_right_homeomorph {X Y : Type*} (T : Type*) [topological_space X] [compact_space X]
  [topological_space Y] [compact_space Y] [metric_space T]
  (f : X ‚âÉ‚Çú Y) : C(Y, T) ‚âÉ‚Çú C(X, T) :=
{ to_fun := comp_right_continuous_map T f.to_continuous_map,
  inv_fun := comp_right_continuous_map T f.symm.to_continuous_map,
  left_inv := Œª g, ext $ Œª _, congr_arg g (f.apply_symm_apply _),
  right_inv := Œª g, ext $ Œª _, congr_arg g (f.symm_apply_apply _) }

lemma comp_right_alg_hom_continuous {X Y : Type*} (R A : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y] [comm_semiring R]
  [semiring A] [metric_space A] [topological_semiring A] [algebra R A] (f : C(X, Y)) :
  continuous (comp_right_alg_hom R A f) :=
map_continuous (comp_right_continuous_map A f)

end comp_right

section local_normal_convergence
/-! ### Local normal convergence

A sum of continuous functions (on a locally compact space) is "locally normally convergent" if the
sum of its sup-norms on any compact subset is summable. This implies convergence in the topology
of `C(X, E)` (i.e. locally uniform convergence). -/

open topological_space

variables {X : Type*} [topological_space X] [t2_space X] [locally_compact_space X]
variables {E : Type*} [normed_add_comm_group E] [complete_space E]

lemma summable_of_locally_summable_norm {Œπ : Type*} {F : Œπ ‚Üí C(X, E)}
  (hF : ‚àÄ K : compacts X, summable (Œª i, ‚Äñ(F i).restrict K‚Äñ)) :
  summable F :=
begin
  refine (continuous_map.exists_tendsto_compact_open_iff_forall _).2 (Œª K hK, _),
  lift K to compacts X using hK,
  have A : ‚àÄ s : finset Œπ, restrict ‚ÜëK (‚àë i in s, F i) = ‚àë i in s, restrict K (F i),
  { intro s, ext1 x, simp },
  simpa only [has_sum, A] using summable_of_summable_norm (hF K)
end

end local_normal_convergence

/-!
### Star structures

In this section, if `Œ≤` is a normed ‚ãÜ-group, then so is the space of
continuous functions from `Œ±` to `Œ≤`, by using the star operation pointwise.

Furthermore, if `Œ±` is compact and `Œ≤` is a C‚ãÜ-ring, then `C(Œ±, Œ≤)` is a C‚ãÜ-ring.  -/

section normed_space

variables {Œ± : Type*} {Œ≤ : Type*}
variables [topological_space Œ±] [normed_add_comm_group Œ≤] [star_add_monoid Œ≤] [normed_star_group Œ≤]

lemma _root_.bounded_continuous_function.mk_of_compact_star [compact_space Œ±] (f : C(Œ±, Œ≤)) :
  mk_of_compact (star f) = star (mk_of_compact f) := rfl

instance [compact_space Œ±] : normed_star_group C(Œ±, Œ≤) :=
{ norm_star := Œª f, by rw [‚Üêbounded_continuous_function.norm_mk_of_compact,
                          bounded_continuous_function.mk_of_compact_star, norm_star,
                          bounded_continuous_function.norm_mk_of_compact] }

end normed_space

section cstar_ring

variables {Œ± : Type*} {Œ≤ : Type*}
variables [topological_space Œ±] [normed_ring Œ≤] [star_ring Œ≤]

instance [compact_space Œ±] [cstar_ring Œ≤] : cstar_ring C(Œ±, Œ≤) :=
{ norm_star_mul_self :=
  begin
    intros f,
    refine le_antisymm _ _,
    { rw [‚Üêsq, continuous_map.norm_le _ (sq_nonneg _)],
      intro x,
      simp only [continuous_map.coe_mul, coe_star, pi.mul_apply, pi.star_apply,
                 cstar_ring.norm_star_mul_self, ‚Üêsq],
      refine sq_le_sq' _ _,
      { linarith [norm_nonneg (f x), norm_nonneg f] },
      { exact continuous_map.norm_coe_le_norm f x }, },
    { rw [‚Üêsq, ‚Üêreal.le_sqrt (norm_nonneg _) (norm_nonneg _),
          continuous_map.norm_le _ (real.sqrt_nonneg _)],
      intro x,
      rw [real.le_sqrt (norm_nonneg _) (norm_nonneg _), sq, ‚Üêcstar_ring.norm_star_mul_self],
      exact continuous_map.norm_coe_le_norm (star f * f) x },
  end }

end cstar_ring

end continuous_map
