/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/

import topology.algebra.algebra
import topology.continuous_function.compact
import topology.urysohns_lemma
import data.is_R_or_C.basic
import analysis.normed_space.units
import topology.algebra.module.character_space

/-!
# Ideals of continuous functions

For a topological ring `R` and a topological space `X` there is a Galois connection between
`ideal C(X, R)` and `set X` given by sending each `I : ideal C(X, R)` to
`{x : X | âˆ€ f âˆˆ I, f x = 0}á¶œ` and mapping `s : set X` to the ideal with carrier
`{f : C(X, R) | âˆ€ x âˆˆ sá¶œ, f x = 0}`, and we call these maps `continuous_map.set_of_ideal` and
`continuous_map.ideal_of_set`. As long as `R` is Hausdorff, `continuous_map.set_of_ideal I` is open,
and if, in addition, `X` is locally compact, then `continuous_map.set_of_ideal s` is closed.

When `R = ğ•œ` with `is_R_or_C ğ•œ` and `X` is compact Hausdorff, then this Galois connection can be
improved to a true Galois correspondence (i.e., order isomorphism) between the type `opens X` and
the subtype of closed ideals of `C(X, ğ•œ)`. Because we do not have a bundled type of closed ideals,
we simply register this as a Galois insertion between `ideal C(X, ğ•œ)` and `opens X`, which is
`continuous_map.ideal_opens_gi`. Consequently, the maximal ideals of `C(X, ğ•œ)` are precisely those
ideals corresponding to (complements of) singletons in `X`.

In addition, when `X` is locally compact and `ğ•œ` is a nontrivial topological integral domain, then
there is a natural continuous map from `X` to `character_space ğ•œ C(X, ğ•œ)` given by point evaluation,
which is herein called `weak_dual.character_space.continuous_map_eval`. Again, when `X` is compact
Hausdorff and `is_R_or_C ğ•œ`, more can be obtained. In particular, in that context this map is
bijective, and since the domain is compact and the codomain is Hausdorff, it is a homeomorphism,
herein called `weak_dual.character_space.homeo_eval`.

## Main definitions

* `continuous_map.ideal_of_set`: ideal of functions which vanish on the complement of a set.
* `continuous_map.set_of_ideal`: complement of the set on which all functions in the ideal vanish.
* `continuous_map.opens_of_ideal`: `continuous_map.set_of_ideal` as a term of `opens X`.
* `continuous_map.ideal_opens_gi`: The Galois insertion `continuous_map.opens_of_ideal` and
  `Î» s, continuous_map.ideal_of_set â†‘s`.
* `weak_dual.character_space.continuous_map_eval`: the natural continuous map from a locally compact
  topological space `X` to the `character_space ğ•œ C(X, ğ•œ)` which sends `x : X` to point evaluation
  at `x`, with modest hypothesis on `ğ•œ`.
* `weak_dual.character_space.homeo_eval`: this is `weak_dual.character_space.continuous_map_eval`
  upgraded to a homeomorphism when `X` is compact Hausdorff and `is_R_or_C ğ•œ`.

## Main statements

* `continuous_map.ideal_of_set_of_ideal_eq_closure`: when `X` is compact Hausdorff and
  `is_R_or_C ğ•œ`, `ideal_of_set ğ•œ (set_of_ideal I) = I.closure` for any ideal `I : ideal C(X, ğ•œ)`.
* `continuous_map.set_of_ideal_of_set_eq_interior`: when `X` is compact Hausdorff and `is_R_or_C ğ•œ`,
  `set_of_ideal (ideal_of_set ğ•œ s) = interior s` for any `s : set X`.
* `continuous_map.ideal_is_maximal_iff`: when `X` is compact Hausdorff and `is_R_or_C ğ•œ`, a closed
  ideal of `C(X, ğ•œ)` is maximal if and only if it is `ideal_of_set ğ•œ {x}á¶œ` for some `x : X`.

## Implementation details

Because there does not currently exist a bundled type of closed ideals, we don't provide the actual
order isomorphism described above, and instead we only consider the Galois insertion
`continuous_map.ideal_opens_gi`.

## Tags

ideal, continuous function, compact, Hausdorff
-/


open_locale nnreal

namespace continuous_map

open topological_space

section topological_ring

variables {X R : Type*} [topological_space X] [ring R] [topological_space R] [topological_ring R]

variable (R)

/-- Given a topological ring `R` and `s : set X`, construct the ideal in `C(X, R)` of functions
which vanish on the complement of `s`. -/
def ideal_of_set (s : set X) : ideal C(X, R) :=
{ carrier := {f : C(X, R) | âˆ€ x âˆˆ sá¶œ, f x = 0},
  add_mem' := Î» f g hf hg x hx, by simp only [hf x hx, hg x hx, coe_add, pi.add_apply, add_zero],
  zero_mem' := Î» _ _, rfl,
  smul_mem' := Î» c f hf x hx, mul_zero (c x) â–¸ congr_arg (Î» y, c x * y) (hf x hx), }

lemma ideal_of_set_closed [locally_compact_space X] [t2_space R] (s : set X) :
  is_closed (ideal_of_set R s : set C(X, R) ) :=
begin
  simp only [ideal_of_set, submodule.coe_set_mk, set.set_of_forall],
  exact is_closed_Inter (Î» x, is_closed_Inter $
    Î» hx, is_closed_eq (continuous_eval_const' x) continuous_const),
end

variable {R}

lemma mem_ideal_of_set {s : set X} {f : C(X, R)} :
  f âˆˆ ideal_of_set R s â†” âˆ€ â¦ƒx : Xâ¦„, x âˆˆ sá¶œ â†’ f x = 0 := iff.rfl

lemma not_mem_ideal_of_set {s : set X} {f : C(X, R)} :
  f âˆ‰ ideal_of_set R s â†” âˆƒ x âˆˆ sá¶œ, f x â‰  0 :=
by { simp_rw [mem_ideal_of_set, exists_prop], push_neg }

/-- Given an ideal `I` of `C(X, R)`, construct the set of points for which every function in the
ideal vanishes on the complement. -/
def set_of_ideal (I : ideal C(X, R)) : set X :=
{x : X | âˆ€ f âˆˆ I, (f : C(X, R)) x = 0}á¶œ

lemma not_mem_set_of_ideal {I : ideal C(X, R)} {x : X} :
  x âˆ‰ set_of_ideal I â†” âˆ€ â¦ƒf : C(X, R)â¦„, f âˆˆ I â†’ f x = 0 :=
by rw [â†set.mem_compl_iff, set_of_ideal, compl_compl, set.mem_set_of]

lemma mem_set_of_ideal {I : ideal C(X, R)} {x : X} :
  x âˆˆ set_of_ideal I â†” âˆƒ f âˆˆ I, (f : C(X, R)) x â‰  0 :=
by { simp_rw [set_of_ideal, set.mem_compl_iff, set.mem_set_of, exists_prop], push_neg }

lemma set_of_ideal_open [t2_space R] (I : ideal C(X, R)) : is_open (set_of_ideal I) :=
begin
  simp only [set_of_ideal, set.set_of_forall, is_open_compl_iff],
  exact is_closed_Inter (Î» f, is_closed_Inter $
    Î» hf, is_closed_eq (map_continuous f) continuous_const)
end

/-- The open set `set_of_ideal I` realized as a term of `opens X`. -/
@[simps] def opens_of_ideal [t2_space R] (I : ideal C(X, R)) : opens X :=
âŸ¨set_of_ideal I, set_of_ideal_open IâŸ©

@[simp] lemma set_of_top_eq_univ [nontrivial R] : (set_of_ideal (âŠ¤ : ideal C(X, R))) = set.univ :=
set.univ_subset_iff.mp $ Î» x hx, mem_set_of_ideal.mpr âŸ¨1, submodule.mem_top, one_ne_zeroâŸ©

@[simp] lemma ideal_of_empty_eq_bot : (ideal_of_set R (âˆ… : set X)) = âŠ¥ :=
ideal.ext (Î» f, by simpa only [mem_ideal_of_set, set.compl_empty, set.mem_univ, forall_true_left,
  ideal.mem_bot, fun_like.ext_iff] using iff.rfl)

@[simp] lemma mem_ideal_of_set_compl_singleton (x : X) (f : C(X, R)) :
  f âˆˆ ideal_of_set R ({x}á¶œ : set X) â†” f x = 0 :=
by simp only [mem_ideal_of_set, compl_compl, set.mem_singleton_iff, forall_eq]

variables (X R)
lemma ideal_gc : galois_connection (set_of_ideal : ideal C(X, R) â†’ set X) (ideal_of_set R) :=
begin
  refine Î» I s, âŸ¨Î» h f hf, _, Î» h x hx, _âŸ©,
  { by_contra h',
    rcases not_mem_ideal_of_set.mp h' with âŸ¨x, hx, hfxâŸ©,
    exact hfx (not_mem_set_of_ideal.mp (mt (@h x) hx) hf) },
  { obtain âŸ¨f, hf, hfxâŸ© := mem_set_of_ideal.mp hx,
    by_contra hx',
    exact not_mem_ideal_of_set.mpr âŸ¨x, hx', hfxâŸ© (h hf) },
end

end topological_ring

section is_R_or_C
open is_R_or_C

variables {X ğ•œ : Type*} [is_R_or_C ğ•œ] [topological_space X]

/-- An auxiliary lemma used in the proof of `ideal_of_set_of_ideal_eq_closure` which may be useful
on its own. -/
lemma exists_mul_le_one_eq_on_ge (f : C(X, â„â‰¥0)) {c : â„â‰¥0} (hc : 0 < c) :
  âˆƒ g : C(X, â„â‰¥0), (âˆ€ x : X, (g * f) x â‰¤ 1) âˆ§ {x : X | c â‰¤ f x}.eq_on (g * f) 1 :=
âŸ¨{ to_fun := (f âŠ” (const X c))â»Â¹,
   continuous_to_fun := ((map_continuous f).sup $ map_continuous _).invâ‚€
     (Î» _, (hc.trans_le le_sup_right).ne')},
 Î» x, (inv_mul_le_iff (hc.trans_le le_sup_right)).mpr ((mul_one (f x âŠ” c)).symm â–¸ le_sup_left),
 Î» x hx, by simpa only [coe_const, coe_mk, pi.mul_apply, pi.inv_apply, pi.sup_apply,
   function.const_apply, pi.one_apply, sup_eq_left.mpr (set.mem_set_of.mp hx)]
   using inv_mul_cancel (hc.trans_le hx).ne'âŸ©

variables [compact_space X] [t2_space X]

@[simp] lemma ideal_of_set_of_ideal_eq_closure (I : ideal C(X, ğ•œ)) :
  ideal_of_set ğ•œ (set_of_ideal I) = I.closure :=
begin
  /- Since `ideal_of_set ğ•œ (set_of_ideal I)` is closed and contains `I`, it contains `I.closure`.
  For the reverse inclusion, given `f âˆˆ ideal_of_set ğ•œ (set_of_ideal I)` and `(Îµ : â„â‰¥0) > 0` it
  suffices to show that `f` is within `Îµ` of `I`.-/
  refine le_antisymm (Î» f hf, metric.mem_closure_iff.mpr (Î» Îµ hÎµ, _))
    ((ideal_of_set_closed ğ•œ $ set_of_ideal I).closure_subset_iff.mpr $
    Î» f hf x hx, not_mem_set_of_ideal.mp hx hf),
  lift Îµ to â„â‰¥0 using hÎµ.lt.le,
  replace hÎµ := (show (0 : â„â‰¥0) < Îµ, from hÎµ),
  simp_rw dist_nndist,
  norm_cast,
  -- Let `t := {x : X | Îµ / 2 â‰¤ â€–f xâ€–â‚Š}}` which is closed and disjoint from `set_of_ideal I`.
  set t := {x : X | Îµ / 2 â‰¤ â€–f xâ€–â‚Š},
  have ht : is_closed t := is_closed_le continuous_const (map_continuous f).nnnorm,
  have htI : disjoint t (set_of_ideal I)á¶œ,
  { refine set.subset_compl_iff_disjoint_left.mp (Î» x hx, _),
    simpa only [t, set.mem_set_of, set.mem_compl_iff, not_le]
      using (nnnorm_eq_zero.mpr (mem_ideal_of_set.mp hf hx)).trans_lt (half_pos hÎµ), },
  /- It suffices to produce `g : C(X, â„â‰¥0)` which takes values in `[0,1]` and is constantly `1` on
  `t` such that when composed with the natural embedding of `â„â‰¥0` into `ğ•œ` lies in the ideal `I`.
  Indeed, then `â€–f - f * â†‘gâ€– â‰¤ â€–f * (1 - â†‘g)â€– â‰¤ â¨† â€–f * (1 - â†‘g) xâ€–`. When `x âˆ‰ t`, `â€–f xâ€– < Îµ / 2`
  and `â€–(1 - â†‘g) xâ€– â‰¤ 1`, and when `x âˆˆ t`, `(1 - â†‘g) x = 0`, and clearly `f * â†‘g âˆˆ I`. -/
  suffices : âˆƒ g : C(X, â„â‰¥0),
    (algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g âˆˆ I âˆ§ (âˆ€ x, g x â‰¤ 1) âˆ§ t.eq_on g 1,
  { obtain âŸ¨g, hgI, hg, hgtâŸ© := this,
    refine âŸ¨f * (algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g, I.mul_mem_left f hgI, _âŸ©,
    rw nndist_eq_nnnorm,
    refine (nnnorm_lt_iff _ hÎµ).2 (Î» x, _),
    simp only [coe_sub, coe_mul, pi.sub_apply, pi.mul_apply],
    by_cases hx : x âˆˆ t,
    { simpa only [hgt hx, comp_apply, pi.one_apply, continuous_map.coe_coe, algebra_map_clm_apply,
        map_one, mul_one, sub_self, nnnorm_zero] using hÎµ, },
    { refine lt_of_le_of_lt _ (half_lt_self hÎµ),
      have := calc â€–((1 - (algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g) x : ğ•œ)â€–â‚Š
            = â€–1 - algebra_map â„â‰¥0 ğ•œ (g x)â€–â‚Š
            : by simp only [coe_sub, coe_one, coe_comp, continuous_map.coe_coe, pi.sub_apply,
                pi.one_apply, function.comp_app, algebra_map_clm_apply]
        ... = â€–algebra_map â„â‰¥0 ğ•œ (1 - g x)â€–â‚Š
            : by simp only [algebra.algebra_map_eq_smul_one, nnreal.smul_def, nnreal.coe_sub (hg x),
                sub_smul, nonneg.coe_one, one_smul]
        ... â‰¤ 1 : (nnnorm_algebra_map_nnreal ğ•œ (1 - g x)).trans_le tsub_le_self,
      calc â€–f x - f x * (algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g xâ€–â‚Š
          = â€–f x * (1 - (algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g) xâ€–â‚Š
          : by simp only [mul_sub, coe_sub, coe_one, pi.sub_apply, pi.one_apply, mul_one]
      ... â‰¤ (Îµ / 2) * â€–(1 - (algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g) xâ€–â‚Š
          : (nnnorm_mul_le _ _).trans (mul_le_mul_right'
              (not_le.mp $ show Â¬ Îµ / 2 â‰¤ â€–f xâ€–â‚Š, from hx).le _)
      ... â‰¤ Îµ / 2 : by simpa only [mul_one] using mul_le_mul_left' this _, } },
  /- There is some `g' : C(X, â„â‰¥0)` which is strictly positive on `t` such that the composition
  `â†‘g` with the natural embedding of `â„â‰¥0` into `ğ•œ` lies in `I`. This follows from compactness of
  `t` and that we can do it in any neighborhood of a point `x âˆˆ t`. Indeed, since `x âˆˆ t`, then
  `fâ‚“ x â‰  0` for some `fâ‚“ âˆˆ I` and so `Î» y, â€–(star fâ‚“ * fâ‚“) yâ€–â‚Š` is strictly posiive in a
  neighborhood of `y`. Moreover, `(â€–(star fâ‚“ * fâ‚“) yâ€–â‚Š : ğ•œ) = (star fâ‚“ * fâ‚“) y`, so composition of
  this map with the natural embedding is just `star fâ‚“ * fâ‚“ âˆˆ I`. -/
  have : âˆƒ g' : C(X, â„â‰¥0), (algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g' âˆˆ I âˆ§ (âˆ€ x âˆˆ t, 0 < g' x),
  { refine @is_compact.induction_on _ _ _ ht.is_compact (Î» s, âˆƒ g' : C(X, â„â‰¥0),
      (algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g' âˆˆ I âˆ§ (âˆ€ x âˆˆ s, 0 < g' x)) _ _ _ _,
    { refine âŸ¨0, _, Î» x hx, false.elim hxâŸ©,
      convert I.zero_mem,
      ext,
      simp only [coe_zero, pi.zero_apply, continuous_map.coe_coe, continuous_map.coe_comp,
        map_zero, pi.comp_zero] },
    { rintro sâ‚ sâ‚‚ hs âŸ¨g, hI, hgtâŸ©, exact âŸ¨g, hI, Î» x hx, hgt x (hs hx)âŸ©, },
    { rintro sâ‚ sâ‚‚ âŸ¨gâ‚, hIâ‚, hgtâ‚âŸ© âŸ¨gâ‚‚, hIâ‚‚, hgtâ‚‚âŸ©,
      refine âŸ¨gâ‚ + gâ‚‚, _, Î» x hx, _âŸ©,
      { convert I.add_mem hIâ‚ hIâ‚‚,
        ext y,
        simp only [coe_add, pi.add_apply, map_add, coe_comp, function.comp_app,
          continuous_map.coe_coe]},
      { rcases hx with (hx | hx),
        simpa only [zero_add] using add_lt_add_of_lt_of_le (hgtâ‚ x hx) zero_le',
        simpa only [zero_add] using add_lt_add_of_le_of_lt zero_le' (hgtâ‚‚ x hx), } },
    { intros x hx,
      replace hx := htI.subset_compl_right hx,
      rw [compl_compl, mem_set_of_ideal] at hx,
      obtain âŸ¨g, hI, hgxâŸ© := hx,
      have := (map_continuous g).continuous_at.eventually_ne hgx,
      refine âŸ¨{y : X | g y â‰  0} âˆ© t, mem_nhds_within_iff_exists_mem_nhds_inter.mpr
        âŸ¨_, this, set.subset.rflâŸ©, âŸ¨âŸ¨Î» x, â€–g xâ€–â‚Š ^ 2, (map_continuous g).nnnorm.pow 2âŸ©, _,
        Î» x hx, pow_pos (norm_pos_iff.mpr hx.1) 2âŸ©âŸ©,
      convert I.mul_mem_left (star g) hI,
      ext,
      simp only [comp_apply, coe_mk, algebra_map_clm_coe, map_pow, coe_mul, coe_star,
        pi.mul_apply, pi.star_apply, star_def, continuous_map.coe_coe],
      simpa only [norm_sq_eq_def', conj_mul_eq_norm_sq_left, of_real_pow], }, },
  /- Get the function `g'` which is guaranteed to exist above. By the extreme value theorem and
  compactness of `t`, there is some `0 < c` such that `c â‰¤ g' x` for all `x âˆˆ t`. Then by
  `main_lemma_aux` there is some `g` for which `g * g'` is the desired function. -/
  obtain âŸ¨g', hI', hgt'âŸ© := this,
  obtain (âŸ¨c, hc, hgc'âŸ© : âˆƒ c (hc : 0 < c), âˆ€ y : X, y âˆˆ t â†’ c â‰¤ g' y) :=
  t.eq_empty_or_nonempty.elim (Î» ht', âŸ¨1, zero_lt_one, Î» y hy, false.elim (by rwa ht' at hy)âŸ©)
    (Î» ht', let âŸ¨x, hx, hx'âŸ© := ht.is_compact.exists_forall_le ht' (map_continuous g').continuous_on
      in âŸ¨g' x, hgt' x hx, hx'âŸ©),
  obtain âŸ¨g, hg, hgcâŸ© := exists_mul_le_one_eq_on_ge g' hc,
  refine âŸ¨g * g', _, hg, hgc.mono hgc'âŸ©,
  convert I.mul_mem_left ((algebra_map_clm â„â‰¥0 ğ•œ : C(â„â‰¥0, ğ•œ)).comp g) hI',
  ext,
  simp only [algebra_map_clm_coe, continuous_map.coe_coe, comp_apply, coe_mul, pi.mul_apply,
    map_mul],
end

lemma ideal_of_set_of_ideal_is_closed {I : ideal C(X, ğ•œ)}
  (hI : is_closed (I : set C(X, ğ•œ))) : ideal_of_set ğ•œ (set_of_ideal I) = I :=
(ideal_of_set_of_ideal_eq_closure I).trans (ideal.ext $ set.ext_iff.mp hI.closure_eq)

variable (ğ•œ)

@[simp] lemma set_of_ideal_of_set_eq_interior (s : set X) :
  set_of_ideal (ideal_of_set ğ•œ s) = interior s:=
begin
  refine set.subset.antisymm ((set_of_ideal_open (ideal_of_set ğ•œ s)).subset_interior_iff.mpr
    (Î» x hx, let âŸ¨f, hf, hfxâŸ© := mem_set_of_ideal.mp hx
    in set.not_mem_compl_iff.mp (mt (@hf x) hfx))) (Î» x hx, _),
  /- If `x âˆ‰ closure sá¶œ`, we must produce `f : C(X, ğ•œ)` which is zero on `sá¶œ` and `f x â‰  0`. -/
  rw [â†compl_compl (interior s), â†closure_compl] at hx,
  simp_rw [mem_set_of_ideal, mem_ideal_of_set],
  haveI : normal_space X := normal_of_compact_t2,
  /- Apply Urysohn's lemma to get `g : C(X, â„)` which is zero on `sá¶œ` and `g x â‰  0`, then compose
  with the natural embedding `â„ â†ª ğ•œ` to produce the desired `f`. -/
  obtain âŸ¨g, hgs, (hgx : set.eq_on g 1 {x}), -âŸ© := exists_continuous_zero_one_of_closed
    is_closed_closure is_closed_singleton (set.disjoint_singleton_right.mpr hx),
  exact âŸ¨âŸ¨Î» x, g x, continuous_of_real.comp (map_continuous g)âŸ©,
    by simpa only [coe_mk, of_real_eq_zero] using Î» x hx, hgs (subset_closure hx),
    by simpa only [coe_mk, hgx (set.mem_singleton x), pi.one_apply, is_R_or_C.of_real_one]
      using one_ne_zeroâŸ©,
end

lemma set_of_ideal_of_set_of_is_open {s : set X} (hs : is_open s) :
  set_of_ideal (ideal_of_set ğ•œ s) = s :=
(set_of_ideal_of_set_eq_interior ğ•œ s).trans hs.interior_eq

variable (X)

/-- The Galois insertion `continuous_map.opens_of_ideal : ideal C(X, ğ•œ) â†’ opens X` and
`Î» s, continuous_map.ideal_of_set â†‘s`. -/
@[simps] def ideal_opens_gi :
  galois_insertion (opens_of_ideal : ideal C(X, ğ•œ) â†’ opens X) (Î» s, ideal_of_set ğ•œ s) :=
{ choice := Î» I hI, opens_of_ideal I.closure,
  gc := Î» I s, ideal_gc X ğ•œ I s,
  le_l_u := Î» s, (set_of_ideal_of_set_of_is_open ğ•œ s.is_open).ge,
  choice_eq := Î» I hI, congr_arg _ $ ideal.ext (set.ext_iff.mp (is_closed_of_closure_subset $
    (ideal_of_set_of_ideal_eq_closure I â–¸ hI : I.closure â‰¤ I)).closure_eq) }

variables {X}

lemma ideal_of_set_is_maximal_iff (s : opens X) :
  (ideal_of_set ğ•œ (s : set X)).is_maximal â†” is_coatom s :=
begin
  rw ideal.is_maximal_def,
  refine (ideal_opens_gi X ğ•œ).is_coatom_iff  (Î» I hI, _) s,
  rw â†ideal.is_maximal_def at hI,
  resetI,
  exact ideal_of_set_of_ideal_is_closed infer_instance,
end

lemma ideal_of_compl_singleton_is_maximal (x : X) : (ideal_of_set ğ•œ ({x}á¶œ : set X)).is_maximal :=
(ideal_of_set_is_maximal_iff ğ•œ (closeds.singleton x).compl).mpr  $ opens.is_coatom_iff.mpr âŸ¨x, rflâŸ©

variables {ğ•œ}

lemma set_of_ideal_eq_compl_singleton (I : ideal C(X, ğ•œ)) [hI : I.is_maximal] :
  âˆƒ x : X, set_of_ideal I = {x}á¶œ :=
begin
  have h : (ideal_of_set ğ•œ (set_of_ideal I)).is_maximal, from
    (ideal_of_set_of_ideal_is_closed (infer_instance : is_closed (I : set C(X, ğ•œ)))).symm â–¸ hI,
  obtain âŸ¨x, hxâŸ© := opens.is_coatom_iff.1 ((ideal_of_set_is_maximal_iff ğ•œ (opens_of_ideal I)).1 h),
  exact âŸ¨x, congr_arg coe hxâŸ©,
end

lemma ideal_is_maximal_iff (I : ideal C(X, ğ•œ)) [hI : is_closed (I : set C(X, ğ•œ))] :
  I.is_maximal â†” âˆƒ x : X, ideal_of_set ğ•œ {x}á¶œ = I :=
begin
  refine âŸ¨_, Î» h, let âŸ¨x, hxâŸ© := h in hx â–¸ ideal_of_compl_singleton_is_maximal ğ•œ xâŸ©,
  introI hI',
  obtain âŸ¨x, hxâŸ© := set_of_ideal_eq_compl_singleton I,
  exact âŸ¨x, by simpa only [ideal_of_set_of_ideal_eq_closure, ideal.closure_eq_of_is_closed]
    using congr_arg (ideal_of_set ğ•œ) hx.symmâŸ©,
end

end is_R_or_C

end continuous_map

namespace weak_dual
namespace character_space

open function continuous_map

variables (X ğ•œ : Type*) [topological_space X]

section continuous_map_eval

variables [locally_compact_space X] [comm_ring ğ•œ] [topological_space ğ•œ] [topological_ring ğ•œ]
variables [nontrivial ğ•œ] [no_zero_divisors ğ•œ]

/-- The natural continuous map from a locally compact topological space `X` to the
`character_space ğ•œ C(X, ğ•œ)` which sends `x : X` to point evaluation at `x`. -/
def continuous_map_eval :
  C(X, character_space ğ•œ C(X, ğ•œ)) :=
{ to_fun := Î» x, âŸ¨{ to_fun := Î» f, f x, map_add' := Î» f g, rfl, map_smul' := Î» z f, rfl,
                    cont := continuous_eval_const' x },
                  by { rw character_space.eq_set_map_one_map_mul, exact âŸ¨rfl, Î» f g, rflâŸ© }âŸ©,
  continuous_to_fun := continuous.subtype_mk (continuous_of_continuous_eval map_continuous) _ }

@[simp] lemma continuous_map_eval_apply_apply (x : X) (f : C(X, ğ•œ)) :
  continuous_map_eval X ğ•œ x f = f x := rfl

end continuous_map_eval

variables [compact_space X] [t2_space X] [is_R_or_C ğ•œ]

lemma continuous_map_eval_bijective : bijective (continuous_map_eval X ğ•œ) :=
begin
  refine âŸ¨Î» x y hxy, _, Î» Ï†, _âŸ©,
  { contrapose! hxy,
    haveI := @normal_of_compact_t2 X _ _ _,
    rcases exists_continuous_zero_one_of_closed (is_closed_singleton : _root_.is_closed {x})
      (is_closed_singleton : _root_.is_closed {y}) (set.disjoint_singleton.mpr hxy)
      with âŸ¨f, fx, fy, -âŸ©,
    rw [â†ne.def, fun_like.ne_iff],
    use (âŸ¨coe, is_R_or_C.continuous_of_realâŸ© : C(â„, ğ•œ)).comp f,
    simpa only [continuous_map_eval_apply_apply, continuous_map.comp_apply, coe_mk, ne.def,
      is_R_or_C.of_real_inj] using ((fx (set.mem_singleton x)).symm â–¸
      (fy (set.mem_singleton y)).symm â–¸ zero_ne_one : f x â‰  f y) },
  { obtain âŸ¨x, hxâŸ© := (ideal_is_maximal_iff (ring_hom.ker Ï†)).mp infer_instance,
    refine âŸ¨x, ext_ker $ ideal.ext $ Î» f, _âŸ©,
    simpa only [ring_hom.mem_ker, continuous_map_eval_apply_apply,
      mem_ideal_of_set_compl_singleton, ring_hom.mem_ker] using set_like.ext_iff.mp hx f }
end

/-- This is the natural homeomorphism between a compact Hausdorff space `X` and the
`character_space ğ•œ C(X, ğ•œ)`. -/
noncomputable def homeo_eval : X â‰ƒâ‚œ character_space ğ•œ C(X, ğ•œ) :=
@continuous.homeo_of_equiv_compact_to_t2 _ _ _ _ _ _
  { to_fun := (continuous_map_eval X ğ•œ),
    .. equiv.of_bijective _ (continuous_map_eval_bijective X ğ•œ) }
  (map_continuous (continuous_map_eval X ğ•œ))

end character_space
end weak_dual
