/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import topology.continuous_function.weierstrass

/-!
# The Stone-Weierstrass theorem

If a subalgebra `A` of `C(X, ‚Ñù)`, where `X` is a compact topological space,
separates points, then it is dense.

We argue as follows.

* In any subalgebra `A` of `C(X, ‚Ñù)`, if `f ‚àà A`, then `abs f ‚àà A.topological_closure`.
  This follows from the Weierstrass approximation theorem on `[-‚à•f‚à•, ‚à•f‚à•]` by
  approximating `abs` uniformly thereon by polynomials.
* This ensures that `A.topological_closure` is actually a sublattice:
  if it contains `f` and `g`, then it contains the pointwise supremum `f ‚äî g`
  and the pointwise infimum `f ‚äì g`.
* Any nonempty sublattice `L` of `C(X, ‚Ñù)` which separates points is dense,
  by a nice argument approximating a given `f` above and below using separating functions.
  For each `x y : X`, we pick a function `g x y ‚àà L` so `g x y x = f x` and `g x y y = f y`.
  By continuity these functions remain close to `f` on small patches around `x` and `y`.
  We use compactness to identify a certain finitely indexed infimum of finitely indexed supremums
  which is then close to `f` everywhere, obtaining the desired approximation.
* Finally we put these pieces together. `L = A.topological_closure` is a nonempty sublattice
  which separates points since `A` does, and so is dense (in fact equal to `‚ä§`).

## Future work

Prove the complex version for self-adjoint subalgebras `A`, by separately approximating
the real and imaginary parts using the real subalgebra of real-valued functions in `A`
(which still separates points, by taking the norm-square of a separating function).

Extend to cover the case of subalgebras of the continuous functions vanishing at infinity,
on non-compact spaces.

-/

noncomputable theory

namespace continuous_map

variables {X : Type*} [topological_space X] [compact_space X]

/--
Turn a function `f : C(X, ‚Ñù)` into a continuous map into `set.Icc (-‚à•f‚à•) (‚à•f‚à•)`,
thereby explicitly attaching bounds.
-/
def attach_bound (f : C(X, ‚Ñù)) : C(X, set.Icc (-‚à•f‚à•) (‚à•f‚à•)) :=
{ to_fun := Œª x, ‚ü®f x, ‚ü®neg_norm_le_apply f x, apply_le_norm f x‚ü©‚ü© }

@[simp] lemma attach_bound_apply_coe (f : C(X, ‚Ñù)) (x : X) : ((attach_bound f) x : ‚Ñù) = f x := rfl

lemma polynomial_comp_attach_bound (A : subalgebra ‚Ñù C(X, ‚Ñù)) (f : A) (g : polynomial ‚Ñù) :
  (g.to_continuous_map_on (set.Icc (-‚à•f‚à•) ‚à•f‚à•)).comp (f : C(X, ‚Ñù)).attach_bound =
    polynomial.aeval f g :=
begin
  ext,
  simp only [continuous_map.comp_coe, function.comp_app,
    continuous_map.attach_bound_apply_coe,
    polynomial.to_continuous_map_on_to_fun,
    polynomial.aeval_subalgebra_coe,
    polynomial.aeval_continuous_map_apply,
    polynomial.to_continuous_map_to_fun],
end

/--
Given a continuous function `f` in a subalgebra of `C(X, ‚Ñù)`, postcomposing by a polynomial
gives another function in `A`.

This lemma proves something slightly more subtle than this:
we take `f`, and think of it as a function into the restricted target `set.Icc (-‚à•f‚à•) ‚à•f‚à•)`,
and then postcompose with a polynomial function on that interval.
This is in fact the same situation as above, and so also gives a function in `A`.
-/
lemma polynomial_comp_attach_bound_mem (A : subalgebra ‚Ñù C(X, ‚Ñù)) (f : A) (g : polynomial ‚Ñù) :
  (g.to_continuous_map_on (set.Icc (-‚à•f‚à•) ‚à•f‚à•)).comp (f : C(X, ‚Ñù)).attach_bound ‚àà A :=
begin
  rw polynomial_comp_attach_bound,
  apply set_like.coe_mem,
end

theorem comp_attach_bound_mem_closure
  (A : subalgebra ‚Ñù C(X, ‚Ñù)) (f : A) (p : C(set.Icc (-‚à•f‚à•) (‚à•f‚à•), ‚Ñù)) :
  p.comp (attach_bound f) ‚àà A.topological_closure :=
begin
  -- `p` itself is in the closure of polynomials, by the Weierstrass theorem,
  have mem_closure : p ‚àà (polynomial_functions (set.Icc (-‚à•f‚à•) (‚à•f‚à•))).topological_closure :=
    continuous_map_mem_polynomial_functions_closure _ _ p,
  -- and so there are polynomials arbitrarily close.
  have frequently_mem_polynomials := mem_closure_iff_frequently.mp mem_closure,
  -- To prove `p.comp (attached_bound f)` is in the closure of `A`,
  -- we show there are elements of `A` arbitrarily close.
  apply mem_closure_iff_frequently.mpr,
  -- To show that, we pull back the polynomials close to `p`,
  refine ((comp_right_continuous_map ‚Ñù (attach_bound (f : C(X, ‚Ñù)))).continuous_at p).tendsto
    .frequently_map _ _ frequently_mem_polynomials,
  -- but need to show that those pullbacks are actually in `A`.
  rintros _ ‚ü®g, ‚ü®-,rfl‚ü©‚ü©,
  simp only [set_like.mem_coe, alg_hom.coe_to_ring_hom, comp_right_continuous_map_apply,
    polynomial.to_continuous_map_on_alg_hom_apply],
  apply polynomial_comp_attach_bound_mem,
end

theorem abs_mem_subalgebra_closure (A : subalgebra ‚Ñù C(X, ‚Ñù)) (f : A) :
  (f : C(X, ‚Ñù)).abs ‚àà A.topological_closure :=
begin
  let M := ‚à•f‚à•,
  let f' := attach_bound (f : C(X, ‚Ñù)),
  let abs : C(set.Icc (-‚à•f‚à•) (‚à•f‚à•), ‚Ñù) :=
  { to_fun := Œª x : set.Icc (-‚à•f‚à•) (‚à•f‚à•), _root_.abs (x : ‚Ñù) },
  change (abs.comp f') ‚àà A.topological_closure,
  apply comp_attach_bound_mem_closure,
end

theorem inf_mem_subalgebra_closure (A : subalgebra ‚Ñù C(X, ‚Ñù)) (f g : A) :
  (f : C(X, ‚Ñù)) ‚äì (g : C(X, ‚Ñù)) ‚àà A.topological_closure :=
begin
  rw inf_eq,
  refine A.topological_closure.smul_mem
    (A.topological_closure.sub_mem
      (A.topological_closure.add_mem (A.subalgebra_topological_closure f.property)
          (A.subalgebra_topological_closure g.property)) _) _,
  exact_mod_cast abs_mem_subalgebra_closure A _,
end

theorem inf_mem_closed_subalgebra (A : subalgebra ‚Ñù C(X, ‚Ñù)) (h : is_closed (A : set C(X, ‚Ñù)))
  (f g : A) : (f : C(X, ‚Ñù)) ‚äì (g : C(X, ‚Ñù)) ‚àà A :=
begin
  convert inf_mem_subalgebra_closure A f g,
  apply set_like.ext',
  symmetry,
  erw closure_eq_iff_is_closed,
  exact h,
end

theorem sup_mem_subalgebra_closure (A : subalgebra ‚Ñù C(X, ‚Ñù)) (f g : A) :
  (f : C(X, ‚Ñù)) ‚äî (g : C(X, ‚Ñù)) ‚àà A.topological_closure :=
begin
  rw sup_eq,
  refine A.topological_closure.smul_mem
    (A.topological_closure.add_mem
      (A.topological_closure.add_mem (A.subalgebra_topological_closure f.property)
          (A.subalgebra_topological_closure g.property)) _) _,
  exact_mod_cast abs_mem_subalgebra_closure A _,
end

theorem sup_mem_closed_subalgebra (A : subalgebra ‚Ñù C(X, ‚Ñù)) (h : is_closed (A : set C(X, ‚Ñù)))
  (f g : A) : (f : C(X, ‚Ñù)) ‚äî (g : C(X, ‚Ñù)) ‚àà A :=
begin
  convert sup_mem_subalgebra_closure A f g,
  apply set_like.ext',
  symmetry,
  erw closure_eq_iff_is_closed,
  exact h,
end

open_locale topological_space

-- Here's the fun part of Stone-Weierstrass!
theorem sublattice_closure_eq_top
  (L : set C(X, ‚Ñù)) (nA : L.nonempty)
  (inf_mem : ‚àÄ f g ‚àà L, f ‚äì g ‚àà L) (sup_mem : ‚àÄ f g ‚àà L, f ‚äî g ‚àà L)
  (sep : L.separates_points_strongly) :
  closure L = ‚ä§ :=
begin
  -- We start by boiling down to a statement about close approximation.
  apply eq_top_iff.mpr,
  rintros f -,
  refine filter.frequently.mem_closure
    ((filter.has_basis.frequently_iff metric.nhds_basis_ball).mpr (Œª Œµ pos, _)),
  simp only [exists_prop, metric.mem_ball],

  -- It will be helpful to assume `X` is nonempty later,
  -- so we get that out of the way here.
  by_cases nX : nonempty X,
  swap,
  exact ‚ü®nA.some, (dist_lt_iff _ _ pos).mpr (Œª x, false.elim (nX ‚ü®x‚ü©)), nA.some_spec‚ü©,

  /-
  The strategy now is to pick a family of continuous functions `g x y` in `A`
  with the property that `g x y x = f x` and `g x y y = f y`
  (this is immediate from `h : separates_points_strongly`)
  then use continuity to see that `g x y` is close to `f` near both `x` and `y`,
  and finally using compactness to produce the desired function `h`
  as a maximum over finitely many `x` of a minimum over finitely many `y` of the `g x y`.
  -/
  dsimp [set.separates_points_strongly] at sep,

  let g : Œ† x y, L := Œª x y, (sep f x y).some,
  have w‚ÇÅ : ‚àÄ x y, g x y x = f x := Œª x y, (sep f x y).some_spec.1,
  have w‚ÇÇ : ‚àÄ x y, g x y y = f y := Œª x y, (sep f x y).some_spec.2,

  -- For each `x y`, we define `U x y` to be `{z | f z - Œµ < g x y z}`,
  -- and observe this is a neighbourhood of `y`.
  let U : Œ† x y, set X := Œª x y, {z | f z - Œµ < g x y z},
  have U_nhd_y : ‚àÄ x y, U x y ‚àà ùìù y,
  { intros x y,
    refine mem_nhds_sets _ _,
    { rw [show U x y = (f - g x y : C(X, ‚Ñù)) ‚Åª¬π' set.Iio Œµ, { ext z, simp [sub_lt], }],
      exact is_open.preimage (by continuity) is_open_Iio, },
    { rw [set.mem_set_of_eq, w‚ÇÇ],
      exact sub_lt_self _ pos, }, },

  -- Fixing `x` for a moment, we have a family of functions `Œª y, g x y`
  -- which on different patches (the `U x y`) are greater than `f z - Œµ`.
  -- Taking the supremum of these functions
  -- indexed by a finite collection of patches which cover `X`
  -- will give us an element of `A` that is globally greater than `f z - Œµ`
  -- and still equal to `f x` at `x`.

  -- Since `X` is compact, for every `x` there is some finset `ys t`
  -- so the union of the `U x y` for `y ‚àà ys x` still covers everything.
  let ys : Œ† x, finset X := Œª x, (compact_space.elim_nhds_subcover (U x) (U_nhd_y x)).some,
  let ys_w : ‚àÄ x, (‚ãÉ y ‚àà ys x, U x y) = ‚ä§ :=
    Œª x, (compact_space.elim_nhds_subcover (U x) (U_nhd_y x)).some_spec,

  have ys_nonempty : ‚àÄ x, (ys x).nonempty :=
    Œª x, set.nonempty_of_union_eq_top_of_nonempty _ _ nX (ys_w x),

  -- Thus for each `x` we have the desired `h x : A` so `f z - Œµ < h x z` everywhere
  -- and `h x x = f x`.
  let h : Œ† x, L := Œª x,
    ‚ü®(ys x).sup' (ys_nonempty x) (Œª y, (g x y : C(X, ‚Ñù))),
      finset.sup'_mem _ sup_mem _ _ _ (Œª y _, (g x y).2)‚ü©,
  have lt_h : ‚àÄ x z, f z - Œµ < h x z,
  { intros x z,
    obtain ‚ü®y, ym, zm‚ü© := set.exists_set_mem_of_union_eq_top _ _ (ys_w x) z,
    dsimp [h],
    simp only [finset.lt_sup'_iff, continuous_map.sup'_apply],
    exact ‚ü®y, ym, zm‚ü©, },

  -- For each `x y`, we define `V x y` to be `{z | g x y z < f z + Œµ}`,
  -- and observe this is a neighbourhood of `x`.
  let V : Œ† x y, set X := Œª x y, { z | g x y z < f z + Œµ },
  have V_nhd_x : ‚àÄ x y, V x y ‚àà ùìù x,
  { intros x y,
    refine mem_nhds_sets _ _,
    { rw [show V x y = (g x y - f : C(X, ‚Ñù)) ‚Åª¬π' set.Iio Œµ, { ext z, simp [sub_lt_iff_lt_add'], }],
      exact is_open.preimage (by continuity) is_open_Iio, },
    { rw [set.mem_set_of_eq, w‚ÇÅ],
      exact (lt_add_iff_pos_right _).mpr pos, }, },

  -- For each `x`, we can take the finite intersection of the `V x y` corresponding to `y ‚àà ys x`.
  let W : Œ† x, set X := Œª x, (‚ãÇ y ‚àà ys x, V x y),
  -- This is still a neighbourhood of `x`.
  have W_nhd : ‚àÄ x, W x ‚àà ùìù x :=
    Œª x, (filter.bInter_finset_mem_sets (ys x)).2 (Œª y m, V_nhd_x x y),
  -- Locally on each `W x`, we have `h x z < f z + Œµ`, since `h x` is a supremum of the `g x y`.
  have h_lt : ‚àÄ (x) (z ‚àà W x), h x z < f z + Œµ,
  { intros x z zm,
    dsimp [h],
    simp only [continuous_map.sup'_apply, finset.sup'_lt_iff],
    intros y ym,
    convert set.mem_of_mem_of_subset zm (set.bInter_subset_of_mem ym), },

  -- Since `X` is compact, there is some finset `ys t`
  -- so the union of the `W x` for `x ‚àà xs` still covers everything.
  let xs : finset X := (compact_space.elim_nhds_subcover W W_nhd).some,
  let xs_w : (‚ãÉ x ‚àà xs, W x) = ‚ä§ :=
    (compact_space.elim_nhds_subcover W W_nhd).some_spec,
  have xs_nonempty : xs.nonempty := set.nonempty_of_union_eq_top_of_nonempty _ _ nX xs_w,

  -- Finally our candidate function is the infimum over `x ‚àà xs` of the `h x`.
  -- This function is then globally less than `f z + Œµ`.
  let k : (L : Type*) :=
    ‚ü®xs.inf' xs_nonempty (Œª x, (h x : C(X, ‚Ñù))),
      finset.inf'_mem _ inf_mem _ _ _ (Œª x _, (h x).2)‚ü©,

  refine ‚ü®k.1, _, k.2‚ü©,

  -- We just need to verify the bound, which we do pointwise.
  rw dist_lt_iff _ _ pos,
  intro z,

  -- We rewrite into this particular form,
  -- so that simp lemmas about inequalities involving `finset.inf'` can fire.
  rw [(show ‚àÄ a b Œµ : ‚Ñù, dist a b < Œµ ‚Üî a < b + Œµ ‚àß b - Œµ < a,
    by { intros, simp only [‚Üê metric.mem_ball, real.ball_eq_Ioo, set.mem_Ioo, and_comm], })],

  fsplit,
  { dsimp [k],
    simp only [finset.inf'_lt_iff, continuous_map.inf'_apply],
    obtain ‚ü®x, xm, zm‚ü© := set.exists_set_mem_of_union_eq_top _ _ xs_w z,
    exact ‚ü®x, xm, h_lt _ _ zm‚ü©, },
  { dsimp [k],
    simp only [finset.lt_inf'_iff, continuous_map.inf'_apply],
    intros x xm,
    apply lt_h, },
end

/--
The Stone-Weierstrass approximation theorem,
that a subalgebra `A` of `C(X, ‚Ñù)`, where `X` is a compact topological space,
is dense if it separates points.
-/
theorem subalgebra_topological_closure_eq_top_of_separates_points
  (A : subalgebra ‚Ñù C(X, ‚Ñù)) (w : A.separates_points) :
  A.topological_closure = ‚ä§ :=
begin
  -- The closure of `A` is closed under taking `sup` and `inf`,
  -- and separates points strongly (since `A` does),
  -- so we can apply `sublattice_closure_eq_top`.
  apply set_like.ext',
  let L := A.topological_closure,
  have n : set.nonempty (L : set C(X, ‚Ñù)) :=
    ‚ü®(1 : C(X, ‚Ñù)), A.subalgebra_topological_closure A.one_mem‚ü©,
  convert sublattice_closure_eq_top
    (L : set C(X, ‚Ñù)) n
    (Œª f g fm gm, inf_mem_closed_subalgebra L A.is_closed_topological_closure ‚ü®f, fm‚ü© ‚ü®g, gm‚ü©)
    (Œª f g fm gm, sup_mem_closed_subalgebra L A.is_closed_topological_closure ‚ü®f, fm‚ü© ‚ü®g, gm‚ü©)
    (subalgebra.separates_points.strongly
      (subalgebra.separates_points_monotone (A.subalgebra_topological_closure) w)),
  { simp, },
  { ext, simp, },
end

/--
An alternative statement of the Stone-Weierstrass theorem.

If `A` is a subalgebra of `C(X, ‚Ñù)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is a uniform limit of elements of `A`.
-/
theorem continuous_map_mem_subalgebra_closure_of_separates_points
  (A : subalgebra ‚Ñù C(X, ‚Ñù)) (w : A.separates_points)
  (f : C(X, ‚Ñù)) :
  f ‚àà A.topological_closure :=
begin
  rw subalgebra_topological_closure_eq_top_of_separates_points A w,
  simp,
end

/--
An alternative statement of the Stone-Weierstrass theorem,
for those who like their epsilons.

If `A` is a subalgebra of `C(X, ‚Ñù)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is within any `Œµ > 0` of some element of `A`.
-/
theorem exists_mem_subalgebra_near_continuous_map_of_separates_points
  (A : subalgebra ‚Ñù C(X, ‚Ñù)) (w : A.separates_points)
  (f : C(X, ‚Ñù)) (Œµ : ‚Ñù) (pos : 0 < Œµ) :
  ‚àÉ (g : A), ‚à•(g : C(X, ‚Ñù)) - f‚à• < Œµ :=
begin
  have w := mem_closure_iff_frequently.mp
    (continuous_map_mem_subalgebra_closure_of_separates_points A w f),
  rw metric.nhds_basis_ball.frequently_iff at w,
  obtain ‚ü®g, H, m‚ü© := w Œµ pos,
  rw [metric.mem_ball, dist_eq_norm] at H,
  exact ‚ü®‚ü®g, m‚ü©, H‚ü©,
end


end continuous_map
