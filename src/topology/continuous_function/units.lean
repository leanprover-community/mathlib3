/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import analysis.normed_space.units
import algebra.algebra.spectrum
import topology.continuous_function.algebra

/-!
# Units of continuous functions

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file concerns itself with `C(X, M)Ë£` and `C(X, MË£)` when `X` is a topological space
and `M` has some monoid structure compatible with its topology.
-/

variables {X M R ğ•œ : Type*} [topological_space X]

namespace continuous_map

section monoid

variables [monoid M] [topological_space M] [has_continuous_mul M]

/-- Equivalence between continuous maps into the units of a monoid with continuous multiplication
and the units of the monoid of continuous maps. -/
@[to_additive "Equivalence between continuous maps into the additive units of an additive monoid
with continuous addition and the additive units of the additive monoid of continuous maps.", simps]
def units_lift : C(X, MË£) â‰ƒ C(X, M)Ë£ :=
{ to_fun := Î» f,
  { val := âŸ¨Î» x, f x, units.continuous_coe.comp f.continuousâŸ©,
    inv := âŸ¨Î» x, â†‘(f x)â»Â¹, units.continuous_coe.comp (continuous_inv.comp f.continuous)âŸ©,
    val_inv := ext $ Î» x, units.mul_inv _,
    inv_val := ext $ Î» x, units.inv_mul _ },
  inv_fun := Î» f,
  { to_fun := Î» x, âŸ¨f x, fâ»Â¹ x, continuous_map.congr_fun f.mul_inv x,
                                continuous_map.congr_fun f.inv_mul xâŸ©,
    continuous_to_fun := continuous_induced_rng.2 $ continuous.prod_mk (f : C(X, M)).continuous
      $ mul_opposite.continuous_op.comp (â†‘fâ»Â¹ : C(X, M)).continuous },
  left_inv := Î» f, by { ext, refl },
  right_inv := Î» f, by { ext, refl } }

end monoid

section normed_ring

variables [normed_ring R] [complete_space R]

lemma _root_.normed_ring.is_unit_unit_continuous {f : C(X, R)} (h : âˆ€ x, is_unit (f x)) :
  continuous (Î» x, (h x).unit) :=
begin
  refine continuous_induced_rng.2 (continuous.prod_mk f.continuous
    (mul_opposite.continuous_op.comp (continuous_iff_continuous_at.mpr (Î» x, _)))),
  have := normed_ring.inverse_continuous_at (h x).unit,
  simp only [â†ring.inverse_unit, is_unit.unit_spec, â†function.comp_apply] at this âŠ¢,
  exact this.comp (f.continuous_at x),
end

/-- Construct a continuous map into the group of units of a normed ring from a function into the
normed ring and a proof that every element of the range is a unit. -/
@[simps]
noncomputable def units_of_forall_is_unit {f : C(X, R)} (h : âˆ€ x, is_unit (f x)) : C(X, RË£) :=
{ to_fun := Î» x, (h x).unit,
  continuous_to_fun :=  normed_ring.is_unit_unit_continuous h }

instance can_lift : can_lift C(X, R) C(X, RË£)
  (Î» f, âŸ¨Î» x, f x, units.continuous_coe.comp f.continuousâŸ©) (Î» f, âˆ€ x, is_unit (f x)) :=
{ prf := Î» f h, âŸ¨units_of_forall_is_unit h, by { ext, refl }âŸ© }

lemma is_unit_iff_forall_is_unit (f : C(X, R)) :
  is_unit f â†” âˆ€ x, is_unit (f x) :=
iff.intro (Î» h, Î» x, âŸ¨units_lift.symm h.unit x, rflâŸ©)
  (Î» h, âŸ¨(units_of_forall_is_unit h).units_lift, by { ext, refl }âŸ©)

end normed_ring

section normed_field

variables [normed_field ğ•œ] [complete_space ğ•œ]

lemma is_unit_iff_forall_ne_zero (f : C(X, ğ•œ)) :
  is_unit f â†” âˆ€ x, f x â‰  0 :=
by simp_rw [f.is_unit_iff_forall_is_unit, is_unit_iff_ne_zero]

lemma spectrum_eq_range (f : C(X, ğ•œ)) :
  spectrum ğ•œ f = set.range f :=
begin
  ext,
  simp only [spectrum.mem_iff, is_unit_iff_forall_ne_zero, not_forall, coe_sub,
    pi.sub_apply, algebra_map_apply, algebra.id.smul_eq_mul, mul_one, not_not, set.mem_range,
    sub_eq_zero, @eq_comm _ x _]
end

end normed_field

end continuous_map
