/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.constructions

/-!
# Neighborhoods and continuity relative to a subset

This file defines relative versions

`nhds_within`           of `nhds`
`continuous_on`         of `continuous`
`continuous_within_at`  of `continuous_at`

and proves their basic properties, including the relationships between
these restricted notions and the corresponding notions for the subtype
equipped with the subspace topology.

-/

open set filter
open_locale topological_space filter

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}
variables [topological_space Î±]

/-- The "neighborhood within" filter. Elements of `nhds_within a s` are sets containing the
intersection of `s` and a neighborhood of `a`. -/
def nhds_within (a : Î±) (s : set Î±) : filter Î± := ğ“ a âŠ“ ğ“Ÿ s

theorem nhds_within_eq (a : Î±) (s : set Î±) :
  nhds_within a s = â¨… t âˆˆ {t : set Î± | a âˆˆ t âˆ§ is_open t}, ğ“Ÿ (t âˆ© s) :=
have set.univ âˆˆ {s : set Î± | a âˆˆ s âˆ§ is_open s}, from âŸ¨set.mem_univ _, is_open_univâŸ©,
begin
  rw [nhds_within, nhds, binfi_inf]; try { exact this },
  simp only [inf_principal]
end

theorem nhds_within_univ (a : Î±) : nhds_within a set.univ = ğ“ a :=
by rw [nhds_within, principal_univ, inf_top_eq]

lemma nhds_within_has_basis {p : Î² â†’ Prop} {s : Î² â†’ set Î±} {a : Î±} (h : (ğ“ a).has_basis p s)
  (t : set Î±) :
  (nhds_within a t).has_basis p (Î» i, s i âˆ© t) :=
h.inf_principal t

lemma nhds_within_basis_open (a : Î±) (t : set Î±) :
  (nhds_within a t).has_basis (Î» u, a âˆˆ u âˆ§ is_open u) (Î» u, u âˆ© t) :=
nhds_within_has_basis (nhds_basis_opens a) t

theorem mem_nhds_within {t : set Î±} {a : Î±} {s : set Î±} :
  t âˆˆ nhds_within a s â†” âˆƒ u, is_open u âˆ§ a âˆˆ u âˆ§ u âˆ© s âŠ† t  :=
by simpa only [exists_prop, and_assoc, and_comm] using (nhds_within_basis_open a s).mem_iff

lemma mem_nhds_within_iff_exists_mem_nhds_inter {t : set Î±} {a : Î±} {s : set Î±} :
  t âˆˆ nhds_within a s â†” âˆƒ u âˆˆ ğ“ a, u âˆ© s âŠ† t :=
(nhds_within_has_basis (ğ“ a).basis_sets s).mem_iff

lemma nhds_of_nhds_within_of_nhds
  {s t : set Î±} {a : Î±} (h1 : s âˆˆ ğ“ a) (h2 : t âˆˆ nhds_within a s) : (t âˆˆ ğ“ a) :=
begin
  rcases mem_nhds_within_iff_exists_mem_nhds_inter.mp h2 with âŸ¨_, Hw, hwâŸ©,
  exact (nhds a).sets_of_superset ((nhds a).inter_sets Hw h1) hw,
end

lemma mem_nhds_within_of_mem_nhds {s t : set Î±} {a : Î±} (h : s âˆˆ ğ“ a) :
  s âˆˆ nhds_within a t :=
mem_inf_sets_of_left h

theorem self_mem_nhds_within {a : Î±} {s : set Î±} : s âˆˆ nhds_within a s :=
mem_inf_sets_of_right (mem_principal_self s)

theorem inter_mem_nhds_within (s : set Î±) {t : set Î±} {a : Î±} (h : t âˆˆ ğ“ a) :
  s âˆ© t âˆˆ nhds_within a s :=
inter_mem_sets (mem_inf_sets_of_right (mem_principal_self s)) (mem_inf_sets_of_left h)

theorem nhds_within_mono (a : Î±) {s t : set Î±} (h : s âŠ† t) : nhds_within a s â‰¤ nhds_within a t :=
inf_le_inf_left _ (principal_mono.mpr h)

lemma mem_of_mem_nhds_within {a : Î±} {s t : set Î±} (ha : a âˆˆ s) (ht : t âˆˆ nhds_within a s) :
  a âˆˆ t :=
let âŸ¨u, hu, HâŸ© := mem_nhds_within.1 ht in H.2 âŸ¨H.1, haâŸ©

lemma filter.eventually.self_of_nhds_within {p : Î± â†’ Prop} {s : set Î±} {x : Î±}
  (h : âˆ€á¶  y in nhds_within x s, p y) (hx : x âˆˆ s) : p x :=
mem_of_mem_nhds_within hx h

theorem nhds_within_restrict'' {a : Î±} (s : set Î±) {t : set Î±} (h : t âˆˆ nhds_within a s) :
  nhds_within a s = nhds_within a (s âˆ© t) :=
le_antisymm
  (le_inf inf_le_left (le_principal_iff.mpr (inter_mem_sets self_mem_nhds_within h)))
  (inf_le_inf_left _ (principal_mono.mpr (set.inter_subset_left _ _)))

theorem nhds_within_restrict' {a : Î±} (s : set Î±) {t : set Î±} (h : t âˆˆ ğ“ a) :
  nhds_within a s = nhds_within a (s âˆ© t) :=
nhds_within_restrict'' s $ mem_inf_sets_of_left h

theorem nhds_within_restrict {a : Î±} (s : set Î±) {t : set Î±} (hâ‚€ : a âˆˆ t) (hâ‚ : is_open t) :
  nhds_within a s = nhds_within a (s âˆ© t) :=
nhds_within_restrict' s (mem_nhds_sets hâ‚ hâ‚€)

theorem nhds_within_le_of_mem {a : Î±} {s t : set Î±} (h : s âˆˆ nhds_within a t) :
  nhds_within a t â‰¤ nhds_within a s :=
begin
  rcases mem_nhds_within.1 h with âŸ¨u, u_open, au, utsâŸ©,
  have : nhds_within a t = nhds_within a (t âˆ© u) := nhds_within_restrict _ au u_open,
  rw [this, inter_comm],
  exact nhds_within_mono _ uts
end

theorem nhds_within_eq_nhds_within {a : Î±} {s t u : set Î±}
    (hâ‚€ : a âˆˆ s) (hâ‚ : is_open s) (hâ‚‚ : t âˆ© s = u âˆ© s) :
  nhds_within a t = nhds_within a u :=
by rw [nhds_within_restrict t hâ‚€ hâ‚, nhds_within_restrict u hâ‚€ hâ‚, hâ‚‚]

theorem nhds_within_eq_of_open {a : Î±} {s : set Î±} (hâ‚€ : a âˆˆ s) (hâ‚ : is_open s) :
  nhds_within a s = ğ“ a :=
by rw [â†nhds_within_univ]; apply nhds_within_eq_nhds_within hâ‚€ hâ‚;
     rw [set.univ_inter, set.inter_self]

@[simp] theorem nhds_within_empty (a : Î±) : nhds_within a {} = âŠ¥ :=
by rw [nhds_within, principal_empty, inf_bot_eq]

theorem nhds_within_union (a : Î±) (s t : set Î±) :
  nhds_within a (s âˆª t) = nhds_within a s âŠ” nhds_within a t :=
by unfold nhds_within; rw [â†inf_sup_left, sup_principal]

theorem nhds_within_inter (a : Î±) (s t : set Î±) :
  nhds_within a (s âˆ© t) = nhds_within a s âŠ“ nhds_within a t :=
by unfold nhds_within; rw [inf_left_comm, inf_assoc, inf_principal,
                             â†inf_assoc, inf_idem]

theorem nhds_within_inter' (a : Î±) (s t : set Î±) :
  nhds_within a (s âˆ© t) = (nhds_within a s) âŠ“ ğ“Ÿ t :=
by { unfold nhds_within, rw [â†inf_principal, inf_assoc] }

lemma nhds_within_prod_eq {Î± : Type*} [topological_space Î±] {Î² : Type*} [topological_space Î²]
  (a : Î±) (b : Î²) (s : set Î±) (t : set Î²) :
  nhds_within (a, b) (s.prod t) = (nhds_within a s).prod (nhds_within b t) :=
by { unfold nhds_within, rw [nhds_prod_eq, â†filter.prod_inf_prod, filter.prod_principal_principal] }

theorem tendsto_if_nhds_within {f g : Î± â†’ Î²} {p : Î± â†’ Prop} [decidable_pred p]
    {a : Î±} {s : set Î±} {l : filter Î²}
    (hâ‚€ : tendsto f (nhds_within a (s âˆ© p)) l)
    (hâ‚ : tendsto g (nhds_within a (s âˆ© {x | Â¬ p x})) l) :
  tendsto (Î» x, if p x then f x else g x) (nhds_within a s) l :=
by apply tendsto_if; rw [â†nhds_within_inter']; assumption

lemma map_nhds_within (f : Î± â†’ Î²) (a : Î±) (s : set Î±) :
  map f (nhds_within a s) =
    â¨… t âˆˆ {t : set Î± | a âˆˆ t âˆ§ is_open t}, ğ“Ÿ (set.image f (t âˆ© s)) :=
((nhds_within_basis_open a s).map f).eq_binfi

theorem tendsto_nhds_within_mono_left {f : Î± â†’ Î²} {a : Î±}
    {s t : set Î±} {l : filter Î²} (hst : s âŠ† t) (h : tendsto f (nhds_within a t) l) :
  tendsto f (nhds_within a s) l :=
tendsto_le_left (nhds_within_mono a hst) h

theorem tendsto_nhds_within_mono_right {f : Î² â†’ Î±} {l : filter Î²}
    {a : Î±} {s t : set Î±} (hst : s âŠ† t) (h : tendsto f l (nhds_within a s)) :
  tendsto f l (nhds_within a t) :=
tendsto_le_right (nhds_within_mono a hst) h

theorem tendsto_nhds_within_of_tendsto_nhds {f : Î± â†’ Î²} {a : Î±}
    {s : set Î±} {l : filter Î²} (h : tendsto f (ğ“ a) l) :
  tendsto f (nhds_within a s) l :=
by rw [â†nhds_within_univ] at h; exact tendsto_nhds_within_mono_left (set.subset_univ _) h

theorem principal_subtype {Î± : Type*} (s : set Î±) (t : set {x // x âˆˆ s}) :
  ğ“Ÿ t = comap coe (ğ“Ÿ ((coe : s â†’ Î±) '' t)) :=
by rw comap_principal; rw set.preimage_image_eq; apply subtype.coe_injective

lemma mem_closure_iff_nhds_within_ne_bot {s : set Î±} {x : Î±} :
  x âˆˆ closure s â†” nhds_within x s â‰  âŠ¥ :=
mem_closure_iff_nhds.trans (nhds_within_has_basis (ğ“ x).basis_sets s).forall_nonempty_iff_ne_bot

lemma nhds_within_ne_bot_of_mem {s : set Î±} {x : Î±} (hx : x âˆˆ s) :
  nhds_within x s â‰  âŠ¥ :=
mem_closure_iff_nhds_within_ne_bot.1 $ subset_closure hx

lemma is_closed.mem_of_nhds_within_ne_bot {s : set Î±} (hs : is_closed s)
  {x : Î±} (hx : nhds_within x s â‰  âŠ¥) : x âˆˆ s :=
by simpa only [hs.closure_eq] using mem_closure_iff_nhds_within_ne_bot.2 hx

/-
nhds_within and subtypes
-/

theorem mem_nhds_within_subtype (s : set Î±) (a : {x // x âˆˆ s}) (t u : set {x // x âˆˆ s}) :
  t âˆˆ nhds_within a u â†”
    t âˆˆ comap (coe : s â†’ Î±) (nhds_within a (coe '' u)) :=
by rw [nhds_within, nhds_subtype, principal_subtype, â†comap_inf, â†nhds_within]

theorem nhds_within_subtype (s : set Î±) (a : {x // x âˆˆ s}) (t : set {x // x âˆˆ s}) :
  nhds_within a t = comap (coe : s â†’ Î±) (nhds_within a (coe '' t)) :=
filter_eq $ by ext u; rw mem_nhds_within_subtype

theorem nhds_within_eq_map_subtype_coe {s : set Î±} {a : Î±} (h : a âˆˆ s) :
  nhds_within a s = map (coe : s â†’ Î±) (ğ“ âŸ¨a, hâŸ©) :=
have hâ‚€ : s âˆˆ nhds_within a s,
  by { rw [mem_nhds_within], existsi set.univ, simp [set.diff_eq] },
have hâ‚ : âˆ€ y âˆˆ s, âˆƒ x : s, â†‘x = y,
  from Î» y h, âŸ¨âŸ¨y, hâŸ©, rflâŸ©,
begin
  rw [â†nhds_within_univ, nhds_within_subtype, subtype.coe_image_univ],
  exact (map_comap_of_surjective' hâ‚€ hâ‚).symm,
end

theorem tendsto_nhds_within_iff_subtype {s : set Î±} {a : Î±} (h : a âˆˆ s) (f : Î± â†’ Î²) (l : filter Î²) :
  tendsto f (nhds_within a s) l â†” tendsto (s.restrict f) (ğ“ âŸ¨a, hâŸ©) l :=
by { simp only [tendsto, nhds_within_eq_map_subtype_coe h, filter.map_map], refl }

variables [topological_space Î²] [topological_space Î³] [topological_space Î´]

/-- A function between topological spaces is continuous at a point `xâ‚€` within a subset `s`
if `f x` tends to `f xâ‚€` when `x` tends to `xâ‚€` while staying within `s`. -/
def continuous_within_at (f : Î± â†’ Î²) (s : set Î±) (x : Î±) : Prop :=
tendsto f (nhds_within x s) (ğ“ (f x))

/-- If a function is continuous within `s` at `x`, then it tends to `f x` within `s` by definition.
We register this fact for use with the dot notation, especially to use `tendsto.comp` as
`continuous_within_at.comp` will have a different meaning. -/
lemma continuous_within_at.tendsto {f : Î± â†’ Î²} {s : set Î±} {x : Î±} (h : continuous_within_at f s x) :
  tendsto f (nhds_within x s) (ğ“ (f x)) := h

/-- A function between topological spaces is continuous on a subset `s`
when it's continuous at every point of `s` within `s`. -/
def continuous_on (f : Î± â†’ Î²) (s : set Î±) : Prop := âˆ€ x âˆˆ s, continuous_within_at f s x

lemma continuous_on.continuous_within_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±} (hf : continuous_on f s)
  (hx : x âˆˆ s) : continuous_within_at f s x :=
hf x hx

theorem continuous_within_at_univ (f : Î± â†’ Î²) (x : Î±) :
  continuous_within_at f set.univ x â†” continuous_at f x :=
by rw [continuous_at, continuous_within_at, nhds_within_univ]

theorem continuous_within_at_iff_continuous_at_restrict (f : Î± â†’ Î²) {x : Î±} {s : set Î±} (h : x âˆˆ s) :
  continuous_within_at f s x â†” continuous_at (s.restrict f) âŸ¨x, hâŸ© :=
tendsto_nhds_within_iff_subtype h f _

theorem continuous_within_at.tendsto_nhds_within_image {f : Î± â†’ Î²} {x : Î±} {s : set Î±}
  (h : continuous_within_at f s x) :
  tendsto f (nhds_within x s) (nhds_within (f x) (f '' s)) :=
tendsto_inf.2 âŸ¨h, tendsto_principal.2 $
  mem_inf_sets_of_right $ mem_principal_sets.2 $
  Î» x, mem_image_of_mem _âŸ©

lemma continuous_within_at.prod_map {f : Î± â†’ Î³} {g : Î² â†’ Î´} {s : set Î±} {t : set Î²}
  {x : Î±} {y : Î²}
  (hf : continuous_within_at f s x) (hg : continuous_within_at g t y) :
  continuous_within_at (prod.map f g) (s.prod t) (x, y) :=
begin
  unfold continuous_within_at at *,
  rw [nhds_within_prod_eq, prod.map, nhds_prod_eq],
  exact hf.prod_map hg,
end

theorem continuous_on_iff {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†” âˆ€ x âˆˆ s, âˆ€ t : set Î², is_open t â†’ f x âˆˆ t â†’ âˆƒ u, is_open u âˆ§ x âˆˆ u âˆ§
    u âˆ© s âŠ† f â»Â¹' t :=
by simp only [continuous_on, continuous_within_at, tendsto_nhds, mem_nhds_within]

theorem continuous_on_iff_continuous_restrict {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†” continuous (s.restrict f) :=
begin
  rw [continuous_on, continuous_iff_continuous_at], split,
  { rintros h âŸ¨x, xsâŸ©,
    exact (continuous_within_at_iff_continuous_at_restrict f xs).mp (h x xs) },
  intros h x xs,
  exact (continuous_within_at_iff_continuous_at_restrict f xs).mpr (h âŸ¨x, xsâŸ©)
end

theorem continuous_on_iff' {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†” âˆ€ t : set Î², is_open t â†’ âˆƒ u, is_open u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s :=
have âˆ€ t, is_open (s.restrict f â»Â¹' t) â†” âˆƒ (u : set Î±), is_open u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s,
  begin
    intro t,
    rw [is_open_induced_iff, set.restrict_eq, set.preimage_comp],
    simp only [subtype.preimage_coe_eq_preimage_coe_iff],
    split; { rintros âŸ¨u, ou, useqâŸ©, exact âŸ¨u, ou, useq.symmâŸ© }
  end,
by rw [continuous_on_iff_continuous_restrict, continuous]; simp only [this]

theorem continuous_on_iff_is_closed {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†” âˆ€ t : set Î², is_closed t â†’ âˆƒ u, is_closed u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s :=
have âˆ€ t, is_closed (s.restrict f â»Â¹' t) â†” âˆƒ (u : set Î±), is_closed u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s,
  begin
    intro t,
    rw [is_closed_induced_iff, set.restrict_eq, set.preimage_comp],
    simp only [subtype.preimage_coe_eq_preimage_coe_iff]
  end,
by rw [continuous_on_iff_continuous_restrict, continuous_iff_is_closed]; simp only [this]

lemma continuous_on.prod_map {f : Î± â†’ Î³} {g : Î² â†’ Î´} {s : set Î±} {t : set Î²}
  (hf : continuous_on f s) (hg : continuous_on g t) :
  continuous_on (prod.map f g) (s.prod t) :=
Î» âŸ¨x, yâŸ© âŸ¨hx, hyâŸ©, continuous_within_at.prod_map (hf x hx) (hg y hy)

lemma continuous_on_empty (f : Î± â†’ Î²) : continuous_on f âˆ… :=
Î» x, false.elim

theorem nhds_within_le_comap {x : Î±} {s : set Î±} {f : Î± â†’ Î²} (ctsf : continuous_within_at f s x) :
  nhds_within x s â‰¤ comap f (nhds_within (f x) (f '' s)) :=
map_le_iff_le_comap.1 ctsf.tendsto_nhds_within_image

theorem continuous_within_at_iff_ptendsto_res (f : Î± â†’ Î²) {x : Î±} {s : set Î±} :
  continuous_within_at f s x â†” ptendsto (pfun.res f s) (ğ“ x) (ğ“ (f x)) :=
tendsto_iff_ptendsto _ _ _ _

lemma continuous_iff_continuous_on_univ {f : Î± â†’ Î²} : continuous f â†” continuous_on f univ :=
by simp [continuous_iff_continuous_at, continuous_on, continuous_at, continuous_within_at,
         nhds_within_univ]

lemma continuous_within_at.mono {f : Î± â†’ Î²} {s t : set Î±} {x : Î±} (h : continuous_within_at f t x)
  (hs : s âŠ† t) : continuous_within_at f s x :=
tendsto_le_left (nhds_within_mono x hs) h

lemma continuous_within_at_inter' {f : Î± â†’ Î²} {s t : set Î±} {x : Î±} (h : t âˆˆ nhds_within x s) :
  continuous_within_at f (s âˆ© t) x â†” continuous_within_at f s x :=
by simp [continuous_within_at, nhds_within_restrict'' s h]

lemma continuous_within_at_inter {f : Î± â†’ Î²} {s t : set Î±} {x : Î±} (h : t âˆˆ ğ“ x) :
  continuous_within_at f (s âˆ© t) x â†” continuous_within_at f s x :=
by simp [continuous_within_at, nhds_within_restrict' s h]

lemma continuous_within_at.union {f : Î± â†’ Î²} {s t : set Î±} {x : Î±}
  (hs : continuous_within_at f s x) (ht : continuous_within_at f t x) :
  continuous_within_at f (s âˆª t) x :=
by simp only [continuous_within_at, nhds_within_union, tendsto, map_sup, sup_le_iff.2 âŸ¨hs, htâŸ©]

lemma continuous_within_at.mem_closure_image  {f : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (hx : x âˆˆ closure s) : f x âˆˆ closure (f '' s) :=
mem_closure_of_tendsto (mem_closure_iff_nhds_within_ne_bot.1 hx) h $
mem_sets_of_superset self_mem_nhds_within (subset_preimage_image f s)

lemma continuous_within_at.mem_closure {f : Î± â†’ Î²} {s : set Î±} {x : Î±} {A : set Î²}
  (h : continuous_within_at f s x) (hx : x âˆˆ closure s) (hA : s âŠ† fâ»Â¹' A) : f x âˆˆ closure A :=
closure_mono (image_subset_iff.2 hA) (h.mem_closure_image hx)

lemma continuous_within_at.image_closure {f : Î± â†’ Î²} {s : set Î±}
  (hf : âˆ€ x âˆˆ closure s, continuous_within_at f s x) :
  f '' (closure s) âŠ† closure (f '' s) :=
begin
  rintros _ âŸ¨x, hx, rflâŸ©,
  exact (hf x hx).mem_closure_image hx
end

theorem is_open_map.continuous_on_image_of_left_inv_on {f : Î± â†’ Î²} {s : set Î±}
  (h : is_open_map (s.restrict f)) {finv : Î² â†’ Î±} (hleft : left_inv_on finv f s) :
  continuous_on finv (f '' s) :=
begin
  rintros _ âŸ¨x, xs, rflâŸ© t ht,
  rw [hleft xs] at ht,
  replace h := h.nhds_le âŸ¨x, xsâŸ©,
  apply mem_nhds_within_of_mem_nhds,
  apply h,
  erw [map_compose.symm, function.comp, mem_map, â† nhds_within_eq_map_subtype_coe],
  apply mem_sets_of_superset (inter_mem_nhds_within _ ht),
  assume y hy,
  rw [mem_set_of_eq, mem_preimage, hleft hy.1],
  exact hy.2
end

theorem is_open_map.continuous_on_range_of_left_inverse {f : Î± â†’ Î²} (hf : is_open_map f)
  {finv : Î² â†’ Î±} (hleft : function.left_inverse finv f) :
  continuous_on finv (range f) :=
begin
  rw [â† image_univ],
  exact (hf.restrict is_open_univ).continuous_on_image_of_left_inv_on (Î» x _, hleft x)
end

lemma continuous_on.congr_mono {f g : Î± â†’ Î²} {s sâ‚ : set Î±} (h : continuous_on f s)
  (h' : eq_on g f sâ‚) (hâ‚ : sâ‚ âŠ† s) : continuous_on g sâ‚ :=
begin
  assume x hx,
  unfold continuous_within_at,
  have A := (h x (hâ‚ hx)).mono hâ‚,
  unfold continuous_within_at at A,
  rw â† h' hx at A,
  have : (g =á¶ [nhds_within x sâ‚] f) := mem_inf_sets_of_right h',
  exact A.congr' this.symm
end

lemma continuous_on.congr {f g : Î± â†’ Î²} {s : set Î±} (h : continuous_on f s) (h' : eq_on g f s) :
  continuous_on g s :=
h.congr_mono h' (subset.refl _)

lemma continuous_on_congr {f g : Î± â†’ Î²} {s : set Î±} (h' : eq_on g f s) :
  continuous_on g s â†” continuous_on f s :=
âŸ¨Î» h, continuous_on.congr h h'.symm, Î» h, h.congr h'âŸ©

lemma continuous_at.continuous_within_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±} (h : continuous_at f x) :
  continuous_within_at f s x :=
continuous_within_at.mono ((continuous_within_at_univ f x).2 h) (subset_univ _)

lemma continuous_within_at.continuous_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (hs : s âˆˆ ğ“ x) : continuous_at f x :=
begin
  have : s = univ âˆ© s, by rw univ_inter,
  rwa [this, continuous_within_at_inter hs, continuous_within_at_univ] at h
end

lemma continuous_on.continuous_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_on f s) (hx : s âˆˆ ğ“ x) : continuous_at f x :=
(h x (mem_of_nhds hx)).continuous_at hx

lemma continuous_within_at.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±} {t : set Î²} {x : Î±}
  (hg : continuous_within_at g t (f x)) (hf : continuous_within_at f s x) (h : s âŠ† f â»Â¹' t) :
  continuous_within_at (g âˆ˜ f) s x :=
begin
  have : tendsto f (ğ“Ÿ s) (ğ“Ÿ t),
    by { rw tendsto_principal_principal, exact Î»x hx, h hx },
  have : tendsto f (nhds_within x s) (ğ“Ÿ t) :=
    tendsto_le_left inf_le_right this,
  have : tendsto f (nhds_within x s) (nhds_within (f x) t) :=
    tendsto_inf.2 âŸ¨hf, thisâŸ©,
  exact tendsto.comp hg this
end

lemma continuous_on.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hg : continuous_on g t) (hf : continuous_on f s) (h : s âŠ† f â»Â¹' t) :
  continuous_on (g âˆ˜ f) s :=
Î»x hx, continuous_within_at.comp (hg _ (h hx)) (hf x hx) h

lemma continuous_on.mono {f : Î± â†’ Î²} {s t : set Î±} (hf : continuous_on f s) (h : t âŠ† s)  :
  continuous_on f t :=
Î»x hx, tendsto_le_left (nhds_within_mono _ h) (hf x (h hx))

lemma continuous.continuous_on {f : Î± â†’ Î²} {s : set Î±} (h : continuous f) :
  continuous_on f s :=
begin
  rw continuous_iff_continuous_on_univ at h,
  exact h.mono (subset_univ _)
end

lemma continuous.continuous_within_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±} (h : continuous f) :
  continuous_within_at f s x :=
tendsto_le_left inf_le_left (h.tendsto x)

lemma continuous.comp_continuous_on {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±}
  (hg : continuous g) (hf : continuous_on f s) :
  continuous_on (g âˆ˜ f) s :=
hg.continuous_on.comp hf subset_preimage_univ

lemma continuous_within_at.preimage_mem_nhds_within {f : Î± â†’ Î²} {x : Î±} {s : set Î±} {t : set Î²}
  (h : continuous_within_at f s x) (ht : t âˆˆ ğ“ (f x)) : f â»Â¹' t âˆˆ nhds_within x s :=
h ht

lemma continuous_within_at.preimage_mem_nhds_within' {f : Î± â†’ Î²} {x : Î±} {s : set Î±} {t : set Î²}
  (h : continuous_within_at f s x) (ht : t âˆˆ nhds_within (f x) (f '' s)) :
  f â»Â¹' t âˆˆ nhds_within x s :=
begin
  rw mem_nhds_within at ht,
  rcases ht with âŸ¨u, u_open, fxu, huâŸ©,
  have : f â»Â¹' u âˆ© s âˆˆ nhds_within x s :=
    filter.inter_mem_sets (h (mem_nhds_sets u_open fxu)) self_mem_nhds_within,
  apply mem_sets_of_superset this,
  calc f â»Â¹' u âˆ© s
    âŠ† f â»Â¹' u âˆ© f â»Â¹' (f '' s) : inter_subset_inter_right _ (subset_preimage_image f s)
    ... = f â»Â¹' (u âˆ© f '' s) : rfl
    ... âŠ† f â»Â¹' t : preimage_mono hu
end

lemma continuous_within_at.congr_of_mem_nhds_within {f fâ‚ : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (hâ‚ : fâ‚ =á¶ [nhds_within x s] f) (hx : fâ‚ x = f x) :
  continuous_within_at fâ‚ s x :=
by rwa [continuous_within_at, filter.tendsto, hx, filter.map_congr hâ‚]

lemma continuous_within_at.congr {f fâ‚ : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (hâ‚ : âˆ€yâˆˆs, fâ‚ y = f y) (hx : fâ‚ x = f x) :
  continuous_within_at fâ‚ s x :=
h.congr_of_mem_nhds_within (mem_sets_of_superset self_mem_nhds_within hâ‚) hx

lemma continuous_on_const {s : set Î±} {c : Î²} : continuous_on (Î»x, c) s :=
continuous_const.continuous_on

lemma continuous_within_at_const {b : Î²} {s : set Î±} {x : Î±} :
  continuous_within_at (Î» _:Î±, b) s x :=
continuous_const.continuous_within_at

lemma continuous_on_id {s : set Î±} : continuous_on id s :=
continuous_id.continuous_on

lemma continuous_within_at_id {s : set Î±} {x : Î±} : continuous_within_at id s x :=
continuous_id.continuous_within_at

lemma continuous_on_open_iff {f : Î± â†’ Î²} {s : set Î±} (hs : is_open s) :
  continuous_on f s â†” (âˆ€t, is_open t â†’ is_open (s âˆ© fâ»Â¹' t)) :=
begin
  rw continuous_on_iff',
  split,
  { assume h t ht,
    rcases h t ht with âŸ¨u, u_open, huâŸ©,
    rw [inter_comm, hu],
    apply is_open_inter u_open hs },
  { assume h t ht,
    refine âŸ¨s âˆ© f â»Â¹' t, h t ht, _âŸ©,
    rw [@inter_comm _ s (f â»Â¹' t), inter_assoc, inter_self] }
end

lemma continuous_on.preimage_open_of_open {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hf : continuous_on f s) (hs : is_open s) (ht : is_open t) : is_open (s âˆ© fâ»Â¹' t) :=
(continuous_on_open_iff hs).1 hf t ht

lemma continuous_on.preimage_closed_of_closed {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hf : continuous_on f s) (hs : is_closed s) (ht : is_closed t) : is_closed (s âˆ© fâ»Â¹' t) :=
begin
  rcases continuous_on_iff_is_closed.1 hf t ht with âŸ¨u, huâŸ©,
  rw [inter_comm, hu.2],
  apply is_closed_inter hu.1 hs
end

lemma continuous_on.preimage_interior_subset_interior_preimage {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hf : continuous_on f s) (hs : is_open s) : s âˆ© fâ»Â¹' (interior t) âŠ† s âˆ© interior (fâ»Â¹' t) :=
calc s âˆ© f â»Â¹' (interior t) âŠ† interior (s âˆ© f â»Â¹' t) :
  interior_maximal (inter_subset_inter (subset.refl _) (preimage_mono interior_subset))
    (hf.preimage_open_of_open hs is_open_interior)
... = s âˆ© interior (f â»Â¹' t) : by rw [interior_inter, interior_eq_of_open hs]

lemma continuous_on_of_locally_continuous_on {f : Î± â†’ Î²} {s : set Î±}
  (h : âˆ€xâˆˆs, âˆƒt, is_open t âˆ§ x âˆˆ t âˆ§ continuous_on f (s âˆ© t)) : continuous_on f s :=
begin
  assume x xs,
  rcases h x xs with âŸ¨t, open_t, xt, ctâŸ©,
  have := ct x âŸ¨xs, xtâŸ©,
  rwa [continuous_within_at, â† nhds_within_restrict _ xt open_t] at this
end

lemma continuous_on_open_of_generate_from {Î² : Type*} {s : set Î±} {T : set (set Î²)} {f : Î± â†’ Î²}
  (hs : is_open s) (h : âˆ€t âˆˆ T, is_open (s âˆ© fâ»Â¹' t)) :
  @continuous_on Î± Î² _ (topological_space.generate_from T) f s :=
begin
  rw continuous_on_open_iff,
  assume t ht,
  induction ht with u hu u v Tu Tv hu hv U hU hU',
  { exact h u hu },
  { simp only [preimage_univ, inter_univ], exact hs },
  { have : s âˆ© f â»Â¹' (u âˆ© v) = (s âˆ© f â»Â¹' u) âˆ© (s âˆ© f â»Â¹' v),
      by { ext x, simp, split, finish, finish },
    rw this,
    exact is_open_inter hu hv },
  { rw [preimage_sUnion, inter_bUnion],
    exact is_open_bUnion hU' },
  { exact hs }
end

lemma continuous_within_at.prod {f : Î± â†’ Î²} {g : Î± â†’ Î³} {s : set Î±} {x : Î±}
  (hf : continuous_within_at f s x) (hg : continuous_within_at g s x) :
  continuous_within_at (Î»x, (f x, g x)) s x :=
hf.prod_mk_nhds hg

lemma continuous_on.prod {f : Î± â†’ Î²} {g : Î± â†’ Î³} {s : set Î±}
  (hf : continuous_on f s) (hg : continuous_on g s) : continuous_on (Î»x, (f x, g x)) s :=
Î»x hx, continuous_within_at.prod (hf x hx) (hg x hx)
