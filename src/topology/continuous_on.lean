/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.constructions

/-!
# Neighborhoods and continuity relative to a subset

This file defines relative versions

* `nhds_within`           of `nhds`
* `continuous_on`         of `continuous`
* `continuous_within_at`  of `continuous_at`

and proves their basic properties, including the relationships between
these restricted notions and the corresponding notions for the subtype
equipped with the subspace topology.

## Notation

* `ğ“ x`: the filter of neighborhoods of a point `x`;
* `ğ“Ÿ s`: the principal filter of a set `s`;
* `ğ“[s] x`: the filter `nhds_within x s` of neighborhoods of a point `x` within a set `s`.

-/

open set filter
open_locale topological_space filter

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}
variables [topological_space Î±]

@[simp] lemma nhds_bind_nhds_within {a : Î±} {s : set Î±} :
  (ğ“ a).bind (Î» x, ğ“[s] x) = ğ“[s] a :=
bind_inf_principal.trans $ congr_arg2 _ nhds_bind_nhds rfl

@[simp] lemma eventually_nhds_nhds_within {a : Î±} {s : set Î±} {p : Î± â†’ Prop} :
  (âˆ€á¶  y in ğ“ a, âˆ€á¶  x in ğ“[s] y, p x) â†” âˆ€á¶  x in ğ“[s] a, p x :=
filter.ext_iff.1 nhds_bind_nhds_within {x | p x}

lemma eventually_nhds_within_iff {a : Î±} {s : set Î±} {p : Î± â†’ Prop} :
  (âˆ€á¶  x in ğ“[s] a, p x) â†” âˆ€á¶  x in ğ“ a, x âˆˆ s â†’ p x :=
eventually_inf_principal

@[simp] lemma eventually_nhds_within_nhds_within {a : Î±} {s : set Î±} {p : Î± â†’ Prop} :
  (âˆ€á¶  y in ğ“[s] a, âˆ€á¶  x in ğ“[s] y, p x) â†” âˆ€á¶  x in ğ“[s] a, p x :=
begin
  refine âŸ¨Î» h, _, Î» h, (eventually_nhds_nhds_within.2 h).filter_mono inf_le_leftâŸ©,
  simp only [eventually_nhds_within_iff] at h âŠ¢,
  exact h.mono (Î» x hx hxs, (hx hxs).self_of_nhds hxs)
end

theorem nhds_within_eq (a : Î±) (s : set Î±) :
  ğ“[s] a = â¨… t âˆˆ {t : set Î± | a âˆˆ t âˆ§ is_open t}, ğ“Ÿ (t âˆ© s) :=
((nhds_basis_opens a).inf_principal s).eq_binfi

theorem nhds_within_univ (a : Î±) : ğ“[set.univ] a = ğ“ a :=
by rw [nhds_within, principal_univ, inf_top_eq]

lemma nhds_within_has_basis {p : Î² â†’ Prop} {s : Î² â†’ set Î±} {a : Î±} (h : (ğ“ a).has_basis p s)
  (t : set Î±) :
  (ğ“[t] a).has_basis p (Î» i, s i âˆ© t) :=
h.inf_principal t

lemma nhds_within_basis_open (a : Î±) (t : set Î±) :
  (ğ“[t] a).has_basis (Î» u, a âˆˆ u âˆ§ is_open u) (Î» u, u âˆ© t) :=
nhds_within_has_basis (nhds_basis_opens a) t

theorem mem_nhds_within {t : set Î±} {a : Î±} {s : set Î±} :
  t âˆˆ ğ“[s] a â†” âˆƒ u, is_open u âˆ§ a âˆˆ u âˆ§ u âˆ© s âŠ† t  :=
by simpa only [exists_prop, and_assoc, and_comm] using (nhds_within_basis_open a s).mem_iff

lemma mem_nhds_within_iff_exists_mem_nhds_inter {t : set Î±} {a : Î±} {s : set Î±} :
  t âˆˆ ğ“[s] a â†” âˆƒ u âˆˆ ğ“ a, u âˆ© s âŠ† t :=
(nhds_within_has_basis (ğ“ a).basis_sets s).mem_iff

lemma diff_mem_nhds_within_compl {x : Î±} {s : set Î±} (hs : s âˆˆ ğ“ x) (t : set Î±) :
  s \ t âˆˆ ğ“[tá¶œ] x :=
diff_mem_inf_principal_compl hs t

lemma diff_mem_nhds_within_diff {x : Î±} {s t : set Î±} (hs : s âˆˆ ğ“[t] x) (t' : set Î±) :
  s \ t' âˆˆ ğ“[t \ t'] x :=
begin
  rw [nhds_within, diff_eq, diff_eq, â† inf_principal, â† inf_assoc],
  exact inter_mem_inf hs (mem_principal_self _)
end

lemma nhds_of_nhds_within_of_nhds
  {s t : set Î±} {a : Î±} (h1 : s âˆˆ ğ“ a) (h2 : t âˆˆ ğ“[s] a) : (t âˆˆ ğ“ a) :=
begin
  rcases mem_nhds_within_iff_exists_mem_nhds_inter.mp h2 with âŸ¨_, Hw, hwâŸ©,
  exact (nhds a).sets_of_superset ((nhds a).inter_sets Hw h1) hw,
end

lemma preimage_nhds_within_coinduced' {Ï€ : Î± â†’ Î²} {s : set Î²} {t : set Î±} {a : Î±}
  (h : a âˆˆ t) (ht : is_open t)
  (hs : s âˆˆ @nhds Î² (topological_space.coinduced (Î» x : t, Ï€ x) subtype.topological_space) (Ï€ a)) :
  Ï€ â»Â¹' s âˆˆ ğ“[t] a :=
begin
  letI := topological_space.coinduced (Î» x : t, Ï€ x) subtype.topological_space,
  rcases mem_nhds_iff.mp hs with âŸ¨V, hVs, V_op, mem_VâŸ©,
  refine mem_nhds_within_iff_exists_mem_nhds_inter.mpr âŸ¨Ï€ â»Â¹' V, mem_nhds_iff.mpr âŸ¨t âˆ© Ï€ â»Â¹' V,
    inter_subset_right t (Ï€ â»Â¹' V), _, mem_sep h mem_VâŸ©, subset.trans (inter_subset_left _ _)
    (preimage_mono hVs)âŸ©,
  obtain âŸ¨u, hu1, hu2âŸ© := is_open_induced_iff.mp (is_open_coinduced.1 V_op),
  rw [preimage_comp] at hu2,
  rw [set.inter_comm, â†(subtype.preimage_coe_eq_preimage_coe_iff.mp hu2)],
  exact hu1.inter ht,
end

lemma mem_nhds_within_of_mem_nhds {s t : set Î±} {a : Î±} (h : s âˆˆ ğ“ a) :
  s âˆˆ ğ“[t] a :=
mem_inf_of_left h

theorem self_mem_nhds_within {a : Î±} {s : set Î±} : s âˆˆ ğ“[s] a :=
mem_inf_of_right (mem_principal_self s)

theorem inter_mem_nhds_within (s : set Î±) {t : set Î±} {a : Î±} (h : t âˆˆ ğ“ a) :
  s âˆ© t âˆˆ ğ“[s] a :=
inter_mem self_mem_nhds_within (mem_inf_of_left h)

theorem nhds_within_mono (a : Î±) {s t : set Î±} (h : s âŠ† t) : ğ“[s] a â‰¤ ğ“[t] a :=
inf_le_inf_left _ (principal_mono.mpr h)

lemma pure_le_nhds_within {a : Î±} {s : set Î±} (ha : a âˆˆ s) : pure a â‰¤ ğ“[s] a :=
le_inf (pure_le_nhds a) (le_principal_iff.2 ha)

lemma mem_of_mem_nhds_within {a : Î±} {s t : set Î±} (ha : a âˆˆ s) (ht : t âˆˆ ğ“[s] a) :
  a âˆˆ t :=
pure_le_nhds_within ha ht

lemma filter.eventually.self_of_nhds_within {p : Î± â†’ Prop} {s : set Î±} {x : Î±}
  (h : âˆ€á¶  y in ğ“[s] x, p y) (hx : x âˆˆ s) : p x :=
mem_of_mem_nhds_within hx h

lemma tendsto_const_nhds_within {l : filter Î²} {s : set Î±} {a : Î±} (ha : a âˆˆ s) :
  tendsto (Î» x : Î², a) l (ğ“[s] a) :=
tendsto_const_pure.mono_right $ pure_le_nhds_within ha

theorem nhds_within_restrict'' {a : Î±} (s : set Î±) {t : set Î±} (h : t âˆˆ ğ“[s] a) :
  ğ“[s] a = ğ“[s âˆ© t] a :=
le_antisymm
  (le_inf inf_le_left (le_principal_iff.mpr (inter_mem self_mem_nhds_within h)))
  (inf_le_inf_left _ (principal_mono.mpr (set.inter_subset_left _ _)))

theorem nhds_within_restrict' {a : Î±} (s : set Î±) {t : set Î±} (h : t âˆˆ ğ“ a) :
  ğ“[s] a = ğ“[s âˆ© t] a :=
nhds_within_restrict'' s $ mem_inf_of_left h

theorem nhds_within_restrict {a : Î±} (s : set Î±) {t : set Î±} (hâ‚€ : a âˆˆ t) (hâ‚ : is_open t) :
  ğ“[s] a = ğ“[s âˆ© t] a :=
nhds_within_restrict' s (is_open.mem_nhds hâ‚ hâ‚€)

theorem nhds_within_le_of_mem {a : Î±} {s t : set Î±} (h : s âˆˆ ğ“[t] a) :
  ğ“[t] a â‰¤ ğ“[s] a :=
begin
  rcases mem_nhds_within.1 h with âŸ¨u, u_open, au, utsâŸ©,
  have : ğ“[t] a = ğ“[t âˆ© u] a := nhds_within_restrict _ au u_open,
  rw [this, inter_comm],
  exact nhds_within_mono _ uts
end

theorem nhds_within_le_nhds {a : Î±} {s : set Î±} : ğ“[s] a â‰¤ ğ“ a :=
by { rw â† nhds_within_univ, apply nhds_within_le_of_mem, exact univ_mem }

lemma nhds_within_eq_nhds_within' {a : Î±} {s t u : set Î±}
  (hs : s âˆˆ ğ“ a) (hâ‚‚ : t âˆ© s = u âˆ© s) : ğ“[t] a = ğ“[u] a :=
by rw [nhds_within_restrict' t hs, nhds_within_restrict' u hs, hâ‚‚]

theorem nhds_within_eq_nhds_within {a : Î±} {s t u : set Î±}
    (hâ‚€ : a âˆˆ s) (hâ‚ : is_open s) (hâ‚‚ : t âˆ© s = u âˆ© s) :
  ğ“[t] a = ğ“[u] a :=
by rw [nhds_within_restrict t hâ‚€ hâ‚, nhds_within_restrict u hâ‚€ hâ‚, hâ‚‚]

theorem is_open.nhds_within_eq {a : Î±} {s : set Î±} (h : is_open s) (ha : a âˆˆ s) :
  ğ“[s] a = ğ“ a :=
inf_eq_left.2 $ le_principal_iff.2 $ is_open.mem_nhds h ha

lemma preimage_nhds_within_coinduced {Ï€ : Î± â†’ Î²} {s : set Î²} {t : set Î±} {a : Î±}
  (h : a âˆˆ t) (ht : is_open t)
  (hs : s âˆˆ @nhds Î² (topological_space.coinduced (Î» x : t, Ï€ x) subtype.topological_space) (Ï€ a)) :
  Ï€ â»Â¹' s âˆˆ ğ“ a :=
by { rw â† ht.nhds_within_eq h, exact preimage_nhds_within_coinduced' h ht hs }

@[simp] theorem nhds_within_empty (a : Î±) : ğ“[âˆ…] a = âŠ¥ :=
by rw [nhds_within, principal_empty, inf_bot_eq]

theorem nhds_within_union (a : Î±) (s t : set Î±) :
  ğ“[s âˆª t] a = ğ“[s] a âŠ” ğ“[t] a :=
by { delta nhds_within, rw [â†inf_sup_left, sup_principal] }

theorem nhds_within_inter (a : Î±) (s t : set Î±) :
  ğ“[s âˆ© t] a = ğ“[s] a âŠ“ ğ“[t] a :=
by { delta nhds_within, rw [inf_left_comm, inf_assoc, inf_principal, â†inf_assoc, inf_idem] }

theorem nhds_within_inter' (a : Î±) (s t : set Î±) :
  ğ“[s âˆ© t] a = (ğ“[s] a) âŠ“ ğ“Ÿ t :=
by { delta nhds_within, rw [â†inf_principal, inf_assoc] }

theorem nhds_within_inter_of_mem {a : Î±} {s t : set Î±} (h : s âˆˆ ğ“[t] a) :
  ğ“[s âˆ© t] a = ğ“[t] a :=
by { rw [nhds_within_inter, inf_eq_right], exact nhds_within_le_of_mem h }

@[simp] theorem nhds_within_singleton (a : Î±) : ğ“[{a}] a = pure a :=
by rw [nhds_within, principal_singleton, inf_eq_right.2 (pure_le_nhds a)]

@[simp] theorem nhds_within_insert (a : Î±) (s : set Î±) :
  ğ“[insert a s] a = pure a âŠ” ğ“[s] a :=
by rw [â† singleton_union, nhds_within_union, nhds_within_singleton]

lemma mem_nhds_within_insert {a : Î±} {s t : set Î±} :
  t âˆˆ ğ“[insert a s] a â†” a âˆˆ t âˆ§ t âˆˆ ğ“[s] a :=
by simp

lemma insert_mem_nhds_within_insert {a : Î±} {s t : set Î±} (h : t âˆˆ ğ“[s] a) :
  insert a t âˆˆ ğ“[insert a s] a :=
by simp [mem_of_superset h]

@[simp] theorem nhds_within_compl_singleton_sup_pure (a : Î±) : ğ“[â‰ ] a âŠ” pure a = ğ“ a :=
by rw [â† nhds_within_singleton, â† nhds_within_union, compl_union_self, nhds_within_univ]

lemma nhds_within_prod_eq {Î± : Type*} [topological_space Î±] {Î² : Type*} [topological_space Î²]
  (a : Î±) (b : Î²) (s : set Î±) (t : set Î²) :
  ğ“[s.prod t] (a, b) = ğ“[s] a Ã—á¶  ğ“[t] b :=
by { delta nhds_within, rw [nhds_prod_eq, â†filter.prod_inf_prod, filter.prod_principal_principal] }

lemma nhds_within_prod {Î± : Type*} [topological_space Î±] {Î² : Type*} [topological_space Î²]
  {s u : set Î±} {t v : set Î²} {a : Î±} {b : Î²}
  (hu : u âˆˆ ğ“[s] a) (hv : v âˆˆ ğ“[t] b) :
  (u.prod v) âˆˆ ğ“[s.prod t] (a, b) :=
by { rw nhds_within_prod_eq, exact prod_mem_prod hu hv, }

lemma nhds_within_pi_eq' {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î  i, topological_space (Î± i)]
  {I : set Î¹} (hI : finite I) (s : Î  i, set (Î± i)) (x : Î  i, Î± i) :
  ğ“[pi I s] x = â¨… i, comap (Î» x, x i) (ğ“ (x i) âŠ“ â¨… (hi : i âˆˆ I), ğ“Ÿ (s i)) :=
by simp only [nhds_within, nhds_pi, filter.pi, comap_inf, comap_infi, pi_def, comap_principal,
  â† infi_principal_finite hI, â† infi_inf_eq]

lemma nhds_within_pi_eq {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î  i, topological_space (Î± i)]
  {I : set Î¹} (hI : finite I) (s : Î  i, set (Î± i)) (x : Î  i, Î± i) :
  ğ“[pi I s] x = (â¨… i âˆˆ I, comap (Î» x, x i) (ğ“[s i] (x i))) âŠ“
    â¨… (i âˆ‰ I), comap (Î» x, x i) (ğ“ (x i)) :=
begin
  simp only [nhds_within, nhds_pi, filter.pi, pi_def, â† infi_principal_finite hI, comap_inf,
    comap_principal, function.eval],
  rw [infi_split _ (Î» i, i âˆˆ I), inf_right_comm],
  simp only [infi_inf_eq]
end

lemma nhds_within_pi_univ_eq {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [fintype Î¹] [Î  i, topological_space (Î± i)]
  (s : Î  i, set (Î± i)) (x : Î  i, Î± i) :
  ğ“[pi univ s] x = â¨… i, comap (Î» x, x i) ğ“[s i] (x i) :=
by simpa [nhds_within] using nhds_within_pi_eq finite_univ s x

lemma nhds_within_pi_eq_bot {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î  i, topological_space (Î± i)]
  {I : set Î¹} {s : Î  i, set (Î± i)} {x : Î  i, Î± i} :
  ğ“[pi I s] x = âŠ¥ â†” âˆƒ i âˆˆ I, ğ“[s i] (x i) = âŠ¥ :=
by simp only [nhds_within, nhds_pi, pi_inf_principal_pi_eq_bot]

lemma nhds_within_pi_ne_bot {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î  i, topological_space (Î± i)]
  {I : set Î¹} {s : Î  i, set (Î± i)} {x : Î  i, Î± i} :
  (ğ“[pi I s] x).ne_bot â†” âˆ€ i âˆˆ I, (ğ“[s i] (x i)).ne_bot :=
by simp [ne_bot_iff, nhds_within_pi_eq_bot]

theorem filter.tendsto.piecewise_nhds_within {f g : Î± â†’ Î²} {t : set Î±} [âˆ€ x, decidable (x âˆˆ t)]
  {a : Î±} {s : set Î±} {l : filter Î²}
  (hâ‚€ : tendsto f (ğ“[s âˆ© t] a) l) (hâ‚ : tendsto g (ğ“[s âˆ© tá¶œ] a) l) :
  tendsto (piecewise t f g) (ğ“[s] a) l :=
by apply tendsto.piecewise; rwa â† nhds_within_inter'

theorem filter.tendsto.if_nhds_within {f g : Î± â†’ Î²} {p : Î± â†’ Prop} [decidable_pred p]
    {a : Î±} {s : set Î±} {l : filter Î²}
    (hâ‚€ : tendsto f (ğ“[s âˆ© {x | p x}] a) l)
    (hâ‚ : tendsto g (ğ“[s âˆ© {x | Â¬ p x}] a) l) :
  tendsto (Î» x, if p x then f x else g x) (ğ“[s] a) l :=
hâ‚€.piecewise_nhds_within hâ‚

lemma map_nhds_within (f : Î± â†’ Î²) (a : Î±) (s : set Î±) :
  map f (ğ“[s] a) = â¨… t âˆˆ {t : set Î± | a âˆˆ t âˆ§ is_open t}, ğ“Ÿ (f  '' (t âˆ© s)) :=
((nhds_within_basis_open a s).map f).eq_binfi

theorem tendsto_nhds_within_mono_left {f : Î± â†’ Î²} {a : Î±}
    {s t : set Î±} {l : filter Î²} (hst : s âŠ† t) (h : tendsto f (ğ“[t] a) l) :
  tendsto f (ğ“[s] a) l :=
h.mono_left $ nhds_within_mono a hst

theorem tendsto_nhds_within_mono_right {f : Î² â†’ Î±} {l : filter Î²}
    {a : Î±} {s t : set Î±} (hst : s âŠ† t) (h : tendsto f l (ğ“[s] a)) :
  tendsto f l (ğ“[t] a) :=
h.mono_right (nhds_within_mono a hst)

theorem tendsto_nhds_within_of_tendsto_nhds {f : Î± â†’ Î²} {a : Î±}
    {s : set Î±} {l : filter Î²} (h : tendsto f (ğ“ a) l) :
  tendsto f (ğ“[s] a) l :=
h.mono_left inf_le_left

theorem principal_subtype {Î± : Type*} (s : set Î±) (t : set {x // x âˆˆ s}) :
  ğ“Ÿ t = comap coe (ğ“Ÿ ((coe : s â†’ Î±) '' t)) :=
by rw [comap_principal, set.preimage_image_eq _ subtype.coe_injective]

lemma nhds_within_ne_bot_of_mem {s : set Î±} {x : Î±} (hx : x âˆˆ s) :
  ne_bot (ğ“[s] x) :=
mem_closure_iff_nhds_within_ne_bot.1 $ subset_closure hx

lemma is_closed.mem_of_nhds_within_ne_bot {s : set Î±} (hs : is_closed s)
  {x : Î±} (hx : ne_bot $ ğ“[s] x) : x âˆˆ s :=
by simpa only [hs.closure_eq] using mem_closure_iff_nhds_within_ne_bot.2 hx

lemma dense_range.nhds_within_ne_bot {Î¹ : Type*} {f : Î¹ â†’ Î±} (h : dense_range f) (x : Î±) :
  ne_bot (ğ“[range f] x) :=
mem_closure_iff_cluster_pt.1 (h x)

lemma mem_closure_pi {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î  i, topological_space (Î± i)]
  {I : set Î¹} {s : Î  i, set (Î± i)} {x : Î  i, Î± i} :
  x âˆˆ closure (pi I s) â†” âˆ€ i âˆˆ I, x i âˆˆ closure (s i) :=
by simp only [mem_closure_iff_nhds_within_ne_bot, nhds_within_pi_ne_bot]

lemma closure_pi_set {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î  i, topological_space (Î± i)]
  (I : set Î¹) (s : Î  i, set (Î± i)) :
  closure (pi I s) = pi I (Î» i, closure (s i)) :=
set.ext $ Î» x, mem_closure_pi

lemma dense_pi {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î  i, topological_space (Î± i)] {s : Î  i, set (Î± i)}
  (I : set Î¹) (hs : âˆ€ i âˆˆ I, dense (s i)) :
  dense (pi I s) :=
by simp only [dense_iff_closure_eq, closure_pi_set,
  pi_congr rfl (Î» i hi, (hs i hi).closure_eq), pi_univ]

lemma eventually_eq_nhds_within_iff {f g : Î± â†’ Î²} {s : set Î±} {a : Î±} :
  (f =á¶ [ğ“[s] a] g) â†” âˆ€á¶  x in ğ“ a, x âˆˆ s â†’ f x = g x :=
mem_inf_principal

lemma eventually_eq_nhds_within_of_eq_on {f g : Î± â†’ Î²} {s : set Î±} {a : Î±} (h : eq_on f g s) :
  f =á¶ [ğ“[s] a] g :=
mem_inf_of_right h

lemma set.eq_on.eventually_eq_nhds_within {f g : Î± â†’ Î²} {s : set Î±} {a : Î±} (h : eq_on f g s) :
  f =á¶ [ğ“[s] a] g :=
eventually_eq_nhds_within_of_eq_on h

lemma tendsto_nhds_within_congr {f g : Î± â†’ Î²} {s : set Î±} {a : Î±} {l : filter Î²}
  (hfg : âˆ€ x âˆˆ s, f x = g x) (hf : tendsto f (ğ“[s] a) l) : tendsto g (ğ“[s] a) l :=
(tendsto_congr' $ eventually_eq_nhds_within_of_eq_on hfg).1 hf

lemma eventually_nhds_within_of_forall {s : set Î±} {a : Î±} {p : Î± â†’ Prop} (h : âˆ€ x âˆˆ s, p x) :
  âˆ€á¶  x in ğ“[s] a, p x :=
mem_inf_of_right h

lemma tendsto_nhds_within_of_tendsto_nhds_of_eventually_within {a : Î±} {l : filter Î²}
  {s : set Î±} (f : Î² â†’ Î±) (h1 : tendsto f l (ğ“ a)) (h2 : âˆ€á¶  x in l, f x âˆˆ s) :
  tendsto f l (ğ“[s] a) :=
tendsto_inf.2 âŸ¨h1, tendsto_principal.2 h2âŸ©

@[simp] lemma tendsto_nhds_within_range {a : Î±} {l : filter Î²} {f : Î² â†’ Î±} :
  tendsto f l (ğ“[range f] a) â†” tendsto f l (ğ“ a) :=
âŸ¨Î» h, h.mono_right inf_le_left, Î» h, tendsto_inf.2
  âŸ¨h, tendsto_principal.2 $ eventually_of_forall mem_range_selfâŸ©âŸ©

lemma filter.eventually_eq.eq_of_nhds_within {s : set Î±} {f g : Î± â†’ Î²} {a : Î±}
  (h : f =á¶ [ğ“[s] a] g) (hmem : a âˆˆ s) : f a = g a :=
h.self_of_nhds_within hmem

lemma eventually_nhds_within_of_eventually_nhds {Î± : Type*} [topological_space Î±]
  {s : set Î±} {a : Î±} {p : Î± â†’ Prop} (h : âˆ€á¶  x in ğ“ a, p x) :
  âˆ€á¶  x in ğ“[s] a, p x :=
mem_nhds_within_of_mem_nhds h

/-!
### `nhds_within` and subtypes
-/

theorem mem_nhds_within_subtype {s : set Î±} {a : {x // x âˆˆ s}} {t u : set {x // x âˆˆ s}} :
  t âˆˆ ğ“[u] a â†” t âˆˆ comap (coe : s â†’ Î±) (ğ“[coe '' u] a) :=
by rw [nhds_within, nhds_subtype, principal_subtype, â†comap_inf, â†nhds_within]

theorem nhds_within_subtype (s : set Î±) (a : {x // x âˆˆ s}) (t : set {x // x âˆˆ s}) :
  ğ“[t] a = comap (coe : s â†’ Î±) (ğ“[coe '' t] a) :=
filter.ext $ Î» u, mem_nhds_within_subtype

theorem nhds_within_eq_map_subtype_coe {s : set Î±} {a : Î±} (h : a âˆˆ s) :
  ğ“[s] a = map (coe : s â†’ Î±) (ğ“ âŸ¨a, hâŸ©) :=
by simpa only [subtype.range_coe] using (embedding_subtype_coe.map_nhds_eq âŸ¨a, hâŸ©).symm

theorem mem_nhds_subtype_iff_nhds_within {s : set Î±} {a : s} {t : set s} :
  t âˆˆ ğ“ a â†” coe '' t âˆˆ ğ“[s] (a : Î±) :=
by rw [nhds_within_eq_map_subtype_coe a.coe_prop, mem_map,
  preimage_image_eq _ subtype.coe_injective, subtype.coe_eta]

theorem preimage_coe_mem_nhds_subtype {s t : set Î±} {a : s} :
  coe â»Â¹' t âˆˆ ğ“ a â†” t âˆˆ ğ“[s] â†‘a :=
by simp only [mem_nhds_subtype_iff_nhds_within, subtype.image_preimage_coe, inter_mem_iff,
  self_mem_nhds_within, and_true]

theorem tendsto_nhds_within_iff_subtype {s : set Î±} {a : Î±} (h : a âˆˆ s) (f : Î± â†’ Î²) (l : filter Î²) :
  tendsto f (ğ“[s] a) l â†” tendsto (s.restrict f) (ğ“ âŸ¨a, hâŸ©) l :=
by simp only [tendsto, nhds_within_eq_map_subtype_coe h, filter.map_map, restrict]

variables [topological_space Î²] [topological_space Î³] [topological_space Î´]

/-- A function between topological spaces is continuous at a point `xâ‚€` within a subset `s`
if `f x` tends to `f xâ‚€` when `x` tends to `xâ‚€` while staying within `s`. -/
def continuous_within_at (f : Î± â†’ Î²) (s : set Î±) (x : Î±) : Prop :=
tendsto f (ğ“[s] x) (ğ“ (f x))

/-- If a function is continuous within `s` at `x`, then it tends to `f x` within `s` by definition.
We register this fact for use with the dot notation, especially to use `tendsto.comp` as
`continuous_within_at.comp` will have a different meaning. -/
lemma continuous_within_at.tendsto {f : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) :
  tendsto f (ğ“[s] x) (ğ“ (f x)) := h

/-- A function between topological spaces is continuous on a subset `s`
when it's continuous at every point of `s` within `s`. -/
def continuous_on (f : Î± â†’ Î²) (s : set Î±) : Prop := âˆ€ x âˆˆ s, continuous_within_at f s x

lemma continuous_on.continuous_within_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±} (hf : continuous_on f s)
  (hx : x âˆˆ s) : continuous_within_at f s x :=
hf x hx

theorem continuous_within_at_univ (f : Î± â†’ Î²) (x : Î±) :
  continuous_within_at f set.univ x â†” continuous_at f x :=
by rw [continuous_at, continuous_within_at, nhds_within_univ]

theorem continuous_within_at_iff_continuous_at_restrict (f : Î± â†’ Î²) {x : Î±} {s : set Î±}
  (h : x âˆˆ s) :
  continuous_within_at f s x â†” continuous_at (s.restrict f) âŸ¨x, hâŸ© :=
tendsto_nhds_within_iff_subtype h f _

theorem continuous_within_at.tendsto_nhds_within {f : Î± â†’ Î²} {x : Î±} {s : set Î±} {t : set Î²}
  (h : continuous_within_at f s x) (ht : maps_to f s t) :
  tendsto f (ğ“[s] x) (ğ“[t] (f x)) :=
tendsto_inf.2 âŸ¨h, tendsto_principal.2 $ mem_inf_of_right $ mem_principal.2 $ htâŸ©

theorem continuous_within_at.tendsto_nhds_within_image {f : Î± â†’ Î²} {x : Î±} {s : set Î±}
  (h : continuous_within_at f s x) :
  tendsto f (ğ“[s] x) (ğ“[f '' s] (f x)) :=
h.tendsto_nhds_within (maps_to_image _ _)

lemma continuous_within_at.prod_map {f : Î± â†’ Î³} {g : Î² â†’ Î´} {s : set Î±} {t : set Î²}
  {x : Î±} {y : Î²}
  (hf : continuous_within_at f s x) (hg : continuous_within_at g t y) :
  continuous_within_at (prod.map f g) (s.prod t) (x, y) :=
begin
  unfold continuous_within_at at *,
  rw [nhds_within_prod_eq, prod.map, nhds_prod_eq],
  exact hf.prod_map hg,
end

lemma continuous_within_at_pi {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [âˆ€ i, topological_space (Ï€ i)]
  {f : Î± â†’ Î  i, Ï€ i} {s : set Î±} {x : Î±} :
  continuous_within_at f s x â†” âˆ€ i, continuous_within_at (Î» y, f y i) s x :=
tendsto_pi_nhds

lemma continuous_on_pi {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [âˆ€ i, topological_space (Ï€ i)]
  {f : Î± â†’ Î  i, Ï€ i} {s : set Î±} :
  continuous_on f s â†” âˆ€ i, continuous_on (Î» y, f y i) s :=
âŸ¨Î» h i x hx, tendsto_pi_nhds.1 (h x hx) i, Î» h x hx, tendsto_pi_nhds.2 (Î» i, h i x hx)âŸ©

lemma continuous_within_at.fin_insert_nth {n} {Ï€ : fin (n + 1) â†’ Type*}
  [Î  i, topological_space (Ï€ i)] (i : fin (n + 1)) {f : Î± â†’ Ï€ i} {a : Î±} {s : set Î±}
  (hf : continuous_within_at f s a)
  {g : Î± â†’ Î  j : fin n, Ï€ (i.succ_above j)} (hg : continuous_within_at g s a) :
  continuous_within_at (Î» a, i.insert_nth (f a) (g a)) s a :=
hf.fin_insert_nth i hg

lemma continuous_on.fin_insert_nth {n} {Ï€ : fin (n + 1) â†’ Type*} [Î  i, topological_space (Ï€ i)]
  (i : fin (n + 1)) {f : Î± â†’ Ï€ i} {s : set Î±} (hf : continuous_on f s)
  {g : Î± â†’ Î  j : fin n, Ï€ (i.succ_above j)} (hg : continuous_on g s) :
  continuous_on (Î» a, i.insert_nth (f a) (g a)) s :=
Î» a ha, (hf a ha).fin_insert_nth i (hg a ha)

theorem continuous_on_iff {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†” âˆ€ x âˆˆ s, âˆ€ t : set Î², is_open t â†’ f x âˆˆ t â†’ âˆƒ u, is_open u âˆ§ x âˆˆ u âˆ§
    u âˆ© s âŠ† f â»Â¹' t :=
by simp only [continuous_on, continuous_within_at, tendsto_nhds, mem_nhds_within]

theorem continuous_on_iff_continuous_restrict {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†” continuous (s.restrict f) :=
begin
  rw [continuous_on, continuous_iff_continuous_at], split,
  { rintros h âŸ¨x, xsâŸ©,
    exact (continuous_within_at_iff_continuous_at_restrict f xs).mp (h x xs) },
  intros h x xs,
  exact (continuous_within_at_iff_continuous_at_restrict f xs).mpr (h âŸ¨x, xsâŸ©)
end

theorem continuous_on_iff' {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†” âˆ€ t : set Î², is_open t â†’ âˆƒ u, is_open u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s :=
have âˆ€ t, is_open (s.restrict f â»Â¹' t) â†” âˆƒ (u : set Î±), is_open u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s,
  begin
    intro t,
    rw [is_open_induced_iff, set.restrict_eq, set.preimage_comp],
    simp only [subtype.preimage_coe_eq_preimage_coe_iff],
    split; { rintros âŸ¨u, ou, useqâŸ©, exact âŸ¨u, ou, useq.symmâŸ© }
  end,
by rw [continuous_on_iff_continuous_restrict, continuous_def]; simp only [this]

theorem continuous_on_iff_is_closed {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†” âˆ€ t : set Î², is_closed t â†’ âˆƒ u, is_closed u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s :=
have âˆ€ t, is_closed (s.restrict f â»Â¹' t) â†” âˆƒ (u : set Î±), is_closed u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s,
  begin
    intro t,
    rw [is_closed_induced_iff, set.restrict_eq, set.preimage_comp],
    simp only [subtype.preimage_coe_eq_preimage_coe_iff, eq_comm]
  end,
by rw [continuous_on_iff_continuous_restrict, continuous_iff_is_closed]; simp only [this]

lemma continuous_on.prod_map {f : Î± â†’ Î³} {g : Î² â†’ Î´} {s : set Î±} {t : set Î²}
  (hf : continuous_on f s) (hg : continuous_on g t) :
  continuous_on (prod.map f g) (s.prod t) :=
Î» âŸ¨x, yâŸ© âŸ¨hx, hyâŸ©, continuous_within_at.prod_map (hf x hx) (hg y hy)

lemma continuous_on_empty (f : Î± â†’ Î²) : continuous_on f âˆ… :=
Î» x, false.elim

theorem nhds_within_le_comap {x : Î±} {s : set Î±} {f : Î± â†’ Î²} (ctsf : continuous_within_at f s x) :
  ğ“[s] x â‰¤ comap f (ğ“[f '' s] (f x)) :=
map_le_iff_le_comap.1 ctsf.tendsto_nhds_within_image

theorem continuous_within_at_iff_ptendsto_res (f : Î± â†’ Î²) {x : Î±} {s : set Î±} :
  continuous_within_at f s x â†” ptendsto (pfun.res f s) (ğ“ x) (ğ“ (f x)) :=
tendsto_iff_ptendsto _ _ _ _

lemma continuous_iff_continuous_on_univ {f : Î± â†’ Î²} : continuous f â†” continuous_on f univ :=
by simp [continuous_iff_continuous_at, continuous_on, continuous_at, continuous_within_at,
         nhds_within_univ]

lemma continuous_within_at.mono {f : Î± â†’ Î²} {s t : set Î±} {x : Î±} (h : continuous_within_at f t x)
  (hs : s âŠ† t) : continuous_within_at f s x :=
h.mono_left (nhds_within_mono x hs)

lemma continuous_within_at.mono_of_mem {f : Î± â†’ Î²} {s t : set Î±} {x : Î±}
  (h : continuous_within_at f t x) (hs : t âˆˆ ğ“[s] x) : continuous_within_at f s x :=
h.mono_left (nhds_within_le_of_mem hs)

lemma continuous_within_at_inter' {f : Î± â†’ Î²} {s t : set Î±} {x : Î±} (h : t âˆˆ ğ“[s] x) :
  continuous_within_at f (s âˆ© t) x â†” continuous_within_at f s x :=
by simp [continuous_within_at, nhds_within_restrict'' s h]

lemma continuous_within_at_inter {f : Î± â†’ Î²} {s t : set Î±} {x : Î±} (h : t âˆˆ ğ“ x) :
  continuous_within_at f (s âˆ© t) x â†” continuous_within_at f s x :=
by simp [continuous_within_at, nhds_within_restrict' s h]

lemma continuous_within_at_union {f : Î± â†’ Î²} {s t : set Î±} {x : Î±} :
  continuous_within_at f (s âˆª t) x â†” continuous_within_at f s x âˆ§ continuous_within_at f t x :=
by simp only [continuous_within_at, nhds_within_union, tendsto_sup]

lemma continuous_within_at.union {f : Î± â†’ Î²} {s t : set Î±} {x : Î±}
  (hs : continuous_within_at f s x) (ht : continuous_within_at f t x) :
  continuous_within_at f (s âˆª t) x :=
continuous_within_at_union.2 âŸ¨hs, htâŸ©

lemma continuous_within_at.mem_closure_image  {f : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (hx : x âˆˆ closure s) : f x âˆˆ closure (f '' s) :=
by haveI := (mem_closure_iff_nhds_within_ne_bot.1 hx);
exact (mem_closure_of_tendsto h $
  mem_of_superset self_mem_nhds_within (subset_preimage_image f s))

lemma continuous_within_at.mem_closure {f : Î± â†’ Î²} {s : set Î±} {x : Î±} {A : set Î²}
  (h : continuous_within_at f s x) (hx : x âˆˆ closure s) (hA : s âŠ† fâ»Â¹' A) : f x âˆˆ closure A :=
closure_mono (image_subset_iff.2 hA) (h.mem_closure_image hx)

lemma continuous_within_at.image_closure {f : Î± â†’ Î²} {s : set Î±}
  (hf : âˆ€ x âˆˆ closure s, continuous_within_at f s x) :
  f '' (closure s) âŠ† closure (f '' s) :=
begin
  rintros _ âŸ¨x, hx, rflâŸ©,
  exact (hf x hx).mem_closure_image hx
end

lemma continuous_on.image_closure {f : Î± â†’ Î²} {s : set Î±} (hf : continuous_on f (closure s)) :
  f '' (closure s) âŠ† closure (f '' s) :=
continuous_within_at.image_closure $ Î» x hx, (hf x hx).mono subset_closure

@[simp] lemma continuous_within_at_singleton {f : Î± â†’ Î²} {x : Î±} : continuous_within_at f {x} x :=
by simp only [continuous_within_at, nhds_within_singleton, tendsto_pure_nhds]

@[simp] lemma continuous_within_at_insert_self {f : Î± â†’ Î²} {x : Î±} {s : set Î±} :
  continuous_within_at f (insert x s) x â†” continuous_within_at f s x :=
by simp only [â† singleton_union, continuous_within_at_union, continuous_within_at_singleton,
  true_and]

alias continuous_within_at_insert_self â†” _ continuous_within_at.insert_self

lemma continuous_within_at.diff_iff {f : Î± â†’ Î²} {s t : set Î±} {x : Î±}
  (ht : continuous_within_at f t x) :
  continuous_within_at f (s \ t) x â†” continuous_within_at f s x :=
âŸ¨Î» h, (h.union ht).mono $ by simp only [diff_union_self, subset_union_left],
  Î» h, h.mono (diff_subset _ _)âŸ©

@[simp] lemma continuous_within_at_diff_self {f : Î± â†’ Î²} {s : set Î±} {x : Î±} :
  continuous_within_at f (s \ {x}) x â†” continuous_within_at f s x :=
continuous_within_at_singleton.diff_iff

lemma continuous_within_at_update_same [decidable_eq Î±] {f : Î± â†’ Î²} {s : set Î±} {x : Î±} {y : Î²} :
  continuous_within_at (function.update f x y) s x â†” tendsto f (ğ“[s \ {x}] x) (ğ“ y) :=
calc continuous_within_at (function.update f x y) s x
    â†” continuous_within_at (function.update f x y) (s \ {x}) x :
  continuous_within_at_diff_self.symm
... â†” tendsto (function.update f x y) (ğ“[s \ {x}] x) (ğ“ y) :
  by rw [continuous_within_at, function.update_same]
... â†” tendsto f (ğ“[s \ {x}] x) (ğ“ y) :
  tendsto_congr' $ mem_of_superset self_mem_nhds_within $
    Î» z hz, by rw [mem_set_of_eq, function.update_noteq hz.2]

theorem is_open_map.continuous_on_image_of_left_inv_on {f : Î± â†’ Î²} {s : set Î±}
  (h : is_open_map (s.restrict f)) {finv : Î² â†’ Î±} (hleft : left_inv_on finv f s) :
  continuous_on finv (f '' s) :=
begin
  refine continuous_on_iff'.2 (Î» t ht, âŸ¨f '' (t âˆ© s), _, _âŸ©),
  { rw â† image_restrict, exact h _ (ht.preimage continuous_subtype_coe) },
  { rw [inter_eq_self_of_subset_left (image_subset f (inter_subset_right t s)),
      hleft.image_inter'] },
end

theorem is_open_map.continuous_on_range_of_left_inverse {f : Î± â†’ Î²} (hf : is_open_map f)
  {finv : Î² â†’ Î±} (hleft : function.left_inverse finv f) :
  continuous_on finv (range f) :=
begin
  rw [â† image_univ],
  exact (hf.restrict is_open_univ).continuous_on_image_of_left_inv_on (Î» x _, hleft x)
end

lemma continuous_on.congr_mono {f g : Î± â†’ Î²} {s sâ‚ : set Î±} (h : continuous_on f s)
  (h' : eq_on g f sâ‚) (hâ‚ : sâ‚ âŠ† s) : continuous_on g sâ‚ :=
begin
  assume x hx,
  unfold continuous_within_at,
  have A := (h x (hâ‚ hx)).mono hâ‚,
  unfold continuous_within_at at A,
  rw â† h' hx at A,
  exact A.congr' h'.eventually_eq_nhds_within.symm
end

lemma continuous_on.congr {f g : Î± â†’ Î²} {s : set Î±} (h : continuous_on f s) (h' : eq_on g f s) :
  continuous_on g s :=
h.congr_mono h' (subset.refl _)

lemma continuous_on_congr {f g : Î± â†’ Î²} {s : set Î±} (h' : eq_on g f s) :
  continuous_on g s â†” continuous_on f s :=
âŸ¨Î» h, continuous_on.congr h h'.symm, Î» h, h.congr h'âŸ©

lemma continuous_at.continuous_within_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±} (h : continuous_at f x) :
  continuous_within_at f s x :=
continuous_within_at.mono ((continuous_within_at_univ f x).2 h) (subset_univ _)

lemma continuous_within_at.continuous_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (hs : s âˆˆ ğ“ x) : continuous_at f x :=
begin
  have : s = univ âˆ© s, by rw univ_inter,
  rwa [this, continuous_within_at_inter hs, continuous_within_at_univ] at h
end

lemma continuous_on.continuous_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_on f s) (hx : s âˆˆ ğ“ x) : continuous_at f x :=
(h x (mem_of_mem_nhds hx)).continuous_at hx

lemma continuous_at.continuous_on {f : Î± â†’ Î²} {s : set Î±} (hcont : âˆ€ x âˆˆ s, continuous_at f x) :
  continuous_on f s :=
Î» x hx, (hcont x hx).continuous_within_at

lemma continuous_within_at.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±} {t : set Î²} {x : Î±}
  (hg : continuous_within_at g t (f x)) (hf : continuous_within_at f s x) (h : s âŠ† f â»Â¹' t) :
  continuous_within_at (g âˆ˜ f) s x :=
hg.tendsto.comp (hf.tendsto_nhds_within h)

lemma continuous_within_at.comp' {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±} {t : set Î²} {x : Î±}
  (hg : continuous_within_at g t (f x)) (hf : continuous_within_at f s x) :
  continuous_within_at (g âˆ˜ f) (s âˆ© fâ»Â¹' t) x :=
hg.comp (hf.mono (inter_subset_left _ _)) (inter_subset_right _ _)

lemma continuous_at.comp_continuous_within_at {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (hg : continuous_at g (f x)) (hf : continuous_within_at f s x) :
  continuous_within_at (g âˆ˜ f) s x :=
hg.continuous_within_at.comp hf subset_preimage_univ

lemma continuous_on.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hg : continuous_on g t) (hf : continuous_on f s) (h : s âŠ† f â»Â¹' t) :
  continuous_on (g âˆ˜ f) s :=
Î»x hx, continuous_within_at.comp (hg _ (h hx)) (hf x hx) h

lemma continuous_on.mono {f : Î± â†’ Î²} {s t : set Î±} (hf : continuous_on f s) (h : t âŠ† s)  :
  continuous_on f t :=
Î»x hx, (hf x (h hx)).mono_left (nhds_within_mono _ h)

lemma continuous_on.comp' {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hg : continuous_on g t) (hf : continuous_on f s) :
  continuous_on (g âˆ˜ f) (s âˆ© fâ»Â¹' t) :=
hg.comp (hf.mono (inter_subset_left _ _)) (inter_subset_right _ _)

lemma continuous.continuous_on {f : Î± â†’ Î²} {s : set Î±} (h : continuous f) :
  continuous_on f s :=
begin
  rw continuous_iff_continuous_on_univ at h,
  exact h.mono (subset_univ _)
end

lemma continuous.continuous_within_at {f : Î± â†’ Î²} {s : set Î±} {x : Î±} (h : continuous f) :
  continuous_within_at f s x :=
h.continuous_at.continuous_within_at

lemma continuous.comp_continuous_on {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î±}
  (hg : continuous g) (hf : continuous_on f s) :
  continuous_on (g âˆ˜ f) s :=
hg.continuous_on.comp hf subset_preimage_univ

lemma continuous_on.comp_continuous {g : Î² â†’ Î³} {f : Î± â†’ Î²} {s : set Î²}
  (hg : continuous_on g s) (hf : continuous f) (hs : âˆ€ x, f x âˆˆ s) : continuous (g âˆ˜ f) :=
begin
  rw continuous_iff_continuous_on_univ at *,
  exact hg.comp hf (Î» x _, hs x),
end

lemma continuous_within_at.preimage_mem_nhds_within {f : Î± â†’ Î²} {x : Î±} {s : set Î±} {t : set Î²}
  (h : continuous_within_at f s x) (ht : t âˆˆ ğ“ (f x)) : f â»Â¹' t âˆˆ ğ“[s] x :=
h ht

lemma set.left_inv_on.map_nhds_within_eq {f : Î± â†’ Î²} {g : Î² â†’ Î±} {x : Î²} {s : set Î²}
  (h : left_inv_on f g s) (hx : f (g x) = x) (hf : continuous_within_at f (g '' s) (g x))
  (hg : continuous_within_at g s x) :
  map g (ğ“[s] x) = ğ“[g '' s] (g x) :=
begin
  apply le_antisymm,
  { exact hg.tendsto_nhds_within (maps_to_image _ _) },
  { have A : g âˆ˜ f =á¶ [ğ“[g '' s] (g x)] id,
      from h.right_inv_on_image.eq_on.eventually_eq_of_mem self_mem_nhds_within,
    refine le_map_of_right_inverse A _,
    simpa only [hx] using hf.tendsto_nhds_within (h.maps_to (surj_on_image _ _)) }
end

lemma function.left_inverse.map_nhds_eq {f : Î± â†’ Î²} {g : Î² â†’ Î±} {x : Î²}
  (h : function.left_inverse f g) (hf : continuous_within_at f (range g) (g x))
  (hg : continuous_at g x) :
  map g (ğ“ x) = ğ“[range g] (g x) :=
by simpa only [nhds_within_univ, image_univ]
  using (h.left_inv_on univ).map_nhds_within_eq (h x) (by rwa image_univ) hg.continuous_within_at

lemma continuous_within_at.preimage_mem_nhds_within' {f : Î± â†’ Î²} {x : Î±} {s : set Î±} {t : set Î²}
  (h : continuous_within_at f s x) (ht : t âˆˆ ğ“[f '' s] (f x)) :
  f â»Â¹' t âˆˆ ğ“[s] x :=
h.tendsto_nhds_within (maps_to_image _ _) ht

lemma filter.eventually_eq.congr_continuous_within_at {f g : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : f =á¶ [ğ“[s] x] g) (hx : f x = g x) :
  continuous_within_at f s x â†” continuous_within_at g s x :=
by rw [continuous_within_at, hx, tendsto_congr' h, continuous_within_at]

lemma continuous_within_at.congr_of_eventually_eq {f fâ‚ : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  continuous_within_at fâ‚ s x :=
(hâ‚.congr_continuous_within_at hx).2 h

lemma continuous_within_at.congr {f fâ‚ : Î± â†’ Î²} {s : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (hâ‚ : âˆ€yâˆˆs, fâ‚ y = f y) (hx : fâ‚ x = f x) :
  continuous_within_at fâ‚ s x :=
h.congr_of_eventually_eq (mem_of_superset self_mem_nhds_within hâ‚) hx

lemma continuous_within_at.congr_mono {f g : Î± â†’ Î²} {s sâ‚ : set Î±} {x : Î±}
  (h : continuous_within_at f s x) (h' : eq_on g f sâ‚) (hâ‚ : sâ‚ âŠ† s) (hx : g x = f x):
  continuous_within_at g sâ‚ x :=
(h.mono hâ‚).congr h' hx

lemma continuous_on_const {s : set Î±} {c : Î²} : continuous_on (Î»x, c) s :=
continuous_const.continuous_on

lemma continuous_within_at_const {b : Î²} {s : set Î±} {x : Î±} :
  continuous_within_at (Î» _:Î±, b) s x :=
continuous_const.continuous_within_at

lemma continuous_on_id {s : set Î±} : continuous_on id s :=
continuous_id.continuous_on

lemma continuous_within_at_id {s : set Î±} {x : Î±} : continuous_within_at id s x :=
continuous_id.continuous_within_at

lemma continuous_on_open_iff {f : Î± â†’ Î²} {s : set Î±} (hs : is_open s) :
  continuous_on f s â†” (âˆ€t, is_open t â†’ is_open (s âˆ© fâ»Â¹' t)) :=
begin
  rw continuous_on_iff',
  split,
  { assume h t ht,
    rcases h t ht with âŸ¨u, u_open, huâŸ©,
    rw [inter_comm, hu],
    apply is_open.inter u_open hs },
  { assume h t ht,
    refine âŸ¨s âˆ© f â»Â¹' t, h t ht, _âŸ©,
    rw [@inter_comm _ s (f â»Â¹' t), inter_assoc, inter_self] }
end

lemma continuous_on.preimage_open_of_open {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hf : continuous_on f s) (hs : is_open s) (ht : is_open t) : is_open (s âˆ© fâ»Â¹' t) :=
(continuous_on_open_iff hs).1 hf t ht

lemma continuous_on.is_open_preimage {f : Î± â†’ Î²} {s : set Î±} {t : set Î²} (h : continuous_on f s)
  (hs : is_open s) (hp : f â»Â¹' t âŠ† s) (ht : is_open t) : is_open (f â»Â¹' t) :=
begin
  convert (continuous_on_open_iff hs).mp h t ht,
  rw [inter_comm, inter_eq_self_of_subset_left hp],
end

lemma continuous_on.preimage_closed_of_closed {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hf : continuous_on f s) (hs : is_closed s) (ht : is_closed t) : is_closed (s âˆ© fâ»Â¹' t) :=
begin
  rcases continuous_on_iff_is_closed.1 hf t ht with âŸ¨u, huâŸ©,
  rw [inter_comm, hu.2],
  apply is_closed.inter hu.1 hs
end

lemma continuous_on.preimage_interior_subset_interior_preimage {f : Î± â†’ Î²} {s : set Î±} {t : set Î²}
  (hf : continuous_on f s) (hs : is_open s) : s âˆ© fâ»Â¹' (interior t) âŠ† s âˆ© interior (fâ»Â¹' t) :=
calc s âˆ© f â»Â¹' (interior t) âŠ† interior (s âˆ© f â»Â¹' t) :
  interior_maximal (inter_subset_inter (subset.refl _) (preimage_mono interior_subset))
    (hf.preimage_open_of_open hs is_open_interior)
... = s âˆ© interior (f â»Â¹' t) : by rw [interior_inter, hs.interior_eq]

lemma continuous_on_of_locally_continuous_on {f : Î± â†’ Î²} {s : set Î±}
  (h : âˆ€xâˆˆs, âˆƒt, is_open t âˆ§ x âˆˆ t âˆ§ continuous_on f (s âˆ© t)) : continuous_on f s :=
begin
  assume x xs,
  rcases h x xs with âŸ¨t, open_t, xt, ctâŸ©,
  have := ct x âŸ¨xs, xtâŸ©,
  rwa [continuous_within_at, â† nhds_within_restrict _ xt open_t] at this
end

lemma continuous_on_open_of_generate_from {Î² : Type*} {s : set Î±} {T : set (set Î²)} {f : Î± â†’ Î²}
  (hs : is_open s) (h : âˆ€t âˆˆ T, is_open (s âˆ© fâ»Â¹' t)) :
  @continuous_on Î± Î² _ (topological_space.generate_from T) f s :=
begin
  rw continuous_on_open_iff,
  assume t ht,
  induction ht with u hu u v Tu Tv hu hv U hU hU',
  { exact h u hu },
  { simp only [preimage_univ, inter_univ], exact hs },
  { have : s âˆ© f â»Â¹' (u âˆ© v) = (s âˆ© f â»Â¹' u) âˆ© (s âˆ© f â»Â¹' v),
      by rw [preimage_inter, inter_assoc, inter_left_comm _ s, â† inter_assoc s s, inter_self],
    rw this,
    exact hu.inter hv },
  { rw [preimage_sUnion, inter_bUnion],
    exact is_open_bUnion hU' },
  { exact hs }
end

lemma continuous_within_at.prod {f : Î± â†’ Î²} {g : Î± â†’ Î³} {s : set Î±} {x : Î±}
  (hf : continuous_within_at f s x) (hg : continuous_within_at g s x) :
  continuous_within_at (Î»x, (f x, g x)) s x :=
hf.prod_mk_nhds hg

lemma continuous_on.prod {f : Î± â†’ Î²} {g : Î± â†’ Î³} {s : set Î±}
  (hf : continuous_on f s) (hg : continuous_on g s) : continuous_on (Î»x, (f x, g x)) s :=
Î»x hx, continuous_within_at.prod (hf x hx) (hg x hx)

lemma inducing.continuous_within_at_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : inducing g) {s : set Î±}
  {x : Î±} : continuous_within_at f s x â†” continuous_within_at (g âˆ˜ f) s x :=
by simp_rw [continuous_within_at, inducing.tendsto_nhds_iff hg]

lemma inducing.continuous_on_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : inducing g) {s : set Î±} :
  continuous_on f s â†” continuous_on (g âˆ˜ f) s :=
by simp_rw [continuous_on, hg.continuous_within_at_iff]

lemma embedding.continuous_on_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : embedding g) {s : set Î±} :
  continuous_on f s â†” continuous_on (g âˆ˜ f) s :=
inducing.continuous_on_iff hg.1

lemma embedding.map_nhds_within_eq {f : Î± â†’ Î²} (hf : embedding f) (s : set Î±) (x : Î±) :
  map f (ğ“[s] x) = ğ“[f '' s] (f x) :=
by rw [nhds_within, map_inf hf.inj, hf.map_nhds_eq, map_principal, â† nhds_within_inter',
  inter_eq_self_of_subset_right (image_subset_range _ _)]

lemma open_embedding.map_nhds_within_preimage_eq {f : Î± â†’ Î²} (hf : open_embedding f)
  (s : set Î²) (x : Î±) :
  map f (ğ“[f â»Â¹' s] x) = ğ“[s] (f x) :=
begin
  rw [hf.to_embedding.map_nhds_within_eq, image_preimage_eq_inter_range],
  apply nhds_within_eq_nhds_within (mem_range_self _) hf.open_range,
  rw [inter_assoc, inter_self]
end

lemma continuous_within_at_of_not_mem_closure {f : Î± â†’ Î²} {s : set Î±} {x : Î±} :
  x âˆ‰ closure s â†’ continuous_within_at f s x :=
begin
  intros hx,
  rw [mem_closure_iff_nhds_within_ne_bot, ne_bot_iff, not_not] at hx,
  rw [continuous_within_at, hx],
  exact tendsto_bot,
end

lemma continuous_on.piecewise' {s t : set Î±} {f g : Î± â†’ Î²} [âˆ€ a, decidable (a âˆˆ t)]
  (hpf : âˆ€ a âˆˆ s âˆ© frontier t, tendsto f (ğ“[s âˆ© t] a) (ğ“ (piecewise t f g a)))
  (hpg : âˆ€ a âˆˆ s âˆ© frontier t, tendsto g (ğ“[s âˆ© tá¶œ] a) (ğ“ (piecewise t f g a)))
  (hf : continuous_on f $ s âˆ© t) (hg : continuous_on g $ s âˆ© tá¶œ) :
  continuous_on (piecewise t f g) s :=
begin
  intros x hx,
  by_cases hx' : x âˆˆ frontier t,
  { exact (hpf x âŸ¨hx, hx'âŸ©).piecewise_nhds_within (hpg x âŸ¨hx, hx'âŸ©) },
  { rw [â† inter_univ s, â† union_compl_self t, inter_union_distrib_left] at hx âŠ¢,
    cases hx,
    { apply continuous_within_at.union,
      { exact (hf x hx).congr (Î» y hy, piecewise_eq_of_mem _ _ _ hy.2)
          (piecewise_eq_of_mem _ _ _ hx.2) },
      { have : x âˆ‰ closure tá¶œ,
          from Î» h, hx' âŸ¨subset_closure hx.2, by rwa closure_compl at hâŸ©,
        exact continuous_within_at_of_not_mem_closure
          (Î» h, this (closure_inter_subset_inter_closure _ _ h).2) } },
    { apply continuous_within_at.union,
      { have : x âˆ‰ closure t,
          from (Î» h, hx' âŸ¨h, (Î» (h' : x âˆˆ interior t), hx.2 (interior_subset h'))âŸ©),
        exact continuous_within_at_of_not_mem_closure
          (Î» h, this (closure_inter_subset_inter_closure _ _ h).2) },
      { exact (hg x hx).congr
          (Î» y hy, piecewise_eq_of_not_mem _ _ _ hy.2)
          (piecewise_eq_of_not_mem _ _ _ hx.2) } } }
end

lemma continuous_on.if' {s : set Î±} {p : Î± â†’ Prop} {f g : Î± â†’ Î²} [âˆ€ a, decidable (p a)]
  (hpf : âˆ€ a âˆˆ s âˆ© frontier {a | p a},
    tendsto f (ğ“[s âˆ© {a | p a}] a) (ğ“ $ if p a then f a else g a))
  (hpg : âˆ€ a âˆˆ s âˆ© frontier {a | p a},
    tendsto g (ğ“[s âˆ© {a | Â¬p a}] a) (ğ“ $ if p a then f a else g a))
  (hf : continuous_on f $ s âˆ© {a | p a}) (hg : continuous_on g $ s âˆ© {a | Â¬p a}) :
  continuous_on (Î» a, if p a then f a else g a) s :=
hf.piecewise' hpf hpg hg

lemma continuous_on.if {Î± Î² : Type*} [topological_space Î±] [topological_space Î²] {p : Î± â†’ Prop}
  [âˆ€ a, decidable (p a)] {s : set Î±} {f g : Î± â†’ Î²}
  (hp : âˆ€ a âˆˆ s âˆ© frontier {a | p a}, f a = g a) (hf : continuous_on f $ s âˆ© closure {a | p a})
  (hg : continuous_on g $ s âˆ© closure {a | Â¬ p a}) :
  continuous_on (Î»a, if p a then f a else g a) s :=
begin
  apply continuous_on.if',
  { rintros a ha,
    simp only [â† hp a ha, if_t_t],
    apply tendsto_nhds_within_mono_left (inter_subset_inter_right s subset_closure),
    exact hf a âŸ¨ha.1, ha.2.1âŸ© },
  { rintros a ha,
    simp only [hp a ha, if_t_t],
    apply tendsto_nhds_within_mono_left (inter_subset_inter_right s subset_closure),
    rcases ha with âŸ¨has, âŸ¨_, haâŸ©âŸ©,
    rw [â† mem_compl_iff, â† closure_compl] at ha,
    apply hg a âŸ¨has, haâŸ© },
  { exact hf.mono (inter_subset_inter_right s subset_closure) },
  { exact hg.mono (inter_subset_inter_right s subset_closure) }
end

lemma continuous_on.piecewise {s t : set Î±} {f g : Î± â†’ Î²} [âˆ€ a, decidable (a âˆˆ t)]
  (ht : âˆ€ a âˆˆ s âˆ© frontier t, f a = g a) (hf : continuous_on f $ s âˆ© closure t)
  (hg : continuous_on g $ s âˆ© closure tá¶œ) :
  continuous_on (piecewise t f g) s :=
hf.if ht hg

lemma continuous_if' {p : Î± â†’ Prop} {f g : Î± â†’ Î²} [âˆ€ a, decidable (p a)]
  (hpf : âˆ€ a âˆˆ frontier {x | p x}, tendsto f (ğ“[{x | p x}] a) (ğ“ $ ite (p a) (f a) (g a)))
  (hpg : âˆ€ a âˆˆ frontier {x | p x}, tendsto g (ğ“[{x | Â¬p x}] a) (ğ“ $ ite (p a) (f a) (g a)))
  (hf : continuous_on f {x | p x}) (hg : continuous_on g {x | Â¬p x}) :
  continuous (Î» a, ite (p a) (f a) (g a)) :=
begin
  rw continuous_iff_continuous_on_univ,
  apply continuous_on.if'; simp *; assumption
end

lemma continuous_if {p : Î± â†’ Prop} {f g : Î± â†’ Î²} [âˆ€ a, decidable (p a)]
  (hp : âˆ€ a âˆˆ frontier {x | p x}, f a = g a) (hf : continuous_on f (closure {x | p x}))
  (hg : continuous_on g (closure {x | Â¬p x})) :
  continuous (Î» a, if p a then f a else g a) :=
begin
  rw continuous_iff_continuous_on_univ,
  apply continuous_on.if; simp; assumption
end

lemma continuous.if {p : Î± â†’ Prop} {f g : Î± â†’ Î²} [âˆ€ a, decidable (p a)]
  (hp : âˆ€ a âˆˆ frontier {x | p x}, f a = g a) (hf : continuous f) (hg : continuous g) :
  continuous (Î» a, if p a then f a else g a) :=
continuous_if hp hf.continuous_on hg.continuous_on

lemma continuous_piecewise {s : set Î±} {f g : Î± â†’ Î²} [âˆ€ a, decidable (a âˆˆ s)]
  (hs : âˆ€ a âˆˆ frontier s, f a = g a) (hf : continuous_on f (closure s))
  (hg : continuous_on g (closure sá¶œ)) :
  continuous (piecewise s f g) :=
continuous_if hs hf hg

lemma continuous.piecewise {s : set Î±} {f g : Î± â†’ Î²} [âˆ€ a, decidable (a âˆˆ s)]
  (hs : âˆ€ a âˆˆ frontier s, f a = g a) (hf : continuous f) (hg : continuous g) :
  continuous (piecewise s f g) :=
hf.if hs hg

lemma is_open.ite' {s s' t : set Î±}
  (hs : is_open s) (hs' : is_open s') (ht : âˆ€ x âˆˆ frontier t, x âˆˆ s â†” x âˆˆ s') :
  is_open (t.ite s s') :=
begin
  classical,
  simp only [is_open_iff_continuous_mem, set.ite] at *,
  convert continuous_piecewise (Î» x hx, propext (ht x hx)) hs.continuous_on hs'.continuous_on,
  ext x, by_cases hx : x âˆˆ t; simp [hx]
end

lemma is_open.ite {s s' t : set Î±} (hs : is_open s) (hs' : is_open s')
  (ht : s âˆ© frontier t = s' âˆ© frontier t) :
  is_open (t.ite s s') :=
hs.ite' hs' $ Î» x hx, by simpa [hx] using ext_iff.1 ht x

lemma ite_inter_closure_eq_of_inter_frontier_eq {s s' t : set Î±}
  (ht : s âˆ© frontier t = s' âˆ© frontier t) :
  t.ite s s' âˆ© closure t = s âˆ© closure t :=
by rw [closure_eq_self_union_frontier, inter_union_distrib_left, inter_union_distrib_left,
  ite_inter_self, ite_inter_of_inter_eq _ ht]

lemma ite_inter_closure_compl_eq_of_inter_frontier_eq {s s' t : set Î±}
  (ht : s âˆ© frontier t = s' âˆ© frontier t) :
  t.ite s s' âˆ© closure tá¶œ = s' âˆ© closure tá¶œ :=
by { rw [â† ite_compl, ite_inter_closure_eq_of_inter_frontier_eq], rwa [frontier_compl, eq_comm] }

lemma continuous_on_piecewise_ite' {s s' t : set Î±} {f f' : Î± â†’ Î²} [âˆ€ x, decidable (x âˆˆ t)]
  (h : continuous_on f (s âˆ© closure t)) (h' : continuous_on f' (s' âˆ© closure tá¶œ))
  (H : s âˆ© frontier t = s' âˆ© frontier t) (Heq : eq_on f f' (s âˆ© frontier t)) :
  continuous_on (t.piecewise f f') (t.ite s s') :=
begin
  apply continuous_on.piecewise,
  { rwa ite_inter_of_inter_eq _ H },
  { rwa ite_inter_closure_eq_of_inter_frontier_eq H },
  { rwa ite_inter_closure_compl_eq_of_inter_frontier_eq H }
end

lemma continuous_on_piecewise_ite {s s' t : set Î±} {f f' : Î± â†’ Î²} [âˆ€ x, decidable (x âˆˆ t)]
  (h : continuous_on f s) (h' : continuous_on f' s')
  (H : s âˆ© frontier t = s' âˆ© frontier t) (Heq : eq_on f f' (s âˆ© frontier t)) :
  continuous_on (t.piecewise f f') (t.ite s s') :=
continuous_on_piecewise_ite' (h.mono (inter_subset_left _ _)) (h'.mono (inter_subset_left _ _))
  H Heq

lemma continuous_on_fst {s : set (Î± Ã— Î²)} : continuous_on prod.fst s :=
continuous_fst.continuous_on

lemma continuous_within_at_fst {s : set (Î± Ã— Î²)} {p : Î± Ã— Î²} :
  continuous_within_at prod.fst s p :=
continuous_fst.continuous_within_at

lemma continuous_on.fst {f : Î± â†’ Î² Ã— Î³} {s : set Î±} (hf : continuous_on f s) :
  continuous_on (Î» x, (f x).1) s :=
continuous_fst.comp_continuous_on hf

lemma continuous_within_at.fst {f : Î± â†’ Î² Ã— Î³} {s : set Î±} {a : Î±}
  (h : continuous_within_at f s a) : continuous_within_at (Î» x, (f x).fst) s a :=
continuous_at_fst.comp_continuous_within_at h

lemma continuous_on_snd {s : set (Î± Ã— Î²)} : continuous_on prod.snd s :=
continuous_snd.continuous_on

lemma continuous_within_at_snd {s : set (Î± Ã— Î²)} {p : Î± Ã— Î²} :
  continuous_within_at prod.snd s p :=
continuous_snd.continuous_within_at

lemma continuous_on.snd {f : Î± â†’ Î² Ã— Î³} {s : set Î±} (hf : continuous_on f s) :
  continuous_on (Î» x, (f x).2) s :=
continuous_snd.comp_continuous_on hf

lemma continuous_within_at.snd {f : Î± â†’ Î² Ã— Î³} {s : set Î±} {a : Î±}
  (h : continuous_within_at f s a) : continuous_within_at (Î» x, (f x).snd) s a :=
continuous_at_snd.comp_continuous_within_at h

lemma continuous_within_at_prod_iff {f : Î± â†’ Î² Ã— Î³} {s : set Î±} {x : Î±} :
  continuous_within_at f s x â†” continuous_within_at (prod.fst âˆ˜ f) s x âˆ§
  continuous_within_at (prod.snd âˆ˜ f) s x :=
âŸ¨Î» h, âŸ¨h.fst, h.sndâŸ©, by { rintro âŸ¨h1, h2âŸ©, convert h1.prod h2, ext, refl, refl }âŸ©
