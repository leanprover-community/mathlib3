/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Anatole Dedecker
-/
import topology.separation

/-!
# Extending a function from a subset

The main definition of this file is `extend_from A f` where `f : X â†’ Y`
and `A : set X`. This defines a new function `g : X â†’ Y` which maps any
`xâ‚€ : X` to the limit of `f` as `x` tends to `xâ‚€`, if such a limit exists.

This is analoguous to the way `dense_inducing.extend` "extends" a function
`f : X â†’ Z` to a function `g : Y â†’ Z` along a dense inducing `i : X â†’ Y`.

The main theorem we prove about this definition is `continuous_on_extend_from`
which states that, for `extend_from A f` to be continuous on a set `B âŠ† closure A`,
it suffices that `f` converges within `A` at any point of `B`, provided that
`f` is a function to a regular space.

-/

noncomputable theory

open_locale topological_space
open filter set

variables {X Y : Type*} [topological_space X] [topological_space Y]

/-- Extend a function from a set `A`. The resulting function `g` is such that
at any `xâ‚€`, if `f` converges to some `y` as `x` tends to `xâ‚€` within `A`,
then `g xâ‚€` is defined to be one of these `y`. Else, `g xâ‚€` could be anything. -/
def extend_from (A : set X) (f : X â†’ Y) : X â†’ Y :=
Î» x, @@lim _ âŸ¨f xâŸ© (ğ“[A] x) f

/-- If `f` converges to some `y` as `x` tends to `xâ‚€` within `A`,
then `f` tends to `extend_from A f x` as `x` tends to `xâ‚€`. -/
lemma tendsto_extend_from {A : set X} {f : X â†’ Y} {x : X}
  (h : âˆƒ y, tendsto f (ğ“[A] x) (ğ“ y)) : tendsto f (ğ“[A] x) (ğ“ $ extend_from A f x) :=
tendsto_nhds_lim h

lemma extend_from_eq [t2_space Y] {A : set X} {f : X â†’ Y} {x : X} {y : Y} (hx : x âˆˆ closure A)
  (hf : tendsto f (ğ“[A] x) (ğ“ y)) : extend_from A f x = y :=
begin
  haveI := mem_closure_iff_nhds_within_ne_bot.mp hx,
  exact tendsto_nhds_unique (tendsto_nhds_lim âŸ¨y, hfâŸ©) hf,
end

lemma extend_from_extends [t2_space Y] {f : X â†’ Y} {A : set X} (hf : continuous_on f A) :
  âˆ€ x âˆˆ A, extend_from A f x = f x :=
Î» x x_in, extend_from_eq (subset_closure x_in) (hf x x_in)

/-- If `f` is a function to a regular space `Y` which has a limit within `A` at any
point of a set `B âŠ† closure A`, then `extend_from A f` is continuous on `B`. -/
lemma continuous_on_extend_from [regular_space Y] {f : X â†’ Y} {A B : set X} (hB : B âŠ† closure A)
  (hf : âˆ€ x âˆˆ B, âˆƒ y, tendsto f (ğ“[A] x) (ğ“ y)) : continuous_on (extend_from A f) B :=
begin
  set Ï† := extend_from A f,
  intros x x_in,
  suffices : âˆ€ V' âˆˆ ğ“ (Ï† x), is_closed V' â†’ Ï† â»Â¹' V' âˆˆ ğ“[B] x,
    by simpa [continuous_within_at, (closed_nhds_basis _).tendsto_right_iff],
  intros V' V'_in V'_closed,
  obtain âŸ¨V, V_in, V_op, hVâŸ© : âˆƒ V âˆˆ ğ“ x, is_open V âˆ§ V âˆ© A âŠ† f â»Â¹' V',
  { have := tendsto_extend_from (hf x x_in),
    rcases (nhds_within_basis_open x A).tendsto_left_iff.mp this V' V'_in with âŸ¨V, âŸ¨hxV, V_opâŸ©, hVâŸ©,
    use [V, is_open.mem_nhds V_op hxV, V_op, hV] },
  suffices : âˆ€ y âˆˆ V âˆ© B, Ï† y âˆˆ V',
    from mem_of_superset (inter_mem_inf V_in $ mem_principal_self B) this,
  rintros y âŸ¨hyV, hyBâŸ©,
  haveI := mem_closure_iff_nhds_within_ne_bot.mp (hB hyB),
  have limy : tendsto f (ğ“[A] y) (ğ“ $ Ï† y) := tendsto_extend_from (hf y hyB),
  have hVy : V âˆˆ ğ“ y := is_open.mem_nhds V_op hyV,
  have : V âˆ© A âˆˆ (ğ“[A] y),
    by simpa [inter_comm] using inter_mem_nhds_within _ hVy,
  exact V'_closed.mem_of_tendsto limy (mem_of_superset this hV)
end

/-- If a function `f` to a regular space `Y` has a limit within a
dense set `A` for any `x`, then `extend_from A f` is continuous. -/
lemma continuous_extend_from [regular_space Y] {f : X â†’ Y} {A : set X} (hA : dense A)
  (hf : âˆ€ x, âˆƒ y, tendsto f (ğ“[A] x) (ğ“ y)) : continuous (extend_from A f) :=
begin
  rw continuous_iff_continuous_on_univ,
  exact continuous_on_extend_from (Î» x _, hA x) (by simpa using hf)
end
