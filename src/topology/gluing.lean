/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import topology.category.Top
import category_theory.glue_data
import category_theory.concrete_category.elementwise

/-!
# Gluing Topological spaces

Given a family of gluing data (see `category_theory/glue_data`), we can then glue them together.

The construction should be "sealed" and considered as a black box, while only using the API
provided.

## Main definitions

* `Top.glue_data`: A structure containing the family of gluing data.
* `category_theory.glue_data.glued`: The glued topological space.
    This is defined as the multicoequalizer of `âˆ V i j â‡‰ âˆ U i`, so that the general colimit API
    can be used.
* `category_theory.glue_data.Î¹`: The immersion `Î¹ i : U i âŸ¶ glued` for each `i : Î¹`.
* `Top.glue_data.rel`: A relation on `Î£ i, D.U i` defined by `âŸ¨i, xâŸ© ~ âŸ¨j, yâŸ©` iff
    `âŸ¨i, xâŸ© = âŸ¨j, yâŸ©` or `t i j x = y`. See `Top.glue_data.Î¹_eq_iff_rel`.
* `Top.glue_data.mk`: A constructor of `glue_data` whose conditions are stated in terms of
  elements rather than subobjects and pullbacks.
* `Top.glue_data.of_open_subsets`: Given a family of open sets, we may glue them into a new
  topological space. This new space embeds into the original space, and is homeomorphic to it if
  the given family is an open cover (`Top.glue_data.open_cover_glue_homeo`).

## Main results

* `Top.glue_data.is_open_iff`: A set in `glued` is open iff its preimage along each `Î¹ i` is
    open.
* `Top.glue_data.Î¹_jointly_surjective`: The `Î¹ i`s are jointly surjective.
* `Top.glue_data.rel_equiv`: `rel` is an equivalence relation.
* `Top.glue_data.Î¹_eq_iff_rel`: `Î¹ i x = Î¹ j y â†” âŸ¨i, xâŸ© ~ âŸ¨j, yâŸ©`.
* `Top.glue_data.image_inter`: The intersection of the images of `U i` and `U j` in `glued` is
    `V i j`.
* `Top.glue_data.preimage_range`: The preimage of the image of `U i` in `U j` is `V i j`.
* `Top.glue_data.preimage_image_eq_preimage_f`: The preimage of the image of some `U âŠ† U i` is
    given by the preimage along `f j i`.
* `Top.glue_data.Î¹_open_embedding`: Each of the `Î¹ i`s are open embeddings.

-/

noncomputable theory

open topological_space category_theory

universes v u
open category_theory.limits
namespace Top

/--
A family of gluing data consists of
1. An index type `J`
2. An object `U i` for each `i : J`.
3. An object `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ Top` rather than `J â†’ J â†’ Top` to connect to the
  limits library easier.)
4. An open embedding `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
    (This merely means that `V i j âˆ© V i k âŠ† t i j â»Â¹' (V j i âˆ© V j k)`.)
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the topological spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.

Most of the times it would be easier to use the constructor `Top.glue_data.mk'` where the conditions
are stated in a less categorical way.
-/
@[nolint has_inhabited_instance]
structure glue_data extends glue_data Top :=
  (f_open : âˆ€ i j, open_embedding (f i j))
  (f_mono := Î» i j, (Top.mono_iff_injective _).mpr (f_open i j).to_embedding.inj)

namespace glue_data

variable (D : glue_data.{u})

local notation `ğ–£` := D.to_glue_data

lemma Ï€_surjective : function.surjective ğ–£ .Ï€ :=
(Top.epi_iff_surjective ğ–£ .Ï€).mp infer_instance

lemma is_open_iff (U : set ğ–£ .glued) : is_open U â†” âˆ€ i, is_open (ğ–£ .Î¹ i â»Â¹' U) :=
begin
  delta category_theory.glue_data.Î¹,
  simp_rw â† multicoequalizer.Î¹_sigma_Ï€ ğ–£ .diagram,
  rw â† (homeo_of_iso (multicoequalizer.iso_coequalizer ğ–£ .diagram).symm).is_open_preimage,
  rw [coequalizer_is_open_iff, colimit_is_open_iff.{u}],
  refl
end

lemma Î¹_jointly_surjective (x : ğ–£ .glued) : âˆƒ i (y : D.U i), ğ–£ .Î¹ i y = x :=
ğ–£ .Î¹_jointly_surjective (forget Top) x

/--
An equivalence relation on `Î£ i, D.U i` that holds iff `ğ–£ .Î¹ i x = ğ–£ .Î¹ j y`.
See `Top.glue_data.Î¹_eq_iff_rel`.
-/
def rel (a b : Î£ i, ((D.U i : Top) : Type*)) : Prop :=
  a = b âˆ¨ âˆƒ (x : D.V (a.1, b.1)) , D.f _ _ x = a.2 âˆ§ D.f _ _ (D.t _ _ x) = b.2

lemma rel_equiv : equivalence D.rel :=
âŸ¨ Î» x, or.inl (refl x),
  begin
    rintros a b (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨x,eâ‚,eâ‚‚âŸ©),
    exacts [or.inl rfl, or.inr âŸ¨D.t _ _ x, by simp [eâ‚, eâ‚‚]âŸ©]
  end,
  begin
    rintros âŸ¨i,aâŸ© âŸ¨j,bâŸ© âŸ¨k,câŸ© (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨x,eâ‚,eâ‚‚âŸ©), exact id,
    rintro (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨y,eâ‚ƒ,eâ‚„âŸ©), exact or.inr âŸ¨x,eâ‚,eâ‚‚âŸ©,
    let z := (pullback_iso_prod_subtype (D.f j i) (D.f j k)).inv âŸ¨âŸ¨_,_âŸ©, eâ‚‚.trans eâ‚ƒ.symmâŸ©,
    have eqâ‚ : (D.t j i) ((pullback.fst : _ âŸ¶ D.V _) z) = x := by simp,
    have eqâ‚‚ : (pullback.snd : _ âŸ¶ D.V _) z = y := pullback_iso_prod_subtype_inv_snd_apply _ _ _,
    clear_value z,
    right,
    use (pullback.fst : _ âŸ¶ D.V (i, k)) (D.t' _ _ _ z),
    dsimp only at *,
    substs eâ‚ eâ‚ƒ eâ‚„ eqâ‚ eqâ‚‚,
    have hâ‚ : D.t' j i k â‰« pullback.fst â‰« D.f i k = pullback.fst â‰« D.t j i â‰« D.f i j,
    { rw â† ğ–£ .t_fac_assoc, congr' 1, exact pullback.condition },
    have hâ‚‚ : D.t' j i k â‰« pullback.fst â‰« D.t i k â‰« D.f k i =
      pullback.snd â‰« D.t j k â‰« D.f k j,
    { rw â† ğ–£ .t_fac_assoc,
      apply @epi.left_cancellation _ _ _ _ (D.t' k j i),
      rw [ğ–£ .cocycle_assoc, ğ–£ .t_fac_assoc, ğ–£ .t_inv_assoc],
      exact pullback.condition.symm },
    exact âŸ¨continuous_map.congr_fun hâ‚ z, continuous_map.congr_fun hâ‚‚ zâŸ©
  endâŸ©

open category_theory.limits.walking_parallel_pair

lemma eqv_gen_of_Ï€_eq {x y : âˆ D.U} (h : ğ–£ .Ï€ x = ğ–£ .Ï€ y) :
  eqv_gen (types.coequalizer_rel ğ–£ .diagram.fst_sigma_map ğ–£ .diagram.snd_sigma_map) x y :=
begin
  delta glue_data.Ï€ multicoequalizer.sigma_Ï€ at h,
  simp_rw comp_app at h,
  replace h := (Top.mono_iff_injective (multicoequalizer.iso_coequalizer ğ–£ .diagram).inv).mp _ h,
  let diagram := parallel_pair ğ–£ .diagram.fst_sigma_map ğ–£ .diagram.snd_sigma_map â‹™ forget _,
  have : colimit.Î¹ diagram one x = colimit.Î¹ diagram one y,
  { rw â†Î¹_preserves_colimits_iso_hom,
    simp [h] },
  have :
    (colimit.Î¹ diagram _ â‰« colim.map _ â‰« (colimit.iso_colimit_cocone _).hom) _ =
    (colimit.Î¹ diagram _ â‰« colim.map _ â‰« (colimit.iso_colimit_cocone _).hom) _ :=
    (congr_arg (colim.map (diagram_iso_parallel_pair diagram).hom
    â‰« (colimit.iso_colimit_cocone (types.coequalizer_colimit _ _)).hom) this : _),
  simp only [eq_to_hom_refl, types_comp_apply, colimit.Î¹_map_assoc,
    diagram_iso_parallel_pair_hom_app, colimit.iso_colimit_cocone_Î¹_hom, types_id_apply] at this,
  exact quot.eq.1 this,
  apply_instance
end

lemma Î¹_eq_iff_rel (i j : D.J) (x : D.U i) (y : D.U j) :
  ğ–£ .Î¹ i x = ğ–£ .Î¹ j y â†” D.rel âŸ¨i, xâŸ© âŸ¨j, yâŸ© :=
begin
  split,
  { delta glue_data.Î¹,
    simp_rw â† multicoequalizer.Î¹_sigma_Ï€,
    intro h,
    rw â† (show _ = sigma.mk i x,
      from concrete_category.congr_hom (sigma_iso_sigma.{u} D.U).inv_hom_id _),
    rw â† (show _ = sigma.mk j y,
      from concrete_category.congr_hom (sigma_iso_sigma.{u} D.U).inv_hom_id _),
    change inv_image D.rel (sigma_iso_sigma.{u} D.U).hom _ _,
    simp only [Top.sigma_iso_sigma_inv_apply],
    rw â† (inv_image.equivalence _ _ D.rel_equiv).eqv_gen_iff,
    refine eqv_gen.mono _ (D.eqv_gen_of_Ï€_eq h : _),
    rintros _ _ âŸ¨xâŸ©,
    rw â† (show (sigma_iso_sigma.{u} _).inv _ = x,
      from concrete_category.congr_hom (sigma_iso_sigma.{u} _).hom_inv_id x),
    generalize : (sigma_iso_sigma.{u} D.V).hom x = x',
    obtain âŸ¨âŸ¨i,jâŸ©,yâŸ© := x',
    unfold inv_image multispan_index.fst_sigma_map multispan_index.snd_sigma_map,
    simp only [opens.inclusion_to_fun, Top.comp_app, sigma_iso_sigma_inv_apply,
      category_theory.limits.colimit.Î¹_desc_apply, cofan.mk_Î¹_app,
      sigma_iso_sigma_hom_Î¹_apply, continuous_map.to_fun_eq_coe],
    erw [sigma_iso_sigma_hom_Î¹_apply, sigma_iso_sigma_hom_Î¹_apply],
    exact or.inr âŸ¨y, by { dsimp [glue_data.diagram], simp }âŸ© },
  { rintro (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨z,eâ‚,eâ‚‚âŸ©),
    refl, dsimp only at *, subst eâ‚, subst eâ‚‚, simp }
end

lemma Î¹_injective (i : D.J) : function.injective (ğ–£ .Î¹ i) :=
begin
  intros x y h,
  rcases (D.Î¹_eq_iff_rel _ _ _ _).mp h with (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨_,eâ‚,eâ‚‚âŸ©),
  { refl },
  { dsimp only at *, cases eâ‚, cases eâ‚‚, simp }
end

instance Î¹_mono (i : D.J) : mono (ğ–£ .Î¹ i) :=
(Top.mono_iff_injective _).mpr (D.Î¹_injective _)

lemma image_inter (i j : D.J) :
  set.range (ğ–£ .Î¹ i) âˆ© set.range (ğ–£ .Î¹ j) = set.range (D.f i j â‰« ğ–£ .Î¹ _) :=
begin
  ext x,
  split,
  { rintro âŸ¨âŸ¨xâ‚, eqâ‚âŸ©, âŸ¨xâ‚‚, eqâ‚‚âŸ©âŸ©,
    obtain (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨y,eâ‚,eâ‚‚âŸ©) := (D.Î¹_eq_iff_rel _ _ _ _).mp (eqâ‚.trans eqâ‚‚.symm),
    { exact âŸ¨inv (D.f i i) xâ‚, by simp [eqâ‚]âŸ© },
    { dsimp only at *, substs eâ‚ eqâ‚, exact âŸ¨y, by simpâŸ© } },
  { rintro âŸ¨x, hxâŸ©,
    exact âŸ¨âŸ¨D.f i j x, hxâŸ©, âŸ¨D.f j i (D.t _ _ x), by simp [â† hx]âŸ©âŸ© }
end

lemma preimage_range (i j : D.J) :
  ğ–£ .Î¹ j â»Â¹' (set.range (ğ–£ .Î¹ i)) = set.range (D.f j i) :=
by rw [â† set.preimage_image_eq (set.range (D.f j i)) (D.Î¹_injective j), â† set.image_univ,
      â† set.image_univ, â†set.image_comp, â†coe_comp, set.image_univ,set.image_univ,
      â† image_inter, set.preimage_range_inter]

lemma preimage_image_eq_image (i j : D.J) (U : set (ğ–£ .U i)) :
  ğ–£ .Î¹ j â»Â¹' (ğ–£ .Î¹ i '' U) = D.f _ _ '' ((D.t j i â‰« D.f _ _) â»Â¹' U) :=
begin
  have : D.f _ _ â»Â¹' (ğ–£ .Î¹ j â»Â¹' (ğ–£ .Î¹ i '' U)) = (D.t j i â‰« D.f _ _) â»Â¹' U,
  { ext x,
    conv_rhs { rw â† set.preimage_image_eq U (D.Î¹_injective _) },
    generalize : ğ–£ .Î¹ i '' U = U',
    simp },
  rw [â† this, set.image_preimage_eq_inter_range],
  symmetry,
  apply set.inter_eq_self_of_subset_left,
  rw â† D.preimage_range i j,
  exact set.preimage_mono (set.image_subset_range _ _),
end

lemma preimage_image_eq_image' (i j : D.J) (U : set (ğ–£ .U i)) :
ğ–£ .Î¹ j â»Â¹' (ğ–£ .Î¹ i '' U) = (D.t i j â‰« D.f _ _) '' ((D.f _ _) â»Â¹' U) :=
begin
  convert D.preimage_image_eq_image i j U using 1,
  rw [coe_comp, coe_comp, â† set.image_image],
  congr' 1,
  rw [â† set.eq_preimage_iff_image_eq, set.preimage_preimage],
  change _ = (D.t i j â‰« D.t j i â‰« _) â»Â¹' _,
  rw ğ–£ .t_inv_assoc,
  rw â† is_iso_iff_bijective,
  apply (forget Top).map_is_iso
end

lemma open_image_open (i : D.J) (U : opens (ğ–£ .U i)) : is_open (ğ–£ .Î¹ i '' U) :=
begin
  rw is_open_iff,
  intro j,
  rw preimage_image_eq_image,
  apply (D.f_open _ _).is_open_map,
  apply (D.t j i â‰« D.f i j).continuous_to_fun.is_open_preimage,
  exact U.property
end

lemma Î¹_open_embedding (i : D.J) : open_embedding (ğ–£ .Î¹ i) :=
open_embedding_of_continuous_injective_open
  (ğ–£ .Î¹ i).continuous_to_fun (D.Î¹_injective i) (Î» U h, D.open_image_open i âŸ¨U, hâŸ©)

/--
A family of gluing data consists of
1. An index type `J`
2. A bundled topological space `U i` for each `i : J`.
3. An open set `V i j âŠ† U i` for each `i j : J`.
4. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `V i i = U i`.
7. `t i i` is the identity.
8. For each `x âˆˆ V i j âˆ© V i k`, `t i j x âˆˆ V j k`.
9. `t j k (t i j x) = t i k x`.

We can then glue the topological spaces `U i` together by identifying `V i j` with `V j i`.
-/
@[nolint has_inhabited_instance]
structure mk_core :=
{J : Type u}
(U : J â†’ Top.{u})
(V : Î  i, J â†’ opens (U i))
(t : Î  i j, (opens.to_Top _).obj (V i j) âŸ¶ (opens.to_Top _).obj (V j i))
(V_id : âˆ€ i, V i i = âŠ¤)
(t_id : âˆ€ i, â‡‘(t i i) = id)
(t_inter : âˆ€ â¦ƒi jâ¦„ k (x : V i j), â†‘x âˆˆ V i k â†’ @coe (V j i) (U j) _ (t i j x) âˆˆ V j k)
(cocycle : âˆ€ i j k (x : V i j) (h : â†‘x âˆˆ V i k),
  @coe (V k j) (U k) _ (t j k âŸ¨â†‘(t i j x), t_inter k x hâŸ©) = @coe (V k i) (U k) _ (t i k âŸ¨x, hâŸ©))

lemma mk_core.t_inv (h : mk_core) (i j : h.J) (x : h.V j i) : h.t i j ((h.t j i) x) = x :=
begin
  have := h.cocycle j i j x _,
  rw h.t_id at this,
  convert subtype.eq this,
  { ext, refl },
  all_goals { rw h.V_id, trivial }
end

instance (h : mk_core.{u}) (i j : h.J) : is_iso (h.t i j) :=
by { use h.t j i, split; ext1, exacts [h.t_inv _ _ _, h.t_inv _ _ _] }

/-- (Implementation) the restricted transition map to be fed into `glue_data`. -/
def mk_core.t' (h : mk_core.{u}) (i j k : h.J) : pullback (h.V i j).inclusion (h.V i k).inclusion âŸ¶
  pullback (h.V j k).inclusion (h.V j i).inclusion :=
begin
  refine (pullback_iso_prod_subtype _ _).hom â‰« âŸ¨_, _âŸ© â‰« (pullback_iso_prod_subtype _ _).inv,
  { intro x,
    refine âŸ¨âŸ¨âŸ¨(h.t i j x.1.1).1, _âŸ©, h.t i j x.1.1âŸ©, rflâŸ©,
    rcases x with âŸ¨âŸ¨âŸ¨x, hxâŸ©, âŸ¨x', hx'âŸ©âŸ©, (rfl : x = x')âŸ©,
    exact h.t_inter _ âŸ¨x, hxâŸ© hx' },
  continuity,
end

/-- This is a constructor of `Top.glue_data` whose arguments are in terms of elements and
intersections rather than subobjects and pullbacks. Please refer to `Top.glue_data.mk_core` for
details. -/
def mk' (h : mk_core.{u}) : Top.glue_data :=
{ J := h.J,
  U := h.U,
  V := Î» i, (opens.to_Top _).obj (h.V i.1 i.2),
  f := Î» i j, (h.V i j).inclusion ,
  f_id := Î» i, (h.V_id i).symm â–¸ is_iso.of_iso (opens.inclusion_top_iso (h.U i)),
  f_open := Î» (i j : h.J), (h.V i j).open_embedding,
  t := h.t,
  t_id := Î» i, by { ext, rw h.t_id, refl },
  t' := h.t',
  t_fac := Î» i j k,
  begin
    delta mk_core.t',
    rw [category.assoc, category.assoc, pullback_iso_prod_subtype_inv_snd, â† iso.eq_inv_comp,
      pullback_iso_prod_subtype_inv_fst_assoc],
    ext âŸ¨âŸ¨âŸ¨x, hxâŸ©, âŸ¨x', hx'âŸ©âŸ©, (rfl : x = x')âŸ©,
    refl,
  end,
  cocycle := Î» i j k,
  begin
    delta mk_core.t',
    simp_rw â† category.assoc,
    rw iso.comp_inv_eq,
    simp only [iso.inv_hom_id_assoc, category.assoc, category.id_comp],
    rw [â† iso.eq_inv_comp, iso.inv_hom_id],
    ext1 âŸ¨âŸ¨âŸ¨x, hxâŸ©, âŸ¨x', hx'âŸ©âŸ©, (rfl : x = x')âŸ©,
    simp only [Top.comp_app, continuous_map.coe_mk, prod.mk.inj_iff,
      Top.id_app, subtype.mk_eq_mk, subtype.coe_mk],
    rw [â† subtype.coe_injective.eq_iff, subtype.val_eq_coe, subtype.coe_mk, and_self],
    convert congr_arg coe (h.t_inv k i âŸ¨x, hx'âŸ©) using 3,
    ext,
    exact h.cocycle i j k âŸ¨x, hxâŸ© hx',
  end }
.

variables {Î± : Type u} [topological_space Î±] {J : Type u} (U : J â†’ opens Î±)

include U

/-- We may construct a glue data from a family of open sets. -/
@[simps to_glue_data_J to_glue_data_U to_glue_data_V to_glue_data_t to_glue_data_f]
def of_open_subsets : Top.glue_data.{u} := mk'.{u}
{ J := J,
  U := Î» i, (opens.to_Top $ Top.of Î±).obj (U i),
  V := Î» i j, (opens.map $ opens.inclusion _).obj (U j),
  t := Î» i j, âŸ¨Î» x, âŸ¨âŸ¨x.1.1, x.2âŸ©, x.1.2âŸ©, by continuityâŸ©,
  V_id := Î» i, by { ext, cases U i, simp },
  t_id := Î» i, by { ext, refl },
  t_inter := Î» i j k x hx, hx,
  cocycle := Î» i j k x h, rfl }

/--
The canonical map from the glue of a family of open subsets `Î±` into `Î±`.
This map is an open embedding (`from_open_subsets_glue_open_embedding`),
and its range is `â‹ƒ i, (U i : set Î±)` (`range_from_open_subsets_glue`).
-/
def from_open_subsets_glue : (of_open_subsets U).to_glue_data.glued âŸ¶ Top.of Î± :=
multicoequalizer.desc _ _ (Î» x, opens.inclusion _) (by { rintro âŸ¨i, jâŸ©, ext x, refl })

@[simp, elementwise]
lemma Î¹_from_open_subsets_glue (i : J) :
  (of_open_subsets U).to_glue_data.Î¹ i â‰« from_open_subsets_glue U = opens.inclusion _ :=
multicoequalizer.Ï€_desc _ _ _ _ _

lemma from_open_subsets_glue_injective : function.injective (from_open_subsets_glue U) :=
begin
  intros x y e,
  obtain âŸ¨i, âŸ¨x, hxâŸ©, rflâŸ© := (of_open_subsets U).Î¹_jointly_surjective x,
  obtain âŸ¨j, âŸ¨y, hyâŸ©, rflâŸ© := (of_open_subsets U).Î¹_jointly_surjective y,
  rw [Î¹_from_open_subsets_glue_apply, Î¹_from_open_subsets_glue_apply] at e,
  change x = y at e,
  subst e,
  rw (of_open_subsets U).Î¹_eq_iff_rel,
  right,
  exact âŸ¨âŸ¨âŸ¨x, hxâŸ©, hyâŸ©, rfl, rflâŸ©,
end

lemma from_open_subsets_glue_is_open_map : is_open_map (from_open_subsets_glue U) :=
begin
  intros s hs,
  rw (of_open_subsets U).is_open_iff at hs,
  rw is_open_iff_forall_mem_open,
  rintros _ âŸ¨x, hx, rflâŸ©,
  obtain âŸ¨i, âŸ¨x, hx'âŸ©, rflâŸ© := (of_open_subsets U).Î¹_jointly_surjective x,
  use from_open_subsets_glue U '' s âˆ© set.range (@opens.inclusion (Top.of Î±) (U i)),
  use set.inter_subset_left _ _,
  split,
  { erw â† set.image_preimage_eq_inter_range,
    apply (@opens.open_embedding (Top.of Î±) (U i)).is_open_map,
    convert hs i using 1,
    rw [â† Î¹_from_open_subsets_glue, coe_comp, set.preimage_comp],
    congr' 1,
    refine set.preimage_image_eq _ (from_open_subsets_glue_injective U) },
  { refine âŸ¨set.mem_image_of_mem _ hx, _âŸ©,
    rw Î¹_from_open_subsets_glue_apply,
    exact set.mem_range_self _ },
end

lemma from_open_subsets_glue_open_embedding : open_embedding (from_open_subsets_glue U) :=
open_embedding_of_continuous_injective_open (continuous_map.continuous_to_fun _)
  (from_open_subsets_glue_injective U) (from_open_subsets_glue_is_open_map U)

lemma range_from_open_subsets_glue : set.range (from_open_subsets_glue U) = â‹ƒ i, (U i : set Î±) :=
begin
  ext,
  split,
  { rintro âŸ¨x, rflâŸ©,
    obtain âŸ¨i, âŸ¨x, hx'âŸ©, rflâŸ© := (of_open_subsets U).Î¹_jointly_surjective x,
    rw Î¹_from_open_subsets_glue_apply,
    exact set.subset_Union _ i hx' },
  { rintro âŸ¨_, âŸ¨i, rflâŸ©, hxâŸ©,
    refine âŸ¨(of_open_subsets U).to_glue_data.Î¹ i âŸ¨x, hxâŸ©, Î¹_from_open_subsets_glue_apply _ _ _âŸ© }
end

/-- The gluing of an open cover is homeomomorphic to the original space. -/
def open_cover_glue_homeo (h : (â‹ƒ i, (U i : set Î±)) = set.univ) :
  (of_open_subsets U).to_glue_data.glued â‰ƒâ‚œ Î± :=
homeomorph.homeomorph_of_continuous_open
  (equiv.of_bijective (from_open_subsets_glue U)
    âŸ¨from_open_subsets_glue_injective U,
      set.range_iff_surjective.mp ((range_from_open_subsets_glue U).symm â–¸ h)âŸ©)
  (from_open_subsets_glue U).2 (from_open_subsets_glue_is_open_map U)

end glue_data

end Top
