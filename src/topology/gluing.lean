/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import topology.category.Top
import category_theory.limits.concrete_category

/-!
# Gluing Topological spaces

Given a family of gluing data, consisting of
1. An index type `Î¹`
2. A topological space `U i` for each `i : Î¹`.
3. An open immersion `f i j : V i j â†ª U i` for each `i j : Î¹`.
4. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i : V i i â†ª U i` is iso.
7. Each `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i` via some
   `t' i j k : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
8. The cocycle condition `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™`.

We can then glue the topological spaces `U i` along `V i j`.

The construction should be "sealed" and considered as a black box, while only using the API
provided.

## Main definitions

* `Top.gluing_data`: A structure containing the family of gluing data.
* `Top.gluing_data.glued`: The glued topological space.
    This is defined as the coequalizer of `âˆ V i j â‡‰ âˆ U i`, so that the general colimit API can
    be used.
* `Top.gluing_data.imm`: The immersion `imm i : U i âŸ¶ glued` for each `i : Î¹`.
* `Top.gluing_data.rel`: A relation on `Î£ i, D.U i` defined by `âŸ¨i, xâŸ© ~ âŸ¨j, yâŸ©` iff
    `âŸ¨i, xâŸ© = âŸ¨j, yâŸ©` or `t i j x = y`. See `Top.gluing_data.imm_eq_iff_rel`.

## Main results

* `Top.gluing_data.is_open_iff`: A set in `glued` is open iff its preimage along each `imm i` is
    open.
* `Top.gluing_data.imm_jointly_surjective`: The `imm i`s are jointly surjective.
* `Top.gluing_data.glue_condition` : `f i j â‰« imm j = imm i`.
* `Top.gluing_data.rel_equiv`: `rel` is an equivalence relation.
* `Top.gluing_data.imm_eq_iff_rel`: `imm i x = imm j y â†” âŸ¨i, xâŸ© ~ âŸ¨j, yâŸ©`.
* `Top.gluing_data.image_inter`: The intersection of the images of `U i` and `U j` in `glued` is
    `V i j`.
* `Top.gluing_data.preimage_range`: The preimage of the image of `U i` in `U j` is `V i j`.
* `Top.gluing_data.preimage_image_eq_preimage_f`: The preimage of the image of some `U âŠ† U i` is
    given by the preimage along `f j i`.
* `Top.gluing_data.imm_open_embedding`: Each of the `imm i`s are open embeddings.

-/

noncomputable theory

open topological_space category_theory

universes v u
open category_theory.limits
namespace Top

/--
A family of gluing data consists of
1. An index type `Î¹`
2. A topological space `U i` for each `i : Î¹`.
3. An open immersion `f i j : V i j â†ª U i` for each `i j : Î¹`.
4. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i : V i i â†ª U i` is iso.
7. Each `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i` via some
   `t' i j k : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
8. The cocycle condition `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™`.

We can then glue the topological spaces `U i` along `V i j`.
-/
@[nolint has_inhabited_instance]
structure glue_data : Type (u+1) :=
  (Î¹ : Type u)
  (U : Î¹ â†’ Top.{u})
  (V : Î¹ Ã— Î¹ â†’ Top.{u})
  (f : Î  i j, V (i, j) âŸ¶ U i)
  (f_open : âˆ€ i j, open_embedding (f i j))
  (f_id : âˆ€ i, is_iso (f i i))
  (t : Î  i j, V (i, j) âŸ¶ V (j, i))
  (t_id : âˆ€ i, t i i = ğŸ™ _)
  (t' : Î  i j k, pullback (f i j) (f i k) âŸ¶ pullback (f j k) (f j i))
  (t_fac : âˆ€ i j k, t' i j k â‰« pullback.snd = pullback.fst â‰« t i j)
  (cocycle : âˆ€ i j k , t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _)

attribute [simp] glue_data.t_id
attribute [instance] glue_data.f_id
attribute [reassoc] glue_data.t_fac glue_data.cocycle

namespace glue_data

variable (D : glue_data.{u})

instance (i j : D.Î¹) : mono (D.f i j) :=
(Top.mono_iff_injective _).mpr (D.f_open i j).to_embedding.inj

@[simp] lemma t'_iij (i j : D.Î¹) : D.t' i i j = (pullback_symmetry _ _).hom :=
begin
  have eqâ‚ := D.t_fac i i j,
  have eqâ‚‚ := (is_iso.eq_comp_inv (D.f i i)).mpr (@pullback.condition _ _ _ _ _ _ (D.f i j) _),
  rw [D.t_id, category.comp_id, eqâ‚‚] at eqâ‚,
  have eqâ‚ƒ := (is_iso.eq_comp_inv (D.f i i)).mp eqâ‚,
  rw [category.assoc, â†pullback.condition, â†category.assoc] at eqâ‚ƒ,
  exact mono.right_cancellation _ _
    ((mono.right_cancellation _ _ eqâ‚ƒ).trans (pullback_symmetry_hom_comp_fst _ _).symm)
end

lemma t'_jii (i j : D.Î¹) : D.t' j i i = pullback.fst â‰« D.t j i â‰« inv pullback.snd :=
by { rw [â†category.assoc, â†D.t_fac], simp }

lemma t'_iji (i j : D.Î¹) : D.t' i j i = pullback.fst â‰« D.t i j â‰« inv pullback.snd :=
by { rw [â†category.assoc, â†D.t_fac], simp }

@[simp, reassoc, elementwise] lemma t_inv (i j : D.Î¹) :
  D.t i j â‰« D.t j i = ğŸ™ _ :=
begin
  have eq : (pullback_symmetry (D.f i i) (D.f i j)).hom = pullback.snd â‰« inv pullback.fst,
  simp,
  have := D.cocycle i j i,
  rw [D.t'_iij, D.t'_jii, D.t'_iji, fst_eq_snd_of_mono_eq, eq] at this,
  simp only [category.assoc, is_iso.inv_hom_id_assoc] at this,
  rw [â†is_iso.eq_inv_comp, â†category.assoc, is_iso.comp_inv_eq] at this,
  simpa using this,
end

instance t_is_iso (i j : D.Î¹) : is_iso (D.t i j) :=
âŸ¨âŸ¨D.t j i, D.t_inv _ _, D.t_inv _ _âŸ©âŸ©

instance t'_is_iso (i j k : D.Î¹) : is_iso (D.t' i j k) :=
âŸ¨âŸ¨D.t' j k i â‰« D.t' k i j, D.cocycle _ _ _, D.cocycle _ _ _âŸ©âŸ©

/-- (Implementation) The disjoint union of `U i`. -/
def sigma_opens : Top := âˆ D.U

/-- (Implementation) The disjoint union of `V i j`. -/
def sigma_inters : Top := âˆ D.V

/-- (Implementation) The projection `âˆ D.inters âŸ¶ âˆ D.U` via left projection. -/
def left_imm : D.sigma_inters âŸ¶ D.sigma_opens :=
sigma.desc (Î» âŸ¨i, jâŸ©, D.f i j â‰« sigma.Î¹ D.U i)

/-- (Implementation) The projection `âˆ D.inters âŸ¶ âˆ D.U` via right projection. -/
def right_imm : D.sigma_inters âŸ¶ D.sigma_opens :=
sigma.desc (Î» âŸ¨i, jâŸ©, D.t i j â‰« D.f j i â‰« sigma.Î¹ D.U j)

/-- (Implementation) The diagram to take colimit of. -/
def diagram := parallel_pair D.left_imm D.right_imm

/-- The glued topological space given a family of gluing data. -/
def glued : Top :=
coequalizer D.left_imm D.right_imm

/-- (Implementation) The projection `âˆ D.U âŸ¶ D.glued` given by the colimit. -/
def Ï€ : D.sigma_opens âŸ¶ D.glued :=
coequalizer.Ï€ _ _

instance Ï€_epi : epi D.Ï€ :=
coequalizer.Ï€_epi

lemma Ï€_surjective : function.surjective D.Ï€ :=
(Top.epi_iff_surjective D.Ï€).mp infer_instance

/-- The open immersion `D.U i âŸ¶ D.glued` for each `i`. -/
def imm (i : D.Î¹) : D.U i âŸ¶ D.glued :=
sigma.Î¹ _ _ â‰« D.Ï€

lemma is_open_iff (U : set D.glued) : is_open U â†” âˆ€ i, is_open (D.imm i â»Â¹' U) :=
by { rw [coequalizer_is_open_iff, colimit_is_open_iff], refl }

lemma imm_jointly_surjective (x : D.glued) : âˆƒ i (y : D.U i), D.imm i y = x :=
begin
  rcases D.Ï€_surjective x with âŸ¨x', rflâŸ©,
  rw â† (show (sigma_iso_sigma _).inv _ = x', from congr_fun (sigma_iso_sigma _).hom_inv_id x'),
  rcases (sigma_iso_sigma _).hom x' with âŸ¨i, yâŸ©,
  exact âŸ¨i, y, by simpaâŸ©
end

@[simp, elementwise]
lemma glue_condition (i j : D.Î¹) :
  D.t i j â‰« D.f j i â‰« D.imm j = D.f i j â‰« D.imm i :=
begin
  ext x,
  symmetry,
  simpa [Ï€, left_imm, right_imm] using continuous_map.congr_fun
    (coequalizer.condition D.left_imm D.right_imm) ((sigma.Î¹ D.V (i, j) : _) x),
end

/--
Given two continuous maps `f : X âŸ¶ Z`, `g : Y âŸ¶ Z`, and two elements `x : X`, `y : Y` such
that `f x = g y`, we may obtain an element in `X Ã—[Z] Y` whose projection onto `X` and `Y` are
`x` and `y`, respectively.
-/
def pullback_preimage {X Y Z : Top.{v}} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (x : X) (y : Y)
  (h : f x = g y) : (pullback f g : Top) :=
(limit.is_limit (cospan _ _)).lift
  (@pullback_cone.mk Top _ _ _ _ f g âŸ¨punitâŸ©
    âŸ¨Î» _, x, by continuityâŸ© âŸ¨Î» _, y, by continuityâŸ©
    (by { ext a, cases a, simp[h] })) punit.star

@[simp] lemma pullback_preimage_fst {X Y Z : Top.{v}} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (x : X) (y : Y)
  (h : f x = g y) :
  (pullback.fst : pullback f g âŸ¶ _) (pullback_preimage f g x y h) = x :=
by { unfold pullback_preimage, simp }

@[simp] lemma pullback_preimage_snd {X Y Z : Top.{v}} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (x : X) (y : Y)
  (h : f x = g y) :
  (pullback.snd : pullback f g âŸ¶ _) (pullback_preimage f g x y h) = y :=
by { unfold pullback_preimage, simp }

/--
 An equivalence relation on `Î£ i, D.U i` that holds iff `D.imm i x = D.imm j y`.
 See `Top.gluing_data.imm_eq_iff_rel`.
 -/
def rel (a b : Î£ i, D.U i) : Prop :=
  a = b âˆ¨ âˆƒ (x : D.V (a.1, b.1)) , D.f _ _ x = a.2 âˆ§ D.f _ _ (D.t _ _ x) = b.2

lemma rel_equiv : equivalence D.rel :=
âŸ¨ Î» x, or.inl (refl x),
  Î» a b h,
  begin
    rcases h with (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨x,eâ‚,eâ‚‚âŸ©), exact or.inl rfl,
    right,
    use (D.t _ _ x), simp[eâ‚, eâ‚‚]
  end,
  begin
    rintros âŸ¨i,aâŸ© âŸ¨j,bâŸ© âŸ¨k,câŸ© (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨x,eâ‚,eâ‚‚âŸ©), exact id,
    rintro (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨y,eâ‚ƒ,eâ‚„âŸ©), exact or.inr âŸ¨x,eâ‚,eâ‚‚âŸ©,
    let z : (pullback (D.f j i) (D.f j k) : Top) :=
      pullback_preimage (D.f j i) (D.f j k) (D.t i j x) y (eâ‚‚.trans eâ‚ƒ.symm),
    have eqâ‚ : (D.t j i) ((pullback.fst : _ âŸ¶ D.V _) z) = x := by simp,
    have eqâ‚‚ : (pullback.snd : _ âŸ¶ D.V _) z = _ := pullback_preimage_snd _ _ _ _ _,
    clear_value z,
    right,
    use (pullback.fst : _ âŸ¶ D.V (i, k)) (D.t' _ _ _ z),
    dsimp only at *,
    cases eâ‚, cases eâ‚ƒ, cases eâ‚„, cases eqâ‚, cases eqâ‚‚, simp,
    have hâ‚ : D.t' j i k â‰« pullback.fst â‰« D.f i k = pullback.fst â‰« D.t j i â‰« D.f i j,
    { rw â†D.t_fac_assoc, congr' 1, exact pullback.condition },
    have hâ‚‚ : D.t' j i k â‰« pullback.fst â‰« D.t i k â‰« D.f k i =
      pullback.snd â‰« D.t j k â‰« D.f k j,
    { rw â†D.t_fac_assoc,
      apply @epi.left_cancellation _ _ _ _ (D.t' k j i),
      rw [D.cocycle_assoc, D.t_fac_assoc, D.t_inv_assoc],
      exact pullback.condition.symm },
    exact âŸ¨continuous_map.congr_fun hâ‚ z, continuous_map.congr_fun hâ‚‚ zâŸ©
  endâŸ©

open category_theory.limits.walking_parallel_pair

lemma eqv_gen_of_Ï€_eq {x y : âˆ D.U} (h : D.Ï€ x = D.Ï€ y) :
  eqv_gen (types.coequalizer_rel (D.left_imm) (D.right_imm)) x y :=
begin
  change colimit.Î¹ D.diagram one x = colimit.Î¹ D.diagram one y at h,
  have : colimit.Î¹ (D.diagram â‹™ forget _) one x = colimit.Î¹ (D.diagram â‹™ forget _) one y,
  { rw â†Î¹_preserves_colimits_iso_hom,
    simp[h] },
  have :
    (colimit.Î¹ (D.diagram â‹™ forget _) _ â‰« colim.map _ â‰« (colimit.iso_colimit_cocone _).hom) _ =
    (colimit.Î¹ (D.diagram â‹™ forget _) _ â‰« colim.map _ â‰« (colimit.iso_colimit_cocone _).hom) _ :=
    (congr_arg (colim.map (diagram_iso_parallel_pair (D.diagram â‹™ forget _)).hom
    â‰« (colimit.iso_colimit_cocone (types.coequalizer_limit _ _)).hom) this : _),
  simp only [eq_to_hom_refl, types_comp_apply, colimit.Î¹_map_assoc,
    diagram_iso_parallel_pair_hom_app, colimit.iso_colimit_cocone_Î¹_hom, types_id_apply] at this,
  exact quot.eq.1 this,
end

lemma inv_image.equivalence {Î± : Sort u} {Î² : Sort v} (r : Î² â†’ Î² â†’ Prop) (f : Î± â†’ Î²)
  (h : equivalence r) : equivalence (inv_image r f) :=
âŸ¨Î» _, h.1 _, Î» _ _ x, h.2.1 x, inv_image.trans r f h.2.2âŸ©

lemma imm_eq_iff_rel (i j : D.Î¹) (x : D.U i) (y : D.U j) :
  D.imm i x = D.imm j y â†” D.rel âŸ¨i, xâŸ© âŸ¨j, yâŸ© :=
begin
  split,
  { intro h,
    rw â† (show _ = sigma.mk i x, from congr_fun (sigma_iso_sigma D.U).inv_hom_id _),
    rw â† (show _ = sigma.mk j y, from congr_fun (sigma_iso_sigma D.U).inv_hom_id _),
    change inv_image D.rel (sigma_iso_sigma D.U).hom _ _,
    simp only [Top.sigma_iso_sigma_inv_app],
    rw â†relation.eqv_gen_iff_of_equivalence (inv_image.equivalence _ _ D.rel_equiv),
    refine relation.eqv_gen_mono _ (D.eqv_gen_of_Ï€_eq h : _),
    rintros _ _ âŸ¨xâŸ©,
    rw â† (show (sigma_iso_sigma _).inv _ = x, from congr_fun (sigma_iso_sigma _).hom_inv_id x),
    generalize : (sigma_iso_sigma D.V).hom x = x',
    rcases x' with âŸ¨âŸ¨i,jâŸ©,yâŸ©,
    unfold inv_image left_imm right_imm,
    simp only [opens.inclusion_to_fun, Top.comp_app, sigma_iso_sigma_inv_app,
      category_theory.limits.colimit.Î¹_desc_apply, cofan.mk_Î¹_app,
      sigma_iso_sigma_hom_app, continuous_map.to_fun_eq_coe],
    erw [Top.sigma_iso_sigma_hom_app, Top.sigma_iso_sigma_hom_app],
    exact or.inr âŸ¨y, by simpâŸ© },
  { rintro (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨_, eâ‚, eâ‚‚âŸ©),
    refl, dsimp only at *, cases eâ‚, cases eâ‚‚, simp }
end

lemma imm_injective (i : D.Î¹) : function.injective (D.imm i) :=
begin
  intros x y h,
  rcases (D.imm_eq_iff_rel _ _ _ _).mp h with (âŸ¨âŸ¨âŸ©âŸ©| âŸ¨_,eâ‚,eâ‚‚âŸ©),
  refl,
  dsimp only at *,
  cases eâ‚, cases eâ‚‚, simp
end

instance imm_mono (i : D.Î¹) : mono (D.imm i) :=
(Top.mono_iff_injective _).mpr (D.imm_injective _)

local attribute [elementwise] is_iso.hom_inv_id is_iso.inv_hom_id

lemma image_inter (i j : D.Î¹) :
  set.range (D.imm i) âˆ© set.range (D.imm j) = set.range (D.f i j â‰« D.imm _) :=
begin
  ext x,
  split,
  { rintro âŸ¨âŸ¨xâ‚, eqâ‚âŸ©, âŸ¨xâ‚‚, eqâ‚‚âŸ©âŸ©,
    have := (D.imm_eq_iff_rel _ _ _ _).mp (eqâ‚.trans eqâ‚‚.symm),
    rcases this with (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨y,eâ‚,eâ‚‚âŸ©),
    exact âŸ¨inv (D.f i i) xâ‚, by simp[eqâ‚]âŸ©,
    dsimp only at *,
    cases eâ‚, cases eqâ‚,
    exact âŸ¨y, by simpaâŸ© },
  { rintro âŸ¨x, hxâŸ©,
    exact âŸ¨âŸ¨D.f i j x, hxâŸ©, âŸ¨D.f j i (D.t _ _ x), by simp[â†hx]âŸ©âŸ© }
end

lemma preimage_range (i j : D.Î¹) :
  D.imm j â»Â¹' (set.range (D.imm i)) = set.range (D.f j i) :=
by rw [ â†set.preimage_image_eq (set.range (D.f j i)) (D.imm_injective j), â†set.image_univ,
        â†set.image_univ, â†set.image_comp, â†coe_comp, set.image_univ,set.image_univ,
        â†image_inter, set.preimage_range_inter]

lemma preimage_image_eq_preimage_f (i j : D.Î¹) (U : set (D.U i)) :
D.imm j â»Â¹' (D.imm i '' U) = D.f _ _ '' ((D.t j i â‰« D.f _ _) â»Â¹' U) :=
begin
  have : D.f _ _ â»Â¹' (D.imm j â»Â¹' (D.imm i '' U)) = (D.t j i â‰« D.f _ _) â»Â¹' U,
  { ext x,
    conv_rhs { rw â† set.preimage_image_eq U (D.imm_injective _) },
    generalize : D.imm i '' U = U',
    simp },
  rw [â†this, set.image_preimage_eq_inter_range],
  symmetry,
  apply set.inter_eq_self_of_subset_left,
  rw â† D.preimage_range i j,
  exact set.preimage_mono (set.image_subset_range _ _),
end

lemma open_image_open (i : D.Î¹) (U : opens (D.U i)) : is_open (D.imm i '' U) :=
begin
  rw is_open_iff,
  intro j,
  rw preimage_image_eq_preimage_f,
  apply (D.f_open _ _).is_open_map,
  apply (D.t j i â‰« D.f i j).continuous_to_fun.is_open_preimage,
  exact U.property
end

lemma imm_open_embedding (i : D.Î¹) : open_embedding (D.imm i) :=
open_embedding_of_continuous_injective_open
  (D.imm i).continuous_to_fun (D.imm_injective i) (Î» U h, D.open_image_open i âŸ¨U, hâŸ©)

end glue_data

end Top
