/-
Copyright (c) 2021 Praneeth Kolichala. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Praneeth Kolichala
-/
import topology.homotopy.basic
import topology.constructions
import topology.homotopy.path
import category_theory.groupoid
import topology.homotopy.fundamental_groupoid
import topology.category.Top.limits
import category_theory.limits.preserves.shapes.products

/-!
# Product of homotopies

In this file, we introduce definitions for the product of
homotopies. We show that the products of relative homotopies
are still relative homotopies. Finally, we specialize to the case
of path homotopies, and provide the definition for the product of path classes.
We show various lemmas associated with these products, such as the fact that
path products commute with path composition, and that projection is the inverse
of products.

## Definitions
### General homotopies
- `continuous_map.homotopy.pi homotopies`: Let f and g be a family of functions
  indexed on I, such that for each i ‚àà I, f·µ¢ and g·µ¢ are maps from A to X·µ¢.
  Let `homotopies` be a family of homotopies from f·µ¢ to g·µ¢ for each i.
  Then `homotopy.pi homotopies` is the canonical homotopy
  from ‚àè f to ‚àè g, where ‚àè f is the product map from A to Œ†i, X·µ¢,
  and similarly for ‚àè g.

- `continuous_map.homotopy_rel.pi homotopies`: Same as `continuous_map.homotopy.pi`, but
  all homotopies are done relative to some set S ‚äÜ A.

- `continuous_map.homotopy.prod F G` is the product of homotopies F and G,
   where F is a homotopy between f‚ÇÄ and f‚ÇÅ, G is a homotopy between g‚ÇÄ and g‚ÇÅ.
   The result F √ó G is a homotopy between (f‚ÇÄ √ó g‚ÇÄ) and (f‚ÇÅ √ó g‚ÇÅ).
   Again, all homotopies are done relative to S.

- `continuous_map.homotopy_rel.prod F G`: Same as `continuous_map.homotopy.prod`, but
  all homotopies are done relative to some set S ‚äÜ A.

### Path products
- `path.homotopic.pi` The product of a family of path classes, where a path class is an equivalence
  class of paths up to path homotopy.

- `path.homotopic.prod` The product of two path classes.

## Fundamental groupoid preserves products
  - `fundamental_groupoid_functor.pi_iso` An isomorphism between Œ† i, (œÄ X·µ¢) and œÄ (Œ†i, X·µ¢), whose
    inverse is precisely the product of the maps œÄ (Œ† i, X·µ¢) ‚Üí œÄ (X·µ¢), each induced by
    the projection in `Top` Œ† i, X·µ¢ ‚Üí X·µ¢.

  - `fundamental_groupoid_functor.prod_iso` An isomorphism between œÄX √ó œÄY and œÄ (X √ó Y), whose
    inverse is precisely the product of the maps œÄ (X √ó Y) ‚Üí œÄX and œÄ (X √ó Y) ‚Üí Y, each induced by
    the projections X √ó Y ‚Üí X and X √ó Y ‚Üí Y

  - `fundamental_groupoid_functor.preserves_product` A proof that the fundamental groupoid functor
    preserves all products.
-/

noncomputable theory

namespace continuous_map
open continuous_map

section pi

variables {I A : Type*} {X : I ‚Üí Type*} [Œ† i, topological_space (X i)] [topological_space A]
  {f g : Œ† i, C(A, X i)} {S : set A}

/-- The product homotopy of `homotopies` between functions `f` and `g` -/
@[simps]
def homotopy.pi (homotopies : Œ† i, homotopy (f i) (g i)) : homotopy (pi f) (pi g) :=
{ to_fun := Œª t i, homotopies i t,
  map_zero_left' := Œª t, by { ext i, simp only [pi_eval, homotopy.apply_zero] },
  map_one_left' := Œª t, by { ext i, simp only [pi_eval, homotopy.apply_one] } }

/-- The relative product homotopy of `homotopies` between functions `f` and `g` -/
@[simps]
def homotopy_rel.pi (homotopies : Œ† i : I, homotopy_rel (f i) (g i) S) :
  homotopy_rel (pi f) (pi g) S :=
{ prop' :=
  begin
    intros t x hx,
    dsimp only [coe_mk, pi_eval, to_fun_eq_coe, homotopy_with.coe_to_continuous_map],
    simp only [function.funext_iff, ‚Üê forall_and_distrib],
    intro i,
    exact (homotopies i).prop' t x hx,
  end,
  ..(homotopy.pi (Œª i, (homotopies i).to_homotopy)), }

end pi

section prod

variables {Œ± Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  {A : Type*} [topological_space A]
  {f‚ÇÄ f‚ÇÅ : C(A, Œ±)} {g‚ÇÄ g‚ÇÅ : C(A, Œ≤)} {S : set A}

/-- The product of homotopies `F` and `G`,
  where `F` takes `f‚ÇÄ` to `f‚ÇÅ`  and `G` takes `g‚ÇÄ` to `g‚ÇÅ` -/
@[simps]
def homotopy.prod (F : homotopy f‚ÇÄ f‚ÇÅ) (G : homotopy g‚ÇÄ g‚ÇÅ) :
  homotopy (prod_mk f‚ÇÄ g‚ÇÄ) (prod_mk f‚ÇÅ g‚ÇÅ) :=
{ to_fun := Œª t, (F t, G t),
  map_zero_left' := Œª x, by simp only [prod_eval, homotopy.apply_zero],
  map_one_left' := Œª x, by simp only [prod_eval, homotopy.apply_one] }

/-- The relative product of homotopies `F` and `G`,
  where `F` takes `f‚ÇÄ` to `f‚ÇÅ`  and `G` takes `g‚ÇÄ` to `g‚ÇÅ` -/
@[simps]
def homotopy_rel.prod (F : homotopy_rel f‚ÇÄ f‚ÇÅ S) (G : homotopy_rel g‚ÇÄ g‚ÇÅ S) :
  homotopy_rel (prod_mk f‚ÇÄ g‚ÇÄ) (prod_mk f‚ÇÅ g‚ÇÅ) S :=
{ prop' :=
  begin
    intros t x hx,
    have hF := F.prop' t x hx,
    have hG := G.prop' t x hx,
    simp only [coe_mk, prod_eval, prod.mk.inj_iff, homotopy.prod] at hF hG ‚ä¢,
    exact ‚ü®‚ü®hF.1, hG.1‚ü©, ‚ü®hF.2, hG.2‚ü©‚ü©,
  end,
  ..(homotopy.prod F.to_homotopy G.to_homotopy) }

end prod
end continuous_map


namespace path.homotopic
local attribute [instance] path.homotopic.setoid
local infix ` ‚¨ù `:70 := quotient.comp

section pi

variables {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, topological_space (X i)]
  {as bs cs : Œ† i, X i}

/-- The product of a family of path homotopies. This is just a specialization of `homotopy_rel` -/
def pi_homotopy (Œ≥‚ÇÄ Œ≥‚ÇÅ : Œ† i, path (as i) (bs i)) (H : ‚àÄ i, path.homotopy (Œ≥‚ÇÄ i) (Œ≥‚ÇÅ i)) :
  path.homotopy (path.pi Œ≥‚ÇÄ) (path.pi Œ≥‚ÇÅ) := continuous_map.homotopy_rel.pi H

/-- The product of a family of path homotopy classes -/
def pi (Œ≥ : Œ† i, path.homotopic.quotient (as i) (bs i)) : path.homotopic.quotient as bs :=
(quotient.map path.pi
  (Œª x y hxy, nonempty.map (pi_homotopy x y) (classical.nonempty_pi.mpr hxy)))
  (quotient.choice Œ≥)

lemma pi_lift (Œ≥ : Œ† i, path (as i) (bs i)) : path.homotopic.pi (Œª i, ‚ü¶Œ≥ i‚üß) = ‚ü¶path.pi Œ≥‚üß :=
by { unfold pi, simp, }

/-- Composition and products commute.
  This is `path.trans_pi_eq_pi_trans` descended to path homotopy classes -/
lemma comp_pi_eq_pi_comp
  (Œ≥‚ÇÄ : Œ† i, path.homotopic.quotient (as i) (bs i))
  (Œ≥‚ÇÅ : Œ† i, path.homotopic.quotient (bs i) (cs i)) :
  pi Œ≥‚ÇÄ ‚¨ù pi Œ≥‚ÇÅ = pi (Œª i, Œ≥‚ÇÄ i ‚¨ù Œ≥‚ÇÅ i) :=
begin
  apply quotient.induction_on_pi Œ≥‚ÇÅ,
  apply quotient.induction_on_pi Œ≥‚ÇÄ,
  intros,
  simp only [pi_lift],
  rw [‚Üê path.homotopic.comp_lift,
      path.trans_pi_eq_pi_trans,
      ‚Üê pi_lift],
  refl,
end

/-- Abbreviation for projection onto the ith coordinate -/
@[reducible]
def proj (i : Œπ) (p : path.homotopic.quotient as bs) : path.homotopic.quotient (as i) (bs i) :=
p.map_fn ‚ü®_, continuous_apply i‚ü©

/-- Lemmas showing projection is the inverse of pi -/
@[simp] lemma proj_pi (i : Œπ) (paths : Œ† i, path.homotopic.quotient (as i) (bs i)) :
  proj i (pi paths) = paths i :=
begin
  apply quotient.induction_on_pi paths,
  intro, unfold proj,
  rw [pi_lift, ‚Üê path.homotopic.map_lift],
  congr, ext, refl,
end

@[simp] lemma pi_proj (p : path.homotopic.quotient as bs) : pi (Œª i, proj i p) = p :=
begin
  apply quotient.induction_on p,
  intro, unfold proj,
  simp_rw ‚Üê path.homotopic.map_lift,
  rw pi_lift,
  congr, ext, refl,
end

end pi

section prod

variables {Œ± Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  {a‚ÇÅ a‚ÇÇ a‚ÇÉ : Œ±} {b‚ÇÅ b‚ÇÇ b‚ÇÉ : Œ≤}
  {p‚ÇÅ p‚ÇÅ' : path a‚ÇÅ a‚ÇÇ} {p‚ÇÇ p‚ÇÇ' : path b‚ÇÅ b‚ÇÇ}
  (q‚ÇÅ : path.homotopic.quotient a‚ÇÅ a‚ÇÇ) (q‚ÇÇ : path.homotopic.quotient b‚ÇÅ b‚ÇÇ)

/-- The product of homotopies h‚ÇÅ and h‚ÇÇ.
    This is `homotopy_rel.prod` specialized for path homotopies. -/
def prod_homotopy (h‚ÇÅ : path.homotopy p‚ÇÅ p‚ÇÅ') (h‚ÇÇ : path.homotopy p‚ÇÇ p‚ÇÇ') :
  path.homotopy (p‚ÇÅ.prod p‚ÇÇ) (p‚ÇÅ'.prod p‚ÇÇ') := continuous_map.homotopy_rel.prod h‚ÇÅ h‚ÇÇ

/-- The product of path classes q‚ÇÅ and q‚ÇÇ. This is `path.prod` descended to the quotient -/
def prod (q‚ÇÅ : path.homotopic.quotient a‚ÇÅ a‚ÇÇ) (q‚ÇÇ : path.homotopic.quotient b‚ÇÅ b‚ÇÇ) :
  path.homotopic.quotient (a‚ÇÅ, b‚ÇÅ) (a‚ÇÇ, b‚ÇÇ) :=
quotient.map‚ÇÇ path.prod (Œª p‚ÇÅ p‚ÇÅ' h‚ÇÅ p‚ÇÇ p‚ÇÇ' h‚ÇÇ, nonempty.map2 prod_homotopy h‚ÇÅ h‚ÇÇ) q‚ÇÅ q‚ÇÇ

variables (p‚ÇÅ p‚ÇÅ' p‚ÇÇ p‚ÇÇ')
lemma prod_lift : prod ‚ü¶p‚ÇÅ‚üß ‚ü¶p‚ÇÇ‚üß = ‚ü¶p‚ÇÅ.prod p‚ÇÇ‚üß := rfl

variables (r‚ÇÅ : path.homotopic.quotient a‚ÇÇ a‚ÇÉ) (r‚ÇÇ : path.homotopic.quotient b‚ÇÇ b‚ÇÉ)
/-- Products commute with path composition.
    This is `trans_prod_eq_prod_trans` descended to the quotient.-/
lemma comp_prod_eq_prod_comp : (prod q‚ÇÅ q‚ÇÇ) ‚¨ù (prod r‚ÇÅ r‚ÇÇ) = prod (q‚ÇÅ ‚¨ù r‚ÇÅ) (q‚ÇÇ ‚¨ù r‚ÇÇ) :=
begin
  apply quotient.induction_on‚ÇÇ q‚ÇÅ q‚ÇÇ,
  apply quotient.induction_on‚ÇÇ r‚ÇÅ r‚ÇÇ,
  intros,
  simp only [prod_lift, ‚Üê path.homotopic.comp_lift, path.trans_prod_eq_prod_trans],
end

variables {c‚ÇÅ c‚ÇÇ : Œ± √ó Œ≤}

/-- Abbreviation for projection onto the left coordinate of a path class -/
@[reducible]
def proj_left (p : path.homotopic.quotient c‚ÇÅ c‚ÇÇ) : path.homotopic.quotient c‚ÇÅ.1 c‚ÇÇ.1 :=
p.map_fn ‚ü®_, continuous_fst‚ü©

/-- Abbreviation for projection onto the right coordinate of a path class -/
@[reducible]
def proj_right (p : path.homotopic.quotient c‚ÇÅ c‚ÇÇ) : path.homotopic.quotient c‚ÇÅ.2 c‚ÇÇ.2 :=
p.map_fn ‚ü®_, continuous_snd‚ü©

/-- Lemmas showing projection is the inverse of product -/
@[simp] lemma proj_left_prod : proj_left (prod q‚ÇÅ q‚ÇÇ) = q‚ÇÅ :=
begin
  apply quotient.induction_on‚ÇÇ q‚ÇÅ q‚ÇÇ,
  intros p‚ÇÅ p‚ÇÇ,
  unfold proj_left,
  rw [prod_lift, ‚Üê path.homotopic.map_lift],
  congr, ext, refl,
end

@[simp] lemma proj_right_prod : proj_right (prod q‚ÇÅ q‚ÇÇ) = q‚ÇÇ :=
begin
  apply quotient.induction_on‚ÇÇ q‚ÇÅ q‚ÇÇ,
  intros p‚ÇÅ p‚ÇÇ,
  unfold proj_right,
  rw [prod_lift, ‚Üê path.homotopic.map_lift],
  congr, ext, refl,
end

@[simp] lemma prod_proj_left_proj_right (p : path.homotopic.quotient (a‚ÇÅ, b‚ÇÅ) (a‚ÇÇ, b‚ÇÇ))
  : prod (proj_left p) (proj_right p) = p :=
begin
  apply quotient.induction_on p,
  intro p',
  unfold proj_left, unfold proj_right,
  simp only [‚Üê path.homotopic.map_lift, prod_lift],
  congr, ext; refl,
end

end prod

end path.homotopic


namespace fundamental_groupoid_functor

open_locale fundamental_groupoid

universes u

section pi

variables {I : Type u} (X : I ‚Üí Top.{u})

/--
The projection map Œ† i, X i ‚Üí X i induces a map œÄ(Œ† i, X i) ‚ü∂ œÄ(X i).
-/
def proj (i : I) : œÄ‚Çì (Top.of (Œ† i, X i)) ‚•§ œÄ‚Çì (X i) := œÄ‚Çò ‚ü®_, continuous_apply i‚ü©

/-- The projection map is precisely path.homotopic.proj interpreted as a functor -/
@[simp] lemma proj_map (i : I) (x‚ÇÄ x‚ÇÅ : œÄ‚Çì (Top.of (Œ† i, X i))) (p : x‚ÇÄ ‚ü∂ x‚ÇÅ) :
  (proj X i).map p = (@path.homotopic.proj _ _ _ _ _ i p) := rfl

/--
The map taking the pi product of a family of fundamental groupoids to the fundamental
groupoid of the pi product. This is actually an isomorphism (see `pi_iso`)
-/
@[simps]
def pi_to_pi_Top : (Œ† i, œÄ‚Çì (X i)) ‚•§ œÄ‚Çì (Top.of (Œ† i, X i)) :=
{ obj := Œª g, g,
  map := Œª v‚ÇÅ v‚ÇÇ p, path.homotopic.pi p,
  map_id' :=
  begin
    intro x,
    change path.homotopic.pi (Œª i, ùüô (x i)) = _,
    simp only [fundamental_groupoid.id_eq_path_refl, path.homotopic.pi_lift],
    refl,
  end,
  map_comp' := Œª x y z f g, (path.homotopic.comp_pi_eq_pi_comp f g).symm, }

/--
Shows `pi_to_pi_Top` is an isomorphism, whose inverse is precisely the pi product
of the induced projections. This shows that `fundamental_groupoid_functor` preserves products.
-/
@[simps]
def pi_iso : category_theory.Groupoid.of (Œ† i : I, œÄ‚Çì (X i)) ‚âÖ œÄ‚Çì (Top.of (Œ† i, X i)) :=
{ hom := pi_to_pi_Top X,
  inv := category_theory.functor.pi' (proj X),
  hom_inv_id' :=
  begin
    change pi_to_pi_Top X ‚ãô (category_theory.functor.pi' (proj X)) = ùü≠ _,
    apply category_theory.functor.ext; intros,
    { ext, simp, }, { refl, },
  end,
  inv_hom_id' :=
  begin
    change (category_theory.functor.pi' (proj X)) ‚ãô pi_to_pi_Top X = ùü≠ _,
    apply category_theory.functor.ext; intros,
    { suffices : path.homotopic.pi ((category_theory.functor.pi' (proj X)).map f) = f, { simpa, },
      change (category_theory.functor.pi' (proj X)).map f
        with Œª i, (category_theory.functor.pi' (proj X)).map f i,
      simp, }, { refl, }
  end }

section preserves
open category_theory

/-- Equivalence between the categories of cones over the objects `œÄ X·µ¢` written in two ways -/
def cone_discrete_comp : limits.cone (discrete.functor X ‚ãô œÄ) ‚âå
  limits.cone (discrete.functor (Œª i, œÄ‚Çì (X i))) :=
limits.cones.postcompose_equivalence (discrete.comp_nat_iso_discrete X œÄ)

lemma cone_discrete_comp_obj_map_cone :
  (cone_discrete_comp X).functor.obj ((œÄ).map_cone (Top.pi_fan X))
  = limits.fan.mk (œÄ‚Çì (Top.of (Œ† i, X i))) (proj X) := rfl

/-- This is `pi_iso.inv` as a cone morphism (in fact, isomorphism) -/
def pi_Top_to_pi_cone : (limits.fan.mk (œÄ‚Çì (Top.of (Œ† i, X i))) (proj X)) ‚ü∂
  Groupoid.pi_limit_fan (Œª i : I, (œÄ‚Çì (X i))) := { hom := category_theory.functor.pi' (proj X) }

instance : is_iso (pi_Top_to_pi_cone X) :=
begin
  haveI : is_iso (pi_Top_to_pi_cone X).hom := (infer_instance : is_iso (pi_iso X).inv),
  exact limits.cones.cone_iso_of_hom_iso (pi_Top_to_pi_cone X),
end

/-- The fundamental groupoid functor preserves products -/
def preserves_product : limits.preserves_limit (discrete.functor X) œÄ :=
begin
  apply limits.preserves_limit_of_preserves_limit_cone (Top.pi_fan_is_limit X),
  apply (limits.is_limit.of_cone_equiv (cone_discrete_comp X)).to_fun,
  simp only [cone_discrete_comp_obj_map_cone],
  apply limits.is_limit.of_iso_limit _ (as_iso (pi_Top_to_pi_cone X)).symm,
  exact (Groupoid.pi_limit_cone _).is_limit,
end

end preserves

end pi

section prod

variables (A B : Top.{u})

/-- The induced map of the left projection map X √ó Y ‚Üí X -/
def proj_left : œÄ‚Çì (Top.of (A √ó B)) ‚•§ œÄ‚Çì A := œÄ‚Çò ‚ü®_, continuous_fst‚ü©

/-- The induced map of the right projection map X √ó Y ‚Üí Y -/
def proj_right : œÄ‚Çì (Top.of (A √ó B)) ‚•§ œÄ‚Çì B := œÄ‚Çò ‚ü®_, continuous_snd‚ü©

@[simp] lemma proj_left_map (x‚ÇÄ x‚ÇÅ : œÄ‚Çì (Top.of (A √ó B))) (p : x‚ÇÄ ‚ü∂ x‚ÇÅ) :
  (proj_left A B).map p = path.homotopic.proj_left p := rfl

@[simp] lemma proj_right_map (x‚ÇÄ x‚ÇÅ : œÄ‚Çì (Top.of (A √ó B))) (p : x‚ÇÄ ‚ü∂ x‚ÇÅ) :
  (proj_right A B).map p = path.homotopic.proj_right p := rfl


/--
The map taking the product of two fundamental groupoids to the fundamental groupoid of the product
of the two topological spaces. This is in fact an isomorphism (see `prod_iso`).
-/
@[simps obj]
def prod_to_prod_Top : (œÄ‚Çì A) √ó (œÄ‚Çì B) ‚•§ œÄ‚Çì (Top.of (A √ó B)) :=
{ obj := Œª g, g,
  map := Œª x y p, match x, y, p with
    | (x‚ÇÄ, x‚ÇÅ), (y‚ÇÄ, y‚ÇÅ), (p‚ÇÄ, p‚ÇÅ) := path.homotopic.prod p‚ÇÄ p‚ÇÅ
  end,
  map_id' :=
  begin
    rintro ‚ü®x‚ÇÄ, x‚ÇÅ‚ü©,
    simp only [category_theory.prod_id, fundamental_groupoid.id_eq_path_refl],
    unfold_aux, rw path.homotopic.prod_lift, refl,
  end,
  map_comp' := Œª x y z f g, match x, y, z, f, g with
    | (x‚ÇÄ, x‚ÇÅ), (y‚ÇÄ, y‚ÇÅ), (z‚ÇÄ, z‚ÇÅ), (f‚ÇÄ, f‚ÇÅ), (g‚ÇÄ, g‚ÇÅ) :=
    (path.homotopic.comp_prod_eq_prod_comp f‚ÇÄ f‚ÇÅ g‚ÇÄ g‚ÇÅ).symm
  end }

lemma prod_to_prod_Top_map {x‚ÇÄ x‚ÇÅ : œÄ‚Çì A} {y‚ÇÄ y‚ÇÅ : œÄ‚Çì B}
  (p‚ÇÄ : x‚ÇÄ ‚ü∂ x‚ÇÅ) (p‚ÇÅ : y‚ÇÄ ‚ü∂ y‚ÇÅ) :
  @category_theory.functor.map _ _ _ _
  (prod_to_prod_Top A B) (x‚ÇÄ, y‚ÇÄ) (x‚ÇÅ, y‚ÇÅ) (p‚ÇÄ, p‚ÇÅ) = path.homotopic.prod p‚ÇÄ p‚ÇÅ := rfl

/--
Shows `prod_to_prod_Top` is an isomorphism, whose inverse is precisely the product
of the induced left and right projections.
-/
@[simps]
def prod_iso : category_theory.Groupoid.of ((œÄ‚Çì A) √ó (œÄ‚Çì B)) ‚âÖ (œÄ‚Çì (Top.of (A √ó B))) :=
{ hom := prod_to_prod_Top A B,
  inv := (proj_left A B).prod' (proj_right A B),
  hom_inv_id' :=
  begin
    change prod_to_prod_Top A B ‚ãô ((proj_left A B).prod' (proj_right A B)) = ùü≠ _,
    apply category_theory.functor.hext, { intros, ext; simp; refl, },
    rintros ‚ü®x‚ÇÄ, x‚ÇÅ‚ü© ‚ü®y‚ÇÄ, y‚ÇÅ‚ü© ‚ü®f‚ÇÄ, f‚ÇÅ‚ü©,
    have := and.intro (path.homotopic.proj_left_prod f‚ÇÄ f‚ÇÅ) (path.homotopic.proj_right_prod f‚ÇÄ f‚ÇÅ),
    simpa,
  end,
  inv_hom_id' :=
  begin
    change ((proj_left A B).prod' (proj_right A B)) ‚ãô prod_to_prod_Top A B = ùü≠ _,
    apply category_theory.functor.hext, { intros, ext; simp; refl, },
    rintros ‚ü®x‚ÇÄ, x‚ÇÅ‚ü© ‚ü®y‚ÇÄ, y‚ÇÅ‚ü© f,
    have := path.homotopic.prod_proj_left_proj_right f,
    simpa,
  end }

end prod

end fundamental_groupoid_functor
