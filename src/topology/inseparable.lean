/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Yury G. Kudryashov
-/
import topology.constructions

/-!
# Inseparable points

In this file we require two relations on a topological space: `specializes` (notation : `x ‚§≥ y`) and
`inseparable`, then prove some basic lemmas about these relations.

## Main definitions

* `specializes` : `specializes x y` (`x ‚§≥ y`) means that `x` specializes to `y`, i.e.
  `y` is in the closure of `x`.

* `specialization_preorder` : specialization gives a preorder on a topological space. In case of a
  T‚ÇÄ space, this preorder is a partial order, see `specialization_order`.

* `inseparable x y` means that two points can't be separated by an open set.
-/

open_locale topological_space
open set

variables {X Y : Type*} [topological_space X] [topological_space Y] {x y z : X}

/-- `x` specializes to `y` if `y` is in the closure of `x`. The notation used is `x ‚§≥ y`. -/
def specializes (x y : X) : Prop := y ‚àà closure ({x} : set X)

infix ` ‚§≥ `:300 := specializes

lemma specializes_iff_mem_closure {x y : X} : x ‚§≥ y ‚Üî y ‚àà closure ({x} : set X) := iff.rfl

lemma specializes_iff_closure_subset : x ‚§≥ y ‚Üî closure ({y} : set X) ‚äÜ closure ({x} : set X) :=
is_closed_closure.mem_iff_closure_subset

lemma specializes_rfl : x ‚§≥ x := subset_closure (mem_singleton x)

lemma specializes_refl (x : X) : x ‚§≥ x := specializes_rfl

lemma specializes.trans : x ‚§≥ y ‚Üí y ‚§≥ z ‚Üí x ‚§≥ z :=
by { simp_rw specializes_iff_closure_subset, exact Œª a b, b.trans a }

lemma specializes_iff_forall_closed :
  x ‚§≥ y ‚Üî ‚àÄ (Z : set X) (h : is_closed Z), x ‚àà Z ‚Üí y ‚àà Z :=
begin
  split,
  { intros h Z hZ,
    rw [hZ.mem_iff_closure_subset, hZ.mem_iff_closure_subset],
    exact (specializes_iff_closure_subset.mp h).trans },
  { intro h, exact h _ is_closed_closure (subset_closure $ set.mem_singleton x) }
end

lemma specializes_iff_forall_open :
  x ‚§≥ y ‚Üî ‚àÄ (U : set X) (h : is_open U), y ‚àà U ‚Üí x ‚àà U :=
begin
  rw specializes_iff_forall_closed,
  exact ‚ü®Œª h U hU, not_imp_not.mp (h _ (is_closed_compl_iff.mpr hU)),
    Œª h U hU, not_imp_not.mp (h _ (is_open_compl_iff.mpr hU))‚ü©,
end

lemma specializes.mem_open (h : x ‚§≥ y) {U : set X} (hU : is_open U) (hy : y ‚àà U) : x ‚àà U :=
specializes_iff_forall_open.1 h U hU hy

lemma specializes.map (h : x ‚§≥ y) {f : X ‚Üí Y} (hf : continuous f) : f x ‚§≥ f y :=
begin
  rw [specializes, ‚Üê set.image_singleton],
  exact image_closure_subset_closure_image hf ‚ü®_, h, rfl‚ü©,
end

section specialize_order

variable (X)

/-- Specialization forms a preorder on the topological space. -/
def specialization_preorder : preorder X :=
{ le := Œª x y, y ‚§≥ x,
  le_refl := Œª x, specializes_refl x,
  le_trans := Œª _ _ _ h‚ÇÅ h‚ÇÇ, specializes.trans h‚ÇÇ h‚ÇÅ }

local attribute [instance] specialization_preorder

variable {X}

lemma specialization_order.monotone_of_continuous (f : X ‚Üí Y) (hf : continuous f) : monotone f :=
Œª x y h, specializes.map h hf

end specialize_order

/-- Two points are topologically inseparable if no open set separates them. -/
def inseparable (x y : X) : Prop := ‚àÄ (U : set X) (hU : is_open U), x ‚àà U ‚Üî y ‚àà U

lemma inseparable_iff_nhds_eq : inseparable x y ‚Üî ùìù x = ùìù y :=
‚ü®Œª h, by simp only [nhds_def', h _] { contextual := tt },
  Œª h U hU, by simp only [‚Üê hU.mem_nhds_iff, h]‚ü©

alias inseparable_iff_nhds_eq ‚Üî inseparable.nhds_eq _

lemma inseparable.map {f : X ‚Üí Y} (h : inseparable x y) (hf : continuous f) :
  inseparable (f x) (f y) :=
Œª U hU, h (f ‚Åª¬π' U) (hU.preimage hf)

lemma inseparable_iff_closed :
  inseparable x y ‚Üî ‚àÄ (U : set X) (hU : is_closed U), x ‚àà U ‚Üî y ‚àà U :=
‚ü®Œª h U hU, not_iff_not.mp (h _ hU.1), Œª h U hU, not_iff_not.mp (h _ (is_closed_compl_iff.mpr hU))‚ü©

lemma inseparable_iff_mem_closure (x y : X) :
  inseparable x y ‚Üî x ‚àà closure ({y} : set X) ‚àß y ‚àà closure ({x} : set X) :=
begin
  rw inseparable_iff_closed,
  exact ‚ü®Œª h, ‚ü®(h _ is_closed_closure).mpr (subset_closure $ set.mem_singleton y),
      (h _ is_closed_closure).mp (subset_closure $ set.mem_singleton x)‚ü©,
    Œª h U hU, ‚ü®Œª hx, (is_closed.closure_subset_iff hU).mpr (set.singleton_subset_iff.mpr hx) h.2,
      Œª hy, (is_closed.closure_subset_iff hU).mpr (set.singleton_subset_iff.mpr hy) h.1‚ü©‚ü©
end

lemma inseparable_iff_specializes_and (x y : X) :
  inseparable x y ‚Üî x ‚§≥ y ‚àß y ‚§≥ x :=
(inseparable_iff_mem_closure x y).trans (and_comm _ _)

lemma inseparable_iff_closure_eq {x y : X} :
  inseparable x y ‚Üî (closure {x} : set X) = closure {y} :=
by simp only [inseparable_iff_specializes_and, specializes_iff_closure_subset,
  subset_antisymm_iff, and.comm]

lemma subtype_inseparable_iff {U : set X} (x y : U) :
  inseparable x y ‚Üî inseparable (x : X) y :=
by { simp_rw [inseparable_iff_mem_closure, closure_subtype, image_singleton] }
