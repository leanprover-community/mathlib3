/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import group_theory.divisible
import group_theory.order_of_element
import ring_theory.int.basic
import algebra.order.floor
import algebra.order.to_interval_mod
import topology.instances.real

/-!
# The additive circle

We define the additive circle `add_circle p` as the quotient `ğ•œ â§¸ (â„¤ âˆ™ p)` for some period `p : ğ•œ`.

See also `circle` and `real.angle`.  For the normed group structure on `add_circle`, see
`add_circle.normed_add_comm_group` in a later file.

## Main definitions:

 * `add_circle`: the additive circle `ğ•œ â§¸ (â„¤ âˆ™ p)` for some period `p : ğ•œ`
 * `unit_add_circle`: the special case `â„ â§¸ â„¤`
 * `add_circle.equiv_add_circle`: the rescaling equivalence `add_circle p â‰ƒ+ add_circle q`
 * `add_circle.equiv_Ico`: the natural equivalence `add_circle p â‰ƒ Ico 0 p`
 * `add_circle.add_order_of_div_of_gcd_eq_one`: rational points have finite order
 * `add_circle.exists_gcd_eq_one_of_is_of_fin_add_order`: finite-order points are rational

## Implementation notes:

Although the most important case is `ğ•œ = â„` we wish to support other types of scalars, such as
the rational circle `add_circle (1 : â„š)`, and so we set things up more generally.

## TODO

 * Link with periodicity
 * Lie group structure
 * Exponential equivalence to `circle`

-/

noncomputable theory

open set function add_subgroup topological_space

variables {ğ•œ : Type*}

/-- The "additive circle": `ğ•œ â§¸ (â„¤ âˆ™ p)`. See also `circle` and `real.angle`. -/
@[derive [add_comm_group, topological_space, topological_add_group, inhabited, has_coe_t ğ•œ],
  nolint unused_arguments]
def add_circle [linear_ordered_add_comm_group ğ•œ] [topological_space ğ•œ] [order_topology ğ•œ] (p : ğ•œ) :=
ğ•œ â§¸ zmultiples p

namespace add_circle

section linear_ordered_add_comm_group
variables [linear_ordered_add_comm_group ğ•œ] [topological_space ğ•œ] [order_topology ğ•œ] (p : ğ•œ)

lemma coe_nsmul {n : â„•} {x : ğ•œ} : (â†‘(n â€¢ x) : add_circle p) = n â€¢ (x : add_circle p) := rfl

lemma coe_zsmul {n : â„¤} {x : ğ•œ} : (â†‘(n â€¢ x) : add_circle p) = n â€¢ (x : add_circle p) := rfl

lemma coe_neg {x : ğ•œ} : (â†‘(-x) : add_circle p) = -(x : add_circle p) := rfl

lemma coe_eq_zero_iff {x : ğ•œ} : (x : add_circle p) = 0 â†” âˆƒ (n : â„¤), n â€¢ p = x :=
by simp [add_subgroup.mem_zmultiples_iff]

lemma coe_eq_zero_of_pos_iff (hp : 0 < p) {x : ğ•œ} (hx : 0 < x) :
  (x : add_circle p) = 0 â†” âˆƒ (n : â„•), n â€¢ p = x :=
begin
  rw coe_eq_zero_iff,
  split;
  rintros âŸ¨n, rflâŸ©,
  { replace hx : 0 < n,
    { contrapose! hx,
      simpa only [â†neg_nonneg, â†zsmul_neg, zsmul_neg'] using zsmul_nonneg hp.le (neg_nonneg.2 hx) },
    exact âŸ¨n.to_nat, by rw [â† coe_nat_zsmul, int.to_nat_of_nonneg hx.le]âŸ©, },
  { exact âŸ¨(n : â„¤), by simpâŸ©, },
end

@[continuity, nolint unused_arguments] protected lemma continuous_mk' :
  continuous (quotient_add_group.mk' (zmultiples p) : ğ•œ â†’ add_circle p) :=
continuous_coinduced_rng

variables [hp : fact (0 < p)]
include hp

variables [archimedean ğ•œ]

/-- The natural equivalence between `add_circle p` and the half-open interval `[0, p)`. -/
def equiv_Ico : add_circle p â‰ƒ Ico 0 p :=
(quotient_add_group.equiv_Ico_mod 0 hp.out).trans $ equiv.set.of_eq $ by rw zero_add

@[continuity] lemma continuous_equiv_Ico_symm : continuous (equiv_Ico p).symm :=
continuous_quotient_mk.comp continuous_subtype_coe

/-- The image of the closed-open interval `[0, p)` under the quotient map `ğ•œ â†’ add_circle p` is the
entire space. -/
@[simp] lemma coe_image_Ico_eq : (coe : ğ•œ â†’ add_circle p) '' Ico 0 p = univ :=
by { rw image_eq_range, exact (equiv_Ico p).symm.range_eq_univ }

/-- The image of the closed interval `[0, p]` under the quotient map `ğ•œ â†’ add_circle p` is the
entire space. -/
@[simp] lemma coe_image_Icc_eq : (coe : ğ•œ â†’ add_circle p) '' Icc 0 p = univ :=
eq_top_mono (image_subset _ Ico_subset_Icc_self) $ coe_image_Ico_eq _

end linear_ordered_add_comm_group

section linear_ordered_field
variables [linear_ordered_field ğ•œ] [topological_space ğ•œ] [order_topology ğ•œ] (p q : ğ•œ)

/-- An auxiliary definition used only for constructing `add_circle.equiv_add_circle`. -/
private def equiv_add_circle_aux (hp : p â‰  0) : add_circle p â†’+ add_circle q :=
quotient_add_group.lift _
  ((quotient_add_group.mk' (zmultiples q)).comp $ add_monoid_hom.mul_right (pâ»Â¹ * q))
  (Î» x h, by obtain âŸ¨z, rflâŸ© := mem_zmultiples_iff.1 h; simp [hp, mul_assoc (z : ğ•œ), â† mul_assoc p])

/-- The rescaling equivalence between additive circles with different periods. -/
def equiv_add_circle (hp : p â‰  0) (hq : q â‰  0) : add_circle p â‰ƒ+ add_circle q :=
{ to_fun := equiv_add_circle_aux p q hp,
  inv_fun := equiv_add_circle_aux q p hq,
  left_inv := by { rintros âŸ¨xâŸ©, show quotient_add_group.mk _ = _, congr, field_simp [hp, hq], },
  right_inv := by { rintros âŸ¨xâŸ©, show quotient_add_group.mk _ = _, congr, field_simp [hp, hq], },
  .. equiv_add_circle_aux p q hp }

@[simp] lemma equiv_add_circle_apply_mk (hp : p â‰  0) (hq : q â‰  0) (x : ğ•œ) :
  equiv_add_circle p q hp hq (x : ğ•œ) = (x * (pâ»Â¹ * q) : ğ•œ) :=
rfl

@[simp] lemma equiv_add_circle_symm_apply_mk (hp : p â‰  0) (hq : q â‰  0) (x : ğ•œ) :
  (equiv_add_circle p q hp hq).symm (x : ğ•œ) = (x * (qâ»Â¹ * p) : ğ•œ) :=
rfl

variables [hp : fact (0 < p)]
include hp

section floor_ring

variables [floor_ring ğ•œ]

@[simp] lemma coe_equiv_Ico_mk_apply (x : ğ•œ) :
  (equiv_Ico p $ quotient_add_group.mk x : ğ•œ) = int.fract (x / p) * p :=
to_Ico_mod_eq_fract_mul _ x

instance : divisible_by (add_circle p) â„¤ :=
{ div := Î» x n, (â†‘(((n : ğ•œ)â»Â¹) * (equiv_Ico p x : ğ•œ)) : add_circle p),
  div_zero := Î» x,
    by simp only [algebra_map.coe_zero, quotient_add_group.coe_zero, inv_zero, zero_mul],
  div_cancel := Î» n x hn,
  begin
    replace hn : (n : ğ•œ) â‰  0, { norm_cast, assumption, },
    change n â€¢ quotient_add_group.mk' _ ((n : ğ•œ)â»Â¹ * â†‘(equiv_Ico p x)) = x,
    rw [â† map_zsmul, â† smul_mul_assoc, zsmul_eq_mul, mul_inv_cancel hn, one_mul],
    exact (equiv_Ico p).symm_apply_apply x,
  end, }

end floor_ring

section finite_order_points

variables {p}

lemma add_order_of_div_of_gcd_eq_one {m n : â„•} (hn : 0 < n) (h : gcd m n = 1) :
  add_order_of (â†‘(â†‘m / â†‘n * p) : add_circle p) = n :=
begin
  rcases m.eq_zero_or_pos with rfl | hm, { rw [gcd_zero_left, normalize_eq] at h, simp [h], },
  set x : add_circle p := â†‘(â†‘m / â†‘n * p),
  have hnâ‚€ : (n : ğ•œ) â‰  0, { norm_cast, exact ne_of_gt hn, },
  have hnx : n â€¢ x = 0,
  { rw [â† coe_nsmul, nsmul_eq_mul, â† mul_assoc, mul_div, mul_div_cancel_left _ hnâ‚€,
      â† nsmul_eq_mul, quotient_add_group.eq_zero_iff],
    exact nsmul_mem_zmultiples p m, },
  apply nat.dvd_antisymm (add_order_of_dvd_of_nsmul_eq_zero hnx),
  suffices : âˆƒ (z : â„•), z * n = (add_order_of x) * m,
  { obtain âŸ¨z, hzâŸ© := this,
    simpa only [h, mul_one, gcd_comm n] using dvd_mul_gcd_of_dvd_mul (dvd.intro_left z hz), },
  replace hp := hp.out,
  have : 0 < add_order_of x â€¢ (â†‘m / â†‘n * p) := smul_pos
    (add_order_of_pos' $ (is_of_fin_add_order_iff_nsmul_eq_zero _).2 âŸ¨n, hn, hnxâŸ©) (by positivity),
  obtain âŸ¨z, hzâŸ© := (coe_eq_zero_of_pos_iff p hp this).mp (add_order_of_nsmul_eq_zero x),
  rw [â† smul_mul_assoc, nsmul_eq_mul, nsmul_eq_mul, mul_left_inj' hp.ne.symm, mul_div,
    eq_div_iff hnâ‚€] at hz,
  norm_cast at hz,
  exact âŸ¨z, hzâŸ©,
end

lemma add_order_of_div_of_gcd_eq_one' {m : â„¤} {n : â„•} (hn : 0 < n) (h : gcd m.nat_abs n = 1) :
  add_order_of (â†‘(â†‘m / â†‘n * p) : add_circle p) = n :=
begin
  induction m,
  { simp only [int.of_nat_eq_coe, int.cast_coe_nat, int.nat_abs_of_nat] at h âŠ¢,
    exact add_order_of_div_of_gcd_eq_one hn h, },
  { simp only [int.cast_neg_succ_of_nat, neg_div, neg_mul, coe_neg, order_of_neg],
    exact add_order_of_div_of_gcd_eq_one hn h, },
end

lemma add_order_of_coe_rat {q : â„š} : add_order_of (â†‘(â†‘q * p) : add_circle p) = q.denom :=
begin
  have : (â†‘(q.denom : â„¤) : ğ•œ) â‰  0, { norm_cast, exact q.pos.ne.symm, },
  rw [â† @rat.num_denom q, rat.cast_mk_of_ne_zero _ _ this, int.cast_coe_nat, rat.num_denom,
    add_order_of_div_of_gcd_eq_one' q.pos q.cop],
  apply_instance,
end

variables (p)

lemma gcd_mul_add_order_of_div_eq {n : â„•} (m : â„•) (hn : 0 < n) :
  gcd m n * add_order_of (â†‘(â†‘m / â†‘n * p) : add_circle p) = n :=
begin
  let n' := n / gcd m n,
  let m' := m / gcd m n,
  have hâ‚€ : 0 < gcd m n,
  { rw zero_lt_iff at hn âŠ¢, contrapose! hn, exact ((gcd_eq_zero_iff m n).mp hn).2, },
  have hk' : 0 < n' := nat.div_pos (nat.le_of_dvd hn $ gcd_dvd_right m n) hâ‚€,
  have hgcd : gcd m' n' = 1 := nat.coprime_div_gcd_div_gcd hâ‚€,
  simp only [mul_left_inj' hp.out.ne.symm,
    â† nat.cast_div_div_div_cancel_right (gcd_dvd_right m n) (gcd_dvd_left m n),
    add_order_of_div_of_gcd_eq_one hk' hgcd, mul_comm _ n', nat.div_mul_cancel (gcd_dvd_right m n)],
end

variables {p} [floor_ring ğ•œ]

lemma exists_gcd_eq_one_of_is_of_fin_add_order {u : add_circle p} (h : is_of_fin_add_order u) :
  âˆƒ m, gcd m (add_order_of u) = 1 âˆ§
       m < (add_order_of u) âˆ§
       â†‘(((m : ğ•œ) / add_order_of u) * p) = u :=
begin
  rcases eq_or_ne u 0 with rfl | hu, { exact âŸ¨0, by simpâŸ©, },
  set n := add_order_of u,
  change âˆƒ m, gcd m n = 1 âˆ§ m < n âˆ§ â†‘((â†‘m / â†‘n) * p) = u,
  have hn : 0 < n := add_order_of_pos' h,
  have hnâ‚€ : (n : ğ•œ) â‰  0, { norm_cast, exact ne_of_gt hn, },
  let x := (equiv_Ico p u : ğ•œ),
  have hxu : (x : add_circle p) = u := (equiv_Ico p).symm_apply_apply u,
  have hxâ‚€ : 0 < (add_order_of (x : add_circle p)), { rw â† hxu at h, exact add_order_of_pos' h, },
  have hxâ‚ : 0 < x,
  { refine lt_of_le_of_ne (equiv_Ico p u).2.1 _,
    contrapose! hu,
    rw [â† hxu, â† hu, quotient_add_group.coe_zero], },
  obtain âŸ¨m, hm : m â€¢ p = add_order_of â†‘x â€¢ xâŸ© := (coe_eq_zero_of_pos_iff p hp.out
    (by positivity)).mp (add_order_of_nsmul_eq_zero (x : add_circle p)),
  replace hm : â†‘m * p = â†‘n * x, { simpa only [hxu, nsmul_eq_mul] using hm, },
  have hux : â†‘(â†‘m / â†‘n * p) = u,
  { rw [â† hxu, â† mul_div_right_comm, hm, mul_comm _ x, mul_div_cancel x hnâ‚€], },
  refine âŸ¨m, (_ : gcd m n = 1), (_ : m < n), huxâŸ©,
  { have := gcd_mul_add_order_of_div_eq p m hn,
    rwa [hux, nat.mul_left_eq_self_iff hn] at this, },
  { have : n â€¢ x < n â€¢ p := smul_lt_smul_of_pos (equiv_Ico p u).2.2 hn,
    rwa [nsmul_eq_mul, nsmul_eq_mul, â† hm, mul_lt_mul_right hp.out, nat.cast_lt] at this, },
end

end finite_order_points

end linear_ordered_field

variables (p : â„)

/-- The "additive circle" `â„ â§¸ (â„¤ âˆ™ p)` is compact. -/
instance compact_space [fact (0 < p)] : compact_space $ add_circle p :=
begin
  rw [â† is_compact_univ_iff, â† coe_image_Icc_eq p],
  exact is_compact_Icc.image (add_circle.continuous_mk' p),
end

/-- The action on `â„` by right multiplication of its the subgroup `zmultiples p` (the multiples of
`p:â„`) is properly discontinuous. -/
instance : properly_discontinuous_vadd (zmultiples p).opposite â„ :=
(zmultiples p).properly_discontinuous_vadd_opposite_of_tendsto_cofinite
  (add_subgroup.tendsto_zmultiples_subtype_cofinite p)

/-- The "additive circle" `â„ â§¸ (â„¤ âˆ™ p)` is Hausdorff. -/
instance : t2_space (add_circle p) := t2_space_of_properly_discontinuous_vadd_of_t2_space

/-- The "additive circle" `â„ â§¸ (â„¤ âˆ™ p)` is normal. -/
instance [fact (0 < p)] : normal_space (add_circle p) := normal_of_compact_t2

/-- The "additive circle" `â„ â§¸ (â„¤ âˆ™ p)` is second-countable. -/
instance : second_countable_topology (add_circle p) := quotient_add_group.second_countable_topology

end add_circle

private lemma fact_zero_lt_one : fact ((0:â„) < 1) := âŸ¨zero_lt_oneâŸ©
local attribute [instance] fact_zero_lt_one

/-- The unit circle `â„ â§¸ â„¤`. -/
@[derive [compact_space, normal_space, second_countable_topology]]
abbreviation unit_add_circle := add_circle (1 : â„)


section identify_Icc_ends
/-! This section proves that the natural map from `[0, T] âŠ‚ â„` to `add_circle T` gives an
identification of `add_circle T`, as a topological space, with the quotient of `[0, T]` by the
equivalence relation identifying `0` with `T`. -/

variables (T : â„) [hT : fact (0 < T)]
include hT
local notation `ğ•‹` := add_circle T

private lemma Icc_to_circle_kernel {x y : â„} (hx : x âˆˆ Icc 0 T) (hy : y âˆˆ Icc 0 T) :
  (x : ğ•‹) = (y : ğ•‹) â†” (x = y) âˆ¨ (x = 0 âˆ§ y = T) âˆ¨ (y = 0 âˆ§ x = T) :=
begin
  simp_rw [quotient_add_group.eq, add_subgroup.mem_zmultiples_iff, zsmul_eq_mul],
  split,
  { rintros âŸ¨n, hnâŸ©,
    have : | -x + y| â‰¤ T,
    { rw abs_le,
      exact âŸ¨by linarith [hx.2, hy.1], by linarith [hx.1, hy.2]âŸ©, },
    rw [â†hn, abs_mul, abs_of_pos hT.out, mul_le_iff_le_one_left hT.out, â†int.cast_abs,
      â†int.cast_one, int.cast_le] at this,
    replace this : |n| = 0 âˆ¨ |n| = 1,
    { rcases eq_or_lt_of_le this with h|h,
      { exact or.inr h, },
      { exact or.inl (by linarith [abs_nonneg n]) } },
    rw [abs_eq (zero_le_one' â„¤), abs_eq_zero] at this,
    rcases this with (rfl | rfl | rfl),
    { rw int.cast_zero at hn,
      exact or.inl (by linarith), },
    { rw int.cast_one at hn,
      exact or.inr (or.inl âŸ¨by linarith [hx.1, hy.2], by linarith [hx.1, hy.2]âŸ©) },
    { rw [int.cast_neg, int.cast_one] at hn,
      exact or.inr (or.inr âŸ¨by linarith [hy.1, hx.2], by linarith [hy.1, hx.2]âŸ©), }, },
  { rintros (rfl | âŸ¨rfl, hâŸ© | âŸ¨rfl, hâŸ©),
    exacts [ âŸ¨0, by simpâŸ©, âŸ¨1, by {rw h, simp}âŸ©, âŸ¨-1, by {rw h, simp}âŸ©] },
end

variables {T}

/-- The equivalence relation on `Icc 0 T` which identifies `0` and `T`. -/
private def S : setoid (Icc 0 T) :=
{ r := Î» x y, (x.1 = y.1) âˆ¨ (x.1 = 0 âˆ§ y.1 = T) âˆ¨ (y.1 = 0 âˆ§ x.1 = T),
  iseqv := âŸ¨(Î» x, by tauto), (Î» x y hxy, by tauto),
    (Î» x y z hxy hyz, (Icc_to_circle_kernel T x.2 z.2).mp
    (((Icc_to_circle_kernel T x.2 y.2).mpr hxy).trans
    ((Icc_to_circle_kernel T y.2 z.2).mpr hyz)))âŸ© }

local attribute [instance] S

private lemma Icc_quot_welldef (a b : Icc 0 T) (hab : a â‰ˆ b) : (a : ğ•‹) = (b : ğ•‹) :=
(Icc_to_circle_kernel T a.2 b.2).mpr hab

private lemma Icc_quot_bijective : bijective (quotient.lift coe $ @Icc_quot_welldef T hT) :=
begin
  refine âŸ¨Î» x y hxy, _, Î» x, _âŸ©,
  { induction x using quotient.induction_on,
    induction y using quotient.induction_on,
    rw [quotient.lift_mk, quotient.lift_mk, coe_coe, coe_coe] at hxy,
    simpa [quotient.eq] using (Icc_to_circle_kernel T x.2 y.2).mp hxy },
  { obtain âŸ¨y, hyâŸ© := (by { rw add_circle.coe_image_Icc_eq T, tauto } : x âˆˆ coe '' Icc 0 T),
    exact âŸ¨quotient.mk âŸ¨y, hy.1âŸ©, hy.2âŸ© },
end

/-- The natural map from `[0, T]` with endpoints identified to `â„ / â„¤ â€¢ T`. This definition
is private since it is subsumed by `Icc_circle_homeo` later. -/
private def Icc_circle_equiv : equiv (quotient S) ğ•‹ :=
{ to_fun    := quotient.lift coe $ @Icc_quot_welldef T hT,
  inv_fun   := classical.some (bijective_iff_has_inverse.mp Icc_quot_bijective),
  left_inv  := (classical.some_spec $ bijective_iff_has_inverse.mp Icc_quot_bijective).1,
  right_inv := (classical.some_spec $ bijective_iff_has_inverse.mp Icc_quot_bijective).2 }

/-- doesn't work if inlined in `homeo_of_equiv_compact_to_t2` -- why? -/
private lemma continuous_Icc_circle_equiv : continuous (@Icc_circle_equiv T hT) :=
continuous_quot_lift _ ((add_circle.continuous_mk' T).comp continuous_subtype_coe)

/-- The natural map from `[0, T]` with endpoints identified to `â„ / â„¤ â€¢ T`, as a homeomorphism of
topological spaces. -/
def add_circle.Icc_circle_homeo : homeomorph (quotient S) ğ•‹ :=
continuous.homeo_of_equiv_compact_to_t2 continuous_Icc_circle_equiv

/-! We now show that a continuous function on `[0, 1]` satisfying `f 0 = f 1` is the
pullback of a continuous function on `unit_add_circle`. -/

variables {B : Type*}

private lemma satisfies_rel {f : â„ â†’ B} (hf : f 0 = f T) (x y : Icc 0 T) : S.rel x y â†’ f x = f y :=
by { rintro (h | âŸ¨h1, h2âŸ© | âŸ¨h1, h2âŸ©), { tauto }, { convert hf }, { convert hf.symm, } }

/-- Given a function on `[0, T]` with `f 0 = f T`, lift it to `add_circle T`. -/
def add_circle.lift_Icc {f : â„ â†’ B} (h : f 0 = f T) : ğ•‹ â†’ B :=
(quotient.lift (restrict (Icc 0 T) f) $ satisfies_rel h) âˆ˜ Icc_circle_equiv.symm

lemma add_circle.lift_Icc_coe_apply {f : â„ â†’ B} (hf : f 0 = f T) {x : â„} (hx : x âˆˆ Icc 0 T) :
add_circle.lift_Icc hf â†‘x = f x :=
begin
  have : Icc_circle_equiv.symm x = quotient.mk âŸ¨x, hxâŸ©,
  { rw equiv.apply_eq_iff_eq_symm_apply,
    refl, },
  rw [add_circle.lift_Icc, comp_apply, this, quotient.lift_mk],
  refl,
end

lemma add_circle.lift_Icc_continuous [topological_space B] {f : â„ â†’ B}
(hf : f 0 = f T) (hc : continuous_on f $ Icc 0 T) : continuous (add_circle.lift_Icc hf) :=
begin
  refine continuous.comp _ add_circle.Icc_circle_homeo.continuous_inv_fun,
  rw [continuous_coinduced_dom, quotient.lift_comp_mk],
  exact continuous_on_iff_continuous_restrict.mp hc,
end

end identify_Icc_ends
