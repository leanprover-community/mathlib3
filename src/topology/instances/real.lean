/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import topology.metric_space.basic
import topology.algebra.uniform_group
import topology.algebra.ring
import ring_theory.subring
import group_theory.archimedean
import algebra.periodic

/-!
# Topological properties of ‚Ñù
-/

noncomputable theory
open classical filter int metric set topological_space
open_locale classical topological_space filter uniformity interval

universes u v w
variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

instance : metric_space ‚Ñö :=
metric_space.induced coe rat.cast_injective real.metric_space

namespace rat

theorem dist_eq (x y : ‚Ñö) : dist x y = |x - y| := rfl

@[norm_cast, simp] lemma dist_cast (x y : ‚Ñö) : dist (x : ‚Ñù) y = dist x y := rfl

theorem uniform_continuous_coe_real : uniform_continuous (coe : ‚Ñö ‚Üí ‚Ñù) :=
uniform_continuous_comap

theorem uniform_embedding_coe_real : uniform_embedding (coe : ‚Ñö ‚Üí ‚Ñù) :=
uniform_embedding_comap rat.cast_injective

theorem dense_embedding_coe_real : dense_embedding (coe : ‚Ñö ‚Üí ‚Ñù) :=
uniform_embedding_coe_real.dense_embedding $
Œª x, mem_closure_iff_nhds.2 $ Œª t ht,
let ‚ü®Œµ,Œµ0, hŒµ‚ü© := metric.mem_nhds_iff.1 ht in
let ‚ü®q, h‚ü© := exists_rat_near x Œµ0 in
‚ü®_, hŒµ (mem_ball'.2 h), q, rfl‚ü©

theorem embedding_coe_real : embedding (coe : ‚Ñö ‚Üí ‚Ñù) := dense_embedding_coe_real.to_embedding

theorem continuous_coe_real : continuous (coe : ‚Ñö ‚Üí ‚Ñù) := uniform_continuous_coe_real.continuous

end rat

namespace int

instance : has_dist ‚Ñ§ := ‚ü®Œª x y, dist (x : ‚Ñù) y‚ü©

theorem dist_eq (x y : ‚Ñ§) : dist x y = |x - y| := rfl

@[norm_cast, simp] theorem dist_cast_real (x y : ‚Ñ§) : dist (x : ‚Ñù) y = dist x y := rfl

@[norm_cast, simp] theorem dist_cast_rat (x y : ‚Ñ§) : dist (x : ‚Ñö) y = dist x y :=
by rw [‚Üê int.dist_cast_real, ‚Üê rat.dist_cast]; congr' 1; norm_cast

lemma pairwise_one_le_dist : pairwise (Œª m n : ‚Ñ§, 1 ‚â§ dist m n) :=
begin
  intros m n hne,
  rw dist_eq, norm_cast, rwa [‚Üê zero_add (1 : ‚Ñ§), int.add_one_le_iff, abs_pos, sub_ne_zero]
end

lemma uniform_embedding_coe_rat : uniform_embedding (coe : ‚Ñ§ ‚Üí ‚Ñö) :=
uniform_embedding_bot_of_pairwise_le_dist zero_lt_one $ by simpa using pairwise_one_le_dist

lemma closed_embedding_coe_rat : closed_embedding (coe : ‚Ñ§ ‚Üí ‚Ñö) :=
closed_embedding_of_pairwise_le_dist zero_lt_one $ by simpa using pairwise_one_le_dist

lemma uniform_embedding_coe_real : uniform_embedding (coe : ‚Ñ§ ‚Üí ‚Ñù) :=
uniform_embedding_bot_of_pairwise_le_dist zero_lt_one pairwise_one_le_dist

lemma closed_embedding_coe_real : closed_embedding (coe : ‚Ñ§ ‚Üí ‚Ñù) :=
closed_embedding_of_pairwise_le_dist zero_lt_one pairwise_one_le_dist

instance : metric_space ‚Ñ§ := int.uniform_embedding_coe_real.comap_metric_space _

theorem preimage_ball (x : ‚Ñ§) (r : ‚Ñù) : coe ‚Åª¬π' (ball (x : ‚Ñù) r) = ball x r := rfl

theorem preimage_closed_ball (x : ‚Ñ§) (r : ‚Ñù) :
  coe ‚Åª¬π' (closed_ball (x : ‚Ñù) r) = closed_ball x r := rfl

theorem ball_eq (x : ‚Ñ§) (r : ‚Ñù) : ball x r = Ioo ‚åä‚Üëx - r‚åã ‚åà‚Üëx + r‚åâ :=
by rw [‚Üê preimage_ball, real.ball_eq, preimage_Ioo]

theorem closed_ball_eq (x : ‚Ñ§) (r : ‚Ñù) : closed_ball x r = Icc ‚åà‚Üëx - r‚åâ ‚åä‚Üëx + r‚åã :=
by rw [‚Üê preimage_closed_ball, real.closed_ball_eq, preimage_Icc]

instance : proper_space ‚Ñ§ :=
‚ü® begin
    intros x r,
    rw closed_ball_eq,
    exact (set.finite_Icc _ _).is_compact,
  end ‚ü©

end int

theorem real.uniform_continuous_add : uniform_continuous (Œªp : ‚Ñù √ó ‚Ñù, p.1 + p.2) :=
metric.uniform_continuous_iff.2 $ Œª Œµ Œµ0,
let ‚ü®Œ¥, Œ¥0, HŒ¥‚ü© := rat_add_continuous_lemma abs Œµ0 in
‚ü®Œ¥, Œ¥0, Œª a b h, let ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := max_lt_iff.1 h in HŒ¥ h‚ÇÅ h‚ÇÇ‚ü©

-- TODO(Mario): Find a way to use rat_add_continuous_lemma
theorem rat.uniform_continuous_add : uniform_continuous (Œªp : ‚Ñö √ó ‚Ñö, p.1 + p.2) :=
rat.uniform_embedding_coe_real.to_uniform_inducing.uniform_continuous_iff.2 $
  by simp only [(‚àò), rat.cast_add]; exact real.uniform_continuous_add.comp
    (rat.uniform_continuous_coe_real.prod_map rat.uniform_continuous_coe_real)

theorem real.uniform_continuous_neg : uniform_continuous (@has_neg.neg ‚Ñù _) :=
metric.uniform_continuous_iff.2 $ Œª Œµ Œµ0, ‚ü®_, Œµ0, Œª a b h,
  by rw dist_comm at h; simpa [real.dist_eq] using h‚ü©

theorem rat.uniform_continuous_neg : uniform_continuous (@has_neg.neg ‚Ñö _) :=
metric.uniform_continuous_iff.2 $ Œª Œµ Œµ0, ‚ü®_, Œµ0, Œª a b h,
  by rw dist_comm at h; simpa [rat.dist_eq] using h‚ü©

instance : uniform_add_group ‚Ñù :=
uniform_add_group.mk' real.uniform_continuous_add real.uniform_continuous_neg

instance : uniform_add_group ‚Ñö :=
uniform_add_group.mk' rat.uniform_continuous_add rat.uniform_continuous_neg

 -- short-circuit type class inference
instance : topological_add_group ‚Ñù := by apply_instance
instance : topological_add_group ‚Ñö := by apply_instance

instance : order_topology ‚Ñö :=
induced_order_topology _ (Œª x y, rat.cast_lt) (@exists_rat_btwn _ _ _)

instance : proper_space ‚Ñù :=
{ is_compact_closed_ball := Œªx r, by { rw real.closed_ball_eq, apply is_compact_Icc } }

instance : second_countable_topology ‚Ñù := second_countable_of_proper

lemma real.is_topological_basis_Ioo_rat :
  @is_topological_basis ‚Ñù _ (‚ãÉ(a b : ‚Ñö) (h : a < b), {Ioo a b}) :=
is_topological_basis_of_open_of_nhds
  (by simp [is_open_Ioo] {contextual:=tt})
  (assume a v hav hv,
    let ‚ü®l, u, ‚ü®hl, hu‚ü©, h‚ü© := mem_nhds_iff_exists_Ioo_subset.mp (is_open.mem_nhds hv hav),
        ‚ü®q, hlq, hqa‚ü© := exists_rat_btwn hl,
        ‚ü®p, hap, hpu‚ü© := exists_rat_btwn hu in
    ‚ü®Ioo q p,
      by { simp only [mem_Union], exact ‚ü®q, p, rat.cast_lt.1 $ hqa.trans hap, rfl‚ü© },
      ‚ü®hqa, hap‚ü©, assume a' ‚ü®hqa', ha'p‚ü©, h ‚ü®hlq.trans hqa', ha'p.trans hpu‚ü©‚ü©)

/- TODO(Mario): Prove that these are uniform isomorphisms instead of uniform embeddings
lemma uniform_embedding_add_rat {r : ‚Ñö} : uniform_embedding (Œªp:‚Ñö, p + r) :=
_

lemma uniform_embedding_mul_rat {q : ‚Ñö} (hq : q ‚â† 0) : uniform_embedding ((*) q) :=
_ -/

lemma real.mem_closure_iff {s : set ‚Ñù} {x : ‚Ñù} :
  x ‚àà closure s ‚Üî ‚àÄ Œµ > 0, ‚àÉ y ‚àà s, |y - x| < Œµ :=
by simp [mem_closure_iff_nhds_basis nhds_basis_ball, real.dist_eq]

lemma real.uniform_continuous_inv (s : set ‚Ñù) {r : ‚Ñù} (r0 : 0 < r) (H : ‚àÄ x ‚àà s, r ‚â§ |x|) :
  uniform_continuous (Œªp:s, p.1‚Åª¬π) :=
metric.uniform_continuous_iff.2 $ Œª Œµ Œµ0,
let ‚ü®Œ¥, Œ¥0, HŒ¥‚ü© := rat_inv_continuous_lemma abs Œµ0 r0 in
‚ü®Œ¥, Œ¥0, Œª a b h, HŒ¥ (H _ a.2) (H _ b.2) h‚ü©

lemma real.uniform_continuous_abs : uniform_continuous (abs : ‚Ñù ‚Üí ‚Ñù) :=
metric.uniform_continuous_iff.2 $ Œª Œµ Œµ0,
  ‚ü®Œµ, Œµ0, Œª a b, lt_of_le_of_lt (abs_abs_sub_abs_le_abs_sub _ _)‚ü©

lemma rat.uniform_continuous_abs : uniform_continuous (abs : ‚Ñö ‚Üí ‚Ñö) :=
metric.uniform_continuous_iff.2 $ Œª Œµ Œµ0,
  ‚ü®Œµ, Œµ0, Œª a b h, lt_of_le_of_lt
    (by simpa [rat.dist_eq] using abs_abs_sub_abs_le_abs_sub _ _) h‚ü©

lemma real.tendsto_inv {r : ‚Ñù} (r0 : r ‚â† 0) : tendsto (Œªq, q‚Åª¬π) (ùìù r) (ùìù r‚Åª¬π) :=
by rw ‚Üê abs_pos at r0; exact
tendsto_of_uniform_continuous_subtype
  (real.uniform_continuous_inv {x | |r| / 2 < |x|} (half_pos r0) (Œª x h, le_of_lt h))
  (is_open.mem_nhds ((is_open_lt' (|r| / 2)).preimage continuous_abs) (half_lt_self r0))

lemma real.continuous_inv : continuous (Œªa:{r:‚Ñù // r ‚â† 0}, a.val‚Åª¬π) :=
continuous_iff_continuous_at.mpr $ assume ‚ü®r, hr‚ü©,
  tendsto.comp (real.tendsto_inv hr) (continuous_iff_continuous_at.mp continuous_subtype_val _)

lemma real.continuous.inv [topological_space Œ±] {f : Œ± ‚Üí ‚Ñù} (h : ‚àÄa, f a ‚â† 0) (hf : continuous f) :
  continuous (Œªa, (f a)‚Åª¬π) :=
show continuous ((has_inv.inv ‚àò @subtype.val ‚Ñù (Œªr, r ‚â† 0)) ‚àò Œªa, ‚ü®f a, h a‚ü©),
  from real.continuous_inv.comp (continuous_subtype_mk _ hf)

lemma real.uniform_continuous_mul_const {x : ‚Ñù} : uniform_continuous ((*) x) :=
metric.uniform_continuous_iff.2 $ Œª Œµ Œµ0, begin
  cases no_top (|x|) with y xy,
  have y0 := lt_of_le_of_lt (abs_nonneg _) xy,
  refine ‚ü®_, div_pos Œµ0 y0, Œª a b h, _‚ü©,
  rw [real.dist_eq, ‚Üê mul_sub, abs_mul, ‚Üê mul_div_cancel' Œµ (ne_of_gt y0)],
  exact mul_lt_mul' (le_of_lt xy) h (abs_nonneg _) y0
end

lemma real.uniform_continuous_mul (s : set (‚Ñù √ó ‚Ñù))
  {r‚ÇÅ r‚ÇÇ : ‚Ñù} (H : ‚àÄ x ‚àà s, |(x : ‚Ñù √ó ‚Ñù).1| < r‚ÇÅ ‚àß |x.2| < r‚ÇÇ) :
  uniform_continuous (Œªp:s, p.1.1 * p.1.2) :=
metric.uniform_continuous_iff.2 $ Œª Œµ Œµ0,
let ‚ü®Œ¥, Œ¥0, HŒ¥‚ü© := rat_mul_continuous_lemma abs Œµ0 in
‚ü®Œ¥, Œ¥0, Œª a b h,
  let ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := max_lt_iff.1 h in HŒ¥ (H _ a.2).1 (H _ b.2).2 h‚ÇÅ h‚ÇÇ‚ü©

protected lemma real.continuous_mul : continuous (Œªp : ‚Ñù √ó ‚Ñù, p.1 * p.2) :=
continuous_iff_continuous_at.2 $ Œª ‚ü®a‚ÇÅ, a‚ÇÇ‚ü©,
tendsto_of_uniform_continuous_subtype
  (real.uniform_continuous_mul
    ({x | |x| < |a‚ÇÅ| + 1}.prod {x | |x| < |a‚ÇÇ| + 1})
    (Œª x, id))
  (is_open.mem_nhds
    (((is_open_gt' (|a‚ÇÅ| + 1)).preimage continuous_abs).prod
      ((is_open_gt' (|a‚ÇÇ| + 1)).preimage continuous_abs ))
    ‚ü®lt_add_one (|a‚ÇÅ|), lt_add_one (|a‚ÇÇ|)‚ü©)

instance : topological_ring ‚Ñù :=
{ continuous_mul := real.continuous_mul, ..real.topological_add_group }

lemma rat.continuous_mul : continuous (Œªp : ‚Ñö √ó ‚Ñö, p.1 * p.2) :=
rat.embedding_coe_real.continuous_iff.2 $ by simp [(‚àò)]; exact
real.continuous_mul.comp ((rat.continuous_coe_real.prod_map rat.continuous_coe_real))

instance : topological_ring ‚Ñö :=
{ continuous_mul := rat.continuous_mul, ..rat.topological_add_group }

theorem real.ball_eq_Ioo (x Œµ : ‚Ñù) : ball x Œµ = Ioo (x - Œµ) (x + Œµ) :=
set.ext $ Œª y, by rw [mem_ball, real.dist_eq,
  abs_sub_lt_iff, sub_lt_iff_lt_add', and_comm, sub_lt]; refl

theorem real.Ioo_eq_ball (x y : ‚Ñù) : Ioo x y = ball ((x + y) / 2) ((y - x) / 2) :=
by rw [real.ball_eq_Ioo, ‚Üê sub_div, add_comm, ‚Üê sub_add,
  add_sub_cancel', add_self_div_two, ‚Üê add_div,
  add_assoc, add_sub_cancel'_right, add_self_div_two]

instance : complete_space ‚Ñù :=
begin
  apply complete_of_cauchy_seq_tendsto,
  intros u hu,
  let c : cau_seq ‚Ñù abs := ‚ü®u, metric.cauchy_seq_iff'.1 hu‚ü©,
  refine ‚ü®c.lim, Œª s h, _‚ü©,
  rcases metric.mem_nhds_iff.1 h with ‚ü®Œµ, Œµ0, hŒµ‚ü©,
  have := c.equiv_lim Œµ Œµ0,
  simp only [mem_map, mem_at_top_sets, mem_set_of_eq],
  refine this.imp (Œª N hN n hn, hŒµ (hN n hn))
end

lemma real.totally_bounded_ball (x Œµ : ‚Ñù) : totally_bounded (ball x Œµ) :=
by rw real.ball_eq_Ioo; apply totally_bounded_Ioo

lemma rat.totally_bounded_Icc (a b : ‚Ñö) : totally_bounded (Icc a b) :=
begin
  have := totally_bounded_preimage rat.uniform_embedding_coe_real (totally_bounded_Icc a b),
  rwa (set.ext (Œª q, _) : Icc _ _ = _), simp
end

section

lemma closure_of_rat_image_lt {q : ‚Ñö} : closure ((coe:‚Ñö ‚Üí ‚Ñù) '' {x | q < x}) = {r | ‚Üëq ‚â§ r} :=
subset.antisymm
  ((is_closed_ge' _).closure_subset_iff.2
    (image_subset_iff.2 $ Œª p h, le_of_lt $ (@rat.cast_lt ‚Ñù _ _ _).2 h)) $
Œª x hx, mem_closure_iff_nhds.2 $ Œª t ht,
let ‚ü®Œµ, Œµ0, hŒµ‚ü© := metric.mem_nhds_iff.1 ht in
let ‚ü®p, h‚ÇÅ, h‚ÇÇ‚ü© := exists_rat_btwn ((lt_add_iff_pos_right x).2 Œµ0) in
‚ü®_, hŒµ (show abs _ < _,
    by rwa [abs_of_nonneg (le_of_lt $ sub_pos.2 h‚ÇÅ), sub_lt_iff_lt_add']),
  p, rat.cast_lt.1 (@lt_of_le_of_lt ‚Ñù _ _ _ _ hx h‚ÇÅ), rfl‚ü©

/- TODO(Mario): Put these back only if needed later
lemma closure_of_rat_image_le_eq {q : ‚Ñö} : closure ((coe:‚Ñö ‚Üí ‚Ñù) '' {x | q ‚â§ x}) = {r | ‚Üëq ‚â§ r} :=
_

lemma closure_of_rat_image_le_le_eq {a b : ‚Ñö} (hab : a ‚â§ b) :
  closure (of_rat '' {q:‚Ñö | a ‚â§ q ‚àß q ‚â§ b}) = {r:‚Ñù | of_rat a ‚â§ r ‚àß r ‚â§ of_rat b} :=
_-/

lemma real.bounded_iff_bdd_below_bdd_above {s : set ‚Ñù} : bounded s ‚Üî bdd_below s ‚àß bdd_above s :=
‚ü®begin
  assume bdd,
  rcases (bounded_iff_subset_ball 0).1 bdd with ‚ü®r, hr‚ü©, -- hr : s ‚äÜ closed_ball 0 r
  rw real.closed_ball_eq at hr, -- hr : s ‚äÜ Icc (0 - r) (0 + r)
  exact ‚ü®bdd_below_Icc.mono hr, bdd_above_Icc.mono hr‚ü©
end,
begin
  intro h,
  rcases bdd_below_bdd_above_iff_subset_Icc.1 h with ‚ü®m, M, I : s ‚äÜ Icc m M‚ü©,
  exact (bounded_Icc m M).mono I
end‚ü©

lemma real.subset_Icc_Inf_Sup_of_bounded {s : set ‚Ñù} (h : bounded s) :
  s ‚äÜ Icc (Inf s) (Sup s) :=
subset_Icc_cInf_cSup (real.bounded_iff_bdd_below_bdd_above.1 h).1
  (real.bounded_iff_bdd_below_bdd_above.1 h).2

lemma real.image_Icc {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù} (hab : a ‚â§ b) (h : continuous_on f $ Icc a b) :
  f '' Icc a b = Icc (Inf $ f '' Icc a b) (Sup $ f '' Icc a b) :=
eq_Icc_of_connected_compact ‚ü®(nonempty_Icc.2 hab).image f, is_preconnected_Icc.image f h‚ü©
  (is_compact_Icc.image_of_continuous_on h)

lemma real.image_interval_eq_Icc {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù} (h : continuous_on f $ [a, b]) :
  f '' [a, b] = Icc (Inf (f '' [a, b])) (Sup (f '' [a, b])) :=
begin
  cases le_total a b with h2 h2,
  { simp_rw [interval_of_le h2] at h ‚ä¢, exact real.image_Icc h2 h },
  { simp_rw [interval_of_ge h2] at h ‚ä¢, exact real.image_Icc h2 h },
end

lemma real.image_interval {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù} (h : continuous_on f $ [a, b]) :
  f '' [a, b] = [Inf (f '' [a, b]), Sup (f '' [a, b])] :=
begin
  refine (real.image_interval_eq_Icc h).trans (interval_of_le _).symm,
  rw [real.image_interval_eq_Icc h],
  exact real.Inf_le_Sup _ bdd_below_Icc bdd_above_Icc
end

lemma real.interval_subset_image_interval {f : ‚Ñù ‚Üí ‚Ñù} {a b x y : ‚Ñù}
  (h : continuous_on f [a, b]) (hx : x ‚àà [a, b]) (hy : y ‚àà [a, b]) :
  [f x, f y] ‚äÜ f '' [a, b] :=
begin
  rw [real.image_interval h, interval_subset_interval_iff_mem, ‚Üê real.image_interval h],
  exact ‚ü®mem_image_of_mem f hx, mem_image_of_mem f hy‚ü©
end

end

section periodic

namespace function

lemma periodic.compact_of_continuous' [topological_space Œ±] {f : ‚Ñù ‚Üí Œ±} {c : ‚Ñù}
  (hp : periodic f c) (hc : 0 < c) (hf : continuous f) :
  is_compact (range f) :=
begin
  convert is_compact_Icc.image hf,
  ext x,
  refine ‚ü®_, mem_range_of_mem_image f (Icc 0 c)‚ü©,
  rintros ‚ü®y, h1‚ü©,
  obtain ‚ü®z, hz, h2‚ü© := hp.exists_mem_Ico hc y,
  exact ‚ü®z, mem_Icc_of_Ico hz, h2.symm.trans h1‚ü©,
end

/-- A continuous, periodic function has compact range. -/
lemma periodic.compact_of_continuous [topological_space Œ±] {f : ‚Ñù ‚Üí Œ±} {c : ‚Ñù}
  (hp : periodic f c) (hc : c ‚â† 0) (hf : continuous f) :
  is_compact (range f) :=
begin
  cases lt_or_gt_of_ne hc with hneg hpos,
  exacts [hp.neg.compact_of_continuous' (neg_pos.mpr hneg) hf, hp.compact_of_continuous' hpos hf],
end

/-- A continuous, periodic function is bounded. -/
lemma periodic.bounded_of_continuous [pseudo_metric_space Œ±] {f : ‚Ñù ‚Üí Œ±} {c : ‚Ñù}
  (hp : periodic f c) (hc : c ‚â† 0) (hf : continuous f) :
  bounded (range f) :=
(hp.compact_of_continuous hc hf).bounded

end function

end periodic

section subgroups

/-- Given a nontrivial subgroup `G ‚äÜ ‚Ñù`, if `G ‚à© ‚Ñù_{>0}` has no minimum then `G` is dense. -/
lemma real.subgroup_dense_of_no_min {G : add_subgroup ‚Ñù} {g‚ÇÄ : ‚Ñù} (g‚ÇÄ_in : g‚ÇÄ ‚àà G) (g‚ÇÄ_ne : g‚ÇÄ ‚â† 0)
  (H' : ¬¨ ‚àÉ a : ‚Ñù, is_least {g : ‚Ñù | g ‚àà G ‚àß 0 < g} a) :
  dense (G : set ‚Ñù) :=
begin
  let G_pos := {g : ‚Ñù | g ‚àà G ‚àß 0 < g},
  push_neg at H',
  intros x,
  suffices : ‚àÄ Œµ > (0 : ‚Ñù), ‚àÉ g ‚àà G, |x - g| < Œµ,
    by simpa only [real.mem_closure_iff, abs_sub_comm],
  intros Œµ Œµ_pos,
  obtain ‚ü®g‚ÇÅ, g‚ÇÅ_in, g‚ÇÅ_pos‚ü© : ‚àÉ g‚ÇÅ : ‚Ñù, g‚ÇÅ ‚àà G ‚àß 0 < g‚ÇÅ,
  { cases lt_or_gt_of_ne g‚ÇÄ_ne with Hg‚ÇÄ Hg‚ÇÄ,
    { exact ‚ü®-g‚ÇÄ, G.neg_mem g‚ÇÄ_in, neg_pos.mpr Hg‚ÇÄ‚ü© },
    { exact ‚ü®g‚ÇÄ, g‚ÇÄ_in, Hg‚ÇÄ‚ü© } },
  obtain ‚ü®a, ha‚ü© : ‚àÉ a, is_glb G_pos a :=
    ‚ü®Inf G_pos, is_glb_cInf ‚ü®g‚ÇÅ, g‚ÇÅ_in, g‚ÇÅ_pos‚ü© ‚ü®0, Œª _ hx, le_of_lt hx.2‚ü©‚ü©,
  have a_notin : a ‚àâ G_pos,
  { intros H,
    exact H' a ‚ü®H, ha.1‚ü© },
  obtain ‚ü®g‚ÇÇ, g‚ÇÇ_in, g‚ÇÇ_pos, g‚ÇÇ_lt‚ü© : ‚àÉ g‚ÇÇ : ‚Ñù, g‚ÇÇ ‚àà G ‚àß 0 < g‚ÇÇ ‚àß g‚ÇÇ < Œµ,
  { obtain ‚ü®b, hb, hb', hb''‚ü© := ha.exists_between_self_add' a_notin Œµ_pos,
    obtain ‚ü®c, hc, hc', hc''‚ü© := ha.exists_between_self_add' a_notin (sub_pos.2 hb'),
    refine ‚ü®b - c, G.sub_mem hb.1 hc.1, _, _‚ü© ;
    linarith },
  refine ‚ü®floor (x/g‚ÇÇ) * g‚ÇÇ, _, _‚ü©,
  { exact add_subgroup.int_mul_mem _ g‚ÇÇ_in },
  { rw abs_of_nonneg (sub_floor_div_mul_nonneg x g‚ÇÇ_pos),
    linarith [sub_floor_div_mul_lt x g‚ÇÇ_pos] }
end

/-- Subgroups of `‚Ñù` are either dense or cyclic. See `real.subgroup_dense_of_no_min` and
`subgroup_cyclic_of_min` for more precise statements. -/
lemma real.subgroup_dense_or_cyclic (G : add_subgroup ‚Ñù) :
  dense (G : set ‚Ñù) ‚à® ‚àÉ a : ‚Ñù, G = add_subgroup.closure {a} :=
begin
  cases add_subgroup.bot_or_exists_ne_zero G with H H,
  { right,
    use 0,
    rw [H, add_subgroup.closure_singleton_zero] },
  { let G_pos := {g : ‚Ñù | g ‚àà G ‚àß 0 < g},
    by_cases H' : ‚àÉ a, is_least G_pos a,
    { right,
      rcases H' with ‚ü®a, ha‚ü©,
      exact ‚ü®a, add_subgroup.cyclic_of_min ha‚ü© },
    { left,
      rcases H with ‚ü®g‚ÇÄ, g‚ÇÄ_in, g‚ÇÄ_ne‚ü©,
      exact real.subgroup_dense_of_no_min g‚ÇÄ_in g‚ÇÄ_ne H' } }
end

end subgroups
