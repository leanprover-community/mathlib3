/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import topology.bases
import order.filter.countable_Inter
import tactic.tfae

/-!
# LindelÃ¶f sets and spaces

In this file we define LindelÃ¶f sets, LindelÃ¶f spaces, and strongly (hereditarily) LindelÃ¶f
spaces. We also prove that a topological space with second countable topology is a strongly LindelÃ¶f
space.

## Main definitions

* We say that a set `s` in a topological space is a *LindelÃ¶f set* if any open cover of `s` admits a
  countable subcover.

* A topological space `X` is said to be *LindelÃ¶f* if the whole space is a LindelÃ¶f set.

* A topological space `X` is said to be *strongly (hereditarily) LindelÃ¶f* if any set in `X` is a
  LindelÃ¶f set.

## Tags

LindelÃ¶f space, open cover
-/

open filter set topological_space
open_locale filter topological_space

variables {Î¹ X Y : Type*} [topological_space X] [topological_space Y] {s t : set X}

/-- A set `s` in a topological space is called a *LindelÃ¶f set* if any open cover of `s` admits a
countable subcover. -/
def is_lindelof (s : set X) : Prop :=
âˆ€ â¦ƒU : set (set X)â¦„, (âˆ€ u âˆˆ U, is_open u) â†’ (s âŠ† â‹ƒâ‚€ U) â†’ âˆƒ V âŠ† U, countable V âˆ§ s âŠ† â‹ƒâ‚€ V

lemma is_lindelof.countable_open_subcover (h : is_lindelof s) {U : Î¹ â†’ set X}
  (hU : âˆ€ i, is_open (U i)) (hsU : s âŠ† â‹ƒ i, U i) :
  âˆƒ I : set Î¹, countable I âˆ§ s âŠ† â‹ƒ i âˆˆ I, U i :=
begin
  rcases @h (range U) (forall_range_iff.2 hU) hsU with âŸ¨V, hVU, hVc, hsVâŸ©,
  haveI := hVc.to_encodable,
  choose i hi using Î» v : V, @hVU v v.2,
  refine âŸ¨range i, countable_range _, _âŸ©,
  simpa only [bUnion_range, hi, â† sUnion_eq_Union]
end

lemma is_lindelof.countable_open_subcoverâ‚‚ (h : is_lindelof s) {t : set Î¹} {U : Î  i âˆˆ t, set X}
  (hU : âˆ€ i âˆˆ t, is_open (U i â€¹_â€º)) (hsU : s âŠ† â‹ƒ i âˆˆ t, U i â€¹i âˆˆ tâ€º) :
  âˆƒ I âŠ† t, countable I âˆ§ s âŠ† â‹ƒ i âˆˆ I, U i (â€¹I âŠ† tâ€º â€¹i âˆˆ Iâ€º) :=
begin
  rw bUnion_eq_Union at hsU,
  rcases h.countable_open_subcover (Î» i : t, hU i i.2) hsU with âŸ¨I, hIc, hsIâŸ©,
  refine âŸ¨coe '' I, subtype.coe_image_subset _ _, hIc.image _, _âŸ©,
  simpa [Union_coe_set] using hsI
end

lemma is_lindelof_of_exists_cluster_pt
  (h : âˆ€ (f : filter X) [ne_bot f] [countable_Inter_filter f], s âˆˆ f â†’ âˆƒ a âˆˆ s, cluster_pt a f) :
  is_lindelof s :=
begin
  intros U hUo hsU,
  set p : set X â†’ Prop := Î» u, âˆƒ I âŠ† U, countable I âˆ§ s \ u âŠ† â‹ƒâ‚€ I,
  have hp : âˆ€ S : set (set X), countable S â†’ (âˆ€ u âˆˆ S, p u) â†’ p (â‹‚â‚€ S),
  { intros S hSc hS,
    choose! I hIU hIc hI using hS,
    refine âŸ¨â‹ƒ u âˆˆ S, I u, Unionâ‚‚_subset hIU, hSc.bUnion hIc, _âŸ©,
    simp only [bUnion_Union, sInter_eq_bInter, diff_Inter, sUnion_Union],
    exact Unionâ‚‚_mono hI },
  have hp_mono : âˆ€ u v, p u â†’ u âŠ† v â†’ p v,
  { rintro u v âŸ¨I, hIU, hIc, hsubâŸ© huv,
    exact âŸ¨I, hIU, hIc, (diff_subset_diff_right huv).trans hsubâŸ© },
  set f : filter X := filter.of_countable_Inter {u | p u} hp hp_mono,
  have hf : âˆ€ {u}, u âˆˆ f â†” p u := Î» _, iff.rfl,
  suffices : Â¬ne_bot f,
  { simp_rw [ne_bot_iff, not_not, â† empty_mem_iff_bot, hf, p, diff_empty] at this,
    exact this },
  introI hfne,
  have : s âˆˆ f,
  { refine âŸ¨âˆ…, empty_subset _, countable_empty, _âŸ©,
    rw diff_self, exact empty_subset _ },
  rcases h _ this with âŸ¨x, hxs, hxfâŸ©,
  rcases hsU hxs with âŸ¨t, htU, hxtâŸ©,
  have : s \ t âˆˆ f,
  { refine âŸ¨{t}, singleton_subset_iff.2 htU, countable_singleton t, _âŸ©,
    rw [sdiff_sdiff_right_self, sUnion_singleton],
    exact inter_subset_right _ _ },
  rcases cluster_pt_iff.1 hxf ((hUo t htU).mem_nhds hxt) this with âŸ¨y, hy, -, hy'âŸ©,
  exact hy' hy
end

/-- A list of properties of a set that are equivalent to `is_lindelof`. Use one of
`is_lindelof_iff_*` or `is_lindelof.*` lemmas instead. -/
lemma is_lindelof_tfae (s : set X) :
  tfae [is_lindelof s,
    âˆ€ t : X â†’ set X, (âˆ€ x âˆˆ s, t x âˆˆ ğ“ x) â†’ âˆƒ I âŠ† s, countable I âˆ§ s âŠ† â‹ƒ x âˆˆ I, t x,
    âˆ€ t : X â†’ set X, (âˆ€ x âˆˆ s, t x âˆˆ ğ“[s] x) â†’ âˆƒ I âŠ† s, countable I âˆ§ s âŠ† â‹ƒ x âˆˆ I, t x,
    âˆ€ U : X â†’ set X, (âˆ€ x, is_open (U x)) â†’ (âˆ€ x, x âˆˆ U x) â†’
      âˆƒ I âŠ† s, countable I âˆ§ s âŠ† â‹ƒ x âˆˆ I, U x,
    âˆ€ T : set (set X), (âˆ€ x âˆˆ s, âˆƒ t âˆˆ T, t âˆˆ ğ“ x) â†’ âˆƒ I âŠ† T, countable I âˆ§ s âŠ† â‹ƒâ‚€ I,
    âˆ€ T : set (set X), (âˆ€ x âˆˆ s, âˆƒ t âˆˆ T, t âˆˆ ğ“[s] x) â†’ âˆƒ I âŠ† T, countable I âˆ§ s âŠ† â‹ƒâ‚€ I,
    âˆ€ â¦ƒfâ¦„ [ne_bot f] [countable_Inter_filter f], s âˆˆ f â†’ âˆƒ a âˆˆ s, cluster_pt a f] :=
begin
  tfae_have : 1 â†’ 3,
  { intros H t ht,
    simp only [mem_nhds_within] at ht,
    choose u huo hxu hut using ht,
    rcases H.countable_open_subcoverâ‚‚ huo (Î» x hx, mem_Unionâ‚‚.2 âŸ¨x, hx, hxu x hxâŸ©)
      with âŸ¨I, hIs, hIc, hsIâŸ©,
    replace hsI := subset_inter hsI subset.rfl, rw Unionâ‚‚_inter at hsI,
    exact âŸ¨I, hIs, hIc, hsI.trans $ Unionâ‚‚_mono $ Î» x hx, hut x _âŸ© },
  tfae_have : 3 â†’ 2, from Î» H t ht, H t (Î» x hx, mem_nhds_within_of_mem_nhds (ht x hx)),
  tfae_have : 2 â†’ 4,
    from Î» H U hUo hU, H U (Î» x hx, (hUo x).mem_nhds (hU x)),
  tfae_have : 4 â†’ 6,
  { intros H T hT,
    simp only [(nhds_within_basis_open _ _).mem_iff] at hT,
    replace hT : âˆ€ x âˆˆ s, âˆƒ (u : {u : set X // x âˆˆ u âˆ§ is_open u}) (t âˆˆ T), â†‘u âˆ© s âŠ† t,
      by simpa only [subtype.exists', @exists_swap {t // t âˆˆ T}] using hT,
    haveI : âˆ€ x, nonempty {u : set X // x âˆˆ u âˆ§ is_open u} := Î» x, âŸ¨âŸ¨univ, trivial, is_open_univâŸ©âŸ©,
    choose! u t htT ht using hT,
    rcases H (Î» x, u x) (Î» x, (u x).2.2) (Î» x, (u x).2.1) with âŸ¨I, hIs, hIc, hsIâŸ©,
    refine âŸ¨t '' I, image_subset_iff.2 (Î» x hx, htT _ $ hIs hx), hIc.image _, Î» x hx, _âŸ©,
    rcases mem_Unionâ‚‚.1 (hsI hx) with âŸ¨i, hi, hxiâŸ©,
    exact âŸ¨t i, mem_image_of_mem t hi, ht i (hIs hi) âŸ¨hxi, hxâŸ©âŸ© },
  tfae_have : 6 â†’ 5,
  { intros H T hT,
    exact H T (Î» x hx, (hT x hx).imp $ Î» t ht, âŸ¨ht.fst, mem_nhds_within_of_mem_nhds ht.sndâŸ©) },
  tfae_have : 5 â†’ 1,
  { refine Î» H U hUo hsU, H U (Î» x hx, _),
    rcases hsU hx with âŸ¨t, ht, hxtâŸ©,
    exact âŸ¨t, ht, (hUo _ ht).mem_nhds hxtâŸ© },
  tfae_have : 7 â†’ 1, from is_lindelof_of_exists_cluster_pt,
  tfae_have : 2 â†’ 7,
  { introsI H f hne hfI hsf,
    simp only [cluster_pt_iff, â† not_disjoint_iff_nonempty_inter],
    by_contra h, push_neg at h,
    choose! t ht V hV hd using h,
    rcases H t ht with âŸ¨I, hIs, hIc, hsIâŸ©,
    have : (â‹ƒ x âˆˆ I, t x) âˆ© (â‹‚ x âˆˆ I, V x) âˆˆ f,
      from inter_mem (mem_of_superset hsf hsI)
        ((countable_bInter_mem hIc).2 $ Î» x hx, hV _ (hIs hx)),
    rcases filter.nonempty_of_mem this with âŸ¨x, hxt, hxVâŸ©,
    rw mem_Interâ‚‚ at hxV, rw mem_Unionâ‚‚ at hxt, rcases hxt with âŸ¨i, hi, hxiâŸ©,
    exact @hd i (hIs hi) x âŸ¨hxi, hxV _ hiâŸ©},
  tfae_finish
end

lemma is_lindelof_iff_countable_cover_nhds : is_lindelof s â†”
  âˆ€ {t : X â†’ set X}, (âˆ€ x âˆˆ s, t x âˆˆ ğ“ x) â†’ âˆƒ I âŠ† s, countable I âˆ§ s âŠ† â‹ƒ x âˆˆ I, t x :=
(is_lindelof_tfae s).out 0 1

alias is_lindelof_iff_countable_cover_nhds â†” is_lindelof.countable_cover_nhds _

lemma is_lindelof_iff_countable_cover_nhds_within : is_lindelof s â†”
  âˆ€ {t : X â†’ set X}, (âˆ€ x âˆˆ s, t x âˆˆ ğ“[s] x) â†’ âˆƒ I âŠ† s, countable I âˆ§ s âŠ† â‹ƒ x âˆˆ I, t x :=
(is_lindelof_tfae s).out 0 2

alias is_lindelof_iff_countable_cover_nhds_within â†” is_lindelof.countable_cover_nhds_within _

lemma is_lindelof_iff_countable_cover_open_nhds : is_lindelof s â†”
  âˆ€ {u : X â†’ set X}, (âˆ€ x, is_open (u x)) â†’ (âˆ€ x, x âˆˆ u x) â†’
    âˆƒ I âŠ† s, countable I âˆ§ s âŠ† â‹ƒ x âˆˆ I, u x :=
(is_lindelof_tfae s).out 0 3

alias is_lindelof_iff_countable_cover_open_nhds â†” is_lindelof.countable_cover_open_nhds _

lemma is_lindelof_iff_countable_sUnion_nhds : is_lindelof s â†”
  âˆ€ {T : set (set X)}, (âˆ€ x âˆˆ s, âˆƒ t âˆˆ T, t âˆˆ ğ“ x) â†’ âˆƒ I âŠ† T, countable I âˆ§ s âŠ† â‹ƒâ‚€ I :=
(is_lindelof_tfae s).out 0 4

alias is_lindelof_iff_countable_sUnion_nhds â†” is_lindelof.countable_sUnion_nhds _

lemma is_lindelof_iff_countable_sUnion_nhds_within : is_lindelof s â†”
  âˆ€ {T : set (set X)}, (âˆ€ x âˆˆ s, âˆƒ t âˆˆ T, t âˆˆ ğ“[s] x) â†’ âˆƒ I âŠ† T, countable I âˆ§ s âŠ† â‹ƒâ‚€ I :=
(is_lindelof_tfae s).out 0 5

alias is_lindelof_iff_countable_sUnion_nhds_within â†” is_lindelof.countable_sUnion_nhds_within _

lemma is_lindelof_iff_exists_cluster_pt : is_lindelof s â†”
  âˆ€ (f : filter X) [ne_bot f] [countable_Inter_filter f], s âˆˆ f â†’ âˆƒ a âˆˆ s, cluster_pt a f :=
(is_lindelof_tfae s).out 0 6

lemma is_lindelof.exists_cluster_pt {f : filter X} [ne_bot f] [countable_Inter_filter f]
  (hs : is_lindelof s) (hsf : s âˆˆ f) : âˆƒ a âˆˆ s, cluster_pt a f :=
is_lindelof_iff_exists_cluster_pt.mp hs f hsf

lemma is_lindelof_Union [encodable Î¹] {s : Î¹ â†’ set X} (h : âˆ€ i, is_lindelof (s i)) :
  is_lindelof (â‹ƒ i, s i) :=
begin
  intros U hUo hsU,
  choose V hVU hVc hsV using Î» i, (h i) hUo (Union_subset_iff.1 hsU i),
  refine âŸ¨â‹ƒ i, V i, Union_subset hVU, countable_Union hVc, _âŸ©,
  simpa only [sUnion_Union] using Union_mono hsV
end

lemma is_lindelof_bUnion {I : set Î¹} (hI : countable I) {s : Î  i âˆˆ I, set X}
  (hs : âˆ€ i âˆˆ I, is_lindelof (s i â€¹i âˆˆ Iâ€º)) : is_lindelof (â‹ƒ i âˆˆ I, s i â€¹i âˆˆ Iâ€º) :=
begin
  haveI := hI.to_encodable,
  simp only [set_coe.forall', bUnion_eq_Union] at hs âŠ¢,
  exact is_lindelof_Union hs
end

protected lemma set.countable.is_lindelof (hs : countable s) : is_lindelof s :=
is_lindelof_iff_countable_cover_nhds.mpr $ Î» t ht,
  âŸ¨s, subset.rfl, hs, Î» x hx, mem_Unionâ‚‚.2 âŸ¨x, hx, mem_of_mem_nhds (ht x hx)âŸ©âŸ©

protected lemma set.finite.is_lindelof (hs : s.finite) : is_lindelof s :=
hs.countable.is_lindelof

protected lemma set.subsingleton.is_lindelof (hs : s.subsingleton) : is_lindelof s :=
hs.finite.is_lindelof

@[simp] lemma is_lindelof_empty : is_lindelof (âˆ… : set X) :=
countable_empty.is_lindelof

@[simp] lemma is_lindelof_singleton (x : X) : is_lindelof ({x} : set X) :=
(countable_singleton x).is_lindelof

lemma is_lindelof.inter_closed (hs : is_lindelof s) (ht : is_closed t) :
  is_lindelof (s âˆ© t) :=
begin
  apply is_lindelof_of_exists_cluster_pt, introsI f hâ‚ hâ‚‚ hstf,
  rw inter_mem_iff at hstf,
  obtain âŸ¨a, hsa, haâŸ© : âˆƒ a âˆˆ s, cluster_pt a f, from hs.exists_cluster_pt hstf.1,
  have : a âˆˆ t :=
    (ht.mem_of_nhds_within_ne_bot $ ha.mono $ le_principal_iff.2 hstf.2),
  exact âŸ¨a, âŸ¨hsa, thisâŸ©, haâŸ©
end

lemma is_closed.inter_lindelof (hs : is_closed s) (ht : is_lindelof t) : is_lindelof (s âˆ© t) :=
inter_comm t s â–¸ ht.inter_closed hs

lemma is_lindelof.subset (hs : is_lindelof s) (hts : t âŠ† s) (ht : is_closed t) : is_lindelof t :=
by simpa only [inter_eq_self_of_subset_right hts] using hs.inter_closed ht

/-- The image of a LindelÃ¶f set under a function continuous on this set is a LindelÃ¶f set. See also
`is_lindelof.image`. -/
lemma is_lindelof.image_of_continuous_on (hs : is_lindelof s) {f : X â†’ Y} (hf : continuous_on f s) :
  is_lindelof (f '' s) :=
begin
  refine is_lindelof_iff_countable_cover_nhds_within.mpr (Î» t ht, _),
  have : âˆ€ x âˆˆ s, f â»Â¹' (t (f x)) âˆˆ ğ“[s] x,
    from Î» x hx, (hf x hx).tendsto_nhds_within_image (ht (f x) (mem_image_of_mem f hx)),
  rcases hs.countable_cover_nhds_within this with âŸ¨I, hIs, hIc, hIâŸ©,
  refine âŸ¨f '' I, image_subset _ hIs, hIc.image f, _âŸ©,
  simpa
end

/-- The image of a LindelÃ¶f set under a continuous map is a LindelÃ¶f set. See also
`is_lindelof.image_of_continuous_on`. -/
lemma is_lindelof.image (hs : is_lindelof s) {f : X â†’ Y} (hf : continuous f) :
  is_lindelof (f '' s) :=
hs.image_of_continuous_on hf.continuous_on

lemma inducing.is_lindelof_image {e : X â†’ Y} (he : inducing e) :
  is_lindelof (e '' s) â†” is_lindelof s :=
begin
  refine âŸ¨Î» h, _, Î» h, h.image he.continuousâŸ©,
  refine is_lindelof_iff_countable_cover_open_nhds.mpr (Î» u huo hxu, _),
  replace huo := Î» x, he.is_open_iff.1 (huo x), -- `simp only ... at huo` fails
  choose v hvo hv using huo, obtain rfl : (Î» x, e â»Â¹' (v x)) = u := funext hv,
  have : e '' s âŠ† â‹ƒ x âˆˆ s, v x,
    from image_subset_iff.2 (Î» x hx, mem_Unionâ‚‚.2 âŸ¨x, hx, hxu xâŸ©),
  simpa using h.countable_open_subcoverâ‚‚ (Î» x _, hvo x) this
end

lemma embedding.is_lindelof_image {e : X â†’ Y} (he : embedding e) :
  is_lindelof (e '' s) â†” is_lindelof s :=
he.to_inducing.is_lindelof_image

lemma locally_finite.countable_nonempty_inter_lindelof {f : Î¹ â†’ set X} (hf : locally_finite f)
  (hs : is_lindelof s) :
  countable {i : Î¹ | (f i âˆ© s).nonempty} :=
begin
  choose U hxU hUf using hf,
  rcases hs.countable_cover_nhds (Î» x _, hxU x) with âŸ¨I, hIs, hIc, hsIâŸ©,
  refine (hIc.bUnion $ Î» x _, (hUf x).countable).mono _,
  rintro i âŸ¨x, hi, hxsâŸ©,
  rcases mem_Unionâ‚‚.1 (hsI hxs) with âŸ¨y, hyI, hxyâŸ©,
  exact mem_Unionâ‚‚.2 âŸ¨y, hyI, âŸ¨x, hi, hxyâŸ©âŸ©
end

/-- A topological space `X` is said to be a *LindelÃ¶f space* if any open cover of `X` admits a
countable subcover. -/
class lindelof_space (X : Type*) [topological_space X] : Prop :=
(is_lindelof_univ [] : is_lindelof (univ : set X))

export lindelof_space (is_lindelof_univ)

lemma is_lindelof_univ_iff : is_lindelof (univ : set X) â†” lindelof_space X := âŸ¨Î» h, âŸ¨hâŸ©, Î» h, h.1âŸ©

/-- A closed set in a LindelÃ¶f space is a LindelÃ¶f set. -/
protected lemma is_closed.is_lindelof [lindelof_space X] {s : set X} (hs : is_closed s) :
  is_lindelof s :=
(is_lindelof_univ X).subset (subset_univ s) hs

lemma inducing.lindelof_space_iff {e : X â†’ Y} (he : inducing e) :
  lindelof_space X â†” is_lindelof (range e) :=
by rw [â† is_lindelof_univ_iff, â† he.is_lindelof_image, image_univ]

lemma embedding.lindelof_space_iff {e : X â†’ Y} (he : embedding e) :
  lindelof_space X â†” is_lindelof (range e) :=
he.to_inducing.lindelof_space_iff

/-- A set is a LindelÃ¶f set if and only if it is a LindelÃ¶f space in the induced topology. -/
lemma is_lindelof_iff_lindelof_space : is_lindelof s â†” lindelof_space s :=
by erw [embedding_subtype_coe.lindelof_space_iff, subtype.range_coe]

alias is_lindelof_iff_lindelof_space â†” is_lindelof.to_subtype _

protected lemma closed_embedding.lindelof_space [lindelof_space Y] {e : X â†’ Y}
  (he : closed_embedding e) : lindelof_space X :=
he.to_embedding.lindelof_space_iff.2 he.closed_range.is_lindelof

/-- In a LindelÃ¶f topological space, if `f` is a function that sends each point `x` to a
neighborhood of `x`, then for some countable set `s`, the neighborhoods `f x`, `x âˆˆ s`, cover the
whole space. -/
lemma countable_cover_nhds [lindelof_space X] {f : X â†’ set X}
  (hf : âˆ€ x, f x âˆˆ ğ“ x) : âˆƒ s : set X, countable s âˆ§ (â‹ƒ x âˆˆ s, f x) = univ :=
by simpa [univ_subset_iff] using (is_lindelof_univ X).countable_cover_nhds (Î» x _, hf x)

/-- If `Î±` is a `Ïƒ`-compact space, then a locally finite family of nonempty sets of `Î±` can have
only countably many elements, `set.countable` version. -/
protected lemma locally_finite.countable_univ [lindelof_space X] {f : Î¹ â†’ set X}
  (hf : locally_finite f) (hne : âˆ€ i, (f i).nonempty) :
  countable (univ : set Î¹) :=
by simpa only [inter_univ, hne] using hf.countable_nonempty_inter_lindelof (is_lindelof_univ X)

/-- If `f : Î¹ â†’ set Î±` is a locally finite covering of a LindelÃ¶f topological space by nonempty
sets, then the index type `Î¹` is encodable. -/
protected noncomputable def locally_finite.encodable [lindelof_space X] {f : Î¹ â†’ set X}
  (hf : locally_finite f) (hne : âˆ€ i, (f i).nonempty) : encodable Î¹ :=
@encodable.of_equiv _ _ (hf.countable_univ hne).to_encodable (equiv.set.univ _).symm

/-!
### Strongly (hereditarily) LindelÃ¶f spaces

A topological space is called *strongly (hereditarily) LindelÃ¶f* if any set in this space is a
LindelÃ¶f set. Any topological space with second countable topology is a strongly LindelÃ¶f space. The
converse is not true.
-/

/-- A topological space is called *strongly (hereditarily) LindelÃ¶f* if any set in this space is a
LindelÃ¶f set. We only require that open sets are LindelÃ¶f in the definition, then deduce that any
set in a strongly LindelÃ¶f space is LindelÃ¶f in `set.is_lindelof`. -/
class strongly_lindelof_space (X : Type*) [topological_space X] : Prop :=
(is_lindelof_open : âˆ€ {s : set X}, is_open s â†’ is_lindelof s)

@[priority 100]
instance second_countable_topology.to_strongly_lindelof_space
  [second_countable_topology X] : strongly_lindelof_space X :=
begin
  refine âŸ¨Î» s hs U hU hsU, _âŸ©, clear hs,
  set B := {b âˆˆ countable_basis X | âˆƒ u âˆˆ U, b âŠ† u},
  choose! u huU hbu using (show âˆ€ b âˆˆ B, âˆƒ u âˆˆ U, b âŠ† u, from Î» b, and.right),
  refine âŸ¨u '' B, image_subset_iff.2 huU,
    ((countable_countable_basis X).mono (sep_subset _ _)).image u, hsU.trans  _âŸ©,
  rintro x âŸ¨v, hvU, hxvâŸ©,
  rcases (is_basis_countable_basis X).mem_nhds_iff.1 ((hU v hvU).mem_nhds hxv)
    with âŸ¨b, hb, hxb, hbvâŸ©,
  exact âŸ¨u b, mem_image_of_mem _ âŸ¨hb, v, hvU, hbvâŸ©, hbu _ âŸ¨hb, v, hvU, hbvâŸ© hxbâŸ©
end

/-- Any set in a strongly LindelÃ¶f space is a LindelÃ¶f set. -/
protected lemma set.is_lindelof [strongly_lindelof_space X] (s : set X) : is_lindelof s :=
begin
  intros U hU hsU,
  have := strongly_lindelof_space.is_lindelof_open (is_open_sUnion hU),
  rcases this.countable_open_subcoverâ‚‚ hU sUnion_eq_bUnion.subset with âŸ¨V, hVU, hVc, hUVâŸ©,
  simp only [â† sUnion_eq_bUnion] at hUV,
  exact âŸ¨V, hVU, hVc, hsU.trans hUVâŸ©
end

/-- In a strongly LindelÃ¶f space (e.g., in a space with second countable topology), an open set,
given as a union of open sets, is equal to the union of countably many of those sets. -/
lemma is_open_Union_countable [strongly_lindelof_space X] (s : Î¹ â†’ set X) (H : âˆ€ i, is_open (s i)) :
  âˆƒ T : set Î¹, countable T âˆ§ (â‹ƒ i âˆˆ T, s i) = â‹ƒ i, s i :=
let âŸ¨T, hTc, hTâŸ© := (â‹ƒ i, s i).is_lindelof.countable_open_subcover H subset.rfl
in âŸ¨T, hTc, (Unionâ‚‚_subset_Union _ _).antisymm hTâŸ©

/-- In a strongly LindelÃ¶f space (e.g., in a space with second countable topology), an open set,
given as a union of open sets, is equal to the union of countably many of those sets. -/
lemma is_open_sUnion_countable [strongly_lindelof_space X] (S : set (set X))
  (H : âˆ€ s âˆˆ S, is_open s) :
  âˆƒ T : set (set X), countable T âˆ§ T âŠ† S âˆ§ â‹ƒâ‚€ T = â‹ƒâ‚€ S :=
let âŸ¨T, hTS, hTc, hSTâŸ© := (â‹ƒâ‚€ S).is_lindelof.countable_open_subcoverâ‚‚ H sUnion_eq_bUnion.subset
in âŸ¨T, hTc, hTS, (sUnion_mono hTS).antisymm (hST.trans sUnion_eq_bUnion.symm.subset)âŸ©

lemma countable_cover_nhds_within [strongly_lindelof_space X] {f : X â†’ set X} {s : set X}
  (hf : âˆ€ x âˆˆ s, f x âˆˆ ğ“[s] x) : âˆƒ t âŠ† s, countable t âˆ§ s âŠ† (â‹ƒ x âˆˆ t, f x) :=
s.is_lindelof.countable_cover_nhds_within hf

/-- A countable topological space is a LindelÃ¶f space. -/
@[priority 100]
instance encodable.strongly_lindelof_space [encodable X] : strongly_lindelof_space X :=
âŸ¨Î» s hs, (countable_encodable s).is_lindelofâŸ©

@[priority 100]
instance strongly_lindelof_space.lindelof_space [strongly_lindelof_space X] : lindelof_space X :=
âŸ¨set.is_lindelof univâŸ©
