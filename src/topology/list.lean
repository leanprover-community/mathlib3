/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import topology.constructions
import topology.algebra.group
/-!
# Topology on lists and vectors

-/
open topological_space set filter
open_locale topological_space filter

variables {Î± : Type*} {Î² : Type*} [topological_space Î±] [topological_space Î²]

instance : topological_space (list Î±) :=
topological_space.mk_of_nhds (traverse nhds)

lemma nhds_list (as : list Î±) : ğ“ as = traverse ğ“ as :=
begin
  refine nhds_mk_of_nhds _ _ _ _,
  { assume l, induction l,
    case list.nil { exact le_refl _ },
    case list.cons : a l ih {
      suffices : list.cons <$> pure a <*> pure l â‰¤ list.cons <$> ğ“ a <*> traverse ğ“ l,
      { simpa only [] with functor_norm using this },
      exact filter.seq_mono (filter.map_mono $ pure_le_nhds a) ih } },
  { assume l s hs,
    rcases (mem_traverse_iff _ _).1 hs with âŸ¨u, hu, husâŸ©, clear as hs,
    have : âˆƒv:list (set Î±), l.forallâ‚‚ (Î»a s, is_open s âˆ§ a âˆˆ s) v âˆ§ sequence v âŠ† s,
    { induction hu generalizing s,
      case list.forallâ‚‚.nil : hs this
        { existsi [], simpa only [list.forallâ‚‚_nil_left_iff, exists_eq_left] },
      case list.forallâ‚‚.cons : a s as ss ht h ih t hts {
        rcases mem_nhds_iff.1 ht with âŸ¨u, hut, huâŸ©,
        rcases ih (subset.refl _) with âŸ¨v, hv, hvssâŸ©,
        exact âŸ¨u::v, list.forallâ‚‚.cons hu hv,
          subset.trans (set.seq_mono (set.image_subset _ hut) hvss) htsâŸ© } },
    rcases this with âŸ¨v, hv, hvsâŸ©,
    refine âŸ¨sequence v, mem_traverse _ _ _, hvs, _âŸ©,
    { exact hv.imp (assume a s âŸ¨hs, haâŸ©, is_open.mem_nhds hs ha) },
    { assume u hu,
      have hu := (list.mem_traverse _ _).1 hu,
      have : list.forallâ‚‚ (Î»a s, is_open s âˆ§ a âˆˆ s) u v,
      { refine list.forallâ‚‚.flip _,
        replace hv := hv.flip,
        simp only [list.forallâ‚‚_and_left, flip] at âŠ¢ hv,
        exact âŸ¨hv.1, hu.flipâŸ© },
      refine mem_of_superset _ hvs,
      exact mem_traverse _ _ (this.imp $ assume a s âŸ¨hs, haâŸ©, is_open.mem_nhds hs ha) } }
end

@[simp] lemma nhds_nil : ğ“ ([] : list Î±) = pure [] :=
by rw [nhds_list, list.traverse_nil _]; apply_instance

lemma nhds_cons (a : Î±) (l : list Î±) :
  ğ“ (a :: l) = list.cons <$> ğ“ a <*> ğ“ l  :=
by rw [nhds_list, list.traverse_cons _, â† nhds_list]; apply_instance

lemma list.tendsto_cons {a : Î±} {l : list Î±} :
  tendsto (Î»p:Î±Ã—list Î±, list.cons p.1 p.2) (ğ“ a Ã—á¶  ğ“ l) (ğ“ (a :: l)) :=
by rw [nhds_cons, tendsto, map_prod]; exact le_refl _

lemma filter.tendsto.cons {Î± : Type*} {f : Î± â†’ Î²} {g : Î± â†’ list Î²}
  {a : _root_.filter Î±} {b : Î²} {l : list Î²} (hf : tendsto f a (ğ“ b)) (hg : tendsto g a (ğ“ l)) :
  tendsto (Î»a, list.cons (f a) (g a)) a (ğ“ (b :: l)) :=
list.tendsto_cons.comp (tendsto.prod_mk hf hg)

namespace list

lemma tendsto_cons_iff {Î² : Type*} {f : list Î± â†’ Î²} {b : _root_.filter Î²} {a : Î±} {l : list Î±} :
  tendsto f (ğ“ (a :: l)) b â†” tendsto (Î»p:Î±Ã—list Î±, f (p.1 :: p.2)) (ğ“ a Ã—á¶  ğ“ l) b :=
have ğ“ (a :: l) = (ğ“ a Ã—á¶  ğ“ l).map (Î»p:Î±Ã—list Î±, (p.1 :: p.2)),
begin
  simp only
    [nhds_cons, filter.prod_eq, (filter.map_def _ _).symm, (filter.seq_eq_filter_seq _ _).symm],
  simp [-filter.seq_eq_filter_seq, -filter.map_def, (âˆ˜)] with functor_norm,
end,
by rw [this, filter.tendsto_map'_iff]

lemma continuous_cons : continuous (Î» x : Î± Ã— list Î±, (x.1 :: x.2 : list Î±)) :=
continuous_iff_continuous_at.mpr $ Î» âŸ¨x, yâŸ©, continuous_at_fst.cons continuous_at_snd

lemma tendsto_nhds {Î² : Type*} {f : list Î± â†’ Î²} {r : list Î± â†’ _root_.filter Î²}
  (h_nil : tendsto f (pure []) (r []))
  (h_cons : âˆ€l a, tendsto f (ğ“ l) (r l) â†’
    tendsto (Î»p:Î±Ã—list Î±, f (p.1 :: p.2)) (ğ“ a Ã—á¶  ğ“ l) (r (a::l))) :
  âˆ€l, tendsto f (ğ“ l) (r l)
| []     := by rwa [nhds_nil]
| (a::l) := by rw [tendsto_cons_iff]; exact h_cons l a (tendsto_nhds l)

lemma continuous_at_length :
  âˆ€(l : list Î±), continuous_at list.length l :=
begin
  simp only [continuous_at, nhds_discrete],
  refine tendsto_nhds _ _,
  { exact tendsto_pure_pure _ _ },
  { assume l a ih,
    dsimp only [list.length],
    refine tendsto.comp (tendsto_pure_pure (Î»x, x + 1) _) _,
    refine tendsto.comp ih tendsto_snd }
end

lemma tendsto_insert_nth' {a : Î±} : âˆ€{n : â„•} {l : list Î±},
  tendsto (Î»p:Î±Ã—list Î±, insert_nth n p.1 p.2) (ğ“ a Ã—á¶  ğ“ l) (ğ“ (insert_nth n a l))
| 0     l  := tendsto_cons
| (n+1) [] := by simp
| (n+1) (a'::l) :=
  have ğ“ a Ã—á¶  ğ“ (a' :: l) =
    (ğ“ a Ã—á¶  (ğ“ a' Ã—á¶  ğ“ l)).map (Î»p:Î±Ã—Î±Ã—list Î±, (p.1, p.2.1 :: p.2.2)),
  begin
    simp only [nhds_cons, filter.prod_eq, â† filter.map_def, â† filter.seq_eq_filter_seq],
    simp [-filter.seq_eq_filter_seq, -filter.map_def, (âˆ˜)] with functor_norm
  end,
  begin
    rw [this, tendsto_map'_iff],
    exact (tendsto_fst.comp tendsto_snd).cons
      ((@tendsto_insert_nth' n l).comp $ tendsto_fst.prod_mk $ tendsto_snd.comp tendsto_snd)
  end

lemma tendsto_insert_nth {Î²} {n : â„•} {a : Î±} {l : list Î±} {f : Î² â†’ Î±} {g : Î² â†’ list Î±}
  {b : _root_.filter Î²} (hf : tendsto f b (ğ“ a)) (hg : tendsto g b (ğ“ l)) :
  tendsto (Î»b:Î², insert_nth n (f b) (g b)) b (ğ“ (insert_nth n a l)) :=
tendsto_insert_nth'.comp (tendsto.prod_mk hf hg)

lemma continuous_insert_nth {n : â„•} : continuous (Î»p:Î±Ã—list Î±, insert_nth n p.1 p.2) :=
continuous_iff_continuous_at.mpr $
  assume âŸ¨a, lâŸ©, by rw [continuous_at, nhds_prod_eq]; exact tendsto_insert_nth'

lemma tendsto_remove_nth : âˆ€{n : â„•} {l : list Î±},
  tendsto (Î»l, remove_nth l n) (ğ“ l) (ğ“ (remove_nth l n))
| _ []      := by rw [nhds_nil]; exact tendsto_pure_nhds _ _
| 0 (a::l) := by rw [tendsto_cons_iff]; exact tendsto_snd
| (n+1) (a::l) :=
  begin
    rw [tendsto_cons_iff],
    dsimp [remove_nth],
    exact tendsto_fst.cons ((@tendsto_remove_nth n l).comp tendsto_snd)
  end

lemma continuous_remove_nth {n : â„•} : continuous (Î»l : list Î±, remove_nth l n) :=
continuous_iff_continuous_at.mpr $ assume a, tendsto_remove_nth

@[to_additive]
lemma tendsto_prod [monoid Î±] [has_continuous_mul Î±] {l : list Î±} :
  tendsto list.prod (ğ“ l) (ğ“ l.prod) :=
begin
  induction l with x l ih,
  { simp [nhds_nil, mem_of_mem_nhds, tendsto_pure_left] {contextual := tt} },
  simp_rw [tendsto_cons_iff, prod_cons],
  have := continuous_iff_continuous_at.mp continuous_mul (x, l.prod),
  rw [continuous_at, nhds_prod_eq] at this,
  exact this.comp (tendsto_id.prod_map ih)
end

@[to_additive]
lemma continuous_prod [monoid Î±] [has_continuous_mul Î±] : continuous (prod : list Î± â†’ Î±) :=
continuous_iff_continuous_at.mpr $ Î» l, tendsto_prod

end list

namespace vector
open list

instance (n : â„•) : topological_space (vector Î± n) :=
by unfold vector; apply_instance

lemma tendsto_cons {n : â„•} {a : Î±} {l : vector Î± n}:
  tendsto (Î»p:Î±Ã—vector Î± n, p.1 ::áµ¥ p.2) (ğ“ a Ã—á¶  ğ“ l) (ğ“ (a ::áµ¥ l)) :=
by { simp [tendsto_subtype_rng, â†subtype.val_eq_coe, cons_val],
  exact tendsto_fst.cons (tendsto.comp continuous_at_subtype_coe tendsto_snd) }

lemma tendsto_insert_nth
  {n : â„•} {i : fin (n+1)} {a:Î±} :
  âˆ€{l:vector Î± n}, tendsto (Î»p:Î±Ã—vector Î± n, insert_nth p.1 i p.2)
    (ğ“ a Ã—á¶  ğ“ l) (ğ“ (insert_nth a i l))
| âŸ¨l, hlâŸ© :=
begin
  rw [insert_nth, tendsto_subtype_rng],
  simp [insert_nth_val],
  exact list.tendsto_insert_nth tendsto_fst (tendsto.comp continuous_at_subtype_coe tendsto_snd : _)
end

lemma continuous_insert_nth' {n : â„•} {i : fin (n+1)} :
  continuous (Î»p:Î±Ã—vector Î± n, insert_nth p.1 i p.2) :=
continuous_iff_continuous_at.mpr $ assume âŸ¨a, lâŸ©,
  by rw [continuous_at, nhds_prod_eq]; exact tendsto_insert_nth

lemma continuous_insert_nth {n : â„•} {i : fin (n+1)}
  {f : Î² â†’ Î±} {g : Î² â†’ vector Î± n} (hf : continuous f) (hg : continuous g) :
  continuous (Î»b, insert_nth (f b) i (g b)) :=
continuous_insert_nth'.comp (hf.prod_mk hg : _)

lemma continuous_at_remove_nth {n : â„•} {i : fin (n+1)} :
  âˆ€{l:vector Î± (n+1)}, continuous_at (remove_nth i) l
| âŸ¨l, hlâŸ© :=
--  âˆ€{l:vector Î± (n+1)}, tendsto (remove_nth i) (ğ“ l) (ğ“ (remove_nth i l))
--| âŸ¨l, hlâŸ© :=
begin
  rw [continuous_at, remove_nth, tendsto_subtype_rng],
  simp only [â† subtype.val_eq_coe, vector.remove_nth_val],
  exact tendsto.comp list.tendsto_remove_nth continuous_at_subtype_coe,
end

lemma continuous_remove_nth {n : â„•} {i : fin (n+1)} :
  continuous (remove_nth i : vector Î± (n+1) â†’ vector Î± n) :=
continuous_iff_continuous_at.mpr $ assume âŸ¨a, lâŸ©, continuous_at_remove_nth

end vector
