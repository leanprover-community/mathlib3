/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import order.filter.extr
import topology.continuous_on

/-!
# Local extrema of functions on topological spaces

## Main definitions

This file defines special versions of `is_*_filter f a l`, `*=min/max/extr`,
from `order/filter/extr` for two kinds of filters: `nhds_within` and `nhds`.
These versions are called `is_local_*_on` and `is_local_*`, respectively.

## Main statements

Many lemmas in this file restate those from `order/filter/extr`, and you can find
a detailed documentation there. These convenience lemmas are provided only to make the dot notation
return propositions of expected types, not just `is_*_filter`.

Here is the list of statements specific to these two types of filters:

* `is_local_*.on`, `is_local_*_on.on_subset`: restrict to a subset;
* `is_local_*_on.inter` : intersect the set with another one;
* `is_*_on.localize` : a global extremum is a local extremum too.
* `is_[local_]*_on.is_local_*` : if we have `is_local_*_on f s a` and `s ‚àà ùìù a`,
  then we have `is_local_* f a`.

-/

universes u v w x

variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} {Œ¥ : Type x} [topological_space Œ±]

open set filter
open_locale topological_space filter

section preorder

variables [preorder Œ≤] [preorder Œ≥] (f : Œ± ‚Üí Œ≤) (s : set Œ±) (a : Œ±)

/-- `is_local_min_on f s a` means that `f a ‚â§ f x` for all `x ‚àà s` in some neighborhood of `a`. -/
def is_local_min_on := is_min_filter f (ùìù[s] a) a

/-- `is_local_max_on f s a` means that `f x ‚â§ f a` for all `x ‚àà s` in some neighborhood of `a`. -/
def is_local_max_on := is_max_filter f (ùìù[s] a) a

/-- `is_local_extr_on f s a` means `is_local_min_on f s a ‚à® is_local_max_on f s a`. -/
def is_local_extr_on := is_extr_filter f (ùìù[s] a) a

/-- `is_local_min f a` means that `f a ‚â§ f x` for all `x` in some neighborhood of `a`. -/
def is_local_min := is_min_filter f (ùìù a) a

/-- `is_local_max f a` means that `f x ‚â§ f a` for all `x ‚àà s` in some neighborhood of `a`. -/
def is_local_max := is_max_filter f (ùìù a) a

/-- `is_local_extr_on f s a` means `is_local_min_on f s a ‚à® is_local_max_on f s a`. -/
def is_local_extr := is_extr_filter f (ùìù a) a

variables {f s a}

lemma is_local_extr_on.elim {p : Prop} :
  is_local_extr_on f s a ‚Üí (is_local_min_on f s a ‚Üí p) ‚Üí (is_local_max_on f s a ‚Üí p) ‚Üí p :=
or.elim

lemma is_local_extr.elim {p : Prop} :
  is_local_extr f a ‚Üí (is_local_min f a ‚Üí p) ‚Üí (is_local_max f a ‚Üí p) ‚Üí p :=
or.elim

/-! ### Restriction to (sub)sets -/

lemma is_local_min.on (h : is_local_min f a) (s) : is_local_min_on f s a :=
h.filter_inf _

lemma is_local_max.on (h : is_local_max f a) (s) : is_local_max_on f s a :=
h.filter_inf _

lemma is_local_extr.on (h : is_local_extr f a) (s) : is_local_extr_on f s a :=
h.filter_inf _

lemma is_local_min_on.on_subset {t : set Œ±} (hf : is_local_min_on f t a) (h : s ‚äÜ t) :
  is_local_min_on f s a :=
hf.filter_mono $ nhds_within_mono a h

lemma is_local_max_on.on_subset {t : set Œ±} (hf : is_local_max_on f t a) (h : s ‚äÜ t) :
  is_local_max_on f s a :=
hf.filter_mono $ nhds_within_mono a h

lemma is_local_extr_on.on_subset {t : set Œ±} (hf : is_local_extr_on f t a) (h : s ‚äÜ t) :
  is_local_extr_on f s a :=
hf.filter_mono $ nhds_within_mono a h

lemma is_local_min_on.inter (hf : is_local_min_on f s a) (t) : is_local_min_on f (s ‚à© t) a :=
hf.on_subset (inter_subset_left s t)

lemma is_local_max_on.inter (hf : is_local_max_on f s a) (t) : is_local_max_on f (s ‚à© t) a :=
hf.on_subset (inter_subset_left s t)

lemma is_local_extr_on.inter (hf : is_local_extr_on f s a) (t) : is_local_extr_on f (s ‚à© t) a :=
hf.on_subset (inter_subset_left s t)

lemma is_min_on.localize (hf : is_min_on f s a) : is_local_min_on f s a :=
hf.filter_mono $ inf_le_right

lemma is_max_on.localize (hf : is_max_on f s a) : is_local_max_on f s a :=
hf.filter_mono $ inf_le_right

lemma is_extr_on.localize (hf : is_extr_on f s a) : is_local_extr_on f s a :=
hf.filter_mono $ inf_le_right

lemma is_local_min_on.is_local_min (hf : is_local_min_on f s a) (hs : s ‚àà ùìù a) : is_local_min f a :=
have ùìù a ‚â§ ùìü s, from le_principal_iff.2 hs,
hf.filter_mono $ le_inf (le_refl _) this

lemma is_local_max_on.is_local_max (hf : is_local_max_on f s a) (hs : s ‚àà ùìù a) : is_local_max f a :=
have ùìù a ‚â§ ùìü s, from le_principal_iff.2 hs,
hf.filter_mono $ le_inf (le_refl _) this

lemma is_local_extr_on.is_local_extr (hf : is_local_extr_on f s a) (hs : s ‚àà ùìù a) :
  is_local_extr f a :=
hf.elim (Œª hf, (hf.is_local_min hs).is_extr) (Œª hf, (hf.is_local_max hs).is_extr)

lemma is_min_on.is_local_min (hf : is_min_on f s a) (hs : s ‚àà ùìù a) : is_local_min f a :=
hf.localize.is_local_min hs

lemma is_max_on.is_local_max (hf : is_max_on f s a) (hs : s ‚àà ùìù a) : is_local_max f a :=
hf.localize.is_local_max hs

lemma is_extr_on.is_local_extr (hf : is_extr_on f s a) (hs : s ‚àà ùìù a) : is_local_extr f a :=
hf.localize.is_local_extr hs

lemma is_local_min_on.not_nhds_le_map [topological_space Œ≤]
  (hf : is_local_min_on f s a) [ne_bot (ùìù[Iio (f a)] (f a))] :
  ¬¨ùìù (f a) ‚â§ map f (ùìù[s] a) :=
Œª hle,
have ‚àÄ·∂† y in ùìù[Iio (f a)] (f a), f a ‚â§ y,
  from (eventually_map.2 hf).filter_mono (inf_le_left.trans hle),
let ‚ü®y, hy‚ü© := (this.and self_mem_nhds_within).exists in hy.1.not_lt hy.2

lemma is_local_max_on.not_nhds_le_map [topological_space Œ≤]
  (hf : is_local_max_on f s a) [ne_bot (ùìù[Ioi (f a)] (f a))] :
  ¬¨ùìù (f a) ‚â§ map f (ùìù[s] a) :=
@is_local_min_on.not_nhds_le_map Œ± (order_dual Œ≤) _ _ _ _ _ ‚Äπ_‚Ä∫ hf ‚Äπ_‚Ä∫

lemma is_local_extr_on.not_nhds_le_map [topological_space Œ≤]
  (hf : is_local_extr_on f s a) [ne_bot (ùìù[Iio (f a)] (f a))] [ne_bot (ùìù[Ioi (f a)] (f a))] :
  ¬¨ùìù (f a) ‚â§ map f (ùìù[s] a) :=
hf.elim (Œª h, h.not_nhds_le_map) (Œª h, h.not_nhds_le_map)

/-! ### Constant -/

lemma is_local_min_on_const {b : Œ≤} : is_local_min_on (Œª _, b) s a := is_min_filter_const
lemma is_local_max_on_const {b : Œ≤} : is_local_max_on (Œª _, b) s a := is_max_filter_const
lemma is_local_extr_on_const {b : Œ≤} : is_local_extr_on (Œª _, b) s a := is_extr_filter_const

lemma is_local_min_const {b : Œ≤} : is_local_min (Œª _, b) a := is_min_filter_const
lemma is_local_max_const {b : Œ≤} : is_local_max (Œª _, b) a := is_max_filter_const
lemma is_local_extr_const {b : Œ≤} : is_local_extr (Œª _, b) a := is_extr_filter_const

/-! ### Composition with (anti)monotone functions -/

lemma is_local_min.comp_mono (hf : is_local_min f a) {g : Œ≤ ‚Üí Œ≥} (hg : monotone g) :
  is_local_min (g ‚àò f) a :=
hf.comp_mono hg

lemma is_local_max.comp_mono (hf : is_local_max f a) {g : Œ≤ ‚Üí Œ≥} (hg : monotone g) :
  is_local_max (g ‚àò f) a :=
hf.comp_mono hg

lemma is_local_extr.comp_mono (hf : is_local_extr f a) {g : Œ≤ ‚Üí Œ≥} (hg : monotone g) :
  is_local_extr (g ‚àò f) a :=
hf.comp_mono hg

lemma is_local_min.comp_antitone (hf : is_local_min f a) {g : Œ≤ ‚Üí Œ≥}
  (hg : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí g y ‚â§ g x) :
  is_local_max (g ‚àò f) a :=
hf.comp_antitone hg

lemma is_local_max.comp_antitone (hf : is_local_max f a) {g : Œ≤ ‚Üí Œ≥}
  (hg : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí g y ‚â§ g x) :
  is_local_min (g ‚àò f) a :=
hf.comp_antitone hg

lemma is_local_extr.comp_antitone (hf : is_local_extr f a) {g : Œ≤ ‚Üí Œ≥}
  (hg : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí g y ‚â§ g x) :
  is_local_extr (g ‚àò f) a :=
hf.comp_antitone hg

lemma is_local_min_on.comp_mono (hf : is_local_min_on f s a) {g : Œ≤ ‚Üí Œ≥} (hg : monotone g) :
  is_local_min_on (g ‚àò f) s a :=
hf.comp_mono hg

lemma is_local_max_on.comp_mono (hf : is_local_max_on f s a) {g : Œ≤ ‚Üí Œ≥} (hg : monotone g) :
  is_local_max_on (g ‚àò f) s a :=
hf.comp_mono hg

lemma is_local_extr_on.comp_mono (hf : is_local_extr_on f s a) {g : Œ≤ ‚Üí Œ≥} (hg : monotone g) :
  is_local_extr_on (g ‚àò f) s a :=
hf.comp_mono hg

lemma is_local_min_on.comp_antitone (hf : is_local_min_on f s a) {g : Œ≤ ‚Üí Œ≥}
  (hg : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí g y ‚â§ g x) :
  is_local_max_on (g ‚àò f) s a :=
hf.comp_antitone hg

lemma is_local_max_on.comp_antitone (hf : is_local_max_on f s a) {g : Œ≤ ‚Üí Œ≥}
  (hg : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí g y ‚â§ g x) :
  is_local_min_on (g ‚àò f) s a :=
hf.comp_antitone hg

lemma is_local_extr_on.comp_antitone (hf : is_local_extr_on f s a) {g : Œ≤ ‚Üí Œ≥}
  (hg : ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí g y ‚â§ g x) :
  is_local_extr_on (g ‚àò f) s a :=
hf.comp_antitone hg

lemma is_local_min.bicomp_mono [preorder Œ¥] {op : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥} (hop : ((‚â§) ‚áí (‚â§) ‚áí (‚â§)) op op)
  (hf : is_local_min f a) {g : Œ± ‚Üí Œ≥} (hg : is_local_min g a) :
  is_local_min (Œª x, op (f x) (g x)) a :=
hf.bicomp_mono hop hg

lemma is_local_max.bicomp_mono [preorder Œ¥] {op : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥} (hop : ((‚â§) ‚áí (‚â§) ‚áí (‚â§)) op op)
  (hf : is_local_max f a) {g : Œ± ‚Üí Œ≥} (hg : is_local_max g a) :
  is_local_max (Œª x, op (f x) (g x)) a :=
hf.bicomp_mono hop hg

-- No `extr` version because we need `hf` and `hg` to be of the same kind

lemma is_local_min_on.bicomp_mono [preorder Œ¥] {op : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥} (hop : ((‚â§) ‚áí (‚â§) ‚áí (‚â§)) op op)
  (hf : is_local_min_on f s a) {g : Œ± ‚Üí Œ≥} (hg : is_local_min_on g s a) :
  is_local_min_on (Œª x, op (f x) (g x)) s a :=
hf.bicomp_mono hop hg

lemma is_local_max_on.bicomp_mono [preorder Œ¥] {op : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥} (hop : ((‚â§) ‚áí (‚â§) ‚áí (‚â§)) op op)
  (hf : is_local_max_on f s a) {g : Œ± ‚Üí Œ≥} (hg : is_local_max_on g s a) :
  is_local_max_on (Œª x, op (f x) (g x)) s a :=
hf.bicomp_mono hop hg

/-! ### Composition with `continuous_at` -/

lemma is_local_min.comp_continuous [topological_space Œ¥] {g : Œ¥ ‚Üí Œ±} {b : Œ¥}
  (hf : is_local_min f (g b)) (hg : continuous_at g b) :
  is_local_min (f ‚àò g) b :=
hg hf

lemma is_local_max.comp_continuous [topological_space Œ¥] {g : Œ¥ ‚Üí Œ±} {b : Œ¥}
  (hf : is_local_max f (g b)) (hg : continuous_at g b) :
  is_local_max (f ‚àò g) b :=
hg hf

lemma is_local_extr.comp_continuous [topological_space Œ¥] {g : Œ¥ ‚Üí Œ±} {b : Œ¥}
  (hf : is_local_extr f (g b)) (hg : continuous_at g b) :
  is_local_extr (f ‚àò g) b :=
hf.comp_tendsto hg

lemma is_local_min.comp_continuous_on [topological_space Œ¥] {s : set Œ¥} {g : Œ¥ ‚Üí Œ±} {b : Œ¥}
  (hf : is_local_min f (g b)) (hg : continuous_on g s) (hb : b ‚àà s) :
  is_local_min_on (f ‚àò g) s b :=
hf.comp_tendsto (hg b hb)

lemma is_local_max.comp_continuous_on [topological_space Œ¥] {s : set Œ¥} {g : Œ¥ ‚Üí Œ±} {b : Œ¥}
  (hf : is_local_max f (g b)) (hg : continuous_on g s) (hb : b ‚àà s) :
  is_local_max_on (f ‚àò g) s b :=
hf.comp_tendsto (hg b hb)

lemma is_local_extr.comp_continuous_on [topological_space Œ¥] {s : set Œ¥} (g : Œ¥ ‚Üí Œ±) {b : Œ¥}
  (hf : is_local_extr f (g b)) (hg : continuous_on g s) (hb : b ‚àà s) :
  is_local_extr_on (f ‚àò g) s b :=
hf.elim (Œª hf, (hf.comp_continuous_on hg hb).is_extr)
  (Œª hf, (is_local_max.comp_continuous_on hf hg hb).is_extr)

lemma is_local_min_on.comp_continuous_on [topological_space Œ¥] {t : set Œ±} {s : set Œ¥} {g : Œ¥ ‚Üí Œ±}
  {b : Œ¥} (hf : is_local_min_on f t (g b)) (hst : s ‚äÜ g ‚Åª¬π' t) (hg : continuous_on g s)
  (hb : b ‚àà s) :
  is_local_min_on (f ‚àò g) s b :=
hf.comp_tendsto (tendsto_nhds_within_mono_right (image_subset_iff.mpr hst)
  (continuous_within_at.tendsto_nhds_within_image (hg b hb)))

lemma is_local_max_on.comp_continuous_on [topological_space Œ¥] {t : set Œ±} {s : set Œ¥} {g : Œ¥ ‚Üí Œ±}
  {b : Œ¥} (hf : is_local_max_on f t (g b)) (hst : s ‚äÜ g ‚Åª¬π' t) (hg : continuous_on g s)
  (hb : b ‚àà s) :
  is_local_max_on (f ‚àò g) s b :=
hf.comp_tendsto (tendsto_nhds_within_mono_right (image_subset_iff.mpr hst)
  (continuous_within_at.tendsto_nhds_within_image (hg b hb)))

lemma is_local_extr_on.comp_continuous_on [topological_space Œ¥] {t : set Œ±} {s : set Œ¥} (g : Œ¥ ‚Üí Œ±)
  {b : Œ¥} (hf : is_local_extr_on f t (g b)) (hst : s ‚äÜ g ‚Åª¬π' t) (hg : continuous_on g s)
  (hb : b ‚àà s) :
  is_local_extr_on (f ‚àò g) s b :=
hf.elim (Œª hf, (hf.comp_continuous_on hst hg hb).is_extr)
  (Œª hf, (is_local_max_on.comp_continuous_on hf hst hg hb).is_extr)

end preorder

/-! ### Pointwise addition -/
section ordered_add_comm_monoid

variables [ordered_add_comm_monoid Œ≤] {f g : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±} {l : filter Œ±}

lemma is_local_min.add (hf : is_local_min f a) (hg : is_local_min g a) :
  is_local_min (Œª x, f x + g x) a :=
hf.add hg

lemma is_local_max.add (hf : is_local_max f a) (hg : is_local_max g a) :
  is_local_max (Œª x, f x + g x) a :=
hf.add hg

lemma is_local_min_on.add (hf : is_local_min_on f s a) (hg : is_local_min_on g s a) :
  is_local_min_on (Œª x, f x + g x) s a :=
hf.add hg

lemma is_local_max_on.add (hf : is_local_max_on f s a) (hg : is_local_max_on g s a) :
  is_local_max_on (Œª x, f x + g x) s a :=
hf.add hg

end ordered_add_comm_monoid

/-! ### Pointwise negation and subtraction -/

section ordered_add_comm_group

variables [ordered_add_comm_group Œ≤] {f g : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±} {l : filter Œ±}

lemma is_local_min.neg (hf : is_local_min f a) : is_local_max (Œª x, -f x) a :=
hf.neg

lemma is_local_max.neg (hf : is_local_max f a) : is_local_min (Œª x, -f x) a :=
hf.neg

lemma is_local_extr.neg (hf : is_local_extr f a) : is_local_extr (Œª x, -f x) a :=
hf.neg

lemma is_local_min_on.neg (hf : is_local_min_on f s a) : is_local_max_on (Œª x, -f x) s a :=
hf.neg

lemma is_local_max_on.neg (hf : is_local_max_on f s a) : is_local_min_on (Œª x, -f x) s a :=
hf.neg

lemma is_local_extr_on.neg (hf : is_local_extr_on f s a) : is_local_extr_on (Œª x, -f x) s a :=
hf.neg

lemma is_local_min.sub (hf : is_local_min f a) (hg : is_local_max g a) :
  is_local_min (Œª x, f x - g x) a :=
hf.sub hg

lemma is_local_max.sub (hf : is_local_max f a) (hg : is_local_min g a) :
  is_local_max (Œª x, f x - g x) a :=
hf.sub hg

lemma is_local_min_on.sub (hf : is_local_min_on f s a) (hg : is_local_max_on g s a) :
  is_local_min_on (Œª x, f x - g x) s a :=
hf.sub hg

lemma is_local_max_on.sub (hf : is_local_max_on f s a) (hg : is_local_min_on g s a) :
  is_local_max_on (Œª x, f x - g x) s a :=
hf.sub hg

end ordered_add_comm_group


/-! ### Pointwise `sup`/`inf` -/

section semilattice_sup

variables [semilattice_sup Œ≤] {f g : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±} {l : filter Œ±}

lemma is_local_min.sup (hf : is_local_min f a) (hg : is_local_min g a) :
  is_local_min (Œª x, f x ‚äî g x) a :=
hf.sup hg

lemma is_local_max.sup (hf : is_local_max f a) (hg : is_local_max g a) :
  is_local_max (Œª x, f x ‚äî g x) a :=
hf.sup hg

lemma is_local_min_on.sup (hf : is_local_min_on f s a) (hg : is_local_min_on g s a) :
  is_local_min_on (Œª x, f x ‚äî g x) s a :=
hf.sup hg

lemma is_local_max_on.sup (hf : is_local_max_on f s a) (hg : is_local_max_on g s a) :
  is_local_max_on (Œª x, f x ‚äî g x) s a :=
hf.sup hg

end semilattice_sup

section semilattice_inf

variables [semilattice_inf Œ≤] {f g : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±} {l : filter Œ±}

lemma is_local_min.inf (hf : is_local_min f a) (hg : is_local_min g a) :
  is_local_min (Œª x, f x ‚äì g x) a :=
hf.inf hg

lemma is_local_max.inf (hf : is_local_max f a) (hg : is_local_max g a) :
  is_local_max (Œª x, f x ‚äì g x) a :=
hf.inf hg

lemma is_local_min_on.inf (hf : is_local_min_on f s a) (hg : is_local_min_on g s a) :
  is_local_min_on (Œª x, f x ‚äì g x) s a :=
hf.inf hg

lemma is_local_max_on.inf (hf : is_local_max_on f s a) (hg : is_local_max_on g s a) :
  is_local_max_on (Œª x, f x ‚äì g x) s a :=
hf.inf hg

end semilattice_inf

/-! ### Pointwise `min`/`max` -/

section linear_order

variables [linear_order Œ≤] {f g : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±} {l : filter Œ±}

lemma is_local_min.min (hf : is_local_min f a) (hg : is_local_min g a) :
  is_local_min (Œª x, min (f x) (g x)) a :=
hf.min hg

lemma is_local_max.min (hf : is_local_max f a) (hg : is_local_max g a) :
  is_local_max (Œª x, min (f x) (g x)) a :=
hf.min hg

lemma is_local_min_on.min (hf : is_local_min_on f s a) (hg : is_local_min_on g s a) :
  is_local_min_on (Œª x, min (f x) (g x)) s a :=
hf.min hg

lemma is_local_max_on.min (hf : is_local_max_on f s a) (hg : is_local_max_on g s a) :
  is_local_max_on (Œª x, min (f x) (g x)) s a :=
hf.min hg

lemma is_local_min.max (hf : is_local_min f a) (hg : is_local_min g a) :
  is_local_min (Œª x, max (f x) (g x)) a :=
hf.max hg

lemma is_local_max.max (hf : is_local_max f a) (hg : is_local_max g a) :
  is_local_max (Œª x, max (f x) (g x)) a :=
hf.max hg

lemma is_local_min_on.max (hf : is_local_min_on f s a) (hg : is_local_min_on g s a) :
  is_local_min_on (Œª x, max (f x) (g x)) s a :=
hf.max hg

lemma is_local_max_on.max (hf : is_local_max_on f s a) (hg : is_local_max_on g s a) :
  is_local_max_on (Œª x, max (f x) (g x)) s a :=
hf.max hg

end linear_order

section eventually

/-! ### Relation with `eventually` comparisons of two functions -/

variables [preorder Œ≤] {s : set Œ±}

lemma filter.eventually_le.is_local_max_on {f g : Œ± ‚Üí Œ≤} {a : Œ±} (hle : g ‚â§·∂†[ùìù[s] a] f)
  (hfga : f a = g a) (h : is_local_max_on f s a) : is_local_max_on g s a :=
hle.is_max_filter hfga h

lemma is_local_max_on.congr {f g : Œ± ‚Üí Œ≤} {a : Œ±} (h : is_local_max_on f s a)
  (heq : f =·∂†[ùìù[s] a] g) (hmem : a ‚àà s) : is_local_max_on g s a :=
h.congr heq $ heq.eq_of_nhds_within hmem

lemma filter.eventually_eq.is_local_max_on_iff {f g : Œ± ‚Üí Œ≤} {a : Œ±} (heq : f =·∂†[ùìù[s] a] g)
  (hmem : a ‚àà s) : is_local_max_on f s a ‚Üî is_local_max_on g s a :=
heq.is_max_filter_iff $ heq.eq_of_nhds_within hmem

lemma filter.eventually_le.is_local_min_on {f g : Œ± ‚Üí Œ≤} {a : Œ±} (hle : f ‚â§·∂†[ùìù[s] a] g)
  (hfga : f a = g a) (h : is_local_min_on f s a) : is_local_min_on g s a :=
hle.is_min_filter hfga h

lemma is_local_min_on.congr {f g : Œ± ‚Üí Œ≤} {a : Œ±} (h : is_local_min_on f s a)
  (heq : f =·∂†[ùìù[s] a] g) (hmem : a ‚àà s) : is_local_min_on g s a :=
h.congr heq $ heq.eq_of_nhds_within hmem

lemma filter.eventually_eq.is_local_min_on_iff {f g : Œ± ‚Üí Œ≤} {a : Œ±} (heq : f =·∂†[ùìù[s] a] g)
  (hmem : a ‚àà s) : is_local_min_on f s a ‚Üî is_local_min_on g s a :=
heq.is_min_filter_iff $ heq.eq_of_nhds_within hmem

lemma is_local_extr_on.congr {f g : Œ± ‚Üí Œ≤} {a : Œ±} (h : is_local_extr_on f s a)
  (heq : f =·∂†[ùìù[s] a] g) (hmem : a ‚àà s) : is_local_extr_on g s a :=
h.congr heq $ heq.eq_of_nhds_within hmem

lemma filter.eventually_eq.is_local_extr_on_iff {f g : Œ± ‚Üí Œ≤} {a : Œ±} (heq : f =·∂†[ùìù[s] a] g)
  (hmem : a ‚àà s) : is_local_extr_on f s a ‚Üî is_local_extr_on g s a :=
heq.is_extr_filter_iff $ heq.eq_of_nhds_within hmem

lemma filter.eventually_le.is_local_max {f g : Œ± ‚Üí Œ≤} {a : Œ±} (hle : g ‚â§·∂†[ùìù a] f) (hfga : f a = g a)
  (h : is_local_max f a) : is_local_max g a :=
hle.is_max_filter hfga h

lemma is_local_max.congr {f g : Œ± ‚Üí Œ≤} {a : Œ±} (h : is_local_max f a) (heq : f =·∂†[ùìù a] g) :
  is_local_max g a :=
h.congr heq heq.eq_of_nhds

lemma filter.eventually_eq.is_local_max_iff {f g : Œ± ‚Üí Œ≤} {a : Œ±} (heq : f =·∂†[ùìù a] g) :
  is_local_max f a ‚Üî is_local_max g a :=
heq.is_max_filter_iff heq.eq_of_nhds

lemma filter.eventually_le.is_local_min {f g : Œ± ‚Üí Œ≤} {a : Œ±} (hle : f ‚â§·∂†[ùìù a] g) (hfga : f a = g a)
  (h : is_local_min f a) : is_local_min g a :=
hle.is_min_filter hfga h

lemma is_local_min.congr {f g : Œ± ‚Üí Œ≤} {a : Œ±} (h : is_local_min f a) (heq : f =·∂†[ùìù a] g) :
  is_local_min g a :=
h.congr heq heq.eq_of_nhds

lemma filter.eventually_eq.is_local_min_iff {f g : Œ± ‚Üí Œ≤} {a : Œ±} (heq : f =·∂†[ùìù a] g) :
  is_local_min f a ‚Üî is_local_min g a :=
heq.is_min_filter_iff heq.eq_of_nhds

lemma is_local_extr.congr {f g : Œ± ‚Üí Œ≤} {a : Œ±} (h : is_local_extr f a) (heq : f =·∂†[ùìù a] g) :
  is_local_extr g a :=
h.congr heq heq.eq_of_nhds

lemma filter.eventually_eq.is_local_extr_iff {f g : Œ± ‚Üí Œ≤} {a : Œ±} (heq : f =·∂†[ùìù a] g) :
  is_local_extr f a ‚Üî is_local_extr g a :=
heq.is_extr_filter_iff heq.eq_of_nhds

end eventually
