/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import data.equiv.local_equiv
import topology.opens

/-!
# Local homeomorphisms

This file defines homeomorphisms between open subsets of topological spaces. An element `e` of
`local_homeomorph Œ± Œ≤` is an extension of `local_equiv Œ± Œ≤`, i.e., it is a pair of functions
`e.to_fun` and `e.inv_fun`, inverse of each other on the sets `e.source` and `e.target`.
Additionally, we require that these sets are open, and that the functions are continuous on them.
Equivalently, they are homeomorphisms there.

As in equivs, we register a coercion to functions, and we use `e x` and `e.symm x` throughout
instead of `e.to_fun x` and `e.inv_fun x`.

## Main definitions

`homeomorph.to_local_homeomorph`: associating a local homeomorphism to a homeomorphism, with
                                  source = target = univ
`local_homeomorph.symm`  : the inverse of a local homeomorphism
`local_homeomorph.trans` : the composition of two local homeomorphisms
`local_homeomorph.refl`  : the identity local homeomorphism
`local_homeomorph.of_set`: the identity on a set `s`
`eq_on_source`           : equivalence relation describing the "right" notion of equality for local
                           homeomorphisms

## Implementation notes

Most statements are copied from their local_equiv versions, although some care is required
especially when restricting to subsets, as these should be open subsets.

For design notes, see `local_equiv.lean`.

### Local coding conventions

If a lemma deals with the intersection of a set with either source or target of a `local_equiv`,
then it should use `e.source ‚à© s` or `e.target ‚à© t`, not `s ‚à© e.source` or `t ‚à© e.target`.
-/

open function set filter topological_space (second_countable_topology)
open_locale topological_space

variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {Œ¥ : Type*}
[topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥] [topological_space Œ¥]

/-- local homeomorphisms, defined on open subsets of the space -/
@[nolint has_inhabited_instance]
structure local_homeomorph (Œ± : Type*) (Œ≤ : Type*) [topological_space Œ±] [topological_space Œ≤]
  extends local_equiv Œ± Œ≤ :=
(open_source        : is_open source)
(open_target        : is_open target)
(continuous_to_fun  : continuous_on to_fun source)
(continuous_inv_fun : continuous_on inv_fun target)

/-- A homeomorphism induces a local homeomorphism on the whole space -/
def homeomorph.to_local_homeomorph (e : Œ± ‚âÉ‚Çú Œ≤) :
  local_homeomorph Œ± Œ≤ :=
{ open_source        := is_open_univ,
  open_target        := is_open_univ,
  continuous_to_fun  := by { erw ‚Üê continuous_iff_continuous_on_univ, exact e.continuous_to_fun },
  continuous_inv_fun := by { erw ‚Üê continuous_iff_continuous_on_univ, exact e.continuous_inv_fun },
  ..e.to_equiv.to_local_equiv }

namespace local_homeomorph

variables (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≤ Œ≥)

instance : has_coe_to_fun (local_homeomorph Œ± Œ≤) (Œª _, Œ± ‚Üí Œ≤) := ‚ü®Œª e, e.to_fun‚ü©

/-- The inverse of a local homeomorphism -/
protected def symm : local_homeomorph Œ≤ Œ± :=
{ open_source        := e.open_target,
  open_target        := e.open_source,
  continuous_to_fun  := e.continuous_inv_fun,
  continuous_inv_fun := e.continuous_to_fun,
  ..e.to_local_equiv.symm }

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def simps.apply (e : local_homeomorph Œ± Œ≤) : Œ± ‚Üí Œ≤ := e
/-- See Note [custom simps projection] -/
def simps.symm_apply (e : local_homeomorph Œ± Œ≤) : Œ≤ ‚Üí Œ± := e.symm

initialize_simps_projections local_homeomorph
  (to_local_equiv_to_fun ‚Üí apply, to_local_equiv_inv_fun ‚Üí symm_apply,
   to_local_equiv_source ‚Üí source, to_local_equiv_target ‚Üí target, -to_local_equiv)

protected lemma continuous_on : continuous_on e e.source := e.continuous_to_fun

lemma continuous_on_symm : continuous_on e.symm e.target := e.continuous_inv_fun

@[simp, mfld_simps] lemma mk_coe (e : local_equiv Œ± Œ≤) (a b c d) :
  (local_homeomorph.mk e a b c d : Œ± ‚Üí Œ≤) = e := rfl

@[simp, mfld_simps] lemma mk_coe_symm (e : local_equiv Œ± Œ≤) (a b c d) :
  ((local_homeomorph.mk e a b c d).symm : Œ≤ ‚Üí Œ±) = e.symm := rfl

/- Register a few simp lemmas to make sure that `simp` puts the application of a local
homeomorphism in its normal form, i.e., in terms of its coercion to a function. -/

@[simp, mfld_simps] lemma to_fun_eq_coe (e : local_homeomorph Œ± Œ≤) : e.to_fun = e := rfl

@[simp, mfld_simps] lemma inv_fun_eq_coe (e : local_homeomorph Œ± Œ≤) : e.inv_fun = e.symm := rfl

@[simp, mfld_simps] lemma coe_coe : (e.to_local_equiv : Œ± ‚Üí Œ≤) = e := rfl

@[simp, mfld_simps] lemma coe_coe_symm : (e.to_local_equiv.symm : Œ≤ ‚Üí Œ±) = e.symm := rfl

@[simp, mfld_simps] lemma map_source {x : Œ±} (h : x ‚àà e.source) : e x ‚àà e.target :=
e.map_source' h

@[simp, mfld_simps] lemma map_target {x : Œ≤} (h : x ‚àà e.target) : e.symm x ‚àà e.source :=
e.map_target' h

@[simp, mfld_simps] lemma left_inv {x : Œ±} (h : x ‚àà e.source) : e.symm (e x) = x :=
e.left_inv' h

@[simp, mfld_simps] lemma right_inv {x : Œ≤} (h : x ‚àà e.target) : e (e.symm x) = x :=
e.right_inv' h

protected lemma maps_to : maps_to e e.source e.target := Œª x, e.map_source
protected lemma symm_maps_to : maps_to e.symm e.target e.source := e.symm.maps_to
protected lemma left_inv_on : left_inv_on e.symm e e.source := Œª x, e.left_inv
protected lemma right_inv_on : right_inv_on e.symm e e.target := Œª x, e.right_inv
protected lemma inv_on : inv_on e.symm e e.source e.target := ‚ü®e.left_inv_on, e.right_inv_on‚ü©
protected lemma inj_on : inj_on e e.source := e.left_inv_on.inj_on
protected lemma bij_on : bij_on e e.source e.target := e.inv_on.bij_on e.maps_to e.symm_maps_to
protected lemma surj_on : surj_on e e.source e.target := e.bij_on.surj_on

/-- Replace `to_local_equiv` field to provide better definitional equalities. -/
def replace_equiv (e : local_homeomorph Œ± Œ≤) (e' : local_equiv Œ± Œ≤) (h : e.to_local_equiv = e') :
  local_homeomorph Œ± Œ≤ :=
{ to_local_equiv := e',
  open_source := h ‚ñ∏ e.open_source,
  open_target := h ‚ñ∏ e.open_target,
  continuous_to_fun := h ‚ñ∏ e.continuous_to_fun,
  continuous_inv_fun := h ‚ñ∏ e.continuous_inv_fun }

lemma replace_equiv_eq_self (e : local_homeomorph Œ± Œ≤) (e' : local_equiv Œ± Œ≤)
  (h : e.to_local_equiv = e') :
  e.replace_equiv e' h = e :=
by { cases e, subst e', refl }

lemma source_preimage_target : e.source ‚äÜ e ‚Åª¬π' e.target := e.maps_to

lemma eq_of_local_equiv_eq {e e' : local_homeomorph Œ± Œ≤}
  (h : e.to_local_equiv = e'.to_local_equiv) : e = e' :=
by { cases e, cases e', cases h, refl }

lemma eventually_left_inverse (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.source) :
  ‚àÄ·∂† y in ùìù x, e.symm (e y) = y :=
(e.open_source.eventually_mem hx).mono e.left_inv'

lemma eventually_left_inverse' (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.target) :
  ‚àÄ·∂† y in ùìù (e.symm x), e.symm (e y) = y :=
e.eventually_left_inverse (e.map_target hx)

lemma eventually_right_inverse (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.target) :
  ‚àÄ·∂† y in ùìù x, e (e.symm y) = y :=
(e.open_target.eventually_mem hx).mono e.right_inv'

lemma eventually_right_inverse' (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.source) :
  ‚àÄ·∂† y in ùìù (e x), e (e.symm y) = y :=
e.eventually_right_inverse (e.map_source hx)

lemma eventually_ne_nhds_within (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.source) :
  ‚àÄ·∂† x' in ùìù[{x}·∂ú] x, e x' ‚â† e x :=
eventually_nhds_within_iff.2 $ (e.eventually_left_inverse hx).mono $
  Œª x' hx', mt $ Œª h, by rw [mem_singleton_iff, ‚Üê e.left_inv hx, ‚Üê h, hx']

lemma nhds_within_source_inter {x} (hx : x ‚àà e.source) (s : set Œ±) :
  ùìù[e.source ‚à© s] x = ùìù[s] x :=
nhds_within_inter_of_mem (mem_nhds_within_of_mem_nhds $ is_open.mem_nhds e.open_source hx)

lemma nhds_within_target_inter {x} (hx : x ‚àà e.target) (s : set Œ≤) :
  ùìù[e.target ‚à© s] x = ùìù[s] x :=
e.symm.nhds_within_source_inter hx s

lemma image_eq_target_inter_inv_preimage {s : set Œ±} (h : s ‚äÜ e.source) :
  e '' s = e.target ‚à© e.symm ‚Åª¬π' s :=
e.to_local_equiv.image_eq_target_inter_inv_preimage h

lemma image_source_inter_eq' (s : set Œ±) :
  e '' (e.source ‚à© s) = e.target ‚à© e.symm ‚Åª¬π' s :=
e.to_local_equiv.image_source_inter_eq' s

lemma image_source_inter_eq (s : set Œ±) :
  e '' (e.source ‚à© s) = e.target ‚à© e.symm ‚Åª¬π' (e.source ‚à© s) :=
e.to_local_equiv.image_source_inter_eq s

lemma symm_image_eq_source_inter_preimage {s : set Œ≤} (h : s ‚äÜ e.target) :
  e.symm '' s = e.source ‚à© e ‚Åª¬π' s :=
e.symm.image_eq_target_inter_inv_preimage h

lemma symm_image_target_inter_eq (s : set Œ≤) :
  e.symm '' (e.target ‚à© s) = e.source ‚à© e ‚Åª¬π' (e.target ‚à© s) :=
e.symm.image_source_inter_eq _

lemma source_inter_preimage_inv_preimage (s : set Œ±) :
  e.source ‚à© e ‚Åª¬π' (e.symm ‚Åª¬π' s) = e.source ‚à© s :=
e.to_local_equiv.source_inter_preimage_inv_preimage s

lemma target_inter_inv_preimage_preimage (s : set Œ≤) :
  e.target ‚à© e.symm ‚Åª¬π' (e ‚Åª¬π' s) = e.target ‚à© s :=
e.symm.source_inter_preimage_inv_preimage _

lemma source_inter_preimage_target_inter (s : set Œ≤) :
  e.source ‚à© (e ‚Åª¬π' (e.target ‚à© s)) = e.source ‚à© (e ‚Åª¬π' s) :=
e.to_local_equiv.source_inter_preimage_target_inter s

/-- Two local homeomorphisms are equal when they have equal `to_fun`, `inv_fun` and `source`.
It is not sufficient to have equal `to_fun` and `source`, as this only determines `inv_fun` on
the target. This would only be true for a weaker notion of equality, arguably the right one,
called `eq_on_source`. -/
@[ext]
protected lemma ext (e' : local_homeomorph Œ± Œ≤) (h : ‚àÄx, e x = e' x)
  (hinv : ‚àÄx, e.symm x = e'.symm x) (hs : e.source = e'.source) : e = e' :=
eq_of_local_equiv_eq (local_equiv.ext h hinv hs)

@[simp, mfld_simps] lemma symm_to_local_equiv : e.symm.to_local_equiv = e.to_local_equiv.symm := rfl
-- The following lemmas are already simp via local_equiv
lemma symm_source : e.symm.source = e.target := rfl
lemma symm_target : e.symm.target = e.source := rfl
@[simp, mfld_simps] lemma symm_symm : e.symm.symm = e := eq_of_local_equiv_eq $ by simp

/-- A local homeomorphism is continuous at any point of its source -/
protected lemma continuous_at {x : Œ±} (h : x ‚àà e.source) : continuous_at e x :=
(e.continuous_on x h).continuous_at (is_open.mem_nhds e.open_source h)

/-- A local homeomorphism inverse is continuous at any point of its target -/
lemma continuous_at_symm {x : Œ≤} (h : x ‚àà e.target) : continuous_at e.symm x :=
e.symm.continuous_at h

lemma tendsto_symm {x} (hx : x ‚àà e.source) :
  tendsto e.symm (ùìù (e x)) (ùìù x) :=
by simpa only [continuous_at, e.left_inv hx] using e.continuous_at_symm (e.map_source hx)

lemma map_nhds_eq {x} (hx : x ‚àà e.source) : map e (ùìù x) = ùìù (e x) :=
le_antisymm (e.continuous_at hx) $
  le_map_of_right_inverse (e.eventually_right_inverse' hx) (e.tendsto_symm hx)

lemma symm_map_nhds_eq {x} (hx : x ‚àà e.source) :
  map e.symm (ùìù (e x)) = ùìù x :=
(e.symm.map_nhds_eq $ e.map_source hx).trans $ by rw e.left_inv hx

lemma image_mem_nhds {x} (hx : x ‚àà e.source) {s : set Œ±} (hs : s ‚àà ùìù x) :
  e '' s ‚àà ùìù (e x) :=
e.map_nhds_eq hx ‚ñ∏ filter.image_mem_map hs

lemma map_nhds_within_eq (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.source) (s : set Œ±) :
  map e (ùìù[s] x) = ùìù[e '' (e.source ‚à© s)] (e x) :=
calc map e (ùìù[s] x) = map e (ùìù[e.source ‚à© s] x) :
  congr_arg (map e) (e.nhds_within_source_inter hx _).symm
... = ùìù[e '' (e.source ‚à© s)] (e x) :
  (e.left_inv_on.mono $ inter_subset_left _ _).map_nhds_within_eq (e.left_inv hx)
    (e.continuous_at_symm (e.map_source hx)).continuous_within_at
    (e.continuous_at hx).continuous_within_at

lemma map_nhds_within_preimage_eq (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.source) (s : set Œ≤) :
  map e (ùìù[e ‚Åª¬π' s] x) = ùìù[s] (e x) :=
by rw [e.map_nhds_within_eq hx, e.image_source_inter_eq', e.target_inter_inv_preimage_preimage,
  e.nhds_within_target_inter (e.map_source hx)]

lemma preimage_open_of_open {s : set Œ≤} (hs : is_open s) : is_open (e.source ‚à© e ‚Åª¬π' s) :=
e.continuous_on.preimage_open_of_open e.open_source hs

/-!
### `local_homeomorph.is_image` relation

We say that `t : set Œ≤` is an image of `s : set Œ±` under a local homeomorphism `e` if any of the
following equivalent conditions hold:

* `e '' (e.source ‚à© s) = e.target ‚à© t`;
* `e.source ‚à© e ‚Åª¬π t = e.source ‚à© s`;
* `‚àÄ x ‚àà e.source, e x ‚àà t ‚Üî x ‚àà s` (this one is used in the definition).

This definition is a restatement of `local_equiv.is_image` for local homeomorphisms. In this section
we transfer API about `local_equiv.is_image` to local homeomorphisms and add a few
`local_homeomorph`-specific lemmas like `local_homeomorph.is_image.closure`.
-/

/-- We say that `t : set Œ≤` is an image of `s : set Œ±` under a local homeomorphism `e` if any of the
following equivalent conditions hold:

* `e '' (e.source ‚à© s) = e.target ‚à© t`;
* `e.source ‚à© e ‚Åª¬π t = e.source ‚à© s`;
* `‚àÄ x ‚àà e.source, e x ‚àà t ‚Üî x ‚àà s` (this one is used in the definition).
-/
def is_image (s : set Œ±) (t : set Œ≤) : Prop := ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà e.source ‚Üí (e x ‚àà t ‚Üî x ‚àà s)

namespace is_image

variables {e} {s : set Œ±} {t : set Œ≤} {x : Œ±} {y : Œ≤}

lemma to_local_equiv (h : e.is_image s t) : e.to_local_equiv.is_image s t := h

lemma apply_mem_iff (h : e.is_image s t) (hx : x ‚àà e.source) : e x ‚àà t ‚Üî x ‚àà s := h hx

protected lemma symm (h : e.is_image s t) : e.symm.is_image t s := h.to_local_equiv.symm

lemma symm_apply_mem_iff (h : e.is_image s t) (hy : y ‚àà e.target) : (e.symm y ‚àà s ‚Üî y ‚àà t) :=
h.symm hy

@[simp] lemma symm_iff : e.symm.is_image t s ‚Üî e.is_image s t := ‚ü®Œª h, h.symm, Œª h, h.symm‚ü©

protected lemma maps_to (h : e.is_image s t) : maps_to e (e.source ‚à© s) (e.target ‚à© t) :=
h.to_local_equiv.maps_to

lemma symm_maps_to (h : e.is_image s t) : maps_to e.symm (e.target ‚à© t) (e.source ‚à© s) :=
h.symm.maps_to

lemma image_eq (h : e.is_image s t) : e '' (e.source ‚à© s) = e.target ‚à© t :=
h.to_local_equiv.image_eq

lemma symm_image_eq (h : e.is_image s t) : e.symm '' (e.target ‚à© t) = e.source ‚à© s :=
h.symm.image_eq

lemma iff_preimage_eq : e.is_image s t ‚Üî e.source ‚à© e ‚Åª¬π' t = e.source ‚à© s :=
local_equiv.is_image.iff_preimage_eq

alias iff_preimage_eq ‚Üî local_homeomorph.is_image.preimage_eq
  local_homeomorph.is_image.of_preimage_eq

lemma iff_symm_preimage_eq : e.is_image s t ‚Üî e.target ‚à© e.symm ‚Åª¬π' s = e.target ‚à© t :=
symm_iff.symm.trans iff_preimage_eq

alias iff_symm_preimage_eq ‚Üî local_homeomorph.is_image.symm_preimage_eq
  local_homeomorph.is_image.of_symm_preimage_eq

lemma iff_symm_preimage_eq' :
  e.is_image s t ‚Üî e.target ‚à© e.symm ‚Åª¬π' (e.source ‚à© s) = e.target ‚à© t :=
by rw [iff_symm_preimage_eq, ‚Üê image_source_inter_eq, ‚Üê image_source_inter_eq']

alias iff_symm_preimage_eq' ‚Üî local_homeomorph.is_image.symm_preimage_eq'
  local_homeomorph.is_image.of_symm_preimage_eq'

lemma iff_preimage_eq' : e.is_image s t ‚Üî e.source ‚à© e ‚Åª¬π' (e.target ‚à© t) = e.source ‚à© s :=
symm_iff.symm.trans iff_symm_preimage_eq'

alias iff_preimage_eq' ‚Üî local_homeomorph.is_image.preimage_eq'
  local_homeomorph.is_image.of_preimage_eq'

lemma of_image_eq (h : e '' (e.source ‚à© s) = e.target ‚à© t) : e.is_image s t :=
local_equiv.is_image.of_image_eq h

lemma of_symm_image_eq (h : e.symm '' (e.target ‚à© t) = e.source ‚à© s) : e.is_image s t :=
local_equiv.is_image.of_symm_image_eq h

protected lemma compl (h : e.is_image s t) : e.is_image s·∂ú t·∂ú :=
Œª x hx, not_congr (h hx)

protected lemma inter {s' t'} (h : e.is_image s t) (h' : e.is_image s' t') :
  e.is_image (s ‚à© s') (t ‚à© t') :=
Œª x hx, and_congr (h hx) (h' hx)

protected lemma union {s' t'} (h : e.is_image s t) (h' : e.is_image s' t') :
  e.is_image (s ‚à™ s') (t ‚à™ t') :=
Œª x hx, or_congr (h hx) (h' hx)

protected lemma diff {s' t'} (h : e.is_image s t) (h' : e.is_image s' t') :
  e.is_image (s \ s') (t \ t') :=
h.inter h'.compl

lemma left_inv_on_piecewise {e' : local_homeomorph Œ± Œ≤} [‚àÄ i, decidable (i ‚àà s)]
  [‚àÄ i, decidable (i ‚àà t)] (h : e.is_image s t) (h' : e'.is_image s t) :
  left_inv_on (t.piecewise e.symm e'.symm) (s.piecewise e e') (s.ite e.source e'.source) :=
h.to_local_equiv.left_inv_on_piecewise h'

lemma inter_eq_of_inter_eq_of_eq_on {e' : local_homeomorph Œ± Œ≤} (h : e.is_image s t)
  (h' : e'.is_image s t) (hs : e.source ‚à© s = e'.source ‚à© s) (Heq : eq_on e e' (e.source ‚à© s)) :
  e.target ‚à© t = e'.target ‚à© t :=
h.to_local_equiv.inter_eq_of_inter_eq_of_eq_on h' hs Heq

lemma symm_eq_on_of_inter_eq_of_eq_on {e' : local_homeomorph Œ± Œ≤} (h : e.is_image s t)
  (hs : e.source ‚à© s = e'.source ‚à© s) (Heq : eq_on e e' (e.source ‚à© s)) :
  eq_on e.symm e'.symm (e.target ‚à© t) :=
h.to_local_equiv.symm_eq_on_of_inter_eq_of_eq_on hs Heq

lemma map_nhds_within_eq (h : e.is_image s t) (hx : x ‚àà e.source) :
  map e (ùìù[s] x) = ùìù[t] (e x) :=
by rw [e.map_nhds_within_eq hx, h.image_eq, e.nhds_within_target_inter (e.map_source hx)]

protected lemma closure (h : e.is_image s t) : e.is_image (closure s) (closure t) :=
Œª x hx, by simp only [mem_closure_iff_nhds_within_ne_bot, ‚Üê h.map_nhds_within_eq hx, map_ne_bot_iff]

protected lemma interior (h : e.is_image s t) : e.is_image (interior s) (interior t) :=
by simpa only [closure_compl, compl_compl] using h.compl.closure.compl

protected lemma frontier (h : e.is_image s t) :
  e.is_image (frontier s) (frontier t) :=
h.closure.diff h.interior

lemma is_open_iff (h : e.is_image s t) :
  is_open (e.source ‚à© s) ‚Üî is_open (e.target ‚à© t) :=
‚ü®Œª hs, h.symm_preimage_eq' ‚ñ∏ e.symm.preimage_open_of_open hs,
  Œª hs, h.preimage_eq' ‚ñ∏ e.preimage_open_of_open hs‚ü©

/-- Restrict a `local_homeomorph` to a pair of corresponding open sets. -/
@[simps to_local_equiv] def restr (h : e.is_image s t) (hs : is_open (e.source ‚à© s)) :
  local_homeomorph Œ± Œ≤ :=
{ to_local_equiv := h.to_local_equiv.restr,
  open_source := hs,
  open_target := h.is_open_iff.1 hs,
  continuous_to_fun := e.continuous_on.mono (inter_subset_left _ _),
  continuous_inv_fun := e.symm.continuous_on.mono (inter_subset_left _ _) }

end is_image

lemma is_image_source_target : e.is_image e.source e.target :=
e.to_local_equiv.is_image_source_target

lemma is_image_source_target_of_disjoint (e' : local_homeomorph Œ± Œ≤)
  (hs : disjoint e.source e'.source) (ht : disjoint e.target e'.target) :
  e.is_image e'.source e'.target :=
e.to_local_equiv.is_image_source_target_of_disjoint e'.to_local_equiv hs ht

/-- Preimage of interior or interior of preimage coincide for local homeomorphisms, when restricted
to the source. -/
lemma preimage_interior (s : set Œ≤) :
  e.source ‚à© e ‚Åª¬π' (interior s) = e.source ‚à© interior (e ‚Åª¬π' s) :=
(is_image.of_preimage_eq rfl).interior.preimage_eq

lemma preimage_closure (s : set Œ≤) :
  e.source ‚à© e ‚Åª¬π' (closure s) = e.source ‚à© closure (e ‚Åª¬π' s) :=
(is_image.of_preimage_eq rfl).closure.preimage_eq

lemma preimage_frontier (s : set Œ≤) :
  e.source ‚à© e ‚Åª¬π' (frontier s) = e.source ‚à© frontier (e ‚Åª¬π' s) :=
(is_image.of_preimage_eq rfl).frontier.preimage_eq

lemma preimage_open_of_open_symm {s : set Œ±} (hs : is_open s) :
  is_open (e.target ‚à© e.symm ‚Åª¬π' s) :=
e.symm.continuous_on.preimage_open_of_open e.open_target hs

/-- The image of an open set in the source is open. -/
lemma image_open_of_open {s : set Œ±} (hs : is_open s) (h : s ‚äÜ e.source) : is_open (e '' s) :=
begin
  have : e '' s = e.target ‚à© e.symm ‚Åª¬π' s :=
    e.to_local_equiv.image_eq_target_inter_inv_preimage h,
  rw this,
  exact e.continuous_on_symm.preimage_open_of_open e.open_target hs
end

/-- The image of the restriction of an open set to the source is open. -/
lemma image_open_of_open' {s : set Œ±} (hs : is_open s) : is_open (e '' (e.source ‚à© s)) :=
image_open_of_open _ (is_open.inter e.open_source hs) (inter_subset_left _ _)

/-- A `local_equiv` with continuous open forward map and an open source is a `local_homeomorph`. -/
def of_continuous_open_restrict (e : local_equiv Œ± Œ≤) (hc : continuous_on e e.source)
  (ho : is_open_map (e.source.restrict e)) (hs : is_open e.source) :
  local_homeomorph Œ± Œ≤ :=
{ to_local_equiv := e,
  open_source := hs,
  open_target := by simpa only [range_restrict, e.image_source_eq_target] using ho.is_open_range,
  continuous_to_fun := hc,
  continuous_inv_fun := e.image_source_eq_target ‚ñ∏
    ho.continuous_on_image_of_left_inv_on e.left_inv_on }

/-- A `local_equiv` with continuous open forward map and an open source is a `local_homeomorph`. -/
def of_continuous_open (e : local_equiv Œ± Œ≤) (hc : continuous_on e e.source)
  (ho : is_open_map e) (hs : is_open e.source) :
  local_homeomorph Œ± Œ≤ :=
of_continuous_open_restrict e hc (ho.restrict hs) hs

/-- Restricting a local homeomorphism `e` to `e.source ‚à© s` when `s` is open. This is sometimes hard
to use because of the openness assumption, but it has the advantage that when it can
be used then its local_equiv is defeq to local_equiv.restr -/
protected def restr_open (s : set Œ±) (hs : is_open s) :
  local_homeomorph Œ± Œ≤ :=
(@is_image.of_symm_preimage_eq Œ± Œ≤ _ _ e s (e.symm ‚Åª¬π' s) rfl).restr
  (is_open.inter e.open_source hs)

@[simp, mfld_simps] lemma restr_open_to_local_equiv (s : set Œ±) (hs : is_open s) :
  (e.restr_open s hs).to_local_equiv = e.to_local_equiv.restr s := rfl

-- Already simp via local_equiv
lemma restr_open_source (s : set Œ±) (hs : is_open s) :
  (e.restr_open s hs).source = e.source ‚à© s := rfl

/-- Restricting a local homeomorphism `e` to `e.source ‚à© interior s`. We use the interior to make
sure that the restriction is well defined whatever the set s, since local homeomorphisms are by
definition defined on open sets. In applications where `s` is open, this coincides with the
restriction of local equivalences -/
@[simps apply symm_apply (mfld_cfg), simps source target {attrs := []}]
protected def restr (s : set Œ±) : local_homeomorph Œ± Œ≤ :=
e.restr_open (interior s) is_open_interior

@[simp, mfld_simps] lemma restr_to_local_equiv (s : set Œ±) :
  (e.restr s).to_local_equiv = (e.to_local_equiv).restr (interior s) := rfl

lemma restr_source' (s : set Œ±) (hs : is_open s) : (e.restr s).source = e.source ‚à© s :=
by rw [e.restr_source, hs.interior_eq]

lemma restr_to_local_equiv' (s : set Œ±) (hs : is_open s):
  (e.restr s).to_local_equiv = e.to_local_equiv.restr s :=
by rw [e.restr_to_local_equiv, hs.interior_eq]

lemma restr_eq_of_source_subset {e : local_homeomorph Œ± Œ≤} {s : set Œ±} (h : e.source ‚äÜ s) :
  e.restr s = e :=
begin
  apply eq_of_local_equiv_eq,
  rw restr_to_local_equiv,
  apply local_equiv.restr_eq_of_source_subset,
  exact interior_maximal h e.open_source
end

@[simp, mfld_simps] lemma restr_univ {e : local_homeomorph Œ± Œ≤} : e.restr univ = e :=
restr_eq_of_source_subset (subset_univ _)

lemma restr_source_inter (s : set Œ±) : e.restr (e.source ‚à© s) = e.restr s :=
begin
  refine local_homeomorph.ext _ _ (Œªx, rfl) (Œªx, rfl) _,
  simp [e.open_source.interior_eq, ‚Üê inter_assoc]
end

/-- The identity on the whole space as a local homeomorphism. -/
@[simps apply (mfld_cfg), simps source target {attrs := []}]
protected def refl (Œ± : Type*) [topological_space Œ±] : local_homeomorph Œ± Œ± :=
(homeomorph.refl Œ±).to_local_homeomorph

@[simp, mfld_simps] lemma refl_local_equiv :
  (local_homeomorph.refl Œ±).to_local_equiv = local_equiv.refl Œ± := rfl
@[simp, mfld_simps] lemma refl_symm : (local_homeomorph.refl Œ±).symm = local_homeomorph.refl Œ± :=
rfl

section
variables {s : set Œ±} (hs : is_open s)

/-- The identity local equiv on a set `s` -/
@[simps apply (mfld_cfg), simps source target {attrs := []}]
def of_set (s : set Œ±) (hs : is_open s) : local_homeomorph Œ± Œ± :=
{ open_source        := hs,
  open_target        := hs,
  continuous_to_fun  := continuous_id.continuous_on,
  continuous_inv_fun := continuous_id.continuous_on,
  ..local_equiv.of_set s }

@[simp, mfld_simps] lemma of_set_to_local_equiv :
  (of_set s hs).to_local_equiv = local_equiv.of_set s := rfl
@[simp, mfld_simps] lemma of_set_symm : (of_set s hs).symm = of_set s hs := rfl

@[simp, mfld_simps] lemma of_set_univ_eq_refl :
  of_set univ is_open_univ = local_homeomorph.refl Œ± :=
by ext; simp

end

/-- Composition of two local homeomorphisms when the target of the first and the source of
the second coincide. -/
protected def trans' (h : e.target = e'.source) : local_homeomorph Œ± Œ≥ :=
{ open_source       := e.open_source,
  open_target       := e'.open_target,
  continuous_to_fun := begin
    apply continuous_on.comp e'.continuous_to_fun e.continuous_to_fun,
    rw ‚Üê h,
    exact e.to_local_equiv.source_subset_preimage_target
  end,
  continuous_inv_fun := begin
    apply continuous_on.comp e.continuous_inv_fun e'.continuous_inv_fun,
    rw h,
    exact e'.to_local_equiv.target_subset_preimage_source
  end,
  ..local_equiv.trans' e.to_local_equiv e'.to_local_equiv h }

/-- Composing two local homeomorphisms, by restricting to the maximal domain where their
composition is well defined. -/
protected def trans : local_homeomorph Œ± Œ≥ :=
  local_homeomorph.trans' (e.symm.restr_open e'.source e'.open_source).symm
    (e'.restr_open e.target e.open_target) (by simp [inter_comm])

@[simp, mfld_simps] lemma trans_to_local_equiv :
  (e.trans e').to_local_equiv = e.to_local_equiv.trans e'.to_local_equiv := rfl
@[simp, mfld_simps] lemma coe_trans : (e.trans e' : Œ± ‚Üí Œ≥) = e' ‚àò e := rfl
@[simp, mfld_simps] lemma coe_trans_symm : ((e.trans e').symm : Œ≥ ‚Üí Œ±) = e.symm ‚àò e'.symm := rfl

lemma trans_symm_eq_symm_trans_symm : (e.trans e').symm = e'.symm.trans e.symm :=
by cases e; cases e'; refl

/- This could be considered as a simp lemma, but there are many situations where it makes something
simple into something more complicated. -/
lemma trans_source : (e.trans e').source = e.source ‚à© e ‚Åª¬π' e'.source :=
local_equiv.trans_source e.to_local_equiv e'.to_local_equiv

lemma trans_source' : (e.trans e').source = e.source ‚à© e ‚Åª¬π' (e.target ‚à© e'.source) :=
local_equiv.trans_source' e.to_local_equiv e'.to_local_equiv

lemma trans_source'' : (e.trans e').source = e.symm '' (e.target ‚à© e'.source) :=
local_equiv.trans_source'' e.to_local_equiv e'.to_local_equiv

lemma image_trans_source : e '' (e.trans e').source = e.target ‚à© e'.source :=
local_equiv.image_trans_source e.to_local_equiv e'.to_local_equiv

lemma trans_target : (e.trans e').target = e'.target ‚à© e'.symm ‚Åª¬π' e.target := rfl

lemma trans_target' : (e.trans e').target = e'.target ‚à© e'.symm ‚Åª¬π' (e'.source ‚à© e.target) :=
trans_source' e'.symm e.symm

lemma trans_target'' : (e.trans e').target = e' '' (e'.source ‚à© e.target) :=
trans_source'' e'.symm e.symm

lemma inv_image_trans_target : e'.symm '' (e.trans e').target = e'.source ‚à© e.target :=
image_trans_source e'.symm e.symm

lemma trans_assoc (e'' : local_homeomorph Œ≥ Œ¥) :
  (e.trans e').trans e'' = e.trans (e'.trans e'') :=
eq_of_local_equiv_eq $ local_equiv.trans_assoc e.to_local_equiv e'.to_local_equiv e''.to_local_equiv

@[simp, mfld_simps] lemma trans_refl : e.trans (local_homeomorph.refl Œ≤) = e :=
eq_of_local_equiv_eq $ local_equiv.trans_refl e.to_local_equiv

@[simp, mfld_simps] lemma refl_trans : (local_homeomorph.refl Œ±).trans e = e :=
eq_of_local_equiv_eq $ local_equiv.refl_trans e.to_local_equiv

lemma trans_of_set {s : set Œ≤} (hs : is_open s) :
  e.trans (of_set s hs) = e.restr (e ‚Åª¬π' s) :=
local_homeomorph.ext _ _ (Œªx, rfl) (Œªx, rfl) $
  by simp [local_equiv.trans_source, (e.preimage_interior _).symm, hs.interior_eq]

lemma trans_of_set' {s : set Œ≤} (hs : is_open s) :
  e.trans (of_set s hs) = e.restr (e.source ‚à© e ‚Åª¬π' s) :=
by rw [trans_of_set, restr_source_inter]

lemma of_set_trans {s : set Œ±} (hs : is_open s) :
  (of_set s hs).trans e = e.restr s :=
local_homeomorph.ext _ _ (Œªx, rfl) (Œªx, rfl) $
  by simp [local_equiv.trans_source, hs.interior_eq, inter_comm]

lemma of_set_trans' {s : set Œ±} (hs : is_open s) :
  (of_set s hs).trans e = e.restr (e.source ‚à© s) :=
by rw [of_set_trans, restr_source_inter]

@[simp, mfld_simps] lemma of_set_trans_of_set
  {s : set Œ±} (hs : is_open s) {s' : set Œ±} (hs' : is_open s') :
  (of_set s hs).trans (of_set s' hs') = of_set (s ‚à© s') (is_open.inter hs hs')  :=
begin
  rw (of_set s hs).trans_of_set hs',
  ext; simp [hs'.interior_eq]
end

lemma restr_trans (s : set Œ±) :
  (e.restr s).trans e' = (e.trans e').restr s :=
eq_of_local_equiv_eq $ local_equiv.restr_trans e.to_local_equiv e'.to_local_equiv (interior s)

/-- `eq_on_source e e'` means that `e` and `e'` have the same source, and coincide there. They
should really be considered the same local equiv. -/
def eq_on_source (e e' : local_homeomorph Œ± Œ≤) : Prop :=
e.source = e'.source ‚àß (eq_on e e' e.source)

lemma eq_on_source_iff (e e' : local_homeomorph Œ± Œ≤) :
eq_on_source e e' ‚Üî local_equiv.eq_on_source e.to_local_equiv e'.to_local_equiv :=
iff.rfl

/-- `eq_on_source` is an equivalence relation -/
instance : setoid (local_homeomorph Œ± Œ≤) :=
{ r     := eq_on_source,
  iseqv := ‚ü®
    Œªe, (@local_equiv.eq_on_source_setoid Œ± Œ≤).iseqv.1 e.to_local_equiv,
    Œªe e' h, (@local_equiv.eq_on_source_setoid Œ± Œ≤).iseqv.2.1 ((eq_on_source_iff e e').1 h),
    Œªe e' e'' h h', (@local_equiv.eq_on_source_setoid Œ± Œ≤).iseqv.2.2
      ((eq_on_source_iff e e').1 h) ((eq_on_source_iff e' e'').1 h')‚ü© }

lemma eq_on_source_refl : e ‚âà e := setoid.refl _

/-- If two local homeomorphisms are equivalent, so are their inverses -/
lemma eq_on_source.symm' {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') : e.symm ‚âà e'.symm :=
local_equiv.eq_on_source.symm' h

/-- Two equivalent local homeomorphisms have the same source -/
lemma eq_on_source.source_eq {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') : e.source = e'.source :=
h.1

/-- Two equivalent local homeomorphisms have the same target -/
lemma eq_on_source.target_eq {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') : e.target = e'.target :=
h.symm'.1

/-- Two equivalent local homeomorphisms have coinciding `to_fun` on the source -/
lemma eq_on_source.eq_on {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') :
  eq_on e e' e.source :=
h.2

/-- Two equivalent local homeomorphisms have coinciding `inv_fun` on the target -/
lemma eq_on_source.symm_eq_on_target {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') :
  eq_on e.symm e'.symm e.target :=
h.symm'.2

/-- Composition of local homeomorphisms respects equivalence -/
lemma eq_on_source.trans' {e e' : local_homeomorph Œ± Œ≤} {f f' : local_homeomorph Œ≤ Œ≥}
  (he : e ‚âà e') (hf : f ‚âà f') : e.trans f ‚âà e'.trans f' :=
local_equiv.eq_on_source.trans' he hf

/-- Restriction of local homeomorphisms respects equivalence -/
lemma eq_on_source.restr {e e' : local_homeomorph Œ± Œ≤} (he : e ‚âà e') (s : set Œ±) :
  e.restr s ‚âà e'.restr s :=
local_equiv.eq_on_source.restr he _

/-- Composition of a local homeomorphism and its inverse is equivalent to the restriction of the
identity to the source -/
lemma trans_self_symm :
  e.trans e.symm ‚âà local_homeomorph.of_set e.source e.open_source :=
local_equiv.trans_self_symm _

lemma trans_symm_self :
  e.symm.trans e ‚âà local_homeomorph.of_set e.target e.open_target :=
e.symm.trans_self_symm

lemma eq_of_eq_on_source_univ {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e')
  (s : e.source = univ) (t : e.target = univ) : e = e' :=
eq_of_local_equiv_eq $ local_equiv.eq_of_eq_on_source_univ _ _ h s t

section prod

/-- The product of two local homeomorphisms, as a local homeomorphism on the product space. -/
@[simps to_local_equiv apply (mfld_cfg), simps source target symm_apply {attrs := []}]
def prod (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) :
  local_homeomorph (Œ± √ó Œ≥) (Œ≤ √ó Œ¥) :=
{ open_source := e.open_source.prod e'.open_source,
  open_target := e.open_target.prod e'.open_target,
  continuous_to_fun := e.continuous_on.prod_map e'.continuous_on,
  continuous_inv_fun := e.continuous_on_symm.prod_map e'.continuous_on_symm,
  to_local_equiv := e.to_local_equiv.prod e'.to_local_equiv }

@[simp, mfld_simps] lemma prod_symm (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) :
  (e.prod e').symm = (e.symm.prod e'.symm) :=
rfl

@[simp, mfld_simps] lemma prod_trans
  {Œ∑ : Type*} {Œµ : Type*} [topological_space Œ∑] [topological_space Œµ]
  (e : local_homeomorph Œ± Œ≤) (f : local_homeomorph Œ≤ Œ≥)
  (e' : local_homeomorph Œ¥ Œ∑) (f' : local_homeomorph Œ∑ Œµ) :
  (e.prod e').trans (f.prod f') = (e.trans f).prod (e'.trans f') :=
local_homeomorph.eq_of_local_equiv_eq $
  by dsimp only [trans_to_local_equiv, prod_to_local_equiv]; apply local_equiv.prod_trans

end prod

section piecewise

/-- Combine two `local_homeomorph`s using `set.piecewise`. The source of the new `local_homeomorph`
is `s.ite e.source e'.source = e.source ‚à© s ‚à™ e'.source \ s`, and similarly for target.  The
function sends `e.source ‚à© s` to `e.target ‚à© t` using `e` and `e'.source \ s` to `e'.target \ t`
using `e'`, and similarly for the inverse function. To ensure that the maps `to_fun` and `inv_fun`
are inverse of each other on the new `source` and `target`, the definition assumes that the sets `s`
and `t` are related both by `e.is_image` and `e'.is_image`. To ensure that the new maps are
continuous on `source`/`target`, it also assumes that `e.source` and `e'.source` meet `frontier s`
on the same set and `e x = e' x` on this intersection. -/
@[simps to_local_equiv apply {fully_applied := ff}]
def piecewise (e e' : local_homeomorph Œ± Œ≤) (s : set Œ±) (t : set Œ≤)
  [‚àÄ x, decidable (x ‚àà s)] [‚àÄ y, decidable (y ‚àà t)] (H : e.is_image s t) (H' : e'.is_image s t)
  (Hs : e.source ‚à© frontier s = e'.source ‚à© frontier s)
  (Heq : eq_on e e' (e.source ‚à© frontier s)) :
  local_homeomorph Œ± Œ≤ :=
{ to_local_equiv := e.to_local_equiv.piecewise e'.to_local_equiv s t H H',
  open_source := e.open_source.ite e'.open_source Hs,
  open_target := e.open_target.ite e'.open_target $
    H.frontier.inter_eq_of_inter_eq_of_eq_on H'.frontier Hs Heq,
  continuous_to_fun := continuous_on_piecewise_ite e.continuous_on e'.continuous_on Hs Heq,
  continuous_inv_fun := continuous_on_piecewise_ite e.continuous_on_symm e'.continuous_on_symm
    (H.frontier.inter_eq_of_inter_eq_of_eq_on H'.frontier Hs Heq)
    (H.frontier.symm_eq_on_of_inter_eq_of_eq_on Hs Heq) }

@[simp] lemma symm_piecewise (e e' : local_homeomorph Œ± Œ≤) {s : set Œ±} {t : set Œ≤}
  [‚àÄ x, decidable (x ‚àà s)] [‚àÄ y, decidable (y ‚àà t)] (H : e.is_image s t) (H' : e'.is_image s t)
  (Hs : e.source ‚à© frontier s = e'.source ‚à© frontier s)
  (Heq : eq_on e e' (e.source ‚à© frontier s)) :
  (e.piecewise e' s t H H' Hs Heq).symm =
    e.symm.piecewise e'.symm t s H.symm H'.symm
      (H.frontier.inter_eq_of_inter_eq_of_eq_on H'.frontier Hs Heq)
      (H.frontier.symm_eq_on_of_inter_eq_of_eq_on Hs Heq) :=
rfl

/-- Combine two `local_homeomorph`s with disjoint sources and disjoint targets. We reuse
`local_homeomorph.piecewise` then override `to_local_equiv` to `local_equiv.disjoint_union`.
This way we have better definitional equalities for `source` and `target`. -/
def disjoint_union (e e' : local_homeomorph Œ± Œ≤)
  [‚àÄ x, decidable (x ‚àà e.source)] [‚àÄ y, decidable (y ‚àà e.target)]
  (Hs : disjoint e.source e'.source) (Ht : disjoint e.target e'.target) :
  local_homeomorph Œ± Œ≤ :=
(e.piecewise e' e.source e.target e.is_image_source_target
  (e'.is_image_source_target_of_disjoint e Hs.symm Ht.symm)
  (by rw [e.open_source.inter_frontier_eq, e'.open_source.inter_frontier_eq_empty_of_disjoint Hs])
  (by { rw e.open_source.inter_frontier_eq, exact eq_on_empty _ _ })).replace_equiv
    (e.to_local_equiv.disjoint_union e'.to_local_equiv Hs Ht)
    (local_equiv.disjoint_union_eq_piecewise _ _ _ _).symm

end piecewise

section pi

variables {Œπ : Type*} [fintype Œπ] {Xi Yi : Œπ ‚Üí Type*} [Œ† i, topological_space (Xi i)]
  [Œ† i, topological_space (Yi i)] (ei : Œ† i, local_homeomorph (Xi i) (Yi i))

/-- The product of a finite family of `local_homeomorph`s. -/
@[simps to_local_equiv] def pi : local_homeomorph (Œ† i, Xi i) (Œ† i, Yi i) :=
{ to_local_equiv := local_equiv.pi (Œª i, (ei i).to_local_equiv),
  open_source := is_open_set_pi finite_univ $ Œª i hi, (ei i).open_source,
  open_target := is_open_set_pi finite_univ $ Œª i hi, (ei i).open_target,
  continuous_to_fun := continuous_on_pi.2 $ Œª i, (ei i).continuous_on.comp
    (continuous_apply _).continuous_on (Œª f hf, hf i trivial),
  continuous_inv_fun := continuous_on_pi.2 $ Œª i, (ei i).continuous_on_symm.comp
    (continuous_apply _).continuous_on (Œª f hf, hf i trivial) }

end pi

section continuity

/-- Continuity within a set at a point can be read under right composition with a local
homeomorphism, if the point is in its target -/
lemma continuous_within_at_iff_continuous_within_at_comp_right
  {f : Œ≤ ‚Üí Œ≥} {s : set Œ≤} {x : Œ≤} (h : x ‚àà e.target) :
  continuous_within_at f s x ‚Üî continuous_within_at (f ‚àò e) (e ‚Åª¬π' s) (e.symm x) :=
by simp_rw [continuous_within_at, ‚Üê @tendsto_map'_iff _ _ _ _ e,
  e.map_nhds_within_preimage_eq (e.map_target h), (‚àò), e.right_inv h]

/-- Continuity at a point can be read under right composition with a local homeomorphism, if the
point is in its target -/
lemma continuous_at_iff_continuous_at_comp_right
  {f : Œ≤ ‚Üí Œ≥} {x : Œ≤} (h : x ‚àà e.target) :
  continuous_at f x ‚Üî continuous_at (f ‚àò e) (e.symm x) :=
by rw [‚Üê continuous_within_at_univ, e.continuous_within_at_iff_continuous_within_at_comp_right h,
       preimage_univ, continuous_within_at_univ]

/-- A function is continuous on a set if and only if its composition with a local homeomorphism
on the right is continuous on the corresponding set. -/
lemma continuous_on_iff_continuous_on_comp_right {f : Œ≤ ‚Üí Œ≥} {s : set Œ≤} (h : s ‚äÜ e.target) :
  continuous_on f s ‚Üî continuous_on (f ‚àò e) (e.source ‚à© e ‚Åª¬π' s) :=
begin
  simp only [‚Üê e.symm_image_eq_source_inter_preimage h, continuous_on, ball_image_iff],
  refine forall_congr (Œª x, forall_congr $ Œª hx, _),
  rw [e.continuous_within_at_iff_continuous_within_at_comp_right (h hx),
    e.symm_image_eq_source_inter_preimage h, inter_comm, continuous_within_at_inter],
  exact is_open.mem_nhds e.open_source (e.map_target (h hx))
end

/-- Continuity within a set at a point can be read under left composition with a local
homeomorphism if a neighborhood of the initial point is sent to the source of the local
homeomorphism-/
lemma continuous_within_at_iff_continuous_within_at_comp_left
  {f : Œ≥ ‚Üí Œ±} {s : set Œ≥} {x : Œ≥} (hx : f x ‚àà e.source) (h : f ‚Åª¬π' e.source ‚àà ùìù[s] x) :
  continuous_within_at f s x ‚Üî continuous_within_at (e ‚àò f) s x :=
begin
  refine ‚ü®(e.continuous_at hx).tendsto.comp, Œª fe_cont, _‚ü©,
  rw [‚Üê continuous_within_at_inter' h] at fe_cont ‚ä¢,
  have : continuous_within_at (e.symm ‚àò (e ‚àò f)) (s ‚à© f ‚Åª¬π' e.source) x,
  { have : continuous_within_at e.symm univ (e (f x))
      := (e.continuous_at_symm (e.map_source hx)).continuous_within_at,
    exact continuous_within_at.comp this fe_cont (subset_univ _) },
  exact this.congr (Œªy hy, by simp [e.left_inv hy.2]) (by simp [e.left_inv hx])
end

/-- Continuity at a point can be read under left composition with a local homeomorphism if a
neighborhood of the initial point is sent to the source of the local homeomorphism-/
lemma continuous_at_iff_continuous_at_comp_left {f : Œ≥ ‚Üí Œ±} {x : Œ≥} (h : f ‚Åª¬π' e.source ‚àà ùìù x) :
  continuous_at f x ‚Üî continuous_at (e ‚àò f) x :=
begin
  have hx : f x ‚àà e.source := (mem_of_mem_nhds h : _),
  have h' : f ‚Åª¬π' e.source ‚àà ùìù[univ] x, by rwa nhds_within_univ,
  rw [‚Üê continuous_within_at_univ, ‚Üê continuous_within_at_univ,
      e.continuous_within_at_iff_continuous_within_at_comp_left hx h']
end

/-- A function is continuous on a set if and only if its composition with a local homeomorphism
on the left is continuous on the corresponding set. -/
lemma continuous_on_iff_continuous_on_comp_left {f : Œ≥ ‚Üí Œ±} {s : set Œ≥} (h : s ‚äÜ f ‚Åª¬π' e.source) :
  continuous_on f s ‚Üî continuous_on (e ‚àò f) s :=
forall_congr $ Œª x, forall_congr $ Œª hx, e.continuous_within_at_iff_continuous_within_at_comp_left
  (h hx) (mem_of_superset self_mem_nhds_within h)

end continuity

/-- A local homeomrphism defines a homeomorphism between its source and target. -/
def to_homeomorph_source_target : e.source ‚âÉ‚Çú e.target :=
{ to_fun := e.maps_to.restrict _ _ _,
  inv_fun := e.symm_maps_to.restrict _ _ _,
  left_inv := Œª x, subtype.eq $ e.left_inv x.2,
  right_inv := Œª x, subtype.eq $ e.right_inv x.2,
  continuous_to_fun := continuous_subtype_mk _ $
    continuous_on_iff_continuous_restrict.1 e.continuous_on,
  continuous_inv_fun := continuous_subtype_mk _ $
    continuous_on_iff_continuous_restrict.1 e.symm.continuous_on }

lemma second_countable_topology_source [second_countable_topology Œ≤]
  (e : local_homeomorph Œ± Œ≤) :
  second_countable_topology e.source :=
e.to_homeomorph_source_target.second_countable_topology

/-- If a local homeomorphism has source and target equal to univ, then it induces a homeomorphism
between the whole spaces, expressed in this definition. -/
@[simps apply symm_apply (mfld_cfg)]
def to_homeomorph_of_source_eq_univ_target_eq_univ (h : e.source = (univ : set Œ±))
  (h' : e.target = univ) : Œ± ‚âÉ‚Çú Œ≤ :=
{ to_fun := e,
  inv_fun := e.symm,
  left_inv := Œªx, e.left_inv $ by { rw h, exact mem_univ _ },
  right_inv := Œªx, e.right_inv $ by { rw h', exact mem_univ _ },
  continuous_to_fun := begin
    rw [continuous_iff_continuous_on_univ],
    convert e.continuous_to_fun,
    rw h
  end,
  continuous_inv_fun := begin
    rw [continuous_iff_continuous_on_univ],
    convert e.continuous_inv_fun,
    rw h'
  end }

/-- A local homeomorphism whose source is all of `Œ±` defines an open embedding of `Œ±` into `Œ≤`.  The
converse is also true; see `open_embedding.to_local_homeomorph`. -/
lemma to_open_embedding (h : e.source = set.univ) : open_embedding e :=
begin
  apply open_embedding_of_continuous_injective_open,
  { apply continuous_iff_continuous_on_univ.mpr,
    rw ‚Üê h,
    exact e.continuous_to_fun },
  { apply set.injective_iff_inj_on_univ.mpr,
    rw ‚Üê h,
    exact e.inj_on },
  { intros U hU,
    simpa only [h, subset_univ] with mfld_simps using e.image_open_of_open hU}
end

end local_homeomorph

namespace homeomorph
variables (e : Œ± ‚âÉ‚Çú Œ≤) (e' : Œ≤ ‚âÉ‚Çú Œ≥)
/- Register as simp lemmas that the fields of a local homeomorphism built from a homeomorphism
correspond to the fields of the original homeomorphism. -/

attribute [simps apply source target {simp_rhs := tt, .. mfld_cfg}] to_local_homeomorph

@[simp, mfld_simps] lemma to_local_homeomorph_coe_symm :
  (e.to_local_homeomorph.symm : Œ≤ ‚Üí Œ±) = e.symm := rfl
@[simp, mfld_simps] lemma refl_to_local_homeomorph :
  (homeomorph.refl Œ±).to_local_homeomorph = local_homeomorph.refl Œ± := rfl
@[simp, mfld_simps] lemma symm_to_local_homeomorph :
  e.symm.to_local_homeomorph = e.to_local_homeomorph.symm := rfl
@[simp, mfld_simps] lemma trans_to_local_homeomorph :
  (e.trans e').to_local_homeomorph = e.to_local_homeomorph.trans e'.to_local_homeomorph :=
local_homeomorph.eq_of_local_equiv_eq $ equiv.trans_to_local_equiv _ _

end homeomorph

namespace open_embedding
variables (f : Œ± ‚Üí Œ≤) (h : open_embedding f)

/-- An open embedding of `Œ±` into `Œ≤`, with `Œ±` nonempty, defines a local homeomorphism whose source
is all of `Œ±`.  The converse is also true; see `local_homeomorph.to_open_embedding`. -/
@[simps apply source target (mfld_cfg)]
noncomputable def to_local_homeomorph [nonempty Œ±] : local_homeomorph Œ± Œ≤ :=
local_homeomorph.of_continuous_open
  ((h.to_embedding.inj.inj_on univ).to_local_equiv _ _)
  h.continuous.continuous_on h.is_open_map is_open_univ

lemma continuous_at_iff
  {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (hf : open_embedding f) {x : Œ±} :
  continuous_at (g ‚àò f) x ‚Üî continuous_at g (f x) :=
begin
  haveI : nonempty Œ± := ‚ü®x‚ü©,
  convert (((hf.to_local_homeomorph f).continuous_at_iff_continuous_at_comp_right) _).symm,
  { apply (local_homeomorph.left_inv _ _).symm,
    simp, },
  { simp, },
end

end open_embedding

namespace topological_space.opens

open topological_space
variables (s : opens Œ±) [nonempty s]

/-- The inclusion of an open subset `s` of a space `Œ±` into `Œ±` is a local homeomorphism from the
subtype `s` to `Œ±`. -/
noncomputable def local_homeomorph_subtype_coe : local_homeomorph s Œ± :=
open_embedding.to_local_homeomorph _ s.2.open_embedding_subtype_coe

@[simp, mfld_simps] lemma local_homeomorph_subtype_coe_coe :
  (s.local_homeomorph_subtype_coe : s ‚Üí Œ±) = coe := rfl

@[simp, mfld_simps] lemma local_homeomorph_subtype_coe_source :
  s.local_homeomorph_subtype_coe.source = set.univ := rfl

@[simp, mfld_simps] lemma local_homeomorph_subtype_coe_target :
  s.local_homeomorph_subtype_coe.target = s :=
by { simp only [local_homeomorph_subtype_coe, subtype.range_coe_subtype] with mfld_simps, refl }

end topological_space.opens

namespace local_homeomorph

open topological_space
variables (e : local_homeomorph Œ± Œ≤)
variables (s : opens Œ±) [nonempty s]

/-- The restriction of a local homeomorphism `e` to an open subset `s` of the domain type produces a
local homeomorphism whose domain is the subtype `s`.-/
noncomputable def subtype_restr : local_homeomorph s Œ≤ := s.local_homeomorph_subtype_coe.trans e

lemma subtype_restr_def : e.subtype_restr s = s.local_homeomorph_subtype_coe.trans e := rfl

@[simp, mfld_simps] lemma subtype_restr_coe : ((e.subtype_restr s : local_homeomorph s Œ≤) : s ‚Üí Œ≤)
  = set.restrict (e : Œ± ‚Üí Œ≤) s := rfl

@[simp, mfld_simps] lemma subtype_restr_source : (e.subtype_restr s).source = coe ‚Åª¬π' e.source :=
by simp only [subtype_restr_def] with mfld_simps

/- This lemma characterizes the transition functions of an open subset in terms of the transition
functions of the original space. -/
lemma subtype_restr_symm_trans_subtype_restr (f f' : local_homeomorph Œ± Œ≤) :
  (f.subtype_restr s).symm.trans (f'.subtype_restr s)
  ‚âà (f.symm.trans f').restr (f.target ‚à© (f.symm) ‚Åª¬π' s) :=
begin
  simp only [subtype_restr_def, trans_symm_eq_symm_trans_symm],
  have openness‚ÇÅ : is_open (f.target ‚à© f.symm ‚Åª¬π' s) := f.preimage_open_of_open_symm s.2,
  rw [‚Üê of_set_trans _ openness‚ÇÅ, ‚Üê trans_assoc, ‚Üê trans_assoc],
  refine eq_on_source.trans' _ (eq_on_source_refl _),
  -- f' has been eliminated !!!
  have sets_identity : f.symm.source ‚à© (f.target ‚à© (f.symm) ‚Åª¬π' s) = f.symm.source ‚à© f.symm ‚Åª¬π' s,
  { mfld_set_tac },
  have openness‚ÇÇ : is_open (s : set Œ±) := s.2,
  rw [of_set_trans', sets_identity, ‚Üê trans_of_set' _ openness‚ÇÇ, trans_assoc],
  refine eq_on_source.trans' (eq_on_source_refl _) _,
  -- f has been eliminated !!!
  refine setoid.trans (trans_symm_self s.local_homeomorph_subtype_coe) _,
  simp only with mfld_simps,
end

end local_homeomorph
