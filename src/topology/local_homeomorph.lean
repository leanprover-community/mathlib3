/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import data.equiv.local_equiv
import topology.homeomorph
import topology.opens

/-!
# Local homeomorphisms

This file defines homeomorphisms between open subsets of topological spaces. An element `e` of
`local_homeomorph Œ± Œ≤` is an extension of `local_equiv Œ± Œ≤`, i.e., it is a pair of functions
`e.to_fun` and `e.inv_fun`, inverse of each other on the sets `e.source` and `e.target`.
Additionally, we require that these sets are open, and that the functions are continuous on them.
Equivalently, they are homeomorphisms there.

As in equivs, we register a coercion to functions, and we use `e x` and `e.symm x` throughout
instead of `e.to_fun x` and `e.inv_fun x`.

## Main definitions

`homeomorph.to_local_homeomorph`: associating a local homeomorphism to a homeomorphism, with
                                  source = target = univ
`local_homeomorph.symm`  : the inverse of a local homeomorphism
`local_homeomorph.trans` : the composition of two local homeomorphisms
`local_homeomorph.refl`  : the identity local homeomorphism
`local_homeomorph.of_set`: the identity on a set `s`
`eq_on_source`           : equivalence relation describing the "right" notion of equality for local
                           homeomorphisms

## Implementation notes

Most statements are copied from their local_equiv versions, although some care is required
especially when restricting to subsets, as these should be open subsets.

For design notes, see `local_equiv.lean`.
-/

open function set
open_locale topological_space

variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {Œ¥ : Type*}
[topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥] [topological_space Œ¥]

/-- local homeomorphisms, defined on open subsets of the space -/
@[nolint has_inhabited_instance]
structure local_homeomorph (Œ± : Type*) (Œ≤ : Type*) [topological_space Œ±] [topological_space Œ≤]
  extends local_equiv Œ± Œ≤ :=
(open_source        : is_open source)
(open_target        : is_open target)
(continuous_to_fun  : continuous_on to_fun source)
(continuous_inv_fun : continuous_on inv_fun target)

/-- A homeomorphism induces a local homeomorphism on the whole space -/
def homeomorph.to_local_homeomorph (e : homeomorph Œ± Œ≤) :
  local_homeomorph Œ± Œ≤ :=
{ open_source        := is_open_univ,
  open_target        := is_open_univ,
  continuous_to_fun  := by { erw ‚Üê continuous_iff_continuous_on_univ, exact e.continuous_to_fun },
  continuous_inv_fun := by { erw ‚Üê continuous_iff_continuous_on_univ, exact e.continuous_inv_fun },
  ..e.to_equiv.to_local_equiv }

namespace local_homeomorph

variables (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≤ Œ≥)

instance : has_coe_to_fun (local_homeomorph Œ± Œ≤) := ‚ü®_, Œª e, e.to_local_equiv.to_fun‚ü©

/-- The inverse of a local homeomorphism -/
protected def symm : local_homeomorph Œ≤ Œ± :=
{ open_source        := e.open_target,
  open_target        := e.open_source,
  continuous_to_fun  := e.continuous_inv_fun,
  continuous_inv_fun := e.continuous_to_fun,
  ..e.to_local_equiv.symm }

protected lemma continuous_on : continuous_on e e.source := e.continuous_to_fun

lemma continuous_on_symm : continuous_on e.symm e.target := e.continuous_inv_fun

@[simp, mfld_simps] lemma mk_coe (e : local_equiv Œ± Œ≤) (a b c d) :
  (local_homeomorph.mk e a b c d : Œ± ‚Üí Œ≤) = e := rfl

@[simp, mfld_simps] lemma mk_coe_symm (e : local_equiv Œ± Œ≤) (a b c d) :
  ((local_homeomorph.mk e a b c d).symm : Œ≤ ‚Üí Œ±) = e.symm := rfl

/- Register a few simp lemmas to make sure that `simp` puts the application of a local
homeomorphism in its normal form, i.e., in terms of its coercion to a function. -/

@[simp, mfld_simps] lemma to_fun_eq_coe (e : local_homeomorph Œ± Œ≤) : e.to_fun = e := rfl

@[simp, mfld_simps] lemma inv_fun_eq_coe (e : local_homeomorph Œ± Œ≤) : e.inv_fun = e.symm := rfl

@[simp, mfld_simps] lemma coe_coe : (e.to_local_equiv : Œ± ‚Üí Œ≤) = e := rfl

@[simp, mfld_simps] lemma coe_coe_symm : (e.to_local_equiv.symm : Œ≤ ‚Üí Œ±) = e.symm := rfl

@[simp, mfld_simps] lemma map_source {x : Œ±} (h : x ‚àà e.source) : e x ‚àà e.target :=
e.map_source' h

@[simp, mfld_simps] lemma map_target {x : Œ≤} (h : x ‚àà e.target) : e.symm x ‚àà e.source :=
e.map_target' h

@[simp, mfld_simps] lemma left_inv {x : Œ±} (h : x ‚àà e.source) : e.symm (e x) = x :=
e.left_inv' h

@[simp, mfld_simps] lemma right_inv {x : Œ≤} (h : x ‚àà e.target) : e (e.symm x) = x :=
e.right_inv' h

lemma source_preimage_target : e.source ‚äÜ e ‚Åª¬π' e.target := Œª _ h, map_source e h

lemma eq_of_local_equiv_eq {e e' : local_homeomorph Œ± Œ≤}
  (h : e.to_local_equiv = e'.to_local_equiv) : e = e' :=
begin
  cases e, cases e',
  dsimp at *,
  induction h,
  refl
end

lemma eventually_left_inverse (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.source) :
  ‚àÄ·∂† y in ùìù x, e.symm (e y) = y :=
filter.eventually.mono (mem_nhds_sets e.open_source hx) e.left_inv'

lemma eventually_left_inverse' (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.target) :
  ‚àÄ·∂† y in ùìù (e.symm x), e.symm (e y) = y :=
e.eventually_left_inverse (e.map_target hx)

lemma eventually_right_inverse (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.target) :
  ‚àÄ·∂† y in ùìù x, e (e.symm y) = y :=
filter.eventually.mono (mem_nhds_sets e.open_target hx) e.right_inv'

lemma eventually_right_inverse' (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.source) :
  ‚àÄ·∂† y in ùìù (e x), e (e.symm y) = y :=
e.eventually_right_inverse (e.map_source hx)

lemma image_eq_target_inter_inv_preimage {s : set Œ±} (h : s ‚äÜ e.source) :
  e '' s = e.target ‚à© e.symm ‚Åª¬π' s :=
e.to_local_equiv.image_eq_target_inter_inv_preimage h

lemma image_inter_source_eq (s : set Œ±) :
  e '' (s ‚à© e.source) = e.target ‚à© e.symm ‚Åª¬π' (s ‚à© e.source) :=
e.image_eq_target_inter_inv_preimage (inter_subset_right _ _)

lemma symm_image_eq_source_inter_preimage {s : set Œ≤} (h : s ‚äÜ e.target) :
  e.symm '' s = e.source ‚à© e ‚Åª¬π' s :=
e.symm.image_eq_target_inter_inv_preimage h

lemma symm_image_inter_target_eq (s : set Œ≤) :
  e.symm '' (s ‚à© e.target) = e.source ‚à© e ‚Åª¬π' (s ‚à© e.target) :=
e.symm.image_inter_source_eq _

/-- Two local homeomorphisms are equal when they have equal `to_fun`, `inv_fun` and `source`.
It is not sufficient to have equal `to_fun` and `source`, as this only determines `inv_fun` on
the target. This would only be true for a weaker notion of equality, arguably the right one,
called `eq_on_source`. -/
@[ext]
protected lemma ext (e' : local_homeomorph Œ± Œ≤) (h : ‚àÄx, e x = e' x)
  (hinv : ‚àÄx, e.symm x = e'.symm x) (hs : e.source = e'.source) : e = e' :=
eq_of_local_equiv_eq (local_equiv.ext h hinv hs)

@[simp, mfld_simps] lemma symm_to_local_equiv : e.symm.to_local_equiv = e.to_local_equiv.symm := rfl
-- The following lemmas are already simp via local_equiv
lemma symm_source : e.symm.source = e.target := rfl
lemma symm_target : e.symm.target = e.source := rfl
@[simp, mfld_simps] lemma symm_symm : e.symm.symm = e := eq_of_local_equiv_eq $ by simp

/-- A local homeomorphism is continuous at any point of its source -/
protected lemma continuous_at {x : Œ±} (h : x ‚àà e.source) : continuous_at e x :=
(e.continuous_on x h).continuous_at (mem_nhds_sets e.open_source h)

/-- A local homeomorphism inverse is continuous at any point of its target -/
lemma continuous_at_symm {x : Œ≤} (h : x ‚àà e.target) : continuous_at e.symm x :=
e.symm.continuous_at h

lemma tendsto_symm (e : local_homeomorph Œ± Œ≤) {x} (hx : x ‚àà e.source) :
  filter.tendsto e.symm (ùìù (e x)) (ùìù x) :=
by simpa only [continuous_at, e.left_inv hx] using e.continuous_at_symm (e.map_source hx)

/-- Preimage of interior or interior of preimage coincide for local homeomorphisms, when restricted
to the source. -/
lemma preimage_interior (s : set Œ≤) :
  e.source ‚à© e ‚Åª¬π' (interior s) = e.source ‚à© interior (e ‚Åª¬π' s) :=
begin
  apply subset.antisymm,
  { exact e.continuous_on.preimage_interior_subset_interior_preimage e.open_source },
  { calc e.source ‚à© interior (e ‚Åª¬π' s)
        = (e.source ‚à© interior (e ‚Åª¬π' s)) ‚à© (e ‚Åª¬π' e.target) : by mfld_set_tac
    ... = (e.source ‚à© e ‚Åª¬π' (e.symm ‚Åª¬π' (interior (e ‚Åª¬π' s)))) ‚à© (e ‚Åª¬π' e.target) :
      begin
        have := e.to_local_equiv.source_inter_preimage_inv_preimage _,
        simp only [coe_coe_symm, coe_coe] at this,
        rw this
      end
    ... = e.source ‚à© e ‚Åª¬π' (e.target ‚à© e.symm ‚Åª¬π' (interior (e ‚Åª¬π' s))) :
       by rw [inter_comm e.target, preimage_inter, inter_assoc]
    ... ‚äÜ e.source ‚à© e ‚Åª¬π' (e.target ‚à© interior (e.symm ‚Åª¬π' (e ‚Åª¬π' s))) : begin
        apply inter_subset_inter (subset.refl _) (preimage_mono _),
        exact e.continuous_on_symm.preimage_interior_subset_interior_preimage e.open_target
      end
    ... = e.source ‚à© e ‚Åª¬π' (interior (e.target ‚à© e.symm ‚Åª¬π' (e ‚Åª¬π' s))) :
      by rw [interior_inter, interior_eq_of_open e.open_target]
    ... = e.source ‚à© e ‚Åª¬π' (interior (e.target ‚à© s)) :
      begin
        have := e.to_local_equiv.target_inter_inv_preimage_preimage,
        simp only [coe_coe_symm, coe_coe] at this,
        rw this
      end
    ... = e.source ‚à© e ‚Åª¬π' e.target ‚à© e ‚Åª¬π' (interior s) :
      by rw [interior_inter, preimage_inter, interior_eq_of_open e.open_target, inter_assoc]
    ... = e.source ‚à© e ‚Åª¬π' (interior s) : by mfld_set_tac }
end

lemma preimage_open_of_open {s : set Œ≤} (hs : is_open s) : is_open (e.source ‚à© e ‚Åª¬π' s) :=
e.continuous_on.preimage_open_of_open e.open_source hs

lemma preimage_open_of_open_symm {s : set Œ±} (hs : is_open s) : is_open (e.target ‚à© e.symm ‚Åª¬π' s) :=
e.symm.continuous_on.preimage_open_of_open e.open_target hs

/-- The image of an open set in the source is open. -/
lemma image_open_of_open {s : set Œ±} (hs : is_open s) (h : s ‚äÜ e.source) : is_open (e '' s) :=
begin
  have : e '' s = e.target ‚à© e.symm ‚Åª¬π' s :=
    e.to_local_equiv.image_eq_target_inter_inv_preimage h,
  rw this,
  exact e.continuous_on_symm.preimage_open_of_open e.open_target hs
end

/-- The image of the restriction of an open set to the source is open. -/
lemma image_open_of_open' {s : set Œ±} (hs : is_open s) : is_open (e '' (s ‚à© e.source)) :=
begin
  refine image_open_of_open _ (is_open_inter hs e.open_source) _,
  simp,
end

/-- Restricting a local homeomorphism `e` to `e.source ‚à© s` when `s` is open. This is sometimes hard
to use because of the openness assumption, but it has the advantage that when it can
be used then its local_equiv is defeq to local_equiv.restr -/
protected def restr_open (s : set Œ±) (hs : is_open s) :
  local_homeomorph Œ± Œ≤ :=
{ open_source := is_open_inter e.open_source hs,
  open_target := (continuous_on_open_iff e.open_target).1 e.continuous_inv_fun s hs,
  continuous_to_fun  := e.continuous_to_fun.mono (inter_subset_left _ _),
  continuous_inv_fun := e.continuous_inv_fun.mono (inter_subset_left _ _),
  ..e.to_local_equiv.restr s}

@[simp, mfld_simps] lemma restr_open_to_local_equiv (s : set Œ±) (hs : is_open s) :
  (e.restr_open s hs).to_local_equiv = e.to_local_equiv.restr s := rfl

-- Already simp via local_equiv
lemma restr_open_source (s : set Œ±) (hs : is_open s) :
  (e.restr_open s hs).source = e.source ‚à© s := rfl

/-- Restricting a local homeomorphism `e` to `e.source ‚à© interior s`. We use the interior to make
sure that the restriction is well defined whatever the set s, since local homeomorphisms are by
definition defined on open sets. In applications where `s` is open, this coincides with the
restriction of local equivalences -/
protected def restr (s : set Œ±) : local_homeomorph Œ± Œ≤ :=
e.restr_open (interior s) is_open_interior

@[simp, mfld_simps] lemma restr_to_local_equiv (s : set Œ±) :
  (e.restr s).to_local_equiv = (e.to_local_equiv).restr (interior s) := rfl
@[simp, mfld_simps] lemma restr_coe (s : set Œ±) : (e.restr s : Œ± ‚Üí Œ≤) = e := rfl
@[simp, mfld_simps] lemma restr_coe_symm (s : set Œ±) : ((e.restr s).symm : Œ≤ ‚Üí Œ±) = e.symm := rfl
lemma restr_source (s : set Œ±)  : (e.restr s).source = e.source ‚à© interior s := rfl
lemma restr_target (s : set Œ±) :
  (e.restr s).target = e.target ‚à© e.symm ‚Åª¬π' (interior s) := rfl

lemma restr_source' (s : set Œ±) (hs : is_open s) : (e.restr s).source = e.source ‚à© s :=
by rw [e.restr_source, interior_eq_of_open hs]

lemma restr_to_local_equiv' (s : set Œ±) (hs : is_open s):
  (e.restr s).to_local_equiv = e.to_local_equiv.restr s :=
by rw [e.restr_to_local_equiv, interior_eq_of_open hs]

lemma restr_eq_of_source_subset {e : local_homeomorph Œ± Œ≤} {s : set Œ±} (h : e.source ‚äÜ s) :
  e.restr s = e :=
begin
  apply eq_of_local_equiv_eq,
  rw restr_to_local_equiv,
  apply local_equiv.restr_eq_of_source_subset,
  have := interior_mono h,
  rwa interior_eq_of_open (e.open_source) at this
end

@[simp, mfld_simps] lemma restr_univ {e : local_homeomorph Œ± Œ≤} : e.restr univ = e :=
restr_eq_of_source_subset (subset_univ _)

lemma restr_source_inter (s : set Œ±) : e.restr (e.source ‚à© s) = e.restr s :=
begin
  refine local_homeomorph.ext _ _ (Œªx, rfl) (Œªx, rfl) _,
  simp [interior_eq_of_open e.open_source],
  rw [‚Üê inter_assoc, inter_self]
end

/-- The identity on the whole space as a local homeomorphism. -/
protected def refl (Œ± : Type*) [topological_space Œ±] : local_homeomorph Œ± Œ± :=
(homeomorph.refl Œ±).to_local_homeomorph

@[simp, mfld_simps] lemma refl_local_equiv :
  (local_homeomorph.refl Œ±).to_local_equiv = local_equiv.refl Œ± := rfl
lemma refl_source : (local_homeomorph.refl Œ±).source = univ := rfl
lemma refl_target : (local_homeomorph.refl Œ±).target = univ := rfl
@[simp, mfld_simps] lemma refl_symm : (local_homeomorph.refl Œ±).symm = local_homeomorph.refl Œ± :=
rfl
@[simp, mfld_simps] lemma refl_coe : (local_homeomorph.refl Œ± : Œ± ‚Üí Œ±) = id := rfl

section
variables {s : set Œ±} (hs : is_open s)

/-- The identity local equiv on a set `s` -/
def of_set (s : set Œ±) (hs : is_open s) : local_homeomorph Œ± Œ± :=
{ open_source        := hs,
  open_target        := hs,
  continuous_to_fun  := continuous_id.continuous_on,
  continuous_inv_fun := continuous_id.continuous_on,
  ..local_equiv.of_set s }

@[simp, mfld_simps] lemma of_set_to_local_equiv :
  (of_set s hs).to_local_equiv = local_equiv.of_set s := rfl
lemma of_set_source : (of_set s hs).source = s := rfl
lemma of_set_target : (of_set s hs).target = s := rfl
@[simp, mfld_simps] lemma of_set_coe : (of_set s hs : Œ± ‚Üí Œ±) = id := rfl
@[simp, mfld_simps] lemma of_set_symm : (of_set s hs).symm = of_set s hs := rfl

@[simp, mfld_simps] lemma of_set_univ_eq_refl :
  of_set univ is_open_univ = local_homeomorph.refl Œ± :=
by ext; simp

end

/-- Composition of two local homeomorphisms when the target of the first and the source of
the second coincide. -/
protected def trans' (h : e.target = e'.source) : local_homeomorph Œ± Œ≥ :=
{ open_source       := e.open_source,
  open_target       := e'.open_target,
  continuous_to_fun := begin
    apply continuous_on.comp e'.continuous_to_fun e.continuous_to_fun,
    rw ‚Üê h,
    exact e.to_local_equiv.source_subset_preimage_target
  end,
  continuous_inv_fun := begin
    apply continuous_on.comp e.continuous_inv_fun e'.continuous_inv_fun,
    rw h,
    exact e'.to_local_equiv.target_subset_preimage_source
  end,
  ..local_equiv.trans' e.to_local_equiv e'.to_local_equiv h }

/-- Composing two local homeomorphisms, by restricting to the maximal domain where their
composition is well defined. -/
protected def trans : local_homeomorph Œ± Œ≥ :=
  local_homeomorph.trans' (e.symm.restr_open e'.source e'.open_source).symm
    (e'.restr_open e.target e.open_target) (by simp [inter_comm])

@[simp, mfld_simps] lemma trans_to_local_equiv :
  (e.trans e').to_local_equiv = e.to_local_equiv.trans e'.to_local_equiv := rfl
@[simp, mfld_simps] lemma coe_trans : (e.trans e' : Œ± ‚Üí Œ≥) = e' ‚àò e := rfl
@[simp, mfld_simps] lemma coe_trans_symm : ((e.trans e').symm : Œ≥ ‚Üí Œ±) = e.symm ‚àò e'.symm := rfl

lemma trans_symm_eq_symm_trans_symm : (e.trans e').symm = e'.symm.trans e.symm :=
by cases e; cases e'; refl

/- This could be considered as a simp lemma, but there are many situations where it makes something
simple into something more complicated. -/
lemma trans_source : (e.trans e').source = e.source ‚à© e ‚Åª¬π' e'.source :=
local_equiv.trans_source e.to_local_equiv e'.to_local_equiv

lemma trans_source' : (e.trans e').source = e.source ‚à© e ‚Åª¬π' (e.target ‚à© e'.source) :=
local_equiv.trans_source' e.to_local_equiv e'.to_local_equiv

lemma trans_source'' : (e.trans e').source = e.symm '' (e.target ‚à© e'.source) :=
local_equiv.trans_source'' e.to_local_equiv e'.to_local_equiv

lemma image_trans_source : e '' (e.trans e').source = e.target ‚à© e'.source :=
local_equiv.image_trans_source e.to_local_equiv e'.to_local_equiv

lemma trans_target : (e.trans e').target = e'.target ‚à© e'.symm ‚Åª¬π' e.target := rfl

lemma trans_target' : (e.trans e').target = e'.target ‚à© e'.symm ‚Åª¬π' (e'.source ‚à© e.target) :=
trans_source' e'.symm e.symm

lemma trans_target'' : (e.trans e').target = e' '' (e'.source ‚à© e.target) :=
trans_source'' e'.symm e.symm

lemma inv_image_trans_target : e'.symm '' (e.trans e').target = e'.source ‚à© e.target :=
image_trans_source e'.symm e.symm

lemma trans_assoc (e'' : local_homeomorph Œ≥ Œ¥) :
  (e.trans e').trans e'' = e.trans (e'.trans e'') :=
eq_of_local_equiv_eq $ local_equiv.trans_assoc e.to_local_equiv e'.to_local_equiv e''.to_local_equiv

@[simp, mfld_simps] lemma trans_refl : e.trans (local_homeomorph.refl Œ≤) = e :=
eq_of_local_equiv_eq $ local_equiv.trans_refl e.to_local_equiv

@[simp, mfld_simps] lemma refl_trans : (local_homeomorph.refl Œ±).trans e = e :=
eq_of_local_equiv_eq $ local_equiv.refl_trans e.to_local_equiv

lemma trans_of_set {s : set Œ≤} (hs : is_open s) :
  e.trans (of_set s hs) = e.restr (e ‚Åª¬π' s) :=
local_homeomorph.ext _ _ (Œªx, rfl) (Œªx, rfl) $
  by simp [local_equiv.trans_source, (e.preimage_interior _).symm, interior_eq_of_open hs]

lemma trans_of_set' {s : set Œ≤} (hs : is_open s) :
  e.trans (of_set s hs) = e.restr (e.source ‚à© e ‚Åª¬π' s) :=
by rw [trans_of_set, restr_source_inter]

lemma of_set_trans {s : set Œ±} (hs : is_open s) :
  (of_set s hs).trans e = e.restr s :=
local_homeomorph.ext _ _ (Œªx, rfl) (Œªx, rfl) $
  by simp [local_equiv.trans_source, interior_eq_of_open hs, inter_comm]

lemma of_set_trans' {s : set Œ±} (hs : is_open s) :
  (of_set s hs).trans e = e.restr (e.source ‚à© s) :=
by rw [of_set_trans, restr_source_inter]

@[simp, mfld_simps] lemma of_set_trans_of_set
  {s : set Œ±} (hs : is_open s) {s' : set Œ±} (hs' : is_open s') :
  (of_set s hs).trans (of_set s' hs') = of_set (s ‚à© s') (is_open_inter hs hs')  :=
begin
  rw (of_set s hs).trans_of_set hs',
  ext; simp [interior_eq_of_open hs']
end

lemma restr_trans (s : set Œ±) :
  (e.restr s).trans e' = (e.trans e').restr s :=
eq_of_local_equiv_eq $ local_equiv.restr_trans e.to_local_equiv e'.to_local_equiv (interior s)

/-- `eq_on_source e e'` means that `e` and `e'` have the same source, and coincide there. They
should really be considered the same local equiv. -/
def eq_on_source (e e' : local_homeomorph Œ± Œ≤) : Prop :=
e.source = e'.source ‚àß (eq_on e e' e.source)

lemma eq_on_source_iff (e e' : local_homeomorph Œ± Œ≤) :
eq_on_source e e' ‚Üî local_equiv.eq_on_source e.to_local_equiv e'.to_local_equiv :=
iff.rfl

/-- `eq_on_source` is an equivalence relation -/
instance : setoid (local_homeomorph Œ± Œ≤) :=
{ r     := eq_on_source,
  iseqv := ‚ü®
    Œªe, (@local_equiv.eq_on_source_setoid Œ± Œ≤).iseqv.1 e.to_local_equiv,
    Œªe e' h, (@local_equiv.eq_on_source_setoid Œ± Œ≤).iseqv.2.1 ((eq_on_source_iff e e').1 h),
    Œªe e' e'' h h', (@local_equiv.eq_on_source_setoid Œ± Œ≤).iseqv.2.2
      ((eq_on_source_iff e e').1 h) ((eq_on_source_iff e' e'').1 h')‚ü© }

lemma eq_on_source_refl : e ‚âà e := setoid.refl _

/-- If two local homeomorphisms are equivalent, so are their inverses -/
lemma eq_on_source.symm' {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') : e.symm ‚âà e'.symm :=
local_equiv.eq_on_source.symm' h

/-- Two equivalent local homeomorphisms have the same source -/
lemma eq_on_source.source_eq {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') : e.source = e'.source :=
h.1

/-- Two equivalent local homeomorphisms have the same target -/
lemma eq_on_source.target_eq {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') : e.target = e'.target :=
h.symm'.1

/-- Two equivalent local homeomorphisms have coinciding `to_fun` on the source -/
lemma eq_on_source.eq_on {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') :
  eq_on e e' e.source :=
h.2

/-- Two equivalent local homeomorphisms have coinciding `inv_fun` on the target -/
lemma eq_on_source.symm_eq_on_target {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e') :
  eq_on e.symm e'.symm e.target :=
h.symm'.2

/-- Composition of local homeomorphisms respects equivalence -/
lemma eq_on_source.trans' {e e' : local_homeomorph Œ± Œ≤} {f f' : local_homeomorph Œ≤ Œ≥}
  (he : e ‚âà e') (hf : f ‚âà f') : e.trans f ‚âà e'.trans f' :=
local_equiv.eq_on_source.trans' he hf

/-- Restriction of local homeomorphisms respects equivalence -/
lemma eq_on_source.restr {e e' : local_homeomorph Œ± Œ≤} (he : e ‚âà e') (s : set Œ±) :
  e.restr s ‚âà e'.restr s :=
local_equiv.eq_on_source.restr he _

/-- Composition of a local homeomorphism and its inverse is equivalent to the restriction of the
identity to the source -/
lemma trans_self_symm :
  e.trans e.symm ‚âà local_homeomorph.of_set e.source e.open_source :=
local_equiv.trans_self_symm _

lemma trans_symm_self :
  e.symm.trans e ‚âà local_homeomorph.of_set e.target e.open_target :=
e.symm.trans_self_symm

lemma eq_of_eq_on_source_univ {e e' : local_homeomorph Œ± Œ≤} (h : e ‚âà e')
  (s : e.source = univ) (t : e.target = univ) : e = e' :=
eq_of_local_equiv_eq $ local_equiv.eq_of_eq_on_source_univ _ _ h s t

section prod

/-- The product of two local homeomorphisms, as a local homeomorphism on the product space. -/
def prod (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) : local_homeomorph (Œ± √ó Œ≥) (Œ≤ √ó Œ¥) :=
{ open_source := is_open_prod e.open_source e'.open_source,
  open_target := is_open_prod e.open_target e'.open_target,
  continuous_to_fun := continuous_on.prod
    (e.continuous_to_fun.comp continuous_fst.continuous_on (prod_subset_preimage_fst _ _))
    (e'.continuous_to_fun.comp continuous_snd.continuous_on (prod_subset_preimage_snd _ _)),
  continuous_inv_fun := continuous_on.prod
    (e.continuous_inv_fun.comp continuous_fst.continuous_on (prod_subset_preimage_fst _ _))
    (e'.continuous_inv_fun.comp continuous_snd.continuous_on (prod_subset_preimage_snd _ _)),
  ..e.to_local_equiv.prod e'.to_local_equiv }

@[simp, mfld_simps] lemma prod_to_local_equiv (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) :
  (e.prod e').to_local_equiv = e.to_local_equiv.prod e'.to_local_equiv := rfl

lemma prod_source (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) :
  (e.prod e').source = set.prod e.source e'.source := rfl

lemma prod_target (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) :
  (e.prod e').target = set.prod e.target e'.target := rfl

@[simp, mfld_simps] lemma prod_coe (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) :
  (e.prod e' : Œ± √ó Œ≥ ‚Üí Œ≤ √ó Œ¥) = Œªp, (e p.1, e' p.2) := rfl

lemma prod_coe_symm (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) :
  ((e.prod e').symm : Œ≤ √ó Œ¥ ‚Üí Œ± √ó Œ≥) = Œªp, (e.symm p.1, e'.symm p.2) := rfl

@[simp, mfld_simps] lemma prod_symm (e : local_homeomorph Œ± Œ≤) (e' : local_homeomorph Œ≥ Œ¥) :
  (e.prod e').symm = (e.symm.prod e'.symm) :=
by ext x; simp [prod_coe_symm]

@[simp, mfld_simps] lemma prod_trans
  {Œ∑ : Type*} {Œµ : Type*} [topological_space Œ∑] [topological_space Œµ]
  (e : local_homeomorph Œ± Œ≤) (f : local_homeomorph Œ≤ Œ≥)
  (e' : local_homeomorph Œ¥ Œ∑) (f' : local_homeomorph Œ∑ Œµ) :
  (e.prod e').trans (f.prod f') = (e.trans f).prod (e'.trans f') :=
by ext x; simp [ext_iff]; tauto

end prod

section continuity

/-- Continuity within a set at a point can be read under right composition with a local
homeomorphism, if the point is in its target -/
lemma continuous_within_at_iff_continuous_within_at_comp_right
  {f : Œ≤ ‚Üí Œ≥} {s : set Œ≤} {x : Œ≤} (h : x ‚àà e.target) :
  continuous_within_at f s x ‚Üî
  continuous_within_at (f ‚àò e) (e ‚Åª¬π' s) (e.symm x) :=
begin
  split,
  { assume f_cont,
    have : e (e.symm x) = x := e.right_inv h,
    rw ‚Üê this at f_cont,
    have : e.source ‚àà ùìù (e.symm x) := mem_nhds_sets e.open_source (e.map_target h),
    rw [‚Üê continuous_within_at_inter this, inter_comm],
    exact continuous_within_at.comp f_cont
      ((e.continuous_at (e.map_target h)).continuous_within_at) (inter_subset_right _ _) },
  { assume fe_cont,
    have : continuous_within_at ((f ‚àò e) ‚àò e.symm) (s ‚à© e.target) x,
    { apply continuous_within_at.comp fe_cont,
      apply (e.continuous_at_symm h).continuous_within_at,
      assume x hx,
      simp [hx.1, hx.2, e.map_target] },
    have : continuous_within_at f (s ‚à© e.target) x :=
      continuous_within_at.congr this (Œªy hy, by simp [hy.2]) (by simp [h]),
    rwa continuous_within_at_inter at this,
    exact mem_nhds_sets e.open_target h }
end

/-- Continuity at a point can be read under right composition with a local homeomorphism, if the
point is in its target -/
lemma continuous_at_iff_continuous_at_comp_right
  {f : Œ≤ ‚Üí Œ≥} {x : Œ≤} (h : x ‚àà e.target) :
  continuous_at f x ‚Üî continuous_at (f ‚àò e) (e.symm x) :=
by rw [‚Üê continuous_within_at_univ, e.continuous_within_at_iff_continuous_within_at_comp_right h,
       preimage_univ, continuous_within_at_univ]

/-- A function is continuous on a set if and only if its composition with a local homeomorphism
on the right is continuous on the corresponding set. -/
lemma continuous_on_iff_continuous_on_comp_right {f : Œ≤ ‚Üí Œ≥} {s : set Œ≤} (h : s ‚äÜ e.target) :
  continuous_on f s ‚Üî continuous_on (f ‚àò e) (e.source ‚à© e ‚Åª¬π' s) :=
begin
  split,
  { assume f_cont x hx,
    have := e.continuous_within_at_iff_continuous_within_at_comp_right (e.map_source hx.1),
    rw e.left_inv hx.1 at this,
    have A := f_cont _ hx.2,
    rw this at A,
    exact A.mono (inter_subset_right _ _), },
  { assume fe_cont x hx,
    have := e.continuous_within_at_iff_continuous_within_at_comp_right (h hx),
    rw this,
    have : e.source ‚àà ùìù (e.symm x) := mem_nhds_sets e.open_source (e.map_target (h hx)),
    rw [‚Üê continuous_within_at_inter this, inter_comm],
    exact fe_cont _ (by simp [hx, h hx, e.map_target (h hx)]) }
end

/-- Continuity within a set at a point can be read under left composition with a local
homeomorphism if a neighborhood of the initial point is sent to the source of the local
homeomorphism-/
lemma continuous_within_at_iff_continuous_within_at_comp_left
  {f : Œ≥ ‚Üí Œ±} {s : set Œ≥} {x : Œ≥} (hx : f x ‚àà e.source) (h : f ‚Åª¬π' e.source ‚àà nhds_within x s) :
  continuous_within_at f s x ‚Üî continuous_within_at (e ‚àò f) s x :=
begin
  rw [‚Üê continuous_within_at_inter' h, ‚Üê continuous_within_at_inter' h],
  split,
  { assume f_cont,
    have : e.source ‚àà ùìù (f x) := mem_nhds_sets e.open_source hx,
    apply continuous_within_at.comp (e.continuous_on (f x) hx) f_cont (inter_subset_right _ _) },
  { assume fe_cont,
    have : continuous_within_at (e.symm ‚àò (e ‚àò f)) (s ‚à© f ‚Åª¬π' e.source) x,
    { have : continuous_within_at e.symm univ (e (f x))
        := (e.continuous_at_symm (e.map_source hx)).continuous_within_at,
      exact continuous_within_at.comp this fe_cont (subset_univ _) },
    exact this.congr (Œªy hy, by simp [e.left_inv hy.2]) (by simp [e.left_inv hx]) }
end

/-- Continuity at a point can be read under left composition with a local homeomorphism if a
neighborhood of the initial point is sent to the source of the local homeomorphism-/
lemma continuous_at_iff_continuous_at_comp_left
  {f : Œ≥ ‚Üí Œ±} {x : Œ≥} (h : f ‚Åª¬π' e.source ‚àà ùìù x) :
  continuous_at f x ‚Üî continuous_at (e ‚àò f) x :=
begin
  have hx : f x ‚àà e.source := (mem_of_nhds h : _),
  have h' : f ‚Åª¬π' e.source ‚àà nhds_within x univ, by rwa nhds_within_univ,
  rw [‚Üê continuous_within_at_univ, ‚Üê continuous_within_at_univ,
      e.continuous_within_at_iff_continuous_within_at_comp_left hx h']
end

/-- A function is continuous on a set if and only if its composition with a local homeomorphism
on the left is continuous on the corresponding set. -/
lemma continuous_on_iff_continuous_on_comp_left {f : Œ≥ ‚Üí Œ±} {s : set Œ≥} (h : s ‚äÜ f ‚Åª¬π' e.source) :
  continuous_on f s ‚Üî continuous_on (e ‚àò f) s :=
begin
  split,
  { assume f_cont,
    exact e.continuous_on.comp f_cont h },
  { assume fe_cont,
    have : continuous_on (e.symm ‚àò e ‚àò f) s,
    { apply continuous_on.comp e.continuous_on_symm fe_cont,
      assume x hx,
      have : f x ‚àà e.source := h hx,
      simp [this] },
    refine continuous_on.congr_mono this (Œªx hx, _) (subset.refl _),
    have : f x ‚àà e.source := h hx,
    simp [this] }
end

end continuity

/-- If a local homeomorphism has source and target equal to univ, then it induces a homeomorphism
between the whole spaces, expressed in this definition. -/
def to_homeomorph_of_source_eq_univ_target_eq_univ (h : e.source = (univ : set Œ±))
  (h' : e.target = univ) : homeomorph Œ± Œ≤ :=
{ to_fun := e,
  inv_fun := e.symm,
  left_inv := Œªx, e.left_inv $ by { rw h, exact mem_univ _ },
  right_inv := Œªx, e.right_inv $ by { rw h', exact mem_univ _ },
  continuous_to_fun := begin
    rw [continuous_iff_continuous_on_univ],
    convert e.continuous_to_fun,
    rw h
  end,
  continuous_inv_fun := begin
    rw [continuous_iff_continuous_on_univ],
    convert e.continuous_inv_fun,
    rw h'
  end }

@[simp, mfld_simps] lemma to_homeomorph_coe (h : e.source = (univ : set Œ±)) (h' : e.target = univ) :
  (e.to_homeomorph_of_source_eq_univ_target_eq_univ h h' : Œ± ‚Üí Œ≤) = e := rfl

@[simp, mfld_simps] lemma to_homeomorph_symm_coe
  (h : e.source = (univ : set Œ±)) (h' : e.target = univ) :
  ((e.to_homeomorph_of_source_eq_univ_target_eq_univ h h').symm : Œ≤ ‚Üí Œ±) = e.symm := rfl

/-- A local homeomorphism whose source is all of `Œ±` defines an open embedding of `Œ±` into `Œ≤`.  The
converse is also true; see `open_embedding.to_local_homeomorph`. -/
lemma to_open_embedding (h : e.source = set.univ) : open_embedding e.to_fun :=
begin
  apply open_embedding_of_continuous_injective_open,
  { apply continuous_iff_continuous_on_univ.mpr,
    rw ‚Üê h,
    exact e.continuous_to_fun },
  { apply set.injective_iff_inj_on_univ.mpr,
    rw ‚Üê h,
    exact e.to_local_equiv.bij_on_source.inj_on },
  { intros U hU,
    simpa only [h, subset_univ] with mfld_simps using e.image_open_of_open hU}
end

end local_homeomorph

namespace homeomorph
variables (e : homeomorph Œ± Œ≤) (e' : homeomorph Œ≤ Œ≥)
/- Register as simp lemmas that the fields of a local homeomorphism built from a homeomorphism
correspond to the fields of the original homeomorphism. -/

@[simp, mfld_simps] lemma to_local_homeomorph_source   : e.to_local_homeomorph.source = univ := rfl
@[simp, mfld_simps] lemma to_local_homeomorph_target   : e.to_local_homeomorph.target = univ := rfl
@[simp, mfld_simps] lemma to_local_homeomorph_coe      : (e.to_local_homeomorph : Œ± ‚Üí Œ≤) = e := rfl
@[simp, mfld_simps] lemma to_local_homeomorph_coe_symm :
  (e.to_local_homeomorph.symm : Œ≤ ‚Üí Œ±) = e.symm := rfl
@[simp, mfld_simps] lemma refl_to_local_homeomorph :
  (homeomorph.refl Œ±).to_local_homeomorph = local_homeomorph.refl Œ± := rfl
@[simp, mfld_simps] lemma symm_to_local_homeomorph :
  e.symm.to_local_homeomorph = e.to_local_homeomorph.symm := rfl
@[simp, mfld_simps] lemma trans_to_local_homeomorph :
  (e.trans e').to_local_homeomorph = e.to_local_homeomorph.trans e'.to_local_homeomorph :=
local_homeomorph.eq_of_local_equiv_eq $ equiv.trans_to_local_equiv _ _

end homeomorph

namespace open_embedding
variables [nonempty Œ±]
variables {f : Œ± ‚Üí Œ≤} (h : open_embedding f)
include f h

/-- An open embedding of `Œ±` into `Œ≤`, with `Œ±` nonempty, defines a local equivalence whose source
is all of `Œ±`.  This is mainly an auxiliary lemma for the stronger result `to_local_homeomorph`. -/
noncomputable def to_local_equiv : local_equiv Œ± Œ≤ :=
set.inj_on.to_local_equiv f set.univ (set.injective_iff_inj_on_univ.mp h.to_embedding.inj)

@[simp, mfld_simps] lemma to_local_equiv_coe : (h.to_local_equiv : Œ± ‚Üí Œ≤) = f := rfl
@[simp, mfld_simps] lemma to_local_equiv_source : h.to_local_equiv.source = set.univ := rfl

@[simp, mfld_simps] lemma to_local_equiv_target : h.to_local_equiv.target = set.range f :=
begin
  rw ‚Üêlocal_equiv.image_source_eq_target,
  ext,
  split,
  { exact Œª ‚ü®a, _, h'‚ü©, ‚ü®a, h'‚ü© },
  { exact Œª ‚ü®a, h'‚ü©, ‚ü®a, by trivial, h'‚ü© }
end

lemma open_target : is_open h.to_local_equiv.target :=
by simpa only with mfld_simps using h.open_range

lemma continuous_inv_fun : continuous_on h.to_local_equiv.inv_fun h.to_local_equiv.target :=
begin
  apply (continuous_on_open_iff h.open_target).mpr,
  intros t ht,
  simp only with mfld_simps,
  convert h.open_iff_image_open.mp ht,
  ext y,
  have hinv : ‚àÄ x : Œ±, (f x = y) ‚Üí h.to_local_equiv.symm y = x :=
    Œª x hxy, by { simpa only [hxy.symm] with mfld_simps using h.to_local_equiv.left_inv },
  simp only [mem_image, mem_range] with mfld_simps,
  split,
  { rintros ‚ü®‚ü®x, hxy‚ü©, hy‚ü©,
    refine ‚ü®x, _, hxy‚ü©,
    rwa (hinv x hxy) at hy },
  { rintros ‚ü®x, hx, hxy‚ü©,
    refine ‚ü®‚ü®x, hxy‚ü©, _‚ü©,
    rwa ‚Üê (hinv x hxy) at hx }
end

/-- An open embedding of `Œ±` into `Œ≤`, with `Œ±` nonempty, defines a local homeomorphism whose source
is all of `Œ±`.  The converse is also true; see `local_homeomorph.to_open_embedding`. -/
noncomputable def to_local_homeomorph : local_homeomorph Œ± Œ≤ :=
{ to_local_equiv := h.to_local_equiv,
  open_source := is_open_univ,
  open_target := h.open_target,
  continuous_to_fun := by simpa only with mfld_simps using h.continuous.continuous_on,
  continuous_inv_fun := h.continuous_inv_fun }

@[simp, mfld_simps] lemma to_local_homeomorph_coe : (h.to_local_homeomorph : Œ± ‚Üí Œ≤) = f := rfl
@[simp, mfld_simps] lemma source : h.to_local_homeomorph.source = set.univ := rfl
@[simp, mfld_simps] lemma target : h.to_local_homeomorph.target = set.range f :=
h.to_local_equiv_target

end open_embedding

namespace topological_space.opens

open topological_space
variables (s : opens Œ±) [nonempty s]

/-- The inclusion of an open subset `s` of a space `Œ±` into `Œ±` is a local homeomorphism from the
subtype `s` to `Œ±`. -/
noncomputable def local_homeomorph_subtype_coe : local_homeomorph s Œ± :=
open_embedding.to_local_homeomorph (s.2.open_embedding_subtype_coe)

@[simp, mfld_simps] lemma local_homeomorph_subtype_coe_coe :
  (s.local_homeomorph_subtype_coe : s ‚Üí Œ±) = coe := rfl

@[simp, mfld_simps] lemma local_homeomorph_subtype_coe_source :
  s.local_homeomorph_subtype_coe.source = set.univ := rfl

@[simp, mfld_simps] lemma local_homeomorph_subtype_coe_target :
  s.local_homeomorph_subtype_coe.target = s :=
by { simp only [local_homeomorph_subtype_coe, subtype.range_coe_subtype] with mfld_simps, refl }

end topological_space.opens

namespace local_homeomorph

open topological_space
variables (e : local_homeomorph Œ± Œ≤)
variables (s : opens Œ±) [nonempty s]

/-- The restriction of a local homeomorphism `e` to an open subset `s` of the domain type produces a
local homeomorphism whose domain is the subtype `s`.-/
noncomputable def subtype_restr : local_homeomorph s Œ≤ := s.local_homeomorph_subtype_coe.trans e

lemma subtype_restr_def : e.subtype_restr s = s.local_homeomorph_subtype_coe.trans e := rfl

@[simp, mfld_simps] lemma subtype_restr_coe : ((e.subtype_restr s : local_homeomorph s Œ≤) : s ‚Üí Œ≤)
  = set.restrict (e : Œ± ‚Üí Œ≤) s := rfl

@[simp, mfld_simps] lemma subtype_restr_source : (e.subtype_restr s).source = coe ‚Åª¬π' e.source :=
by simp only [subtype_restr_def] with mfld_simps

/- This lemma characterizes the transition functions of an open subset in terms of the transition
functions of the original space. -/
lemma subtype_restr_symm_trans_subtype_restr (f f' : local_homeomorph Œ± Œ≤) :
  (f.subtype_restr s).symm.trans (f'.subtype_restr s)
  ‚âà (f.symm.trans f').restr (f.target ‚à© (f.symm) ‚Åª¬π' s) :=
begin
  simp only [subtype_restr_def, trans_symm_eq_symm_trans_symm],
  have openness‚ÇÅ : is_open (f.target ‚à© f.symm ‚Åª¬π' s) := f.preimage_open_of_open_symm s.2,
  rw [‚Üê of_set_trans _ openness‚ÇÅ, ‚Üê trans_assoc, ‚Üê trans_assoc],
  refine eq_on_source.trans' _ (eq_on_source_refl _),
  -- f' has been eliminated !!!
  have sets_identity : f.symm.source ‚à© (f.target ‚à© (f.symm) ‚Åª¬π' s) = f.symm.source ‚à© f.symm ‚Åª¬π' s,
  { mfld_set_tac },
  have openness‚ÇÇ : is_open (s : set Œ±) := s.2,
  rw [of_set_trans', sets_identity, ‚Üê trans_of_set' _ openness‚ÇÇ, trans_assoc],
  refine eq_on_source.trans' (eq_on_source_refl _) _,
  -- f has been eliminated !!!
  refine setoid.trans (trans_symm_self s.local_homeomorph_subtype_coe) _,
  simp only with mfld_simps,
end

end local_homeomorph
