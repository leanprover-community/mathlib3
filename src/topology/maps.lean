/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import topology.order

/-!
# Specific classes of maps between topological spaces

This file introduces the following properties of a map `f : X â†’ Y` between topological spaces:

* `is_open_map f` means the image of an open set under `f` is open.
* `is_closed_map f` means the image of a closed set under `f` is closed.

(Open and closed maps need not be continuous.)

* `inducing f` means the topology on `X` is the one induced via `f` from the topology on `Y`.
  These behave like embeddings except they need not be injective. Instead, points of `X` which
  are identified by `f` are also indistinguishable in the topology on `X`.
* `embedding f` means `f` is inducing and also injective. Equivalently, `f` identifies `X` with
  a subspace of `Y`.
* `open_embedding f` means `f` is an embedding with open image, so it identifies `X` with an
  open subspace of `Y`. Equivalently, `f` is an embedding and an open map.
* `closed_embedding f` similarly means `f` is an embedding with closed image, so it identifies
  `X` with a closed subspace of `Y`. Equivalently, `f` is an embedding and a closed map.

* `quotient_map f` is the dual condition to `embedding f`: `f` is surjective and the topology
  on `Y` is the one coinduced via `f` from the topology on `X`. Equivalently, `f` identifies
  `Y` with a quotient of `X`. Quotient maps are also sometimes known as identification maps.

## References

* <https://en.wikipedia.org/wiki/Open_and_closed_maps>
* <https://en.wikipedia.org/wiki/Embedding#General_topology>
* <https://en.wikipedia.org/wiki/Quotient_space_(topology)#Quotient_map>

## Tags

open map, closed map, embedding, quotient map, identification map

-/

open set filter
open_locale topological_space filter

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

section inducing
structure inducing [tÎ± : topological_space Î±] [tÎ² : topological_space Î²] (f : Î± â†’ Î²) : Prop :=
(induced : tÎ± = tÎ².induced f)

variables [topological_space Î±] [topological_space Î²] [topological_space Î³] [topological_space Î´]

lemma inducing_id : inducing (@id Î±) :=
âŸ¨induced_id.symmâŸ©

protected lemma inducing.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : inducing g) (hf : inducing f) :
  inducing (g âˆ˜ f) :=
âŸ¨by rw [hf.induced, hg.induced, induced_compose]âŸ©

lemma inducing_of_inducing_compose {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : continuous f) (hg : continuous g)
  (hgf : inducing (g âˆ˜ f)) : inducing f :=
âŸ¨le_antisymm
    (by rwa â† continuous_iff_le_induced)
    (by { rw [hgf.induced, â† continuous_iff_le_induced], apply hg.comp continuous_induced_dom })âŸ©

lemma inducing.nhds_eq_comap {f : Î± â†’ Î²} (hf : inducing f) :
  âˆ€ (a : Î±), ğ“ a = comap f (ğ“ $ f a) :=
(induced_iff_nhds_eq f).1 hf.induced

lemma inducing.map_nhds_eq {f : Î± â†’ Î²} (hf : inducing f) (a : Î±) :
  (ğ“ a).map f = ğ“[range f] (f a) :=
hf.induced.symm â–¸ map_nhds_induced_eq a

lemma inducing.map_nhds_of_mem {f : Î± â†’ Î²} (hf : inducing f) (a : Î±) (h : range f âˆˆ ğ“ (f a)) :
  (ğ“ a).map f = ğ“ (f a) :=
hf.induced.symm â–¸ map_nhds_induced_of_mem h

lemma inducing.tendsto_nhds_iff {Î¹ : Type*}
  {f : Î¹ â†’ Î²} {g : Î² â†’ Î³} {a : filter Î¹} {b : Î²} (hg : inducing g) :
  tendsto f a (ğ“ b) â†” tendsto (g âˆ˜ f) a (ğ“ (g b)) :=
by rw [tendsto, tendsto, hg.induced, nhds_induced, â† map_le_iff_le_comap, filter.map_map]

lemma inducing.continuous_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : inducing g) :
  continuous f â†” continuous (g âˆ˜ f) :=
by simp [continuous_iff_continuous_at, continuous_at, inducing.tendsto_nhds_iff hg]

lemma inducing.continuous {f : Î± â†’ Î²} (hf : inducing f) : continuous f :=
hf.continuous_iff.mp continuous_id

lemma inducing.closure_eq_preimage_closure_image {f : Î± â†’ Î²} (hf : inducing f) (s : set Î±) :
  closure s = f â»Â¹' closure (f '' s) :=
by { ext x, rw [set.mem_preimage, â† closure_induced, hf.induced] }

lemma inducing.is_closed_iff {f : Î± â†’ Î²} (hf : inducing f) {s : set Î±} :
  is_closed s â†” âˆƒ t, is_closed t âˆ§ f â»Â¹' t = s :=
by rw [hf.induced, is_closed_induced_iff]

lemma inducing.is_open_iff {f : Î± â†’ Î²} (hf : inducing f) {s : set Î±} :
  is_open s â†” âˆƒ t, is_open t âˆ§ f â»Â¹' t = s :=
by rw [hf.induced, is_open_induced_iff]

end inducing

section embedding

/-- A function between topological spaces is an embedding if it is injective,
  and for all `s : set Î±`, `s` is open iff it is the preimage of an open set. -/
structure embedding [tÎ± : topological_space Î±] [tÎ² : topological_space Î²] (f : Î± â†’ Î²)
  extends inducing f : Prop :=
(inj : function.injective f)

variables [topological_space Î±] [topological_space Î²] [topological_space Î³]

lemma embedding.mk' (f : Î± â†’ Î²) (inj : function.injective f)
  (induced : âˆ€a, comap f (ğ“ (f a)) = ğ“ a) : embedding f :=
âŸ¨âŸ¨(induced_iff_nhds_eq f).2 (Î» a, (induced a).symm)âŸ©, injâŸ©

lemma embedding_id : embedding (@id Î±) :=
âŸ¨inducing_id, assume aâ‚ aâ‚‚ h, hâŸ©

lemma embedding.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : embedding g) (hf : embedding f) :
  embedding (g âˆ˜ f) :=
{ inj:= assume aâ‚ aâ‚‚ h, hf.inj $ hg.inj h,
  ..hg.to_inducing.comp hf.to_inducing }

lemma embedding_of_embedding_compose {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : continuous f) (hg : continuous g)
  (hgf : embedding (g âˆ˜ f)) : embedding f :=
{ induced := (inducing_of_inducing_compose hf hg hgf.to_inducing).induced,
  inj := assume aâ‚ aâ‚‚ h, hgf.inj $ by simp [h, (âˆ˜)] }

protected lemma function.left_inverse.embedding {f : Î± â†’ Î²} {g : Î² â†’ Î±}
  (h : function.left_inverse f g) (hf : continuous f) (hg : continuous g) :
  embedding g :=
embedding_of_embedding_compose hg hf $ h.comp_eq_id.symm â–¸ embedding_id

lemma embedding.map_nhds_eq {f : Î± â†’ Î²} (hf : embedding f) (a : Î±) :
  (ğ“ a).map f = ğ“[range f] (f a) :=
hf.1.map_nhds_eq a

lemma embedding.map_nhds_of_mem {f : Î± â†’ Î²}
  (hf : embedding f) (a : Î±) (h : range f âˆˆ ğ“ (f a)) : (ğ“ a).map f = ğ“ (f a) :=
hf.1.map_nhds_of_mem a h

lemma embedding.tendsto_nhds_iff {Î¹ : Type*}
  {f : Î¹ â†’ Î²} {g : Î² â†’ Î³} {a : filter Î¹} {b : Î²} (hg : embedding g) :
  tendsto f a (ğ“ b) â†” tendsto (g âˆ˜ f) a (ğ“ (g b)) :=
hg.to_inducing.tendsto_nhds_iff

lemma embedding.continuous_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : embedding g) :
  continuous f â†” continuous (g âˆ˜ f) :=
inducing.continuous_iff hg.1

lemma embedding.continuous {f : Î± â†’ Î²} (hf : embedding f) : continuous f :=
inducing.continuous hf.1

lemma embedding.closure_eq_preimage_closure_image {e : Î± â†’ Î²} (he : embedding e) (s : set Î±) :
  closure s = e â»Â¹' closure (e '' s) :=
he.1.closure_eq_preimage_closure_image s

end embedding

/-- A function between topological spaces is a quotient map if it is surjective,
  and for all `s : set Î²`, `s` is open iff its preimage is an open set. -/
def quotient_map {Î± : Type*} {Î² : Type*} [tÎ± : topological_space Î±] [tÎ² : topological_space Î²]
  (f : Î± â†’ Î²) : Prop :=
function.surjective f âˆ§ tÎ² = tÎ±.coinduced f

lemma quotient_map_iff {Î± Î² : Type*} [topological_space Î±] [topological_space Î²] {f : Î± â†’ Î²} :
  quotient_map f â†” function.surjective f âˆ§ âˆ€ s : set Î², is_open s â†” is_open (f â»Â¹' s) :=
and_congr iff.rfl topological_space_eq_iff

namespace quotient_map
variables [topological_space Î±] [topological_space Î²] [topological_space Î³] [topological_space Î´]

protected lemma id : quotient_map (@id Î±) :=
âŸ¨assume a, âŸ¨a, rflâŸ©, coinduced_id.symmâŸ©

protected lemma comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : quotient_map g) (hf : quotient_map f) :
  quotient_map (g âˆ˜ f) :=
âŸ¨hg.left.comp hf.left, by rw [hg.right, hf.right, coinduced_compose]âŸ©

protected lemma of_quotient_map_compose {f : Î± â†’ Î²} {g : Î² â†’ Î³}
  (hf : continuous f) (hg : continuous g)
  (hgf : quotient_map (g âˆ˜ f)) : quotient_map g :=
âŸ¨assume b, let âŸ¨a, hâŸ© := hgf.left b in âŸ¨f a, hâŸ©,
  le_antisymm
    (by rw [hgf.right, â† continuous_iff_coinduced_le];
        apply continuous_coinduced_rng.comp hf)
    (by rwa â† continuous_iff_coinduced_le)âŸ©

protected lemma continuous_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : quotient_map f) :
  continuous g â†” continuous (g âˆ˜ f) :=
by rw [continuous_iff_coinduced_le, continuous_iff_coinduced_le, hf.right, coinduced_compose]

protected lemma continuous {f : Î± â†’ Î²} (hf : quotient_map f) : continuous f :=
hf.continuous_iff.mp continuous_id

protected lemma surjective {f : Î± â†’ Î²} (hf : quotient_map f) : function.surjective f := hf.1

protected lemma is_open_preimage {f : Î± â†’ Î²} (hf : quotient_map f) {s : set Î²} :
  is_open (f â»Â¹' s) â†” is_open s :=
((quotient_map_iff.1 hf).2 s).symm

end quotient_map

/-- A map `f : Î± â†’ Î²` is said to be an *open map*, if the image of any open `U : set Î±`
is open in `Î²`. -/
def is_open_map [topological_space Î±] [topological_space Î²] (f : Î± â†’ Î²) :=
âˆ€ U : set Î±, is_open U â†’ is_open (f '' U)

namespace is_open_map
variables [topological_space Î±] [topological_space Î²] [topological_space Î³] {f : Î± â†’ Î²}
open function

protected lemma id : is_open_map (@id Î±) := assume s hs, by rwa [image_id]

protected lemma comp
  {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : is_open_map g) (hf : is_open_map f) : is_open_map (g âˆ˜ f) :=
by intros s hs; rw [image_comp]; exact hg _ (hf _ hs)

lemma is_open_range (hf : is_open_map f) : is_open (range f) :=
by { rw â† image_univ, exact hf _ is_open_univ }

lemma image_mem_nhds (hf : is_open_map f) {x : Î±} {s : set Î±} (hx : s âˆˆ ğ“ x) :
  f '' s âˆˆ ğ“ (f x) :=
let âŸ¨t, hts, ht, hxtâŸ© := mem_nhds_iff.1 hx in
mem_of_superset (is_open.mem_nhds (hf t ht) (mem_image_of_mem _ hxt)) (image_subset _ hts)

lemma image_interior_subset (hf : is_open_map f) (s : set Î±) :
  f '' interior s âŠ† interior (f '' s) :=
interior_maximal (image_subset _ interior_subset) (hf _ is_open_interior)

lemma nhds_le (hf : is_open_map f) (a : Î±) : ğ“ (f a) â‰¤ (ğ“ a).map f :=
le_map $ Î» s, hf.image_mem_nhds

lemma of_nhds_le (hf : âˆ€ a, ğ“ (f a) â‰¤ map f (ğ“ a)) : is_open_map f :=
Î» s hs, is_open_iff_mem_nhds.2 $ Î» b âŸ¨a, has, habâŸ©,
  hab â–¸ hf _ (image_mem_map $ is_open.mem_nhds hs has)

lemma of_inverse {f : Î± â†’ Î²} {f' : Î² â†’ Î±}
  (h : continuous f') (l_inv : left_inverse f f') (r_inv : right_inverse f f') :
  is_open_map f :=
begin
  assume s hs,
  rw [image_eq_preimage_of_inverse r_inv l_inv],
  exact hs.preimage h
end

lemma to_quotient_map {f : Î± â†’ Î²}
  (open_map : is_open_map f) (cont : continuous f) (surj : function.surjective f) :
  quotient_map f :=
âŸ¨ surj,
  begin
    ext s,
    show is_open s â†” is_open (f â»Â¹' s),
    split,
    { exact continuous_def.1 cont s },
    { assume h,
      rw â† surj.image_preimage s,
      exact open_map _ h }
  endâŸ©

end is_open_map

lemma is_open_map_iff_nhds_le [topological_space Î±] [topological_space Î²] {f : Î± â†’ Î²} :
  is_open_map f â†” âˆ€(a:Î±), ğ“ (f a) â‰¤ (ğ“ a).map f :=
âŸ¨Î» hf, hf.nhds_le, is_open_map.of_nhds_leâŸ©

lemma inducing.is_open_map [topological_space Î±] [topological_space Î²] {f : Î± â†’ Î²}
  (hi : inducing f) (ho : is_open (range f)) :
  is_open_map f :=
is_open_map.of_nhds_le $ Î» x, (hi.map_nhds_of_mem _ $ is_open.mem_nhds ho $ mem_range_self _).ge

section is_closed_map
variables [topological_space Î±] [topological_space Î²]

/-- A map `f : Î± â†’ Î²` is said to be a *closed map*, if the image of any closed `U : set Î±`
is closed in `Î²`. -/
def is_closed_map (f : Î± â†’ Î²) := âˆ€ U : set Î±, is_closed U â†’ is_closed (f '' U)

end is_closed_map

namespace is_closed_map

variables [topological_space Î±] [topological_space Î²] [topological_space Î³]
open function

protected lemma id : is_closed_map (@id Î±) := assume s hs, by rwa image_id

protected lemma comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : is_closed_map g) (hf : is_closed_map f) :
  is_closed_map (g âˆ˜ f) :=
by { intros s hs, rw image_comp, exact hg _ (hf _ hs) }

lemma of_inverse {f : Î± â†’ Î²} {f' : Î² â†’ Î±}
  (h : continuous f') (l_inv : left_inverse f f') (r_inv : right_inverse f f') :
  is_closed_map f :=
assume s hs,
have f' â»Â¹' s = f '' s, by ext x; simp [mem_image_iff_of_inverse r_inv l_inv],
this â–¸ hs.preimage h

lemma of_nonempty {f : Î± â†’ Î²} (h : âˆ€ s, is_closed s â†’ s.nonempty â†’ is_closed (f '' s)) :
  is_closed_map f :=
begin
  intros s hs, cases eq_empty_or_nonempty s with h2s h2s,
  { simp_rw [h2s, image_empty, is_closed_empty] },
  { exact h s hs h2s }
end

end is_closed_map

lemma inducing.is_closed_map [topological_space Î±] [topological_space Î²]
  {f : Î± â†’ Î²} (hf : inducing f) (h : is_closed (range f)) : is_closed_map f :=
begin
  intros s hs,
  rcases hf.is_closed_iff.1 hs with âŸ¨t, ht, rflâŸ©,
  rw image_preimage_eq_inter_range,
  exact is_closed.inter ht h
end

section open_embedding
variables [topological_space Î±] [topological_space Î²] [topological_space Î³]

/-- An open embedding is an embedding with open image. -/
structure open_embedding (f : Î± â†’ Î²) extends embedding f : Prop :=
(open_range : is_open $ range f)

lemma open_embedding.is_open_map {f : Î± â†’ Î²} (hf : open_embedding f) : is_open_map f :=
hf.to_embedding.to_inducing.is_open_map hf.open_range

lemma open_embedding.map_nhds_eq {f : Î± â†’ Î²} (hf : open_embedding f) (a : Î±) :
  map f (ğ“ a) = ğ“ (f a) :=
hf.to_embedding.map_nhds_of_mem _ $ is_open.mem_nhds hf.open_range $ mem_range_self _

lemma open_embedding.open_iff_image_open {f : Î± â†’ Î²} (hf : open_embedding f)
  {s : set Î±} : is_open s â†” is_open (f '' s) :=
âŸ¨hf.is_open_map s,
 Î» h, begin
   convert â† h.preimage hf.to_embedding.continuous,
   apply preimage_image_eq _ hf.inj
 endâŸ©

lemma open_embedding.tendsto_nhds_iff {Î¹ : Type*}
  {f : Î¹ â†’ Î²} {g : Î² â†’ Î³} {a : filter Î¹} {b : Î²} (hg : open_embedding g) :
  tendsto f a (ğ“ b) â†” tendsto (g âˆ˜ f) a (ğ“ (g b)) :=
hg.to_embedding.tendsto_nhds_iff

lemma open_embedding.continuous {f : Î± â†’ Î²} (hf : open_embedding f) : continuous f :=
hf.to_embedding.continuous

lemma open_embedding.open_iff_preimage_open {f : Î± â†’ Î²} (hf : open_embedding f)
  {s : set Î²} (hs : s âŠ† range f) : is_open s â†” is_open (f â»Â¹' s) :=
begin
  convert â†hf.open_iff_image_open.symm,
  rwa [image_preimage_eq_inter_range, inter_eq_self_of_subset_left]
end

lemma open_embedding_of_embedding_open {f : Î± â†’ Î²} (hâ‚ : embedding f)
  (hâ‚‚ : is_open_map f) : open_embedding f :=
âŸ¨hâ‚, hâ‚‚.is_open_rangeâŸ©

lemma open_embedding_of_continuous_injective_open {f : Î± â†’ Î²} (hâ‚ : continuous f)
  (hâ‚‚ : function.injective f) (hâ‚ƒ : is_open_map f) : open_embedding f :=
begin
  refine open_embedding_of_embedding_open âŸ¨âŸ¨_âŸ©, hâ‚‚âŸ© hâ‚ƒ,
  apply le_antisymm (continuous_iff_le_induced.mp hâ‚) _,
  intro s,
  change is_open _ â†’ is_open _,
  rw is_open_induced_iff,
  refine Î» hs, âŸ¨f '' s, hâ‚ƒ s hs, _âŸ©,
  rw preimage_image_eq _ hâ‚‚
end

lemma open_embedding_id : open_embedding (@id Î±) :=
âŸ¨embedding_id, by convert is_open_univ; apply range_idâŸ©

lemma open_embedding.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²}
  (hg : open_embedding g) (hf : open_embedding f) : open_embedding (g âˆ˜ f) :=
âŸ¨hg.1.comp hf.1, show is_open (range (g âˆ˜ f)),
 by rw [range_comp, â†hg.open_iff_image_open]; exact hf.2âŸ©

end open_embedding

section closed_embedding
variables [topological_space Î±] [topological_space Î²] [topological_space Î³]

/-- A closed embedding is an embedding with closed image. -/
structure closed_embedding (f : Î± â†’ Î²) extends embedding f : Prop :=
(closed_range : is_closed $ range f)

variables {f : Î± â†’ Î²}

lemma closed_embedding.tendsto_nhds_iff {Î¹ : Type*}
  {g : Î¹ â†’ Î±} {a : filter Î¹} {b : Î±} (hf : closed_embedding f) :
  tendsto g a (ğ“ b) â†” tendsto (f âˆ˜ g) a (ğ“ (f b)) :=
hf.to_embedding.tendsto_nhds_iff

lemma closed_embedding.continuous (hf : closed_embedding f) : continuous f :=
hf.to_embedding.continuous

lemma closed_embedding.is_closed_map (hf : closed_embedding f) : is_closed_map f :=
hf.to_embedding.to_inducing.is_closed_map hf.closed_range

lemma closed_embedding.closed_iff_image_closed (hf : closed_embedding f)
  {s : set Î±} : is_closed s â†” is_closed (f '' s) :=
âŸ¨hf.is_closed_map s,
 Î» h, begin
   convert â†continuous_iff_is_closed.mp hf.continuous _ h,
   apply preimage_image_eq _ hf.inj
 endâŸ©

lemma closed_embedding.closed_iff_preimage_closed (hf : closed_embedding f)
  {s : set Î²} (hs : s âŠ† range f) : is_closed s â†” is_closed (f â»Â¹' s) :=
begin
  convert â†hf.closed_iff_image_closed.symm,
  rwa [image_preimage_eq_inter_range, inter_eq_self_of_subset_left]
end

lemma closed_embedding_of_embedding_closed (hâ‚ : embedding f)
  (hâ‚‚ : is_closed_map f) : closed_embedding f :=
âŸ¨hâ‚, by convert hâ‚‚ univ is_closed_univ; simpâŸ©

lemma closed_embedding_of_continuous_injective_closed (hâ‚ : continuous f)
  (hâ‚‚ : function.injective f) (hâ‚ƒ : is_closed_map f) : closed_embedding f :=
begin
  refine closed_embedding_of_embedding_closed âŸ¨âŸ¨_âŸ©, hâ‚‚âŸ© hâ‚ƒ,
  apply le_antisymm (continuous_iff_le_induced.mp hâ‚) _,
  intro s',
  change is_open _ â‰¤ is_open _,
  rw [â†is_closed_compl_iff, â†is_closed_compl_iff],
  generalize : s'á¶œ = s,
  rw is_closed_induced_iff,
  refine Î» hs, âŸ¨f '' s, hâ‚ƒ s hs, _âŸ©,
  rw preimage_image_eq _ hâ‚‚
end

lemma closed_embedding_id : closed_embedding (@id Î±) :=
âŸ¨embedding_id, by convert is_closed_univ; apply range_idâŸ©

lemma closed_embedding.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²}
  (hg : closed_embedding g) (hf : closed_embedding f) : closed_embedding (g âˆ˜ f) :=
âŸ¨hg.to_embedding.comp hf.to_embedding, show is_closed (range (g âˆ˜ f)),
 by rw [range_comp, â†hg.closed_iff_image_closed]; exact hf.closed_rangeâŸ©

end closed_embedding
