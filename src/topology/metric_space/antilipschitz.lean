/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import topology.metric_space.lipschitz
import topology.uniform_space.complete_separated

/-!
# Antilipschitz functions

We say that a map `f : Œ± ‚Üí Œ≤` between two (extended) metric spaces is
`antilipschitz_with K`, `K ‚â• 0`, if for all `x, y` we have `edist x y ‚â§ K * edist (f x) (f y)`.
For a metric space, the latter inequality is equivalent to `dist x y ‚â§ K * dist (f x) (f y)`.

## Implementation notes

The parameter `K` has type `‚Ñù‚â•0`. This way we avoid conjuction in the definition and have
coercions both to `‚Ñù` and `‚Ñù‚â•0‚àû`. We do not require `0 < K` in the definition, mostly because
we do not have a `posreal` type.
-/

variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}

open_locale nnreal ennreal uniformity
open set

/-- We say that `f : Œ± ‚Üí Œ≤` is `antilipschitz_with K` if for any two points `x`, `y` we have
`K * edist x y ‚â§ edist (f x) (f y)`. -/
def antilipschitz_with [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] (K : ‚Ñù‚â•0) (f : Œ± ‚Üí Œ≤) :=
‚àÄ x y, edist x y ‚â§ K * edist (f x) (f y)

section metric

variables [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤}

lemma antilipschitz_with_iff_le_mul_nndist :
  antilipschitz_with K f ‚Üî ‚àÄ x y, nndist x y ‚â§ K * nndist (f x) (f y) :=
by { simp only [antilipschitz_with, edist_nndist], norm_cast }

alias antilipschitz_with_iff_le_mul_nndist ‚Üî antilipschitz_with.le_mul_nndist
  antilipschitz_with.of_le_mul_nndist

lemma antilipschitz_with_iff_le_mul_dist :
  antilipschitz_with K f ‚Üî ‚àÄ x y, dist x y ‚â§ K * dist (f x) (f y) :=
by { simp only [antilipschitz_with_iff_le_mul_nndist, dist_nndist], norm_cast }

alias antilipschitz_with_iff_le_mul_dist ‚Üî antilipschitz_with.le_mul_dist
  antilipschitz_with.of_le_mul_dist

namespace antilipschitz_with

lemma mul_le_nndist (hf : antilipschitz_with K f) (x y : Œ±) :
  K‚Åª¬π * nndist x y ‚â§ nndist (f x) (f y) :=
by simpa only [div_eq_inv_mul] using nnreal.div_le_of_le_mul' (hf.le_mul_nndist x y)

lemma mul_le_dist (hf : antilipschitz_with K f) (x y : Œ±) :
  (K‚Åª¬π * dist x y : ‚Ñù) ‚â§ dist (f x) (f y) :=
by exact_mod_cast hf.mul_le_nndist x y

end antilipschitz_with

end metric

namespace antilipschitz_with

variables [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] [pseudo_emetric_space Œ≥]
variables {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤}

open emetric

/-- Extract the constant from `hf : antilipschitz_with K f`. This is useful, e.g.,
if `K` is given by a long formula, and we want to reuse this value. -/
@[nolint unused_arguments] -- uses neither `f` nor `hf`
protected def K (hf : antilipschitz_with K f) : ‚Ñù‚â•0 := K

protected lemma injective {Œ± : Type*} {Œ≤ : Type*} [emetric_space Œ±] [pseudo_emetric_space Œ≤]
  {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤} (hf : antilipschitz_with K f) : function.injective f :=
Œª x y h, by simpa only [h, edist_self, mul_zero, edist_le_zero] using hf x y

lemma mul_le_edist (hf : antilipschitz_with K f) (x y : Œ±) :
  (K‚Åª¬π * edist x y : ‚Ñù‚â•0‚àû) ‚â§ edist (f x) (f y) :=
begin
  rw [mul_comm, ‚Üê div_eq_mul_inv],
  exact ennreal.div_le_of_le_mul' (hf x y)
end

lemma ediam_preimage_le (hf : antilipschitz_with K f) (s : set Œ≤) : diam (f ‚Åª¬π' s) ‚â§ K * diam s :=
diam_le $ Œª x hx y hy, (hf x y).trans $ mul_le_mul_left' (edist_le_diam_of_mem hx hy) K

lemma le_mul_ediam_image (hf : antilipschitz_with K f) (s : set Œ±) : diam s ‚â§ K * diam (f '' s) :=
(diam_mono (subset_preimage_image _ _)).trans (hf.ediam_preimage_le (f '' s))

protected lemma id : antilipschitz_with 1 (id : Œ± ‚Üí Œ±) :=
Œª x y, by simp only [ennreal.coe_one, one_mul, id, le_refl]

lemma comp {Kg : ‚Ñù‚â•0} {g : Œ≤ ‚Üí Œ≥} (hg : antilipschitz_with Kg g)
  {Kf : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤} (hf : antilipschitz_with Kf f) :
  antilipschitz_with (Kf * Kg) (g ‚àò f) :=
Œª x y,
calc edist x y ‚â§ Kf * edist (f x) (f y) : hf x y
... ‚â§ Kf * (Kg * edist (g (f x)) (g (f y))) : ennreal.mul_left_mono (hg _ _)
... = _ : by rw [ennreal.coe_mul, mul_assoc]

lemma restrict (hf : antilipschitz_with K f) (s : set Œ±) :
  antilipschitz_with K (s.restrict f) :=
Œª x y, hf x y

lemma cod_restrict (hf : antilipschitz_with K f) {s : set Œ≤} (hs : ‚àÄ x, f x ‚àà s) :
  antilipschitz_with K (s.cod_restrict f hs) :=
Œª x y, hf x y

lemma to_right_inv_on' {s : set Œ±} (hf : antilipschitz_with K (s.restrict f))
  {g : Œ≤ ‚Üí Œ±} {t : set Œ≤} (g_maps : maps_to g t s) (g_inv : right_inv_on g f t) :
  lipschitz_with K (t.restrict g) :=
Œª x y, by simpa only [restrict_apply, g_inv x.mem, g_inv y.mem, subtype.edist_eq, subtype.coe_mk]
  using hf ‚ü®g x, g_maps x.mem‚ü© ‚ü®g y, g_maps y.mem‚ü©

lemma to_right_inv_on (hf : antilipschitz_with K f) {g : Œ≤ ‚Üí Œ±} {t : set Œ≤}
  (h : right_inv_on g f t) :
  lipschitz_with K (t.restrict g) :=
(hf.restrict univ).to_right_inv_on' (maps_to_univ g t) h

lemma to_right_inverse (hf : antilipschitz_with K f) {g : Œ≤ ‚Üí Œ±} (hg : function.right_inverse g f) :
  lipschitz_with K g :=
begin
  intros x y,
  have := hf (g x) (g y),
  rwa [hg x, hg y] at this
end

lemma comap_uniformity_le (hf : antilipschitz_with K f) :
  (ùì§ Œ≤).comap (prod.map f f) ‚â§ ùì§ Œ± :=
begin
  refine ((uniformity_basis_edist.comap _).le_basis_iff uniformity_basis_edist).2 (Œª Œµ h‚ÇÄ, _),
  refine ‚ü®K‚Åª¬π * Œµ, ennreal.mul_pos.2 ‚ü®ennreal.inv_pos.2 ennreal.coe_ne_top, h‚ÇÄ‚ü©, _‚ü©,
  refine Œª x hx, (hf x.1 x.2).trans_lt _,
  rw [mul_comm, ‚Üê div_eq_mul_inv] at hx,
  rw mul_comm,
  exact ennreal.mul_lt_of_lt_div hx
end

protected lemma uniform_inducing (hf : antilipschitz_with K f) (hfc : uniform_continuous f) :
  uniform_inducing f :=
‚ü®le_antisymm hf.comap_uniformity_le hfc.le_comap‚ü©

protected lemma uniform_embedding {Œ± : Type*} {Œ≤ : Type*} [emetric_space Œ±] [pseudo_emetric_space Œ≤]
  {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤} (hf : antilipschitz_with K f) (hfc : uniform_continuous f) :
  uniform_embedding f :=
‚ü®hf.uniform_inducing hfc, hf.injective‚ü©

lemma is_complete_range [complete_space Œ±] (hf : antilipschitz_with K f)
  (hfc : uniform_continuous f) : is_complete (range f) :=
(hf.uniform_inducing hfc).is_complete_range

lemma is_closed_range {Œ± Œ≤ : Type*} [pseudo_emetric_space Œ±] [emetric_space Œ≤] [complete_space Œ±]
  {f : Œ± ‚Üí Œ≤} {K : ‚Ñù‚â•0} (hf : antilipschitz_with K f) (hfc : uniform_continuous f) :
  is_closed (range f) :=
(hf.is_complete_range hfc).is_closed

lemma closed_embedding {Œ± : Type*} {Œ≤ : Type*} [emetric_space Œ±] [emetric_space Œ≤] {K : ‚Ñù‚â•0}
  {f : Œ± ‚Üí Œ≤} [complete_space Œ±] (hf : antilipschitz_with K f) (hfc : uniform_continuous f) :
  closed_embedding f :=
{ closed_range := hf.is_closed_range hfc,
  .. (hf.uniform_embedding hfc).embedding }

lemma subtype_coe (s : set Œ±) : antilipschitz_with 1 (coe : s ‚Üí Œ±) :=
antilipschitz_with.id.restrict s

lemma of_subsingleton [subsingleton Œ±] {K : ‚Ñù‚â•0} : antilipschitz_with K f :=
Œª x y, by simp only [subsingleton.elim x y, edist_self, zero_le]

/-- If `f : Œ± ‚Üí Œ≤` is `0`-antilipschitz, then `Œ±` is a `subsingleton`. -/
protected lemma subsingleton {Œ± Œ≤} [emetric_space Œ±] [pseudo_emetric_space Œ≤] {f : Œ± ‚Üí Œ≤}
  (h : antilipschitz_with 0 f) : subsingleton Œ± :=
‚ü®Œª x y, edist_le_zero.1 $ (h x y).trans_eq $ zero_mul _‚ü©

end antilipschitz_with

namespace antilipschitz_with

open metric

variables [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤}

lemma bounded_preimage (hf : antilipschitz_with K f)
  {s : set Œ≤} (hs : bounded s) :
  bounded (f ‚Åª¬π' s) :=
exists.intro (K * diam s) $ Œª x y hx hy,
calc dist x y ‚â§ K * dist (f x) (f y) : hf.le_mul_dist x y
... ‚â§ K * diam s : mul_le_mul_of_nonneg_left (dist_le_diam_of_mem hs hx hy) K.2

/-- The image of a proper space under an expanding onto map is proper. -/
protected lemma proper_space {Œ± : Type*} [metric_space Œ±] {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤} [proper_space Œ±]
  (hK : antilipschitz_with K f) (f_cont : continuous f) (hf : function.surjective f) :
  proper_space Œ≤ :=
begin
  apply proper_space_of_compact_closed_ball_of_le 0 (Œªx‚ÇÄ r hr, _),
  let K := f ‚Åª¬π' (closed_ball x‚ÇÄ r),
  have A : is_closed K := is_closed_ball.preimage f_cont,
  have B : bounded K := hK.bounded_preimage bounded_closed_ball,
  have : is_compact K := compact_iff_closed_bounded.2 ‚ü®A, B‚ü©,
  convert this.image f_cont,
  exact (hf.image_preimage _).symm
end

end antilipschitz_with

lemma lipschitz_with.to_right_inverse [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] {K : ‚Ñù‚â•0}
  {f : Œ± ‚Üí Œ≤} (hf : lipschitz_with K f) {g : Œ≤ ‚Üí Œ±} (hg : function.right_inverse g f) :
  antilipschitz_with K g :=
Œª x y, by simpa only [hg _] using hf (g x) (g y)
