/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.specific_limits
import order.filter.countable_Inter

/-!
# Baire theorem

In a complete metric space, a countable intersection of dense open subsets is dense.

The good concept underlying the theorem is that of a GÎ´ set, i.e., a countable intersection
of open sets. Then Baire theorem can also be formulated as the fact that a countable
intersection of dense GÎ´ sets is a dense GÎ´ set. We prove Baire theorem, giving several different
formulations that can be handy. We also prove the important consequence that, if the space is
covered by a countable union of closed sets, then the union of their interiors is dense.

The names of the theorems do not contain the string "Baire", but are instead built from the form of
the statement. "Baire" is however in the docstring of all the theorems, to facilitate grep searches.

We also define the filter `residual Î±` generated by dense `GÎ´` sets and prove that this filter
has the countable intersection property.
-/

noncomputable theory
open_locale classical topological_space filter

open filter encodable set

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î¹ : Type*}

section is_GÎ´
variable [topological_space Î±]

/-- A GÎ´ set is a countable intersection of open sets. -/
def is_GÎ´ (s : set Î±) : Prop :=
  âˆƒT : set (set Î±), (âˆ€t âˆˆ T, is_open t) âˆ§ countable T âˆ§ s = (â‹‚â‚€ T)

/-- An open set is a GÎ´ set. -/
lemma is_open.is_GÎ´ {s : set Î±} (h : is_open s) : is_GÎ´ s :=
âŸ¨{s}, by simp [h], countable_singleton _, (set.sInter_singleton _).symmâŸ©

lemma is_GÎ´_univ : is_GÎ´ (univ : set Î±) := is_open_univ.is_GÎ´

lemma is_GÎ´_bInter_of_open {I : set Î¹} (hI : countable I) {f : Î¹ â†’ set Î±}
  (hf : âˆ€i âˆˆ I, is_open (f i)) : is_GÎ´ (â‹‚iâˆˆI, f i) :=
âŸ¨f '' I, by rwa ball_image_iff, hI.image _, by rw sInter_imageâŸ©

lemma is_GÎ´_Inter_of_open [encodable Î¹] {f : Î¹ â†’ set Î±}
  (hf : âˆ€i, is_open (f i)) : is_GÎ´ (â‹‚i, f i) :=
âŸ¨range f, by rwa forall_range_iff, countable_range _, by rw sInter_rangeâŸ©

/-- A countable intersection of GÎ´ sets is a GÎ´ set. -/
lemma is_GÎ´_sInter {S : set (set Î±)} (h : âˆ€sâˆˆS, is_GÎ´ s) (hS : countable S) : is_GÎ´ (â‹‚â‚€ S) :=
begin
  choose T hT using h,
  refine âŸ¨_, _, _, (sInter_bUnion (Î» s hs, (hT s hs).2.2)).symmâŸ©,
  { simp only [mem_Union],
    rintros t âŸ¨s, hs, tTsâŸ©,
    exact (hT s hs).1 t tTs },
  { exact hS.bUnion (Î»s hs, (hT s hs).2.1) },
end

lemma is_GÎ´_Inter [encodable Î¹]  {s : Î¹ â†’ set Î±} (hs : âˆ€ i, is_GÎ´ (s i)) : is_GÎ´ (â‹‚ i, s i) :=
is_GÎ´_sInter (forall_range_iff.2 hs) $ countable_range s

lemma is_GÎ´_bInter {s : set Î¹} (hs : countable s) {t : Î  i âˆˆ s, set Î±} (ht : âˆ€ i âˆˆ s, is_GÎ´ (t i â€¹_â€º)) :
  is_GÎ´ (â‹‚ i âˆˆ s, t i â€¹_â€º) :=
begin
  rw [bInter_eq_Inter],
  haveI := hs.to_encodable,
  exact is_GÎ´_Inter (Î» x, ht x x.2)
end

lemma is_GÎ´.inter {s t : set Î±} (hs : is_GÎ´ s) (ht : is_GÎ´ t) : is_GÎ´ (s âˆ© t) :=
by { rw inter_eq_Inter, exact is_GÎ´_Inter (bool.forall_bool.2 âŸ¨ht, hsâŸ©) }

/-- The union of two GÎ´ sets is a GÎ´ set. -/
lemma is_GÎ´.union {s t : set Î±} (hs : is_GÎ´ s) (ht : is_GÎ´ t) : is_GÎ´ (s âˆª t) :=
begin
  rcases hs with âŸ¨S, Sopen, Scount, rflâŸ©,
  rcases ht with âŸ¨T, Topen, Tcount, rflâŸ©,
  rw [sInter_union_sInter],
  apply is_GÎ´_bInter_of_open (countable_prod Scount Tcount),
  rintros âŸ¨a, bâŸ© hab,
  exact is_open_union (Sopen a hab.1) (Topen b hab.2)
end

end is_GÎ´

/-- A set `s` is called *residual* if it includes a dense `GÎ´` set. If `Î±` is a Baire space
(e.g., a complete metric space), then residual sets form a filter, see `mem_residual`.

 For technical reasons we define the filter `residual` in any topological space
 but in a non-Baire space it is not useful because it may contain some non-residual
 sets. -/
def residual (Î± : Type*) [topological_space Î±] : filter Î± :=
â¨… t (ht : is_GÎ´ t) (ht' : closure t = univ), ğ“Ÿ t

section Baire_theorem
open emetric ennreal
variables [emetric_space Î±] [complete_space Î±]

/-- Baire theorem: a countable intersection of dense open sets is dense. Formulated here when
the source space is â„• (and subsumed below by `dense_Inter_of_open` working with any
encodable source space). -/
theorem dense_Inter_of_open_nat {f : â„• â†’ set Î±} (ho : âˆ€n, is_open (f n))
  (hd : âˆ€n, closure (f n) = univ) : closure (â‹‚n, f n) = univ :=
begin
  let B : â„• â†’ ennreal := Î»n, 1/2^n,
  have Bpos : âˆ€n, 0 < B n,
  { intro n,
    simp only [B, div_def, one_mul, ennreal.inv_pos],
    exact pow_ne_top two_ne_top },
  /- Translate the density assumption into two functions `center` and `radius` associating
  to any n, x, Î´, Î´pos a center and a positive radius such that
  `closed_ball center radius` is included both in `f n` and in `closed_ball x Î´`.
  We can also require `radius â‰¤ (1/2)^(n+1), to ensure we get a Cauchy sequence later. -/
  have : âˆ€n x Î´, âˆƒy r, Î´ > 0 â†’ (r > 0 âˆ§ r â‰¤ B (n+1) âˆ§ closed_ball y r âŠ† (closed_ball x Î´) âˆ© f n),
  { assume n x Î´,
    by_cases Î´pos : Î´ > 0,
    { have : x âˆˆ closure (f n) := by simpa only [(hd n).symm] using mem_univ x,
      rcases emetric.mem_closure_iff.1 this (Î´/2) (ennreal.half_pos Î´pos) with âŸ¨y, ys, xyâŸ©,
      rw edist_comm at xy,
      obtain âŸ¨r, rpos, hrâŸ© : âˆƒ r > 0, closed_ball y r âŠ† f n :=
        nhds_basis_closed_eball.mem_iff.1 (is_open_iff_mem_nhds.1 (ho n) y ys),
      refine âŸ¨y, min (min (Î´/2) r) (B (n+1)), Î»_, âŸ¨_, _, Î»z hz, âŸ¨_, _âŸ©âŸ©âŸ©,
      show 0 < min (min (Î´ / 2) r) (B (n+1)),
        from lt_min (lt_min (ennreal.half_pos Î´pos) rpos) (Bpos (n+1)),
      show min (min (Î´ / 2) r) (B (n+1)) â‰¤ B (n+1), from min_le_right _ _,
      show z âˆˆ closed_ball x Î´, from calc
        edist z x â‰¤ edist z y + edist y x : edist_triangle _ _ _
        ... â‰¤ (min (min (Î´ / 2) r) (B (n+1))) + (Î´/2) : add_le_add hz (le_of_lt xy)
        ... â‰¤ Î´/2 + Î´/2 : add_le_add (le_trans (min_le_left _ _) (min_le_left _ _)) (le_refl _)
        ... = Î´ : ennreal.add_halves Î´,
      show z âˆˆ f n, from hr (calc
        edist z y â‰¤ min (min (Î´ / 2) r) (B (n+1)) : hz
        ... â‰¤ r : le_trans (min_le_left _ _) (min_le_right _ _)) },
    { use [x, 0] }},
  choose center radius H using this,

  refine subset.antisymm (subset_univ _) (Î»x hx, _),
  refine (mem_closure_iff_nhds_basis nhds_basis_closed_eball).2 (Î» Îµ Îµpos, _),
  /- Îµ is positive. We have to find a point in the ball of radius Îµ around x belonging to all `f n`.
  For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed ball
  `closed_ball (c n) (r n)` is included in the previous ball and in `f n`, and such that
  `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
  limit which belongs to all the `f n`. -/
  let F : â„• â†’ (Î± Ã— ennreal) := Î»n, nat.rec_on n (prod.mk x (min Îµ (B 0)))
                              (Î»n p, prod.mk (center n p.1 p.2) (radius n p.1 p.2)),
  let c : â„• â†’ Î± := Î»n, (F n).1,
  let r : â„• â†’ ennreal := Î»n, (F n).2,
  have rpos : âˆ€n, r n > 0,
  { assume n,
    induction n with n hn,
    exact lt_min Îµpos (Bpos 0),
    exact (H n (c n) (r n) hn).1 },
  have rB : âˆ€n, r n â‰¤ B n,
  { assume n,
    induction n with n hn,
    exact min_le_right _ _,
    exact (H n (c n) (r n) (rpos n)).2.1 },
  have incl : âˆ€n, closed_ball (c (n+1)) (r (n+1)) âŠ† (closed_ball (c n) (r n)) âˆ© (f n) :=
    Î»n, (H n (c n) (r n) (rpos n)).2.2,
  have cdist : âˆ€n, edist (c n) (c (n+1)) â‰¤ B n,
  { assume n,
    rw edist_comm,
    have A : c (n+1) âˆˆ closed_ball (c (n+1)) (r (n+1)) := mem_closed_ball_self,
    have I := calc
      closed_ball (c (n+1)) (r (n+1)) âŠ† closed_ball (c n) (r n) :
        subset.trans (incl n) (inter_subset_left _ _)
      ... âŠ† closed_ball (c n) (B n) : closed_ball_subset_closed_ball (rB n),
    exact I A },
  have : cauchy_seq c :=
    cauchy_seq_of_edist_le_geometric_two _ one_ne_top cdist,
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchy_seq_tendsto_of_complete this with âŸ¨y, ylimâŸ©,
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Îµ`.
  use y,
  simp only [exists_prop, set.mem_Inter],
  have I : âˆ€n, âˆ€m â‰¥ n, closed_ball (c m) (r m) âŠ† closed_ball (c n) (r n),
  { assume n,
    refine nat.le_induction _ (Î»m hnm h, _),
    { exact subset.refl _ },
    { exact subset.trans (incl m) (subset.trans (inter_subset_left _ _) h) }},
  have yball : âˆ€n, y âˆˆ closed_ball (c n) (r n),
  { assume n,
    refine mem_of_closed_of_tendsto (by simp) ylim is_closed_ball _,
    simp only [filter.mem_at_top_sets, nonempty_of_inhabited, set.mem_preimage],
    exact âŸ¨n, Î»m hm, I n m hm mem_closed_ball_selfâŸ© },
  split,
  show âˆ€n, y âˆˆ f n,
  { assume n,
    have : closed_ball (c (n+1)) (r (n+1)) âŠ† f n := subset.trans (incl n) (inter_subset_right _ _),
    exact this (yball (n+1)) },
  show edist y x â‰¤ Îµ, from le_trans (yball 0) (min_le_left _ _),
end

/-- Baire theorem: a countable intersection of dense open sets is dense. Formulated here with â‹‚â‚€. -/
theorem dense_sInter_of_open {S : set (set Î±)} (ho : âˆ€sâˆˆS, is_open s) (hS : countable S)
  (hd : âˆ€sâˆˆS, closure s = univ) : closure (â‹‚â‚€S) = univ :=
begin
  cases S.eq_empty_or_nonempty with h h,
  { simp [h] },
  { rcases hS.exists_surjective h with âŸ¨f, hfâŸ©,
    have F : âˆ€n, f n âˆˆ S := Î»n, by rw hf; exact mem_range_self _,
    rw [hf, sInter_range],
    exact dense_Inter_of_open_nat (Î»n, ho _ (F n)) (Î»n, hd _ (F n)) }
end

/-- Baire theorem: a countable intersection of dense open sets is dense. Formulated here with
an index set which is a countable set in any type. -/
theorem dense_bInter_of_open {S : set Î²} {f : Î² â†’ set Î±} (ho : âˆ€sâˆˆS, is_open (f s))
  (hS : countable S) (hd : âˆ€sâˆˆS, closure (f s) = univ) : closure (â‹‚sâˆˆS, f s) = univ :=
begin
  rw â† sInter_image,
  apply dense_sInter_of_open,
  { rwa ball_image_iff },
  { exact hS.image _ },
  { rwa ball_image_iff }
end

/-- Baire theorem: a countable intersection of dense open sets is dense. Formulated here with
an index set which is an encodable type. -/
theorem dense_Inter_of_open [encodable Î²] {f : Î² â†’ set Î±} (ho : âˆ€s, is_open (f s))
  (hd : âˆ€s, closure (f s) = univ) : closure (â‹‚s, f s) = univ :=
begin
  rw â† sInter_range,
  apply dense_sInter_of_open,
  { rwa forall_range_iff },
  { exact countable_range _ },
  { rwa forall_range_iff }
end

/-- Baire theorem: a countable intersection of dense GÎ´ sets is dense. Formulated here with â‹‚â‚€. -/
theorem dense_sInter_of_GÎ´ {S : set (set Î±)} (ho : âˆ€sâˆˆS, is_GÎ´ s) (hS : countable S)
  (hd : âˆ€sâˆˆS, closure s = univ) : closure (â‹‚â‚€S) = univ :=
begin
  -- the result follows from the result for a countable intersection of dense open sets,
  -- by rewriting each set as a countable intersection of open sets, which are of course dense.
  choose T hT using ho,
  have : â‹‚â‚€ S = â‹‚â‚€ (â‹ƒsâˆˆS, T s â€¹_â€º) := (sInter_bUnion (Î»s hs, (hT s hs).2.2)).symm,
  rw this,
  refine dense_sInter_of_open _ (hS.bUnion (Î»s hs, (hT s hs).2.1)) _;
    simp only [set.mem_Union, exists_prop]; rintro t âŸ¨s, hs, tTsâŸ©,
  show is_open t,
  { exact (hT s hs).1 t tTs },
  show closure t = univ,
  { apply eq_univ_of_univ_subset,
    rw [â† hd s hs, (hT s hs).2.2],
    exact closure_mono (sInter_subset_of_mem tTs) }
end

/-- Baire theorem: a countable intersection of dense GÎ´ sets is dense. Formulated here with
an index set which is an encodable type. -/
theorem dense_Inter_of_GÎ´ [encodable Î²] {f : Î² â†’ set Î±} (ho : âˆ€s, is_GÎ´ (f s))
  (hd : âˆ€s, closure (f s) = univ) : closure (â‹‚s, f s) = univ :=
begin
  rw â† sInter_range,
  exact dense_sInter_of_GÎ´ (forall_range_iff.2 â€¹_â€º) (countable_range _) (forall_range_iff.2 â€¹_â€º)
end

/-- Baire theorem: a countable intersection of dense GÎ´ sets is dense. Formulated here with
an index set which is a countable set in any type. -/
theorem dense_bInter_of_GÎ´ {S : set Î²} {f : Î  x âˆˆ S, set Î±} (ho : âˆ€sâˆˆS, is_GÎ´ (f s â€¹_â€º))
  (hS : countable S) (hd : âˆ€sâˆˆS, closure (f s â€¹_â€º) = univ) : closure (â‹‚sâˆˆS, f s â€¹_â€º) = univ :=
begin
  rw bInter_eq_Inter,
  haveI := hS.to_encodable,
  exact dense_Inter_of_GÎ´ (Î» s, ho s s.2) (Î» s, hd s s.2)
end

/-- Baire theorem: the intersection of two dense GÎ´ sets is dense. -/
theorem dense_inter_of_GÎ´ {s t : set Î±} (hs : is_GÎ´ s) (ht : is_GÎ´ t) (hsc : closure s = univ)
  (htc : closure t = univ) :
  closure (s âˆ© t) = univ :=
begin
  rw [inter_eq_Inter],
  apply dense_Inter_of_GÎ´; simp [bool.forall_bool, *]
end

/-- A property holds on a residual (comeagre) set if and only if it holds on some dense `GÎ´` set. -/
lemma eventually_residual {p : Î± â†’ Prop} :
  (âˆ€á¶  x in residual Î±, p x) â†” âˆƒ (t : set Î±), is_GÎ´ t âˆ§ closure t = univ âˆ§ âˆ€ x âˆˆ t, p x :=
calc (âˆ€á¶  x in residual Î±, p x) â†”
  âˆ€á¶  x in â¨… (t : set Î±) (ht : is_GÎ´ t âˆ§ closure t = univ), ğ“Ÿ t, p x :
    by simp only [residual, infi_and]
... â†” âˆƒ (t : set Î±) (ht : is_GÎ´ t âˆ§ closure t = univ), âˆ€á¶  x in ğ“Ÿ t, p x :
  mem_binfi (Î» tâ‚ hâ‚ tâ‚‚ hâ‚‚, âŸ¨tâ‚ âˆ© tâ‚‚, âŸ¨hâ‚.1.inter hâ‚‚.1, dense_inter_of_GÎ´ hâ‚.1 hâ‚‚.1 hâ‚.2 hâ‚‚.2âŸ©,
    by simpâŸ©) âŸ¨univ, is_GÎ´_univ, closure_univâŸ©
... â†” _ : by simp [and_assoc]

/-- A set is residual (comeagre) if and only if it includes a dense `GÎ´` set. -/
lemma mem_residual {s : set Î±} :
  s âˆˆ residual Î± â†” âˆƒ t âŠ† s, is_GÎ´ t âˆ§ closure t = univ :=
(@eventually_residual Î± _ _ (Î» x, x âˆˆ s)).trans $ exists_congr $
Î» t, by rw [exists_prop, and_comm (t âŠ† s), subset_def, and_assoc]

instance : countable_Inter_filter (residual Î±) :=
âŸ¨begin
  intros S hSc hS,
  simp only [mem_residual] at *,
  choose T hTs hT using hS,
  refine âŸ¨â‹‚ s âˆˆ S, T s â€¹_â€º, _, _, _âŸ©,
  { rw [sInter_eq_bInter],
    exact Inter_subset_Inter (Î» s, Inter_subset_Inter $ hTs s) },
  { exact is_GÎ´_bInter hSc (Î» s hs, (hT s hs).1) },
  { exact dense_bInter_of_GÎ´ (Î» s hs, (hT s hs).1) hSc (Î» s hs, (hT s hs).2) }
endâŸ©

/-- Baire theorem: if countably many closed sets cover the whole space, then their interiors
are dense. Formulated here with an index set which is a countable set in any type. -/
theorem dense_bUnion_interior_of_closed {S : set Î²} {f : Î² â†’ set Î±} (hc : âˆ€sâˆˆS, is_closed (f s))
  (hS : countable S) (hU : (â‹ƒsâˆˆS, f s) = univ) : closure (â‹ƒsâˆˆS, interior (f s)) = univ :=
begin
  let g := Î»s, (frontier (f s))á¶œ,
  have clos_g : closure (â‹‚sâˆˆS, g s) = univ,
  { refine dense_bInter_of_open (Î»s hs, _) hS (Î»s hs, _),
    show is_open (g s), from is_open_compl_iff.2 is_closed_frontier,
    show closure (g s) = univ,
    { apply subset.antisymm (subset_univ _),
      simp [interior_frontier (hc s hs)] }},
  have : (â‹‚sâˆˆS, g s) âŠ† (â‹ƒsâˆˆS, interior (f s)),
  { assume x hx,
    have : x âˆˆ â‹ƒsâˆˆS, f s, { have := mem_univ x, rwa â† hU at this },
    rcases mem_bUnion_iff.1 this with âŸ¨s, hs, xsâŸ©,
    have : x âˆˆ g s := mem_bInter_iff.1 hx s hs,
    have : x âˆˆ interior (f s),
    { have : x âˆˆ f s \ (frontier (f s)) := mem_inter xs this,
      simpa [frontier, xs, (hc s hs).closure_eq] using this },
    exact mem_bUnion_iff.2 âŸ¨s, âŸ¨hs, thisâŸ©âŸ© },
  have := closure_mono this,
  rw clos_g at this,
  exact subset.antisymm (subset_univ _) this
end

/-- Baire theorem: if countably many closed sets cover the whole space, then their interiors
are dense. Formulated here with â‹ƒâ‚€. -/
theorem dense_sUnion_interior_of_closed {S : set (set Î±)} (hc : âˆ€sâˆˆS, is_closed s)
  (hS : countable S) (hU : (â‹ƒâ‚€ S) = univ) : closure (â‹ƒsâˆˆS, interior s) = univ :=
by rw sUnion_eq_bUnion at hU; exact dense_bUnion_interior_of_closed hc hS hU

/-- Baire theorem: if countably many closed sets cover the whole space, then their interiors
are dense. Formulated here with an index set which is an encodable type. -/
theorem dense_Union_interior_of_closed [encodable Î²] {f : Î² â†’ set Î±} (hc : âˆ€s, is_closed (f s))
  (hU : (â‹ƒs, f s) = univ) : closure (â‹ƒs, interior (f s)) = univ :=
begin
  rw â† bUnion_univ,
  apply dense_bUnion_interior_of_closed,
  { simp [hc] },
  { apply countable_encodable },
  { rwa â† bUnion_univ at hU }
end

/-- One of the most useful consequences of Baire theorem: if a countable union of closed sets
covers the space, then one of the sets has nonempty interior. -/
theorem nonempty_interior_of_Union_of_closed [nonempty Î±] [encodable Î²] {f : Î² â†’ set Î±}
  (hc : âˆ€s, is_closed (f s)) (hU : (â‹ƒs, f s) = univ) :
  âˆƒs, (interior $ f s).nonempty :=
begin
  by_contradiction h,
  simp only [not_exists, not_nonempty_iff_eq_empty] at h,
  have := calc âˆ… = closure (â‹ƒs, interior (f s)) : by simp [h]
             ... = univ : dense_Union_interior_of_closed hc hU,
  exact univ_nonempty.ne_empty this.symm
end

end Baire_theorem
