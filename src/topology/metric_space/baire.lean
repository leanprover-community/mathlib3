/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.specific_limits
import order.filter.countable_Inter
import topology.G_delta

/-!
# Baire theorem

In a complete metric space, a countable intersection of dense open subsets is dense.

The good concept underlying the theorem is that of a GÎ´ set, i.e., a countable intersection
of open sets. Then Baire theorem can also be formulated as the fact that a countable
intersection of dense GÎ´ sets is a dense GÎ´ set. We prove Baire theorem, giving several different
formulations that can be handy. We also prove the important consequence that, if the space is
covered by a countable union of closed sets, then the union of their interiors is dense.

The names of the theorems do not contain the string "Baire", but are instead built from the form of
the statement. "Baire" is however in the docstring of all the theorems, to facilitate grep searches.

We also define the filter `residual Î±` generated by dense `GÎ´` sets and prove that this filter
has the countable intersection property.
-/

noncomputable theory
open_locale classical topological_space filter ennreal

open filter encodable set

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î¹ : Type*}

section Baire_theorem
open emetric ennreal
variables [emetric_space Î±] [complete_space Î±]

/-- Baire theorem: a countable intersection of dense open sets is dense. Formulated here when
the source space is â„• (and subsumed below by `dense_Inter_of_open` working with any
encodable source space). -/
theorem dense_Inter_of_open_nat {f : â„• â†’ set Î±} (ho : âˆ€n, is_open (f n))
  (hd : âˆ€n, dense (f n)) : dense (â‹‚n, f n) :=
begin
  let B : â„• â†’ â„â‰¥0âˆ := Î»n, 1/2^n,
  have Bpos : âˆ€n, 0 < B n,
  { intro n,
    simp only [B, one_div, one_mul, ennreal.inv_pos],
    exact pow_ne_top two_ne_top },
  /- Translate the density assumption into two functions `center` and `radius` associating
  to any n, x, Î´, Î´pos a center and a positive radius such that
  `closed_ball center radius` is included both in `f n` and in `closed_ball x Î´`.
  We can also require `radius â‰¤ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have : âˆ€n x Î´, Î´ > 0 â†’ âˆƒy r, r > 0 âˆ§ r â‰¤ B (n+1) âˆ§ closed_ball y r âŠ† (closed_ball x Î´) âˆ© f n,
  { assume n x Î´ Î´pos,
    have : x âˆˆ closure (f n) := hd n x,
    rcases emetric.mem_closure_iff.1 this (Î´/2) (ennreal.half_pos Î´pos) with âŸ¨y, ys, xyâŸ©,
    rw edist_comm at xy,
    obtain âŸ¨r, rpos, hrâŸ© : âˆƒ r > 0, closed_ball y r âŠ† f n :=
      nhds_basis_closed_eball.mem_iff.1 (is_open_iff_mem_nhds.1 (ho n) y ys),
    refine âŸ¨y, min (min (Î´/2) r) (B (n+1)), _, _, Î»z hz, âŸ¨_, _âŸ©âŸ©,
    show 0 < min (min (Î´ / 2) r) (B (n+1)),
      from lt_min (lt_min (ennreal.half_pos Î´pos) rpos) (Bpos (n+1)),
    show min (min (Î´ / 2) r) (B (n+1)) â‰¤ B (n+1), from min_le_right _ _,
    show z âˆˆ closed_ball x Î´, from calc
      edist z x â‰¤ edist z y + edist y x : edist_triangle _ _ _
      ... â‰¤ (min (min (Î´ / 2) r) (B (n+1))) + (Î´/2) : add_le_add hz (le_of_lt xy)
      ... â‰¤ Î´/2 + Î´/2 : add_le_add (le_trans (min_le_left _ _) (min_le_left _ _)) (le_refl _)
      ... = Î´ : ennreal.add_halves Î´,
    show z âˆˆ f n, from hr (calc
      edist z y â‰¤ min (min (Î´ / 2) r) (B (n+1)) : hz
      ... â‰¤ r : le_trans (min_le_left _ _) (min_le_right _ _)) },
  choose! center radius H using this,
  refine Î» x, (mem_closure_iff_nhds_basis nhds_basis_closed_eball).2 (Î» Îµ Îµpos, _),
  /- `Îµ` is positive. We have to find a point in the ball of radius `Îµ` around `x` belonging to all
  `f n`. For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed ball
  `closed_ball (c n) (r n)` is included in the previous ball and in `f n`, and such that
  `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
  limit which belongs to all the `f n`. -/
  let F : â„• â†’ (Î± Ã— â„â‰¥0âˆ) := Î»n, nat.rec_on n (prod.mk x (min Îµ (B 0)))
                              (Î»n p, prod.mk (center n p.1 p.2) (radius n p.1 p.2)),
  let c : â„• â†’ Î± := Î»n, (F n).1,
  let r : â„• â†’ â„â‰¥0âˆ := Î»n, (F n).2,
  have rpos : âˆ€n, r n > 0,
  { assume n,
    induction n with n hn,
    exact lt_min Îµpos (Bpos 0),
    exact (H n (c n) (r n) hn).1 },
  have rB : âˆ€n, r n â‰¤ B n,
  { assume n,
    induction n with n hn,
    exact min_le_right _ _,
    exact (H n (c n) (r n) (rpos n)).2.1 },
  have incl : âˆ€n, closed_ball (c (n+1)) (r (n+1)) âŠ† (closed_ball (c n) (r n)) âˆ© (f n) :=
    Î»n, (H n (c n) (r n) (rpos n)).2.2,
  have cdist : âˆ€n, edist (c n) (c (n+1)) â‰¤ B n,
  { assume n,
    rw edist_comm,
    have A : c (n+1) âˆˆ closed_ball (c (n+1)) (r (n+1)) := mem_closed_ball_self,
    have I := calc
      closed_ball (c (n+1)) (r (n+1)) âŠ† closed_ball (c n) (r n) :
        subset.trans (incl n) (inter_subset_left _ _)
      ... âŠ† closed_ball (c n) (B n) : closed_ball_subset_closed_ball (rB n),
    exact I A },
  have : cauchy_seq c :=
    cauchy_seq_of_edist_le_geometric_two _ one_ne_top cdist,
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchy_seq_tendsto_of_complete this with âŸ¨y, ylimâŸ©,
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Îµ`.
  use y,
  simp only [exists_prop, set.mem_Inter],
  have I : âˆ€n, âˆ€m â‰¥ n, closed_ball (c m) (r m) âŠ† closed_ball (c n) (r n),
  { assume n,
    refine nat.le_induction _ (Î»m hnm h, _),
    { exact subset.refl _ },
    { exact subset.trans (incl m) (subset.trans (inter_subset_left _ _) h) }},
  have yball : âˆ€n, y âˆˆ closed_ball (c n) (r n),
  { assume n,
    refine is_closed_ball.mem_of_tendsto ylim _,
    refine (filter.eventually_ge_at_top n).mono (Î» m hm, _),
    exact I n m hm mem_closed_ball_self },
  split,
  show âˆ€n, y âˆˆ f n,
  { assume n,
    have : closed_ball (c (n+1)) (r (n+1)) âŠ† f n := subset.trans (incl n) (inter_subset_right _ _),
    exact this (yball (n+1)) },
  show edist y x â‰¤ Îµ, from le_trans (yball 0) (min_le_left _ _),
end

/-- Baire theorem: a countable intersection of dense open sets is dense. Formulated here with â‹‚â‚€. -/
theorem dense_sInter_of_open {S : set (set Î±)} (ho : âˆ€sâˆˆS, is_open s) (hS : countable S)
  (hd : âˆ€sâˆˆS, dense s) : dense (â‹‚â‚€S) :=
begin
  cases S.eq_empty_or_nonempty with h h,
  { simp [h] },
  { rcases hS.exists_surjective h with âŸ¨f, hfâŸ©,
    have F : âˆ€n, f n âˆˆ S := Î»n, by rw hf; exact mem_range_self _,
    rw [hf, sInter_range],
    exact dense_Inter_of_open_nat (Î»n, ho _ (F n)) (Î»n, hd _ (F n)) }
end

/-- Baire theorem: a countable intersection of dense open sets is dense. Formulated here with
an index set which is a countable set in any type. -/
theorem dense_bInter_of_open {S : set Î²} {f : Î² â†’ set Î±} (ho : âˆ€sâˆˆS, is_open (f s))
  (hS : countable S) (hd : âˆ€sâˆˆS, dense (f s)) : dense (â‹‚sâˆˆS, f s) :=
begin
  rw â† sInter_image,
  apply dense_sInter_of_open,
  { rwa ball_image_iff },
  { exact hS.image _ },
  { rwa ball_image_iff }
end

/-- Baire theorem: a countable intersection of dense open sets is dense. Formulated here with
an index set which is an encodable type. -/
theorem dense_Inter_of_open [encodable Î²] {f : Î² â†’ set Î±} (ho : âˆ€s, is_open (f s))
  (hd : âˆ€s, dense (f s)) : dense (â‹‚s, f s) :=
begin
  rw â† sInter_range,
  apply dense_sInter_of_open,
  { rwa forall_range_iff },
  { exact countable_range _ },
  { rwa forall_range_iff }
end

/-- Baire theorem: a countable intersection of dense GÎ´ sets is dense. Formulated here with â‹‚â‚€. -/
theorem dense_sInter_of_GÎ´ {S : set (set Î±)} (ho : âˆ€sâˆˆS, is_GÎ´ s) (hS : countable S)
  (hd : âˆ€sâˆˆS, dense s) : dense (â‹‚â‚€S) :=
begin
  -- the result follows from the result for a countable intersection of dense open sets,
  -- by rewriting each set as a countable intersection of open sets, which are of course dense.
  choose T hT using ho,
  have : â‹‚â‚€ S = â‹‚â‚€ (â‹ƒsâˆˆS, T s â€¹_â€º) := (sInter_bUnion (Î»s hs, (hT s hs).2.2)).symm,
  rw this,
  refine dense_sInter_of_open _ (hS.bUnion (Î»s hs, (hT s hs).2.1)) _;
    simp only [set.mem_Union, exists_prop]; rintro t âŸ¨s, hs, tTsâŸ©,
  show is_open t,
  { exact (hT s hs).1 t tTs },
  show dense t,
  { intro x,
    have := hd s hs x,
    rw (hT s hs).2.2 at this,
    exact closure_mono (sInter_subset_of_mem tTs) this }
end

/-- Baire theorem: a countable intersection of dense GÎ´ sets is dense. Formulated here with
an index set which is an encodable type. -/
theorem dense_Inter_of_GÎ´ [encodable Î²] {f : Î² â†’ set Î±} (ho : âˆ€s, is_GÎ´ (f s))
  (hd : âˆ€s, dense (f s)) : dense (â‹‚s, f s) :=
begin
  rw â† sInter_range,
  exact dense_sInter_of_GÎ´ (forall_range_iff.2 â€¹_â€º) (countable_range _) (forall_range_iff.2 â€¹_â€º)
end

/-- Baire theorem: a countable intersection of dense GÎ´ sets is dense. Formulated here with
an index set which is a countable set in any type. -/
theorem dense_bInter_of_GÎ´ {S : set Î²} {f : Î  x âˆˆ S, set Î±} (ho : âˆ€sâˆˆS, is_GÎ´ (f s â€¹_â€º))
  (hS : countable S) (hd : âˆ€sâˆˆS, dense (f s â€¹_â€º)) : dense (â‹‚sâˆˆS, f s â€¹_â€º) :=
begin
  rw bInter_eq_Inter,
  haveI := hS.to_encodable,
  exact dense_Inter_of_GÎ´ (Î» s, ho s s.2) (Î» s, hd s s.2)
end

/-- Baire theorem: the intersection of two dense GÎ´ sets is dense. -/
theorem dense.inter_of_GÎ´ {s t : set Î±} (hs : is_GÎ´ s) (ht : is_GÎ´ t) (hsc : dense s)
  (htc : dense t) :
  dense (s âˆ© t) :=
begin
  rw [inter_eq_Inter],
  apply dense_Inter_of_GÎ´; simp [bool.forall_bool, *]
end

/-- A property holds on a residual (comeagre) set if and only if it holds on some dense `GÎ´` set. -/
lemma eventually_residual {p : Î± â†’ Prop} :
  (âˆ€á¶  x in residual Î±, p x) â†” âˆƒ (t : set Î±), is_GÎ´ t âˆ§ dense t âˆ§ âˆ€ x âˆˆ t, p x :=
calc (âˆ€á¶  x in residual Î±, p x) â†”
  âˆ€á¶  x in â¨… (t : set Î±) (ht : is_GÎ´ t âˆ§ dense t), ğ“Ÿ t, p x :
    by simp only [residual, infi_and]
... â†” âˆƒ (t : set Î±) (ht : is_GÎ´ t âˆ§ dense t), âˆ€á¶  x in ğ“Ÿ t, p x : mem_binfi_of_directed
    (Î» tâ‚ hâ‚ tâ‚‚ hâ‚‚, âŸ¨tâ‚ âˆ© tâ‚‚, âŸ¨hâ‚.1.inter hâ‚‚.1, dense.inter_of_GÎ´ hâ‚.1 hâ‚‚.1 hâ‚.2 hâ‚‚.2âŸ©, by simpâŸ©)
    âŸ¨univ, is_GÎ´_univ, dense_univâŸ©
... â†” _ : by simp [and_assoc]

/-- A set is residual (comeagre) if and only if it includes a dense `GÎ´` set. -/
lemma mem_residual {s : set Î±} : s âˆˆ residual Î± â†” âˆƒ t âŠ† s, is_GÎ´ t âˆ§ dense t :=
(@eventually_residual Î± _ _ (Î» x, x âˆˆ s)).trans $ exists_congr $
Î» t, by rw [exists_prop, and_comm (t âŠ† s), subset_def, and_assoc]

instance : countable_Inter_filter (residual Î±) :=
âŸ¨begin
  intros S hSc hS,
  simp only [mem_residual] at *,
  choose T hTs hT using hS,
  refine âŸ¨â‹‚ s âˆˆ S, T s â€¹_â€º, _, _, _âŸ©,
  { rw [sInter_eq_bInter],
    exact Inter_subset_Inter (Î» s, Inter_subset_Inter $ hTs s) },
  { exact is_GÎ´_bInter hSc (Î» s hs, (hT s hs).1) },
  { exact dense_bInter_of_GÎ´ (Î» s hs, (hT s hs).1) hSc (Î» s hs, (hT s hs).2) }
endâŸ©

/-- Baire theorem: if countably many closed sets cover the whole space, then their interiors
are dense. Formulated here with an index set which is a countable set in any type. -/
theorem dense_bUnion_interior_of_closed {S : set Î²} {f : Î² â†’ set Î±} (hc : âˆ€sâˆˆS, is_closed (f s))
  (hS : countable S) (hU : (â‹ƒsâˆˆS, f s) = univ) : dense (â‹ƒsâˆˆS, interior (f s)) :=
begin
  let g := Î»s, (frontier (f s))á¶œ,
  have : dense (â‹‚sâˆˆS, g s),
  { refine dense_bInter_of_open (Î»s hs, _) hS (Î»s hs, _),
    show is_open (g s), from is_open_compl_iff.2 is_closed_frontier,
    show dense (g s),
    { intro x,
      simp [interior_frontier (hc s hs)] }},
  refine this.mono _,
  show (â‹‚sâˆˆS, g s) âŠ† (â‹ƒsâˆˆS, interior (f s)),
  assume x hx,
  have : x âˆˆ â‹ƒsâˆˆS, f s, { have := mem_univ x, rwa â† hU at this },
  rcases mem_bUnion_iff.1 this with âŸ¨s, hs, xsâŸ©,
  have : x âˆˆ g s := mem_bInter_iff.1 hx s hs,
  have : x âˆˆ interior (f s),
  { have : x âˆˆ f s \ (frontier (f s)) := mem_inter xs this,
    simpa [frontier, xs, (hc s hs).closure_eq] using this },
  exact mem_bUnion_iff.2 âŸ¨s, âŸ¨hs, thisâŸ©âŸ©
end

/-- Baire theorem: if countably many closed sets cover the whole space, then their interiors
are dense. Formulated here with `â‹ƒâ‚€`. -/
theorem dense_sUnion_interior_of_closed {S : set (set Î±)} (hc : âˆ€sâˆˆS, is_closed s)
  (hS : countable S) (hU : (â‹ƒâ‚€ S) = univ) : dense (â‹ƒsâˆˆS, interior s) :=
by rw sUnion_eq_bUnion at hU; exact dense_bUnion_interior_of_closed hc hS hU

/-- Baire theorem: if countably many closed sets cover the whole space, then their interiors
are dense. Formulated here with an index set which is an encodable type. -/
theorem dense_Union_interior_of_closed [encodable Î²] {f : Î² â†’ set Î±} (hc : âˆ€s, is_closed (f s))
  (hU : (â‹ƒs, f s) = univ) : dense (â‹ƒs, interior (f s)) :=
begin
  rw â† bUnion_univ,
  apply dense_bUnion_interior_of_closed,
  { simp [hc] },
  { apply countable_encodable },
  { rwa â† bUnion_univ at hU }
end

/-- One of the most useful consequences of Baire theorem: if a countable union of closed sets
covers the space, then one of the sets has nonempty interior. -/
theorem nonempty_interior_of_Union_of_closed [nonempty Î±] [encodable Î²] {f : Î² â†’ set Î±}
  (hc : âˆ€s, is_closed (f s)) (hU : (â‹ƒs, f s) = univ) :
  âˆƒs, (interior $ f s).nonempty :=
begin
  by_contradiction h,
  simp only [not_exists, not_nonempty_iff_eq_empty] at h,
  have := calc âˆ… = closure (â‹ƒs, interior (f s)) : by simp [h]
             ... = univ : (dense_Union_interior_of_closed hc hU).closure_eq,
  exact univ_nonempty.ne_empty this.symm
end

end Baire_theorem
