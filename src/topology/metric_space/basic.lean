/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/

import data.int.interval
import topology.algebra.ordered.compact
import topology.metric_space.emetric_space

/-!
# Metric spaces

This file defines metric spaces. Many definitions and theorems expected
on metric spaces are already introduced on uniform spaces and topological spaces.
For example: open and closed sets, compactness, completeness, continuity and uniform continuity

## Main definitions

* `has_dist Î±`: Endows a space `Î±` with a function `dist a b`.
* `pseudo_metric_space Î±`: A space endowed with a distance function, which can
  be zero even if the two elements are non-equal.
* `metric.ball x Îµ`: The set of all points `y` with `dist y x < Îµ`.
* `metric.bounded s`: Whether a subset of a `pseudo_metric_space` is bounded.
* `metric_space Î±`: A `pseudo_metric_space` with the guarantee `dist x y = 0 â†’ x = y`.

Additional useful definitions:

* `nndist a b`: `dist` as a function to the non-negative reals.
* `metric.closed_ball x Îµ`: The set of all points `y` with `dist y x â‰¤ Îµ`.
* `metric.sphere x Îµ`: The set of all points `y` with `dist y x = Îµ`.
* `proper_space Î±`: A `pseudo_metric_space` where all closed balls are compact.
* `metric.diam s` : The `supr` of the distances of members of `s`.
  Defined in terms of `emetric.diam`, for better handling of the case when it should be infinite.

TODO (anyone): Add "Main results" section.

## Implementation notes

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory of `pseudo_metric_space`, where we don't require `dist x y = 0 â†’ x = y` and we specialize
to `metric_space` at the end.

## Tags

metric, pseudo_metric, dist
-/

open set filter topological_space
noncomputable theory

open_locale uniformity topological_space big_operators filter nnreal ennreal

universes u v w
variables {Î± : Type u} {Î² : Type v}

/-- Construct a uniform structure core from a distance function and metric space axioms.
This is a technical construction that can be immediately used to construct a uniform structure
from a distance function and metric space axioms but is also useful when discussing
metrizable topologies, see `pseudo_metric_space.of_metrizable`. -/
def uniform_space.core_of_dist {Î± : Type*} (dist : Î± â†’ Î± â†’ â„)
  (dist_self : âˆ€ x : Î±, dist x x = 0)
  (dist_comm : âˆ€ x y : Î±, dist x y = dist y x)
  (dist_triangle : âˆ€ x y z : Î±, dist x z â‰¤ dist x y + dist y z) : uniform_space.core Î± :=
{ uniformity := (â¨… Îµ>0, ğ“Ÿ {p:Î±Ã—Î± | dist p.1 p.2 < Îµ}),
  refl       := le_infi $ assume Îµ, le_infi $
    by simp [set.subset_def, id_rel, dist_self, (>)] {contextual := tt},
  comp       := le_infi $ assume Îµ, le_infi $ assume h, lift'_le
    (mem_infi_of_mem (Îµ / 2) $ mem_infi_of_mem (div_pos h zero_lt_two) (subset.refl _)) $
    have âˆ€ (a b c : Î±), dist a c < Îµ / 2 â†’ dist c b < Îµ / 2 â†’ dist a b < Îµ,
      from assume a b c hac hcb,
      calc dist a b â‰¤ dist a c + dist c b : dist_triangle _ _ _
        ... < Îµ / 2 + Îµ / 2 : add_lt_add hac hcb
        ... = Îµ : by rw [div_add_div_same, add_self_div_two],
    by simpa [comp_rel],
  symm       := tendsto_infi.2 $ assume Îµ, tendsto_infi.2 $ assume h,
    tendsto_infi' Îµ $ tendsto_infi' h $ tendsto_principal_principal.2 $ by simp [dist_comm] }

/-- Construct a uniform structure from a distance function and metric space axioms -/
def uniform_space_of_dist
  (dist : Î± â†’ Î± â†’ â„)
  (dist_self : âˆ€ x : Î±, dist x x = 0)
  (dist_comm : âˆ€ x y : Î±, dist x y = dist y x)
  (dist_triangle : âˆ€ x y z : Î±, dist x z â‰¤ dist x y + dist y z) : uniform_space Î± :=
uniform_space.of_core (uniform_space.core_of_dist dist dist_self dist_comm dist_triangle)

/-- The distance function (given an ambient metric space on `Î±`), which returns
  a nonnegative real number `dist x y` given `x y : Î±`. -/
class has_dist (Î± : Type*) := (dist : Î± â†’ Î± â†’ â„)

export has_dist (dist)

-- the uniform structure and the emetric space structure are embedded in the metric space structure
-- to avoid instance diamond issues. See Note [forgetful inheritance].

/-- Metric space

Each metric space induces a canonical `uniform_space` and hence a canonical `topological_space`.
This is enforced in the type class definition, by extending the `uniform_space` structure. When
instantiating a `metric_space` structure, the uniformity fields are not necessary, they will be
filled in by default. In the same way, each metric space induces an emetric space structure.
It is included in the structure, but filled in by default.
-/
class pseudo_metric_space (Î± : Type u) extends has_dist Î± : Type u :=
(dist_self : âˆ€ x : Î±, dist x x = 0)
(dist_comm : âˆ€ x y : Î±, dist x y = dist y x)
(dist_triangle : âˆ€ x y z : Î±, dist x z â‰¤ dist x y + dist y z)
(edist : Î± â†’ Î± â†’ â„â‰¥0âˆ := Î»x y, ennreal.of_real (dist x y))
(edist_dist : âˆ€ x y : Î±, edist x y = ennreal.of_real (dist x y) . control_laws_tac)
(to_uniform_space : uniform_space Î± := uniform_space_of_dist dist dist_self dist_comm dist_triangle)
(uniformity_dist : ğ“¤ Î± = â¨… Îµ>0, ğ“Ÿ {p:Î±Ã—Î± | dist p.1 p.2 < Îµ} . control_laws_tac)

variables [pseudo_metric_space Î±]

@[priority 100] -- see Note [lower instance priority]
instance metric_space.to_uniform_space' : uniform_space Î± :=
pseudo_metric_space.to_uniform_space

@[priority 200] -- see Note [lower instance priority]
instance pseudo_metric_space.to_has_edist : has_edist Î± := âŸ¨pseudo_metric_space.edistâŸ©

/-- Construct a pseudo-metric space structure whose underlying topological space structure
(definitionally) agrees which a pre-existing topology which is compatible with a given distance
function. -/
def pseudo_metric_space.of_metrizable {Î± : Type*} [topological_space Î±] (dist : Î± â†’ Î± â†’ â„)
  (dist_self : âˆ€ x : Î±, dist x x = 0)
  (dist_comm : âˆ€ x y : Î±, dist x y = dist y x)
  (dist_triangle : âˆ€ x y z : Î±, dist x z â‰¤ dist x y + dist y z)
  (H : âˆ€ s : set Î±, is_open s â†” âˆ€ x âˆˆ s, âˆƒ Îµ > 0, âˆ€ y, dist x y < Îµ â†’ y âˆˆ s) :
pseudo_metric_space Î± :=
{ dist := dist,
  dist_self := dist_self,
  dist_comm := dist_comm,
  dist_triangle := dist_triangle,
  to_uniform_space := { is_open_uniformity := begin
    dsimp only [uniform_space.core_of_dist],
    intros s,
    change is_open s â†” _,
    rw H s,
    apply forall_congr, intro x,
    apply forall_congr, intro x_in,
    erw (has_basis_binfi_principal _ nonempty_Ioi).mem_iff,
    { apply exists_congr, intros Îµ,
      apply exists_congr, intros Îµ_pos,
      simp only [prod.forall, set_of_subset_set_of],
      split,
      { rintros h _ y H rfl,
        exact h y H },
      { intros h y hxy,
        exact h _ _ hxy rfl } },
      { exact Î» r (hr : 0 < r) p (hp : 0 < p), âŸ¨min r p, lt_min hr hp,
        Î» x (hx : dist _ _ < _), lt_of_lt_of_le hx (min_le_left r p),
        Î» x (hx : dist _ _ < _), lt_of_lt_of_le hx (min_le_right r p)âŸ© },
      { apply_instance }
    end,
    ..uniform_space.core_of_dist dist dist_self dist_comm dist_triangle },
  uniformity_dist := rfl }

@[simp] theorem dist_self (x : Î±) : dist x x = 0 := pseudo_metric_space.dist_self x

theorem dist_comm (x y : Î±) : dist x y = dist y x := pseudo_metric_space.dist_comm x y

theorem edist_dist (x y : Î±) : edist x y = ennreal.of_real (dist x y) :=
pseudo_metric_space.edist_dist x y

theorem dist_triangle (x y z : Î±) : dist x z â‰¤ dist x y + dist y z :=
pseudo_metric_space.dist_triangle x y z

theorem dist_triangle_left (x y z : Î±) : dist x y â‰¤ dist z x + dist z y :=
by rw dist_comm z; apply dist_triangle

theorem dist_triangle_right (x y z : Î±) : dist x y â‰¤ dist x z + dist y z :=
by rw dist_comm y; apply dist_triangle

lemma dist_triangle4 (x y z w : Î±) :
  dist x w â‰¤ dist x y + dist y z + dist z w :=
calc dist x w â‰¤ dist x z + dist z w : dist_triangle x z w
          ... â‰¤ (dist x y + dist y z) + dist z w : add_le_add_right (dist_triangle x y z) _

lemma dist_triangle4_left (xâ‚ yâ‚ xâ‚‚ yâ‚‚ : Î±) :
  dist xâ‚‚ yâ‚‚ â‰¤ dist xâ‚ yâ‚ + (dist xâ‚ xâ‚‚ + dist yâ‚ yâ‚‚) :=
by { rw [add_left_comm, dist_comm xâ‚, â† add_assoc], apply dist_triangle4 }

lemma dist_triangle4_right (xâ‚ yâ‚ xâ‚‚ yâ‚‚ : Î±) :
  dist xâ‚ yâ‚ â‰¤ dist xâ‚ xâ‚‚ + dist yâ‚ yâ‚‚ + dist xâ‚‚ yâ‚‚ :=
by { rw [add_right_comm, dist_comm yâ‚], apply dist_triangle4 }

/-- The triangle (polygon) inequality for sequences of points; `finset.Ico` version. -/
lemma dist_le_Ico_sum_dist (f : â„• â†’ Î±) {m n} (h : m â‰¤ n) :
  dist (f m) (f n) â‰¤ âˆ‘ i in finset.Ico m n, dist (f i) (f (i + 1)) :=
begin
  revert n,
  apply nat.le_induction,
  { simp only [finset.sum_empty, finset.Ico_self, dist_self] },
  { assume n hn hrec,
    calc dist (f m) (f (n+1)) â‰¤ dist (f m) (f n) + dist _ _ : dist_triangle _ _ _
      ... â‰¤ âˆ‘ i in finset.Ico m n, _ + _ : add_le_add hrec (le_refl _)
      ... = âˆ‘ i in finset.Ico m (n+1), _ :
        by rw [nat.Ico_succ_right_eq_insert_Ico hn, finset.sum_insert, add_comm]; simp }
end

/-- The triangle (polygon) inequality for sequences of points; `finset.range` version. -/
lemma dist_le_range_sum_dist (f : â„• â†’ Î±) (n : â„•) :
  dist (f 0) (f n) â‰¤ âˆ‘ i in finset.range n, dist (f i) (f (i + 1)) :=
nat.Ico_zero_eq_range n â–¸ dist_le_Ico_sum_dist f (nat.zero_le n)

/-- A version of `dist_le_Ico_sum_dist` with each intermediate distance replaced
with an upper estimate. -/
lemma dist_le_Ico_sum_of_dist_le {f : â„• â†’ Î±} {m n} (hmn : m â‰¤ n)
  {d : â„• â†’ â„} (hd : âˆ€ {k}, m â‰¤ k â†’ k < n â†’ dist (f k) (f (k + 1)) â‰¤ d k) :
  dist (f m) (f n) â‰¤ âˆ‘ i in finset.Ico m n, d i :=
le_trans (dist_le_Ico_sum_dist f hmn) $
finset.sum_le_sum $ Î» k hk, hd (finset.mem_Ico.1 hk).1 (finset.mem_Ico.1 hk).2

/-- A version of `dist_le_range_sum_dist` with each intermediate distance replaced
with an upper estimate. -/
lemma dist_le_range_sum_of_dist_le {f : â„• â†’ Î±} (n : â„•)
  {d : â„• â†’ â„} (hd : âˆ€ {k}, k < n â†’ dist (f k) (f (k + 1)) â‰¤ d k) :
  dist (f 0) (f n) â‰¤ âˆ‘ i in finset.range n, d i :=
nat.Ico_zero_eq_range n â–¸ dist_le_Ico_sum_of_dist_le (zero_le n) (Î» _ _, hd)

theorem swap_dist : function.swap (@dist Î± _) = dist :=
by funext x y; exact dist_comm _ _

theorem abs_dist_sub_le (x y z : Î±) : |dist x z - dist y z| â‰¤ dist x y :=
abs_sub_le_iff.2
 âŸ¨sub_le_iff_le_add.2 (dist_triangle _ _ _),
  sub_le_iff_le_add.2 (dist_triangle_left _ _ _)âŸ©

theorem dist_nonneg {x y : Î±} : 0 â‰¤ dist x y :=
have 2 * dist x y â‰¥ 0,
  from calc 2 * dist x y = dist x y + dist y x : by rw [dist_comm x y, two_mul]
    ... â‰¥ 0 : by rw â† dist_self x; apply dist_triangle,
nonneg_of_mul_nonneg_left this zero_lt_two

@[simp] theorem abs_dist {a b : Î±} : |dist a b| = dist a b :=
abs_of_nonneg dist_nonneg

/-- A version of `has_dist` that takes value in `â„â‰¥0`. -/
class has_nndist (Î± : Type*) := (nndist : Î± â†’ Î± â†’ â„â‰¥0)

export has_nndist (nndist)

/-- Distance as a nonnegative real number. -/
@[priority 100] -- see Note [lower instance priority]
instance pseudo_metric_space.to_has_nndist : has_nndist Î± := âŸ¨Î» a b, âŸ¨dist a b, dist_nonnegâŸ©âŸ©

/--Express `nndist` in terms of `edist`-/
lemma nndist_edist (x y : Î±) : nndist x y = (edist x y).to_nnreal :=
by simp [nndist, edist_dist, real.to_nnreal, max_eq_left dist_nonneg, ennreal.of_real]

/--Express `edist` in terms of `nndist`-/
lemma edist_nndist (x y : Î±) : edist x y = â†‘(nndist x y) :=
by { simpa only [edist_dist, ennreal.of_real_eq_coe_nnreal dist_nonneg] }

@[simp, norm_cast] lemma coe_nnreal_ennreal_nndist (x y : Î±) : â†‘(nndist x y) = edist x y :=
(edist_nndist x y).symm

@[simp, norm_cast] lemma edist_lt_coe {x y : Î±} {c : â„â‰¥0} :
  edist x y < c â†” nndist x y < c :=
by rw [edist_nndist, ennreal.coe_lt_coe]

@[simp, norm_cast] lemma edist_le_coe {x y : Î±} {c : â„â‰¥0} :
  edist x y â‰¤ c â†” nndist x y â‰¤ c :=
by rw [edist_nndist, ennreal.coe_le_coe]

/--In a pseudometric space, the extended distance is always finite-/
lemma edist_lt_top {Î± : Type*} [pseudo_metric_space Î±] (x y : Î±) : edist x y < âŠ¤ :=
(edist_dist x y).symm â–¸ ennreal.of_real_lt_top

/--In a pseudometric space, the extended distance is always finite-/
lemma edist_ne_top (x y : Î±) : edist x y â‰  âŠ¤ := (edist_lt_top x y).ne

/--`nndist x x` vanishes-/
@[simp] lemma nndist_self (a : Î±) : nndist a a = 0 := (nnreal.coe_eq_zero _).1 (dist_self a)

/--Express `dist` in terms of `nndist`-/
lemma dist_nndist (x y : Î±) : dist x y = â†‘(nndist x y) := rfl

@[simp, norm_cast] lemma coe_nndist (x y : Î±) : â†‘(nndist x y) = dist x y :=
(dist_nndist x y).symm

@[simp, norm_cast] lemma dist_lt_coe {x y : Î±} {c : â„â‰¥0} :
  dist x y < c â†” nndist x y < c :=
iff.rfl

@[simp, norm_cast] lemma dist_le_coe {x y : Î±} {c : â„â‰¥0} :
  dist x y â‰¤ c â†” nndist x y â‰¤ c :=
iff.rfl

/--Express `nndist` in terms of `dist`-/
lemma nndist_dist (x y : Î±) : nndist x y = real.to_nnreal (dist x y) :=
by rw [dist_nndist, real.to_nnreal_coe]

theorem nndist_comm (x y : Î±) : nndist x y = nndist y x :=
by simpa only [dist_nndist, nnreal.coe_eq] using dist_comm x y

/--Triangle inequality for the nonnegative distance-/
theorem nndist_triangle (x y z : Î±) : nndist x z â‰¤ nndist x y + nndist y z :=
dist_triangle _ _ _

theorem nndist_triangle_left (x y z : Î±) : nndist x y â‰¤ nndist z x + nndist z y :=
dist_triangle_left _ _ _

theorem nndist_triangle_right (x y z : Î±) : nndist x y â‰¤ nndist x z + nndist y z :=
dist_triangle_right _ _ _

/--Express `dist` in terms of `edist`-/
lemma dist_edist (x y : Î±) : dist x y = (edist x y).to_real :=
by rw [edist_dist, ennreal.to_real_of_real (dist_nonneg)]

namespace metric

/- instantiate pseudometric space as a topology -/
variables {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : â„} {s : set Î±}

/-- `ball x Îµ` is the set of all points `y` with `dist y x < Îµ` -/
def ball (x : Î±) (Îµ : â„) : set Î± := {y | dist y x < Îµ}

@[simp] theorem mem_ball : y âˆˆ ball x Îµ â†” dist y x < Îµ := iff.rfl

theorem mem_ball' : y âˆˆ ball x Îµ â†” dist x y < Îµ := by rw dist_comm; refl

theorem pos_of_mem_ball (hy : y âˆˆ ball x Îµ) : 0 < Îµ :=
dist_nonneg.trans_lt hy

theorem mem_ball_self (h : 0 < Îµ) : x âˆˆ ball x Îµ :=
show dist x x < Îµ, by rw dist_self; assumption

@[simp] lemma nonempty_ball : (ball x Îµ).nonempty â†” 0 < Îµ :=
âŸ¨Î» âŸ¨x, hxâŸ©, pos_of_mem_ball hx, Î» h, âŸ¨x, mem_ball_self hâŸ©âŸ©

@[simp] lemma ball_eq_empty : ball x Îµ = âˆ… â†” Îµ â‰¤ 0 :=
by rw [â† not_nonempty_iff_eq_empty, nonempty_ball, not_lt]

@[simp] lemma ball_zero : ball x 0 = âˆ… :=
by rw [ball_eq_empty]

lemma ball_eq_ball (Îµ : â„) (x : Î±) :
  uniform_space.ball x {p | dist p.2 p.1 < Îµ} = metric.ball x Îµ := rfl

lemma ball_eq_ball' (Îµ : â„) (x : Î±) :
  uniform_space.ball x {p | dist p.1 p.2 < Îµ} = metric.ball x Îµ :=
by { ext, simp [dist_comm, uniform_space.ball] }

@[simp] lemma Union_ball_nat (x : Î±) : (â‹ƒ n : â„•, ball x n) = univ :=
Union_eq_univ_iff.2 $ Î» y, exists_nat_gt (dist y x)

@[simp] lemma Union_ball_nat_succ (x : Î±) : (â‹ƒ n : â„•, ball x (n + 1)) = univ :=
Union_eq_univ_iff.2 $ Î» y, (exists_nat_gt (dist y x)).imp $ Î» n hn,
  hn.trans (lt_add_one _)

/-- `closed_ball x Îµ` is the set of all points `y` with `dist y x â‰¤ Îµ` -/
def closed_ball (x : Î±) (Îµ : â„) := {y | dist y x â‰¤ Îµ}

@[simp] theorem mem_closed_ball : y âˆˆ closed_ball x Îµ â†” dist y x â‰¤ Îµ := iff.rfl

/-- `sphere x Îµ` is the set of all points `y` with `dist y x = Îµ` -/
def sphere (x : Î±) (Îµ : â„) := {y | dist y x = Îµ}

@[simp] theorem mem_sphere : y âˆˆ sphere x Îµ â†” dist y x = Îµ := iff.rfl

theorem mem_closed_ball' : y âˆˆ closed_ball x Îµ â†” dist x y â‰¤ Îµ :=
by { rw dist_comm, refl }

theorem mem_closed_ball_self (h : 0 â‰¤ Îµ) : x âˆˆ closed_ball x Îµ :=
show dist x x â‰¤ Îµ, by rw dist_self; assumption

@[simp] lemma nonempty_closed_ball : (closed_ball x Îµ).nonempty â†” 0 â‰¤ Îµ :=
âŸ¨Î» âŸ¨x, hxâŸ©, dist_nonneg.trans hx, Î» h, âŸ¨x, mem_closed_ball_self hâŸ©âŸ©

@[simp] lemma closed_ball_eq_empty : closed_ball x Îµ = âˆ… â†” Îµ < 0 :=
by rw [â† not_nonempty_iff_eq_empty, nonempty_closed_ball, not_le]

theorem ball_subset_closed_ball : ball x Îµ âŠ† closed_ball x Îµ :=
assume y (hy : _ < _), le_of_lt hy

theorem sphere_subset_closed_ball : sphere x Îµ âŠ† closed_ball x Îµ :=
Î» y, le_of_eq

lemma ball_disjoint_ball (x y : Î±) (rx ry : â„) (h : rx + ry â‰¤ dist x y) :
  disjoint (ball x rx) (ball y ry) :=
begin
  rw disjoint_left,
  assume a ax ay,
  apply lt_irrefl (dist x y),
  calc dist x y â‰¤ dist x a + dist a y : dist_triangle _ _ _
  ... < rx + ry : add_lt_add (mem_ball'.1 ax) (mem_ball.1 ay)
  ... â‰¤ dist x y : h
end

theorem sphere_disjoint_ball : disjoint (sphere x Îµ) (ball x Îµ) :=
Î» y âŸ¨hyâ‚, hyâ‚‚âŸ©, absurd hyâ‚ $ ne_of_lt hyâ‚‚

@[simp] theorem ball_union_sphere : ball x Îµ âˆª sphere x Îµ = closed_ball x Îµ :=
set.ext $ Î» y, (@le_iff_lt_or_eq â„ _ _ _).symm

@[simp] theorem sphere_union_ball : sphere x Îµ âˆª ball x Îµ = closed_ball x Îµ :=
by rw [union_comm, ball_union_sphere]

@[simp] theorem closed_ball_diff_sphere : closed_ball x Îµ \ sphere x Îµ = ball x Îµ :=
by rw [â† ball_union_sphere, set.union_diff_cancel_right sphere_disjoint_ball.symm]

@[simp] theorem closed_ball_diff_ball : closed_ball x Îµ \ ball x Îµ = sphere x Îµ :=
by rw [â† ball_union_sphere, set.union_diff_cancel_left sphere_disjoint_ball.symm]

theorem mem_ball_comm : x âˆˆ ball y Îµ â†” y âˆˆ ball x Îµ :=
by simp [dist_comm]

theorem ball_subset_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) : ball x Îµâ‚ âŠ† ball x Îµâ‚‚ :=
Î» y (yx : _ < Îµâ‚), lt_of_lt_of_le yx h

lemma ball_subset_ball' (h : Îµâ‚ + dist x y â‰¤ Îµâ‚‚) : ball x Îµâ‚ âŠ† ball y Îµâ‚‚ :=
Î» z hz, calc
  dist z y â‰¤ dist z x + dist x y : dist_triangle _ _ _
  ... < Îµâ‚ + dist x y : add_lt_add_right hz _
  ... â‰¤ Îµâ‚‚ : h

theorem closed_ball_subset_closed_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) :
  closed_ball x Îµâ‚ âŠ† closed_ball x Îµâ‚‚ :=
Î» y (yx : _ â‰¤ Îµâ‚), le_trans yx h

lemma closed_ball_subset_closed_ball' (h : Îµâ‚ + dist x y â‰¤ Îµâ‚‚) :
  closed_ball x Îµâ‚ âŠ† closed_ball y Îµâ‚‚ :=
Î» z hz, calc
  dist z y â‰¤ dist z x + dist x y : dist_triangle _ _ _
  ... â‰¤ Îµâ‚ + dist x y : add_le_add_right hz _
  ... â‰¤ Îµâ‚‚ : h

theorem closed_ball_subset_ball (h : Îµâ‚ < Îµâ‚‚) :
  closed_ball x Îµâ‚ âŠ† ball x Îµâ‚‚ :=
Î» y (yh : dist y x â‰¤ Îµâ‚), lt_of_le_of_lt yh h

lemma dist_le_add_of_nonempty_closed_ball_inter_closed_ball
  (h : (closed_ball x Îµâ‚ âˆ© closed_ball y Îµâ‚‚).nonempty) :
  dist x y â‰¤ Îµâ‚ + Îµâ‚‚ :=
let âŸ¨z, hzâŸ© := h in calc
  dist x y â‰¤ dist z x + dist z y : dist_triangle_left _ _ _
  ... â‰¤ Îµâ‚ + Îµâ‚‚ : add_le_add hz.1 hz.2

lemma dist_lt_add_of_nonempty_closed_ball_inter_ball (h : (closed_ball x Îµâ‚ âˆ© ball y Îµâ‚‚).nonempty) :
  dist x y < Îµâ‚ + Îµâ‚‚ :=
let âŸ¨z, hzâŸ© := h in calc
  dist x y â‰¤ dist z x + dist z y : dist_triangle_left _ _ _
  ... < Îµâ‚ + Îµâ‚‚ : add_lt_add_of_le_of_lt hz.1 hz.2

lemma dist_lt_add_of_nonempty_ball_inter_closed_ball (h : (ball x Îµâ‚ âˆ© closed_ball y Îµâ‚‚).nonempty) :
  dist x y < Îµâ‚ + Îµâ‚‚ :=
begin
  rw inter_comm at h,
  rw [add_comm, dist_comm],
  exact dist_lt_add_of_nonempty_closed_ball_inter_ball h
end

lemma dist_lt_add_of_nonempty_ball_inter_ball (h : (ball x Îµâ‚ âˆ© ball y Îµâ‚‚).nonempty) :
  dist x y < Îµâ‚ + Îµâ‚‚ :=
dist_lt_add_of_nonempty_closed_ball_inter_ball $
  h.mono (inter_subset_inter ball_subset_closed_ball subset.rfl)

@[simp] lemma Union_closed_ball_nat (x : Î±) : (â‹ƒ n : â„•, closed_ball x n) = univ :=
Union_eq_univ_iff.2 $ Î» y, exists_nat_ge (dist y x)

theorem ball_disjoint (h : Îµâ‚ + Îµâ‚‚ â‰¤ dist x y) : ball x Îµâ‚ âˆ© ball y Îµâ‚‚ = âˆ… :=
eq_empty_iff_forall_not_mem.2 $ Î» z âŸ¨hâ‚, hâ‚‚âŸ©,
not_lt_of_le (dist_triangle_left x y z)
  (lt_of_lt_of_le (add_lt_add hâ‚ hâ‚‚) h)

theorem ball_disjoint_same (h : Îµ â‰¤ dist x y / 2) : ball x Îµ âˆ© ball y Îµ = âˆ… :=
ball_disjoint $ by rwa [â† two_mul, â† le_div_iff' (@zero_lt_two â„ _ _)]

theorem ball_subset (h : dist x y â‰¤ Îµâ‚‚ - Îµâ‚) : ball x Îµâ‚ âŠ† ball y Îµâ‚‚ :=
Î» z zx, by rw â† add_sub_cancel'_right Îµâ‚ Îµâ‚‚; exact
lt_of_le_of_lt (dist_triangle z x y) (add_lt_add_of_lt_of_le zx h)

theorem ball_half_subset (y) (h : y âˆˆ ball x (Îµ / 2)) : ball y (Îµ / 2) âŠ† ball x Îµ :=
ball_subset $ by rw sub_self_div_two; exact le_of_lt h

theorem exists_ball_subset_ball (h : y âˆˆ ball x Îµ) : âˆƒ Îµ' > 0, ball y Îµ' âŠ† ball x Îµ :=
âŸ¨_, sub_pos.2 h, ball_subset $ by rw sub_sub_selfâŸ©

theorem uniformity_basis_dist :
  (ğ“¤ Î±).has_basis (Î» Îµ : â„, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | dist p.1 p.2 < Îµ}) :=
begin
  rw â† pseudo_metric_space.uniformity_dist.symm,
  refine has_basis_binfi_principal _ nonempty_Ioi,
  exact Î» r (hr : 0 < r) p (hp : 0 < p), âŸ¨min r p, lt_min hr hp,
     Î» x (hx : dist _ _ < _), lt_of_lt_of_le hx (min_le_left r p),
     Î» x (hx : dist _ _ < _), lt_of_lt_of_le hx (min_le_right r p)âŸ©
end

/-- Given `f : Î² â†’ â„`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then `f i`-neighborhoods of the diagonal form a basis of `ğ“¤ Î±`.

For specific bases see `uniformity_basis_dist`, `uniformity_basis_dist_inv_nat_succ`,
and `uniformity_basis_dist_inv_nat_pos`. -/
protected theorem mk_uniformity_basis {Î² : Type*} {p : Î² â†’ Prop} {f : Î² â†’ â„}
  (hfâ‚€ : âˆ€ i, p i â†’ 0 < f i) (hf : âˆ€ â¦ƒÎµâ¦„, 0 < Îµ â†’ âˆƒ i (hi : p i), f i â‰¤ Îµ) :
  (ğ“¤ Î±).has_basis p (Î» i, {p:Î±Ã—Î± | dist p.1 p.2 < f i}) :=
begin
  refine âŸ¨Î» s, uniformity_basis_dist.mem_iff.trans _âŸ©,
  split,
  { rintros âŸ¨Îµ, Îµâ‚€, hÎµâŸ©,
    obtain âŸ¨i, hi, HâŸ© : âˆƒ i (hi : p i), f i â‰¤ Îµ, from hf Îµâ‚€,
    exact âŸ¨i, hi, Î» x (hx : _ < _), hÎµ $ lt_of_lt_of_le hx HâŸ© },
  { exact Î» âŸ¨i, hi, HâŸ©, âŸ¨f i, hfâ‚€ i hi, HâŸ© }
end

theorem uniformity_basis_dist_inv_nat_succ :
  (ğ“¤ Î±).has_basis (Î» _, true) (Î» n:â„•, {p:Î±Ã—Î± | dist p.1 p.2 < 1 / (â†‘n+1) }) :=
metric.mk_uniformity_basis (Î» n _, div_pos zero_lt_one $ nat.cast_add_one_pos n)
  (Î» Îµ Îµ0, (exists_nat_one_div_lt Îµ0).imp $ Î» n hn, âŸ¨trivial, le_of_lt hnâŸ©)

theorem uniformity_basis_dist_inv_nat_pos :
  (ğ“¤ Î±).has_basis (Î» n:â„•, 0<n) (Î» n:â„•, {p:Î±Ã—Î± | dist p.1 p.2 < 1 / â†‘n }) :=
metric.mk_uniformity_basis (Î» n hn, div_pos zero_lt_one $ nat.cast_pos.2 hn)
  (Î» Îµ Îµ0, let âŸ¨n, hnâŸ© := exists_nat_one_div_lt Îµ0 in âŸ¨n+1, nat.succ_pos n, hn.leâŸ©)

theorem uniformity_basis_dist_pow {r : â„} (h0 : 0 < r) (h1 : r < 1) :
  (ğ“¤ Î±).has_basis (Î» n:â„•, true) (Î» n:â„•, {p:Î±Ã—Î± | dist p.1 p.2 < r ^ n }) :=
metric.mk_uniformity_basis (Î» n hn, pow_pos h0 _)
  (Î» Îµ Îµ0, let âŸ¨n, hnâŸ© := exists_pow_lt_of_lt_one Îµ0 h1 in âŸ¨n, trivial, hn.leâŸ©)

theorem uniformity_basis_dist_lt {R : â„} (hR : 0 < R) :
  (ğ“¤ Î±).has_basis (Î» r : â„, 0 < r âˆ§ r < R) (Î» r, {p : Î± Ã— Î± | dist p.1 p.2 < r}) :=
metric.mk_uniformity_basis (Î» r, and.left) $ Î» r hr,
  âŸ¨min r (R / 2), âŸ¨lt_min hr (half_pos hR), min_lt_iff.2 $ or.inr (half_lt_self hR)âŸ©,
    min_le_left _ _âŸ©

/-- Given `f : Î² â†’ â„`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then closed neighborhoods of the diagonal of sizes `{f i | p i}`
form a basis of `ğ“¤ Î±`.

Currently we have only one specific basis `uniformity_basis_dist_le` based on this constructor.
More can be easily added if needed in the future. -/
protected theorem mk_uniformity_basis_le {Î² : Type*} {p : Î² â†’ Prop} {f : Î² â†’ â„}
  (hfâ‚€ : âˆ€ x, p x â†’ 0 < f x) (hf : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ x (hx : p x), f x â‰¤ Îµ) :
  (ğ“¤ Î±).has_basis p (Î» x, {p:Î±Ã—Î± | dist p.1 p.2 â‰¤ f x}) :=
begin
  refine âŸ¨Î» s, uniformity_basis_dist.mem_iff.trans _âŸ©,
  split,
  { rintros âŸ¨Îµ, Îµâ‚€, hÎµâŸ©,
    rcases exists_between Îµâ‚€ with âŸ¨Îµ', hÎµ'âŸ©,
    rcases hf Îµ' hÎµ'.1 with âŸ¨i, hi, HâŸ©,
    exact âŸ¨i, hi, Î» x (hx : _ â‰¤ _), hÎµ $ lt_of_le_of_lt (le_trans hx H) hÎµ'.2âŸ© },
  { exact Î» âŸ¨i, hi, HâŸ©, âŸ¨f i, hfâ‚€ i hi, Î» x (hx : _ < _), H (le_of_lt hx)âŸ© }
end

/-- Contant size closed neighborhoods of the diagonal form a basis
of the uniformity filter. -/
theorem uniformity_basis_dist_le :
  (ğ“¤ Î±).has_basis (Î» Îµ : â„, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | dist p.1 p.2 â‰¤ Îµ}) :=
metric.mk_uniformity_basis_le (Î» _, id) (Î» Îµ Îµâ‚€, âŸ¨Îµ, Îµâ‚€, le_refl ÎµâŸ©)

theorem uniformity_basis_dist_le_pow {r : â„} (h0 : 0 < r) (h1 : r < 1) :
  (ğ“¤ Î±).has_basis (Î» n:â„•, true) (Î» n:â„•, {p:Î±Ã—Î± | dist p.1 p.2 â‰¤ r ^ n }) :=
metric.mk_uniformity_basis_le (Î» n hn, pow_pos h0 _)
  (Î» Îµ Îµ0, let âŸ¨n, hnâŸ© := exists_pow_lt_of_lt_one Îµ0 h1 in âŸ¨n, trivial, hn.leâŸ©)

theorem mem_uniformity_dist {s : set (Î±Ã—Î±)} :
  s âˆˆ ğ“¤ Î± â†” (âˆƒÎµ>0, âˆ€{a b:Î±}, dist a b < Îµ â†’ (a, b) âˆˆ s) :=
uniformity_basis_dist.mem_uniformity_iff

/-- A constant size neighborhood of the diagonal is an entourage. -/
theorem dist_mem_uniformity {Îµ:â„} (Îµ0 : 0 < Îµ) :
  {p:Î±Ã—Î± | dist p.1 p.2 < Îµ} âˆˆ ğ“¤ Î± :=
mem_uniformity_dist.2 âŸ¨Îµ, Îµ0, Î» a b, idâŸ©

theorem uniform_continuous_iff [pseudo_metric_space Î²] {f : Î± â†’ Î²} :
  uniform_continuous f â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0,
    âˆ€{a b:Î±}, dist a b < Î´ â†’ dist (f a) (f b) < Îµ :=
uniformity_basis_dist.uniform_continuous_iff uniformity_basis_dist

lemma uniform_continuous_on_iff [pseudo_metric_space Î²] {f : Î± â†’ Î²} {s : set Î±} :
  uniform_continuous_on f s â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x y âˆˆ s, dist x y < Î´ â†’ dist (f x) (f y) < Îµ :=
metric.uniformity_basis_dist.uniform_continuous_on_iff metric.uniformity_basis_dist

lemma uniform_continuous_on_iff_le [pseudo_metric_space Î²] {f : Î± â†’ Î²} {s : set Î±} :
  uniform_continuous_on f s â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x y âˆˆ s, dist x y â‰¤ Î´ â†’ dist (f x) (f y) â‰¤ Îµ :=
metric.uniformity_basis_dist_le.uniform_continuous_on_iff metric.uniformity_basis_dist_le

theorem uniform_embedding_iff [pseudo_metric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†” function.injective f âˆ§ uniform_continuous f âˆ§
    âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, dist (f a) (f b) < Îµ â†’ dist a b < Î´ :=
uniform_embedding_def'.trans $ and_congr iff.rfl $ and_congr iff.rfl
âŸ¨Î» H Î´ Î´0, let âŸ¨t, tu, htâŸ© := H _ (dist_mem_uniformity Î´0),
               âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_dist.1 tu in
  âŸ¨Îµ, Îµ0, Î» a b h, ht _ _ (hÎµ h)âŸ©,
 Î» H s su, let âŸ¨Î´, Î´0, hÎ´âŸ© := mem_uniformity_dist.1 su, âŸ¨Îµ, Îµ0, hÎµâŸ© := H _ Î´0 in
  âŸ¨_, dist_mem_uniformity Îµ0, Î» a b h, hÎ´ (hÎµ h)âŸ©âŸ©

/-- If a map between pseudometric spaces is a uniform embedding then the distance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y`. -/
theorem controlled_of_uniform_embedding [pseudo_metric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†’
  (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î±}, dist a b < Î´ â†’ dist (f a) (f b) < Îµ) âˆ§
  (âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, dist (f a) (f b) < Îµ â†’ dist a b < Î´) :=
begin
  assume h,
  exact âŸ¨uniform_continuous_iff.1 (uniform_embedding_iff.1 h).2.1, (uniform_embedding_iff.1 h).2.2âŸ©
end

theorem totally_bounded_iff {s : set Î±} :
  totally_bounded s â†” âˆ€ Îµ > 0, âˆƒt : set Î±, finite t âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
âŸ¨Î» H Îµ Îµ0, H _ (dist_mem_uniformity Îµ0),
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_dist.1 ru,
               âŸ¨t, ft, hâŸ© := H Îµ Îµ0 in
  âŸ¨t, ft, subset.trans h $ Union_subset_Union $ Î» y, Union_subset_Union $ Î» yt z, hÎµâŸ©âŸ©

/-- A pseudometric space is totally bounded if one can reconstruct up to any Îµ>0 any element of the
space from finitely many data. -/
lemma totally_bounded_of_finite_discretization {s : set Î±}
  (H : âˆ€Îµ > (0 : â„), âˆƒ (Î² : Type u) (_ : fintype Î²) (F : s â†’ Î²),
    âˆ€x y, F x = F y â†’ dist (x:Î±) y < Îµ) :
  totally_bounded s :=
begin
  cases s.eq_empty_or_nonempty with hs hs,
  { rw hs, exact totally_bounded_empty },
  rcases hs with âŸ¨x0, hx0âŸ©,
  haveI : inhabited s := âŸ¨âŸ¨x0, hx0âŸ©âŸ©,
  refine totally_bounded_iff.2 (Î» Îµ Îµ0, _),
  rcases H Îµ Îµ0 with âŸ¨Î², fÎ², F, hFâŸ©,
  resetI,
  let Finv := function.inv_fun F,
  refine âŸ¨range (subtype.val âˆ˜ Finv), finite_range _, Î» x xs, _âŸ©,
  let x' := Finv (F âŸ¨x, xsâŸ©),
  have : F x' = F âŸ¨x, xsâŸ© := function.inv_fun_eq âŸ¨âŸ¨x, xsâŸ©, rflâŸ©,
  simp only [set.mem_Union, set.mem_range],
  exact âŸ¨_, âŸ¨F âŸ¨x, xsâŸ©, rflâŸ©, hF _ _ this.symmâŸ©
end

theorem finite_approx_of_totally_bounded {s : set Î±} (hs : totally_bounded s) :
  âˆ€ Îµ > 0, âˆƒ t âŠ† s, finite t âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
begin
  intros Îµ Îµ_pos,
  rw totally_bounded_iff_subset at hs,
  exact hs _ (dist_mem_uniformity Îµ_pos),
end

/-- Expressing locally uniform convergence on a set using `dist`. -/
lemma tendsto_locally_uniformly_on_iff {Î¹ : Type*} [topological_space Î²]
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} {s : set Î²} :
  tendsto_locally_uniformly_on F f p s â†”
  âˆ€ Îµ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, dist (f y) (F n y) < Îµ :=
begin
  refine âŸ¨Î» H Îµ hÎµ, H _ (dist_mem_uniformity hÎµ), Î» H u hu x hx, _âŸ©,
  rcases mem_uniformity_dist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  rcases H Îµ Îµpos x hx with âŸ¨t, ht, HtâŸ©,
  exact âŸ¨t, ht, Ht.mono (Î» n hs x hx, hÎµ (hs x hx))âŸ©
end

/-- Expressing uniform convergence on a set using `dist`. -/
lemma tendsto_uniformly_on_iff {Î¹ : Type*}
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} {s : set Î²} :
  tendsto_uniformly_on F f p s â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x âˆˆ s, dist (f x) (F n x) < Îµ :=
begin
  refine âŸ¨Î» H Îµ hÎµ, H _ (dist_mem_uniformity hÎµ), Î» H u hu, _âŸ©,
  rcases mem_uniformity_dist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  exact (H Îµ Îµpos).mono (Î» n hs x hx, hÎµ (hs x hx))
end

/-- Expressing locally uniform convergence using `dist`. -/
lemma tendsto_locally_uniformly_iff {Î¹ : Type*} [topological_space Î²]
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} :
  tendsto_locally_uniformly F f p â†”
  âˆ€ Îµ > 0, âˆ€ (x : Î²), âˆƒ t âˆˆ ğ“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, dist (f y) (F n y) < Îµ :=
by simp only [â† tendsto_locally_uniformly_on_univ, tendsto_locally_uniformly_on_iff,
  nhds_within_univ, mem_univ, forall_const, exists_prop]

/-- Expressing uniform convergence using `dist`. -/
lemma tendsto_uniformly_iff {Î¹ : Type*}
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} :
  tendsto_uniformly F f p â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x, dist (f x) (F n x) < Îµ :=
by { rw [â† tendsto_uniformly_on_univ, tendsto_uniformly_on_iff], simp }

protected lemma cauchy_iff {f : filter Î±} :
  cauchy f â†” ne_bot f âˆ§ âˆ€ Îµ > 0, âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, dist x y < Îµ :=
uniformity_basis_dist.cauchy_iff

theorem nhds_basis_ball : (ğ“ x).has_basis (Î» Îµ:â„, 0 < Îµ) (ball x) :=
nhds_basis_uniformity uniformity_basis_dist

theorem mem_nhds_iff : s âˆˆ ğ“ x â†” âˆƒÎµ>0, ball x Îµ âŠ† s :=
nhds_basis_ball.mem_iff

theorem eventually_nhds_iff {p : Î± â†’ Prop} :
  (âˆ€á¶  y in ğ“ x, p y) â†” âˆƒÎµ>0, âˆ€ â¦ƒyâ¦„, dist y x < Îµ â†’ p y :=
mem_nhds_iff

lemma eventually_nhds_iff_ball {p : Î± â†’ Prop} :
  (âˆ€á¶  y in ğ“ x, p y) â†” âˆƒ Îµ>0, âˆ€ y âˆˆ ball x Îµ, p y :=
mem_nhds_iff

theorem nhds_basis_closed_ball : (ğ“ x).has_basis (Î» Îµ:â„, 0 < Îµ) (closed_ball x) :=
nhds_basis_uniformity uniformity_basis_dist_le

theorem nhds_basis_ball_inv_nat_succ :
  (ğ“ x).has_basis (Î» _, true) (Î» n:â„•, ball x (1 / (â†‘n+1))) :=
nhds_basis_uniformity uniformity_basis_dist_inv_nat_succ

theorem nhds_basis_ball_inv_nat_pos :
  (ğ“ x).has_basis (Î» n, 0<n) (Î» n:â„•, ball x (1 / â†‘n)) :=
nhds_basis_uniformity uniformity_basis_dist_inv_nat_pos

theorem nhds_basis_ball_pow {r : â„} (h0 : 0 < r) (h1 : r < 1) :
  (ğ“ x).has_basis (Î» n, true) (Î» n:â„•, ball x (r ^ n)) :=
nhds_basis_uniformity (uniformity_basis_dist_pow h0 h1)

theorem nhds_basis_closed_ball_pow {r : â„} (h0 : 0 < r) (h1 : r < 1) :
  (ğ“ x).has_basis (Î» n, true) (Î» n:â„•, closed_ball x (r ^ n)) :=
nhds_basis_uniformity (uniformity_basis_dist_le_pow h0 h1)

theorem is_open_iff : is_open s â†” âˆ€xâˆˆs, âˆƒÎµ>0, ball x Îµ âŠ† s :=
by simp only [is_open_iff_mem_nhds, mem_nhds_iff]

theorem is_open_ball : is_open (ball x Îµ) :=
is_open_iff.2 $ Î» y, exists_ball_subset_ball

theorem ball_mem_nhds (x : Î±) {Îµ : â„} (Îµ0 : 0 < Îµ) : ball x Îµ âˆˆ ğ“ x :=
is_open.mem_nhds is_open_ball (mem_ball_self Îµ0)

theorem closed_ball_mem_nhds (x : Î±) {Îµ : â„} (Îµ0 : 0 < Îµ) : closed_ball x Îµ âˆˆ ğ“ x :=
mem_of_superset (ball_mem_nhds x Îµ0) ball_subset_closed_ball

theorem nhds_within_basis_ball {s : set Î±} :
  (ğ“[s] x).has_basis (Î» Îµ:â„, 0 < Îµ) (Î» Îµ, ball x Îµ âˆ© s) :=
nhds_within_has_basis nhds_basis_ball s

theorem mem_nhds_within_iff {t : set Î±} : s âˆˆ ğ“[t] x â†” âˆƒÎµ>0, ball x Îµ âˆ© t âŠ† s :=
nhds_within_basis_ball.mem_iff

theorem tendsto_nhds_within_nhds_within [pseudo_metric_space Î²] {t : set Î²} {f : Î± â†’ Î²} {a b} :
  tendsto f (ğ“[s] a) (ğ“[t] b) â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€{x:Î±}, x âˆˆ s â†’ dist x a < Î´ â†’ f x âˆˆ t âˆ§ dist (f x) b < Îµ :=
(nhds_within_basis_ball.tendsto_iff nhds_within_basis_ball).trans $
  by simp only [inter_comm, mem_inter_iff, and_imp, mem_ball]

theorem tendsto_nhds_within_nhds [pseudo_metric_space Î²] {f : Î± â†’ Î²} {a b} :
  tendsto f (ğ“[s] a) (ğ“ b) â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€{x:Î±}, x âˆˆ s â†’ dist x a < Î´ â†’ dist (f x) b < Îµ :=
by { rw [â† nhds_within_univ b, tendsto_nhds_within_nhds_within],
  simp only [mem_univ, true_and] }

theorem tendsto_nhds_nhds [pseudo_metric_space Î²] {f : Î± â†’ Î²} {a b} :
  tendsto f (ğ“ a) (ğ“ b) â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€{x:Î±}, dist x a < Î´ â†’ dist (f x) b < Îµ :=
nhds_basis_ball.tendsto_iff nhds_basis_ball

theorem continuous_at_iff [pseudo_metric_space Î²] {f : Î± â†’ Î²} {a : Î±} :
  continuous_at f a â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€{x:Î±}, dist x a < Î´ â†’ dist (f x) (f a) < Îµ :=
by rw [continuous_at, tendsto_nhds_nhds]

theorem continuous_within_at_iff [pseudo_metric_space Î²] {f : Î± â†’ Î²} {a : Î±} {s : set Î±} :
  continuous_within_at f s a â†”
  âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€{x:Î±}, x âˆˆ s â†’ dist x a < Î´ â†’ dist (f x) (f a) < Îµ :=
by rw [continuous_within_at, tendsto_nhds_within_nhds]

theorem continuous_on_iff [pseudo_metric_space Î²] {f : Î± â†’ Î²} {s : set Î±} :
  continuous_on f s â†”
  âˆ€ (b âˆˆ s) (Îµ > 0), âˆƒ Î´ > 0, âˆ€a âˆˆ s, dist a b < Î´ â†’ dist (f a) (f b) < Îµ :=
by simp [continuous_on, continuous_within_at_iff]

theorem continuous_iff [pseudo_metric_space Î²] {f : Î± â†’ Î²} :
  continuous f â†”
  âˆ€b (Îµ > 0), âˆƒ Î´ > 0, âˆ€a, dist a b < Î´ â†’ dist (f a) (f b) < Îµ :=
continuous_iff_continuous_at.trans $ forall_congr $ Î» b, tendsto_nhds_nhds

theorem tendsto_nhds {f : filter Î²} {u : Î² â†’ Î±} {a : Î±} :
  tendsto u f (ğ“ a) â†” âˆ€ Îµ > 0, âˆ€á¶  x in f, dist (u x) a < Îµ :=
nhds_basis_ball.tendsto_right_iff

theorem continuous_at_iff' [topological_space Î²] {f : Î² â†’ Î±} {b : Î²} :
  continuous_at f b â†”
  âˆ€ Îµ > 0, âˆ€á¶  x in ğ“ b, dist (f x) (f b) < Îµ :=
by rw [continuous_at, tendsto_nhds]

theorem continuous_within_at_iff' [topological_space Î²] {f : Î² â†’ Î±} {b : Î²} {s : set Î²} :
  continuous_within_at f s b â†”
  âˆ€ Îµ > 0, âˆ€á¶  x in ğ“[s] b, dist (f x) (f b) < Îµ :=
by rw [continuous_within_at, tendsto_nhds]

theorem continuous_on_iff' [topological_space Î²] {f : Î² â†’ Î±} {s : set Î²} :
  continuous_on f s â†”
  âˆ€ (b âˆˆ s) (Îµ > 0), âˆ€á¶  x in ğ“[s] b, dist (f x) (f b) < Îµ  :=
by simp [continuous_on, continuous_within_at_iff']

theorem continuous_iff' [topological_space Î²] {f : Î² â†’ Î±} :
  continuous f â†” âˆ€a (Îµ > 0), âˆ€á¶  x in ğ“ a, dist (f x) (f a) < Îµ :=
continuous_iff_continuous_at.trans $ forall_congr $ Î» b, tendsto_nhds

theorem tendsto_at_top [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} {a : Î±} :
  tendsto u at_top (ğ“ a) â†” âˆ€Îµ>0, âˆƒN, âˆ€nâ‰¥N, dist (u n) a < Îµ :=
(at_top_basis.tendsto_iff nhds_basis_ball).trans $
  by { simp only [exists_prop, true_and], refl }

/--
A variant of `tendsto_at_top` that
uses `âˆƒ N, âˆ€ n > N, ...` rather than `âˆƒ N, âˆ€ n â‰¥ N, ...`
-/
theorem tendsto_at_top' [nonempty Î²] [semilattice_sup Î²] [no_top_order Î²] {u : Î² â†’ Î±} {a : Î±} :
  tendsto u at_top (ğ“ a) â†” âˆ€Îµ>0, âˆƒN, âˆ€n>N, dist (u n) a < Îµ :=
(at_top_basis_Ioi.tendsto_iff nhds_basis_ball).trans $
  by { simp only [exists_prop, true_and], refl }

lemma is_open_singleton_iff {Î± : Type*} [pseudo_metric_space Î±] {x : Î±} :
  is_open ({x} : set Î±) â†” âˆƒ Îµ > 0, âˆ€ y, dist y x < Îµ â†’ y = x :=
by simp [is_open_iff, subset_singleton_iff, mem_ball]

/-- Given a point `x` in a discrete subset `s` of a pseudometric space, there is an open ball
centered at `x` and intersecting `s` only at `x`. -/
lemma exists_ball_inter_eq_singleton_of_mem_discrete [discrete_topology s] {x : Î±} (hx : x âˆˆ s) :
  âˆƒ Îµ > 0, metric.ball x Îµ âˆ© s = {x} :=
nhds_basis_ball.exists_inter_eq_singleton_of_mem_discrete hx

/-- Given a point `x` in a discrete subset `s` of a pseudometric space, there is a closed ball
of positive radius centered at `x` and intersecting `s` only at `x`. -/
lemma exists_closed_ball_inter_eq_singleton_of_discrete [discrete_topology s] {x : Î±} (hx : x âˆˆ s) :
  âˆƒ Îµ > 0, metric.closed_ball x Îµ âˆ© s = {x} :=
nhds_basis_closed_ball.exists_inter_eq_singleton_of_mem_discrete hx

end metric

open metric

/-Instantiate a pseudometric space as a pseudoemetric space. Before we can state the instance,
we need to show that the uniform structure coming from the edistance and the
distance coincide. -/

/-- Expressing the uniformity in terms of `edist` -/
protected lemma pseudo_metric.uniformity_basis_edist :
  (ğ“¤ Î±).has_basis (Î» Îµ:â„â‰¥0âˆ, 0 < Îµ) (Î» Îµ, {p | edist p.1 p.2 < Îµ}) :=
âŸ¨begin
  intro t,
  refine mem_uniformity_dist.trans âŸ¨_, _âŸ©; rintro âŸ¨Îµ, Îµ0, HÎµâŸ©,
  { use [ennreal.of_real Îµ, ennreal.of_real_pos.2 Îµ0],
    rintros âŸ¨a, bâŸ©,
    simp only [edist_dist, ennreal.of_real_lt_of_real_iff Îµ0],
    exact HÎµ },
  { rcases ennreal.lt_iff_exists_real_btwn.1 Îµ0 with âŸ¨Îµ', _, Îµ0', hÎµâŸ©,
    rw [ennreal.of_real_pos] at Îµ0',
    refine âŸ¨Îµ', Îµ0', Î» a b h, HÎµ (lt_trans _ hÎµ)âŸ©,
    rwa [edist_dist, ennreal.of_real_lt_of_real_iff Îµ0'] }
endâŸ©

theorem metric.uniformity_edist : ğ“¤ Î± = (â¨… Îµ>0, ğ“Ÿ {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
pseudo_metric.uniformity_basis_edist.eq_binfi

/-- A pseudometric space induces a pseudoemetric space -/
@[priority 100] -- see Note [lower instance priority]
instance pseudo_metric_space.to_pseudo_emetric_space : pseudo_emetric_space Î± :=
{ edist               := edist,
  edist_self          := by simp [edist_dist],
  edist_comm          := by simp only [edist_dist, dist_comm]; simp,
  edist_triangle      := assume x y z, begin
    simp only [edist_dist, â† ennreal.of_real_add, dist_nonneg],
    rw ennreal.of_real_le_of_real_iff _,
    { exact dist_triangle _ _ _ },
    { simpa using add_le_add (dist_nonneg : 0 â‰¤ dist x y) dist_nonneg }
  end,
  uniformity_edist    := metric.uniformity_edist,
  ..â€¹pseudo_metric_space Î±â€º }

/-- In a pseudometric space, an open ball of infinite radius is the whole space -/
lemma metric.eball_top_eq_univ (x : Î±) :
  emetric.ball x âˆ = set.univ :=
set.eq_univ_iff_forall.mpr (Î» y, edist_lt_top y x)

/-- Balls defined using the distance or the edistance coincide -/
@[simp] lemma metric.emetric_ball {x : Î±} {Îµ : â„} : emetric.ball x (ennreal.of_real Îµ) = ball x Îµ :=
begin
  ext y,
  simp only [emetric.mem_ball, mem_ball, edist_dist],
  exact ennreal.of_real_lt_of_real_iff_of_nonneg dist_nonneg
end

/-- Balls defined using the distance or the edistance coincide -/
@[simp] lemma metric.emetric_ball_nnreal {x : Î±} {Îµ : â„â‰¥0} : emetric.ball x Îµ = ball x Îµ :=
by { convert metric.emetric_ball, simp }

/-- Closed balls defined using the distance or the edistance coincide -/
lemma metric.emetric_closed_ball {x : Î±} {Îµ : â„} (h : 0 â‰¤ Îµ) :
  emetric.closed_ball x (ennreal.of_real Îµ) = closed_ball x Îµ :=
by ext y; simp [edist_dist]; rw ennreal.of_real_le_of_real_iff h

/-- Closed balls defined using the distance or the edistance coincide -/
@[simp] lemma metric.emetric_closed_ball_nnreal {x : Î±} {Îµ : â„â‰¥0} :
  emetric.closed_ball x Îµ = closed_ball x Îµ :=
by { convert metric.emetric_closed_ball Îµ.2, simp }

@[simp] lemma metric.emetric_ball_top (x : Î±) : emetric.ball x âŠ¤ = univ :=
eq_univ_of_forall $ Î» y, edist_lt_top _ _

/-- Build a new pseudometric space from an old one where the bundled uniform structure is provably
(but typically non-definitionaly) equal to some given uniform structure.
See Note [forgetful inheritance].
-/
def pseudo_metric_space.replace_uniformity {Î±} [U : uniform_space Î±] (m : pseudo_metric_space Î±)
  (H : @uniformity _ U = @uniformity _ pseudo_emetric_space.to_uniform_space') :
  pseudo_metric_space Î± :=
{ dist               := @dist _ m.to_has_dist,
  dist_self          := dist_self,
  dist_comm          := dist_comm,
  dist_triangle      := dist_triangle,
  edist              := edist,
  edist_dist         := edist_dist,
  to_uniform_space   := U,
  uniformity_dist    := H.trans pseudo_metric_space.uniformity_dist }

/-- One gets a pseudometric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the pseudometric space and the pseudoemetric space. In this definition, the
distance is given separately, to be able to prescribe some expression which is not defeq to the
push-forward of the edistance to reals. -/
def pseudo_emetric_space.to_pseudo_metric_space_of_dist {Î± : Type u} [e : pseudo_emetric_space Î±]
  (dist : Î± â†’ Î± â†’ â„)
  (edist_ne_top : âˆ€x y: Î±, edist x y â‰  âŠ¤)
  (h : âˆ€x y, dist x y = ennreal.to_real (edist x y)) :
  pseudo_metric_space Î± :=
let m : pseudo_metric_space Î± :=
{ dist := dist,
  dist_self          := Î»x, by simp [h],
  dist_comm          := Î»x y, by simp [h, pseudo_emetric_space.edist_comm],
  dist_triangle      := Î»x y z, begin
    simp only [h],
    rw [â† ennreal.to_real_add (edist_ne_top _ _) (edist_ne_top _ _),
        ennreal.to_real_le_to_real (edist_ne_top _ _)],
    { exact edist_triangle _ _ _ },
    { simp [ennreal.add_eq_top, edist_ne_top] }
  end,
  edist := Î»x y, edist x y,
  edist_dist := Î»x y, by simp [h, ennreal.of_real_to_real, edist_ne_top] } in
m.replace_uniformity $ by { rw [uniformity_pseudoedist, metric.uniformity_edist], refl }

/-- One gets a pseudometric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the pseudometric space and the emetric space. -/
def pseudo_emetric_space.to_pseudo_metric_space {Î± : Type u} [e : pseudo_emetric_space Î±]
  (h : âˆ€x y: Î±, edist x y â‰  âŠ¤) : pseudo_metric_space Î± :=
pseudo_emetric_space.to_pseudo_metric_space_of_dist
  (Î»x y, ennreal.to_real (edist x y)) h (Î»x y, rfl)

/-- A very useful criterion to show that a space is complete is to show that all sequences
which satisfy a bound of the form `dist (u n) (u m) < B N` for all `n m â‰¥ N` are
converging. This is often applied for `B N = 2^{-N}`, i.e., with a very fast convergence to
`0`, which makes it possible to use arguments of converging series, while this is impossible
to do in general for arbitrary Cauchy sequences. -/
theorem metric.complete_of_convergent_controlled_sequences (B : â„• â†’ real) (hB : âˆ€n, 0 < B n)
  (H : âˆ€u : â„• â†’ Î±, (âˆ€N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ dist (u n) (u m) < B N) â†’
    âˆƒx, tendsto u at_top (ğ“ x)) :
  complete_space Î± :=
begin
  -- this follows from the same criterion in emetric spaces. We just need to translate
  -- the convergence assumption from `dist` to `edist`
  apply emetric.complete_of_convergent_controlled_sequences (Î»n, ennreal.of_real (B n)),
  { simp [hB] },
  { assume u Hu,
    apply H,
    assume N n m hn hm,
    rw [â† ennreal.of_real_lt_of_real_iff (hB N), â† edist_dist],
    exact Hu N n m hn hm }
end

theorem metric.complete_of_cauchy_seq_tendsto :
  (âˆ€ u : â„• â†’ Î±, cauchy_seq u â†’ âˆƒa, tendsto u at_top (ğ“ a)) â†’ complete_space Î± :=
emetric.complete_of_cauchy_seq_tendsto

section real

/-- Instantiate the reals as a pseudometric space. -/
instance real.pseudo_metric_space : pseudo_metric_space â„ :=
{ dist               := Î»x y, |x - y|,
  dist_self          := by simp [abs_zero],
  dist_comm          := assume x y, abs_sub_comm _ _,
  dist_triangle      := assume x y z, abs_sub_le _ _ _ }

theorem real.dist_eq (x y : â„) : dist x y = |x - y| := rfl

theorem real.nndist_eq (x y : â„) : nndist x y = real.nnabs (x - y) := rfl

theorem real.nndist_eq' (x y : â„) : nndist x y = real.nnabs (y - x) := nndist_comm _ _

theorem real.dist_0_eq_abs (x : â„) : dist x 0 = |x| :=
by simp [real.dist_eq]

theorem real.dist_left_le_of_mem_interval {x y z : â„} (h : y âˆˆ interval x z) :
  dist x y â‰¤ dist x z :=
by simpa only [dist_comm x] using abs_sub_left_of_mem_interval h

theorem real.dist_right_le_of_mem_interval {x y z : â„} (h : y âˆˆ interval x z) :
  dist y z â‰¤ dist x z :=
by simpa only [dist_comm _ z] using abs_sub_right_of_mem_interval h

theorem real.dist_le_of_mem_interval {x y x' y' : â„} (hx : x âˆˆ interval x' y')
  (hy : y âˆˆ interval x' y') : dist x y â‰¤ dist x' y' :=
abs_sub_le_of_subinterval $ interval_subset_interval (by rwa interval_swap) (by rwa interval_swap)

theorem real.dist_le_of_mem_Icc {x y x' y' : â„} (hx : x âˆˆ Icc x' y') (hy : y âˆˆ Icc x' y') :
  dist x y â‰¤ y' - x' :=
by simpa only [real.dist_eq, abs_of_nonpos (sub_nonpos.2 $ hx.1.trans hx.2), neg_sub]
  using real.dist_le_of_mem_interval (Icc_subset_interval hx) (Icc_subset_interval hy)

theorem real.dist_le_of_mem_Icc_01 {x y : â„} (hx : x âˆˆ Icc (0:â„) 1) (hy : y âˆˆ Icc (0:â„) 1) :
  dist x y â‰¤ 1 :=
by simpa only [sub_zero] using real.dist_le_of_mem_Icc hx hy

instance : order_topology â„ :=
order_topology_of_nhds_abs $ Î» x,
  by simp only [nhds_basis_ball.eq_binfi, ball, real.dist_eq, abs_sub_comm]

lemma real.ball_eq (x r : â„) : ball x r = Ioo (x - r) (x + r) :=
set.ext $ Î» y, by rw [mem_ball, dist_comm, real.dist_eq,
  abs_sub_lt_iff, mem_Ioo, â† sub_lt_iff_lt_add', sub_lt]

lemma real.closed_ball_eq {x r : â„} : closed_ball x r = Icc (x - r) (x + r) :=
by ext y; rw [mem_closed_ball, dist_comm, real.dist_eq,
  abs_sub_le_iff, mem_Icc, â† sub_le_iff_le_add', sub_le]

section metric_ordered

variables [conditionally_complete_linear_order Î±] [order_topology Î±]

lemma totally_bounded_Icc (a b : Î±) : totally_bounded (Icc a b) :=
is_compact_Icc.totally_bounded

lemma totally_bounded_Ico (a b : Î±) : totally_bounded (Ico a b) :=
totally_bounded_subset Ico_subset_Icc_self (totally_bounded_Icc a b)

lemma totally_bounded_Ioc (a b : Î±) : totally_bounded (Ioc a b) :=
totally_bounded_subset Ioc_subset_Icc_self (totally_bounded_Icc a b)

lemma totally_bounded_Ioo (a b : Î±) : totally_bounded (Ioo a b) :=
totally_bounded_subset Ioo_subset_Icc_self (totally_bounded_Icc a b)

end metric_ordered

/-- Special case of the sandwich theorem; see `tendsto_of_tendsto_of_tendsto_of_le_of_le'` for the
general case. -/
lemma squeeze_zero' {Î±} {f g : Î± â†’ â„} {tâ‚€ : filter Î±} (hf : âˆ€á¶  t in tâ‚€, 0 â‰¤ f t)
  (hft : âˆ€á¶  t in tâ‚€, f t â‰¤ g t) (g0 : tendsto g tâ‚€ (nhds 0)) : tendsto f tâ‚€ (ğ“ 0) :=
tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds g0 hf hft

/-- Special case of the sandwich theorem; see `tendsto_of_tendsto_of_tendsto_of_le_of_le`
and  `tendsto_of_tendsto_of_tendsto_of_le_of_le'` for the general case. -/
lemma squeeze_zero {Î±} {f g : Î± â†’ â„} {tâ‚€ : filter Î±} (hf : âˆ€t, 0 â‰¤ f t) (hft : âˆ€t, f t â‰¤ g t)
  (g0 : tendsto g tâ‚€ (ğ“ 0)) : tendsto f tâ‚€ (ğ“ 0) :=
squeeze_zero' (eventually_of_forall hf) (eventually_of_forall hft) g0

theorem metric.uniformity_eq_comap_nhds_zero :
  ğ“¤ Î± = comap (Î»p:Î±Ã—Î±, dist p.1 p.2) (ğ“ (0 : â„)) :=
by { ext s,
  simp [mem_uniformity_dist, (nhds_basis_ball.comap _).mem_iff, subset_def, real.dist_0_eq_abs] }

lemma cauchy_seq_iff_tendsto_dist_at_top_0 [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” tendsto (Î» (n : Î² Ã— Î²), dist (u n.1) (u n.2)) at_top (ğ“ 0) :=
by rw [cauchy_seq_iff_tendsto, metric.uniformity_eq_comap_nhds_zero, tendsto_comap_iff,
  prod.map_def]

lemma tendsto_uniformity_iff_dist_tendsto_zero {Î¹ : Type*} {f : Î¹ â†’ Î± Ã— Î±} {p : filter Î¹} :
  tendsto f p (ğ“¤ Î±) â†” tendsto (Î» x, dist (f x).1 (f x).2) p (ğ“ 0) :=
by rw [metric.uniformity_eq_comap_nhds_zero, tendsto_comap_iff]

lemma filter.tendsto.congr_dist {Î¹ : Type*} {fâ‚ fâ‚‚ : Î¹ â†’ Î±} {p : filter Î¹} {a : Î±}
  (hâ‚ : tendsto fâ‚ p (ğ“ a)) (h : tendsto (Î» x, dist (fâ‚ x) (fâ‚‚ x)) p (ğ“ 0)) :
  tendsto fâ‚‚ p (ğ“ a) :=
hâ‚.congr_uniformity $ tendsto_uniformity_iff_dist_tendsto_zero.2 h

alias filter.tendsto.congr_dist â†  tendsto_of_tendsto_of_dist

lemma tendsto_iff_of_dist {Î¹ : Type*} {fâ‚ fâ‚‚ : Î¹ â†’ Î±} {p : filter Î¹} {a : Î±}
  (h : tendsto (Î» x, dist (fâ‚ x) (fâ‚‚ x)) p (ğ“ 0)) :
  tendsto fâ‚ p (ğ“ a) â†” tendsto fâ‚‚ p (ğ“ a) :=
uniform.tendsto_congr $ tendsto_uniformity_iff_dist_tendsto_zero.2 h

end real

section cauchy_seq
variables [nonempty Î²] [semilattice_sup Î²]

/-- In a pseudometric space, Cauchy sequences are characterized by the fact that, eventually,
the distance between its elements is arbitrarily small -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem metric.cauchy_seq_iff {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>0, âˆƒN, âˆ€m nâ‰¥N, dist (u m) (u n) < Îµ :=
uniformity_basis_dist.cauchy_seq_iff

/-- A variation around the pseudometric characterization of Cauchy sequences -/
theorem metric.cauchy_seq_iff' {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>0, âˆƒN, âˆ€nâ‰¥N, dist (u n) (u N) < Îµ :=
uniformity_basis_dist.cauchy_seq_iff'

/-- If the distance between `s n` and `s m`, `n, m â‰¥ N` is bounded above by `b N`
and `b` converges to zero, then `s` is a Cauchy sequence.  -/
lemma cauchy_seq_of_le_tendsto_0 {s : Î² â†’ Î±} (b : Î² â†’ â„)
  (h : âˆ€ n m N : Î², N â‰¤ n â†’ N â‰¤ m â†’ dist (s n) (s m) â‰¤ b N) (hâ‚€ : tendsto b at_top (nhds 0)) :
  cauchy_seq s :=
metric.cauchy_seq_iff.2 $ Î» Îµ Îµ0,
  (metric.tendsto_at_top.1 hâ‚€ Îµ Îµ0).imp $ Î» N hN m n hm hn,
  calc dist (s m) (s n) â‰¤ b N : h m n N hm hn
                    ... â‰¤ |b N| : le_abs_self _
                    ... = dist (b N) 0 : by rw real.dist_0_eq_abs; refl
                    ... < Îµ : (hN _ (le_refl N))

/-- A Cauchy sequence on the natural numbers is bounded. -/
theorem cauchy_seq_bdd {u : â„• â†’ Î±} (hu : cauchy_seq u) :
  âˆƒ R > 0, âˆ€ m n, dist (u m) (u n) < R :=
begin
  rcases metric.cauchy_seq_iff'.1 hu 1 zero_lt_one with âŸ¨N, hNâŸ©,
  suffices : âˆƒ R > 0, âˆ€ n, dist (u n) (u N) < R,
  { rcases this with âŸ¨R, R0, HâŸ©,
    exact âŸ¨_, add_pos R0 R0, Î» m n,
      lt_of_le_of_lt (dist_triangle_right _ _ _) (add_lt_add (H m) (H n))âŸ© },
  let R := finset.sup (finset.range N) (Î» n, nndist (u n) (u N)),
  refine âŸ¨â†‘R + 1, add_pos_of_nonneg_of_pos R.2 zero_lt_one, Î» n, _âŸ©,
  cases le_or_lt N n,
  { exact lt_of_lt_of_le (hN _ h) (le_add_of_nonneg_left R.2) },
  { have : _ â‰¤ R := finset.le_sup (finset.mem_range.2 h),
    exact lt_of_le_of_lt this (lt_add_of_pos_right _ zero_lt_one) }
end

/-- Yet another metric characterization of Cauchy sequences on integers. This one is often the
most efficient. -/
lemma cauchy_seq_iff_le_tendsto_0 {s : â„• â†’ Î±} : cauchy_seq s â†” âˆƒ b : â„• â†’ â„,
  (âˆ€ n, 0 â‰¤ b n) âˆ§
  (âˆ€ n m N : â„•, N â‰¤ n â†’ N â‰¤ m â†’ dist (s n) (s m) â‰¤ b N) âˆ§
  tendsto b at_top (ğ“ 0) :=
âŸ¨Î» hs, begin
  /- `s` is a Cauchy sequence. The sequence `b` will be constructed by taking
  the supremum of the distances between `s n` and `s m` for `n m â‰¥ N`.
  First, we prove that all these distances are bounded, as otherwise the Sup
  would not make sense. -/
  let S := Î» N, (Î»(p : â„• Ã— â„•), dist (s p.1) (s p.2)) '' {p | p.1 â‰¥ N âˆ§ p.2 â‰¥ N},
  have hS : âˆ€ N, âˆƒ x, âˆ€ y âˆˆ S N, y â‰¤ x,
  { rcases cauchy_seq_bdd hs with âŸ¨R, R0, hRâŸ©,
    refine Î» N, âŸ¨R, _âŸ©, rintro _ âŸ¨âŸ¨m, nâŸ©, _, rflâŸ©,
    exact le_of_lt (hR m n) },
  have bdd : bdd_above (range (Î»(p : â„• Ã— â„•), dist (s p.1) (s p.2))),
  { rcases cauchy_seq_bdd hs with âŸ¨R, R0, hRâŸ©,
    use R, rintro _ âŸ¨âŸ¨m, nâŸ©, rflâŸ©, exact le_of_lt (hR m n) },
  -- Prove that it bounds the distances of points in the Cauchy sequence
  have ub : âˆ€ m n N, N â‰¤ m â†’ N â‰¤ n â†’ dist (s m) (s n) â‰¤ Sup (S N) :=
    Î» m n N hm hn, le_cSup (hS N) âŸ¨âŸ¨_, _âŸ©, âŸ¨hm, hnâŸ©, rflâŸ©,
  have S0m : âˆ€ n, (0:â„) âˆˆ S n := Î» n, âŸ¨âŸ¨n, nâŸ©, âŸ¨le_refl _, le_refl _âŸ©, dist_self _âŸ©,
  have S0 := Î» n, le_cSup (hS n) (S0m n),
  -- Prove that it tends to `0`, by using the Cauchy property of `s`
  refine âŸ¨Î» N, Sup (S N), S0, ub, metric.tendsto_at_top.2 (Î» Îµ Îµ0, _)âŸ©,
  refine (metric.cauchy_seq_iff.1 hs (Îµ/2) (half_pos Îµ0)).imp (Î» N hN n hn, _),
  rw [real.dist_0_eq_abs, abs_of_nonneg (S0 n)],
  refine lt_of_le_of_lt (cSup_le âŸ¨_, S0m _âŸ© _) (half_lt_self Îµ0),
  rintro _ âŸ¨âŸ¨m', n'âŸ©, âŸ¨hm', hn'âŸ©, rflâŸ©,
  exact le_of_lt (hN _ _ (le_trans hn hm') (le_trans hn hn'))
  end,
Î» âŸ¨b, _, b_bound, b_limâŸ©, cauchy_seq_of_le_tendsto_0 b b_bound b_limâŸ©

end cauchy_seq

/-- Pseudometric space structure pulled back by a function. -/
def pseudo_metric_space.induced {Î± Î²} (f : Î± â†’ Î²)
  (m : pseudo_metric_space Î²) : pseudo_metric_space Î± :=
{ dist               := Î» x y, dist (f x) (f y),
  dist_self          := Î» x, dist_self _,
  dist_comm          := Î» x y, dist_comm _ _,
  dist_triangle      := Î» x y z, dist_triangle _ _ _,
  edist              := Î» x y, edist (f x) (f y),
  edist_dist         := Î» x y, edist_dist _ _,
  to_uniform_space   := uniform_space.comap f m.to_uniform_space,
  uniformity_dist    := begin
    apply @uniformity_dist_of_mem_uniformity _ _ _ _ _ (Î» x y, dist (f x) (f y)),
    refine Î» s, mem_comap.trans _,
    split; intro H,
    { rcases H with âŸ¨r, ru, rsâŸ©,
      rcases mem_uniformity_dist.1 ru with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      refine âŸ¨Îµ, Îµ0, Î» a b h, rs (hÎµ _)âŸ©, exact h },
    { rcases H with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      exact âŸ¨_, dist_mem_uniformity Îµ0, Î» âŸ¨a, bâŸ©, hÎµâŸ© }
  end }

/-- Pull back a pseudometric space structure by a uniform inducing map. This is a version of
`pseudo_metric_space.induced` useful in case if the domain already has a `uniform_space`
structure. -/
def uniform_inducing.comap_pseudo_metric_space {Î± Î²} [uniform_space Î±] [pseudo_metric_space Î²]
  (f : Î± â†’ Î²) (h : uniform_inducing f) : pseudo_metric_space Î± :=
(pseudo_metric_space.induced f â€¹_â€º).replace_uniformity h.comap_uniformity.symm

instance subtype.psudo_metric_space {Î± : Type*} {p : Î± â†’ Prop} [t : pseudo_metric_space Î±] :
  pseudo_metric_space (subtype p) :=
pseudo_metric_space.induced coe t

theorem subtype.pseudo_dist_eq {p : Î± â†’ Prop} (x y : subtype p) : dist x y = dist (x : Î±) y := rfl

section nnreal

instance : pseudo_metric_space â„â‰¥0 := by unfold nnreal; apply_instance

lemma nnreal.dist_eq (a b : â„â‰¥0) : dist a b = |(a:â„) - b| := rfl

lemma nnreal.nndist_eq (a b : â„â‰¥0) :
  nndist a b = max (a - b) (b - a) :=
begin
  wlog h : a â‰¤ b,
  { apply nnreal.coe_eq.1,
    rw [sub_eq_zero_iff_le.2 h, max_eq_right (zero_le $ b - a), â† dist_nndist, nnreal.dist_eq,
      nnreal.coe_sub h, abs_eq_max_neg, neg_sub],
    apply max_eq_right,
    linarith [nnreal.coe_le_coe.2 h] },
  rwa [nndist_comm, max_comm]
end
end nnreal

section prod

instance prod.pseudo_metric_space_max [pseudo_metric_space Î²] : pseudo_metric_space (Î± Ã— Î²) :=
{ dist := Î» x y, max (dist x.1 y.1) (dist x.2 y.2),
  dist_self := Î» x, by simp,
  dist_comm := Î» x y, by simp [dist_comm],
  dist_triangle := Î» x y z, max_le
    (le_trans (dist_triangle _ _ _) (add_le_add (le_max_left _ _) (le_max_left _ _)))
    (le_trans (dist_triangle _ _ _) (add_le_add (le_max_right _ _) (le_max_right _ _))),
  edist := Î» x y, max (edist x.1 y.1) (edist x.2 y.2),
  edist_dist := assume x y, begin
    have : monotone ennreal.of_real := assume x y h, ennreal.of_real_le_of_real h,
    rw [edist_dist, edist_dist, â† this.map_max]
  end,
  uniformity_dist := begin
    refine uniformity_prod.trans _,
    simp only [uniformity_basis_dist.eq_binfi, comap_infi],
    rw â† infi_inf_eq, congr, funext,
    rw â† infi_inf_eq, congr, funext,
    simp [inf_principal, ext_iff, max_lt_iff]
  end,
  to_uniform_space := prod.uniform_space }

lemma prod.dist_eq [pseudo_metric_space Î²] {x y : Î± Ã— Î²} :
  dist x y = max (dist x.1 y.1) (dist x.2 y.2) := rfl

theorem ball_prod_same [pseudo_metric_space Î²] (x : Î±) (y : Î²) (r : â„) :
  (ball x r).prod (ball y r) = ball (x, y) r :=
ext $ Î» z, by simp [prod.dist_eq]

theorem closed_ball_prod_same [pseudo_metric_space Î²] (x : Î±) (y : Î²) (r : â„) :
  (closed_ball x r).prod (closed_ball y r) = closed_ball (x, y) r :=
ext $ Î» z, by simp [prod.dist_eq]

end prod

theorem uniform_continuous_dist : uniform_continuous (Î»p:Î±Ã—Î±, dist p.1 p.2) :=
metric.uniform_continuous_iff.2 (Î» Îµ Îµ0, âŸ¨Îµ/2, half_pos Îµ0,
begin
  suffices,
  { intros p q h, cases p with pâ‚ pâ‚‚, cases q with qâ‚ qâ‚‚,
    cases max_lt_iff.1 h with hâ‚ hâ‚‚, clear h,
    dsimp at hâ‚ hâ‚‚ âŠ¢,
    rw real.dist_eq,
    refine abs_sub_lt_iff.2 âŸ¨_, _âŸ©,
    { revert pâ‚ pâ‚‚ qâ‚ qâ‚‚ hâ‚ hâ‚‚, exact this },
    { apply this; rwa dist_comm } },
  intros pâ‚ pâ‚‚ qâ‚ qâ‚‚ hâ‚ hâ‚‚,
  have := add_lt_add
    (abs_sub_lt_iff.1 (lt_of_le_of_lt (abs_dist_sub_le pâ‚ qâ‚ pâ‚‚) hâ‚)).1
    (abs_sub_lt_iff.1 (lt_of_le_of_lt (abs_dist_sub_le pâ‚‚ qâ‚‚ qâ‚) hâ‚‚)).1,
  rwa [add_halves, dist_comm pâ‚‚, sub_add_sub_cancel, dist_comm qâ‚‚] at this
endâŸ©)

theorem uniform_continuous.dist [uniform_space Î²] {f g : Î² â†’ Î±}
  (hf : uniform_continuous f) (hg : uniform_continuous g) :
  uniform_continuous (Î»b, dist (f b) (g b)) :=
uniform_continuous_dist.comp (hf.prod_mk hg)

@[continuity]
theorem continuous_dist : continuous (Î»p:Î±Ã—Î±, dist p.1 p.2) :=
uniform_continuous_dist.continuous

@[continuity]
theorem continuous.dist [topological_space Î²] {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) : continuous (Î»b, dist (f b) (g b)) :=
continuous_dist.comp (hf.prod_mk hg : _)

theorem filter.tendsto.dist {f g : Î² â†’ Î±} {x : filter Î²} {a b : Î±}
  (hf : tendsto f x (ğ“ a)) (hg : tendsto g x (ğ“ b)) :
  tendsto (Î»x, dist (f x) (g x)) x (ğ“ (dist a b)) :=
(continuous_dist.tendsto (a, b)).comp (hf.prod_mk_nhds hg)

lemma nhds_comap_dist (a : Î±) : (ğ“ (0 : â„)).comap (Î»a', dist a' a) = ğ“ a :=
by simp only [@nhds_eq_comap_uniformity Î±, metric.uniformity_eq_comap_nhds_zero,
  comap_comap, (âˆ˜), dist_comm]

lemma tendsto_iff_dist_tendsto_zero {f : Î² â†’ Î±} {x : filter Î²} {a : Î±} :
  (tendsto f x (ğ“ a)) â†” (tendsto (Î»b, dist (f b) a) x (ğ“ 0)) :=
by rw [â† nhds_comap_dist a, tendsto_comap_iff]

lemma uniform_continuous_nndist : uniform_continuous (Î»p:Î±Ã—Î±, nndist p.1 p.2) :=
uniform_continuous_subtype_mk uniform_continuous_dist _

lemma uniform_continuous.nndist [uniform_space Î²] {f g : Î² â†’ Î±} (hf : uniform_continuous f)
  (hg : uniform_continuous g) :
  uniform_continuous (Î» b, nndist (f b) (g b)) :=
uniform_continuous_nndist.comp (hf.prod_mk hg)

lemma continuous_nndist : continuous (Î»p:Î±Ã—Î±, nndist p.1 p.2) :=
uniform_continuous_nndist.continuous

lemma continuous.nndist [topological_space Î²] {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) : continuous (Î»b, nndist (f b) (g b)) :=
continuous_nndist.comp (hf.prod_mk hg : _)

theorem filter.tendsto.nndist {f g : Î² â†’ Î±} {x : filter Î²} {a b : Î±}
  (hf : tendsto f x (ğ“ a)) (hg : tendsto g x (ğ“ b)) :
  tendsto (Î»x, nndist (f x) (g x)) x (ğ“ (nndist a b)) :=
(continuous_nndist.tendsto (a, b)).comp (hf.prod_mk_nhds hg)

namespace metric
variables {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : â„} {s : set Î±}

theorem is_closed_ball : is_closed (closed_ball x Îµ) :=
is_closed_le (continuous_id.dist continuous_const) continuous_const

lemma is_closed_sphere : is_closed (sphere x Îµ) :=
is_closed_eq (continuous_id.dist continuous_const) continuous_const

@[simp] theorem closure_closed_ball : closure (closed_ball x Îµ) = closed_ball x Îµ :=
is_closed_ball.closure_eq

theorem closure_ball_subset_closed_ball : closure (ball x Îµ) âŠ† closed_ball x Îµ :=
closure_minimal ball_subset_closed_ball is_closed_ball

theorem frontier_ball_subset_sphere : frontier (ball x Îµ) âŠ† sphere x Îµ :=
frontier_lt_subset_eq (continuous_id.dist continuous_const) continuous_const

theorem frontier_closed_ball_subset_sphere : frontier (closed_ball x Îµ) âŠ† sphere x Îµ :=
frontier_le_subset_eq (continuous_id.dist continuous_const) continuous_const

theorem ball_subset_interior_closed_ball : ball x Îµ âŠ† interior (closed_ball x Îµ) :=
interior_maximal ball_subset_closed_ball is_open_ball

/-- Îµ-characterization of the closure in pseudometric spaces-/
theorem mem_closure_iff {Î± : Type u} [pseudo_metric_space Î±] {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆ€Îµ>0, âˆƒb âˆˆ s, dist a b < Îµ :=
(mem_closure_iff_nhds_basis nhds_basis_ball).trans $
  by simp only [mem_ball, dist_comm]

lemma mem_closure_range_iff {Î± : Type u} [pseudo_metric_space Î±] {e : Î² â†’ Î±} {a : Î±} :
  a âˆˆ closure (range e) â†” âˆ€Îµ>0, âˆƒ k : Î², dist a (e k) < Îµ :=
by simp only [mem_closure_iff, exists_range_iff]

lemma mem_closure_range_iff_nat {Î± : Type u} [pseudo_metric_space Î±] {e : Î² â†’ Î±} {a : Î±} :
  a âˆˆ closure (range e) â†” âˆ€n : â„•, âˆƒ k : Î², dist a (e k) < 1 / ((n : â„) + 1) :=
(mem_closure_iff_nhds_basis nhds_basis_ball_inv_nat_succ).trans $
  by simp only [mem_ball, dist_comm, exists_range_iff, forall_const]

theorem mem_of_closed' {Î± : Type u} [pseudo_metric_space Î±] {s : set Î±} (hs : is_closed s)
  {a : Î±} : a âˆˆ s â†” âˆ€Îµ>0, âˆƒb âˆˆ s, dist a b < Îµ :=
by simpa only [hs.closure_eq] using @mem_closure_iff _ _ s a

end metric

section pi
open finset
variables {Ï€ : Î² â†’ Type*} [fintype Î²] [âˆ€b, pseudo_metric_space (Ï€ b)]

/-- A finite product of pseudometric spaces is a pseudometric space, with the sup distance. -/
instance pseudo_metric_space_pi : pseudo_metric_space (Î b, Ï€ b) :=
begin
  /- we construct the instance from the pseudoemetric space instance to avoid checking again that
  the uniformity is the same as the product uniformity, but we register nevertheless a nice formula
  for the distance -/
  refine pseudo_emetric_space.to_pseudo_metric_space_of_dist
    (Î»f g, ((sup univ (Î»b, nndist (f b) (g b)) : â„â‰¥0) : â„)) _ _,
  show âˆ€ (x y : Î  (b : Î²), Ï€ b), edist x y â‰  âŠ¤,
  { assume x y,
    rw â† lt_top_iff_ne_top,
    have : (âŠ¥ : â„â‰¥0âˆ) < âŠ¤ := ennreal.coe_lt_top,
    simp [edist_pi_def, finset.sup_lt_iff this, edist_lt_top] },
  show âˆ€ (x y : Î  (b : Î²), Ï€ b), â†‘(sup univ (Î» (b : Î²), nndist (x b) (y b))) =
    ennreal.to_real (sup univ (Î» (b : Î²), edist (x b) (y b))),
  { assume x y,
    simp only [edist_nndist],
    norm_cast }
end

lemma nndist_pi_def (f g : Î b, Ï€ b) : nndist f g = sup univ (Î»b, nndist (f b) (g b)) :=
subtype.eta _ _

lemma dist_pi_def (f g : Î b, Ï€ b) :
  dist f g = (sup univ (Î»b, nndist (f b) (g b)) : â„â‰¥0) := rfl

@[simp] lemma dist_pi_const [nonempty Î²] (a b : Î±) : dist (Î» x : Î², a) (Î» _, b) = dist a b :=
by simpa only [dist_edist] using congr_arg ennreal.to_real (edist_pi_const a b)

@[simp] lemma nndist_pi_const [nonempty Î²] (a b : Î±) :
  nndist (Î» x : Î², a) (Î» _, b) = nndist a b := nnreal.eq $ dist_pi_const a b

lemma dist_pi_lt_iff {f g : Î b, Ï€ b} {r : â„} (hr : 0 < r) :
  dist f g < r â†” âˆ€b, dist (f b) (g b) < r :=
begin
  lift r to â„â‰¥0 using hr.le,
  simp [dist_pi_def, finset.sup_lt_iff (show âŠ¥ < r, from hr)],
end

lemma dist_pi_le_iff {f g : Î b, Ï€ b} {r : â„} (hr : 0 â‰¤ r) :
  dist f g â‰¤ r â†” âˆ€b, dist (f b) (g b) â‰¤ r :=
begin
  lift r to â„â‰¥0 using hr,
  simp [nndist_pi_def]
end

lemma nndist_le_pi_nndist (f g : Î b, Ï€ b) (b : Î²) : nndist (f b) (g b) â‰¤ nndist f g :=
by { rw [nndist_pi_def], exact finset.le_sup (finset.mem_univ b) }

lemma dist_le_pi_dist (f g : Î b, Ï€ b) (b : Î²) : dist (f b) (g b) â‰¤ dist f g :=
by simp only [dist_nndist, nnreal.coe_le_coe, nndist_le_pi_nndist f g b]

/-- An open ball in a product space is a product of open balls. See also `metric.ball_pi'`
for a version assuming `nonempty Î²` instead of `0 < r`. -/
lemma ball_pi (x : Î b, Ï€ b) {r : â„} (hr : 0 < r) :
  ball x r = set.pi univ (Î» b, ball (x b) r) :=
by { ext p, simp [dist_pi_lt_iff hr] }

/-- An open ball in a product space is a product of open balls. See also `metric.ball_pi`
for a version assuming `0 < r` instead of `nonempty Î²`. -/
lemma ball_pi' [nonempty Î²] (x : Î  b, Ï€ b) (r : â„) :
  ball x r = set.pi univ (Î» b, ball (x b) r) :=
(lt_or_le 0 r).elim (ball_pi x) $ Î» hr, by simp [ball_eq_empty.2 hr]

/-- A closed ball in a product space is a product of closed balls. See also `metric.closed_ball_pi'`
for a version assuming `nonempty Î²` instead of `0 â‰¤ r`. -/
lemma closed_ball_pi (x : Î b, Ï€ b) {r : â„} (hr : 0 â‰¤ r) :
  closed_ball x r = set.pi univ (Î» b, closed_ball (x b) r) :=
by { ext p, simp [dist_pi_le_iff hr] }

/-- A closed ball in a product space is a product of closed balls. See also `metric.closed_ball_pi`
for a version assuming `0 â‰¤ r` instead of `nonempty Î²`. -/
lemma closed_ball_pi' [nonempty Î²] (x : Î  b, Ï€ b) (r : â„) :
  closed_ball x r = set.pi univ (Î» b, closed_ball (x b) r) :=
(le_or_lt 0 r).elim (closed_ball_pi x) $ Î» hr, by simp [closed_ball_eq_empty.2 hr]

lemma real.dist_le_of_mem_pi_Icc {x y x' y' : Î² â†’ â„} (hx : x âˆˆ Icc x' y') (hy : y âˆˆ Icc x' y') :
  dist x y â‰¤ dist x' y' :=
begin
  refine (dist_pi_le_iff dist_nonneg).2 (Î» b, (real.dist_le_of_mem_interval _ _).trans
    (dist_le_pi_dist _ _ b)); refine Icc_subset_interval _,
  exacts [âŸ¨hx.1 _, hx.2 _âŸ©, âŸ¨hy.1 _, hy.2 _âŸ©]
end

end pi

section compact

/-- Any compact set in a pseudometric space can be covered by finitely many balls of a given
positive radius -/
lemma finite_cover_balls_of_compact {Î± : Type u} [pseudo_metric_space Î±] {s : set Î±}
  (hs : is_compact s) {e : â„} (he : 0 < e) :
  âˆƒt âŠ† s, finite t âˆ§ s âŠ† â‹ƒxâˆˆt, ball x e :=
begin
  apply hs.elim_finite_subcover_image,
  { simp [is_open_ball] },
  { intros x xs,
    simp,
    exact âŸ¨x, âŸ¨xs, by simpaâŸ©âŸ© }
end

alias finite_cover_balls_of_compact â† is_compact.finite_cover_balls

end compact

section proper_space
open metric

/-- A pseudometric space is proper if all closed balls are compact. -/
class proper_space (Î± : Type u) [pseudo_metric_space Î±] : Prop :=
(is_compact_closed_ball : âˆ€x:Î±, âˆ€r, is_compact (closed_ball x r))

/-- In a proper pseudometric space, all spheres are compact. -/
lemma is_compact_sphere {Î± : Type*} [pseudo_metric_space Î±] [proper_space Î±] (x : Î±) (r : â„) :
  is_compact (sphere x r) :=
compact_of_is_closed_subset (proper_space.is_compact_closed_ball x r) is_closed_sphere
  sphere_subset_closed_ball

/-- In a proper pseudometric space, any sphere is a `compact_space` when considered as a subtype. -/
instance {Î± : Type*} [pseudo_metric_space Î±] [proper_space Î±] (x : Î±) (r : â„) :
  compact_space (sphere x r) :=
is_compact_iff_compact_space.mp (is_compact_sphere _ _)

/-- A proper pseudo metric space is sigma compact, and therefore second countable. -/
@[priority 100] -- see Note [lower instance priority]
instance second_countable_of_proper [proper_space Î±] :
  second_countable_topology Î± :=
begin
  -- We already have `sigma_compact_space_of_locally_compact_second_countable`, so we don't
  -- add an instance for `sigma_compact_space`.
  suffices : sigma_compact_space Î±, by exactI emetric.second_countable_of_sigma_compact Î±,
  rcases em (nonempty Î±) with âŸ¨âŸ¨xâŸ©âŸ©|hn,
  { exact âŸ¨âŸ¨Î» n, closed_ball x n, Î» n, proper_space.is_compact_closed_ball _ _,
      Union_closed_ball_nat _âŸ©âŸ© },
  { exact âŸ¨âŸ¨Î» n, âˆ…, Î» n, is_compact_empty, Union_eq_univ_iff.2 $ Î» x, (hn âŸ¨xâŸ©).elimâŸ©âŸ© }
end

lemma tendsto_dist_right_cocompact_at_top [proper_space Î±] (x : Î±) :
  tendsto (Î» y, dist y x) (cocompact Î±) at_top :=
(has_basis_cocompact.tendsto_iff at_top_basis).2 $ Î» r hr,
  âŸ¨closed_ball x r, proper_space.is_compact_closed_ball x r,
    Î» y hy, (not_le.1 $ mt mem_closed_ball.2 hy).leâŸ©

lemma tendsto_dist_left_cocompact_at_top [proper_space Î±] (x : Î±) :
  tendsto (dist x) (cocompact Î±) at_top :=
by simpa only [dist_comm] using tendsto_dist_right_cocompact_at_top x

/-- If all closed balls of large enough radius are compact, then the space is proper. Especially
useful when the lower bound for the radius is 0. -/
lemma proper_space_of_compact_closed_ball_of_le
  (R : â„) (h : âˆ€x:Î±, âˆ€r, R â‰¤ r â†’ is_compact (closed_ball x r)) :
  proper_space Î± :=
âŸ¨begin
  assume x r,
  by_cases hr : R â‰¤ r,
  { exact h x r hr },
  { have : closed_ball x r = closed_ball x R âˆ© closed_ball x r,
    { symmetry,
      apply inter_eq_self_of_subset_right,
      exact closed_ball_subset_closed_ball (le_of_lt (not_le.1 hr)) },
    rw this,
    exact (h x R (le_refl _)).inter_right is_closed_ball }
endâŸ©

/- A compact pseudometric space is proper -/
@[priority 100] -- see Note [lower instance priority]
instance proper_of_compact [compact_space Î±] : proper_space Î± :=
âŸ¨assume x r, is_closed_ball.is_compactâŸ©

/-- A proper space is locally compact -/
@[priority 100] -- see Note [lower instance priority]
instance locally_compact_of_proper [proper_space Î±] :
  locally_compact_space Î± :=
locally_compact_space_of_has_basis (Î» x, nhds_basis_closed_ball) $
  Î» x Îµ Îµ0, proper_space.is_compact_closed_ball _ _

/-- A proper space is complete -/
@[priority 100] -- see Note [lower instance priority]
instance complete_of_proper [proper_space Î±] : complete_space Î± :=
âŸ¨begin
  intros f hf,
  /- We want to show that the Cauchy filter `f` is converging. It suffices to find a closed
  ball (therefore compact by properness) where it is nontrivial. -/
  obtain âŸ¨t, t_fset, htâŸ© : âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, dist x y < 1 :=
    (metric.cauchy_iff.1 hf).2 1 zero_lt_one,
  rcases hf.1.nonempty_of_mem t_fset with âŸ¨x, xtâŸ©,
  have : closed_ball x 1 âˆˆ f := mem_of_superset t_fset (Î» y yt, (ht y x yt xt).le),
  rcases (compact_iff_totally_bounded_complete.1 (proper_space.is_compact_closed_ball x 1)).2 f hf
    (le_principal_iff.2 this) with âŸ¨y, -, hyâŸ©,
  exact âŸ¨y, hyâŸ©
endâŸ©

/-- A finite product of proper spaces is proper. -/
instance pi_proper_space {Ï€ : Î² â†’ Type*} [fintype Î²] [âˆ€b, pseudo_metric_space (Ï€ b)]
  [h : âˆ€b, proper_space (Ï€ b)] : proper_space (Î b, Ï€ b) :=
begin
  refine proper_space_of_compact_closed_ball_of_le 0 (Î»x r hr, _),
  rw closed_ball_pi _ hr,
  apply is_compact_univ_pi (Î»b, _),
  apply (h b).is_compact_closed_ball
end

variables [proper_space Î±] {x : Î±} {r : â„} {s : set Î±}

/-- If a nonempty ball in a proper space includes a closed set `s`, then there exists a nonempty
ball with the same center and a strictly smaller radius that includes `s`. -/
lemma exists_pos_lt_subset_ball (hr : 0 < r) (hs : is_closed s) (h : s âŠ† ball x r) :
  âˆƒ r' âˆˆ Ioo 0 r, s âŠ† ball x r' :=
begin
  unfreezingI { rcases eq_empty_or_nonempty s with rfl|hne },
  { exact âŸ¨r / 2, âŸ¨half_pos hr, half_lt_self hrâŸ©, empty_subset _âŸ© },
  have : is_compact s,
    from compact_of_is_closed_subset (proper_space.is_compact_closed_ball x r) hs
      (subset.trans h ball_subset_closed_ball),
  obtain âŸ¨y, hys, hyâŸ© : âˆƒ y âˆˆ s, s âŠ† closed_ball x (dist y x),
    from this.exists_forall_ge hne (continuous_id.dist continuous_const).continuous_on,
  have hyr : dist y x < r, from h hys,
  rcases exists_between hyr with âŸ¨r', hyr', hrr'âŸ©,
  exact âŸ¨r', âŸ¨dist_nonneg.trans_lt hyr', hrr'âŸ©, subset.trans hy $ closed_ball_subset_ball hyr'âŸ©
end

/-- If a ball in a proper space includes a closed set `s`, then there exists a ball with the same
center and a strictly smaller radius that includes `s`. -/
lemma exists_lt_subset_ball (hs : is_closed s) (h : s âŠ† ball x r) :
  âˆƒ r' < r, s âŠ† ball x r' :=
begin
  cases le_or_lt r 0 with hr hr,
  { rw [ball_eq_empty.2 hr, subset_empty_iff] at h, unfreezingI { subst s },
    exact (no_bot r).imp (Î» r' hr', âŸ¨hr', empty_subset _âŸ©) },
  { exact (exists_pos_lt_subset_ball hr hs h).imp (Î» r' hr', âŸ¨hr'.fst.2, hr'.sndâŸ©) }
end

end proper_space

namespace metric
section second_countable
open topological_space

/-- A pseudometric space is second countable if, for every `Îµ > 0`, there is a countable set which
is `Îµ`-dense. -/
lemma second_countable_of_almost_dense_set
  (H : âˆ€Îµ > (0 : â„), âˆƒ s : set Î±, countable s âˆ§ (âˆ€x, âˆƒy âˆˆ s, dist x y â‰¤ Îµ)) :
  second_countable_topology Î± :=
begin
  refine emetric.second_countable_of_almost_dense_set (Î» Îµ Îµ0, _),
  rcases ennreal.lt_iff_exists_nnreal_btwn.1 Îµ0 with âŸ¨Îµ', Îµ'0, Îµ'ÎµâŸ©,
  choose s hsc y hys hyx using H Îµ' (by exact_mod_cast Îµ'0),
  refine âŸ¨s, hsc, bUnion_eq_univ_iff.2 (Î» x, âŸ¨y x, hys _, le_trans _ Îµ'Îµ.leâŸ©)âŸ©,
  exact_mod_cast hyx x
end

end second_countable
end metric

lemma lebesgue_number_lemma_of_metric
  {s : set Î±} {Î¹} {c : Î¹ â†’ set Î±} (hs : is_compact s)
  (hcâ‚ : âˆ€ i, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) :
  âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ i, ball x Î´ âŠ† c i :=
let âŸ¨n, en, hnâŸ© := lebesgue_number_lemma hs hcâ‚ hcâ‚‚,
    âŸ¨Î´, Î´0, hÎ´âŸ© := mem_uniformity_dist.1 en in
âŸ¨Î´, Î´0, assume x hx, let âŸ¨i, hiâŸ© := hn x hx in
 âŸ¨i, assume y hy, hi (hÎ´ (mem_ball'.mp hy))âŸ©âŸ©

lemma lebesgue_number_lemma_of_metric_sUnion
  {s : set Î±} {c : set (set Î±)} (hs : is_compact s)
  (hcâ‚ : âˆ€ t âˆˆ c, is_open t) (hcâ‚‚ : s âŠ† â‹ƒâ‚€ c) :
  âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ c, ball x Î´ âŠ† t :=
by rw sUnion_eq_Union at hcâ‚‚;
   simpa using lebesgue_number_lemma_of_metric hs (by simpa) hcâ‚‚

namespace metric

/-- Boundedness of a subset of a pseudometric space. We formulate the definition to work
even in the empty space. -/
def bounded (s : set Î±) : Prop :=
âˆƒC, âˆ€x y âˆˆ s, dist x y â‰¤ C

section bounded
variables {x : Î±} {s t : set Î±} {r : â„}

@[simp] lemma bounded_empty : bounded (âˆ… : set Î±) :=
âŸ¨0, by simpâŸ©

lemma bounded_iff_mem_bounded : bounded s â†” âˆ€ x âˆˆ s, bounded s :=
âŸ¨Î» h _ _, h, Î» H,
  s.eq_empty_or_nonempty.elim
  (Î» hs, hs.symm â–¸ bounded_empty)
  (Î» âŸ¨x, hxâŸ©, H x hx)âŸ©

/-- Subsets of a bounded set are also bounded -/
lemma bounded.mono (incl : s âŠ† t) : bounded t â†’ bounded s :=
Exists.imp $ Î» C hC x y hx hy, hC x y (incl hx) (incl hy)

/-- Closed balls are bounded -/
lemma bounded_closed_ball : bounded (closed_ball x r) :=
âŸ¨r + r, Î» y z hy hz, begin
  simp only [mem_closed_ball] at *,
  calc dist y z â‰¤ dist y x + dist z x : dist_triangle_right _ _ _
            ... â‰¤ r + r : add_le_add hy hz
endâŸ©

/-- Open balls are bounded -/
lemma bounded_ball : bounded (ball x r) :=
bounded_closed_ball.mono ball_subset_closed_ball

/-- Given a point, a bounded subset is included in some ball around this point -/
lemma bounded_iff_subset_ball (c : Î±) : bounded s â†” âˆƒr, s âŠ† closed_ball c r :=
begin
  split; rintro âŸ¨C, hCâŸ©,
  { cases s.eq_empty_or_nonempty with h h,
    { subst s, exact âŸ¨0, by simpâŸ© },
    { rcases h with âŸ¨x, hxâŸ©,
      exact âŸ¨C + dist x c, Î» y hy, calc
        dist y c â‰¤ dist y x + dist x c : dist_triangle _ _ _
            ... â‰¤ C + dist x c : add_le_add_right (hC y x hy hx) _âŸ© } },
  { exact bounded_closed_ball.mono hC }
end

lemma bounded.subset_ball (h : bounded s) (c : Î±) : âˆƒ r, s âŠ† closed_ball c r :=
(bounded_iff_subset_ball c).1 h

lemma bounded_closure_of_bounded (h : bounded s) : bounded (closure s) :=
let âŸ¨C, hâŸ© := h in
âŸ¨C, Î» a b ha hb, (is_closed_le' C).closure_subset $ map_mem_closure2 continuous_dist ha hb hâŸ©

alias bounded_closure_of_bounded â† metric.bounded.closure

@[simp] lemma bounded_closure_iff : bounded (closure s) â†” bounded s :=
âŸ¨Î» h, h.mono subset_closure, Î» h, h.closureâŸ©

/-- The union of two bounded sets is bounded iff each of the sets is bounded -/
@[simp] lemma bounded_union :
  bounded (s âˆª t) â†” bounded s âˆ§ bounded t :=
âŸ¨Î»h, âŸ¨h.mono (by simp), h.mono (by simp)âŸ©,
begin
  rintro âŸ¨hs, htâŸ©,
  refine bounded_iff_mem_bounded.2 (Î» x _, _),
  rw bounded_iff_subset_ball x at hs ht âŠ¢,
  rcases hs with âŸ¨Cs, hCsâŸ©, rcases ht with âŸ¨Ct, hCtâŸ©,
  exact âŸ¨max Cs Ct, union_subset
    (subset.trans hCs $ closed_ball_subset_closed_ball $ le_max_left _ _)
    (subset.trans hCt $ closed_ball_subset_closed_ball $ le_max_right _ _)âŸ©,
endâŸ©

/-- A finite union of bounded sets is bounded -/
lemma bounded_bUnion {I : set Î²} {s : Î² â†’ set Î±} (H : finite I) :
  bounded (â‹ƒiâˆˆI, s i) â†” âˆ€i âˆˆ I, bounded (s i) :=
finite.induction_on H (by simp) $ Î» x I _ _ IH,
by simp [or_imp_distrib, forall_and_distrib, IH]

/-- A totally bounded set is bounded -/
lemma _root_.totally_bounded.bounded {s : set Î±} (h : totally_bounded s) : bounded s :=
-- We cover the totally bounded set by finitely many balls of radius 1,
-- and then argue that a finite union of bounded sets is bounded
let âŸ¨t, fint, subsâŸ© := (totally_bounded_iff.mp h) 1 zero_lt_one in
bounded.mono subs $ (bounded_bUnion fint).2 $ Î» i hi, bounded_ball

/-- A compact set is bounded -/
lemma _root_.is_compact.bounded {s : set Î±} (h : is_compact s) : bounded s :=
-- A compact set is totally bounded, thus bounded
h.totally_bounded.bounded

/-- A finite set is bounded -/
lemma bounded_of_finite {s : set Î±} (h : finite s) : bounded s :=
h.is_compact.bounded

alias bounded_of_finite â† set.finite.bounded

/-- A singleton is bounded -/
lemma bounded_singleton {x : Î±} : bounded ({x} : set Î±) :=
bounded_of_finite $ finite_singleton _

/-- Characterization of the boundedness of the range of a function -/
lemma bounded_range_iff {f : Î² â†’ Î±} : bounded (range f) â†” âˆƒC, âˆ€x y, dist (f x) (f y) â‰¤ C :=
exists_congr $ Î» C, âŸ¨
  Î» H x y, H _ _ âŸ¨x, rflâŸ© âŸ¨y, rflâŸ©,
  by rintro H _ _ âŸ¨x, rflâŸ© âŸ¨y, rflâŸ©; exact H x yâŸ©

/-- In a compact space, all sets are bounded -/
lemma bounded_of_compact_space [compact_space Î±] : bounded s :=
compact_univ.bounded.mono (subset_univ _)

lemma is_compact_of_is_closed_bounded [proper_space Î±] (hc : is_closed s) (hb : bounded s) :
  is_compact s :=
begin
  unfreezingI { rcases eq_empty_or_nonempty s with (rfl|âŸ¨x, hxâŸ©) },
  { exact is_compact_empty },
  { rcases hb.subset_ball x with âŸ¨r, hrâŸ©,
    exact compact_of_is_closed_subset (proper_space.is_compact_closed_ball x r) hc hr }
end

/-- The Heineâ€“Borel theorem:
In a proper space, a set is compact if and only if it is closed and bounded -/
lemma compact_iff_closed_bounded [t2_space Î±] [proper_space Î±] :
  is_compact s â†” is_closed s âˆ§ bounded s :=
âŸ¨Î» h, âŸ¨h.is_closed, h.boundedâŸ©, Î» h, is_compact_of_is_closed_bounded h.1 h.2âŸ©

lemma compact_space_iff_bounded_univ [proper_space Î±] : compact_space Î± â†” bounded (univ : set Î±) :=
âŸ¨@bounded_of_compact_space Î± _ _, Î» hb, âŸ¨is_compact_of_is_closed_bounded is_closed_univ hbâŸ©âŸ©

section conditionally_complete_linear_order

variables [conditionally_complete_linear_order Î±] [order_topology Î±]

lemma bounded_Icc (a b : Î±) : bounded (Icc a b) :=
(totally_bounded_Icc a b).bounded

lemma bounded_Ico (a b : Î±) : bounded (Ico a b) :=
(totally_bounded_Ico a b).bounded

lemma bounded_Ioc (a b : Î±) : bounded (Ioc a b) :=
(totally_bounded_Ioc a b).bounded

lemma bounded_Ioo (a b : Î±) : bounded (Ioo a b) :=
(totally_bounded_Ioo a b).bounded

/-- In a pseudo metric space with a conditionally complete linear order such that the order and the
    metric structure give the same topology, any order-bounded set is metric-bounded. -/
lemma bounded_of_bdd_above_of_bdd_below {s : set Î±} (hâ‚ : bdd_above s) (hâ‚‚ : bdd_below s) :
  bounded s :=
let âŸ¨u, huâŸ© := hâ‚, âŸ¨l, hlâŸ© := hâ‚‚ in
bounded.mono (Î» x hx, mem_Icc.mpr âŸ¨hl hx, hu hxâŸ©) (bounded_Icc l u)

end conditionally_complete_linear_order

end bounded

section diam
variables {s : set Î±} {x y z : Î±}

/-- The diameter of a set in a metric space. To get controllable behavior even when the diameter
should be infinite, we express it in terms of the emetric.diameter -/
def diam (s : set Î±) : â„ := ennreal.to_real (emetric.diam s)

/-- The diameter of a set is always nonnegative -/
lemma diam_nonneg : 0 â‰¤ diam s := ennreal.to_real_nonneg

lemma diam_subsingleton (hs : s.subsingleton) : diam s = 0 :=
by simp only [diam, emetric.diam_subsingleton hs, ennreal.zero_to_real]

/-- The empty set has zero diameter -/
@[simp] lemma diam_empty : diam (âˆ… : set Î±) = 0 :=
diam_subsingleton subsingleton_empty

/-- A singleton has zero diameter -/
@[simp] lemma diam_singleton : diam ({x} : set Î±) = 0 :=
diam_subsingleton subsingleton_singleton

-- Does not work as a simp-lemma, since {x, y} reduces to (insert y {x})
lemma diam_pair : diam ({x, y} : set Î±) = dist x y :=
by simp only [diam, emetric.diam_pair, dist_edist]

-- Does not work as a simp-lemma, since {x, y, z} reduces to (insert z (insert y {x}))
lemma diam_triple :
  metric.diam ({x, y, z} : set Î±) = max (max (dist x y) (dist x z)) (dist y z) :=
begin
  simp only [metric.diam, emetric.diam_triple, dist_edist],
  rw [ennreal.to_real_max, ennreal.to_real_max];
    apply_rules [ne_of_lt, edist_lt_top, max_lt]
end

/-- If the distance between any two points in a set is bounded by some constant `C`,
then `ennreal.of_real C`  bounds the emetric diameter of this set. -/
lemma ediam_le_of_forall_dist_le {C : â„} (h : âˆ€ (x âˆˆ s) (y âˆˆ s), dist x y â‰¤ C) :
  emetric.diam s â‰¤ ennreal.of_real C :=
emetric.diam_le $
Î» x hx y hy, (edist_dist x y).symm â–¸ ennreal.of_real_le_of_real (h x hx y hy)

/-- If the distance between any two points in a set is bounded by some non-negative constant,
this constant bounds the diameter. -/
lemma diam_le_of_forall_dist_le {C : â„} (hâ‚€ : 0 â‰¤ C) (h : âˆ€ (x âˆˆ s) (y âˆˆ s), dist x y â‰¤ C) :
  diam s â‰¤ C :=
ennreal.to_real_le_of_le_of_real hâ‚€ (ediam_le_of_forall_dist_le h)

/-- If the distance between any two points in a nonempty set is bounded by some constant,
this constant bounds the diameter. -/
lemma diam_le_of_forall_dist_le_of_nonempty (hs : s.nonempty) {C : â„}
  (h : âˆ€ (x âˆˆ s) (y âˆˆ s), dist x y â‰¤ C) : diam s â‰¤ C :=
have hâ‚€ : 0 â‰¤ C, from let âŸ¨x, hxâŸ© := hs in le_trans dist_nonneg (h x hx x hx),
diam_le_of_forall_dist_le hâ‚€ h

/-- The distance between two points in a set is controlled by the diameter of the set. -/
lemma dist_le_diam_of_mem' (h : emetric.diam s â‰  âŠ¤) (hx : x âˆˆ s) (hy : y âˆˆ s) :
  dist x y â‰¤ diam s :=
begin
  rw [diam, dist_edist],
  rw ennreal.to_real_le_to_real (edist_ne_top _ _) h,
  exact emetric.edist_le_diam_of_mem hx hy
end

/-- Characterize the boundedness of a set in terms of the finiteness of its emetric.diameter. -/
lemma bounded_iff_ediam_ne_top : bounded s â†” emetric.diam s â‰  âŠ¤ :=
iff.intro
  (Î» âŸ¨C, hCâŸ©, ne_top_of_le_ne_top ennreal.of_real_ne_top
    (ediam_le_of_forall_dist_le $ Î» x hx y hy, hC x y hx hy))
  (Î» h, âŸ¨diam s, Î» x y hx hy, dist_le_diam_of_mem' h hx hyâŸ©)

lemma bounded.ediam_ne_top (h : bounded s) : emetric.diam s â‰  âŠ¤ :=
bounded_iff_ediam_ne_top.1 h

/-- The distance between two points in a set is controlled by the diameter of the set. -/
lemma dist_le_diam_of_mem (h : bounded s) (hx : x âˆˆ s) (hy : y âˆˆ s) : dist x y â‰¤ diam s :=
dist_le_diam_of_mem' h.ediam_ne_top hx hy

lemma ediam_of_unbounded (h : Â¬(bounded s)) : emetric.diam s = âˆ :=
by rwa [bounded_iff_ediam_ne_top, not_not] at h

/-- An unbounded set has zero diameter. If you would prefer to get the value âˆ, use `emetric.diam`.
This lemma makes it possible to avoid side conditions in some situations -/
lemma diam_eq_zero_of_unbounded (h : Â¬(bounded s)) : diam s = 0 :=
by rw [diam, ediam_of_unbounded h, ennreal.top_to_real]

/-- If `s âŠ† t`, then the diameter of `s` is bounded by that of `t`, provided `t` is bounded. -/
lemma diam_mono {s t : set Î±} (h : s âŠ† t) (ht : bounded t) : diam s â‰¤ diam t :=
begin
  unfold diam,
  rw ennreal.to_real_le_to_real (bounded.mono h ht).ediam_ne_top ht.ediam_ne_top,
  exact emetric.diam_mono h
end

/-- The diameter of a union is controlled by the sum of the diameters, and the distance between
any two points in each of the sets. This lemma is true without any side condition, since it is
obviously true if `s âˆª t` is unbounded. -/
lemma diam_union {t : set Î±} (xs : x âˆˆ s) (yt : y âˆˆ t) :
  diam (s âˆª t) â‰¤ diam s + dist x y + diam t :=
begin
  by_cases H : bounded (s âˆª t),
  { have hs : bounded s, from H.mono (subset_union_left _ _),
    have ht : bounded t, from H.mono (subset_union_right _ _),
    rw [bounded_iff_ediam_ne_top] at H hs ht,
    rw [dist_edist, diam, diam, diam, â† ennreal.to_real_add, â† ennreal.to_real_add,
      ennreal.to_real_le_to_real];
      repeat { apply ennreal.add_ne_top.2; split }; try { assumption };
      try { apply edist_ne_top },
    exact emetric.diam_union xs yt },
  { rw [diam_eq_zero_of_unbounded H],
    apply_rules [add_nonneg, diam_nonneg, dist_nonneg] }
end

/-- If two sets intersect, the diameter of the union is bounded by the sum of the diameters. -/
lemma diam_union' {t : set Î±} (h : (s âˆ© t).nonempty) : diam (s âˆª t) â‰¤ diam s + diam t :=
begin
  rcases h with âŸ¨x, âŸ¨xs, xtâŸ©âŸ©,
  simpa using diam_union xs xt
end

/-- The diameter of a closed ball of radius `r` is at most `2 r`. -/
lemma diam_closed_ball {r : â„} (h : 0 â‰¤ r) : diam (closed_ball x r) â‰¤ 2 * r :=
diam_le_of_forall_dist_le (mul_nonneg (le_of_lt zero_lt_two) h) $ Î»a ha b hb, calc
  dist a b â‰¤ dist a x + dist b x : dist_triangle_right _ _ _
  ... â‰¤ r + r : add_le_add ha hb
  ... = 2 * r : by simp [mul_two, mul_comm]

/-- The diameter of a ball of radius `r` is at most `2 r`. -/
lemma diam_ball {r : â„} (h : 0 â‰¤ r) : diam (ball x r) â‰¤ 2 * r :=
le_trans (diam_mono ball_subset_closed_ball bounded_closed_ball) (diam_closed_ball h)

end diam

end metric

lemma comap_dist_right_at_top_le_cocompact (x : Î±) : comap (Î» y, dist y x) at_top â‰¤ cocompact Î± :=
begin
  refine filter.has_basis_cocompact.ge_iff.2 (Î» s hs, mem_comap.2 _),
  rcases hs.bounded.subset_ball x with âŸ¨r, hrâŸ©,
  exact âŸ¨Ioi r, Ioi_mem_at_top r, Î» y hy hys, (mem_closed_ball.1 $ hr hys).not_lt hyâŸ©
end

lemma comap_dist_left_at_top_le_cocompact (x : Î±) : comap (dist x) at_top â‰¤ cocompact Î± :=
by simpa only [dist_comm _ x] using comap_dist_right_at_top_le_cocompact x

lemma comap_dist_right_at_top_eq_cocompact [proper_space Î±] (x : Î±) :
  comap (Î» y, dist y x) at_top = cocompact Î± :=
(comap_dist_right_at_top_le_cocompact x).antisymm $ (tendsto_dist_right_cocompact_at_top x).le_comap

lemma comap_dist_left_at_top_eq_cocompact [proper_space Î±] (x : Î±) :
  comap (dist x) at_top = cocompact Î± :=
(comap_dist_left_at_top_le_cocompact x).antisymm $ (tendsto_dist_left_cocompact_at_top x).le_comap

lemma tendsto_cocompact_of_tendsto_dist_comp_at_top {f : Î² â†’ Î±} {l : filter Î²} (x : Î±)
  (h : tendsto (Î» y, dist (f y) x) l at_top) : tendsto f l (cocompact Î±) :=
by { refine tendsto.mono_right _ (comap_dist_right_at_top_le_cocompact x), rwa tendsto_comap_iff }

namespace int
open metric

/-- Under the coercion from `â„¤` to `â„`, inverse images of compact sets are finite. -/
lemma tendsto_coe_cofinite : tendsto (coe : â„¤ â†’ â„) cofinite (cocompact â„) :=
begin
  refine tendsto_cocompact_of_tendsto_dist_comp_at_top (0 : â„) _,
  simp only [filter.tendsto_at_top, eventually_cofinite, not_le, â† mem_ball],
  change âˆ€ r : â„, finite (coe â»Â¹' (ball (0 : â„) r)),
  simp [real.ball_eq, set.finite_Ioo],
end

end int

/-- We now define `metric_space`, extending `pseudo_metric_space`. -/
class metric_space (Î± : Type u) extends pseudo_metric_space Î± : Type u :=
(eq_of_dist_eq_zero : âˆ€ {x y : Î±}, dist x y = 0 â†’ x = y)

/-- Construct a metric space structure whose underlying topological space structure
(definitionally) agrees which a pre-existing topology which is compatible with a given distance
function. -/
def metric_space.of_metrizable {Î± : Type*} [topological_space Î±] (dist : Î± â†’ Î± â†’ â„)
  (dist_self : âˆ€ x : Î±, dist x x = 0)
  (dist_comm : âˆ€ x y : Î±, dist x y = dist y x)
  (dist_triangle : âˆ€ x y z : Î±, dist x z â‰¤ dist x y + dist y z)
  (H : âˆ€ s : set Î±, is_open s â†” âˆ€ x âˆˆ s, âˆƒ Îµ > 0, âˆ€ y, dist x y < Îµ â†’ y âˆˆ s)
  (eq_of_dist_eq_zero : âˆ€ x y : Î±, dist x y = 0 â†’ x = y) : metric_space Î± :=
{ eq_of_dist_eq_zero := eq_of_dist_eq_zero,
  ..pseudo_metric_space.of_metrizable dist dist_self dist_comm dist_triangle H }

variables {Î³ : Type w} [metric_space Î³]

theorem eq_of_dist_eq_zero {x y : Î³} : dist x y = 0 â†’ x = y :=
metric_space.eq_of_dist_eq_zero

@[simp] theorem dist_eq_zero {x y : Î³} : dist x y = 0 â†” x = y :=
iff.intro eq_of_dist_eq_zero (assume : x = y, this â–¸ dist_self _)

@[simp] theorem zero_eq_dist {x y : Î³} : 0 = dist x y â†” x = y :=
by rw [eq_comm, dist_eq_zero]

theorem dist_ne_zero {x y : Î³} : dist x y â‰  0 â†” x â‰  y :=
by simpa only [not_iff_not] using dist_eq_zero

@[simp] theorem dist_le_zero {x y : Î³} : dist x y â‰¤ 0 â†” x = y :=
by simpa [le_antisymm_iff, dist_nonneg] using @dist_eq_zero _ _ x y

@[simp] theorem dist_pos {x y : Î³} : 0 < dist x y â†” x â‰  y :=
by simpa only [not_le] using not_congr dist_le_zero

theorem eq_of_forall_dist_le {x y : Î³} (h : âˆ€ Îµ > 0, dist x y â‰¤ Îµ) : x = y :=
eq_of_dist_eq_zero (eq_of_le_of_forall_le_of_dense dist_nonneg h)

/--Deduce the equality of points with the vanishing of the nonnegative distance-/
theorem eq_of_nndist_eq_zero {x y : Î³} : nndist x y = 0 â†’ x = y :=
by simp only [â† nnreal.eq_iff, â† dist_nndist, imp_self, nnreal.coe_zero, dist_eq_zero]

/--Characterize the equality of points with the vanishing of the nonnegative distance-/
@[simp] theorem nndist_eq_zero {x y : Î³} : nndist x y = 0 â†” x = y :=
by simp only [â† nnreal.eq_iff, â† dist_nndist, imp_self, nnreal.coe_zero, dist_eq_zero]

@[simp] theorem zero_eq_nndist {x y : Î³} : 0 = nndist x y â†” x = y :=
by simp only [â† nnreal.eq_iff, â† dist_nndist, imp_self, nnreal.coe_zero, zero_eq_dist]

namespace metric

variables {x : Î³} {s : set Î³}

@[simp] lemma closed_ball_zero : closed_ball x 0 = {x} :=
set.ext $ Î» y, dist_le_zero

/-- A map between metric spaces is a uniform embedding if and only if the distance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y` and conversely. -/
theorem uniform_embedding_iff' [metric_space Î²] {f : Î³ â†’ Î²} :
  uniform_embedding f â†”
  (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î³}, dist a b < Î´ â†’ dist (f a) (f b) < Îµ) âˆ§
  (âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î³}, dist (f a) (f b) < Îµ â†’ dist a b < Î´) :=
begin
  split,
  { assume h,
    exact âŸ¨uniform_continuous_iff.1 (uniform_embedding_iff.1 h).2.1,
          (uniform_embedding_iff.1 h).2.2âŸ© },
  { rintros âŸ¨hâ‚, hâ‚‚âŸ©,
    refine uniform_embedding_iff.2 âŸ¨_, uniform_continuous_iff.2 hâ‚, hâ‚‚âŸ©,
    assume x y hxy,
    have : dist x y â‰¤ 0,
    { refine le_of_forall_lt' (Î»Î´ Î´pos, _),
      rcases hâ‚‚ Î´ Î´pos with âŸ¨Îµ, Îµpos, hÎµâŸ©,
      have : dist (f x) (f y) < Îµ, by simpa [hxy],
      exact hÎµ this },
    simpa using this }
end

@[priority 100] -- see Note [lower instance priority]
instance metric_space.to_separated : separated_space Î³ :=
separated_def.2 $ Î» x y h, eq_of_forall_dist_le $
  Î» Îµ Îµ0, le_of_lt (h _ (dist_mem_uniformity Îµ0))

/-- If a  `pseudo_metric_space` is separated, then it is a `metric_space`. -/
def of_t2_pseudo_metric_space {Î± : Type*} [pseudo_metric_space Î±]
  (h : separated_space Î±) : metric_space Î± :=
{ eq_of_dist_eq_zero := Î» x y hdist,
  begin
    refine separated_def.1 h x y (Î» s hs, _),
    obtain âŸ¨Îµ, hÎµ, HâŸ© := mem_uniformity_dist.1 hs,
    exact H (show dist x y < Îµ, by rwa [hdist])
  end
  ..â€¹pseudo_metric_space Î±â€º }

/-- A metric space induces an emetric space -/
@[priority 100] -- see Note [lower instance priority]
instance metric_space.to_emetric_space : emetric_space Î³ :=
{ eq_of_edist_eq_zero := assume x y h, by simpa [edist_dist] using h,
  ..pseudo_metric_space.to_pseudo_emetric_space, }

lemma is_closed_of_pairwise_on_le_dist {s : set Î³} {Îµ : â„} (hÎµ : 0 < Îµ)
  (hs : pairwise_on s (Î» x y, Îµ â‰¤ dist x y)) : is_closed s :=
is_closed_of_spaced_out (dist_mem_uniformity hÎµ) $ by simpa using hs

lemma closed_embedding_of_pairwise_le_dist {Î± : Type*} [topological_space Î±] [discrete_topology Î±]
  {Îµ : â„} (hÎµ : 0 < Îµ) {f : Î± â†’ Î³} (hf : pairwise (Î» x y, Îµ â‰¤ dist (f x) (f y))) :
  closed_embedding f :=
closed_embedding_of_spaced_out (dist_mem_uniformity hÎµ) $ by simpa using hf

/-- If `f : Î² â†’ Î±` sends any two distinct points to points at distance at least `Îµ > 0`, then
`f` is a uniform embedding with respect to the discrete uniformity on `Î²`. -/
lemma uniform_embedding_bot_of_pairwise_le_dist {Î² : Type*} {Îµ : â„} (hÎµ : 0 < Îµ) {f : Î² â†’ Î±}
  (hf : pairwise (Î» x y, Îµ â‰¤ dist (f x) (f y))) : @uniform_embedding _ _ âŠ¥ (by apply_instance) f :=
uniform_embedding_of_spaced_out (dist_mem_uniformity hÎµ) $ by simpa using hf

end metric

/-- Build a new metric space from an old one where the bundled uniform structure is provably
(but typically non-definitionaly) equal to some given uniform structure.
See Note [forgetful inheritance].
-/
def metric_space.replace_uniformity {Î³} [U : uniform_space Î³] (m : metric_space Î³)
  (H : @uniformity _ U = @uniformity _ emetric_space.to_uniform_space') :
  metric_space Î³ :=
{ eq_of_dist_eq_zero := @eq_of_dist_eq_zero _ _,
  ..pseudo_metric_space.replace_uniformity m.to_pseudo_metric_space H, }

  /-- One gets a metric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the metric space and the emetric space. In this definition, the distance
is given separately, to be able to prescribe some expression which is not defeq to the push-forward
of the edistance to reals. -/
def emetric_space.to_metric_space_of_dist {Î± : Type u} [e : emetric_space Î±]
  (dist : Î± â†’ Î± â†’ â„)
  (edist_ne_top : âˆ€x y: Î±, edist x y â‰  âŠ¤)
  (h : âˆ€x y, dist x y = ennreal.to_real (edist x y)) :
  metric_space Î± :=
{ dist := dist,
  eq_of_dist_eq_zero := Î»x y hxy,
    by simpa [h, ennreal.to_real_eq_zero_iff, edist_ne_top x y] using hxy,
  ..pseudo_emetric_space.to_pseudo_metric_space_of_dist dist edist_ne_top h, }

/-- One gets a metric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the metric space and the emetric space. -/
def emetric_space.to_metric_space {Î± : Type u} [e : emetric_space Î±] (h : âˆ€x y: Î±, edist x y â‰  âŠ¤) :
  metric_space Î± :=
emetric_space.to_metric_space_of_dist (Î»x y, ennreal.to_real (edist x y)) h (Î»x y, rfl)

/-- Metric space structure pulled back by an injective function. Injectivity is necessary to
ensure that `dist x y = 0` only if `x = y`. -/
def metric_space.induced {Î³ Î²} (f : Î³ â†’ Î²) (hf : function.injective f)
  (m : metric_space Î²) : metric_space Î³ :=
{ eq_of_dist_eq_zero := Î» x y h, hf (dist_eq_zero.1 h),
  ..pseudo_metric_space.induced f m.to_pseudo_metric_space }

/-- Pull back a metric space structure by a uniform embedding. This is a version of
`metric_space.induced` useful in case if the domain already has a `uniform_space` structure. -/
def uniform_embedding.comap_metric_space {Î± Î²} [uniform_space Î±] [metric_space Î²] (f : Î± â†’ Î²)
  (h : uniform_embedding f) : metric_space Î± :=
(metric_space.induced f h.inj â€¹_â€º).replace_uniformity h.comap_uniformity.symm

instance subtype.metric_space {Î± : Type*} {p : Î± â†’ Prop} [t : metric_space Î±] :
  metric_space (subtype p) :=
metric_space.induced coe (Î» x y, subtype.ext) t

theorem subtype.dist_eq {p : Î± â†’ Prop} (x y : subtype p) : dist x y = dist (x : Î±) y := rfl

instance : metric_space empty :=
{ dist := Î» _ _, 0,
  dist_self := Î» _, rfl,
  dist_comm := Î» _ _, rfl,
  eq_of_dist_eq_zero := Î» _ _ _, subsingleton.elim _ _,
  dist_triangle := Î» _ _ _, show (0:â„) â‰¤ 0 + 0, by rw add_zero, }

instance : metric_space punit :=
{ dist := Î» _ _, 0,
  dist_self := Î» _, rfl,
  dist_comm := Î» _ _, rfl,
  eq_of_dist_eq_zero := Î» _ _ _, subsingleton.elim _ _,
  dist_triangle := Î» _ _ _, show (0:â„) â‰¤ 0 + 0, by rw add_zero, }

section real

/-- Instantiate the reals as a metric space. -/
instance real.metric_space : metric_space â„ :=
{ eq_of_dist_eq_zero := Î» x y h, by simpa [dist, sub_eq_zero] using h,
  ..real.pseudo_metric_space }

end real

section nnreal

instance : metric_space â„â‰¥0 := subtype.metric_space

end nnreal

section prod

instance prod.metric_space_max [metric_space Î²] : metric_space (Î³ Ã— Î²) :=
{ eq_of_dist_eq_zero := Î» x y h, begin
    cases max_le_iff.1 (le_of_eq h) with hâ‚ hâ‚‚,
    exact prod.ext_iff.2 âŸ¨dist_le_zero.1 hâ‚, dist_le_zero.1 hâ‚‚âŸ©
  end,
  ..prod.pseudo_metric_space_max, }

end prod

section pi
open finset
variables {Ï€ : Î² â†’ Type*} [fintype Î²] [âˆ€b, metric_space (Ï€ b)]

/-- A finite product of metric spaces is a metric space, with the sup distance. -/
instance metric_space_pi : metric_space (Î b, Ï€ b) :=
  /- we construct the instance from the emetric space instance to avoid checking again that the
  uniformity is the same as the product uniformity, but we register nevertheless a nice formula
  for the distance -/
{ eq_of_dist_eq_zero := assume f g eq0,
  begin
    have eq1 : edist f g = 0 := by simp only [edist_dist, eq0, ennreal.of_real_zero],
    have eq2 : sup univ (Î» (b : Î²), edist (f b) (g b)) â‰¤ 0 := le_of_eq eq1,
    simp only [finset.sup_le_iff] at eq2,
    exact (funext $ assume b, edist_le_zero.1 $ eq2 b $ mem_univ b)
  end,
  ..pseudo_metric_space_pi }

end pi

namespace metric
section second_countable
open topological_space

/-- A metric space is second countable if one can reconstruct up to any `Îµ>0` any element of the
space from countably many data. -/
lemma second_countable_of_countable_discretization {Î± : Type u} [metric_space Î±]
  (H : âˆ€Îµ > (0 : â„), âˆƒ (Î² : Type*) (_ : encodable Î²) (F : Î± â†’ Î²), âˆ€x y, F x = F y â†’ dist x y â‰¤ Îµ) :
  second_countable_topology Î± :=
begin
  cases (univ : set Î±).eq_empty_or_nonempty with hs hs,
  { haveI : compact_space Î± := âŸ¨by rw hs; exact is_compact_emptyâŸ©, by apply_instance },
  rcases hs with âŸ¨x0, hx0âŸ©,
  letI : inhabited Î± := âŸ¨x0âŸ©,
  refine second_countable_of_almost_dense_set (Î»Îµ Îµ0, _),
  rcases H Îµ Îµ0 with âŸ¨Î², fÎ², F, hFâŸ©,
  resetI,
  let Finv := function.inv_fun F,
  refine âŸ¨range Finv, âŸ¨countable_range _, Î»x, _âŸ©âŸ©,
  let x' := Finv (F x),
  have : F x' = F x := function.inv_fun_eq âŸ¨x, rflâŸ©,
  exact âŸ¨x', mem_range_self _, hF _ _ this.symmâŸ©
end

end second_countable
end metric

section eq_rel

/-- The canonical equivalence relation on a pseudometric space. -/
def pseudo_metric.dist_setoid (Î± : Type u) [pseudo_metric_space Î±] : setoid Î± :=
setoid.mk (Î»x y, dist x y = 0)
begin
  unfold equivalence,
  repeat { split },
  { exact pseudo_metric_space.dist_self },
  { assume x y h, rwa pseudo_metric_space.dist_comm },
  { assume x y z hxy hyz,
    refine le_antisymm _ dist_nonneg,
    calc dist x z â‰¤ dist x y + dist y z : pseudo_metric_space.dist_triangle _ _ _
         ... = 0 + 0 : by rw [hxy, hyz]
         ... = 0 : by simp }
end

local attribute [instance] pseudo_metric.dist_setoid

/-- The canonical quotient of a pseudometric space, identifying points at distance `0`. -/
@[reducible] definition pseudo_metric_quot (Î± : Type u) [pseudo_metric_space Î±] : Type* :=
quotient (pseudo_metric.dist_setoid Î±)

instance has_dist_metric_quot {Î± : Type u} [pseudo_metric_space Î±] :
  has_dist (pseudo_metric_quot Î±) :=
{ dist := quotient.liftâ‚‚ (Î»p q : Î±, dist p q)
begin
  assume x y x' y' hxx' hyy',
  have Hxx' : dist x x' = 0 := hxx',
  have Hyy' : dist y y' = 0 := hyy',
  have A : dist x y â‰¤ dist x' y' := calc
    dist x y â‰¤ dist x x' + dist x' y : pseudo_metric_space.dist_triangle _ _ _
    ... = dist x' y : by simp [Hxx']
    ... â‰¤ dist x' y' + dist y' y : pseudo_metric_space.dist_triangle _ _ _
    ... = dist x' y' : by simp [pseudo_metric_space.dist_comm, Hyy'],
  have B : dist x' y' â‰¤ dist x y := calc
    dist x' y' â‰¤ dist x' x + dist x y' : pseudo_metric_space.dist_triangle _ _ _
    ... = dist x y' : by simp [pseudo_metric_space.dist_comm, Hxx']
    ... â‰¤ dist x y + dist y y' : pseudo_metric_space.dist_triangle _ _ _
    ... = dist x y : by simp [Hyy'],
  exact le_antisymm A B
end }

lemma pseudo_metric_quot_dist_eq {Î± : Type u} [pseudo_metric_space Î±] (p q : Î±) :
  dist âŸ¦pâŸ§ âŸ¦qâŸ§ = dist p q := rfl

instance metric_space_quot {Î± : Type u} [pseudo_metric_space Î±] :
  metric_space (pseudo_metric_quot Î±) :=
{ dist_self := begin
    refine quotient.ind (Î»y, _),
    exact pseudo_metric_space.dist_self _
  end,
  eq_of_dist_eq_zero := Î»xc yc, by exact quotient.induction_onâ‚‚ xc yc (Î»x y H, quotient.sound H),
  dist_comm :=
    Î»xc yc, quotient.induction_onâ‚‚ xc yc (Î»x y, pseudo_metric_space.dist_comm _ _),
  dist_triangle :=
    Î»xc yc zc, quotient.induction_onâ‚ƒ xc yc zc (Î»x y z, pseudo_metric_space.dist_triangle _ _ _) }

end eq_rel
