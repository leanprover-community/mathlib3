/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Metric spaces.

Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel

Many definitions and theorems expected on metric spaces are already introduced on uniform spaces and
topological spaces. For example:
  open and closed sets, compactness, completeness, continuity and uniform continuity
-/
import data.real.nnreal topology.metric_space.emetric_space topology.algebra.ordered
open lattice set filter classical topological_space
noncomputable theory

open_locale uniformity

universes u v w
variables {Î± : Type u} {Î² : Type v} {Î³ : Type w}

/-- Construct a uniform structure from a distance function and metric space axioms -/
def uniform_space_of_dist
  (dist : Î± â†’ Î± â†’ â„)
  (dist_self : âˆ€ x : Î±, dist x x = 0)
  (dist_comm : âˆ€ x y : Î±, dist x y = dist y x)
  (dist_triangle : âˆ€ x y z : Î±, dist x z â‰¤ dist x y + dist y z) : uniform_space Î± :=
uniform_space.of_core {
  uniformity := (â¨… Îµ>0, principal {p:Î±Ã—Î± | dist p.1 p.2 < Îµ}),
  refl       := le_infi $ assume Îµ, le_infi $
    by simp [set.subset_def, id_rel, dist_self, (>)] {contextual := tt},
  comp       := le_infi $ assume Îµ, le_infi $ assume h, lift'_le
    (mem_infi_sets (Îµ / 2) $ mem_infi_sets (div_pos_of_pos_of_pos h two_pos) (subset.refl _)) $
    have âˆ€ (a b c : Î±), dist a c < Îµ / 2 â†’ dist c b < Îµ / 2 â†’ dist a b < Îµ,
      from assume a b c hac hcb,
      calc dist a b â‰¤ dist a c + dist c b : dist_triangle _ _ _
        ... < Îµ / 2 + Îµ / 2 : add_lt_add hac hcb
        ... = Îµ : by rw [div_add_div_same, add_self_div_two],
    by simpa [comp_rel],
  symm       := tendsto_infi.2 $ assume Îµ, tendsto_infi.2 $ assume h,
    tendsto_infi' Îµ $ tendsto_infi' h $ tendsto_principal_principal.2 $ by simp [dist_comm] }

/-- The distance function (given an ambient metric space on `Î±`), which returns
  a nonnegative real number `dist x y` given `x y : Î±`. -/
class has_dist (Î± : Type*) := (dist : Î± â†’ Î± â†’ â„)

export has_dist (dist)

/-- Metric space

Each metric space induces a canonical `uniform_space` and hence a canonical `topological_space`.
This is enforced in the type class definition, by extending the `uniform_space` structure. When
instantiating a `metric_space` structure, the uniformity fields are not necessary, they will be
filled in by default. In the same way, each metric space induces an emetric space structure.
It is included in the structure, but filled in by default.

When one instantiates a metric space structure, for instance a product structure,
this makes it possible to use a uniform structure and an edistance that are exactly
the ones for the uniform spaces product and the emetric spaces products, thereby
ensuring that everything in defeq in diamonds.-/
class metric_space (Î± : Type u) extends has_dist Î± : Type u :=
(dist_self : âˆ€ x : Î±, dist x x = 0)
(eq_of_dist_eq_zero : âˆ€ {x y : Î±}, dist x y = 0 â†’ x = y)
(dist_comm : âˆ€ x y : Î±, dist x y = dist y x)
(dist_triangle : âˆ€ x y z : Î±, dist x z â‰¤ dist x y + dist y z)
(edist : Î± â†’ Î± â†’ ennreal := Î»x y, ennreal.of_real (dist x y))
(edist_dist : âˆ€ x y : Î±, edist x y = ennreal.of_real (dist x y) . control_laws_tac)
(to_uniform_space : uniform_space Î± := uniform_space_of_dist dist dist_self dist_comm dist_triangle)
(uniformity_dist : ğ“¤ Î± = â¨… Îµ>0, principal {p:Î±Ã—Î± | dist p.1 p.2 < Îµ} . control_laws_tac)

variables [metric_space Î±]

instance metric_space.to_uniform_space' : uniform_space Î± :=
metric_space.to_uniform_space Î±

instance metric_space.to_has_edist : has_edist Î± := âŸ¨metric_space.edistâŸ©

@[simp] theorem dist_self (x : Î±) : dist x x = 0 := metric_space.dist_self x

theorem eq_of_dist_eq_zero {x y : Î±} : dist x y = 0 â†’ x = y :=
metric_space.eq_of_dist_eq_zero

theorem dist_comm (x y : Î±) : dist x y = dist y x := metric_space.dist_comm x y

theorem edist_dist (x y : Î±) : edist x y = ennreal.of_real (dist x y) :=
metric_space.edist_dist _ x y

@[simp] theorem dist_eq_zero {x y : Î±} : dist x y = 0 â†” x = y :=
iff.intro eq_of_dist_eq_zero (assume : x = y, this â–¸ dist_self _)

@[simp] theorem zero_eq_dist {x y : Î±} : 0 = dist x y â†” x = y :=
by rw [eq_comm, dist_eq_zero]

theorem dist_triangle (x y z : Î±) : dist x z â‰¤ dist x y + dist y z :=
metric_space.dist_triangle x y z

theorem dist_triangle_left (x y z : Î±) : dist x y â‰¤ dist z x + dist z y :=
by rw dist_comm z; apply dist_triangle

theorem dist_triangle_right (x y z : Î±) : dist x y â‰¤ dist x z + dist y z :=
by rw dist_comm y; apply dist_triangle

lemma dist_triangle4 (x y z w : Î±) :
  dist x w â‰¤ dist x y + dist y z + dist z w :=
calc
  dist x w â‰¤ dist x z + dist z w : dist_triangle x z w
       ... â‰¤ (dist x y + dist y z) + dist z w : add_le_add_right (metric_space.dist_triangle x y z) _

lemma dist_triangle4_left (xâ‚ yâ‚ xâ‚‚ yâ‚‚ : Î±) :
  dist xâ‚‚ yâ‚‚ â‰¤ dist xâ‚ yâ‚ + (dist xâ‚ xâ‚‚ + dist yâ‚ yâ‚‚) :=
by rw [add_left_comm, dist_comm xâ‚, â† add_assoc]; apply dist_triangle4

lemma dist_triangle4_right (xâ‚ yâ‚ xâ‚‚ yâ‚‚ : Î±) :
  dist xâ‚ yâ‚ â‰¤ dist xâ‚ xâ‚‚ + dist yâ‚ yâ‚‚ + dist xâ‚‚ yâ‚‚ :=
by rw [add_right_comm, dist_comm yâ‚]; apply dist_triangle4

theorem swap_dist : function.swap (@dist Î± _) = dist :=
by funext x y; exact dist_comm _ _

theorem abs_dist_sub_le (x y z : Î±) : abs (dist x z - dist y z) â‰¤ dist x y :=
abs_sub_le_iff.2
 âŸ¨sub_le_iff_le_add.2 (dist_triangle _ _ _),
  sub_le_iff_le_add.2 (dist_triangle_left _ _ _)âŸ©

theorem dist_nonneg {x y : Î±} : 0 â‰¤ dist x y :=
have 2 * dist x y â‰¥ 0,
  from calc 2 * dist x y = dist x y + dist y x : by rw [dist_comm x y, two_mul]
    ... â‰¥ 0 : by rw â† dist_self x; apply dist_triangle,
nonneg_of_mul_nonneg_left this two_pos

@[simp] theorem dist_le_zero {x y : Î±} : dist x y â‰¤ 0 â†” x = y :=
by simpa [le_antisymm_iff, dist_nonneg] using @dist_eq_zero _ _ x y

@[simp] theorem dist_pos {x y : Î±} : 0 < dist x y â†” x â‰  y :=
by simpa [-dist_le_zero] using not_congr (@dist_le_zero _ _ x y)

@[simp] theorem abs_dist {a b : Î±} : abs (dist a b) = dist a b :=
abs_of_nonneg dist_nonneg

theorem eq_of_forall_dist_le {x y : Î±} (h : âˆ€Îµ, Îµ > 0 â†’ dist x y â‰¤ Îµ) : x = y :=
eq_of_dist_eq_zero (eq_of_le_of_forall_le_of_dense dist_nonneg h)

def nndist (a b : Î±) : nnreal := âŸ¨dist a b, dist_nonnegâŸ©

/--Express `nndist` in terms of `edist`-/
lemma nndist_edist (x y : Î±) : nndist x y = (edist x y).to_nnreal :=
by simp [nndist, edist_dist, nnreal.of_real, max_eq_left dist_nonneg, ennreal.of_real]

/--Express `edist` in terms of `nndist`-/
lemma edist_nndist (x y : Î±) : edist x y = â†‘(nndist x y) :=
by { rw [edist_dist, nndist, ennreal.of_real_eq_coe_nnreal] }

/--In a metric space, the extended distance is always finite-/
lemma edist_ne_top (x y : Î±) : edist x y â‰  âŠ¤ :=
by rw [edist_dist x y]; apply ennreal.coe_ne_top

/--`nndist x x` vanishes-/
@[simp] lemma nndist_self (a : Î±) : nndist a a = 0 := (nnreal.coe_eq_zero _).1 (dist_self a)

/--Express `dist` in terms of `nndist`-/
lemma dist_nndist (x y : Î±) : dist x y = â†‘(nndist x y) := rfl

/--Express `nndist` in terms of `dist`-/
lemma nndist_dist (x y : Î±) : nndist x y = nnreal.of_real (dist x y) :=
by rw [dist_nndist, nnreal.of_real_coe]

/--Deduce the equality of points with the vanishing of the nonnegative distance-/
theorem eq_of_nndist_eq_zero {x y : Î±} : nndist x y = 0 â†’ x = y :=
by simp only [nnreal.eq_iff.symm, (dist_nndist _ _).symm, imp_self, nnreal.coe_zero, dist_eq_zero]

theorem nndist_comm (x y : Î±) : nndist x y = nndist y x :=
by simpa [nnreal.eq_iff.symm] using dist_comm x y

/--Characterize the equality of points with the vanishing of the nonnegative distance-/
@[simp] theorem nndist_eq_zero {x y : Î±} : nndist x y = 0 â†” x = y :=
by simp only [nnreal.eq_iff.symm, (dist_nndist _ _).symm, imp_self, nnreal.coe_zero, dist_eq_zero]

@[simp] theorem zero_eq_nndist {x y : Î±} : 0 = nndist x y â†” x = y :=
by simp only [nnreal.eq_iff.symm, (dist_nndist _ _).symm, imp_self, nnreal.coe_zero, zero_eq_dist]

/--Triangle inequality for the nonnegative distance-/
theorem nndist_triangle (x y z : Î±) : nndist x z â‰¤ nndist x y + nndist y z :=
by simpa [nnreal.coe_le] using dist_triangle x y z

theorem nndist_triangle_left (x y z : Î±) : nndist x y â‰¤ nndist z x + nndist z y :=
by simpa [nnreal.coe_le] using dist_triangle_left x y z

theorem nndist_triangle_right (x y z : Î±) : nndist x y â‰¤ nndist x z + nndist y z :=
by simpa [nnreal.coe_le] using dist_triangle_right x y z

/--Express `dist` in terms of `edist`-/
lemma dist_edist (x y : Î±) : dist x y = (edist x y).to_real :=
by rw [edist_dist, ennreal.to_real_of_real (dist_nonneg)]

namespace metric

/- instantiate metric space as a topology -/
variables {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : â„} {s : set Î±}

/-- `ball x Îµ` is the set of all points `y` with `dist y x < Îµ` -/
def ball (x : Î±) (Îµ : â„) : set Î± := {y | dist y x < Îµ}

@[simp] theorem mem_ball : y âˆˆ ball x Îµ â†” dist y x < Îµ := iff.rfl

theorem mem_ball' : y âˆˆ ball x Îµ â†” dist x y < Îµ := by rw dist_comm; refl

/-- `closed_ball x Îµ` is the set of all points `y` with `dist y x â‰¤ Îµ` -/
def closed_ball (x : Î±) (Îµ : â„) := {y | dist y x â‰¤ Îµ}

@[simp] theorem mem_closed_ball : y âˆˆ closed_ball x Îµ â†” dist y x â‰¤ Îµ := iff.rfl

theorem ball_subset_closed_ball : ball x Îµ âŠ† closed_ball x Îµ :=
assume y, by simp; intros h; apply le_of_lt h

theorem pos_of_mem_ball (hy : y âˆˆ ball x Îµ) : Îµ > 0 :=
lt_of_le_of_lt dist_nonneg hy

theorem mem_ball_self (h : Îµ > 0) : x âˆˆ ball x Îµ :=
show dist x x < Îµ, by rw dist_self; assumption

theorem mem_closed_ball_self (h : Îµ â‰¥ 0) : x âˆˆ closed_ball x Îµ :=
show dist x x â‰¤ Îµ, by rw dist_self; assumption

theorem mem_ball_comm : x âˆˆ ball y Îµ â†” y âˆˆ ball x Îµ :=
by simp [dist_comm]

theorem ball_subset_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) : ball x Îµâ‚ âŠ† ball x Îµâ‚‚ :=
Î» y (yx : _ < Îµâ‚), lt_of_lt_of_le yx h

theorem closed_ball_subset_closed_ball {Î± : Type u} [metric_space Î±] {Îµâ‚ Îµâ‚‚ : â„} {x : Î±} (h : Îµâ‚ â‰¤ Îµâ‚‚) :
  closed_ball x Îµâ‚ âŠ† closed_ball x Îµâ‚‚ :=
Î» y (yx : _ â‰¤ Îµâ‚), le_trans yx h

theorem ball_disjoint (h : Îµâ‚ + Îµâ‚‚ â‰¤ dist x y) : ball x Îµâ‚ âˆ© ball y Îµâ‚‚ = âˆ… :=
eq_empty_iff_forall_not_mem.2 $ Î» z âŸ¨hâ‚, hâ‚‚âŸ©,
not_lt_of_le (dist_triangle_left x y z)
  (lt_of_lt_of_le (add_lt_add hâ‚ hâ‚‚) h)

theorem ball_disjoint_same (h : Îµ â‰¤ dist x y / 2) : ball x Îµ âˆ© ball y Îµ = âˆ… :=
ball_disjoint $ by rwa [â† two_mul, â† le_div_iff' two_pos]

theorem ball_subset (h : dist x y â‰¤ Îµâ‚‚ - Îµâ‚) : ball x Îµâ‚ âŠ† ball y Îµâ‚‚ :=
Î» z zx, by rw â† add_sub_cancel'_right Îµâ‚ Îµâ‚‚; exact
lt_of_le_of_lt (dist_triangle z x y) (add_lt_add_of_lt_of_le zx h)

theorem ball_half_subset (y) (h : y âˆˆ ball x (Îµ / 2)) : ball y (Îµ / 2) âŠ† ball x Îµ :=
ball_subset $ by rw sub_self_div_two; exact le_of_lt h

theorem exists_ball_subset_ball (h : y âˆˆ ball x Îµ) : âˆƒ Îµ' > 0, ball y Îµ' âŠ† ball x Îµ :=
âŸ¨_, sub_pos.2 h, ball_subset $ by rw sub_sub_selfâŸ©

theorem ball_eq_empty_iff_nonpos : Îµ â‰¤ 0 â†” ball x Îµ = âˆ… :=
(eq_empty_iff_forall_not_mem.trans
âŸ¨Î» h, le_of_not_gt $ Î» Îµ0, h _ $ mem_ball_self Îµ0,
 Î» Îµ0 y h, not_lt_of_le Îµ0 $ pos_of_mem_ball hâŸ©).symm

theorem uniformity_dist : ğ“¤ Î± = (â¨… Îµ>0, principal {p:Î±Ã—Î± | dist p.1 p.2 < Îµ}) :=
metric_space.uniformity_dist _

theorem uniformity_dist' : ğ“¤ Î± = (â¨…Îµ:{Îµ:â„ // Îµ>0}, principal {p:Î±Ã—Î± | dist p.1 p.2 < Îµ.val}) :=
by simp [infi_subtype]; exact uniformity_dist

theorem mem_uniformity_dist {s : set (Î±Ã—Î±)} :
  s âˆˆ ğ“¤ Î± â†” (âˆƒÎµ>0, âˆ€{a b:Î±}, dist a b < Îµ â†’ (a, b) âˆˆ s) :=
begin
  rw [uniformity_dist', mem_infi],
  simp [subset_def],
  exact assume âŸ¨r, hrâŸ© âŸ¨p, hpâŸ©, âŸ¨âŸ¨min r p, lt_min hr hpâŸ©, by simp [lt_min_iff, (â‰¥)] {contextual := tt}âŸ©,
  exact âŸ¨âŸ¨1, zero_lt_oneâŸ©âŸ©
end

theorem dist_mem_uniformity {Îµ:â„} (Îµ0 : 0 < Îµ) :
  {p:Î±Ã—Î± | dist p.1 p.2 < Îµ} âˆˆ ğ“¤ Î± :=
mem_uniformity_dist.2 âŸ¨Îµ, Îµ0, Î» a b, idâŸ©

theorem uniform_continuous_iff [metric_space Î²] {f : Î± â†’ Î²} :
  uniform_continuous f â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0,
    âˆ€{a b:Î±}, dist a b < Î´ â†’ dist (f a) (f b) < Îµ :=
uniform_continuous_def.trans
âŸ¨Î» H Îµ Îµ0, mem_uniformity_dist.1 $ H _ $ dist_mem_uniformity Îµ0,
 Î» H r ru,
  let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_dist.1 ru, âŸ¨Î´, Î´0, hÎ´âŸ© := H _ Îµ0 in
  mem_uniformity_dist.2 âŸ¨Î´, Î´0, Î» a b h, hÎµ (hÎ´ h)âŸ©âŸ©

theorem uniform_embedding_iff [metric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†” function.injective f âˆ§ uniform_continuous f âˆ§
    âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, dist (f a) (f b) < Îµ â†’ dist a b < Î´ :=
uniform_embedding_def'.trans $ and_congr iff.rfl $ and_congr iff.rfl
âŸ¨Î» H Î´ Î´0, let âŸ¨t, tu, htâŸ© := H _ (dist_mem_uniformity Î´0),
               âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_dist.1 tu in
  âŸ¨Îµ, Îµ0, Î» a b h, ht _ _ (hÎµ h)âŸ©,
 Î» H s su, let âŸ¨Î´, Î´0, hÎ´âŸ© := mem_uniformity_dist.1 su, âŸ¨Îµ, Îµ0, hÎµâŸ© := H _ Î´0 in
  âŸ¨_, dist_mem_uniformity Îµ0, Î» a b h, hÎ´ (hÎµ h)âŸ©âŸ©

theorem totally_bounded_iff {s : set Î±} :
  totally_bounded s â†” âˆ€ Îµ > 0, âˆƒt : set Î±, finite t âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
âŸ¨Î» H Îµ Îµ0, H _ (dist_mem_uniformity Îµ0),
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_dist.1 ru,
               âŸ¨t, ft, hâŸ© := H Îµ Îµ0 in
  âŸ¨t, ft, subset.trans h $ Union_subset_Union $ Î» y, Union_subset_Union $ Î» yt z, hÎµâŸ©âŸ©

/-- A metric space space is totally bounded if one can reconstruct up to any Îµ>0 any element of the
space from finitely many data. -/
lemma totally_bounded_of_finite_discretization {Î± : Type u} [metric_space Î±] {s : set Î±}
  (H : âˆ€Îµ > (0 : â„), âˆƒ (Î² : Type u) [fintype Î²] (F : s â†’ Î²),
    âˆ€x y, F x = F y â†’ dist (x:Î±) y < Îµ) :
  totally_bounded s :=
begin
  classical, by_cases hs : s = âˆ…,
  { rw hs, exact totally_bounded_empty },
  rcases exists_mem_of_ne_empty hs with âŸ¨x0, hx0âŸ©,
  haveI : inhabited s := âŸ¨âŸ¨x0, hx0âŸ©âŸ©,
  refine totally_bounded_iff.2 (Î» Îµ Îµ0, _),
  rcases H Îµ Îµ0 with âŸ¨Î², fÎ², F, hFâŸ©,
  let Finv := function.inv_fun F,
  refine âŸ¨range (subtype.val âˆ˜ Finv), finite_range _, Î» x xs, _âŸ©,
  let x' := Finv (F âŸ¨x, xsâŸ©),
  have : F x' = F âŸ¨x, xsâŸ© := function.inv_fun_eq âŸ¨âŸ¨x, xsâŸ©, rflâŸ©,
  simp only [set.mem_Union, set.mem_range],
  exact âŸ¨_, âŸ¨F âŸ¨x, xsâŸ©, rflâŸ©, hF _ _ this.symmâŸ©
end

protected lemma cauchy_iff {f : filter Î±} :
  cauchy f â†” f â‰  âŠ¥ âˆ§ âˆ€ Îµ > 0, âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, dist x y < Îµ :=
cauchy_iff.trans $ and_congr iff.rfl
âŸ¨Î» H Îµ Îµ0, let âŸ¨t, tf, tsâŸ© := H _ (dist_mem_uniformity Îµ0) in
   âŸ¨t, tf, Î» x y xt yt, @ts (x, y) âŸ¨xt, ytâŸ©âŸ©,
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_dist.1 ru,
               âŸ¨t, tf, hâŸ© := H Îµ Îµ0 in
   âŸ¨t, tf, Î» âŸ¨x, yâŸ© âŸ¨hx, hyâŸ©, hÎµ (h x y hx hy)âŸ©âŸ©

theorem nhds_eq : nhds x = (â¨…Îµ:{Îµ:â„ // Îµ>0}, principal (ball x Îµ.val)) :=
begin
  rw [nhds_eq_uniformity, uniformity_dist', lift'_infi],
  { apply congr_arg, funext Îµ,
    rw [lift'_principal],
    { simp [ball, dist_comm] },
    { exact monotone_preimage } },
  { exact âŸ¨âŸ¨1, zero_lt_oneâŸ©âŸ© },
  { intros, refl }
end

theorem mem_nhds_iff : s âˆˆ nhds x â†” âˆƒÎµ>0, ball x Îµ âŠ† s :=
begin
  rw [nhds_eq, mem_infi],
  { simp },
  { intros y z, cases y with y hy, cases z with z hz,
    refine âŸ¨âŸ¨min y z, lt_min hy hzâŸ©, _âŸ©,
    simp [ball_subset_ball, min_le_left, min_le_right, (â‰¥)] },
  { exact âŸ¨âŸ¨1, zero_lt_oneâŸ©âŸ© }
end

theorem is_open_iff : is_open s â†” âˆ€xâˆˆs, âˆƒÎµ>0, ball x Îµ âŠ† s :=
by simp [is_open_iff_nhds, mem_nhds_iff]

theorem is_open_ball : is_open (ball x Îµ) :=
is_open_iff.2 $ Î» y, exists_ball_subset_ball

theorem ball_mem_nhds (x : Î±) {Îµ : â„} (Îµ0 : 0 < Îµ) : ball x Îµ âˆˆ nhds x :=
mem_nhds_sets is_open_ball (mem_ball_self Îµ0)

theorem tendsto_nhds_nhds [metric_space Î²] {f : Î± â†’ Î²} {a b} :
  tendsto f (nhds a) (nhds b) â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€{x:Î±}, dist x a < Î´ â†’ dist (f x) b < Îµ :=
âŸ¨Î» H Îµ Îµ0, mem_nhds_iff.1 (H (ball_mem_nhds _ Îµ0)),
 Î» H s hs,
  let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_nhds_iff.1 hs, âŸ¨Î´, Î´0, hÎ´âŸ© := H _ Îµ0 in
  mem_nhds_iff.2 âŸ¨Î´, Î´0, Î» x h, hÎµ (hÎ´ h)âŸ©âŸ©

theorem continuous_iff [metric_space Î²] {f : Î± â†’ Î²} :
  continuous f â†”
    âˆ€b (Îµ > 0), âˆƒ Î´ > 0, âˆ€a, dist a b < Î´ â†’ dist (f a) (f b) < Îµ :=
continuous_iff_continuous_at.trans $ forall_congr $ Î» b, tendsto_nhds_nhds

theorem exists_delta_of_continuous [metric_space Î²] {f : Î± â†’ Î²} {Îµ : â„}
  (hf : continuous f) (hÎµ : Îµ > 0) (b : Î±) :
  âˆƒ Î´ > 0, âˆ€a, dist a b â‰¤ Î´ â†’ dist (f a) (f b) < Îµ :=
let âŸ¨Î´, Î´_pos, hÎ´âŸ© := continuous_iff.1 hf b Îµ hÎµ in
âŸ¨Î´ / 2, half_pos Î´_pos, assume a ha, hÎ´ a $ lt_of_le_of_lt ha $ div_two_lt_of_pos Î´_posâŸ©

theorem tendsto_nhds {f : filter Î²} {u : Î² â†’ Î±} {a : Î±} :
  tendsto u f (nhds a) â†” âˆ€ Îµ > 0, âˆƒ n âˆˆ f, âˆ€x âˆˆ n,  dist (u x) a < Îµ :=
by simp only [metric.nhds_eq, tendsto_infi, subtype.forall, tendsto_principal, mem_ball];
  exact forall_congr (assume Îµ, forall_congr (assume hÎµ, exists_sets_subset_iff.symm))

theorem continuous_iff' [topological_space Î²] {f : Î² â†’ Î±} :
  continuous f â†” âˆ€a (Îµ > 0), âˆƒ n âˆˆ nhds a, âˆ€b âˆˆ n, dist (f b) (f a) < Îµ :=
continuous_iff_continuous_at.trans $ forall_congr $ Î» b, tendsto_nhds

theorem tendsto_at_top [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} {a : Î±} :
  tendsto u at_top (nhds a) â†” âˆ€Îµ>0, âˆƒN, âˆ€nâ‰¥N, dist (u n) a < Îµ :=
by simp only [metric.nhds_eq, tendsto_infi, subtype.forall, tendsto_at_top_principal]; refl

end metric

open metric

instance metric_space.to_separated : separated Î± :=
separated_def.2 $ Î» x y h, eq_of_forall_dist_le $
  Î» Îµ Îµ0, le_of_lt (h _ (dist_mem_uniformity Îµ0))

/-Instantiate a metric space as an emetric space. Before we can state the instance,
we need to show that the uniform structure coming from the edistance and the
distance coincide. -/

/-- Expressing the uniformity in terms of `edist` -/
protected lemma metric.mem_uniformity_edist {s : set (Î±Ã—Î±)} :
  s âˆˆ ğ“¤ Î± â†” (âˆƒÎµ>0, âˆ€{a b:Î±}, edist a b < Îµ â†’ (a, b) âˆˆ s) :=
begin
  refine mem_uniformity_dist.trans âŸ¨_, _âŸ©; rintro âŸ¨Îµ, Îµ0, HÎµâŸ©,
  { refine âŸ¨ennreal.of_real Îµ, _, Î» a b, _âŸ©,
    { rwa [gt, ennreal.of_real_pos] },
    { rw [edist_dist, ennreal.of_real_lt_of_real_iff Îµ0],
      exact HÎµ } },
  { rcases ennreal.lt_iff_exists_real_btwn.1 Îµ0 with âŸ¨Îµ', _, Îµ0', hÎµâŸ©,
    rw [ennreal.of_real_pos] at Îµ0',
    refine âŸ¨Îµ', Îµ0', Î» a b h, HÎµ (lt_trans _ hÎµ)âŸ©,
    rwa [edist_dist, ennreal.of_real_lt_of_real_iff Îµ0'] }
end

protected theorem metric.uniformity_edist' : ğ“¤ Î± = (â¨…Îµ:{Îµ:ennreal // Îµ>0}, principal {p:Î±Ã—Î± | edist p.1 p.2 < Îµ.val}) :=
begin
  ext s, rw mem_infi,
  { simp [metric.mem_uniformity_edist, subset_def] },
  { rintro âŸ¨r, hrâŸ© âŸ¨p, hpâŸ©, use âŸ¨min r p, lt_min hr hpâŸ©,
    simp [lt_min_iff, (â‰¥)] {contextual := tt} },
  { exact âŸ¨âŸ¨1, ennreal.zero_lt_oneâŸ©âŸ© }
end

theorem uniformity_edist : ğ“¤ Î± = (â¨… Îµ>0, principal {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
by simpa [infi_subtype] using @metric.uniformity_edist' Î± _

/-- A metric space induces an emetric space -/
instance metric_space.to_emetric_space : emetric_space Î± :=
{ edist               := edist,
  edist_self          := by simp [edist_dist],
  eq_of_edist_eq_zero := assume x y h, by simpa [edist_dist] using h,
  edist_comm          := by simp only [edist_dist, dist_comm]; simp,
  edist_triangle      := assume x y z, begin
    simp only [edist_dist, (ennreal.of_real_add _ _).symm, dist_nonneg],
    rw ennreal.of_real_le_of_real_iff _,
    { exact dist_triangle _ _ _ },
    { simpa using add_le_add (dist_nonneg : 0 â‰¤ dist x y) dist_nonneg }
  end,
  uniformity_edist    := uniformity_edist,
  ..â€¹metric_space Î±â€º }

/-- Balls defined using the distance or the edistance coincide -/
lemma metric.emetric_ball {x : Î±} {Îµ : â„} : emetric.ball x (ennreal.of_real Îµ) = ball x Îµ :=
begin
  classical, by_cases h : 0 < Îµ,
  { ext y, by simp [edist_dist, ennreal.of_real_lt_of_real_iff h] },
  { have h' : Îµ â‰¤ 0, by simpa using h,
    have A : ball x Îµ = âˆ…, by simpa [ball_eq_empty_iff_nonpos.symm],
    have B : emetric.ball x (ennreal.of_real Îµ) = âˆ…,
      by simp [ennreal.of_real_eq_zero.2 h', emetric.ball_eq_empty_iff],
    rwa [A, B] }
end

/-- Closed balls defined using the distance or the edistance coincide -/
lemma metric.emetric_closed_ball {x : Î±} {Îµ : â„} (h : 0 â‰¤ Îµ) :
  emetric.closed_ball x (ennreal.of_real Îµ) = closed_ball x Îµ :=
by ext y; simp [edist_dist]; rw ennreal.of_real_le_of_real_iff h

def metric_space.replace_uniformity {Î±} [U : uniform_space Î±] (m : metric_space Î±)
  (H : @uniformity _ U = @uniformity _ (metric_space.to_uniform_space Î±)) :
  metric_space Î± :=
{ dist               := @dist _ m.to_has_dist,
  dist_self          := dist_self,
  eq_of_dist_eq_zero := @eq_of_dist_eq_zero _ _,
  dist_comm          := dist_comm,
  dist_triangle      := dist_triangle,
  edist              := edist,
  edist_dist         := edist_dist,
  to_uniform_space   := U,
  uniformity_dist    := H.trans (metric_space.uniformity_dist Î±) }

/-- One gets a metric space from an emetric space if the edistance
is everywhere finite. We set it up so that the edist and the uniformity are
defeq in the metric space and the emetric space -/

def emetric_space.to_metric_space {Î± : Type u} [e : emetric_space Î±] (h : âˆ€x y: Î±, edist x y â‰  âŠ¤) :
  metric_space Î± :=
let m : metric_space Î± :=
{ dist               := Î»x y, ennreal.to_real (edist x y),
  eq_of_dist_eq_zero := Î»x y hxy, by simpa [dist, ennreal.to_real_eq_zero_iff, h x y] using hxy,
  dist_self          := Î»x, by simp,
  dist_comm          := Î»x y, by simp [emetric_space.edist_comm],
  dist_triangle      := Î»x y z, begin
    rw [â† ennreal.to_real_add (h _ _) (h _ _), ennreal.to_real_le_to_real (h _ _)],
    { exact edist_triangle _ _ _ },
    { simp [ennreal.add_eq_top, h] }
  end,
  edist              := Î»x y, edist x y,
  edist_dist         := Î»x y, by simp [ennreal.of_real_to_real, h] } in
metric_space.replace_uniformity m (by rw [uniformity_edist, uniformity_edist']; refl)

section real

/-- Instantiate the reals as a metric space. -/
instance real.metric_space : metric_space â„ :=
{ dist               := Î»x y, abs (x - y),
  dist_self          := by simp [abs_zero],
  eq_of_dist_eq_zero := by simp [add_neg_eq_zero],
  dist_comm          := assume x y, abs_sub _ _,
  dist_triangle      := assume x y z, abs_sub_le _ _ _ }

theorem real.dist_eq (x y : â„) : dist x y = abs (x - y) := rfl

theorem real.dist_0_eq_abs (x : â„) : dist x 0 = abs x :=
by simp [real.dist_eq]

instance : orderable_topology â„ :=
orderable_topology_of_nhds_abs $ Î» x, begin
  simp only [show âˆ€ r, {b : â„ | abs (x - b) < r} = ball x r,
    by simp [-sub_eq_add_neg, abs_sub, ball, real.dist_eq]],
  apply le_antisymm,
  { simp [le_infi_iff],
    exact Î» Îµ Îµ0, mem_nhds_sets (is_open_ball) (mem_ball_self Îµ0) },
  { intros s h,
    rcases mem_nhds_iff.1 h with âŸ¨Îµ, Îµ0, ssâŸ©,
    exact mem_infi_sets _ (mem_infi_sets Îµ0 (mem_principal_sets.2 ss)) },
end

lemma closed_ball_Icc {x r : â„} : closed_ball x r = Icc (x-r) (x+r) :=
by ext y; rw [mem_closed_ball, dist_comm, real.dist_eq,
  abs_sub_le_iff, mem_Icc, â† sub_le_iff_le_add', sub_le]

lemma squeeze_zero {Î±} {f g : Î± â†’ â„} {tâ‚€ : filter Î±} (hf : âˆ€t, 0 â‰¤ f t) (hft : âˆ€t, f t â‰¤ g t)
  (g0 : tendsto g tâ‚€ (nhds 0)) : tendsto f tâ‚€ (nhds 0) :=
begin
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le (tendsto_const_nhds) g0;
  simp [*]; exact filter.univ_mem_sets
end

theorem metric.uniformity_eq_comap_nhds_zero :
  ğ“¤ Î± = comap (Î»p:Î±Ã—Î±, dist p.1 p.2) (nhds (0 : â„)) :=
begin
  simp only [uniformity_dist', nhds_eq, comap_infi, comap_principal],
  congr, funext Îµ,
  rw [principal_eq_iff_eq],
  ext âŸ¨a, bâŸ©,
  simp [real.dist_0_eq_abs]
end

lemma cauchy_seq_iff_tendsto_dist_at_top_0 [inhabited Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” tendsto (Î» (n : Î² Ã— Î²), dist (u n.1) (u n.2)) at_top (nhds 0) :=
by rw [cauchy_seq_iff_prod_map, metric.uniformity_eq_comap_nhds_zero, â† map_le_iff_le_comap,
  filter.map_map, tendsto, prod.map_def]

end real

section cauchy_seq
variables [inhabited Î²] [semilattice_sup Î²]

/-- In a metric space, Cauchy sequences are characterized by the fact that, eventually,
the distance between its elements is arbitrarily small -/
theorem metric.cauchy_seq_iff {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>0, âˆƒN, âˆ€m nâ‰¥N, dist (u m) (u n) < Îµ :=
begin
  unfold cauchy_seq,
  rw metric.cauchy_iff,
  simp only [true_and, exists_prop, filter.mem_at_top_sets, filter.at_top_ne_bot,
             filter.mem_map, ne.def, filter.map_eq_bot_iff, not_false_iff, set.mem_set_of_eq],
  split,
  { intros H Îµ Îµpos,
    rcases H Îµ Îµpos with âŸ¨t, âŸ¨N, hNâŸ©, htâŸ©,
    exact âŸ¨N, Î»m n hm hn, ht _ _ (hN _ hm) (hN _ hn)âŸ© },
  { intros H Îµ Îµpos,
    rcases H (Îµ/2) (half_pos Îµpos) with âŸ¨N, hNâŸ©,
    existsi ball (u N) (Îµ/2),
    split,
    { exact âŸ¨N, Î»x hx, hN _ _ hx (le_refl N)âŸ© },
    { exact Î»x y hx hy, calc
        dist x y â‰¤ dist x (u N) + dist y (u N) : dist_triangle_right _ _ _
        ... < Îµ/2 + Îµ/2 : add_lt_add hx hy
        ... = Îµ : add_halves _ } }
end

/-- A variation around the metric characterization of Cauchy sequences -/
theorem metric.cauchy_seq_iff' {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>0, âˆƒN, âˆ€nâ‰¥N, dist (u n) (u N) < Îµ :=
begin
  rw metric.cauchy_seq_iff,
  split,
  { intros H Îµ Îµpos,
    rcases H Îµ Îµpos with âŸ¨N, hNâŸ©,
    exact âŸ¨N, Î»n hn, hN _ _ hn (le_refl N)âŸ© },
  { intros H Îµ Îµpos,
    rcases H (Îµ/2) (half_pos Îµpos) with âŸ¨N, hNâŸ©,
    exact âŸ¨N, Î» m n hm hn, calc
       dist (u m) (u n) â‰¤ dist (u m) (u N) + dist (u n) (u N) : dist_triangle_right _ _ _
                    ... < Îµ/2 + Îµ/2 : add_lt_add (hN _ hm) (hN _ hn)
                    ... = Îµ : add_halves _âŸ© }
end

/-- A Cauchy sequence on the natural numbers is bounded. -/
theorem cauchy_seq_bdd {u : â„• â†’ Î±} (hu : cauchy_seq u) :
  âˆƒ R > 0, âˆ€ m n, dist (u m) (u n) < R :=
begin
  rcases metric.cauchy_seq_iff'.1 hu 1 zero_lt_one with âŸ¨N, hNâŸ©,
  suffices : âˆƒ R > 0, âˆ€ n, dist (u n) (u N) < R,
  { rcases this with âŸ¨R, R0, HâŸ©,
    exact âŸ¨_, add_pos R0 R0, Î» m n,
      lt_of_le_of_lt (dist_triangle_right _ _ _) (add_lt_add (H m) (H n))âŸ© },
  let R := finset.sup (finset.range N) (Î» n, nndist (u n) (u N)),
  refine âŸ¨â†‘R + 1, add_pos_of_nonneg_of_pos R.2 zero_lt_one, Î» n, _âŸ©,
  cases le_or_lt N n,
  { exact lt_of_lt_of_le (hN _ h) (le_add_of_nonneg_left R.2) },
  { have : _ â‰¤ R := finset.le_sup (finset.mem_range.2 h),
    exact lt_of_le_of_lt this (lt_add_of_pos_right _ zero_lt_one) }
end

/-- Yet another metric characterization of Cauchy sequences on integers. This one is often the
most efficient. -/
lemma cauchy_seq_iff_le_tendsto_0 {s : â„• â†’ Î±} : cauchy_seq s â†” âˆƒ b : â„• â†’ â„,
  (âˆ€ n, 0 â‰¤ b n) âˆ§
  (âˆ€ n m N : â„•, N â‰¤ n â†’ N â‰¤ m â†’ dist (s n) (s m) â‰¤ b N) âˆ§
  tendsto b at_top (nhds 0) :=
âŸ¨Î» hs, begin
  /- `s` is a Cauchy sequence. The sequence `b` will be constructed by taking
  the supremum of the distances between `s n` and `s m` for `n m â‰¥ N`.
  First, we prove that all these distances are bounded, as otherwise the Sup
  would not make sense. -/
  let S := Î» N, (Î»(p : â„• Ã— â„•), dist (s p.1) (s p.2)) '' {p | p.1 â‰¥ N âˆ§ p.2 â‰¥ N},
  have hS : âˆ€ N, âˆƒ x, âˆ€ y âˆˆ S N, y â‰¤ x,
  { rcases cauchy_seq_bdd hs with âŸ¨R, R0, hRâŸ©,
    refine Î» N, âŸ¨R, _âŸ©, rintro _ âŸ¨âŸ¨m, nâŸ©, _, rflâŸ©,
    exact le_of_lt (hR m n) },
  have bdd : bdd_above (range (Î»(p : â„• Ã— â„•), dist (s p.1) (s p.2))),
  { rcases cauchy_seq_bdd hs with âŸ¨R, R0, hRâŸ©,
    use R, rintro _ âŸ¨âŸ¨m, nâŸ©, rflâŸ©, exact le_of_lt (hR m n) },
  -- Prove that it bounds the distances of points in the Cauchy sequence
  have ub : âˆ€ m n N, N â‰¤ m â†’ N â‰¤ n â†’ dist (s m) (s n) â‰¤ real.Sup (S N) :=
    Î» m n N hm hn, real.le_Sup _ (hS N) âŸ¨âŸ¨_, _âŸ©, âŸ¨hm, hnâŸ©, rflâŸ©,
  have S0m : âˆ€ n, (0:â„) âˆˆ S n := Î» n, âŸ¨âŸ¨n, nâŸ©, âŸ¨le_refl _, le_refl _âŸ©, dist_self _âŸ©,
  have S0 := Î» n, real.le_Sup _ (hS n) (S0m n),
  -- Prove that it tends to `0`, by using the Cauchy property of `s`
  refine âŸ¨Î» N, real.Sup (S N), S0, ub, metric.tendsto_at_top.2 (Î» Îµ Îµ0, _)âŸ©,
  refine (metric.cauchy_seq_iff.1 hs (Îµ/2) (half_pos Îµ0)).imp (Î» N hN n hn, _),
  rw [real.dist_0_eq_abs, abs_of_nonneg (S0 n)],
  refine lt_of_le_of_lt (real.Sup_le_ub _ âŸ¨_, S0m _âŸ© _) (half_lt_self Îµ0),
  rintro _ âŸ¨âŸ¨m', n'âŸ©, âŸ¨hm', hn'âŸ©, rflâŸ©,
  exact le_of_lt (hN _ _ (le_trans hn hm') (le_trans hn hn'))
  end,
Î» âŸ¨b, _, b_bound, b_limâŸ©, metric.cauchy_seq_iff.2 $ Î» Îµ Îµ0,
  (metric.tendsto_at_top.1 b_lim Îµ Îµ0).imp $ Î» N hN m n hm hn,
  calc dist (s m) (s n) â‰¤ b N : b_bound m n N hm hn
                    ... â‰¤ abs (b N) : le_abs_self _
                    ... = dist (b N) 0 : by rw real.dist_0_eq_abs; refl
                    ... < Îµ : (hN _ (le_refl N)) âŸ©

end cauchy_seq

def metric_space.induced {Î± Î²} (f : Î± â†’ Î²) (hf : function.injective f)
  (m : metric_space Î²) : metric_space Î± :=
{ dist               := Î» x y, dist (f x) (f y),
  dist_self          := Î» x, dist_self _,
  eq_of_dist_eq_zero := Î» x y h, hf (dist_eq_zero.1 h),
  dist_comm          := Î» x y, dist_comm _ _,
  dist_triangle      := Î» x y z, dist_triangle _ _ _,
  edist              := Î» x y, edist (f x) (f y),
  edist_dist         := Î» x y, edist_dist _ _,
  to_uniform_space   := uniform_space.comap f m.to_uniform_space,
  uniformity_dist    := begin
    apply @uniformity_dist_of_mem_uniformity _ _ _ _ _ (Î» x y, dist (f x) (f y)),
    refine Î» s, mem_comap_sets.trans _,
    split; intro H,
    { rcases H with âŸ¨r, ru, rsâŸ©,
      rcases mem_uniformity_dist.1 ru with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      refine âŸ¨Îµ, Îµ0, Î» a b h, rs (hÎµ _)âŸ©, exact h },
    { rcases H with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      exact âŸ¨_, dist_mem_uniformity Îµ0, Î» âŸ¨a, bâŸ©, hÎµâŸ© }
  end }

instance subtype.metric_space {Î± : Type*} {p : Î± â†’ Prop} [t : metric_space Î±] :
  metric_space (subtype p) :=
metric_space.induced subtype.val (Î» x y, subtype.eq) t

theorem subtype.dist_eq {p : Î± â†’ Prop} (x y : subtype p) : dist x y = dist x.1 y.1 := rfl

section nnreal

instance : metric_space nnreal := by unfold nnreal; apply_instance

end nnreal

section prod

instance prod.metric_space_max [metric_space Î²] : metric_space (Î± Ã— Î²) :=
{ dist := Î» x y, max (dist x.1 y.1) (dist x.2 y.2),
  dist_self := Î» x, by simp,
  eq_of_dist_eq_zero := Î» x y h, begin
    cases max_le_iff.1 (le_of_eq h) with hâ‚ hâ‚‚,
    exact prod.ext_iff.2 âŸ¨dist_le_zero.1 hâ‚, dist_le_zero.1 hâ‚‚âŸ©
  end,
  dist_comm := Î» x y, by simp [dist_comm],
  dist_triangle := Î» x y z, max_le
    (le_trans (dist_triangle _ _ _) (add_le_add (le_max_left _ _) (le_max_left _ _)))
    (le_trans (dist_triangle _ _ _) (add_le_add (le_max_right _ _) (le_max_right _ _))),
  edist := Î» x y, max (edist x.1 y.1) (edist x.2 y.2),
  edist_dist := assume x y, begin
    have : monotone ennreal.of_real := assume x y h, ennreal.of_real_le_of_real h,
    rw [edist_dist, edist_dist, (max_distrib_of_monotone this).symm]
  end,
  uniformity_dist := begin
    refine uniformity_prod.trans _,
    simp [uniformity_dist, comap_infi],
    rw â† infi_inf_eq, congr, funext,
    rw â† infi_inf_eq, congr, funext,
    simp [inf_principal, ext_iff, max_lt_iff]
  end,
  to_uniform_space := prod.uniform_space }

lemma prod.dist_eq [metric_space Î²] {x y : Î± Ã— Î²} :
  dist x y = max (dist x.1 y.1) (dist x.2 y.2) := rfl

end prod

theorem uniform_continuous_dist' : uniform_continuous (Î»p:Î±Ã—Î±, dist p.1 p.2) :=
metric.uniform_continuous_iff.2 (Î» Îµ Îµ0, âŸ¨Îµ/2, half_pos Îµ0,
begin
  suffices,
  { intros p q h, cases p with pâ‚ pâ‚‚, cases q with qâ‚ qâ‚‚,
    cases max_lt_iff.1 h with hâ‚ hâ‚‚, clear h,
    dsimp at hâ‚ hâ‚‚ âŠ¢,
    rw real.dist_eq,
    refine abs_sub_lt_iff.2 âŸ¨_, _âŸ©,
    { revert pâ‚ pâ‚‚ qâ‚ qâ‚‚ hâ‚ hâ‚‚, exact this },
    { apply this; rwa dist_comm } },
  intros pâ‚ pâ‚‚ qâ‚ qâ‚‚ hâ‚ hâ‚‚,
  have := add_lt_add
    (abs_sub_lt_iff.1 (lt_of_le_of_lt (abs_dist_sub_le pâ‚ qâ‚ pâ‚‚) hâ‚)).1
    (abs_sub_lt_iff.1 (lt_of_le_of_lt (abs_dist_sub_le pâ‚‚ qâ‚‚ qâ‚) hâ‚‚)).1,
  rwa [add_halves, dist_comm pâ‚‚, sub_add_sub_cancel, dist_comm qâ‚‚] at this
endâŸ©)

theorem uniform_continuous_dist [uniform_space Î²] {f g : Î² â†’ Î±}
  (hf : uniform_continuous f) (hg : uniform_continuous g) :
  uniform_continuous (Î»b, dist (f b) (g b)) :=
uniform_continuous_dist'.comp (hf.prod_mk hg)

theorem continuous_dist' : continuous (Î»p:Î±Ã—Î±, dist p.1 p.2) :=
uniform_continuous_dist'.continuous

theorem continuous_dist [topological_space Î²] {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) : continuous (Î»b, dist (f b) (g b)) :=
continuous_dist'.comp (hf.prod_mk hg)

theorem tendsto_dist {f g : Î² â†’ Î±} {x : filter Î²} {a b : Î±}
  (hf : tendsto f x (nhds a)) (hg : tendsto g x (nhds b)) :
  tendsto (Î»x, dist (f x) (g x)) x (nhds (dist a b)) :=
have tendsto (Î»p:Î±Ã—Î±, dist p.1 p.2) (nhds (a, b)) (nhds (dist a b)),
  from continuous_iff_continuous_at.mp continuous_dist' (a, b),
tendsto.comp (by rw [nhds_prod_eq] at this; exact this) (hf.prod_mk hg)

lemma nhds_comap_dist (a : Î±) : (nhds (0 : â„)).comap (Î»a', dist a' a) = nhds a :=
have hâ‚ : âˆ€Îµ, (Î»a', dist a' a) â»Â¹' ball 0 Îµ âŠ† ball a Îµ,
  by simp [subset_def, real.dist_0_eq_abs],
have hâ‚‚ : tendsto (Î»a', dist a' a) (nhds a) (nhds (dist a a)),
  from tendsto_dist tendsto_id tendsto_const_nhds,
le_antisymm
  (by simp [hâ‚, nhds_eq, infi_le_infi, principal_mono,
      -le_principal_iff, -le_infi_iff])
  (by simpa [map_le_iff_le_comap.symm, tendsto] using hâ‚‚)

lemma tendsto_iff_dist_tendsto_zero {f : Î² â†’ Î±} {x : filter Î²} {a : Î±} :
  (tendsto f x (nhds a)) â†” (tendsto (Î»b, dist (f b) a) x (nhds 0)) :=
by rw [â† nhds_comap_dist a, tendsto_comap_iff]

lemma uniform_continuous_nndist' : uniform_continuous (Î»p:Î±Ã—Î±, nndist p.1 p.2) :=
uniform_continuous_subtype_mk uniform_continuous_dist' _

lemma continuous_nndist' : continuous (Î»p:Î±Ã—Î±, nndist p.1 p.2) :=
uniform_continuous_nndist'.continuous

lemma continuous_nndist [topological_space Î²] {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) : continuous (Î»b, nndist (f b) (g b)) :=
continuous_nndist'.comp (hf.prod_mk hg)

lemma tendsto_nndist' (a b :Î±) :
  tendsto (Î»p:Î±Ã—Î±, nndist p.1 p.2) (filter.prod (nhds a) (nhds b)) (nhds (nndist a b)) :=
by rw [â† nhds_prod_eq]; exact continuous_iff_continuous_at.1 continuous_nndist' _

namespace metric
variables {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : â„} {s : set Î±}

theorem is_closed_ball : is_closed (closed_ball x Îµ) :=
is_closed_le (continuous_dist continuous_id continuous_const) continuous_const

/-- Îµ-characterization of the closure in metric spaces-/
theorem mem_closure_iff' {Î± : Type u} [metric_space Î±] {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆ€Îµ>0, âˆƒb âˆˆ s, dist a b < Îµ :=
âŸ¨begin
  intros ha Îµ hÎµ,
  have A : ball a Îµ âˆ© s â‰  âˆ… := mem_closure_iff.1 ha _ is_open_ball (mem_ball_self hÎµ),
  cases ne_empty_iff_exists_mem.1 A with b hb,
  simp,
  exact âŸ¨b, âŸ¨hb.2, by have B := hb.1; simpa [mem_ball'] using BâŸ©âŸ©
end,
begin
  intros H,
  apply mem_closure_iff.2,
  intros o ho ao,
  rcases is_open_iff.1 ho a ao with âŸ¨Îµ, âŸ¨Îµpos, hÎµâŸ©âŸ©,
  rcases H Îµ Îµpos with âŸ¨b, âŸ¨bs, bdistâŸ©âŸ©,
  have B : b âˆˆ o âˆ© s := âŸ¨hÎµ (by simpa [dist_comm]), bsâŸ©,
  apply ne_empty_of_mem B
endâŸ©

lemma mem_closure_range_iff {Î± : Type u} [metric_space Î±] {e : Î² â†’ Î±} {a : Î±} :
  a âˆˆ closure (range e) â†” âˆ€Îµ>0, âˆƒ k : Î², dist a (e k) < Îµ :=
iff.intro
( assume ha Îµ hÎµ,
  let âŸ¨b, âŸ¨hb, habâŸ©âŸ© := metric.mem_closure_iff'.1 ha Îµ hÎµ in
  let âŸ¨k, hkâŸ© := mem_range.1 hb in
  âŸ¨k, by { rw hk, exact hab }âŸ© )
( assume h, metric.mem_closure_iff'.2 (assume Îµ hÎµ,
  let âŸ¨k, hkâŸ© := h Îµ hÎµ in
  âŸ¨e k, âŸ¨mem_range.2 âŸ¨k, rflâŸ©, hkâŸ©âŸ©) )

lemma mem_closure_range_iff_nat {Î± : Type u} [metric_space Î±] {e : Î² â†’ Î±} {a : Î±} :
  a âˆˆ closure (range e) â†” âˆ€n : â„•, âˆƒ k : Î², dist a (e k) < 1 / ((n : â„) + 1) :=
âŸ¨assume ha n, mem_closure_range_iff.1 ha (1 / ((n : â„) + 1)) nat.one_div_pos_of_nat,
 assume h, mem_closure_range_iff.2 $ assume Îµ hÎµ,
  let âŸ¨n, hnâŸ© := exists_nat_one_div_lt hÎµ in
  let âŸ¨k, hkâŸ© := h n  in
  âŸ¨k, calc dist a (e k) < 1 / ((n : â„) + 1) : hk ... < Îµ : hnâŸ©âŸ©

theorem mem_of_closed' {Î± : Type u} [metric_space Î±] {s : set Î±} (hs : is_closed s)
  {a : Î±} : a âˆˆ s â†” âˆ€Îµ>0, âˆƒb âˆˆ s, dist a b < Îµ :=
by simpa only [closure_eq_of_is_closed hs] using @mem_closure_iff' _ _ s a

end metric

section pi
open finset lattice
variables {Ï€ : Î² â†’ Type*} [fintype Î²] [âˆ€b, metric_space (Ï€ b)]

instance has_dist_pi : has_dist (Î b, Ï€ b) :=
âŸ¨Î»f g, ((finset.sup univ (Î»b, nndist (f b) (g b)) : nnreal) : â„)âŸ©

lemma dist_pi_def (f g : Î b, Ï€ b) :
  dist f g = (finset.sup univ (Î»b, nndist (f b) (g b)) : nnreal) := rfl

instance metric_space_pi : metric_space (Î b, Ï€ b) :=
{ dist := dist,
  dist_self := assume f, (nnreal.coe_eq_zero _).2 $ bot_unique $ finset.sup_le $ by simp,
  dist_comm := assume f g, nnreal.eq_iff.2 $ by congr; ext a; exact nndist_comm _ _,
  dist_triangle := assume f g h, show dist f h â‰¤ (dist f g) + (dist g h), from
    begin
      simp only [dist_pi_def, (nnreal.coe_add _ _).symm, nnreal.coe_le.symm,
        finset.sup_le_iff],
      assume b hb,
      exact le_trans (nndist_triangle _ (g b) _) (add_le_add (le_sup hb) (le_sup hb))
    end,
  eq_of_dist_eq_zero := assume f g eq0,
    begin
      simp only [dist_pi_def, nnreal.coe_eq_zero, nnreal.bot_eq_zero.symm, eq_bot_iff,
        finset.sup_le_iff] at eq0,
      exact (funext $ assume b, eq_of_nndist_eq_zero $ bot_unique $ eq0 b $ mem_univ b),
    end,
  edist := Î» f g, finset.sup univ (Î»b, edist (f b) (g b)),
  edist_dist := assume x y, begin
    have A : sup univ (Î» (b : Î²), ((nndist (x b) (y b)) : ennreal)) = â†‘(sup univ (Î» (b : Î²), nndist (x b) (y b))),
    { refine eq.symm (comp_sup_eq_sup_comp _ _ _),
      exact (assume x y h, ennreal.coe_le_coe.2 h), refl },
    simp [dist, edist_nndist, ennreal.of_real, A]
  end,
  to_uniform_space := Pi.uniform_space _,
  uniformity_dist := begin
    -- with simp only on next line, the proof fails for no reason...
    simp [Pi.uniformity, uniformity_dist, comap_infi, comap_infi, gt_iff_lt, preimage_set_of_eq,
          comap_principal],
    rw infi_comm, congr, funext Îµ,
    rw infi_comm, congr, funext Îµpos,
    simp only [ext_iff, Îµpos, dist, principal_eq_iff_eq, prod.forall, mem_Inter, mem_set_of_eq,
               infi_principal_fintype],
    assume a b,
    let Îµ' : nnreal := âŸ¨Îµ, le_of_lt ÎµposâŸ©,
    have A : Îµ' = nnreal.of_real Îµ, by simp [nnreal.of_real, Îµ', le_of_lt Îµpos],
    change (âˆ€ (i : Î²), dist (a i) (b i) < Îµ) â†” (sup univ (Î» (i : Î²), nndist (a i) (b i))) < Îµ',
    simp only [finset.sup_lt_iff (show âŠ¥ < Îµ', from Îµpos)],
    simp [nndist_dist, A, Îµpos],
  end }

end pi

section compact

/-- Any compact set in a metric space can be covered by finitely many balls of a given positive
radius -/
lemma finite_cover_balls_of_compact {Î± : Type u} [metric_space Î±] {s : set Î±}
  (hs : compact s) {e : â„} (he : e > 0) :
  âˆƒt âŠ† s, finite t âˆ§ s âŠ† â‹ƒxâˆˆt, ball x e :=
begin
  apply compact_elim_finite_subcover_image hs,
  { simp [is_open_ball] },
  { intros x xs,
    simp,
    exact âŸ¨x, âŸ¨xs, by simpaâŸ©âŸ© }
end

end compact

section proper_space
open metric

/-- A metric space is proper if all closed balls are compact. -/
class proper_space (Î± : Type u) [metric_space Î±] : Prop :=
(compact_ball : âˆ€x:Î±, âˆ€r, compact (closed_ball x r))

/- A compact metric space is proper -/
instance proper_of_compact [compact_space Î±] : proper_space Î± :=
âŸ¨assume x r, compact_of_is_closed_subset compact_univ is_closed_ball (subset_univ _)âŸ©

/-- A proper space is locally compact -/
instance locally_compact_of_proper [proper_space Î±] :
  locally_compact_space Î± :=
begin
  apply locally_compact_of_compact_nhds,
  intros x,
  existsi closed_ball x 1,
  split,
  { apply mem_nhds_iff.2,
    existsi (1 : â„),
    simp,
    exact âŸ¨zero_lt_one, ball_subset_closed_ballâŸ© },
  { apply proper_space.compact_ball }
end

/-- A proper space is complete -/
instance complete_of_proper [proper_space Î±] : complete_space Î± :=
âŸ¨begin
  intros f hf,
  /- We want to show that the Cauchy filter `f` is converging. It suffices to find a closed
  ball (therefore compact by properness) where it is nontrivial. -/
  have A : âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, dist x y < 1 := (metric.cauchy_iff.1 hf).2 1 zero_lt_one,
  rcases A with âŸ¨t, âŸ¨t_fset, htâŸ©âŸ©,
  rcases inhabited_of_mem_sets hf.1 t_fset with âŸ¨x, xtâŸ©,
  have : t âŠ† closed_ball x 1 := by intros y yt; simp [dist_comm]; apply le_of_lt (ht x y xt yt),
  have : closed_ball x 1 âˆˆ f := f.sets_of_superset t_fset this,
  rcases (compact_iff_totally_bounded_complete.1 (proper_space.compact_ball x 1)).2 f hf (le_principal_iff.2 this)
    with âŸ¨y, _, hyâŸ©,
  exact âŸ¨y, hyâŸ©
endâŸ©

/-- A proper metric space is separable, and therefore second countable. Indeed, any ball is
compact, and therefore admits a countable dense subset. Taking a countable union over the balls
centered at a fixed point and with integer radius, one obtains a countable set which is
dense in the whole space. -/
instance second_countable_of_proper [proper_space Î±] :
  second_countable_topology Î± :=
begin
  /- We show that the space admits a countable dense subset. The case where the space is empty
  is special, and trivial. -/
  have A : (univ : set Î±) = âˆ… â†’ âˆƒ(s : set Î±), countable s âˆ§ closure s = (univ : set Î±) :=
    assume H, âŸ¨âˆ…, âŸ¨by simp, by simp; exact H.symmâŸ©âŸ©,
  have B : (univ : set Î±) â‰  âˆ… â†’ âˆƒ(s : set Î±), countable s âˆ§ closure s = (univ : set Î±) :=
  begin
    /- When the space is not empty, we take a point `x` in the space, and then a countable set
    `T r` which is dense in the closed ball `closed_ball x r` for each `r`. Then the set
    `t = â‹ƒ T n` (where the union is over all integers `n`) is countable, as a countable union
    of countable sets, and dense in the space by construction. -/
    assume non_empty,
    rcases ne_empty_iff_exists_mem.1 non_empty with âŸ¨x, x_univâŸ©,
    choose T a using show âˆ€ (r:â„), âˆƒ t âŠ† closed_ball x r, (countable (t : set Î±) âˆ§ closed_ball x r = closure t),
      from assume r, emetric.countable_closure_of_compact (proper_space.compact_ball _ _),
    let t := (â‹ƒn:â„•, T (n : â„)),
    have Tâ‚ : countable t := by finish [countable_Union],
    have Tâ‚‚ : closure t âŠ† univ := by simp,
    have Tâ‚ƒ : univ âŠ† closure t :=
    begin
      intros y y_univ,
      rcases exists_nat_gt (dist y x) with âŸ¨n, n_largeâŸ©,
      have h : y âˆˆ closed_ball x (n : â„) := by simp; apply le_of_lt n_large,
      have h' : closed_ball x (n : â„) = closure (T (n : â„)) := by finish,
      have : y âˆˆ closure (T (n : â„)) := by rwa h' at h,
      show y âˆˆ closure t, from mem_of_mem_of_subset this (by apply closure_mono; apply subset_Union (Î»(n:â„•), T (n:â„))),
    end,
    exact âŸ¨t, âŸ¨Tâ‚, subset.antisymm Tâ‚‚ Tâ‚ƒâŸ©âŸ©
  end,
  haveI : separable_space Î± := âŸ¨by_cases A BâŸ©,
  apply emetric.second_countable_of_separable,
end

end proper_space

namespace metric
section second_countable
open topological_space

/-- A metric space is second countable if, for every Îµ > 0, there is a countable set which is Îµ-dense. -/
lemma second_countable_of_almost_dense_set
  (H : âˆ€Îµ > (0 : â„), âˆƒ s : set Î±, countable s âˆ§ (âˆ€x, âˆƒy âˆˆ s, dist x y â‰¤ Îµ)) :
  second_countable_topology Î± :=
begin
  choose T T_dense using H,
  have I1 : âˆ€n:â„•, (n:â„) + 1 > 0 :=
    Î»n, lt_of_lt_of_le zero_lt_one (le_add_of_nonneg_left (nat.cast_nonneg _)),
  have I : âˆ€n:â„•, (n+1 : â„)â»Â¹ > 0 := Î»n, inv_pos'.2 (I1 n),
  let t := â‹ƒn:â„•, T (n+1)â»Â¹ (I n),
  have count_t : countable t := by finish [countable_Union],
  have clos_t : closure t = univ,
  { refine subset.antisymm (subset_univ _) (Î»x xuniv, mem_closure_iff'.2 (Î»Îµ Îµpos, _)),
    rcases exists_nat_gt Îµâ»Â¹ with âŸ¨n, hnâŸ©,
    have : Îµâ»Â¹ < n + 1 := lt_of_lt_of_le hn (le_add_of_nonneg_right zero_le_one),
    have nÎµ : ((n:â„)+1)â»Â¹ < Îµ := (inv_lt (I1 n) Îµpos).2 this,
    rcases (T_dense (n+1)â»Â¹ (I n)).2 x with âŸ¨y, yT, DxyâŸ©,
    have : y âˆˆ t := mem_of_mem_of_subset yT (by apply subset_Union (Î» (n:â„•), T (n+1)â»Â¹ (I n))),
    exact âŸ¨y, this, lt_of_le_of_lt Dxy nÎµâŸ© },
  haveI : separable_space Î± := âŸ¨âŸ¨t, âŸ¨count_t, clos_tâŸ©âŸ©âŸ©,
  exact emetric.second_countable_of_separable Î±
end

/-- A metric space space is second countable if one can reconstruct up to any Îµ>0 any element of the
space from countably many data. -/
lemma second_countable_of_countable_discretization {Î± : Type u} [metric_space Î±]
  (H : âˆ€Îµ > (0 : â„), âˆƒ (Î² : Type u) [encodable Î²] (F : Î± â†’ Î²), âˆ€x y, F x = F y â†’ dist x y â‰¤ Îµ) :
  second_countable_topology Î± :=
begin
  classical, by_cases hs : (univ : set Î±) = âˆ…,
  { haveI : compact_space Î± := âŸ¨by rw hs; exact compact_of_finite (set.finite_empty)âŸ©, by apply_instance },
  rcases exists_mem_of_ne_empty hs with âŸ¨x0, hx0âŸ©,
  letI : inhabited Î± := âŸ¨x0âŸ©,
  refine second_countable_of_almost_dense_set (Î»Îµ Îµ0, _),
  rcases H Îµ Îµ0 with âŸ¨Î², fÎ², F, hFâŸ©,
  let Finv := function.inv_fun F,
  refine âŸ¨range Finv, âŸ¨countable_range _, Î»x, _âŸ©âŸ©,
  let x' := Finv (F x),
  have : F x' = F x := function.inv_fun_eq âŸ¨x, rflâŸ©,
  exact âŸ¨x', mem_range_self _, hF _ _ this.symmâŸ©
end

end second_countable
end metric

lemma lebesgue_number_lemma_of_metric
  {s : set Î±} {Î¹} {c : Î¹ â†’ set Î±} (hs : compact s)
  (hcâ‚ : âˆ€ i, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) :
  âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ i, ball x Î´ âŠ† c i :=
let âŸ¨n, en, hnâŸ© := lebesgue_number_lemma hs hcâ‚ hcâ‚‚,
    âŸ¨Î´, Î´0, hÎ´âŸ© := mem_uniformity_dist.1 en in
âŸ¨Î´, Î´0, assume x hx, let âŸ¨i, hiâŸ© := hn x hx in
 âŸ¨i, assume y hy, hi (hÎ´ (mem_ball'.mp hy))âŸ©âŸ©

lemma lebesgue_number_lemma_of_metric_sUnion
  {s : set Î±} {c : set (set Î±)} (hs : compact s)
  (hcâ‚ : âˆ€ t âˆˆ c, is_open t) (hcâ‚‚ : s âŠ† â‹ƒâ‚€ c) :
  âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ c, ball x Î´ âŠ† t :=
by rw sUnion_eq_Union at hcâ‚‚;
   simpa using lebesgue_number_lemma_of_metric hs (by simpa) hcâ‚‚

namespace metric

/-- Boundedness of a subset of a metric space. We formulate the definition to work
even in the empty space. -/
def bounded (s : set Î±) : Prop :=
âˆƒC, âˆ€x y âˆˆ s, dist x y â‰¤ C

section bounded
variables {x : Î±} {s t : set Î±} {r : â„}

@[simp] lemma bounded_empty : bounded (âˆ… : set Î±) :=
âŸ¨0, by simpâŸ©

lemma bounded_iff_mem_bounded : bounded s â†” âˆ€ x âˆˆ s, bounded s :=
âŸ¨Î» h _ _, h, Î» H, begin
  classical, by_cases s = âˆ…,
  { subst s, exact âŸ¨0, by simpâŸ© },
  { rcases exists_mem_of_ne_empty h with âŸ¨x, hxâŸ©,
    exact H x hx }
endâŸ©

/-- Subsets of a bounded set are also bounded -/
lemma bounded.subset (incl : s âŠ† t) : bounded t â†’ bounded s :=
Exists.imp $ Î» C hC x y hx hy, hC x y (incl hx) (incl hy)

/-- Closed balls are bounded -/
lemma bounded_closed_ball : bounded (closed_ball x r) :=
âŸ¨r + r, Î» y z hy hz, begin
  simp only [mem_closed_ball] at *,
  calc dist y z â‰¤ dist y x + dist z x : dist_triangle_right _ _ _
            ... â‰¤ r + r : add_le_add hy hz
endâŸ©

/-- Open balls are bounded -/
lemma bounded_ball : bounded (ball x r) :=
bounded_closed_ball.subset ball_subset_closed_ball

/-- Given a point, a bounded subset is included in some ball around this point -/
lemma bounded_iff_subset_ball (c : Î±) : bounded s â†” âˆƒr, s âŠ† closed_ball c r :=
begin
  split; rintro âŸ¨C, hCâŸ©,
  { classical, by_cases s = âˆ…,
    { subst s, exact âŸ¨0, by simpâŸ© },
    { rcases exists_mem_of_ne_empty h with âŸ¨x, hxâŸ©,
      exact âŸ¨C + dist x c, Î» y hy, calc
        dist y c â‰¤ dist y x + dist x c : dist_triangle _ _ _
            ... â‰¤ C + dist x c : add_le_add_right (hC y x hy hx) _âŸ© } },
  { exact bounded_closed_ball.subset hC }
end

/-- The union of two bounded sets is bounded iff each of the sets is bounded -/
@[simp] lemma bounded_union :
  bounded (s âˆª t) â†” bounded s âˆ§ bounded t :=
âŸ¨Î»h, âŸ¨h.subset (by simp), h.subset (by simp)âŸ©,
begin
  rintro âŸ¨hs, htâŸ©,
  refine bounded_iff_mem_bounded.2 (Î» x _, _),
  rw bounded_iff_subset_ball x at hs ht âŠ¢,
  rcases hs with âŸ¨Cs, hCsâŸ©, rcases ht with âŸ¨Ct, hCtâŸ©,
  exact âŸ¨max Cs Ct, union_subset
    (subset.trans hCs $ closed_ball_subset_closed_ball $ le_max_left _ _)
    (subset.trans hCt $ closed_ball_subset_closed_ball $ le_max_right _ _)âŸ©,
endâŸ©

/-- A finite union of bounded sets is bounded -/
lemma bounded_bUnion {I : set Î²} {s : Î² â†’ set Î±} (H : finite I) :
  bounded (â‹ƒiâˆˆI, s i) â†” âˆ€i âˆˆ I, bounded (s i) :=
finite.induction_on H (by simp) $ Î» x I _ _ IH,
by simp [or_imp_distrib, forall_and_distrib, IH]

/-- A compact set is bounded -/
lemma bounded_of_compact {s : set Î±} (h : compact s) : bounded s :=
-- We cover the compact set by finitely many balls of radius 1,
-- and then argue that a finite union of bounded sets is bounded
let âŸ¨t, ht, fint, subsâŸ© := finite_cover_balls_of_compact h zero_lt_one in
bounded.subset subs $ (bounded_bUnion fint).2 $ Î» i hi, bounded_ball

/-- A finite set is bounded -/
lemma bounded_of_finite {s : set Î±} (h : finite s) : bounded s :=
bounded_of_compact $ compact_of_finite h

/-- A singleton is bounded -/
lemma bounded_singleton {x : Î±} : bounded ({x} : set Î±) :=
bounded_of_finite $ finite_singleton _

/-- Characterization of the boundedness of the range of a function -/
lemma bounded_range_iff {f : Î² â†’ Î±} : bounded (range f) â†” âˆƒC, âˆ€x y, dist (f x) (f y) â‰¤ C :=
exists_congr $ Î» C, âŸ¨
  Î» H x y, H _ _ âŸ¨x, rflâŸ© âŸ¨y, rflâŸ©,
  by rintro H _ _ âŸ¨x, rflâŸ© âŸ¨y, rflâŸ©; exact H x yâŸ©

/-- In a compact space, all sets are bounded -/
lemma bounded_of_compact_space [compact_space Î±] : bounded s :=
(bounded_of_compact compact_univ).subset (subset_univ _)

/-- In a proper space, a set is compact if and only if it is closed and bounded -/
lemma compact_iff_closed_bounded [proper_space Î±] :
  compact s â†” is_closed s âˆ§ bounded s :=
âŸ¨Î» h, âŸ¨closed_of_compact _ h, bounded_of_compact hâŸ©, begin
  rintro âŸ¨hc, hbâŸ©,
  classical, by_cases s = âˆ…, {simp [h, compact_empty]},
  rcases exists_mem_of_ne_empty h with âŸ¨x, hxâŸ©,
  rcases (bounded_iff_subset_ball x).1 hb with âŸ¨r, hrâŸ©,
  exact compact_of_is_closed_subset (proper_space.compact_ball x r) hc hr
endâŸ©

end bounded

section diam
variables {s : set Î±} {x y : Î±}

/-- The diameter of a set in a metric space. To get controllable behavior even when the diameter
should be infinite, we express it in terms of the emetric.diameter -/
def diam (s : set Î±) : â„ := ennreal.to_real (emetric.diam s)

/-- The diameter of a set is always nonnegative -/
lemma diam_nonneg : 0 â‰¤ diam s :=
by simp [diam]

/-- The empty set has zero diameter -/
@[simp] lemma diam_empty : diam (âˆ… : set Î±) = 0 :=
by simp [diam]

/-- A singleton has zero diameter -/
@[simp] lemma diam_singleton : diam ({x} : set Î±) = 0 :=
by simp [diam]

/-- Characterize the boundedness of a set in terms of the finiteness of its emetric.diameter. -/
lemma bounded_iff_diam_ne_top : bounded s â†” emetric.diam s â‰  âŠ¤ :=
begin
  classical, by_cases hs : s = âˆ…,
  { simp [hs] },
  { rcases ne_empty_iff_exists_mem.1 hs with âŸ¨x, hxâŸ©,
    split,
    { assume bs,
      rcases (bounded_iff_subset_ball x).1 bs with âŸ¨r, hrâŸ©,
      have r0 : 0 â‰¤ r := by simpa [closed_ball] using hr hx,
      have : emetric.diam s < âŠ¤ := calc
        emetric.diam s â‰¤ emetric.diam (emetric.closed_ball x (ennreal.of_real r)) :
          by rw emetric_closed_ball r0; exact emetric.diam_mono hr
        ... â‰¤ 2 * (ennreal.of_real r) : emetric.diam_closed_ball
        ... < âŠ¤ : begin apply ennreal.lt_top_iff_ne_top.2, simp [ennreal.mul_eq_top], end,
      exact ennreal.lt_top_iff_ne_top.1 this },
    { assume ds,
      have : s âŠ† closed_ball x (ennreal.to_real (emetric.diam s)),
      { rw [â† emetric_closed_ball ennreal.to_real_nonneg, ennreal.of_real_to_real ds],
        exact Î»y hy, emetric.edist_le_diam_of_mem hy hx },
      exact bounded.subset this (bounded_closed_ball) }}
end

/-- An unbounded set has zero diameter. If you would prefer to get the value âˆ, use `emetric.diam`.
This lemma makes it possible to avoid side conditions in some situations -/
lemma diam_eq_zero_of_unbounded (h : Â¬(bounded s)) : diam s = 0 :=
begin
  simp only [bounded_iff_diam_ne_top, not_not, ne.def] at h,
  simp [diam, h]
end

/-- If `s âŠ† t`, then the diameter of `s` is bounded by that of `t`, provided `t` is bounded. -/
lemma diam_mono {s t : set Î±} (h : s âŠ† t) (ht : bounded t) : diam s â‰¤ diam t :=
begin
  unfold diam,
  rw ennreal.to_real_le_to_real (bounded_iff_diam_ne_top.1 (bounded.subset h ht)) (bounded_iff_diam_ne_top.1 ht),
  exact emetric.diam_mono h
end

/-- The distance between two points in a set is controlled by the diameter of the set. -/
lemma dist_le_diam_of_mem (h : bounded s) (hx : x âˆˆ s) (hy : y âˆˆ s) : dist x y â‰¤ diam s :=
begin
  rw [diam, dist_edist],
  rw ennreal.to_real_le_to_real (edist_ne_top _ _) (bounded_iff_diam_ne_top.1 h),
  exact emetric.edist_le_diam_of_mem hx hy
end

/-- If the distance between any two points in a set is bounded by some constant, this constant
bounds the diameter. -/
lemma diam_le_of_forall_dist_le {d : real} (hd : d â‰¥ 0) (h : âˆ€x y âˆˆ s, dist x y â‰¤ d) : diam s â‰¤ d :=
begin
  have I : emetric.diam s â‰¤ ennreal.of_real d,
  { refine emetric.diam_le_of_forall_edist_le (Î»x y hx hy, _),
    rw [edist_dist],
    exact ennreal.of_real_le_of_real (h x y hx hy) },
  have A : emetric.diam s â‰  âŠ¤ :=
    ennreal.lt_top_iff_ne_top.1 (lt_of_le_of_lt I (ennreal.lt_top_iff_ne_top.2 (by simp))),
  rw [â† ennreal.to_real_of_real hd, diam, ennreal.to_real_le_to_real A],
  { exact I },
  { simp }
end

/-- The diameter of a union is controlled by the sum of the diameters, and the distance between
any two points in each of the sets. This lemma is true without any side condition, since it is
obviously true if `s âˆª t` is unbounded. -/
lemma diam_union {t : set Î±} (xs : x âˆˆ s) (yt : y âˆˆ t) : diam (s âˆª t) â‰¤ diam s + dist x y + diam t :=
have I1 : Â¬(bounded (s âˆª t)) â†’ diam (s âˆª t) â‰¤ diam s + dist x y + diam t := Î»h, calc
  diam (s âˆª t) = 0 + 0 + 0 : by simp [diam_eq_zero_of_unbounded h]
  ... â‰¤ diam s + dist x y + diam t : add_le_add (add_le_add diam_nonneg dist_nonneg) diam_nonneg,
have I2 : (bounded (s âˆª t)) â†’ diam (s âˆª t) â‰¤ diam s + dist x y + diam t := Î»h,
begin
  have : bounded s := bounded.subset (subset_union_left _ _) h,
  have : bounded t := bounded.subset (subset_union_right _ _) h,
  have A : âˆ€a âˆˆ s, âˆ€b âˆˆ t, dist a b â‰¤ diam s + dist x y + diam t := Î»a ha b hb, calc
    dist a b â‰¤ dist a x + dist x y + dist y b : dist_triangle4 _ _ _ _
    ... â‰¤ diam s + dist x y + diam t :
      add_le_add (add_le_add (dist_le_diam_of_mem â€¹bounded sâ€º ha xs) (le_refl _)) (dist_le_diam_of_mem â€¹bounded tâ€º yt hb),
  have B : âˆ€a b âˆˆ s âˆª t, dist a b â‰¤ diam s + dist x y + diam t := Î»a b ha hb,
  begin
    cases (mem_union _ _ _).1 ha with h'a h'a; cases (mem_union _ _ _).1 hb with h'b h'b,
    { calc dist a b â‰¤ diam s : dist_le_diam_of_mem â€¹bounded sâ€º h'a h'b
           ... = diam s + (0 + 0) : by simp
           ... â‰¤ diam s + (dist x y + diam t) : add_le_add (le_refl _) (add_le_add dist_nonneg diam_nonneg)
           ... = diam s + dist x y + diam t : by simp only [add_comm, eq_self_iff_true, add_left_comm] },
    { exact A a h'a b h'b },
    { have Z := A b h'b a h'a, rwa [dist_comm] at Z },
    { calc dist a b â‰¤ diam t : dist_le_diam_of_mem â€¹bounded tâ€º h'a h'b
           ... = (0 + 0) + diam t : by simp
           ... â‰¤ (diam s + dist x y) + diam t : add_le_add (add_le_add diam_nonneg dist_nonneg) (le_refl _) }
  end,
  have C : 0 â‰¤ diam s + dist x y + diam t := calc
    0 = 0 + 0 + 0 : by simp
    ... â‰¤ diam s + dist x y + diam t : add_le_add (add_le_add diam_nonneg dist_nonneg) diam_nonneg,
  exact diam_le_of_forall_dist_le C B
end,
classical.by_cases I2 I1

/-- If two sets intersect, the diameter of the union is bounded by the sum of the diameters. -/
lemma diam_union' {t : set Î±} (h : s âˆ© t â‰  âˆ…) : diam (s âˆª t) â‰¤ diam s + diam t :=
begin
  rcases ne_empty_iff_exists_mem.1 h with âŸ¨x, âŸ¨xs, xtâŸ©âŸ©,
  simpa using diam_union xs xt
end

/-- The diameter of a closed ball of radius `r` is at most `2 r`. -/
lemma diam_closed_ball {r : â„} (h : r â‰¥ 0) : diam (closed_ball x r) â‰¤ 2 * r :=
diam_le_of_forall_dist_le (mul_nonneg (by norm_num) h) $ Î»a b ha hb, calc
  dist a b â‰¤ dist a x + dist b x : dist_triangle_right _ _ _
  ... â‰¤ r + r : add_le_add ha hb
  ... = 2 * r : by simp [mul_two, mul_comm]

/-- The diameter of a ball of radius `r` is at most `2 r`. -/
lemma diam_ball {r : â„} (h : r â‰¥ 0) : diam (ball x r) â‰¤ 2 * r :=
le_trans (diam_mono ball_subset_closed_ball bounded_closed_ball) (diam_closed_ball h)

end diam

end metric
