/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.uniform_space.completion
import topology.metric_space.isometry
import topology.instances.real

/-!
# The completion of a metric space

Completion of uniform spaces are already defined in `topology.uniform_space.completion`. We show
here that the uniform space completion of a metric space inherits a metric space structure,
by extending the distance to the completion and checking that it is indeed a distance, and that
it defines the same uniformity as the already defined uniform structure on the completion
-/

open set filter uniform_space uniform_space.completion
open_locale filter
noncomputable theory

universes u
variables {Î± : Type u} [pseudo_metric_space Î±]

namespace metric

/-- The distance on the completion is obtained by extending the distance on the original space,
by uniform continuity. -/
instance : has_dist (completion Î±) :=
âŸ¨completion.extensionâ‚‚ distâŸ©

/-- The new distance is uniformly continuous. -/
protected lemma completion.uniform_continuous_dist :
  uniform_continuous (Î»p:completion Î± Ã— completion Î±, dist p.1 p.2) :=
uniform_continuous_extensionâ‚‚ dist

/-- The new distance is an extension of the original distance. -/
protected lemma completion.dist_eq (x y : Î±) : dist (x : completion Î±) y = dist x y :=
completion.extensionâ‚‚_coe_coe uniform_continuous_dist _ _

/- Let us check that the new distance satisfies the axioms of a distance, by starting from the
properties on Î± and extending them to `completion Î±` by continuity. -/
protected lemma completion.dist_self (x : completion Î±) : dist x x = 0 :=
begin
  apply induction_on x,
  { refine is_closed_eq _ continuous_const,
    exact (completion.uniform_continuous_dist.continuous.comp
             (continuous.prod_mk continuous_id continuous_id : _) : _) },
  { assume a,
    rw [completion.dist_eq, dist_self] }
end

protected lemma completion.dist_comm (x y : completion Î±) : dist x y = dist y x :=
begin
  apply induction_onâ‚‚ x y,
  { refine is_closed_eq completion.uniform_continuous_dist.continuous _,
    exact completion.uniform_continuous_dist.continuous.comp
      (@continuous_swap (completion Î±) (completion Î±) _ _) },
  { assume a b,
    rw [completion.dist_eq, completion.dist_eq, dist_comm] }
end

protected lemma completion.dist_triangle (x y z : completion Î±) : dist x z â‰¤ dist x y + dist y z :=
begin
  apply induction_onâ‚ƒ x y z,
  { refine is_closed_le _ (continuous.add _ _),
    { have : continuous (Î»p : completion Î± Ã— completion Î± Ã— completion Î±, (p.1, p.2.2)) :=
        continuous.prod_mk continuous_fst (continuous.comp continuous_snd continuous_snd),
      exact (completion.uniform_continuous_dist.continuous.comp this : _) },
    { have : continuous (Î»p : completion Î± Ã— completion Î± Ã— completion Î±, (p.1, p.2.1)) :=
        continuous.prod_mk continuous_fst (continuous_fst.comp continuous_snd),
      exact (completion.uniform_continuous_dist.continuous.comp this : _) },
    { have : continuous (Î»p : completion Î± Ã— completion Î± Ã— completion Î±, (p.2.1, p.2.2)) :=
        continuous.prod_mk (continuous_fst.comp continuous_snd)
                           (continuous.comp continuous_snd continuous_snd),
      exact (continuous.comp completion.uniform_continuous_dist.continuous this : _) } },
  { assume a b c,
    rw [completion.dist_eq, completion.dist_eq, completion.dist_eq],
    exact dist_triangle a b c }
end

/-- Elements of the uniformity (defined generally for completions) can be characterized in terms
of the distance. -/
protected lemma completion.mem_uniformity_dist (s : set (completion Î± Ã— completion Î±)) :
  s âˆˆ uniformity (completion Î±) â†” (âˆƒÎµ>0, âˆ€{a b}, dist a b < Îµ â†’ (a, b) âˆˆ s) :=
begin
  split,
  { /- Start from an entourage `s`. It contains a closed entourage `t`. Its pullback in Î± is an
    entourage, so it contains an Îµ-neighborhood of the diagonal by definition of the entourages
    in metric spaces. Then `t` contains an Îµ-neighborhood of the diagonal in `completion Î±`, as
    closed properties pass to the completion. -/
    assume hs,
    rcases mem_uniformity_is_closed hs with âŸ¨t, ht, âŸ¨tclosed, tsâŸ©âŸ©,
    have A : {x : Î± Ã— Î± | (coe (x.1), coe (x.2)) âˆˆ t} âˆˆ uniformity Î± :=
      uniform_continuous_def.1 (uniform_continuous_coe Î±) t ht,
    rcases mem_uniformity_dist.1 A with âŸ¨Îµ, Îµpos, hÎµâŸ©,
    refine âŸ¨Îµ, Îµpos, Î»x y hxy, _âŸ©,
    have : Îµ â‰¤ dist x y âˆ¨ (x, y) âˆˆ t,
    { apply induction_onâ‚‚ x y,
      { have : {x : completion Î± Ã— completion Î± | Îµ â‰¤ dist (x.fst) (x.snd) âˆ¨ (x.fst, x.snd) âˆˆ t}
               = {p : completion Î± Ã— completion Î± | Îµ â‰¤ dist p.1 p.2} âˆª t, by ext; simp,
        rw this,
        apply is_closed.union _ tclosed,
        exact is_closed_le continuous_const completion.uniform_continuous_dist.continuous },
      { assume x y,
        rw completion.dist_eq,
        by_cases h : Îµ â‰¤ dist x y,
        { exact or.inl h },
        { have Z := hÎµ (not_le.1 h),
          simp only [set.mem_set_of_eq] at Z,
          exact or.inr Z }}},
    simp only [not_le.mpr hxy, false_or, not_le] at this,
    exact ts this },
  { /- Start from a set `s` containing an Îµ-neighborhood of the diagonal in `completion Î±`. To show
    that it is an entourage, we use the fact that `dist` is uniformly continuous on
    `completion Î± Ã— completion Î±` (this is a general property of the extension of uniformly
    continuous functions). Therefore, the preimage of the Îµ-neighborhood of the diagonal in â„
    is an entourage in `completion Î± Ã— completion Î±`. Massaging this property, it follows that
    the Îµ-neighborhood of the diagonal is an entourage in `completion Î±`, and therefore this is
    also the case of `s`. -/
    rintros âŸ¨Îµ, Îµpos, hÎµâŸ©,
    let r : set (â„ Ã— â„) := {p | dist p.1 p.2 < Îµ},
    have : r âˆˆ uniformity â„ := metric.dist_mem_uniformity Îµpos,
    have T := uniform_continuous_def.1 (@completion.uniform_continuous_dist Î± _) r this,
    simp only [uniformity_prod_eq_prod, mem_prod_iff, exists_prop,
               filter.mem_map, set.mem_set_of_eq] at T,
    rcases T with âŸ¨t1, ht1, t2, ht2, htâŸ©,
    refine mem_of_superset ht1 _,
    have A : âˆ€a b : completion Î±, (a, b) âˆˆ t1 â†’ dist a b < Îµ,
    { assume a b hab,
      have : ((a, b), (a, a)) âˆˆ set.prod t1 t2 := âŸ¨hab, refl_mem_uniformity ht2âŸ©,
      have I := ht this,
      simp [completion.dist_self, real.dist_eq, completion.dist_comm] at I,
      exact lt_of_le_of_lt (le_abs_self _) I },
    show t1 âŠ† s,
    { rintros âŸ¨a, bâŸ© hp,
      have : dist a b < Îµ := A a b hp,
      exact hÎµ this }}
end

/-- If two points are at distance 0, then they coincide. -/
protected lemma completion.eq_of_dist_eq_zero (x y : completion Î±) (h : dist x y = 0) : x = y :=
begin
  /- This follows from the separation of `completion Î±` and from the description of
  entourages in terms of the distance. -/
  have : separated_space (completion Î±) := by apply_instance,
  refine separated_def.1 this x y (Î»s hs, _),
  rcases (completion.mem_uniformity_dist s).1 hs with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  rw â† h at Îµpos,
  exact hÎµ Îµpos
end

/-- Reformulate `completion.mem_uniformity_dist` in terms that are suitable for the definition
of the metric space structure. -/
protected lemma completion.uniformity_dist' :
  uniformity (completion Î±) = (â¨…Îµ:{Îµ : â„ // 0 < Îµ}, ğ“Ÿ {p | dist p.1 p.2 < Îµ.val}) :=
begin
  ext s, rw mem_infi_of_directed,
  { simp [completion.mem_uniformity_dist, subset_def] },
  { rintro âŸ¨r, hrâŸ© âŸ¨p, hpâŸ©, use âŸ¨min r p, lt_min hr hpâŸ©,
    simp [lt_min_iff, (â‰¥)] {contextual := tt} }
end

protected lemma completion.uniformity_dist :
  uniformity (completion Î±) = (â¨… Îµ>0, ğ“Ÿ {p | dist p.1 p.2 < Îµ}) :=
by simpa [infi_subtype] using @completion.uniformity_dist' Î± _

/-- Metric space structure on the completion of a pseudo_metric space. -/
instance completion.metric_space : metric_space (completion Î±) :=
{ dist_self          := completion.dist_self,
  eq_of_dist_eq_zero := completion.eq_of_dist_eq_zero,
  dist_comm          := completion.dist_comm,
  dist_triangle      := completion.dist_triangle,
  to_uniform_space   := by apply_instance,
  uniformity_dist    := completion.uniformity_dist }

/-- The embedding of a metric space in its completion is an isometry. -/
lemma completion.coe_isometry : isometry (coe : Î± â†’ completion Î±) :=
isometry_emetric_iff_metric.2 completion.dist_eq

end metric
