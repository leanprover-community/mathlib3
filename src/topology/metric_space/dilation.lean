/-
Copyright (c) 2022 Hanting Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Dilations of emetric and metric spaces
Authors: Hanting Zhang
-/
import topology.metric_space.antilipschitz
import data.fun_like.basic

/-!
# Dilations
We define dilations, i.e., maps between emetric spaces that
satisfy `edist (f x) (f y) = r * edist x y`.
Defines `ratio f`, which is the ratio of some `f : dilation Œ± Œ≤`.
Note that `ratio f : ‚Ñù‚â•0`, so we do not exclude the degenerate case of dilations
which collapse into constant maps. Statements that do need strict dilations should
say `f : dilation Œ± Œ≤` and `hr : ratio f ‚â† 0`.
TODO: Introduce dilation equivs. Refactor the isometry API
to match the `*_hom_class` API below.
Since a lot of elementary properties don't require `eq_of_dist_eq_zero`
we start setting up the theory for `pseudo_emetric_space` and
we specialize to `pseudo_metric_space` and `metric_space` when needed.
# Notes
The type of dilations defined in this file are also referred to as
"similiarties" or "similitudes" by other authors. The name `dilation` was choosen
to match the Wikipedia name.
# References
- https://en.wikipedia.org/wiki/Dilation_(metric_space)
- Marcel Berger, Geometry
-/

noncomputable theory

open function set
open_locale topological_space ennreal nnreal classical

section defs

variables (Œ± : Type*) (Œ≤ : Type*) [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤]

/-- A dilation is a map that uniformly scales the edistance between any two points.  -/
structure dilation :=
(to_fun : Œ± ‚Üí Œ≤)
(edist_eq' : ‚àÉ r : ‚Ñù‚â•0, r ‚â† 0 ‚àß ‚àÄ x y : Œ±, edist (to_fun x) (to_fun y) = r * edist x y)

attribute [nolint has_inhabited_instance] dilation

/--
`dilation_class F Œ± Œ≤ r` states that `F` is a type of `r`-dilations.
You should extend this typeclass when you extend `dilation`.
-/
class dilation_class (F : Type*) (Œ± Œ≤ : out_param $ Type*)
  [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] extends fun_like F Œ± (Œª _, Œ≤) :=
(edist_eq' : ‚àÄ (f : F), ‚àÉ r : ‚Ñù‚â•0, r ‚â† 0 ‚àß ‚àÄ (x y : Œ±), edist (f x) (f y) = r * edist x y)

attribute [nolint dangerous_instance] dilation_class.to_fun_like

end defs

namespace dilation
variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {F : Type*} {G : Type*}

section setup
variables [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤]

instance to_dilation_class :
  dilation_class (dilation Œ± Œ≤) Œ± Œ≤ :=
{ coe := to_fun,
  coe_injective' := Œª f g h, by { cases f; cases g; congr', },
  edist_eq' := Œª f, edist_eq' f }

instance : has_coe_to_fun (dilation Œ± Œ≤) (Œª _, Œ± ‚Üí Œ≤) := fun_like.has_coe_to_fun

@[simp] lemma to_fun_eq_coe {f : dilation Œ± Œ≤} : f.to_fun = (f : Œ± ‚Üí Œ≤) := rfl

@[simp] lemma coe_mk (f : Œ± ‚Üí Œ≤) (h) : ‚áë(‚ü®f, h‚ü© : dilation Œ± Œ≤) = f := rfl

lemma congr_fun {f g : dilation Œ± Œ≤} (h : f = g) (x : Œ±) : f x = g x := fun_like.congr_fun h x
lemma congr_arg (f : dilation Œ± Œ≤) {x y : Œ±} (h : x = y) : f x = f y := fun_like.congr_arg f h

@[ext] theorem ext {f g : dilation Œ± Œ≤} (h : ‚àÄ x, f x = g x) : f = g :=
fun_like.ext f g h

lemma ext_iff {f g : dilation Œ± Œ≤} : f = g ‚Üî ‚àÄ x, f x = g x := fun_like.ext_iff

@[simp] lemma mk_coe (f : dilation Œ± Œ≤) (h) : dilation.mk f h = f := ext $ Œª _, rfl

/-- Copy of a `dilation` with a new `to_fun` equal to the old one. Useful to fix definitional
equalities. -/
protected def copy (f : dilation Œ± Œ≤) (f' : Œ± ‚Üí Œ≤) (h : f' = ‚áëf) : dilation Œ± Œ≤ :=
{ to_fun := f',
  edist_eq'' := h.symm ‚ñ∏ f.edist_eq'' }

/-- The ratio of a dilation `f`. If the ratio is undefined (i.e., the distance between any two
points in `Œ±` is either zero or infinity), then we choose one as the ratio. -/
def ratio [dilation_class F Œ± Œ≤] (f : F) : ‚Ñù‚â•0 :=
if ‚àÄ x y : Œ±, edist x y = 0 ‚à® edist x y = ‚ä§ then 1 else (dilation_class.edist_eq' f).some

lemma ratio_ne_zero [dilation_class F Œ± Œ≤] (f : F) : ratio f ‚â† 0 :=
begin
  rw ratio, split_ifs,
  { exact one_ne_zero, },
  exact (dilation_class.edist_eq' f).some_spec.1,
end

lemma ratio_pos [dilation_class F Œ± Œ≤] (f : F) : 0 < ratio f :=
(ratio_ne_zero f).bot_lt

@[simp] lemma edist_eq [dilation_class F Œ± Œ≤] (f : F) (x y : Œ±) :
  edist (f x) (f y) = ratio f * edist x y :=
begin
  rw ratio, split_ifs with key,
  { rcases dilation_class.edist_eq' f with ‚ü®r, hne, hr‚ü©,
    replace hr := hr x y,
    cases key x y,
    { simp only [hr, h, mul_zero] },
    { simp [hr, h, hne] } },
  exact (dilation_class.edist_eq' f).some_spec.2 x y,
end

@[simp] lemma nndist_eq {Œ± Œ≤} {F : Type*}
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) (x y : Œ±) : nndist (f x) (f y) = ratio f * nndist x y :=
by simp only [‚Üê ennreal.coe_eq_coe, ‚Üê edist_nndist, ennreal.coe_mul, edist_eq]

@[simp] lemma dist_eq {Œ± Œ≤} {F : Type*}
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) (x y : Œ±) : dist (f x) (f y) = ratio f * dist x y :=
by simp only [dist_nndist, nndist_eq, nnreal.coe_mul]

/-- The `ratio` is equal to the distance ratio for any two points with nonzero finite distance.
`dist` and `nndist` versions below -/
lemma ratio_unique [dilation_class F Œ± Œ≤] {f : F} {x y : Œ±} {r : ‚Ñù‚â•0}
  (h‚ÇÄ : edist x y ‚â† 0) (htop : edist x y ‚â† ‚ä§) (hr : edist (f x) (f y) = r * edist x y) :
  r = ratio f :=
begin
  have h := edist_eq f x y,
  rwa [hr, ennreal.mul_eq_mul_right hxy.1 hxy.2, ennreal.coe_eq_coe] at h,
end

/-- The `ratio` is equal to the distance ratio for any two points
with nonzero finite distance; `nndist version` -/
lemma ratio_unique_of_nndist_ne_zero
  {Œ± Œ≤} {F : Type*} [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤]
  [dilation_class F Œ± Œ≤] {f : F} {x y : Œ±} {r : ‚Ñù‚â•0}
  (hxy : nndist x y ‚â† 0) (hr : nndist (f x) (f y) = r * nndist x y) :
  r = ratio f :=
ratio_unique (by rwa [edist_nndist, ennreal.coe_ne_zero]) (edist_ne_top x y)
  (by rw [edist_nndist, edist_nndist, hr, ennreal.coe_mul])

/-- The `ratio` is equal to the distance ratio for any two points
with nonzero finite distance; `dist version` -/
lemma ratio_unique_of_dist_ne_zero {Œ± Œ≤} {F : Type*} [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤]
  [dilation_class F Œ± Œ≤] {f : F} {x y : Œ±} {r : ‚Ñù‚â•0}
  (hxy : dist x y ‚â† 0) (hr : dist (f x) (f y) = r * dist x y) :
  r = ratio f :=
ratio_unique_of_nndist_ne_zero (nnreal.coe_ne_zero.1 hxy) $ nnreal.eq $
  by rw [coe_nndist, hr, nnreal.coe_mul, coe_nndist]

/-- Alternative `dilation` constructor when the distance hypothesis is over `nndist` -/
def mk_of_nndist_eq {Œ± Œ≤}
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤]
  (f : Œ± ‚Üí Œ≤) (h : ‚àÉ (r : ‚Ñù‚â•0), r ‚â† 0 ‚àß ‚àÄ (x y : Œ±), nndist (f x) (f y) = r * nndist x y) :
  dilation Œ± Œ≤ :=
{ to_fun := f,
  edist_eq' :=
  begin
    rcases h with ‚ü®r, hne, h‚ü©,
    refine ‚ü®r, hne, Œª x y, _‚ü©,
    rw [edist_nndist, edist_nndist, ‚Üê ennreal.coe_mul, h x y],
  end }

@[simp] lemma coe_mk_of_nndist_eq {Œ± Œ≤}
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤]
  (f : Œ± ‚Üí Œ≤) (h) : ‚áë(mk_of_nndist_eq f h : dilation Œ± Œ≤) = f := rfl

@[simp] lemma mk_coe_of_nndist_eq {Œ± Œ≤}
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤]
  (f : dilation Œ± Œ≤) (h) : dilation.mk_of_nndist_eq f h = f :=
ext $ Œª _, rfl

/-- Alternative `dilation` constructor when the distance hypothesis is over `dist` -/
def mk_of_dist_eq {Œ± Œ≤}
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤]
  (f : Œ± ‚Üí Œ≤) (h : ‚àÉ (r : ‚Ñù‚â•0), r ‚â† 0 ‚àß ‚àÄ (x y : Œ±), dist (f x) (f y) = r * dist x y) :
  dilation Œ± Œ≤ :=
mk_of_nndist_eq f $ h.imp $ Œª r hr,
  ‚ü®hr.1, Œª x y, nnreal.eq $ by rw [coe_nndist, hr.2, nnreal.coe_mul, coe_nndist]‚ü©

@[simp] lemma coe_mk_of_dist_eq {Œ± Œ≤}
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤]
  (f : Œ± ‚Üí Œ≤) (h) : ‚áë(mk_of_dist_eq f h : dilation Œ± Œ≤) = f := rfl

@[simp] lemma mk_coe_of_dist_eq {Œ± Œ≤}
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤]
  (f : dilation Œ± Œ≤) (h) : dilation.mk_of_dist_eq f h = f :=
ext $ Œª _, rfl

end setup

section pseudo_emetric_dilation
variables [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] [pseudo_emetric_space Œ≥]
variables [dilation_class F Œ± Œ≤] [dilation_class G Œ≤ Œ≥]
variables (f : F) (g : G) {x y z : Œ±}  {s : set Œ±}

lemma lipschitz : lipschitz_with (ratio f) (f : Œ± ‚Üí Œ≤) :=
Œª x y, (edist_eq f x y).le

-- TODO: add `instance ennreal.div_inv_comm_monoid`
-- TODO: fix `antilipschitz_with` decl header
lemma antilipschitz : antilipschitz_with (ratio f)‚Åª¬π (f : Œ± ‚Üí Œ≤) :=
Œª x y, begin
  have hr : ratio f ‚â† 0 := ratio_ne_zero f,
  -- `div_eq_mul_inv` should be `div_eq_inv_mul`
  rw [mul_comm, ennreal.coe_inv hr, ‚Üê div_eq_mul_inv, ennreal.le_div_iff_mul_le, edist_eq],
  { exact (mul_comm _ _).le },
  all_goals { simp [hr] },
end

/-- A dilation from an emetric space is injective -/
lemma injective {Œ± : Type*} [emetric_space Œ±]  [dilation_class F Œ± Œ≤] (f : F) :
  injective f := (dilation.antilipschitz f).injective

/-- The identity is a dilation -/
def id (Œ±) [pseudo_emetric_space Œ±] : dilation Œ± Œ± :=
{ to_fun := _root_.id,
  edist_eq' := ‚ü®1, one_ne_zero, Œª x y, by simp only [id.def, ennreal.coe_one, one_mul]‚ü© }

instance : inhabited (dilation Œ± Œ±) := ‚ü®id Œ±‚ü©

@[simp] lemma id_apply (x : Œ±) : id Œ± x = x := rfl

lemma id_ratio : ratio (id Œ±) = 1 :=
begin
  by_cases h : ‚àÄ x y : Œ±, edist x y = 0 ‚à® edist x y = ‚àû,
  { rw [ratio, if_pos h] },
  { push_neg at h,
    rcases h with ‚ü®x, y, hne‚ü©,
    refine (ratio_unique hne.1 hne.2 _).symm,
    simp }
end

/-- The composition of dilations is a dilation -/
def comp (g : dilation Œ≤ Œ≥) (f : dilation Œ± Œ≤) : dilation Œ± Œ≥ :=
{ to_fun := g ‚àò f,
  edist_eq' := ‚ü®ratio g * ratio f,
    mul_ne_zero (ratio_ne_zero g) (ratio_ne_zero f),
    Œª x y, by { simp only [edist_eq, ennreal.coe_mul], ring, }‚ü© }

lemma comp_assoc {Œ¥ : Type*} [pseudo_emetric_space Œ¥]
  (f : dilation Œ± Œ≤) (g : dilation Œ≤ Œ≥) (h : dilation Œ≥ Œ¥) :
  (h.comp g).comp f = h.comp (g.comp f) := rfl

@[simp] lemma coe_comp (g : dilation Œ≤ Œ≥) (f : dilation Œ± Œ≤) :
  (g.comp f : Œ± ‚Üí Œ≥) = g ‚àò f := rfl

lemma comp_apply (g : dilation Œ≤ Œ≥) (f : dilation Œ± Œ≤) (x : Œ±) :
  (g.comp f : Œ± ‚Üí Œ≥) x = (g (f x)) := rfl

/-- Ratio of the composition `g.comp f` of two dilations is the product of their ratios. We assume
that the domain `Œ±` of `f` is nontrivial, otherwise `ratio f = ratio (g.comp f) = 1` but `ratio g`
may have any value. -/
@[simp] lemma comp_ratio
  {g : dilation Œ≤ Œ≥} {f : dilation Œ± Œ≤} (hne : ‚àÉ x y : Œ±, edist x y ‚â† 0 ‚àß edist x y ‚â† ‚ä§) :
  ratio g * ratio f = ratio (g.comp f) :=
begin
  rcases hne with ‚ü®x, y, hŒ±‚ü©,
  have hgf := edist_eq (g.comp f) x y,
  simp only [dist_eq, coe_comp, ‚Üê mul_assoc, mul_eq_mul_right_iff] at hgf,
  rw [edist_eq, edist_eq, ‚Üê mul_assoc, ennreal.mul_eq_mul_right hŒ±.1 hŒ±.2] at hgf,
  rwa [‚Üê ennreal.coe_eq_coe, ennreal.coe_mul],
end

@[simp] lemma comp_id (f : dilation Œ± Œ≤) : f.comp (id Œ±) = f := ext $ Œª x, rfl

@[simp] lemma id_comp (f : dilation Œ± Œ≤) : (id Œ≤).comp f = f := ext $ Œª x, rfl

instance : monoid (dilation Œ± Œ±) :=
{ one := id Œ±,
  mul := comp,
  mul_one := comp_id,
  one_mul := id_comp,
  mul_assoc := Œª f g h, comp_assoc _ _ _ }

lemma one_def : (1 : dilation Œ± Œ±) = id Œ± := rfl
lemma mul_def (f g : dilation Œ± Œ±) : f * g = f.comp g := rfl

@[simp] lemma coe_one : ‚áë(1 : dilation Œ± Œ±) = _root_.id := rfl
@[simp] lemma coe_mul (f g : dilation Œ± Œ±) : ‚áë(f * g) = f ‚àò g := rfl

lemma cancel_right {g‚ÇÅ g‚ÇÇ : dilation Œ≤ Œ≥} {f : dilation Œ± Œ≤} (hf : surjective f) :
  g‚ÇÅ.comp f = g‚ÇÇ.comp f ‚Üî g‚ÇÅ = g‚ÇÇ :=
‚ü®Œª h, dilation.ext $ hf.forall.2 (ext_iff.1 h), Œª h, h ‚ñ∏ rfl‚ü©

lemma cancel_left {g : dilation Œ≤ Œ≥} {f‚ÇÅ f‚ÇÇ : dilation Œ± Œ≤} (hg : function.injective g) :
  g.comp f‚ÇÅ = g.comp f‚ÇÇ ‚Üî f‚ÇÅ = f‚ÇÇ :=
‚ü®Œª h, dilation.ext $ Œª x, hg $ by rw [‚Üê comp_apply, h, comp_apply], Œª h, h ‚ñ∏ rfl‚ü©

/-- A dilation from a metric space is a uniform inducing map -/
theorem uniform_inducing : uniform_inducing (f : Œ± ‚Üí Œ≤) :=
(antilipschitz f).uniform_inducing (lipschitz f).uniform_continuous

lemma tendsto_nhds_iff {Œπ : Type*} {g : Œπ ‚Üí Œ±} {a : filter Œπ} {b : Œ±} :
  filter.tendsto g a (ùìù b) ‚Üî filter.tendsto ((f : Œ± ‚Üí Œ≤) ‚àò g) a (ùìù (f b)) :=
(uniform_inducing f).inducing.tendsto_nhds_iff

/-- A dilation is continuous. -/
lemma to_continuous : continuous (f : Œ± ‚Üí Œ≤) :=
(lipschitz f).continuous

/-- Dilations scale the diameter by `ratio f` in pseudoemetric spaces. -/
lemma ediam_image (s : set Œ±) :
  emetric.diam ((f: Œ± ‚Üí Œ≤) '' s) = ratio f * emetric.diam s :=
begin
  refine ((lipschitz f).ediam_image_le s).antisymm _,
  apply ennreal.mul_le_of_le_div',
  rw [div_eq_mul_inv, mul_comm, ‚Üê ennreal.coe_inv],
  exacts [(antilipschitz f).le_mul_ediam_image s, ratio_ne_zero f],
end

/-- A dilation scales scale the diameter of the range by `ratio f`. -/
lemma ediam_range :
  emetric.diam (range (f : Œ± ‚Üí Œ≤)) = ratio f * emetric.diam (univ : set Œ±) :=
by { rw ‚Üê image_univ, exact ediam_image f univ }

/-- A dilation maps balls to balls and scales the radius by `ratio f`. -/
lemma maps_to_emetric_ball (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
  maps_to (f : Œ± ‚Üí Œ≤) (emetric.ball x r) (emetric.ball (f x) (ratio f * r)) :=
begin
  intros y hy,
  simp only [emetric.mem_ball, edist_eq] at *,
  rwa ennreal.mul_lt_mul_left _ _; simp [ratio_ne_zero]
end

/-- A dilation maps closed balls to closed balls and scales the radius by `ratio f`. -/
lemma maps_to_emetric_closed_ball (x : Œ±) (r' : ‚Ñù‚â•0‚àû) :
  maps_to (f : Œ± ‚Üí Œ≤) (emetric.closed_ball x r') (emetric.closed_ball (f x) (ratio f * r')) :=
begin
  intros y hy,
  simp only [emetric.mem_closed_ball, edist_eq] at *,
  exact mul_le_mul_left' hy _,
end

lemma comp_continuous_on_iff
  {Œ≥} [topological_space Œ≥] {g : Œ≥ ‚Üí Œ±} {s : set Œ≥} :
  continuous_on ((f : Œ± ‚Üí Œ≤) ‚àò g) s ‚Üî continuous_on g s :=
(uniform_inducing f).inducing.continuous_on_iff.symm

lemma comp_continuous_iff
  {Œ≥} [topological_space Œ≥] {g : Œ≥ ‚Üí Œ±} :
  continuous ((f : Œ± ‚Üí Œ≤) ‚àò g) ‚Üî continuous g :=
(uniform_inducing f).inducing.continuous_iff.symm

end pseudo_emetric_dilation --section

section emetric_dilation
variables [emetric_space Œ±]

/-- A dilation from a metric space is a uniform embedding -/
theorem uniform_embedding [pseudo_emetric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) : uniform_embedding f :=
(antilipschitz f).uniform_embedding (lipschitz f).uniform_continuous

/-- A dilation from a metric space is an embedding -/
theorem embedding [pseudo_emetric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) : embedding (f : Œ± ‚Üí Œ≤) :=
(uniform_embedding f).embedding

/-- A dilation from a complete emetric space is a closed embedding -/
theorem closed_embedding
  [complete_space Œ±] [emetric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) : closed_embedding (f : Œ± ‚Üí Œ≤) :=
(antilipschitz f).closed_embedding (lipschitz f).uniform_continuous

end emetric_dilation --section

section pseudo_metric_dilation
variables [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] [dilation_class F Œ± Œ≤] (f : F)

/-- A dilation scales the diameter by `ratio f` in pseudometric spaces. -/
lemma diam_image (s : set Œ±) : metric.diam ((f : Œ± ‚Üí Œ≤) '' s) = ratio f * metric.diam s :=
by { simp [metric.diam, ediam_image, ennreal.to_real_mul], }

lemma diam_range : metric.diam (range (f : Œ± ‚Üí Œ≤)) = ratio f * metric.diam (univ : set Œ±) :=
by rw [‚Üê image_univ, diam_image]

/-- A dilation maps balls to balls and scales the radius by `ratio f`. -/
lemma maps_to_ball (x : Œ±) (r' : ‚Ñù) :
  maps_to (f : Œ± ‚Üí Œ≤) (metric.ball x r') (metric.ball (f x) (ratio f * r')) :=
begin
  intros y hy,
  rw [metric.mem_ball, dist_eq],
  exact (mul_lt_mul_left $ nnreal.coe_pos.2 $ ratio_pos f).2 hy,
end

/-- A dilation maps spheres to spheres and scales the radius by `ratio f`. -/
lemma maps_to_sphere (x : Œ±) (r' : ‚Ñù) :
  maps_to (f : Œ± ‚Üí Œ≤) (metric.sphere x r') (metric.sphere (f x) (ratio f * r')) :=
begin
  intros y hy,
  rw metric.mem_sphere at hy,
  rw [metric.mem_sphere, dist_eq, hy],
end

/-- A dilation maps closed balls to closed balls and scales the radius by `ratio f`. -/
lemma maps_to_closed_ball (x : Œ±) (r' : ‚Ñù) :
  maps_to (f : Œ± ‚Üí Œ≤) (metric.closed_ball x r') (metric.closed_ball (f x) (ratio f * r')) :=
begin
  intros y hy,
  rw [metric.mem_closed_ball] at hy,
  rw [metric.mem_closed_ball, dist_eq],
  exact mul_le_mul_of_nonneg_left hy (nnreal.coe_nonneg _)
end

end pseudo_metric_dilation -- section

end dilation
