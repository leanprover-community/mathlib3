/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/
import data.nat.interval
import data.real.ennreal
import topology.uniform_space.pi
import topology.uniform_space.uniform_convergence
import topology.uniform_space.uniform_embedding

/-!
# Extended metric spaces

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file is devoted to the definition and study of `emetric_spaces`, i.e., metric
spaces in which the distance is allowed to take the value âˆ. This extended distance is
called `edist`, and takes values in `â„â‰¥0âˆ`.

Many definitions and theorems expected on emetric spaces are already introduced on uniform spaces
and topological spaces. For example: open and closed sets, compactness, completeness, continuity and
uniform continuity.

The class `emetric_space` therefore extends `uniform_space` (and `topological_space`).

Since a lot of elementary properties don't require `eq_of_edist_eq_zero` we start setting up the
theory of `pseudo_emetric_space`, where we don't require `edist x y = 0 â†’ x = y` and we specialize
to `emetric_space` at the end.
-/

open set filter classical

open_locale uniformity topology big_operators filter nnreal ennreal

universes u v w
variables {Î± : Type u} {Î² : Type v} {X : Type*}

/-- Characterizing uniformities associated to a (generalized) distance function `D`
in terms of the elements of the uniformity. -/
theorem uniformity_dist_of_mem_uniformity [linear_order Î²] {U : filter (Î± Ã— Î±)} (z : Î²)
  (D : Î± â†’ Î± â†’ Î²) (H : âˆ€ s, s âˆˆ U â†” âˆƒÎµ>z, âˆ€{a b:Î±}, D a b < Îµ â†’ (a, b) âˆˆ s) :
  U = â¨… Îµ > z, ğ“Ÿ {p:Î±Ã—Î± | D p.1 p.2 < Îµ} :=
has_basis.eq_binfi âŸ¨Î» s, by simp only [H, subset_def, prod.forall, mem_set_of]âŸ©

/-- `has_edist Î±` means that `Î±` is equipped with an extended distance. -/
class has_edist (Î± : Type*) := (edist : Î± â†’ Î± â†’ â„â‰¥0âˆ)
export has_edist (edist)

/-- Creating a uniform space from an extended distance. -/
noncomputable def uniform_space_of_edist (edist : Î± â†’ Î± â†’ â„â‰¥0âˆ)
  (edist_self : âˆ€ x : Î±, edist x x = 0) (edist_comm : âˆ€ x y : Î±, edist x y = edist y x)
  (edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z) : uniform_space Î± :=
uniform_space.of_fun edist edist_self edist_comm edist_triangle $
  Î» Îµ Îµ0, âŸ¨Îµ / 2, ennreal.half_pos Îµ0.lt.ne', Î» _ hâ‚ _ hâ‚‚,
      (ennreal.add_lt_add hâ‚ hâ‚‚).trans_eq (ennreal.add_halves _)âŸ©

-- the uniform structure is embedded in the emetric space structure
-- to avoid instance diamond issues. See Note [forgetful inheritance].

/-- Extended (pseudo) metric spaces, with an extended distance `edist` possibly taking the
value âˆ

Each pseudo_emetric space induces a canonical `uniform_space` and hence a canonical
`topological_space`.
This is enforced in the type class definition, by extending the `uniform_space` structure. When
instantiating a `pseudo_emetric_space` structure, the uniformity fields are not necessary, they
will be filled in by default. There is a default value for the uniformity, that can be substituted
in cases of interest, for instance when instantiating a `pseudo_emetric_space` structure
on a product.

Continuity of `edist` is proved in `topology.instances.ennreal`
-/
class pseudo_emetric_space (Î± : Type u) extends has_edist Î± : Type u :=
(edist_self : âˆ€ x : Î±, edist x x = 0)
(edist_comm : âˆ€ x y : Î±, edist x y = edist y x)
(edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z)
(to_uniform_space : uniform_space Î± :=
  uniform_space_of_edist edist edist_self edist_comm edist_triangle)
(uniformity_edist : ğ“¤ Î± = â¨… Îµ>0, ğ“Ÿ {p:Î±Ã—Î± | edist p.1 p.2 < Îµ} . control_laws_tac)

attribute [priority 100, instance] pseudo_emetric_space.to_uniform_space

/- Pseudoemetric spaces are less common than metric spaces. Therefore, we work in a dedicated
namespace, while notions associated to metric spaces are mostly in the root namespace. -/
variables [pseudo_emetric_space Î±]

export pseudo_emetric_space (edist_self edist_comm edist_triangle)

attribute [simp] edist_self

/-- Triangle inequality for the extended distance -/
theorem edist_triangle_left (x y z : Î±) : edist x y â‰¤ edist z x + edist z y :=
by rw edist_comm z; apply edist_triangle

theorem edist_triangle_right (x y z : Î±) : edist x y â‰¤ edist x z + edist y z :=
by rw edist_comm y; apply edist_triangle

lemma edist_congr_right {x y z : Î±} (h : edist x y = 0) : edist x z = edist y z :=
begin
  apply le_antisymm,
  { rw [â†zero_add (edist y z), â†h],
    apply edist_triangle, },
  { rw edist_comm at h,
    rw [â†zero_add (edist x z), â†h],
    apply edist_triangle, },
end

lemma edist_congr_left {x y z : Î±} (h : edist x y = 0) : edist z x = edist z y :=
by { rw [edist_comm z x, edist_comm z y], apply edist_congr_right h,  }

lemma edist_triangle4 (x y z t : Î±) :
  edist x t â‰¤ edist x y + edist y z + edist z t :=
calc
  edist x t â‰¤ edist x z + edist z t : edist_triangle x z t
... â‰¤ (edist x y + edist y z) + edist z t : add_le_add_right (edist_triangle x y z) _

/-- The triangle (polygon) inequality for sequences of points; `finset.Ico` version. -/
lemma edist_le_Ico_sum_edist (f : â„• â†’ Î±) {m n} (h : m â‰¤ n) :
  edist (f m) (f n) â‰¤ âˆ‘ i in finset.Ico m n, edist (f i) (f (i + 1)) :=
begin
  revert n,
  refine nat.le_induction _ _,
  { simp only [finset.sum_empty, finset.Ico_self, edist_self],
    -- TODO: Why doesn't Lean close this goal automatically? `exact le_rfl` fails too.
    exact le_refl (0:â„â‰¥0âˆ) },
  { assume n hn hrec,
    calc edist (f m) (f (n+1)) â‰¤ edist (f m) (f n) + edist (f n) (f (n+1)) : edist_triangle _ _ _
      ... â‰¤ âˆ‘ i in finset.Ico m n, _ + _ : add_le_add hrec le_rfl
      ... = âˆ‘ i in finset.Ico m (n+1), _ :
        by rw [nat.Ico_succ_right_eq_insert_Ico hn, finset.sum_insert, add_comm]; simp }
end

/-- The triangle (polygon) inequality for sequences of points; `finset.range` version. -/
lemma edist_le_range_sum_edist (f : â„• â†’ Î±) (n : â„•) :
  edist (f 0) (f n) â‰¤ âˆ‘ i in finset.range n, edist (f i) (f (i + 1)) :=
nat.Ico_zero_eq_range â–¸ edist_le_Ico_sum_edist f (nat.zero_le n)

/-- A version of `edist_le_Ico_sum_edist` with each intermediate distance replaced
with an upper estimate. -/
lemma edist_le_Ico_sum_of_edist_le {f : â„• â†’ Î±} {m n} (hmn : m â‰¤ n)
  {d : â„• â†’ â„â‰¥0âˆ} (hd : âˆ€ {k}, m â‰¤ k â†’ k < n â†’ edist (f k) (f (k + 1)) â‰¤ d k) :
  edist (f m) (f n) â‰¤ âˆ‘ i in finset.Ico m n, d i :=
le_trans (edist_le_Ico_sum_edist f hmn) $
finset.sum_le_sum $ Î» k hk, hd (finset.mem_Ico.1 hk).1 (finset.mem_Ico.1 hk).2

/-- A version of `edist_le_range_sum_edist` with each intermediate distance replaced
with an upper estimate. -/
lemma edist_le_range_sum_of_edist_le {f : â„• â†’ Î±} (n : â„•)
  {d : â„• â†’ â„â‰¥0âˆ} (hd : âˆ€ {k}, k < n â†’ edist (f k) (f (k + 1)) â‰¤ d k) :
  edist (f 0) (f n) â‰¤ âˆ‘ i in finset.range n, d i :=
nat.Ico_zero_eq_range â–¸ edist_le_Ico_sum_of_edist_le (zero_le n) (Î» _ _, hd)

/-- Reformulation of the uniform structure in terms of the extended distance -/
theorem uniformity_pseudoedist :
  ğ“¤ Î± = â¨… Îµ>0, ğ“Ÿ {p:Î±Ã—Î± | edist p.1 p.2 < Îµ} :=
pseudo_emetric_space.uniformity_edist

theorem uniform_space_edist : â€¹pseudo_emetric_space Î±â€º.to_uniform_space =
  uniform_space_of_edist edist edist_self edist_comm edist_triangle :=
uniform_space_eq uniformity_pseudoedist

theorem uniformity_basis_edist :
  (ğ“¤ Î±).has_basis (Î» Îµ : â„â‰¥0âˆ, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
(@uniform_space_edist Î± _).symm â–¸ uniform_space.has_basis_of_fun âŸ¨1, one_posâŸ© _ _ _ _ _

/-- Characterization of the elements of the uniformity in terms of the extended distance -/
theorem mem_uniformity_edist {s : set (Î±Ã—Î±)} :
  s âˆˆ ğ“¤ Î± â†” (âˆƒÎµ>0, âˆ€{a b:Î±}, edist a b < Îµ â†’ (a, b) âˆˆ s) :=
uniformity_basis_edist.mem_uniformity_iff

/-- Given `f : Î² â†’ â„â‰¥0âˆ`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then `f i`-neighborhoods of the diagonal form a basis of `ğ“¤ Î±`.

For specific bases see `uniformity_basis_edist`, `uniformity_basis_edist'`,
`uniformity_basis_edist_nnreal`, and `uniformity_basis_edist_inv_nat`. -/
protected theorem emetric.mk_uniformity_basis {Î² : Type*} {p : Î² â†’ Prop} {f : Î² â†’ â„â‰¥0âˆ}
  (hfâ‚€ : âˆ€ x, p x â†’ 0 < f x) (hf : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ x (hx : p x), f x â‰¤ Îµ) :
  (ğ“¤ Î±).has_basis p (Î» x, {p:Î±Ã—Î± | edist p.1 p.2 < f x}) :=
begin
  refine âŸ¨Î» s, uniformity_basis_edist.mem_iff.trans _âŸ©,
  split,
  { rintros âŸ¨Îµ, Îµâ‚€, hÎµâŸ©,
    rcases hf Îµ Îµâ‚€ with âŸ¨i, hi, HâŸ©,
    exact âŸ¨i, hi, Î» x hx, hÎµ $ lt_of_lt_of_le hx HâŸ© },
  { exact Î» âŸ¨i, hi, HâŸ©, âŸ¨f i, hfâ‚€ i hi, HâŸ© }
end

/-- Given `f : Î² â†’ â„â‰¥0âˆ`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then closed `f i`-neighborhoods of the diagonal form a basis of `ğ“¤ Î±`.

For specific bases see `uniformity_basis_edist_le` and `uniformity_basis_edist_le'`. -/
protected theorem emetric.mk_uniformity_basis_le {Î² : Type*} {p : Î² â†’ Prop} {f : Î² â†’ â„â‰¥0âˆ}
  (hfâ‚€ : âˆ€ x, p x â†’ 0 < f x) (hf : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ x (hx : p x), f x â‰¤ Îµ) :
  (ğ“¤ Î±).has_basis p (Î» x, {p:Î±Ã—Î± | edist p.1 p.2 â‰¤ f x}) :=
begin
  refine âŸ¨Î» s, uniformity_basis_edist.mem_iff.trans _âŸ©,
  split,
  { rintros âŸ¨Îµ, Îµâ‚€, hÎµâŸ©,
    rcases exists_between Îµâ‚€ with âŸ¨Îµ', hÎµ'âŸ©,
    rcases hf Îµ' hÎµ'.1 with âŸ¨i, hi, HâŸ©,
    exact âŸ¨i, hi, Î» x hx, hÎµ $ lt_of_le_of_lt (le_trans hx H) hÎµ'.2âŸ© },
  { exact Î» âŸ¨i, hi, HâŸ©, âŸ¨f i, hfâ‚€ i hi, Î» x hx, H (le_of_lt hx)âŸ© }
end

theorem uniformity_basis_edist_le :
  (ğ“¤ Î±).has_basis (Î» Îµ : â„â‰¥0âˆ, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 â‰¤ Îµ}) :=
emetric.mk_uniformity_basis_le (Î» _, id) (Î» Îµ Îµâ‚€, âŸ¨Îµ, Îµâ‚€, le_refl ÎµâŸ©)

theorem uniformity_basis_edist' (Îµ' : â„â‰¥0âˆ) (hÎµ' : 0 < Îµ') :
  (ğ“¤ Î±).has_basis (Î» Îµ : â„â‰¥0âˆ, Îµ âˆˆ Ioo 0 Îµ') (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
emetric.mk_uniformity_basis (Î» _, and.left)
  (Î» Îµ Îµâ‚€, let âŸ¨Î´, hÎ´âŸ© := exists_between hÎµ' in
    âŸ¨min Îµ Î´, âŸ¨lt_min Îµâ‚€ hÎ´.1, lt_of_le_of_lt (min_le_right _ _) hÎ´.2âŸ©, min_le_left _ _âŸ©)

theorem uniformity_basis_edist_le' (Îµ' : â„â‰¥0âˆ) (hÎµ' : 0 < Îµ') :
  (ğ“¤ Î±).has_basis (Î» Îµ : â„â‰¥0âˆ, Îµ âˆˆ Ioo 0 Îµ') (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 â‰¤ Îµ}) :=
emetric.mk_uniformity_basis_le (Î» _, and.left)
  (Î» Îµ Îµâ‚€, let âŸ¨Î´, hÎ´âŸ© := exists_between hÎµ' in
    âŸ¨min Îµ Î´, âŸ¨lt_min Îµâ‚€ hÎ´.1, lt_of_le_of_lt (min_le_right _ _) hÎ´.2âŸ©, min_le_left _ _âŸ©)

theorem uniformity_basis_edist_nnreal :
  (ğ“¤ Î±).has_basis (Î» Îµ : â„â‰¥0, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
emetric.mk_uniformity_basis (Î» _, ennreal.coe_pos.2)
  (Î» Îµ Îµâ‚€, let âŸ¨Î´, hÎ´âŸ© := ennreal.lt_iff_exists_nnreal_btwn.1 Îµâ‚€ in
  âŸ¨Î´, ennreal.coe_pos.1 hÎ´.1, le_of_lt hÎ´.2âŸ©)

theorem uniformity_basis_edist_nnreal_le :
  (ğ“¤ Î±).has_basis (Î» Îµ : â„â‰¥0, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 â‰¤ Îµ}) :=
emetric.mk_uniformity_basis_le (Î» _, ennreal.coe_pos.2)
  (Î» Îµ Îµâ‚€, let âŸ¨Î´, hÎ´âŸ© := ennreal.lt_iff_exists_nnreal_btwn.1 Îµâ‚€ in
  âŸ¨Î´, ennreal.coe_pos.1 hÎ´.1, le_of_lt hÎ´.2âŸ©)

theorem uniformity_basis_edist_inv_nat :
  (ğ“¤ Î±).has_basis (Î» _, true) (Î» n:â„•, {p:Î±Ã—Î± | edist p.1 p.2 < (â†‘n)â»Â¹}) :=
emetric.mk_uniformity_basis
  (Î» n _, ennreal.inv_pos.2 $ ennreal.nat_ne_top n)
  (Î» Îµ Îµâ‚€, let âŸ¨n, hnâŸ© := ennreal.exists_inv_nat_lt (ne_of_gt Îµâ‚€) in âŸ¨n, trivial, le_of_lt hnâŸ©)

theorem uniformity_basis_edist_inv_two_pow :
  (ğ“¤ Î±).has_basis (Î» _, true) (Î» n:â„•, {p:Î±Ã—Î± | edist p.1 p.2 < 2â»Â¹ ^ n}) :=
emetric.mk_uniformity_basis
  (Î» n _, ennreal.pow_pos (ennreal.inv_pos.2 ennreal.two_ne_top) _)
  (Î» Îµ Îµâ‚€, let âŸ¨n, hnâŸ© := ennreal.exists_inv_two_pow_lt (ne_of_gt Îµâ‚€) in âŸ¨n, trivial, le_of_lt hnâŸ©)

/-- Fixed size neighborhoods of the diagonal belong to the uniform structure -/
theorem edist_mem_uniformity {Îµ:â„â‰¥0âˆ} (Îµ0 : 0 < Îµ) :
  {p:Î±Ã—Î± | edist p.1 p.2 < Îµ} âˆˆ ğ“¤ Î± :=
mem_uniformity_edist.2 âŸ¨Îµ, Îµ0, Î» a b, idâŸ©

namespace emetric

@[priority 900]
instance : is_countably_generated (ğ“¤ Î±) :=
is_countably_generated_of_seq âŸ¨_, uniformity_basis_edist_inv_nat.eq_infiâŸ©

/-- Îµ-Î´ characterization of uniform continuity on a set for pseudoemetric spaces -/
theorem uniform_continuous_on_iff [pseudo_emetric_space Î²] {f : Î± â†’ Î²} {s : set Î±} :
  uniform_continuous_on f s â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0,
    âˆ€ {a b âˆˆ s}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ :=
uniformity_basis_edist.uniform_continuous_on_iff uniformity_basis_edist

/-- Îµ-Î´ characterization of uniform continuity on pseudoemetric spaces -/
theorem uniform_continuous_iff [pseudo_emetric_space Î²] {f : Î± â†’ Î²} :
  uniform_continuous f â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0,
    âˆ€{a b:Î±}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ :=
uniformity_basis_edist.uniform_continuous_iff uniformity_basis_edist

/-- Îµ-Î´ characterization of uniform embeddings on pseudoemetric spaces -/
theorem uniform_embedding_iff [pseudo_emetric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†” function.injective f âˆ§ uniform_continuous f âˆ§
    âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´ :=
begin
  simp only [uniformity_basis_edist.uniform_embedding_iff uniformity_basis_edist, exists_prop],
  refl
end

/-- If a map between pseudoemetric spaces is a uniform embedding then the edistance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y`. -/
theorem controlled_of_uniform_embedding [pseudo_emetric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†’
  (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î±}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ) âˆ§
  (âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´) :=
Î» h, âŸ¨uniform_continuous_iff.1 (uniform_embedding_iff.1 h).2.1, (uniform_embedding_iff.1 h).2.2âŸ©

/-- Îµ-Î´ characterization of Cauchy sequences on pseudoemetric spaces -/
protected lemma cauchy_iff {f : filter Î±} :
  cauchy f â†” f â‰  âŠ¥ âˆ§ âˆ€ Îµ > 0, âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, edist x y < Îµ :=
by rw â† ne_bot_iff; exact uniformity_basis_edist.cauchy_iff

/-- A very useful criterion to show that a space is complete is to show that all sequences
which satisfy a bound of the form `edist (u n) (u m) < B N` for all `n m â‰¥ N` are
converging. This is often applied for `B N = 2^{-N}`, i.e., with a very fast convergence to
`0`, which makes it possible to use arguments of converging series, while this is impossible
to do in general for arbitrary Cauchy sequences. -/
theorem complete_of_convergent_controlled_sequences (B : â„• â†’ â„â‰¥0âˆ) (hB : âˆ€n, 0 < B n)
  (H : âˆ€u : â„• â†’ Î±, (âˆ€N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ edist (u n) (u m) < B N) â†’
    âˆƒx, tendsto u at_top (ğ“ x)) :
  complete_space Î± :=
uniform_space.complete_of_convergent_controlled_sequences
  (Î» n, {p:Î±Ã—Î± | edist p.1 p.2 < B n}) (Î» n, edist_mem_uniformity $ hB n) H

/-- A sequentially complete pseudoemetric space is complete. -/
theorem complete_of_cauchy_seq_tendsto :
  (âˆ€ u : â„• â†’ Î±, cauchy_seq u â†’ âˆƒa, tendsto u at_top (ğ“ a)) â†’ complete_space Î± :=
uniform_space.complete_of_cauchy_seq_tendsto

/-- Expressing locally uniform convergence on a set using `edist`. -/
lemma tendsto_locally_uniformly_on_iff {Î¹ : Type*} [topological_space Î²]
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} {s : set Î²} :
  tendsto_locally_uniformly_on F f p s â†”
  âˆ€ Îµ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, edist (f y) (F n y) < Îµ :=
begin
  refine âŸ¨Î» H Îµ hÎµ, H _ (edist_mem_uniformity hÎµ), Î» H u hu x hx, _âŸ©,
  rcases mem_uniformity_edist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  rcases H Îµ Îµpos x hx with âŸ¨t, ht, HtâŸ©,
  exact âŸ¨t, ht, Ht.mono (Î» n hs x hx, hÎµ (hs x hx))âŸ©
end

/-- Expressing uniform convergence on a set using `edist`. -/
lemma tendsto_uniformly_on_iff {Î¹ : Type*}
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} {s : set Î²} :
  tendsto_uniformly_on F f p s â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x âˆˆ s, edist (f x) (F n x) < Îµ :=
begin
  refine âŸ¨Î» H Îµ hÎµ, H _ (edist_mem_uniformity hÎµ), Î» H u hu, _âŸ©,
  rcases mem_uniformity_edist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  exact (H Îµ Îµpos).mono (Î» n hs x hx, hÎµ (hs x hx))
end

/-- Expressing locally uniform convergence using `edist`. -/
lemma tendsto_locally_uniformly_iff {Î¹ : Type*} [topological_space Î²]
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} :
  tendsto_locally_uniformly F f p â†”
  âˆ€ Îµ > 0, âˆ€ (x : Î²), âˆƒ t âˆˆ ğ“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, edist (f y) (F n y) < Îµ :=
by simp only [â† tendsto_locally_uniformly_on_univ, tendsto_locally_uniformly_on_iff,
  mem_univ, forall_const, exists_prop, nhds_within_univ]

/-- Expressing uniform convergence using `edist`. -/
lemma tendsto_uniformly_iff {Î¹ : Type*}
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} :
  tendsto_uniformly F f p â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x, edist (f x) (F n x) < Îµ :=
by simp only [â† tendsto_uniformly_on_univ, tendsto_uniformly_on_iff, mem_univ, forall_const]

end emetric

open emetric

/-- Auxiliary function to replace the uniformity on a pseudoemetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct a pseudoemetric space with a
specified uniformity. See Note [forgetful inheritance] explaining why having definitionally
the right uniformity is often important.
-/
def pseudo_emetric_space.replace_uniformity {Î±} [U : uniform_space Î±] (m : pseudo_emetric_space Î±)
  (H : ğ“¤[U] = ğ“¤[pseudo_emetric_space.to_uniform_space]) :
  pseudo_emetric_space Î± :=
{ edist               := @edist _ m.to_has_edist,
  edist_self          := edist_self,
  edist_comm          := edist_comm,
  edist_triangle      := edist_triangle,
  to_uniform_space    := U,
  uniformity_edist    := H.trans (@pseudo_emetric_space.uniformity_edist Î± _) }

/-- The extended pseudometric induced by a function taking values in a pseudoemetric space. -/
def pseudo_emetric_space.induced {Î± Î²} (f : Î± â†’ Î²)
  (m : pseudo_emetric_space Î²) : pseudo_emetric_space Î± :=
{ edist               := Î» x y, edist (f x) (f y),
  edist_self          := Î» x, edist_self _,
  edist_comm          := Î» x y, edist_comm _ _,
  edist_triangle      := Î» x y z, edist_triangle _ _ _,
  to_uniform_space    := uniform_space.comap f m.to_uniform_space,
  uniformity_edist    := (uniformity_basis_edist.comap _).eq_binfi }

/-- Pseudoemetric space instance on subsets of pseudoemetric spaces -/
instance {Î± : Type*} {p : Î± â†’ Prop} [pseudo_emetric_space Î±] : pseudo_emetric_space (subtype p) :=
pseudo_emetric_space.induced coe â€¹_â€º

/-- The extended psuedodistance on a subset of a pseudoemetric space is the restriction of
the original pseudodistance, by definition -/
theorem subtype.edist_eq {p : Î± â†’ Prop} (x y : subtype p) : edist x y = edist (x : Î±) y := rfl

namespace mul_opposite

/-- Pseudoemetric space instance on the multiplicative opposite of a pseudoemetric space. -/
@[to_additive "Pseudoemetric space instance on the additive opposite of a pseudoemetric space."]
instance {Î± : Type*} [pseudo_emetric_space Î±] : pseudo_emetric_space Î±áµáµ’áµ– :=
pseudo_emetric_space.induced unop â€¹_â€º

@[to_additive] theorem edist_unop (x y : Î±áµáµ’áµ–) : edist (unop x) (unop y) = edist x y := rfl
@[to_additive] theorem edist_op (x y : Î±) : edist (op x) (op y) = edist x y := rfl

end mul_opposite

section ulift

instance : pseudo_emetric_space (ulift Î±) :=
pseudo_emetric_space.induced ulift.down â€¹_â€º

lemma ulift.edist_eq (x y : ulift Î±) : edist x y = edist x.down y.down := rfl
@[simp] lemma ulift.edist_up_up (x y : Î±) : edist (ulift.up x) (ulift.up y) = edist x y := rfl

end ulift

/-- The product of two pseudoemetric spaces, with the max distance, is an extended
pseudometric spaces. We make sure that the uniform structure thus constructed is the one
corresponding to the product of uniform spaces, to avoid diamond problems. -/
instance prod.pseudo_emetric_space_max [pseudo_emetric_space Î²] : pseudo_emetric_space (Î± Ã— Î²) :=
{ edist := Î» x y, edist x.1 y.1 âŠ” edist x.2 y.2,
  edist_self := Î» x, by simp,
  edist_comm := Î» x y, by simp [edist_comm],
  edist_triangle := Î» x y z, max_le
    (le_trans (edist_triangle _ _ _) (add_le_add (le_max_left _ _) (le_max_left _ _)))
    (le_trans (edist_triangle _ _ _) (add_le_add (le_max_right _ _) (le_max_right _ _))),
  uniformity_edist := begin
    refine uniformity_prod.trans _,
    simp only [pseudo_emetric_space.uniformity_edist, comap_infi],
    rw â† infi_inf_eq, congr, funext,
    rw â† infi_inf_eq, congr, funext,
    simp [inf_principal, ext_iff, max_lt_iff]
  end,
  to_uniform_space := prod.uniform_space }

lemma prod.edist_eq [pseudo_emetric_space Î²] (x y : Î± Ã— Î²) :
  edist x y = max (edist x.1 y.1) (edist x.2 y.2) :=
rfl

section pi
open finset
variables {Ï€ : Î² â†’ Type*} [fintype Î²]

/-- The product of a finite number of pseudoemetric spaces, with the max distance, is still
a pseudoemetric space.
This construction would also work for infinite products, but it would not give rise
to the product topology. Hence, we only formalize it in the good situation of finitely many
spaces. -/
instance pseudo_emetric_space_pi [âˆ€b, pseudo_emetric_space (Ï€ b)] :
  pseudo_emetric_space (Î b, Ï€ b) :=
{ edist := Î» f g, finset.sup univ (Î»b, edist (f b) (g b)),
  edist_self := assume f, bot_unique $ finset.sup_le $ by simp,
  edist_comm := assume f g, by unfold edist; congr; funext a; exact edist_comm _ _,
  edist_triangle := assume f g h,
    begin
      simp only [finset.sup_le_iff],
      assume b hb,
      exact le_trans (edist_triangle _ (g b) _) (add_le_add (le_sup hb) (le_sup hb))
    end,
  to_uniform_space := Pi.uniform_space _,
  uniformity_edist := begin
    simp only [Pi.uniformity, pseudo_emetric_space.uniformity_edist, comap_infi, gt_iff_lt,
      preimage_set_of_eq, comap_principal],
    rw infi_comm, congr, funext Îµ,
    rw infi_comm, congr, funext Îµpos,
    change 0 < Îµ at Îµpos,
    simp [set.ext_iff, Îµpos]
  end }

lemma edist_pi_def [Î  b, pseudo_emetric_space (Ï€ b)] (f g : Î  b, Ï€ b) :
  edist f g = finset.sup univ (Î»b, edist (f b) (g b)) := rfl

lemma edist_le_pi_edist [Î  b, pseudo_emetric_space (Ï€ b)] (f g : Î  b, Ï€ b) (b : Î²) :
  edist (f b) (g b) â‰¤ edist f g :=
finset.le_sup (finset.mem_univ b)

lemma edist_pi_le_iff [Î  b, pseudo_emetric_space (Ï€ b)] {f g : Î  b, Ï€ b} {d : â„â‰¥0âˆ} :
  edist f g â‰¤ d â†” âˆ€ b, edist (f b) (g b) â‰¤ d :=
finset.sup_le_iff.trans $ by simp only [finset.mem_univ, forall_const]

lemma edist_pi_const_le (a b : Î±) : edist (Î» _ : Î², a) (Î» _, b) â‰¤ edist a b :=
edist_pi_le_iff.2 $ Î» _, le_rfl

@[simp] lemma edist_pi_const [nonempty Î²] (a b : Î±) : edist (Î» x : Î², a) (Î» _, b) = edist a b :=
finset.sup_const univ_nonempty (edist a b)

end pi

namespace emetric
variables {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : â„â‰¥0âˆ} {s t : set Î±}

/-- `emetric.ball x Îµ` is the set of all points `y` with `edist y x < Îµ` -/
def ball (x : Î±) (Îµ : â„â‰¥0âˆ) : set Î± := {y | edist y x < Îµ}

@[simp] theorem mem_ball : y âˆˆ ball x Îµ â†” edist y x < Îµ := iff.rfl

theorem mem_ball' : y âˆˆ ball x Îµ â†” edist x y < Îµ :=
by rw [edist_comm, mem_ball]

/-- `emetric.closed_ball x Îµ` is the set of all points `y` with `edist y x â‰¤ Îµ` -/
def closed_ball (x : Î±) (Îµ : â„â‰¥0âˆ) := {y | edist y x â‰¤ Îµ}

@[simp] theorem mem_closed_ball : y âˆˆ closed_ball x Îµ â†” edist y x â‰¤ Îµ := iff.rfl

theorem mem_closed_ball' : y âˆˆ closed_ball x Îµ â†” edist x y â‰¤ Îµ :=
by rw [edist_comm, mem_closed_ball]

@[simp] theorem closed_ball_top (x : Î±) : closed_ball x âˆ = univ :=
eq_univ_of_forall $ Î» y, le_top

theorem ball_subset_closed_ball : ball x Îµ âŠ† closed_ball x Îµ :=
assume y hy, le_of_lt hy

theorem pos_of_mem_ball (hy : y âˆˆ ball x Îµ) : 0 < Îµ :=
lt_of_le_of_lt (zero_le _) hy

theorem mem_ball_self (h : 0 < Îµ) : x âˆˆ ball x Îµ :=
show edist x x < Îµ, by rw edist_self; assumption

theorem mem_closed_ball_self : x âˆˆ closed_ball x Îµ :=
show edist x x â‰¤ Îµ, by rw edist_self; exact bot_le

theorem mem_ball_comm : x âˆˆ ball y Îµ â†” y âˆˆ ball x Îµ :=
by rw [mem_ball', mem_ball]

theorem mem_closed_ball_comm : x âˆˆ closed_ball y Îµ â†” y âˆˆ closed_ball x Îµ :=
by rw [mem_closed_ball', mem_closed_ball]

theorem ball_subset_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) : ball x Îµâ‚ âŠ† ball x Îµâ‚‚ :=
Î» y (yx : _ < Îµâ‚), lt_of_lt_of_le yx h

theorem closed_ball_subset_closed_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) :
  closed_ball x Îµâ‚ âŠ† closed_ball x Îµâ‚‚ :=
Î» y (yx : _ â‰¤ Îµâ‚), le_trans yx h

theorem ball_disjoint (h : Îµâ‚ + Îµâ‚‚ â‰¤ edist x y) : disjoint (ball x Îµâ‚) (ball y Îµâ‚‚) :=
set.disjoint_left.mpr $ Î» z hâ‚ hâ‚‚,
  (edist_triangle_left x y z).not_lt $ (ennreal.add_lt_add hâ‚ hâ‚‚).trans_le h

theorem ball_subset (h : edist x y + Îµâ‚ â‰¤ Îµâ‚‚) (h' : edist x y â‰  âˆ) : ball x Îµâ‚ âŠ† ball y Îµâ‚‚ :=
Î» z zx, calc
  edist z y â‰¤ edist z x + edist x y : edist_triangle _ _ _
  ... = edist x y + edist z x : add_comm _ _
  ... < edist x y + Îµâ‚ : ennreal.add_lt_add_left h' zx
  ... â‰¤ Îµâ‚‚ : h

theorem exists_ball_subset_ball (h : y âˆˆ ball x Îµ) : âˆƒ Îµ' > 0, ball y Îµ' âŠ† ball x Îµ :=
begin
  have : 0 < Îµ - edist y x := by simpa using h,
  refine âŸ¨Îµ - edist y x, this, ball_subset _ (ne_top_of_lt h)âŸ©,
  exact (add_tsub_cancel_of_le (mem_ball.mp h).le).le
end

theorem ball_eq_empty_iff : ball x Îµ = âˆ… â†” Îµ = 0 :=
eq_empty_iff_forall_not_mem.trans
âŸ¨Î»h, le_bot_iff.1 (le_of_not_gt (Î» Îµ0, h _ (mem_ball_self Îµ0))),
Î»Îµ0 y h, not_lt_of_le (le_of_eq Îµ0) (pos_of_mem_ball h)âŸ©

lemma ord_connected_set_of_closed_ball_subset (x : Î±) (s : set Î±) :
  ord_connected {r | closed_ball x r âŠ† s} :=
âŸ¨Î» râ‚ hrâ‚ râ‚‚ hrâ‚‚ r hr, (closed_ball_subset_closed_ball hr.2).trans hrâ‚‚âŸ©

lemma ord_connected_set_of_ball_subset (x : Î±) (s : set Î±) :
  ord_connected {r | ball x r âŠ† s} :=
âŸ¨Î» râ‚ hrâ‚ râ‚‚ hrâ‚‚ r hr, (ball_subset_ball hr.2).trans hrâ‚‚âŸ©

/-- Relation â€œtwo points are at a finite edistanceâ€ is an equivalence relation. -/
def edist_lt_top_setoid : setoid Î± :=
{ r := Î» x y, edist x y < âŠ¤,
  iseqv := âŸ¨Î» x, by { rw edist_self, exact ennreal.coe_lt_top },
    Î» x y h, by rwa edist_comm,
    Î» x y z hxy hyz, lt_of_le_of_lt (edist_triangle x y z) (ennreal.add_lt_top.2 âŸ¨hxy, hyzâŸ©)âŸ© }

@[simp] lemma ball_zero : ball x 0 = âˆ… :=
by rw [emetric.ball_eq_empty_iff]

theorem nhds_basis_eball : (ğ“ x).has_basis (Î» Îµ:â„â‰¥0âˆ, 0 < Îµ) (ball x) :=
nhds_basis_uniformity uniformity_basis_edist

lemma nhds_within_basis_eball : (ğ“[s] x).has_basis (Î» Îµ : â„â‰¥0âˆ, 0 < Îµ) (Î» Îµ, ball x Îµ âˆ© s) :=
nhds_within_has_basis nhds_basis_eball s

theorem nhds_basis_closed_eball : (ğ“ x).has_basis (Î» Îµ:â„â‰¥0âˆ, 0 < Îµ) (closed_ball x) :=
nhds_basis_uniformity uniformity_basis_edist_le

lemma nhds_within_basis_closed_eball :
  (ğ“[s] x).has_basis (Î» Îµ : â„â‰¥0âˆ, 0 < Îµ) (Î» Îµ, closed_ball x Îµ âˆ© s) :=
nhds_within_has_basis nhds_basis_closed_eball s

theorem nhds_eq : ğ“ x = (â¨…Îµ>0, ğ“Ÿ (ball x Îµ)) :=
nhds_basis_eball.eq_binfi

theorem mem_nhds_iff : s âˆˆ ğ“ x â†” âˆƒÎµ>0, ball x Îµ âŠ† s := nhds_basis_eball.mem_iff

lemma mem_nhds_within_iff : s âˆˆ ğ“[t] x â†” âˆƒ Îµ > 0, ball x Îµ âˆ© t âŠ† s :=
nhds_within_basis_eball.mem_iff

section
variables [pseudo_emetric_space Î²] {f : Î± â†’ Î²}

lemma tendsto_nhds_within_nhds_within {t : set Î²} {a b} :
  tendsto f (ğ“[s] a) (ğ“[t] b) â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ edist x a < Î´ â†’ f x âˆˆ t âˆ§ edist (f x) b < Îµ :=
(nhds_within_basis_eball.tendsto_iff nhds_within_basis_eball).trans $
  forallâ‚‚_congr $ Î» Îµ hÎµ, existsâ‚‚_congr $ Î» Î´ hÎ´,
  forall_congr $ Î» x, by simp; itauto

lemma tendsto_nhds_within_nhds {a b} :
  tendsto f (ğ“[s] a) (ğ“ b) â†”
    âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€{x:Î±}, x âˆˆ s â†’ edist x a < Î´ â†’ edist (f x) b < Îµ :=
by { rw [â† nhds_within_univ b, tendsto_nhds_within_nhds_within], simp only [mem_univ, true_and] }

lemma tendsto_nhds_nhds {a b} :
  tendsto f (ğ“ a) (ğ“ b) â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ â¦ƒxâ¦„, edist x a < Î´ â†’ edist (f x) b < Îµ :=
nhds_basis_eball.tendsto_iff nhds_basis_eball

end

theorem is_open_iff : is_open s â†” âˆ€xâˆˆs, âˆƒÎµ>0, ball x Îµ âŠ† s :=
by simp [is_open_iff_nhds, mem_nhds_iff]

theorem is_open_ball : is_open (ball x Îµ) :=
is_open_iff.2 $ Î» y, exists_ball_subset_ball

theorem is_closed_ball_top : is_closed (ball x âŠ¤) :=
is_open_compl_iff.1 $ is_open_iff.2 $ Î» y hy, âŸ¨âŠ¤, ennreal.coe_lt_top,
  (ball_disjoint $ by { rw top_add, exact le_of_not_lt hy }).subset_compl_rightâŸ©

theorem ball_mem_nhds (x : Î±) {Îµ : â„â‰¥0âˆ} (Îµ0 : 0 < Îµ) : ball x Îµ âˆˆ ğ“ x :=
is_open_ball.mem_nhds (mem_ball_self Îµ0)

theorem closed_ball_mem_nhds (x : Î±) {Îµ : â„â‰¥0âˆ} (Îµ0 : 0 < Îµ) : closed_ball x Îµ âˆˆ ğ“ x :=
mem_of_superset (ball_mem_nhds x Îµ0) ball_subset_closed_ball

theorem ball_prod_same [pseudo_emetric_space Î²] (x : Î±) (y : Î²) (r : â„â‰¥0âˆ) :
  ball x r Ã—Ë¢ ball y r = ball (x, y) r :=
ext $ Î» z, max_lt_iff.symm

theorem closed_ball_prod_same [pseudo_emetric_space Î²] (x : Î±) (y : Î²) (r : â„â‰¥0âˆ) :
  closed_ball x r Ã—Ë¢ closed_ball y r = closed_ball (x, y) r :=
ext $ Î» z, max_le_iff.symm

/-- Îµ-characterization of the closure in pseudoemetric spaces -/
theorem mem_closure_iff :
  x âˆˆ closure s â†” âˆ€Îµ>0, âˆƒy âˆˆ s, edist x y < Îµ :=
(mem_closure_iff_nhds_basis nhds_basis_eball).trans $
  by simp only [mem_ball, edist_comm x]

theorem tendsto_nhds {f : filter Î²} {u : Î² â†’ Î±} {a : Î±} :
  tendsto u f (ğ“ a) â†” âˆ€ Îµ > 0, âˆ€á¶  x in f, edist (u x) a < Îµ :=
nhds_basis_eball.tendsto_right_iff

theorem tendsto_at_top [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} {a : Î±} :
  tendsto u at_top (ğ“ a) â†” âˆ€Îµ>0, âˆƒN, âˆ€nâ‰¥N, edist (u n) a < Îµ :=
(at_top_basis.tendsto_iff nhds_basis_eball).trans $
  by simp only [exists_prop, true_and, mem_Ici, mem_ball]

theorem inseparable_iff : inseparable x y â†” edist x y = 0 :=
by simp [inseparable_iff_mem_closure, mem_closure_iff, edist_comm, forall_lt_iff_le']

/-- In a pseudoemetric space, Cauchy sequences are characterized by the fact that, eventually,
the pseudoedistance between its elements is arbitrarily small -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem cauchy_seq_iff [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>0, âˆƒN, âˆ€m nâ‰¥N, edist (u m) (u n) < Îµ :=
uniformity_basis_edist.cauchy_seq_iff

/-- A variation around the emetric characterization of Cauchy sequences -/
theorem cauchy_seq_iff' [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>(0 : â„â‰¥0âˆ), âˆƒN, âˆ€nâ‰¥N, edist (u n) (u N) < Îµ :=
uniformity_basis_edist.cauchy_seq_iff'

/-- A variation of the emetric characterization of Cauchy sequences that deals with
`â„â‰¥0` upper bounds. -/
theorem cauchy_seq_iff_nnreal [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€ Îµ : â„â‰¥0, 0 < Îµ â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ edist (u n) (u N) < Îµ :=
uniformity_basis_edist_nnreal.cauchy_seq_iff'

theorem totally_bounded_iff {s : set Î±} :
  totally_bounded s â†” âˆ€ Îµ > 0, âˆƒt : set Î±, t.finite âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
âŸ¨Î» H Îµ Îµ0, H _ (edist_mem_uniformity Îµ0),
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru,
               âŸ¨t, ft, hâŸ© := H Îµ Îµ0 in
  âŸ¨t, ft, h.trans $ Unionâ‚‚_mono $ Î» y yt z, hÎµâŸ©âŸ©

theorem totally_bounded_iff' {s : set Î±} :
  totally_bounded s â†” âˆ€ Îµ > 0, âˆƒtâŠ†s, set.finite t âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
âŸ¨Î» H Îµ Îµ0, (totally_bounded_iff_subset.1 H) _ (edist_mem_uniformity Îµ0),
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru,
               âŸ¨t, _, ft, hâŸ© := H Îµ Îµ0 in
  âŸ¨t, ft, h.trans $ Unionâ‚‚_mono $ Î» y yt z, hÎµâŸ©âŸ©

section compact

/-- For a set `s` in a pseudo emetric space, if for every `Îµ > 0` there exists a countable
set that is `Îµ`-dense in `s`, then there exists a countable subset `t âŠ† s` that is dense in `s`. -/
lemma subset_countable_closure_of_almost_dense_set (s : set Î±)
  (hs : âˆ€ Îµ > 0, âˆƒ t : set Î±, t.countable âˆ§ s âŠ† â‹ƒ x âˆˆ t, closed_ball x Îµ) :
  âˆƒ t âŠ† s, (t.countable âˆ§ s âŠ† closure t) :=
begin
  rcases s.eq_empty_or_nonempty with rfl|âŸ¨xâ‚€, hxâ‚€âŸ©,
  { exact âŸ¨âˆ…, empty_subset _, countable_empty, empty_subset _âŸ© },
  choose! T hTc hsT using (Î» n : â„•, hs nâ»Â¹ (by simp)),
  have : âˆ€ r x, âˆƒ y âˆˆ s, closed_ball x r âˆ© s âŠ† closed_ball y (r * 2),
  { intros r x,
    rcases (closed_ball x r âˆ© s).eq_empty_or_nonempty with he|âŸ¨y, hxy, hysâŸ©,
    { refine âŸ¨xâ‚€, hxâ‚€, _âŸ©, rw he, exact empty_subset _ },
    { refine âŸ¨y, hys, Î» z hz, _âŸ©,
      calc edist z y â‰¤ edist z x + edist y x : edist_triangle_right _ _ _
      ... â‰¤ r + r : add_le_add hz.1 hxy
      ... = r * 2 : (mul_two r).symm } },
  choose f hfs hf,
  refine âŸ¨â‹ƒ n : â„•, (f nâ»Â¹) '' (T n), Union_subset $ Î» n, image_subset_iff.2 (Î» z hz, hfs _ _),
    countable_Union $ Î» n, (hTc n).image _, _âŸ©,
  refine Î» x hx, mem_closure_iff.2 (Î» Îµ Îµ0, _),
  rcases ennreal.exists_inv_nat_lt (ennreal.half_pos Îµ0.lt.ne').ne' with âŸ¨n, hnâŸ©,
  rcases mem_Unionâ‚‚.1 (hsT n hx) with âŸ¨y, hyn, hyxâŸ©,
  refine âŸ¨f nâ»Â¹ y, mem_Union.2 âŸ¨n, mem_image_of_mem _ hynâŸ©, _âŸ©,
  calc edist x (f nâ»Â¹ y) â‰¤ nâ»Â¹ * 2 : hf _ _ âŸ¨hyx, hxâŸ©
                     ... < Îµ      : ennreal.mul_lt_of_lt_div hn
end

/-- A compact set in a pseudo emetric space is separable, i.e., it is a subset of the closure of a
countable set.  -/
lemma subset_countable_closure_of_compact {s : set Î±} (hs : is_compact s) :
  âˆƒ t âŠ† s, (t.countable âˆ§ s âŠ† closure t) :=
begin
  refine subset_countable_closure_of_almost_dense_set s (Î» Îµ hÎµ, _),
  rcases totally_bounded_iff'.1 hs.totally_bounded Îµ hÎµ with âŸ¨t, hts, htf, hstâŸ©,
  exact âŸ¨t, htf.countable,
    subset.trans hst $ Unionâ‚‚_mono $ Î» _ _, ball_subset_closed_ballâŸ©
end

end compact

section second_countable
open _root_.topological_space

variables (Î±)

/-- A sigma compact pseudo emetric space has second countable topology. This is not an instance
to avoid a loop with `sigma_compact_space_of_locally_compact_second_countable`.  -/
lemma second_countable_of_sigma_compact [sigma_compact_space Î±] :
  second_countable_topology Î± :=
begin

  suffices : separable_space Î±, by exactI uniform_space.second_countable_of_separable Î±,
  choose T hTsub hTc hsubT
    using Î» n, subset_countable_closure_of_compact (is_compact_compact_covering Î± n),
  refine âŸ¨âŸ¨â‹ƒ n, T n, countable_Union hTc, Î» x, _âŸ©âŸ©,
  rcases Union_eq_univ_iff.1 (Union_compact_covering Î±) x with âŸ¨n, hnâŸ©,
  exact closure_mono (subset_Union _ n) (hsubT _ hn)
end

variable {Î±}

lemma second_countable_of_almost_dense_set
  (hs : âˆ€ Îµ > 0, âˆƒ t : set Î±, t.countable âˆ§ (â‹ƒ x âˆˆ t, closed_ball x Îµ) = univ) :
  second_countable_topology Î± :=
begin
  suffices : separable_space Î±, by exactI uniform_space.second_countable_of_separable Î±,
  rcases subset_countable_closure_of_almost_dense_set (univ : set Î±) (Î» Îµ Îµ0, _)
    with âŸ¨t, -, htc, htâŸ©,
  { exact âŸ¨âŸ¨t, htc, Î» x, ht (mem_univ x)âŸ©âŸ© },
  { rcases hs Îµ Îµ0 with âŸ¨t, htc, htâŸ©,
    exact âŸ¨t, htc, univ_subset_iff.2 htâŸ© }
end

end second_countable

section diam

/-- The diameter of a set in a pseudoemetric space, named `emetric.diam` -/
noncomputable def diam (s : set Î±) := â¨† (x âˆˆ s) (y âˆˆ s), edist x y

lemma diam_le_iff {d : â„â‰¥0âˆ} :
  diam s â‰¤ d â†” âˆ€ (x âˆˆ s) (y âˆˆ s), edist x y â‰¤ d :=
by simp only [diam, supr_le_iff]

lemma diam_image_le_iff {d : â„â‰¥0âˆ} {f : Î² â†’ Î±} {s : set Î²} :
  diam (f '' s) â‰¤ d â†” âˆ€ (x âˆˆ s) (y âˆˆ s), edist (f x) (f y) â‰¤ d :=
by simp only [diam_le_iff, ball_image_iff]

lemma edist_le_of_diam_le {d} (hx : x âˆˆ s) (hy : y âˆˆ s) (hd : diam s â‰¤ d) : edist x y â‰¤ d :=
diam_le_iff.1 hd x hx y hy

/-- If two points belong to some set, their edistance is bounded by the diameter of the set -/
lemma edist_le_diam_of_mem (hx : x âˆˆ s) (hy : y âˆˆ s) : edist x y â‰¤ diam s :=
edist_le_of_diam_le hx hy le_rfl

/-- If the distance between any two points in a set is bounded by some constant, this constant
bounds the diameter. -/
lemma diam_le {d : â„â‰¥0âˆ} (h : âˆ€ (x âˆˆ s) (y âˆˆ s), edist x y â‰¤ d) : diam s â‰¤ d := diam_le_iff.2 h

/-- The diameter of a subsingleton vanishes. -/
lemma diam_subsingleton (hs : s.subsingleton) : diam s = 0 :=
nonpos_iff_eq_zero.1 $ diam_le $
Î» x hx y hy, (hs hx hy).symm â–¸ edist_self y â–¸ le_rfl

/-- The diameter of the empty set vanishes -/
@[simp] lemma diam_empty : diam (âˆ… : set Î±) = 0 :=
diam_subsingleton subsingleton_empty

/-- The diameter of a singleton vanishes -/
@[simp] lemma diam_singleton : diam ({x} : set Î±) = 0 :=
diam_subsingleton subsingleton_singleton

lemma diam_Union_mem_option {Î¹ : Type*} (o : option Î¹) (s : Î¹ â†’ set Î±) :
  diam (â‹ƒ i âˆˆ o, s i) = â¨† i âˆˆ o, diam (s i) :=
by cases o; simp

lemma diam_insert : diam (insert x s) = max (â¨† y âˆˆ s, edist x y) (diam s) :=
eq_of_forall_ge_iff $ Î» d, by simp only [diam_le_iff, ball_insert_iff,
  edist_self, edist_comm x, max_le_iff, supr_le_iff, zero_le, true_and,
  forall_and_distrib, and_self, â† and_assoc]

lemma diam_pair : diam ({x, y} : set Î±) = edist x y :=
by simp only [supr_singleton, diam_insert, diam_singleton, ennreal.max_zero_right]

lemma diam_triple :
  diam ({x, y, z} : set Î±) = max (max (edist x y) (edist x z)) (edist y z) :=
by simp only [diam_insert, supr_insert, supr_singleton, diam_singleton,
  ennreal.max_zero_right, ennreal.sup_eq_max]

/-- The diameter is monotonous with respect to inclusion -/
lemma diam_mono {s t : set Î±} (h : s âŠ† t) : diam s â‰¤ diam t :=
diam_le $ Î» x hx y hy, edist_le_diam_of_mem (h hx) (h hy)

/-- The diameter of a union is controlled by the diameter of the sets, and the edistance
between two points in the sets. -/
lemma diam_union {t : set Î±} (xs : x âˆˆ s) (yt : y âˆˆ t) :
  diam (s âˆª t) â‰¤ diam s + edist x y + diam t :=
begin
  have A : âˆ€a âˆˆ s, âˆ€b âˆˆ t, edist a b â‰¤ diam s + edist x y + diam t := Î»a ha b hb, calc
    edist a b â‰¤ edist a x + edist x y + edist y b : edist_triangle4 _ _ _ _
    ... â‰¤ diam s + edist x y + diam t :
      add_le_add (add_le_add (edist_le_diam_of_mem ha xs) le_rfl) (edist_le_diam_of_mem yt hb),
  refine diam_le (Î»a ha b hb, _),
  cases (mem_union _ _ _).1 ha with h'a h'a; cases (mem_union _ _ _).1 hb with h'b h'b,
  { calc edist a b â‰¤ diam s : edist_le_diam_of_mem h'a h'b
        ... â‰¤ diam s + (edist x y + diam t) : le_self_add
        ... = diam s + edist x y + diam t : (add_assoc _ _ _).symm },
  { exact A a h'a b h'b },
  { have Z := A b h'b a h'a, rwa [edist_comm] at Z },
  { calc edist a b â‰¤ diam t : edist_le_diam_of_mem h'a h'b
        ... â‰¤ (diam s + edist x y) + diam t : le_add_self }
end

lemma diam_union' {t : set Î±} (h : (s âˆ© t).nonempty) : diam (s âˆª t) â‰¤ diam s + diam t :=
let âŸ¨x, âŸ¨xs, xtâŸ©âŸ© := h in by simpa using diam_union xs xt

lemma diam_closed_ball {r : â„â‰¥0âˆ} : diam (closed_ball x r) â‰¤ 2 * r :=
diam_le $ Î»a ha b hb, calc
  edist a b â‰¤ edist a x + edist b x : edist_triangle_right _ _ _
  ... â‰¤ r + r : add_le_add ha hb
  ... = 2 * r : (two_mul r).symm

lemma diam_ball {r : â„â‰¥0âˆ} : diam (ball x r) â‰¤ 2 * r :=
le_trans (diam_mono ball_subset_closed_ball) diam_closed_ball

lemma diam_pi_le_of_le {Ï€ : Î² â†’ Type*} [fintype Î²] [âˆ€ b, pseudo_emetric_space (Ï€ b)]
  {s : Î  (b : Î²), set (Ï€ b)} {c : â„â‰¥0âˆ} (h : âˆ€ b, diam (s b) â‰¤ c) :
  diam (set.pi univ s) â‰¤ c :=
begin
  apply diam_le (Î» x hx y hy, edist_pi_le_iff.mpr _),
  rw [mem_univ_pi] at hx hy,
  exact Î» b, diam_le_iff.1 (h b) (x b) (hx b) (y b) (hy b),
end

end diam

end emetric --namespace

/-- We now define `emetric_space`, extending `pseudo_emetric_space`. -/

class emetric_space (Î± : Type u) extends pseudo_emetric_space Î± : Type u :=
(eq_of_edist_eq_zero : âˆ€ {x y : Î±}, edist x y = 0 â†’ x = y)

variables {Î³ : Type w} [emetric_space Î³]

export emetric_space (eq_of_edist_eq_zero)

/-- Characterize the equality of points by the vanishing of their extended distance -/
@[simp] theorem edist_eq_zero {x y : Î³} : edist x y = 0 â†” x = y :=
iff.intro eq_of_edist_eq_zero (assume : x = y, this â–¸ edist_self _)

@[simp] theorem zero_eq_edist {x y : Î³} : 0 = edist x y â†” x = y :=
iff.intro (assume h, eq_of_edist_eq_zero (h.symm))
          (assume : x = y, this â–¸ (edist_self _).symm)

theorem edist_le_zero {x y : Î³} : (edist x y â‰¤ 0) â†” x = y :=
nonpos_iff_eq_zero.trans edist_eq_zero

@[simp] theorem edist_pos {x y : Î³} : 0 < edist x y â†” x â‰  y := by simp [â† not_le]

/-- Two points coincide if their distance is `< Îµ` for all positive Îµ -/
theorem eq_of_forall_edist_le {x y : Î³} (h : âˆ€Îµ > 0, edist x y â‰¤ Îµ) : x = y :=
eq_of_edist_eq_zero (eq_of_le_of_forall_le_of_dense bot_le h)

/-- An emetric space is separated -/
@[priority 100] -- see Note [lower instance priority]
instance to_separated : separated_space Î³ :=
separated_def.2 $ Î» x y h, eq_of_forall_edist_le $
Î» Îµ Îµ0, le_of_lt (h _ (edist_mem_uniformity Îµ0))

/-- A map between emetric spaces is a uniform embedding if and only if the edistance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y` and conversely. -/
theorem emetric.uniform_embedding_iff' [emetric_space Î²] {f : Î³ â†’ Î²} :
  uniform_embedding f â†”
    (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î³}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ) âˆ§
    (âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î³}, edist (f a) (f b) < Îµ â†’ edist a b < Î´) :=
begin
  simp only [uniform_embedding_iff_uniform_inducing,
    uniformity_basis_edist.uniform_inducing_iff uniformity_basis_edist, exists_prop],
  refl
end

/-- If a `pseudo_emetric_space` is a Tâ‚€ space, then it is an `emetric_space`. -/
def emetric_space.of_t0_pseudo_emetric_space (Î± : Type*) [pseudo_emetric_space Î±] [t0_space Î±] :
  emetric_space Î± :=
{ eq_of_edist_eq_zero := Î» x y hdist, (emetric.inseparable_iff.2 hdist).eq,
  ..â€¹pseudo_emetric_space Î±â€º }

/-- Auxiliary function to replace the uniformity on an emetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct an emetric space with a
specified uniformity. See Note [forgetful inheritance] explaining why having definitionally
the right uniformity is often important.
-/
def emetric_space.replace_uniformity {Î³} [U : uniform_space Î³] (m : emetric_space Î³)
  (H : ğ“¤[U] = ğ“¤[pseudo_emetric_space.to_uniform_space]) :
  emetric_space Î³ :=
{ edist               := @edist _ m.to_has_edist,
  edist_self          := edist_self,
  eq_of_edist_eq_zero := @eq_of_edist_eq_zero _ _,
  edist_comm          := edist_comm,
  edist_triangle      := edist_triangle,
  to_uniform_space    := U,
  uniformity_edist    := H.trans (@pseudo_emetric_space.uniformity_edist Î³ _) }

  /-- The extended metric induced by an injective function taking values in a emetric space. -/
def emetric_space.induced {Î³ Î²} (f : Î³ â†’ Î²) (hf : function.injective f)
  (m : emetric_space Î²) : emetric_space Î³ :=
{ edist               := Î» x y, edist (f x) (f y),
  edist_self          := Î» x, edist_self _,
  eq_of_edist_eq_zero := Î» x y h, hf (edist_eq_zero.1 h),
  edist_comm          := Î» x y, edist_comm _ _,
  edist_triangle      := Î» x y z, edist_triangle _ _ _,
  to_uniform_space    := uniform_space.comap f m.to_uniform_space,
  uniformity_edist    := (uniformity_basis_edist.comap _).eq_binfi }

/-- Emetric space instance on subsets of emetric spaces -/
instance {Î± : Type*} {p : Î± â†’ Prop} [emetric_space Î±] : emetric_space (subtype p) :=
emetric_space.induced coe subtype.coe_injective â€¹_â€º

/-- Emetric space instance on the multiplicative opposite of an emetric space. -/
@[to_additive "Emetric space instance on the additive opposite of an emetric space."]
instance {Î± : Type*} [emetric_space Î±] : emetric_space Î±áµáµ’áµ– :=
emetric_space.induced mul_opposite.unop mul_opposite.unop_injective â€¹_â€º

instance {Î± : Type*} [emetric_space Î±] : emetric_space (ulift Î±) :=
emetric_space.induced ulift.down ulift.down_injective â€¹_â€º

/-- The product of two emetric spaces, with the max distance, is an extended
metric spaces. We make sure that the uniform structure thus constructed is the one
corresponding to the product of uniform spaces, to avoid diamond problems. -/
instance prod.emetric_space_max [emetric_space Î²] : emetric_space (Î³ Ã— Î²) :=
{ eq_of_edist_eq_zero := Î» x y h, begin
    cases max_le_iff.1 (le_of_eq h) with hâ‚ hâ‚‚,
    have A : x.fst = y.fst := edist_le_zero.1 hâ‚,
    have B : x.snd = y.snd := edist_le_zero.1 hâ‚‚,
    exact prod.ext_iff.2 âŸ¨A, BâŸ©
  end,
  ..prod.pseudo_emetric_space_max }

/-- Reformulation of the uniform structure in terms of the extended distance -/
theorem uniformity_edist :
  ğ“¤ Î³ = â¨… Îµ>0, ğ“Ÿ {p:Î³Ã—Î³ | edist p.1 p.2 < Îµ} :=
pseudo_emetric_space.uniformity_edist

section pi
open finset
variables {Ï€ : Î² â†’ Type*} [fintype Î²]

/-- The product of a finite number of emetric spaces, with the max distance, is still
an emetric space.
This construction would also work for infinite products, but it would not give rise
to the product topology. Hence, we only formalize it in the good situation of finitely many
spaces. -/
instance emetric_space_pi [âˆ€b, emetric_space (Ï€ b)] : emetric_space (Î b, Ï€ b) :=
{ eq_of_edist_eq_zero := assume f g eq0,
  begin
    have eq1 : sup univ (Î» (b : Î²), edist (f b) (g b)) â‰¤ 0 := le_of_eq eq0,
    simp only [finset.sup_le_iff] at eq1,
    exact (funext $ assume b, edist_le_zero.1 $ eq1 b $ mem_univ b),
  end,
  ..pseudo_emetric_space_pi }

end pi

namespace emetric

/-- A compact set in an emetric space is separable, i.e., it is the closure of a countable set. -/
lemma countable_closure_of_compact {s : set Î³} (hs : is_compact s) :
  âˆƒ t âŠ† s, (t.countable âˆ§ s = closure t) :=
begin
  rcases subset_countable_closure_of_compact hs with âŸ¨t, hts, htc, hsubâŸ©,
  exact âŸ¨t, hts, htc, subset.antisymm hsub (closure_minimal hts hs.is_closed)âŸ©
end

section diam

variables {s : set Î³}

lemma diam_eq_zero_iff : diam s = 0 â†” s.subsingleton :=
âŸ¨Î» h x hx y hy, edist_le_zero.1 $ h â–¸ edist_le_diam_of_mem hx hy, diam_subsingletonâŸ©

lemma diam_pos_iff : 0 < diam s â†” âˆƒ (x âˆˆ s) (y âˆˆ s), x â‰  y :=
by simp only [pos_iff_ne_zero, ne.def, diam_eq_zero_iff, set.subsingleton, not_forall]

end diam

end emetric

/-!
### Separation quotient
-/

instance [pseudo_emetric_space X] : has_edist (uniform_space.separation_quotient X) :=
âŸ¨Î» x y, quotient.lift_onâ‚‚' x y edist $ Î» x y x' y' hx hy,
  calc edist x y = edist x' y : edist_congr_right $
    emetric.inseparable_iff.1 $ separation_rel_iff_inseparable.1 hx
  ... = edist x' y' : edist_congr_left $
    emetric.inseparable_iff.1 $ separation_rel_iff_inseparable.1 hyâŸ©

@[simp] theorem uniform_space.separation_quotient.edist_mk [pseudo_emetric_space X] (x y : X) :
  @edist (uniform_space.separation_quotient X) _ (quot.mk _ x) (quot.mk _ y) = edist x y :=
rfl

instance [pseudo_emetric_space X] : emetric_space (uniform_space.separation_quotient X) :=
@emetric_space.of_t0_pseudo_emetric_space (uniform_space.separation_quotient X)
  { edist_self := Î» x, quotient.induction_on' x edist_self,
    edist_comm := Î» x y, quotient.induction_onâ‚‚' x y edist_comm,
    edist_triangle := Î» x y z, quotient.induction_onâ‚ƒ' x y z edist_triangle,
    to_uniform_space := infer_instance,
    uniformity_edist := (uniformity_basis_edist.map _).eq_binfi.trans $ infi_congr $ Î» Îµ,
      infi_congr $ Î» hÎµ, congr_arg ğ“Ÿ
      begin
        ext âŸ¨âŸ¨xâŸ©, âŸ¨yâŸ©âŸ©,
        refine âŸ¨_, Î» h, âŸ¨(x, y), h, rflâŸ©âŸ©,
        rintro âŸ¨âŸ¨x', y'âŸ©, h', hâŸ©,
        simp only [prod.ext_iff] at h,
        rwa [â† h.1, â† h.2]
      end } _

/-!
### `additive`, `multiplicative`

The distance on those type synonyms is inherited without change.
-/

open additive multiplicative

section
variables [has_edist X]

instance : has_edist (additive X) := â€¹has_edist Xâ€º
instance : has_edist (multiplicative X) := â€¹has_edist Xâ€º

@[simp] lemma edist_of_mul (a b : X) : edist (of_mul a) (of_mul b) = edist a b := rfl
@[simp] lemma edist_of_add (a b : X) : edist (of_add a) (of_add b) = edist a b := rfl
@[simp] lemma edist_to_mul (a b : additive X) : edist (to_mul a) (to_mul b) = edist a b := rfl
@[simp] lemma edist_to_add (a b : multiplicative X) : edist (to_add a) (to_add b) = edist a b := rfl

end

instance [pseudo_emetric_space X] : pseudo_emetric_space (additive X) := â€¹pseudo_emetric_space Xâ€º
instance [pseudo_emetric_space X] : pseudo_emetric_space (multiplicative X) :=
â€¹pseudo_emetric_space Xâ€º
instance [emetric_space X] : emetric_space (additive X) := â€¹emetric_space Xâ€º
instance [emetric_space X] : emetric_space (multiplicative X) := â€¹emetric_space Xâ€º

/-!
### Order dual

The distance on this type synonym is inherited without change.
-/

open order_dual

section
variables [has_edist X]

instance : has_edist Xáµ’áµˆ := â€¹has_edist Xâ€º

@[simp] lemma edist_to_dual (a b : X) : edist (to_dual a) (to_dual b) = edist a b := rfl
@[simp] lemma edist_of_dual (a b : Xáµ’áµˆ) : edist (of_dual a) (of_dual b) = edist a b := rfl

end

instance [pseudo_emetric_space X] : pseudo_emetric_space Xáµ’áµˆ := â€¹pseudo_emetric_space Xâ€º
instance [emetric_space X] : emetric_space Xáµ’áµˆ := â€¹emetric_space Xâ€º
