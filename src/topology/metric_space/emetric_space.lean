/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/

import data.real.nnreal data.real.ennreal
import topology.uniform_space.separation topology.uniform_space.uniform_embedding topology.uniform_space.pi
import topology.bases

/-!
# Extended metric spaces

This file is devoted to the definition and study of `emetric_spaces`, i.e., metric
spaces in which the distance is allowed to take the value âˆ. This extended distance is
called `edist`, and takes values in `ennreal`.

Many definitions and theorems expected on emetric spaces are already introduced on uniform spaces and
topological spaces. For example:
  open and closed sets, compactness, completeness, continuity and uniform continuity

The class `emetric_space` therefore extends `uniform_space` (and `topological_space`).
-/

open lattice set filter classical
noncomputable theory

open_locale uniformity topological_space

universes u v w
variables {Î± : Type u} {Î² : Type v} {Î³ : Type w}

/-- Characterizing uniformities associated to a (generalized) distance function `D`
in terms of the elements of the uniformity. -/
theorem uniformity_dist_of_mem_uniformity [linear_order Î²] {U : filter (Î± Ã— Î±)} (z : Î²) (D : Î± â†’ Î± â†’ Î²)
  (H : âˆ€ s, s âˆˆ U â†” âˆƒÎµ>z, âˆ€{a b:Î±}, D a b < Îµ â†’ (a, b) âˆˆ s) :
  U = â¨… Îµ>z, principal {p:Î±Ã—Î± | D p.1 p.2 < Îµ} :=
le_antisymm
  (le_infi $ Î» Îµ, le_infi $ Î» Îµ0, le_principal_iff.2 $ (H _).2 âŸ¨Îµ, Îµ0, Î» a b, idâŸ©)
  (Î» r ur, let âŸ¨Îµ, Îµ0, hâŸ© := (H _).1 ur in
    mem_infi_sets Îµ $ mem_infi_sets Îµ0 $ mem_principal_sets.2 $ Î» âŸ¨a, bâŸ©, h)

class has_edist (Î± : Type*) := (edist : Î± â†’ Î± â†’ ennreal)
export has_edist (edist)

/- Design note: one could define an `emetric_space` just by giving `edist`, and then
derive an instance of `uniform_space` by taking the natural uniform structure
associated to the distance. This creates diamonds problem for products, as the
uniform structure on the product of two emetric spaces could be obtained first
by obtaining two uniform spaces and then taking their products, or by taking
the product of the emetric spaces and then the associated uniform structure.
The two uniform structure we have just described are equal, but not defeq, which
creates a lot of problem.

The idea is to add, in the very definition of an `emetric_space`, a uniform structure
with a uniformity which equal to the one given by the distance, but maybe not defeq.
And the instance from `emetric_space` to `uniform_space` uses this uniformity.
In this way, when we create the product of emetric spaces, we put in the product
the uniformity corresponding to the product of the uniformities. There is one more
proof obligation, that this product uniformity is equal to the uniformity corresponding
to the product metric. But the diamond problem disappears.

The same trick is used in the definition of a metric space, where one stores as well
a uniform structure and an edistance. -/

/-- Creating a uniform space from an extended distance. -/
def uniform_space_of_edist
  (edist : Î± â†’ Î± â†’ ennreal)
  (edist_self : âˆ€ x : Î±, edist x x = 0)
  (edist_comm : âˆ€ x y : Î±, edist x y = edist y x)
  (edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z) : uniform_space Î± :=
uniform_space.of_core {
  uniformity := (â¨… Îµ>0, principal {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}),
  refl       := le_infi $ assume Îµ, le_infi $
    by simp [set.subset_def, id_rel, edist_self, (>)] {contextual := tt},
  comp       :=
    le_infi $ assume Îµ, le_infi $ assume h,
    have (2 : ennreal) = (2 : â„•) := by simp,
    have A : 0 < Îµ / 2 := ennreal.div_pos_iff.2 âŸ¨ne_of_gt h, this â–¸ ennreal.nat_ne_top 2âŸ©,
    lift'_le
    (mem_infi_sets (Îµ / 2) $ mem_infi_sets A (subset.refl _)) $
    have âˆ€ (a b c : Î±), edist a c < Îµ / 2 â†’ edist c b < Îµ / 2 â†’ edist a b < Îµ,
      from assume a b c hac hcb,
      calc edist a b â‰¤ edist a c + edist c b : edist_triangle _ _ _
        ... < Îµ / 2 + Îµ / 2 : ennreal.add_lt_add hac hcb
        ... = Îµ : by rw [ennreal.add_halves],
    by simpa [comp_rel],
  symm       := tendsto_infi.2 $ assume Îµ, tendsto_infi.2 $ assume h,
    tendsto_infi' Îµ $ tendsto_infi' h $ tendsto_principal_principal.2 $ by simp [edist_comm] }

section prio
set_option default_priority 100 -- see Note [default priority]

/-- Extended premetric space, with an extended distance `edist` possibly taking the
value âˆ

A premetric space is a space endowed with a "distance" function satisfying the triangular inequality,
but `dist x y = 0` does not imply x = y. -/
class epremetric_space (Î± : Type u) extends has_edist Î± : Type u :=
(edist_self : âˆ€ x : Î±, edist x x = 0)
(edist_comm : âˆ€ x y : Î±, edist x y = edist y x)
(edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z)
(to_uniform_space : uniform_space Î± := uniform_space_of_edist edist edist_self edist_comm edist_triangle)
(uniformity_edist : ğ“¤ Î± = â¨… Îµ>0, principal {p:Î±Ã—Î± | edist p.1 p.2 < Îµ} . control_laws_tac)

/-- Extended metric spaces, with an extended distance `edist` possibly taking the
value âˆ

Each emetric space induces a canonical `uniform_space` and hence a canonical `topological_space`.
This is enforced in the type class definition, by extending the `uniform_space` structure. When
instantiating an `emetric_space` structure, the uniformity fields are not necessary, they will be
filled in by default. There is a default value for the uniformity, that can be substituted
in cases of interest, for instance when instantiating an `emetric_space` structure
on a product.

Continuity of `edist` is finally proving in `topology.instances.ennreal`
-/
class emetric_space (Î± : Type u) extends epremetric_space Î± : Type u :=
(eq_of_edist_eq_zero : âˆ€ {x y : Î±}, edist x y = 0 â†’ x = y)
end prio

/- emetric spaces are less common than metric spaces. Therefore, we work in a dedicated
namespace, while notions associated to metric spaces are mostly in the root namespace. -/

export epremetric_space (edist_self edist_comm edist_triangle)
export emetric_space (eq_of_edist_eq_zero)

attribute [simp] edist_self

section emetric
variables [emetric_space Î±]

/-- Characterize the equality of points by the vanishing of their extended distance -/
@[simp] theorem edist_eq_zero {x y : Î±} : edist x y = 0 â†” x = y :=
iff.intro eq_of_edist_eq_zero (assume : x = y, this â–¸ edist_self _)

@[simp] theorem zero_eq_edist {x y : Î±} : 0 = edist x y â†” x = y :=
iff.intro (assume h, eq_of_edist_eq_zero (h.symm))
          (assume : x = y, this â–¸ (edist_self _).symm)

theorem edist_le_zero {x y : Î±} : (edist x y â‰¤ 0) â†” x = y :=
le_zero_iff_eq.trans edist_eq_zero

/-- Two points coincide if their distance is `< Îµ` for all positive Îµ -/
theorem eq_of_forall_edist_le {x y : Î±} (h : âˆ€Îµ, Îµ > 0 â†’ edist x y â‰¤ Îµ) : x = y :=
eq_of_edist_eq_zero (eq_of_le_of_forall_le_of_dense bot_le h)
end emetric

variables [epremetric_space Î±]

@[priority 100] -- see Note [lower instance priority]
instance epremetric_space.to_uniform_space' : uniform_space Î± :=
epremetric_space.to_uniform_space Î±

/-- Triangle inequality for the extended distance -/
theorem edist_triangle_left (x y z : Î±) : edist x y â‰¤ edist z x + edist z y :=
by rw edist_comm z; apply edist_triangle

theorem edist_triangle_right (x y z : Î±) : edist x y â‰¤ edist x z + edist y z :=
by rw edist_comm y; apply edist_triangle

lemma edist_triangle4 (x y z t : Î±) :
  edist x t â‰¤ edist x y + edist y z + edist z t :=
calc
  edist x t â‰¤ edist x z + edist z t : edist_triangle x z t
... â‰¤ (edist x y + edist y z) + edist z t : add_le_add_right' (edist_triangle x y z)

/-- The triangle (polygon) inequality for sequences of points; `finset.Ico` version. -/
lemma edist_le_Ico_sum_edist (f : â„• â†’ Î±) {m n} (h : m â‰¤ n) :
  edist (f m) (f n) â‰¤ (finset.Ico m n).sum (Î» i, edist (f i) (f (i + 1))) :=
begin
  revert n,
  refine nat.le_induction _ _,
  { simp only [finset.sum_empty, finset.Ico.self_eq_empty, edist_self],
    -- TODO: Why doesn't Lean close this goal automatically? `apply le_refl` fails too.
    exact le_refl (0:ennreal) },
  { assume n hn hrec,
    calc edist (f m) (f (n+1)) â‰¤ edist (f m) (f n) + edist (f n) (f (n+1)) : edist_triangle _ _ _
      ... â‰¤ (finset.Ico m n).sum _ + _ : add_le_add' hrec (le_refl _)
      ... = (finset.Ico m (n+1)).sum _ :
        by rw [finset.Ico.succ_top hn, finset.sum_insert, add_comm]; simp }
end

/-- The triangle (polygon) inequality for sequences of points; `finset.range` version. -/
lemma edist_le_range_sum_edist (f : â„• â†’ Î±) (n : â„•) :
  edist (f 0) (f n) â‰¤ (finset.range n).sum (Î» i, edist (f i) (f (i + 1))) :=
finset.Ico.zero_bot n â–¸ edist_le_Ico_sum_edist f (nat.zero_le n)

/-- A version of `edist_le_Ico_sum_edist` with each intermediate distance replaced
with an upper estimate. -/
lemma edist_le_Ico_sum_of_edist_le {f : â„• â†’ Î±} {m n} (hmn : m â‰¤ n)
  {d : â„• â†’ ennreal} (hd : âˆ€ {k}, m â‰¤ k â†’ k < n â†’ edist (f k) (f (k + 1)) â‰¤ d k) :
  edist (f m) (f n) â‰¤ (finset.Ico m n).sum d :=
le_trans (edist_le_Ico_sum_edist f hmn) $
finset.sum_le_sum $ Î» k hk, hd (finset.Ico.mem.1 hk).1 (finset.Ico.mem.1 hk).2

/-- A version of `edist_le_range_sum_edist` with each intermediate distance replaced
with an upper estimate. -/
lemma edist_le_range_sum_of_edist_le {f : â„• â†’ Î±} (n : â„•)
  {d : â„• â†’ ennreal} (hd : âˆ€ {k}, k < n â†’ edist (f k) (f (k + 1)) â‰¤ d k) :
  edist (f 0) (f n) â‰¤ (finset.range n).sum d :=
finset.Ico.zero_bot n â–¸ edist_le_Ico_sum_of_edist_le (zero_le n) (Î» _ _, hd)

/-- Reformulation of the uniform structure in terms of the extended distance -/
theorem uniformity_edist' : ğ“¤ Î± = (â¨… Îµ>0, principal {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
epremetric_space.uniformity_edist _

/-- Reformulation of the uniform structure in terms of the extended distance on a subtype -/
theorem uniformity_edist'' : ğ“¤ Î± = (â¨…Îµ:{Îµ:ennreal // Îµ>0}, principal {p:Î±Ã—Î± | edist p.1 p.2 < Îµ.val}) :=
by { simp only [infi_subtype], exact uniformity_edist' }

/-- Characterization of the elements of the uniformity in terms of the extended distance -/
theorem mem_uniformity_edist {s : set (Î±Ã—Î±)} :
  s âˆˆ ğ“¤ Î± â†” (âˆƒÎµ>0, âˆ€{a b:Î±}, edist a b < Îµ â†’ (a, b) âˆˆ s) :=
begin
  rw [uniformity_edist'', mem_infi],
  simp [subset_def],
  exact assume âŸ¨r, hrâŸ© âŸ¨p, hpâŸ©, âŸ¨âŸ¨min r p, lt_min hr hpâŸ©, by simp [lt_min_iff, (â‰¥)] {contextual := tt}âŸ©,
  exact âŸ¨âŸ¨1, ennreal.zero_lt_oneâŸ©âŸ©
end

/-- Fixed size neighborhoods of the diagonal belong to the uniform structure -/
theorem edist_mem_uniformity {Îµ:ennreal} (Îµ0 : 0 < Îµ) :
  {p:Î±Ã—Î± | edist p.1 p.2 < Îµ} âˆˆ ğ“¤ Î± :=
mem_uniformity_edist.2 âŸ¨Îµ, Îµ0, Î» a b, idâŸ©

theorem uniformity_edist_nnreal :
  ğ“¤ Î± = (â¨…(Îµ:nnreal) (h : Îµ > 0), principal {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
begin
  rw [uniformity_edist', ennreal.infi_ennreal, inf_of_le_left],
  { congr, funext Îµ, refine infi_congr_Prop ennreal.coe_pos _, assume h, refl },
  refine le_infi (assume h, infi_le_of_le 1 $ infi_le_of_le ennreal.zero_lt_one $ _),
  exact principal_mono.2 (assume p h, lt_of_lt_of_le h le_top)
end

theorem mem_uniformity_edist_inv_nat {s : set (Î±Ã—Î±)} :
  s âˆˆ ğ“¤ Î± â†” (âˆƒn:â„•, âˆ€ a b : Î±, edist a b < nâ»Â¹ â†’ (a, b) âˆˆ s) :=
begin
  refine mem_uniformity_edist.trans âŸ¨Î» hs, _, Î» hs, _âŸ©,
  { rcases hs with âŸ¨Îµ, Îµ_pos, hÎµâŸ©,
    rcases ennreal.exists_inv_nat_lt (ne_of_gt Îµ_pos) with âŸ¨n, hnâŸ©,
    exact âŸ¨n, Î» a b hab, hÎµ (lt_trans hab hn)âŸ© },
  { rcases hs with âŸ¨n, hnâŸ©,
    exact âŸ¨nâ»Â¹, ennreal.inv_pos.2 ennreal.coe_nat_ne_top, hnâŸ© }
end

theorem uniformity_edist_inv_nat :
  ğ“¤ Î± = (â¨… n:â„•, principal {p:Î±Ã—Î± | edist p.1 p.2 < nâ»Â¹}) :=
begin
  refine eq_infi_of_mem_sets_iff_exists_mem (Î» s, mem_uniformity_edist_inv_nat.trans _),
  exact exists_congr (Î»n, by simp only [prod.forall, mem_principal_sets, subset_def, mem_set_of_eq])
end

namespace emetric

theorem uniformity_has_countable_basis : has_countable_basis (ğ“¤ Î±) :=
has_countable_basis_of_seq _ _ uniformity_edist_inv_nat

/-- Îµ-Î´ characterization of uniform continuity on emetric spaces -/
theorem uniform_continuous_iff [epremetric_space Î²] {f : Î± â†’ Î²} :
  uniform_continuous f â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0,
    âˆ€{a b:Î±}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ :=
uniform_continuous_def.trans
âŸ¨Î» H Îµ Îµ0, mem_uniformity_edist.1 $ H _ $ edist_mem_uniformity Îµ0,
 Î» H r ru,
  let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru, âŸ¨Î´, Î´0, hÎ´âŸ© := H _ Îµ0 in
  mem_uniformity_edist.2 âŸ¨Î´, Î´0, Î» a b h, hÎµ (hÎ´ h)âŸ©âŸ©

/-- Îµ-Î´ characterization of uniform embeddings on emetric spaces -/
theorem uniform_embedding_iff [epremetric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†” function.injective f âˆ§ uniform_continuous f âˆ§
    âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´ :=
uniform_embedding_def'.trans $ and_congr iff.rfl $ and_congr iff.rfl
âŸ¨Î» H Î´ Î´0, let âŸ¨t, tu, htâŸ© := H _ (edist_mem_uniformity Î´0),
               âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 tu in
  âŸ¨Îµ, Îµ0, Î» a b h, ht _ _ (hÎµ h)âŸ©,
 Î» H s su, let âŸ¨Î´, Î´0, hÎ´âŸ© := mem_uniformity_edist.1 su, âŸ¨Îµ, Îµ0, hÎµâŸ© := H _ Î´0 in
  âŸ¨_, edist_mem_uniformity Îµ0, Î» a b h, hÎ´ (hÎµ h)âŸ©âŸ©

/-- A map between emetric spaces is a uniform embedding if and only if the edistance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y` and conversely. -/
theorem uniform_embedding_iff' {Î±} [emetric_space Î±] [emetric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†”
  (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î±}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ) âˆ§
  (âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´) :=
begin
  split,
  { assume h,
    exact âŸ¨uniform_continuous_iff.1 (uniform_embedding_iff.1 h).2.1,
          (uniform_embedding_iff.1 h).2.2âŸ© },
  { rintros âŸ¨hâ‚, hâ‚‚âŸ©,
    refine uniform_embedding_iff.2 âŸ¨_, uniform_continuous_iff.2 hâ‚, hâ‚‚âŸ©,
    assume x y hxy,
    have : edist x y â‰¤ 0,
    { refine le_of_forall_lt' (Î»Î´ Î´pos, _),
      rcases hâ‚‚ Î´ Î´pos with âŸ¨Îµ, Îµpos, hÎµâŸ©,
      have : edist (f x) (f y) < Îµ, by simpa [hxy],
      exact hÎµ this },
    simpa using this }
end

/-- Îµ-Î´ characterization of Cauchy sequences on emetric spaces -/
protected lemma cauchy_iff {f : filter Î±} :
  cauchy f â†” f â‰  âŠ¥ âˆ§ âˆ€ Îµ > 0, âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, edist x y < Îµ :=
cauchy_iff.trans $ and_congr iff.rfl
âŸ¨Î» H Îµ Îµ0, let âŸ¨t, tf, tsâŸ© := H _ (edist_mem_uniformity Îµ0) in
   âŸ¨t, tf, Î» x y xt yt, @ts (x, y) âŸ¨xt, ytâŸ©âŸ©,
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru,
               âŸ¨t, tf, hâŸ© := H Îµ Îµ0 in
   âŸ¨t, tf, Î» âŸ¨x, yâŸ© âŸ¨hx, hyâŸ©, hÎµ (h x y hx hy)âŸ©âŸ©

/-- A very useful criterion to show that a space is complete is to show that all sequences
which satisfy a bound of the form `edist (u n) (u m) < B N` for all `n m â‰¥ N` are
converging. This is often applied for `B N = 2^{-N}`, i.e., with a very fast convergence to
`0`, which makes it possible to use arguments of converging series, while this is impossible
to do in general for arbitrary Cauchy sequences. -/
theorem complete_of_convergent_controlled_sequences (B : â„• â†’ ennreal) (hB : âˆ€n, 0 < B n)
  (H : âˆ€u : â„• â†’ Î±, (âˆ€N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ edist (u n) (u m) < B N) â†’ âˆƒx, tendsto u at_top (ğ“ x)) :
  complete_space Î± :=
uniform_space.complete_of_convergent_controlled_sequences
  uniformity_has_countable_basis
  (Î» n, {p:Î±Ã—Î± | edist p.1 p.2 < B n}) (Î» n, edist_mem_uniformity $ hB n) H

/-- A sequentially complete emetric space is complete. -/
theorem complete_of_cauchy_seq_tendsto :
  (âˆ€ u : â„• â†’ Î±, cauchy_seq u â†’ âˆƒa, tendsto u at_top (ğ“ a)) â†’ complete_space Î± :=
uniform_space.complete_of_cauchy_seq_tendsto uniformity_has_countable_basis

end emetric

open emetric

-- TODO : epremetric + t0 = emetric

/-- An emetric space is separated -/
@[priority 100] -- see Note [lower instance priority]
instance to_separated {Î±} [emetric_space Î±] : separated Î± :=
separated_def.2 $ Î» x y h, eq_of_forall_edist_le $
Î» Îµ Îµ0, le_of_lt (h _ (edist_mem_uniformity Îµ0))

/-- Auxiliary function to replace the uniformity on an extended premetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct an extended premetric space with a
specified uniformity. -/
def epremetric_space.replace_uniformity {Î±} [U : uniform_space Î±] (m : epremetric_space Î±)
  (H : @uniformity _ U = @uniformity _ (epremetric_space.to_uniform_space Î±)) :
  epremetric_space Î± :=
{ edist               := @edist _ m.to_has_edist,
  edist_self          := edist_self,
  edist_comm          := edist_comm,
  edist_triangle      := edist_triangle,
  to_uniform_space    := U,
  uniformity_edist    := H.trans (@epremetric_space.uniformity_edist Î± _) }

/-- Auxiliary function to replace the uniformity on an emetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct an emetric space with a
specified uniformity. -/
def emetric_space.replace_uniformity {Î±} [U : uniform_space Î±] (m : emetric_space Î±)
  (H : @uniformity _ U = @uniformity _ (epremetric_space.to_uniform_space Î±)) :
  emetric_space Î± :=
{ edist               := @edist _ m.to_has_edist,
  edist_self          := edist_self,
  eq_of_edist_eq_zero := @eq_of_edist_eq_zero _ _,
  edist_comm          := edist_comm,
  edist_triangle      := edist_triangle,
  to_uniform_space    := U,
  uniformity_edist    := H.trans (@epremetric_space.uniformity_edist Î± _) }

/-- The extended premetric induced by a function taking values in an extended premetric space.
    The function does not need to be injective. -/
def epremetric_space.induced {Î± Î²} (f : Î± â†’ Î²) (m : epremetric_space Î²) : epremetric_space Î± :=
{ edist               := Î» x y, edist (f x) (f y),
  edist_self          := Î» x, edist_self _,
  edist_comm          := Î» x y, edist_comm _ _,
  edist_triangle      := Î» x y z, edist_triangle _ _ _,
  to_uniform_space    := uniform_space.comap f m.to_uniform_space,
  uniformity_edist    := begin
    apply @uniformity_dist_of_mem_uniformity _ _ _ _ _ (Î» x y, edist (f x) (f y)),
    refine Î» s, mem_comap_sets.trans _,
    split; intro H,
    { rcases H with âŸ¨r, ru, rsâŸ©,
      rcases mem_uniformity_edist.1 ru with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      refine âŸ¨Îµ, Îµ0, Î» a b h, rs (hÎµ _)âŸ©, exact h },
    { rcases H with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      exact âŸ¨_, edist_mem_uniformity Îµ0, Î» âŸ¨a, bâŸ©, hÎµâŸ© }
  end }

/-- The extended metric induced by an injective function taking values in an emetric space. -/
def emetric_space.induced {Î± Î²} (f : Î± â†’ Î²) (hf : function.injective f)
  (m : emetric_space Î²) : emetric_space Î± :=
{ edist               := Î» x y, edist (f x) (f y),
  edist_self          := Î» x, edist_self _,
  eq_of_edist_eq_zero := Î» x y h, hf (edist_eq_zero.1 h),
  edist_comm          := Î» x y, edist_comm _ _,
  edist_triangle      := Î» x y z, edist_triangle _ _ _,
  to_uniform_space    := uniform_space.comap f m.to_uniform_space,
  uniformity_edist    := begin
    apply @uniformity_dist_of_mem_uniformity _ _ _ _ _ (Î» x y, edist (f x) (f y)),
    refine Î» s, mem_comap_sets.trans _,
    split; intro H,
    { rcases H with âŸ¨r, ru, rsâŸ©,
      rcases mem_uniformity_edist.1 ru with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      refine âŸ¨Îµ, Îµ0, Î» a b h, rs (hÎµ _)âŸ©, exact h },
    { rcases H with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      exact âŸ¨_, edist_mem_uniformity Îµ0, Î» âŸ¨a, bâŸ©, hÎµâŸ© }
  end }

instance {Î±} {p : Î± â†’ Prop} [t : epremetric_space Î±] : epremetric_space (subtype p) :=
t.induced subtype.val

/-- Emetric space instance on subsets of emetric spaces -/
instance {Î±} {p : Î± â†’ Prop} [t : emetric_space Î±] : emetric_space (subtype p) :=
t.induced subtype.val (Î» x y, subtype.eq)

/-- The extended distance on a subset of an emetric space is the restriction of
the original distance, by definition -/
theorem subtype.edist_eq {p : Î± â†’ Prop} (x y : subtype p) :
  edist x y = edist x.1 y.1 := rfl

instance prod.epremetric_space_max [epremetric_space Î²] : epremetric_space (Î± Ã— Î²) :=
{ edist := Î» x y, max (edist x.1 y.1) (edist x.2 y.2),
  edist_self := Î» x, by simp,
  edist_comm := Î» x y, by simp [edist_comm],
  edist_triangle := Î» x y z, max_le
    (le_trans (edist_triangle _ _ _) (add_le_add' (le_max_left _ _) (le_max_left _ _)))
    (le_trans (edist_triangle _ _ _) (add_le_add' (le_max_right _ _) (le_max_right _ _))),
  uniformity_edist := begin
    refine uniformity_prod.trans _,
    simp [epremetric_space.uniformity_edist, comap_infi],
    rw â† infi_inf_eq, congr, funext,
    rw â† infi_inf_eq, congr, funext,
    simp [inf_principal, ext_iff, max_lt_iff]
  end,
  to_uniform_space := prod.uniform_space }

/-- The product of two emetric spaces, with the max distance, is an extended
metric spaces. We make sure that the uniform structure thus constructed is the one
corresponding to the product of uniform spaces, to avoid diamond problems. -/
instance prod.emetric_space_max {Î±} [emetric_space Î±] [emetric_space Î²] : emetric_space (Î± Ã— Î²) :=
{ eq_of_edist_eq_zero := Î» x y h, begin
    cases max_le_iff.1 (le_of_eq h) with hâ‚ hâ‚‚,
    have A : x.fst = y.fst := edist_le_zero.1 hâ‚,
    have B : x.snd = y.snd := edist_le_zero.1 hâ‚‚,
    exact prod.ext_iff.2 âŸ¨A, BâŸ©
  end,
  .. prod.epremetric_space_max }

lemma prod.edist_eq [epremetric_space Î²] {x y : Î± Ã— Î²} :
  edist x y = max (edist x.1 y.1) (edist x.2 y.2) := rfl

section pi
open finset
variables {Ï€ : Î² â†’ Type*} [fintype Î²]

instance epremetric_space_pi [âˆ€b, epremetric_space (Ï€ b)] : epremetric_space (Î b, Ï€ b) :=
{ edist := Î» f g, finset.sup univ (Î»b, edist (f b) (g b)),
  edist_self := assume f, bot_unique $ finset.sup_le $ by simp,
  edist_comm := assume f g, by unfold edist; congr; funext a; exact edist_comm _ _,
  edist_triangle := assume f g h,
    begin
      simp only [finset.sup_le_iff],
      assume b hb,
      exact le_trans (edist_triangle _ (g b) _) (add_le_add' (le_sup hb) (le_sup hb))
    end,
  to_uniform_space := Pi.uniform_space _,
  uniformity_edist := begin
    simp only [Pi.uniformity, epremetric_space.uniformity_edist, comap_infi, gt_iff_lt, preimage_set_of_eq,
          comap_principal],
    rw infi_comm, congr, funext Îµ,
    rw infi_comm, congr, funext Îµpos,
    change 0 < Îµ at Îµpos,
    simp [ext_iff, Îµpos]
  end }

/-- The product of a finite number of emetric spaces, with the max distance, is still
an emetric space.
This construction would also work for infinite products, but it would not give rise
to the product topology. Hence, we only formalize it in the good situation of finitely many
spaces. -/
instance emetric_space_pi [âˆ€b, emetric_space (Ï€ b)] : emetric_space (Î b, Ï€ b) :=
{ eq_of_edist_eq_zero := assume f g eq0,
    begin
      have eq1 : sup univ (Î» (b : Î²), edist (f b) (g b)) â‰¤ 0 := le_of_eq eq0,
      simp only [finset.sup_le_iff] at eq1,
      exact (funext $ assume b, edist_le_zero.1 $ eq1 b $ mem_univ b),
    end }

variables [âˆ€b, epremetric_space (Ï€ b)]

lemma edist_pi_def (f g : Î b, Ï€ b) : edist f g = sup univ (Î»b, edist (f b) (g b)) := rfl

lemma edist_pi_lt_iff {f g : Î b, Ï€ b} {r : ennreal} (hr : 0 < r) :
  edist f g < r â†” âˆ€b, edist (f b) (g b) < r :=
begin
  rw [edist_pi_def, finset.sup_lt_iff],
  { simp only [forall_prop_of_true, finset.mem_univ, iff_self] },
  { exact hr }
end

lemma edist_pi_le_iff {f g : Î b, Ï€ b} {r : ennreal} :
  edist f g â‰¤ r â†” âˆ€b, edist (f b) (g b) â‰¤ r :=
begin
  rw [edist_pi_def, finset.sup_le_iff],
  simp only [forall_prop_of_true, finset.mem_univ, iff_self]
end

end pi

namespace emetric
variables {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : ennreal} {s : set Î±}

/-- `emetric.ball x Îµ` is the set of all points `y` with `edist y x < Îµ` -/
def ball (x : Î±) (Îµ : ennreal) : set Î± := {y | edist y x < Îµ}

@[simp] theorem mem_ball : y âˆˆ ball x Îµ â†” edist y x < Îµ := iff.rfl

theorem mem_ball' : y âˆˆ ball x Îµ â†” edist x y < Îµ := by rw edist_comm; refl

/-- `emetric.closed_ball x Îµ` is the set of all points `y` with `edist y x â‰¤ Îµ` -/
def closed_ball (x : Î±) (Îµ : ennreal) := {y | edist y x â‰¤ Îµ}

@[simp] theorem mem_closed_ball : y âˆˆ closed_ball x Îµ â†” edist y x â‰¤ Îµ := iff.rfl

theorem ball_subset_closed_ball : ball x Îµ âŠ† closed_ball x Îµ :=
assume y, by simp; intros h; apply le_of_lt h

theorem pos_of_mem_ball (hy : y âˆˆ ball x Îµ) : 0 < Îµ :=
lt_of_le_of_lt (zero_le _) hy

theorem mem_ball_self (h : 0 < Îµ) : x âˆˆ ball x Îµ :=
show edist x x < Îµ, by rw edist_self; assumption

theorem mem_closed_ball_self : x âˆˆ closed_ball x Îµ :=
show edist x x â‰¤ Îµ, by rw edist_self; exact bot_le

theorem mem_ball_comm : x âˆˆ ball y Îµ â†” y âˆˆ ball x Îµ :=
by simp [edist_comm]

theorem ball_subset_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) : ball x Îµâ‚ âŠ† ball x Îµâ‚‚ :=
Î» y (yx : _ < Îµâ‚), lt_of_lt_of_le yx h

theorem closed_ball_subset_closed_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) :
  closed_ball x Îµâ‚ âŠ† closed_ball x Îµâ‚‚ :=
Î» y (yx : _ â‰¤ Îµâ‚), le_trans yx h

theorem ball_disjoint (h : Îµâ‚ + Îµâ‚‚ â‰¤ edist x y) : ball x Îµâ‚ âˆ© ball y Îµâ‚‚ = âˆ… :=
eq_empty_iff_forall_not_mem.2 $ Î» z âŸ¨hâ‚, hâ‚‚âŸ©,
not_lt_of_le (edist_triangle_left x y z)
  (lt_of_lt_of_le (ennreal.add_lt_add hâ‚ hâ‚‚) h)

theorem ball_subset (h : edist x y + Îµâ‚ â‰¤ Îµâ‚‚) (h' : edist x y < âŠ¤) : ball x Îµâ‚ âŠ† ball y Îµâ‚‚ :=
Î» z zx, calc
  edist z y â‰¤ edist z x + edist x y : edist_triangle _ _ _
  ... = edist x y + edist z x : add_comm _ _
  ... < edist x y + Îµâ‚ : (ennreal.add_lt_add_iff_left h').2 zx
  ... â‰¤ Îµâ‚‚ : h

theorem exists_ball_subset_ball (h : y âˆˆ ball x Îµ) : âˆƒ Îµ' > 0, ball y Îµ' âŠ† ball x Îµ :=
begin
  have : 0 < Îµ - edist y x := by simpa using h,
  refine âŸ¨Îµ - edist y x, this, ball_subset _ _âŸ©,
  { rw ennreal.add_sub_cancel_of_le (le_of_lt h), apply le_refl _},
  { have : edist y x â‰  âŠ¤ := lattice.ne_top_of_lt h, apply lt_top_iff_ne_top.2 this }
end

theorem ball_eq_empty_iff : ball x Îµ = âˆ… â†” Îµ = 0 :=
eq_empty_iff_forall_not_mem.trans
âŸ¨Î»h, le_bot_iff.1 (le_of_not_gt (Î» Îµ0, h _ (mem_ball_self Îµ0))),
Î»Îµ0 y h, not_lt_of_le (le_of_eq Îµ0) (pos_of_mem_ball h)âŸ©

theorem nhds_eq : ğ“ x = (â¨…Îµ:{Îµ:ennreal // Îµ>0}, principal (ball x Îµ.val)) :=
begin
  rw [nhds_eq_uniformity, uniformity_edist'', lift'_infi],
  { apply congr_arg, funext Îµ,
    rw [lift'_principal],
    { simp [ball, edist_comm] },
    { exact monotone_preimage } },
  { exact âŸ¨âŸ¨1, ennreal.zero_lt_oneâŸ©âŸ© },
  { intros, refl }
end

theorem mem_nhds_iff : s âˆˆ ğ“ x â†” âˆƒÎµ>0, ball x Îµ âŠ† s :=
begin
  rw [nhds_eq, mem_infi],
  { simp },
  { intros y z, cases y with y hy, cases z with z hz,
    refine âŸ¨âŸ¨min y z, lt_min hy hzâŸ©, _âŸ©,
    simp [ball_subset_ball, min_le_left, min_le_right, (â‰¥)] },
  { exact âŸ¨âŸ¨1, ennreal.zero_lt_oneâŸ©âŸ© }
end

theorem is_open_iff : is_open s â†” âˆ€xâˆˆs, âˆƒÎµ>0, ball x Îµ âŠ† s :=
by simp [is_open_iff_nhds, mem_nhds_iff]

theorem is_open_ball : is_open (ball x Îµ) :=
is_open_iff.2 $ Î» y, exists_ball_subset_ball

theorem ball_mem_nhds (x : Î±) {Îµ : ennreal} (Îµ0 : 0 < Îµ) : ball x Îµ âˆˆ ğ“ x :=
mem_nhds_sets is_open_ball (mem_ball_self Îµ0)

/-- Îµ-characterization of the closure in emetric spaces -/
theorem mem_closure_iff' :
  x âˆˆ closure s â†” âˆ€Îµ>0, âˆƒy âˆˆ s, edist x y < Îµ :=
âŸ¨begin
  intros hx Îµ hÎµ,
  have A : ball x Îµ âˆ© s â‰  âˆ… := mem_closure_iff.1 hx _ is_open_ball (mem_ball_self hÎµ),
  cases ne_empty_iff_exists_mem.1 A with y hy,
  simp,
  exact âŸ¨y, âŸ¨hy.2, by have B := hy.1; simpa [mem_ball'] using BâŸ©âŸ©
end,
begin
  intros H,
  apply mem_closure_iff.2,
  intros o ho xo,
  rcases is_open_iff.1 ho x xo with âŸ¨Îµ, âŸ¨Îµpos, hÎµâŸ©âŸ©,
  rcases H Îµ Îµpos with âŸ¨y, âŸ¨ys, ydistâŸ©âŸ©,
  have B : y âˆˆ o âˆ© s := âŸ¨hÎµ (by simpa [edist_comm]), ysâŸ©,
  apply ne_empty_of_mem B
endâŸ©

theorem tendsto_nhds {f : filter Î²} {u : Î² â†’ Î±} {a : Î±} :
  tendsto u f (ğ“ a) â†” âˆ€ Îµ > 0, âˆƒ n âˆˆ f, âˆ€x âˆˆ n, edist (u x) a < Îµ :=
âŸ¨Î» H Îµ Îµ0, âŸ¨uâ»Â¹' (ball a Îµ), H (ball_mem_nhds _ Îµ0), by simpâŸ©,
 Î» H s hs,
  let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_nhds_iff.1 hs, âŸ¨Î´, Î´0, hÎ´âŸ© := H _ Îµ0 in
  f.sets_of_superset Î´0 (Î»x xÎ´, hÎµ (hÎ´ x xÎ´))âŸ©

theorem tendsto_at_top [inhabited Î²] [semilattice_sup Î²] (u : Î² â†’ Î±) {a : Î±} :
  tendsto u at_top (ğ“ a) â†” âˆ€Îµ>0, âˆƒN, âˆ€nâ‰¥N, edist (u n) a < Îµ :=
begin
  rw tendsto_nhds,
  apply forall_congr,
  intro Îµ,
  apply forall_congr,
  intro hÎµ,
  simp,
  exact âŸ¨Î» âŸ¨s, âŸ¨N, hNâŸ©, hsâŸ©, âŸ¨N, Î»n hn, hs _ (hN _ hn)âŸ©, Î» âŸ¨N, hNâŸ©, âŸ¨{n | n â‰¥ N}, âŸ¨âŸ¨N, by simpâŸ©, hNâŸ©âŸ©âŸ©,
end

/-- In an emetric space, Cauchy sequences are characterized by the fact that, eventually,
the edistance between its elements is arbitrarily small -/
theorem cauchy_seq_iff [inhabited Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>0, âˆƒN, âˆ€m nâ‰¥N, edist (u n) (u m) < Îµ :=
begin
  simp only [cauchy_seq, emetric.cauchy_iff, true_and, exists_prop, filter.mem_at_top_sets,
    filter.at_top_ne_bot, filter.mem_map, ne.def, filter.map_eq_bot_iff, not_false_iff, set.mem_set_of_eq],
  split,
  { intros H Îµ Îµpos,
    rcases H Îµ Îµpos with âŸ¨t, âŸ¨N, hNâŸ©, htâŸ©,
    exact âŸ¨N, Î»m n hm hn, ht _ _ (hN _ hn) (hN _ hm)âŸ© },
  { intros H Îµ Îµpos,
    rcases H (Îµ/2) (ennreal.half_pos Îµpos) with âŸ¨N, hNâŸ©,
    existsi ball (u N) (Îµ/2),
    split,
    { exact âŸ¨N, Î»x hx, hN _ _ (le_refl N) hxâŸ© },
    { exact Î»x y hx hy, calc
        edist x y â‰¤ edist x (u N) + edist y (u N) : edist_triangle_right _ _ _
        ... < Îµ/2 + Îµ/2 : ennreal.add_lt_add hx hy
        ... = Îµ : ennreal.add_halves _ } }
end

/-- A variation around the emetric characterization of Cauchy sequences -/
theorem cauchy_seq_iff' [inhabited Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>(0 : ennreal), âˆƒN, âˆ€nâ‰¥N, edist (u n) (u N) < Îµ :=
begin
  rw cauchy_seq_iff,
  split,
  { intros H Îµ Îµpos,
    rcases H Îµ Îµpos with âŸ¨N, hNâŸ©,
    exact âŸ¨N, Î»n hn, hN _ _ (le_refl N) hnâŸ© },
  { intros H Îµ Îµpos,
    rcases H (Îµ/2) (ennreal.half_pos Îµpos) with âŸ¨N, hNâŸ©,
    exact âŸ¨N, Î» m n hm hn, calc
       edist (u n) (u m) â‰¤ edist (u n) (u N) + edist (u m) (u N) : edist_triangle_right _ _ _
                    ... < Îµ/2 + Îµ/2 : ennreal.add_lt_add (hN _ hn) (hN _ hm)
                    ... = Îµ : ennreal.add_halves _âŸ© }
end

/-- A variation of the emetric characterization of Cauchy sequences that deals with
`nnreal` upper bounds. -/
theorem cauchy_seq_iff_nnreal [inhabited Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€ Îµ : nnreal, 0 < Îµ â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ edist (u N) (u n) < Îµ :=
begin
  refine cauchy_seq_iff'.trans
    âŸ¨Î» H Îµ Îµpos, (H Îµ (ennreal.coe_pos.2 Îµpos)).imp $
      Î» N hN n hn, edist_comm (u n) (u N) â–¸ hN n hn,
      Î» H Îµ Îµpos, _âŸ©,
  specialize H ((min 1 Îµ).to_nnreal)
    (ennreal.to_nnreal_pos_iff.2 âŸ¨lt_min ennreal.zero_lt_one Îµpos,
      ennreal.lt_top_iff_ne_top.1 $ min_lt_iff.2 $ or.inl ennreal.coe_lt_topâŸ©),
  refine H.imp (Î» N hN n hn, edist_comm (u N) (u n) â–¸ lt_of_lt_of_le (hN n hn) _),
  refine ennreal.coe_le_iff.2 _,
  rintros Îµ rfl,
  rw [â† ennreal.coe_one, â† ennreal.coe_min, ennreal.to_nnreal_coe],
  apply min_le_right
end

theorem totally_bounded_iff {s : set Î±} :
  totally_bounded s â†” âˆ€ Îµ > 0, âˆƒt : set Î±, finite t âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
âŸ¨Î» H Îµ Îµ0, H _ (edist_mem_uniformity Îµ0),
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru,
               âŸ¨t, ft, hâŸ© := H Îµ Îµ0 in
  âŸ¨t, ft, subset.trans h $ Union_subset_Union $ Î» y, Union_subset_Union $ Î» yt z, hÎµâŸ©âŸ©

theorem totally_bounded_iff' {s : set Î±} :
  totally_bounded s â†” âˆ€ Îµ > 0, âˆƒtâŠ†s, finite t âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
âŸ¨Î» H Îµ Îµ0, (totally_bounded_iff_subset.1 H) _ (edist_mem_uniformity Îµ0),
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru,
               âŸ¨t, _, ft, hâŸ© := H Îµ Îµ0 in
  âŸ¨t, ft, subset.trans h $ Union_subset_Union $ Î» y, Union_subset_Union $ Î» yt z, hÎµâŸ©âŸ©

section pi
open finset emetric
variables {Ï€ : Î² â†’ Type*} [fintype Î²] [âˆ€b, epremetric_space (Ï€ b)]

/-- An open ball in a product space is a product of open balls. The assumption `0 < r`
is necessary for the case of the empty product. -/
lemma ball_pi (x : Î b, Ï€ b) {r : ennreal} (hr : 0 < r) :
  ball x r = { y | âˆ€b, y b âˆˆ ball (x b) r } :=
by { ext p, simp [edist_pi_lt_iff hr] }

/-- A closed ball in a product space is a product of closed balls. The assumption `0 â‰¤ r`
is necessary for the case of the empty product. -/
lemma closed_ball_pi (x : Î b, Ï€ b) {r : ennreal} :
  closed_ball x r = { y | âˆ€b, y b âˆˆ closed_ball (x b) r } :=
by { ext p, simp [edist_pi_le_iff] }

end pi

section compact

/-- A compact set in an emetric space is separable, i.e., it is the closure of a countable set -/
lemma countable_closure_of_compact {Î± : Type u} [emetric_space Î±] {s : set Î±} (hs : compact s) :
  âˆƒ t âŠ† s, (countable t âˆ§ s = closure t) :=
begin
  have A : âˆ€ (e:ennreal), e > 0 â†’ âˆƒ t âŠ† s, (finite t âˆ§ s âŠ† (â‹ƒxâˆˆt, ball x e)) :=
    totally_bounded_iff'.1 (compact_iff_totally_bounded_complete.1 hs).1,
--    assume e, finite_cover_balls_of_compact hs,
  have B : âˆ€ (e:ennreal), âˆƒ t âŠ† s, finite t âˆ§ (e > 0 â†’ s âŠ† (â‹ƒxâˆˆt, ball x e)),
  { intro e,
    cases le_or_gt e 0 with h,
    { exact âŸ¨âˆ…, by finishâŸ© },
    { rcases A e h with âŸ¨s, âŸ¨finite_s, closure_sâŸ©âŸ©, existsi s, finish }},
  /-The desired countable set is obtained by taking for each `n` the centers of a finite cover
  by balls of radius `1/n`, and then the union over `n`. -/
  choose T T_in_s finite_T using B,
  let t := â‹ƒn:â„•, T nâ»Â¹,
  have Tâ‚ : t âŠ† s := begin apply Union_subset, assume n, apply T_in_s end,
  have Tâ‚‚ : countable t := by finish [countable_Union, countable_finite],
  have Tâ‚ƒ : s âŠ† closure t,
  { intros x x_in_s,
    apply mem_closure_iff'.2,
    intros Îµ Îµpos,
    rcases ennreal.exists_inv_nat_lt (bot_lt_iff_ne_bot.1 Îµpos) with âŸ¨n, hnâŸ©,
    have inv_n_pos : (0 : ennreal) < (n : â„•)â»Â¹ := by simp [ennreal.bot_lt_iff_ne_bot],
    have C : x âˆˆ (â‹ƒyâˆˆ T (n : â„•)â»Â¹, ball y (n : â„•)â»Â¹) :=
      mem_of_mem_of_subset x_in_s ((finite_T (n : â„•)â»Â¹).2 inv_n_pos),
    rcases mem_Union.1 C with âŸ¨y, _, âŸ¨y_in_T, rflâŸ©, DxyâŸ©,
    simp at Dxy,  -- Dxy : edist x y < 1 / â†‘n
    have : y âˆˆ t := mem_of_mem_of_subset y_in_T (by apply subset_Union (Î» (n:â„•), T (n : â„•)â»Â¹)),
    have : edist x y < Îµ := lt_trans Dxy hn,
    exact âŸ¨y, â€¹y âˆˆ tâ€º, â€¹edist x y < Îµâ€ºâŸ© },
  have Tâ‚„ : closure t âŠ† s := calc
    closure t âŠ† closure s : closure_mono Tâ‚
    ... = s : closure_eq_of_is_closed (closed_of_compact _ hs),
  exact âŸ¨t, âŸ¨Tâ‚, Tâ‚‚, subset.antisymm Tâ‚ƒ Tâ‚„âŸ©âŸ©
end

end compact

section first_countable

@[priority 100] -- see Note [lower instance priority]
instance (Î± : Type u) [epremetric_space Î±] :
  topological_space.first_countable_topology Î± :=
uniform_space.first_countable_topology uniformity_has_countable_basis

end first_countable

section second_countable
open topological_space

/-- A separable emetric space is second countable: one obtains a countable basis by taking
the balls centered at points in a dense subset, and with rational radii. We do not register
this as an instance, as there is already an instance going in the other direction
from second countable spaces to separable spaces, and we want to avoid loops. -/
lemma second_countable_of_separable (Î± : Type u) [epremetric_space Î±] [separable_space Î±] :
  second_countable_topology Î± :=
let âŸ¨S, âŸ¨S_countable, S_denseâŸ©âŸ© := separable_space.exists_countable_closure_eq_univ Î± in
âŸ¨âŸ¨â‹ƒx âˆˆ S, â‹ƒ (n : nat), {ball x (nâ»Â¹)},
âŸ¨show countable â‹ƒx âˆˆ S, â‹ƒ (n : nat), {ball x (nâ»Â¹)},
{ apply countable_bUnion S_countable,
  intros a aS,
  apply countable_Union,
  simp },
show uniform_space.to_topological_space Î± = generate_from (â‹ƒx âˆˆ S, â‹ƒ (n : nat), {ball x (nâ»Â¹)}),
{ have A : âˆ€ (u : set Î±), (u âˆˆ â‹ƒx âˆˆ S, â‹ƒ (n : nat), ({ball x ((n : ennreal)â»Â¹)} : set (set Î±))) â†’ is_open u,
  { simp only [and_imp, exists_prop, set.mem_Union, set.mem_singleton_iff, exists_imp_distrib],
    intros u x hx i u_ball,
    rw [u_ball],
    exact is_open_ball },
  have B : is_topological_basis (â‹ƒx âˆˆ S, â‹ƒ (n : nat), ({ball x (nâ»Â¹)} : set (set Î±))),
  { refine is_topological_basis_of_open_of_nhds A (Î»a u au open_u, _),
    rcases is_open_iff.1 open_u a au with âŸ¨Îµ, Îµpos, ÎµballâŸ©,
    have : Îµ / 2 > 0 := ennreal.half_pos Îµpos,
    /- The ball `ball a Îµ` is included in `u`. We need to find one of our balls `ball x (nâ»Â¹)`
    containing `a` and contained in `ball a Îµ`. For this, we take `n` larger than `2/Îµ`, and
    then `x` in `S` at distance at most `nâ»Â¹` of `a` -/
    rcases ennreal.exists_inv_nat_lt (bot_lt_iff_ne_bot.1 (ennreal.half_pos Îµpos)) with âŸ¨n, ÎµnâŸ©,
    have : (0 : ennreal) < nâ»Â¹ := by simp [ennreal.bot_lt_iff_ne_bot],
    have : (a : Î±) âˆˆ closure (S : set Î±) := by rw [S_dense]; simp,
    rcases mem_closure_iff'.1 this _ â€¹(0 : ennreal) <  nâ»Â¹â€º with âŸ¨x, xS, xdistâŸ©,
    existsi ball x (â†‘n)â»Â¹,
    have I : ball x (nâ»Â¹) âŠ† ball a Îµ := Î»y ydist, calc
      edist y a = edist a y : edist_comm _ _
      ... â‰¤ edist a x + edist y x : edist_triangle_right _ _ _
      ... < nâ»Â¹ + nâ»Â¹ : ennreal.add_lt_add xdist ydist
      ... < Îµ/2 + Îµ/2 : ennreal.add_lt_add Îµn Îµn
      ... = Îµ : ennreal.add_halves _,
    simp only [emetric.mem_ball, exists_prop, set.mem_Union, set.mem_singleton_iff],
    exact âŸ¨âŸ¨x, âŸ¨xS, âŸ¨n, rflâŸ©âŸ©âŸ©, âŸ¨by simpa, subset.trans I ÎµballâŸ©âŸ© },
  exact B.2.2 }âŸ©âŸ©âŸ©

end second_countable

section diam

/-- The diameter of a set in an emetric space, named `emetric.diam` -/
def diam (s : set Î±) := Sup ((Î»p : Î± Ã— Î±, edist p.1 p.2) '' (set.prod s s))

/-- If two points belong to some set, their edistance is bounded by the diameter of the set -/
lemma edist_le_diam_of_mem (hx : x âˆˆ s) (hy : y âˆˆ s) : edist x y â‰¤ diam s :=
le_Sup ((mem_image _ _ _).2 âŸ¨(âŸ¨x, yâŸ© : Î± Ã— Î±), by simp [hx, hy]âŸ©)

/-- If the distance between any two points in a set is bounded by some constant, this constant
bounds the diameter. -/
lemma diam_le_of_forall_edist_le {d : ennreal} (h : âˆ€x y âˆˆ s, edist x y â‰¤ d) : diam s â‰¤ d :=
begin
  apply Sup_le _,
  simp only [and_imp, set.mem_image, set.mem_prod, exists_imp_distrib, prod.exists],
  assume b x y xs ys dxy,
  rw â† dxy,
  exact h x y xs ys
end

/-- The diameter of the empty set vanishes -/
@[simp] lemma diam_empty : diam (âˆ… : set Î±) = 0 :=
by simp [diam]

/-- The diameter of a singleton vanishes -/
@[simp] lemma diam_singleton : diam ({x} : set Î±) = 0 :=
by simp [diam]

/-- The diameter is monotonous with respect to inclusion -/
lemma diam_mono {s t : set Î±} (h : s âŠ† t) : diam s â‰¤ diam t :=
begin
  refine Sup_le_Sup (Î»p hp, _),
  simp only [set.mem_image, set.mem_prod, prod.exists] at hp,
  rcases hp with âŸ¨x, y, âŸ¨âŸ¨xs, ysâŸ©, dxyâŸ©âŸ©,
  exact (mem_image _ _ _).2 âŸ¨âŸ¨x, yâŸ©, âŸ¨âŸ¨h xs, h ysâŸ©, dxyâŸ©âŸ©
end

/-- The diameter of a union is controlled by the diameter of the sets, and the edistance
between two points in the sets. -/
lemma diam_union {t : set Î±} (xs : x âˆˆ s) (yt : y âˆˆ t) : diam (s âˆª t) â‰¤ diam s + edist x y + diam t :=
begin
  have A : âˆ€a âˆˆ s, âˆ€b âˆˆ t, edist a b â‰¤ diam s + edist x y + diam t := Î»a ha b hb, calc
    edist a b â‰¤ edist a x + edist x y + edist y b : edist_triangle4 _ _ _ _
    ... â‰¤ diam s + edist x y + diam t :
      add_le_add' (add_le_add' (edist_le_diam_of_mem ha xs) (le_refl _)) (edist_le_diam_of_mem yt hb),
  refine diam_le_of_forall_edist_le (Î»a b ha hb, _),
  cases (mem_union _ _ _).1 ha with h'a h'a; cases (mem_union _ _ _).1 hb with h'b h'b,
  { calc edist a b â‰¤ diam s : edist_le_diam_of_mem h'a h'b
        ... â‰¤ diam s + (edist x y + diam t) : le_add_right (le_refl _)
        ... = diam s + edist x y + diam t : by simp only [add_comm, eq_self_iff_true, add_left_comm] },
  { exact A a h'a b h'b },
  { have Z := A b h'b a h'a, rwa [edist_comm] at Z },
  { calc edist a b â‰¤ diam t : edist_le_diam_of_mem h'a h'b
        ... â‰¤ (diam s + edist x y) + diam t : le_add_left (le_refl _) }
end

lemma diam_union' {t : set Î±} (h : s âˆ© t â‰  âˆ…) : diam (s âˆª t) â‰¤ diam s + diam t :=
let âŸ¨x, âŸ¨xs, xtâŸ©âŸ© := ne_empty_iff_exists_mem.1 h in by simpa using diam_union xs xt

lemma diam_closed_ball {r : ennreal} : diam (closed_ball x r) â‰¤ 2 * r :=
diam_le_of_forall_edist_le $ Î»a b ha hb, calc
  edist a b â‰¤ edist a x + edist b x : edist_triangle_right _ _ _
  ... â‰¤ r + r : add_le_add' ha hb
  ... = 2 * r : by simp [mul_two, mul_comm]

lemma diam_ball {r : ennreal} : diam (ball x r) â‰¤ 2 * r :=
le_trans (diam_mono ball_subset_closed_ball) diam_closed_ball

end diam

end emetric --namespace
