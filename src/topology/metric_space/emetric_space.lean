/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/
import data.real.ennreal
import data.finset.intervals
import topology.uniform_space.uniform_embedding
import topology.uniform_space.pi
import topology.uniform_space.uniform_convergence

/-!
# Extended metric spaces

This file is devoted to the definition and study of `emetric_spaces`, i.e., metric
spaces in which the distance is allowed to take the value âˆ. This extended distance is
called `edist`, and takes values in `ennreal`.

Many definitions and theorems expected on emetric spaces are already introduced on uniform spaces and
topological spaces. For example:
  open and closed sets, compactness, completeness, continuity and uniform continuity

The class `emetric_space` therefore extends `uniform_space` (and `topological_space`).
-/

open set filter classical
noncomputable theory

open_locale uniformity topological_space big_operators filter

universes u v w
variables {Î± : Type u} {Î² : Type v} {Î³ : Type w}

/-- Characterizing uniformities associated to a (generalized) distance function `D`
in terms of the elements of the uniformity. -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem uniformity_dist_of_mem_uniformity [linear_order Î²] {U : filter (Î± Ã— Î±)} (z : Î²) (D : Î± â†’ Î± â†’ Î²)
  (H : âˆ€ s, s âˆˆ U â†” âˆƒÎµ>z, âˆ€{a b:Î±}, D a b < Îµ â†’ (a, b) âˆˆ s) :
  U = â¨… Îµ>z, ğ“Ÿ {p:Î±Ã—Î± | D p.1 p.2 < Îµ} :=
le_antisymm
  (le_infi $ Î» Îµ, le_infi $ Î» Îµ0, le_principal_iff.2 $ (H _).2 âŸ¨Îµ, Îµ0, Î» a b, idâŸ©)
  (Î» r ur, let âŸ¨Îµ, Îµ0, hâŸ© := (H _).1 ur in
    mem_infi_sets Îµ $ mem_infi_sets Îµ0 $ mem_principal_sets.2 $ Î» âŸ¨a, bâŸ©, h)

class has_edist (Î± : Type*) := (edist : Î± â†’ Î± â†’ ennreal)
export has_edist (edist)

/-- Creating a uniform space from an extended distance. -/
def uniform_space_of_edist
  (edist : Î± â†’ Î± â†’ ennreal)
  (edist_self : âˆ€ x : Î±, edist x x = 0)
  (edist_comm : âˆ€ x y : Î±, edist x y = edist y x)
  (edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z) : uniform_space Î± :=
uniform_space.of_core {
  uniformity := (â¨… Îµ>0, ğ“Ÿ {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}),
  refl       := le_infi $ assume Îµ, le_infi $
    by simp [set.subset_def, id_rel, edist_self, (>)] {contextual := tt},
  comp       :=
    le_infi $ assume Îµ, le_infi $ assume h,
    have (2 : ennreal) = (2 : â„•) := by simp,
    have A : 0 < Îµ / 2 := ennreal.div_pos_iff.2
      âŸ¨ne_of_gt h, by { convert ennreal.nat_ne_top 2 }âŸ©,
    lift'_le
    (mem_infi_sets (Îµ / 2) $ mem_infi_sets A (subset.refl _)) $
    have âˆ€ (a b c : Î±), edist a c < Îµ / 2 â†’ edist c b < Îµ / 2 â†’ edist a b < Îµ,
      from assume a b c hac hcb,
      calc edist a b â‰¤ edist a c + edist c b : edist_triangle _ _ _
        ... < Îµ / 2 + Îµ / 2 : ennreal.add_lt_add hac hcb
        ... = Îµ : by rw [ennreal.add_halves],
    by simpa [comp_rel],
  symm       := tendsto_infi.2 $ assume Îµ, tendsto_infi.2 $ assume h,
    tendsto_infi' Îµ $ tendsto_infi' h $ tendsto_principal_principal.2 $ by simp [edist_comm] }

section prio
set_option default_priority 100 -- see Note [default priority]

-- the uniform structure is embedded in the emetric space structure
-- to avoid instance diamond issues. See Note [forgetful inheritance].

/-- Extended metric spaces, with an extended distance `edist` possibly taking the
value âˆ

Each emetric space induces a canonical `uniform_space` and hence a canonical `topological_space`.
This is enforced in the type class definition, by extending the `uniform_space` structure. When
instantiating an `emetric_space` structure, the uniformity fields are not necessary, they will be
filled in by default. There is a default value for the uniformity, that can be substituted
in cases of interest, for instance when instantiating an `emetric_space` structure
on a product.

Continuity of `edist` is proved in `topology.instances.ennreal`
-/
@[nolint ge_or_gt] -- see Note [nolint_ge]
class emetric_space (Î± : Type u) extends has_edist Î± : Type u :=
(edist_self : âˆ€ x : Î±, edist x x = 0)
(eq_of_edist_eq_zero : âˆ€ {x y : Î±}, edist x y = 0 â†’ x = y)
(edist_comm : âˆ€ x y : Î±, edist x y = edist y x)
(edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z)
(to_uniform_space : uniform_space Î± := uniform_space_of_edist edist edist_self edist_comm edist_triangle)
(uniformity_edist : ğ“¤ Î± = â¨… Îµ>0, ğ“Ÿ {p:Î±Ã—Î± | edist p.1 p.2 < Îµ} . control_laws_tac)
end prio

/- emetric spaces are less common than metric spaces. Therefore, we work in a dedicated
namespace, while notions associated to metric spaces are mostly in the root namespace. -/
variables [emetric_space Î±]

@[priority 100] -- see Note [lower instance priority]
instance emetric_space.to_uniform_space' : uniform_space Î± :=
emetric_space.to_uniform_space

export emetric_space (edist_self eq_of_edist_eq_zero edist_comm edist_triangle)

attribute [simp] edist_self

/-- Characterize the equality of points by the vanishing of their extended distance -/
@[simp] theorem edist_eq_zero {x y : Î±} : edist x y = 0 â†” x = y :=
iff.intro eq_of_edist_eq_zero (assume : x = y, this â–¸ edist_self _)

@[simp] theorem zero_eq_edist {x y : Î±} : 0 = edist x y â†” x = y :=
iff.intro (assume h, eq_of_edist_eq_zero (h.symm))
          (assume : x = y, this â–¸ (edist_self _).symm)

theorem edist_le_zero {x y : Î±} : (edist x y â‰¤ 0) â†” x = y :=
le_zero_iff_eq.trans edist_eq_zero

/-- Triangle inequality for the extended distance -/
theorem edist_triangle_left (x y z : Î±) : edist x y â‰¤ edist z x + edist z y :=
by rw edist_comm z; apply edist_triangle

theorem edist_triangle_right (x y z : Î±) : edist x y â‰¤ edist x z + edist y z :=
by rw edist_comm y; apply edist_triangle

lemma edist_triangle4 (x y z t : Î±) :
  edist x t â‰¤ edist x y + edist y z + edist z t :=
calc
  edist x t â‰¤ edist x z + edist z t : edist_triangle x z t
... â‰¤ (edist x y + edist y z) + edist z t : add_le_add_right (edist_triangle x y z) _

/-- The triangle (polygon) inequality for sequences of points; `finset.Ico` version. -/
lemma edist_le_Ico_sum_edist (f : â„• â†’ Î±) {m n} (h : m â‰¤ n) :
  edist (f m) (f n) â‰¤ âˆ‘ i in finset.Ico m n, edist (f i) (f (i + 1)) :=
begin
  revert n,
  refine nat.le_induction _ _,
  { simp only [finset.sum_empty, finset.Ico.self_eq_empty, edist_self],
    -- TODO: Why doesn't Lean close this goal automatically? `apply le_refl` fails too.
    exact le_refl (0:ennreal) },
  { assume n hn hrec,
    calc edist (f m) (f (n+1)) â‰¤ edist (f m) (f n) + edist (f n) (f (n+1)) : edist_triangle _ _ _
      ... â‰¤ âˆ‘ i in finset.Ico m n, _ + _ : add_le_add hrec (le_refl _)
      ... = âˆ‘ i in finset.Ico m (n+1), _ :
        by rw [finset.Ico.succ_top hn, finset.sum_insert, add_comm]; simp }
end

/-- The triangle (polygon) inequality for sequences of points; `finset.range` version. -/
lemma edist_le_range_sum_edist (f : â„• â†’ Î±) (n : â„•) :
  edist (f 0) (f n) â‰¤ âˆ‘ i in finset.range n, edist (f i) (f (i + 1)) :=
finset.Ico.zero_bot n â–¸ edist_le_Ico_sum_edist f (nat.zero_le n)

/-- A version of `edist_le_Ico_sum_edist` with each intermediate distance replaced
with an upper estimate. -/
lemma edist_le_Ico_sum_of_edist_le {f : â„• â†’ Î±} {m n} (hmn : m â‰¤ n)
  {d : â„• â†’ ennreal} (hd : âˆ€ {k}, m â‰¤ k â†’ k < n â†’ edist (f k) (f (k + 1)) â‰¤ d k) :
  edist (f m) (f n) â‰¤ âˆ‘ i in finset.Ico m n, d i :=
le_trans (edist_le_Ico_sum_edist f hmn) $
finset.sum_le_sum $ Î» k hk, hd (finset.Ico.mem.1 hk).1 (finset.Ico.mem.1 hk).2

/-- A version of `edist_le_range_sum_edist` with each intermediate distance replaced
with an upper estimate. -/
lemma edist_le_range_sum_of_edist_le {f : â„• â†’ Î±} (n : â„•)
  {d : â„• â†’ ennreal} (hd : âˆ€ {k}, k < n â†’ edist (f k) (f (k + 1)) â‰¤ d k) :
  edist (f 0) (f n) â‰¤ âˆ‘ i in finset.range n, d i :=
finset.Ico.zero_bot n â–¸ edist_le_Ico_sum_of_edist_le (zero_le n) (Î» _ _, hd)

/-- Two points coincide if their distance is `< Îµ` for all positive Îµ -/
theorem eq_of_forall_edist_le {x y : Î±} (h : âˆ€Îµ > 0, edist x y â‰¤ Îµ) : x = y :=
eq_of_edist_eq_zero (eq_of_le_of_forall_le_of_dense bot_le h)

/-- Reformulation of the uniform structure in terms of the extended distance -/
theorem uniformity_edist :
  ğ“¤ Î± = â¨… Îµ>0, ğ“Ÿ {p:Î±Ã—Î± | edist p.1 p.2 < Îµ} :=
emetric_space.uniformity_edist

theorem uniformity_basis_edist :
  (ğ“¤ Î±).has_basis (Î» Îµ : ennreal, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
(@uniformity_edist Î± _).symm â–¸ has_basis_binfi_principal
  (Î» r hr p hp, âŸ¨min r p, lt_min hr hp,
    Î» x hx, lt_of_lt_of_le hx (min_le_left _ _),
    Î» x hx, lt_of_lt_of_le hx (min_le_right _ _)âŸ©)
  âŸ¨1, ennreal.zero_lt_oneâŸ©

/-- Characterization of the elements of the uniformity in terms of the extended distance -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem mem_uniformity_edist {s : set (Î±Ã—Î±)} :
  s âˆˆ ğ“¤ Î± â†” (âˆƒÎµ>0, âˆ€{a b:Î±}, edist a b < Îµ â†’ (a, b) âˆˆ s) :=
uniformity_basis_edist.mem_uniformity_iff

/-- Given `f : Î² â†’ ennreal`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then `f i`-neighborhoods of the diagonal form a basis of `ğ“¤ Î±`.

For specific bases see `uniformity_basis_edist`, `uniformity_basis_edist'`,
`uniformity_basis_edist_nnreal`, and `uniformity_basis_edist_inv_nat`. -/
protected theorem emetric.mk_uniformity_basis {Î² : Type*} {p : Î² â†’ Prop} {f : Î² â†’ ennreal}
  (hfâ‚€ : âˆ€ x, p x â†’ 0 < f x) (hf : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ x (hx : p x), f x â‰¤ Îµ) :
  (ğ“¤ Î±).has_basis p (Î» x, {p:Î±Ã—Î± | edist p.1 p.2 < f x}) :=
begin
  refine âŸ¨Î» s, uniformity_basis_edist.mem_iff.trans _âŸ©,
  split,
  { rintros âŸ¨Îµ, Îµâ‚€, hÎµâŸ©,
    rcases hf Îµ Îµâ‚€ with âŸ¨i, hi, HâŸ©,
    exact âŸ¨i, hi, Î» x hx, hÎµ $ lt_of_lt_of_le hx HâŸ© },
  { exact Î» âŸ¨i, hi, HâŸ©, âŸ¨f i, hfâ‚€ i hi, HâŸ© }
end

/-- Given `f : Î² â†’ ennreal`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then closed `f i`-neighborhoods of the diagonal form a basis of `ğ“¤ Î±`.

For specific bases see `uniformity_basis_edist_le` and `uniformity_basis_edist_le'`. -/
protected theorem emetric.mk_uniformity_basis_le {Î² : Type*} {p : Î² â†’ Prop} {f : Î² â†’ ennreal}
  (hfâ‚€ : âˆ€ x, p x â†’ 0 < f x) (hf : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ x (hx : p x), f x â‰¤ Îµ) :
  (ğ“¤ Î±).has_basis p (Î» x, {p:Î±Ã—Î± | edist p.1 p.2 â‰¤ f x}) :=
begin
  refine âŸ¨Î» s, uniformity_basis_edist.mem_iff.trans _âŸ©,
  split,
  { rintros âŸ¨Îµ, Îµâ‚€, hÎµâŸ©,
    rcases dense Îµâ‚€ with âŸ¨Îµ', hÎµ'âŸ©,
    rcases hf Îµ' hÎµ'.1 with âŸ¨i, hi, HâŸ©,
    exact âŸ¨i, hi, Î» x hx, hÎµ $ lt_of_le_of_lt (le_trans hx H) hÎµ'.2âŸ© },
  { exact Î» âŸ¨i, hi, HâŸ©, âŸ¨f i, hfâ‚€ i hi, Î» x hx, H (le_of_lt hx)âŸ© }
end

theorem uniformity_basis_edist_le :
  (ğ“¤ Î±).has_basis (Î» Îµ : ennreal, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 â‰¤ Îµ}) :=
emetric.mk_uniformity_basis_le (Î» _, id) (Î» Îµ Îµâ‚€, âŸ¨Îµ, Îµâ‚€, le_refl ÎµâŸ©)

theorem uniformity_basis_edist' (Îµ' : ennreal) (hÎµ' : 0 < Îµ') :
  (ğ“¤ Î±).has_basis (Î» Îµ : ennreal, Îµ âˆˆ Ioo 0 Îµ') (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
emetric.mk_uniformity_basis (Î» _, and.left)
  (Î» Îµ Îµâ‚€, let âŸ¨Î´, hÎ´âŸ© := dense hÎµ' in
    âŸ¨min Îµ Î´, âŸ¨lt_min Îµâ‚€ hÎ´.1, lt_of_le_of_lt (min_le_right _ _) hÎ´.2âŸ©, min_le_left _ _âŸ©)

theorem uniformity_basis_edist_le' (Îµ' : ennreal) (hÎµ' : 0 < Îµ') :
  (ğ“¤ Î±).has_basis (Î» Îµ : ennreal, Îµ âˆˆ Ioo 0 Îµ') (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 â‰¤ Îµ}) :=
emetric.mk_uniformity_basis_le (Î» _, and.left)
  (Î» Îµ Îµâ‚€, let âŸ¨Î´, hÎ´âŸ© := dense hÎµ' in
    âŸ¨min Îµ Î´, âŸ¨lt_min Îµâ‚€ hÎ´.1, lt_of_le_of_lt (min_le_right _ _) hÎ´.2âŸ©, min_le_left _ _âŸ©)

theorem uniformity_basis_edist_nnreal :
  (ğ“¤ Î±).has_basis (Î» Îµ : nnreal, 0 < Îµ) (Î» Îµ, {p:Î±Ã—Î± | edist p.1 p.2 < Îµ}) :=
emetric.mk_uniformity_basis (Î» _, ennreal.coe_pos.2)
  (Î» Îµ Îµâ‚€, let âŸ¨Î´, hÎ´âŸ© := ennreal.lt_iff_exists_nnreal_btwn.1 Îµâ‚€ in
  âŸ¨Î´, ennreal.coe_pos.1 hÎ´.1, le_of_lt hÎ´.2âŸ©)

theorem uniformity_basis_edist_inv_nat :
  (ğ“¤ Î±).has_basis (Î» _, true) (Î» n:â„•, {p:Î±Ã—Î± | edist p.1 p.2 < (â†‘n)â»Â¹}) :=
emetric.mk_uniformity_basis
  (Î» n _, ennreal.inv_pos.2 $ ennreal.nat_ne_top n)
  (Î» Îµ Îµâ‚€, let âŸ¨n, hnâŸ© := ennreal.exists_inv_nat_lt (ne_of_gt Îµâ‚€) in âŸ¨n, trivial, le_of_lt hnâŸ©)

/-- Fixed size neighborhoods of the diagonal belong to the uniform structure -/
theorem edist_mem_uniformity {Îµ:ennreal} (Îµ0 : 0 < Îµ) :
  {p:Î±Ã—Î± | edist p.1 p.2 < Îµ} âˆˆ ğ“¤ Î± :=
mem_uniformity_edist.2 âŸ¨Îµ, Îµ0, Î» a b, idâŸ©

namespace emetric

theorem uniformity_has_countable_basis : is_countably_generated (ğ“¤ Î±) :=
is_countably_generated_of_seq âŸ¨_, uniformity_basis_edist_inv_nat.eq_infiâŸ©

/-- Îµ-Î´ characterization of uniform continuity on emetric spaces -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem uniform_continuous_iff [emetric_space Î²] {f : Î± â†’ Î²} :
  uniform_continuous f â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0,
    âˆ€{a b:Î±}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ :=
uniformity_basis_edist.uniform_continuous_iff uniformity_basis_edist

/-- Îµ-Î´ characterization of uniform embeddings on emetric spaces -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem uniform_embedding_iff [emetric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†” function.injective f âˆ§ uniform_continuous f âˆ§
    âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´ :=
uniform_embedding_def'.trans $ and_congr iff.rfl $ and_congr iff.rfl
âŸ¨Î» H Î´ Î´0, let âŸ¨t, tu, htâŸ© := H _ (edist_mem_uniformity Î´0),
               âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 tu in
  âŸ¨Îµ, Îµ0, Î» a b h, ht _ _ (hÎµ h)âŸ©,
 Î» H s su, let âŸ¨Î´, Î´0, hÎ´âŸ© := mem_uniformity_edist.1 su, âŸ¨Îµ, Îµ0, hÎµâŸ© := H _ Î´0 in
  âŸ¨_, edist_mem_uniformity Îµ0, Î» a b h, hÎ´ (hÎµ h)âŸ©âŸ©

/-- A map between emetric spaces is a uniform embedding if and only if the edistance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y` and conversely. -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem uniform_embedding_iff' [emetric_space Î²] {f : Î± â†’ Î²} :
  uniform_embedding f â†”
  (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î±}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ) âˆ§
  (âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´) :=
begin
  split,
  { assume h,
    exact âŸ¨uniform_continuous_iff.1 (uniform_embedding_iff.1 h).2.1,
          (uniform_embedding_iff.1 h).2.2âŸ© },
  { rintros âŸ¨hâ‚, hâ‚‚âŸ©,
    refine uniform_embedding_iff.2 âŸ¨_, uniform_continuous_iff.2 hâ‚, hâ‚‚âŸ©,
    assume x y hxy,
    have : edist x y â‰¤ 0,
    { refine le_of_forall_lt' (Î»Î´ Î´pos, _),
      rcases hâ‚‚ Î´ Î´pos with âŸ¨Îµ, Îµpos, hÎµâŸ©,
      have : edist (f x) (f y) < Îµ, by simpa [hxy],
      exact hÎµ this },
    simpa using this }
end

/-- Îµ-Î´ characterization of Cauchy sequences on emetric spaces -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
protected lemma cauchy_iff {f : filter Î±} :
  cauchy f â†” f â‰  âŠ¥ âˆ§ âˆ€ Îµ > 0, âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, edist x y < Îµ :=
uniformity_basis_edist.cauchy_iff

/-- A very useful criterion to show that a space is complete is to show that all sequences
which satisfy a bound of the form `edist (u n) (u m) < B N` for all `n m â‰¥ N` are
converging. This is often applied for `B N = 2^{-N}`, i.e., with a very fast convergence to
`0`, which makes it possible to use arguments of converging series, while this is impossible
to do in general for arbitrary Cauchy sequences. -/
theorem complete_of_convergent_controlled_sequences (B : â„• â†’ ennreal) (hB : âˆ€n, 0 < B n)
  (H : âˆ€u : â„• â†’ Î±, (âˆ€N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ edist (u n) (u m) < B N) â†’ âˆƒx, tendsto u at_top (ğ“ x)) :
  complete_space Î± :=
uniform_space.complete_of_convergent_controlled_sequences
  uniformity_has_countable_basis
  (Î» n, {p:Î±Ã—Î± | edist p.1 p.2 < B n}) (Î» n, edist_mem_uniformity $ hB n) H

/-- A sequentially complete emetric space is complete. -/
theorem complete_of_cauchy_seq_tendsto :
  (âˆ€ u : â„• â†’ Î±, cauchy_seq u â†’ âˆƒa, tendsto u at_top (ğ“ a)) â†’ complete_space Î± :=
uniform_space.complete_of_cauchy_seq_tendsto uniformity_has_countable_basis

/-- Expressing locally uniform convergence on a set using `edist`. -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma tendsto_locally_uniformly_on_iff {Î¹ : Type*} [topological_space Î²]
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} {s : set Î²} :
  tendsto_locally_uniformly_on F f p s â†”
  âˆ€ Îµ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ nhds_within x s, âˆ€á¶  n in p, âˆ€ y âˆˆ t, edist (f y) (F n y) < Îµ :=
begin
  refine âŸ¨Î» H Îµ hÎµ, H _ (edist_mem_uniformity hÎµ), Î» H u hu x hx, _âŸ©,
  rcases mem_uniformity_edist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  rcases H Îµ Îµpos x hx with âŸ¨t, ht, HtâŸ©,
  exact âŸ¨t, ht, Ht.mono (Î» n hs x hx, hÎµ (hs x hx))âŸ©
end

/-- Expressing uniform convergence on a set using `edist`. -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma tendsto_uniformly_on_iff {Î¹ : Type*}
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} {s : set Î²} :
  tendsto_uniformly_on F f p s â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x âˆˆ s, edist (f x) (F n x) < Îµ :=
begin
  refine âŸ¨Î» H Îµ hÎµ, H _ (edist_mem_uniformity hÎµ), Î» H u hu, _âŸ©,
  rcases mem_uniformity_edist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  exact (H Îµ Îµpos).mono (Î» n hs x hx, hÎµ (hs x hx))
end

/-- Expressing locally uniform convergence using `edist`. -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma tendsto_locally_uniformly_iff {Î¹ : Type*} [topological_space Î²]
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} :
  tendsto_locally_uniformly F f p â†”
  âˆ€ Îµ > 0, âˆ€ (x : Î²), âˆƒ t âˆˆ ğ“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, edist (f y) (F n y) < Îµ :=
by simp [â† nhds_within_univ, â† tendsto_locally_uniformly_on_univ, tendsto_locally_uniformly_on_iff]

/-- Expressing uniform convergence using `edist`. -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma tendsto_uniformly_iff {Î¹ : Type*}
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : filter Î¹} :
  tendsto_uniformly F f p â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x, edist (f x) (F n x) < Îµ :=
by { rw [â† tendsto_uniformly_on_univ, tendsto_uniformly_on_iff], simp }

end emetric

open emetric

/-- An emetric space is separated -/
@[priority 100] -- see Note [lower instance priority]
instance to_separated : separated_space Î± :=
separated_def.2 $ Î» x y h, eq_of_forall_edist_le $
Î» Îµ Îµ0, le_of_lt (h _ (edist_mem_uniformity Îµ0))

/-- Auxiliary function to replace the uniformity on an emetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct an emetric space with a
specified uniformity. See Note [forgetful inheritance] explaining why having definitionally
the right uniformity is often important.
-/
def emetric_space.replace_uniformity {Î±} [U : uniform_space Î±] (m : emetric_space Î±)
  (H : @uniformity _ U = @uniformity _ emetric_space.to_uniform_space) :
  emetric_space Î± :=
{ edist               := @edist _ m.to_has_edist,
  edist_self          := edist_self,
  eq_of_edist_eq_zero := @eq_of_edist_eq_zero _ _,
  edist_comm          := edist_comm,
  edist_triangle      := edist_triangle,
  to_uniform_space    := U,
  uniformity_edist    := H.trans (@emetric_space.uniformity_edist Î± _) }

/-- The extended metric induced by an injective function taking values in an emetric space. -/
def emetric_space.induced {Î± Î²} (f : Î± â†’ Î²) (hf : function.injective f)
  (m : emetric_space Î²) : emetric_space Î± :=
{ edist               := Î» x y, edist (f x) (f y),
  edist_self          := Î» x, edist_self _,
  eq_of_edist_eq_zero := Î» x y h, hf (edist_eq_zero.1 h),
  edist_comm          := Î» x y, edist_comm _ _,
  edist_triangle      := Î» x y z, edist_triangle _ _ _,
  to_uniform_space    := uniform_space.comap f m.to_uniform_space,
  uniformity_edist    := begin
    apply @uniformity_dist_of_mem_uniformity _ _ _ _ _ (Î» x y, edist (f x) (f y)),
    refine Î» s, mem_comap_sets.trans _,
    split; intro H,
    { rcases H with âŸ¨r, ru, rsâŸ©,
      rcases mem_uniformity_edist.1 ru with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      refine âŸ¨Îµ, Îµ0, Î» a b h, rs (hÎµ _)âŸ©, exact h },
    { rcases H with âŸ¨Îµ, Îµ0, hÎµâŸ©,
      exact âŸ¨_, edist_mem_uniformity Îµ0, Î» âŸ¨a, bâŸ©, hÎµâŸ© }
  end }

/-- Emetric space instance on subsets of emetric spaces -/
instance {Î± : Type*} {p : Î± â†’ Prop} [t : emetric_space Î±] : emetric_space (subtype p) :=
t.induced coe (Î» x y, subtype.ext_iff_val.2)

/-- The extended distance on a subset of an emetric space is the restriction of
the original distance, by definition -/
theorem subtype.edist_eq {p : Î± â†’ Prop} (x y : subtype p) : edist x y = edist (x : Î±) y := rfl

/-- The product of two emetric spaces, with the max distance, is an extended
metric spaces. We make sure that the uniform structure thus constructed is the one
corresponding to the product of uniform spaces, to avoid diamond problems. -/
instance prod.emetric_space_max [emetric_space Î²] : emetric_space (Î± Ã— Î²) :=
{ edist := Î» x y, max (edist x.1 y.1) (edist x.2 y.2),
  edist_self := Î» x, by simp,
  eq_of_edist_eq_zero := Î» x y h, begin
    cases max_le_iff.1 (le_of_eq h) with hâ‚ hâ‚‚,
    have A : x.fst = y.fst := edist_le_zero.1 hâ‚,
    have B : x.snd = y.snd := edist_le_zero.1 hâ‚‚,
    exact prod.ext_iff.2 âŸ¨A, BâŸ©
  end,
  edist_comm := Î» x y, by simp [edist_comm],
  edist_triangle := Î» x y z, max_le
    (le_trans (edist_triangle _ _ _) (add_le_add (le_max_left _ _) (le_max_left _ _)))
    (le_trans (edist_triangle _ _ _) (add_le_add (le_max_right _ _) (le_max_right _ _))),
  uniformity_edist := begin
    refine uniformity_prod.trans _,
    simp [emetric_space.uniformity_edist, comap_infi],
    rw â† infi_inf_eq, congr, funext,
    rw â† infi_inf_eq, congr, funext,
    simp [inf_principal, ext_iff, max_lt_iff]
  end,
  to_uniform_space := prod.uniform_space }

lemma prod.edist_eq [emetric_space Î²] (x y : Î± Ã— Î²) :
  edist x y = max (edist x.1 y.1) (edist x.2 y.2) :=
rfl

section pi
open finset
variables {Ï€ : Î² â†’ Type*} [fintype Î²]

/-- The product of a finite number of emetric spaces, with the max distance, is still
an emetric space.
This construction would also work for infinite products, but it would not give rise
to the product topology. Hence, we only formalize it in the good situation of finitely many
spaces. -/
instance emetric_space_pi [âˆ€b, emetric_space (Ï€ b)] : emetric_space (Î b, Ï€ b) :=
{ edist := Î» f g, finset.sup univ (Î»b, edist (f b) (g b)),
  edist_self := assume f, bot_unique $ finset.sup_le $ by simp,
  edist_comm := assume f g, by unfold edist; congr; funext a; exact edist_comm _ _,
  edist_triangle := assume f g h,
    begin
      simp only [finset.sup_le_iff],
      assume b hb,
      exact le_trans (edist_triangle _ (g b) _) (add_le_add (le_sup hb) (le_sup hb))
    end,
  eq_of_edist_eq_zero := assume f g eq0,
    begin
      have eq1 : sup univ (Î» (b : Î²), edist (f b) (g b)) â‰¤ 0 := le_of_eq eq0,
      simp only [finset.sup_le_iff] at eq1,
      exact (funext $ assume b, edist_le_zero.1 $ eq1 b $ mem_univ b),
    end,
  to_uniform_space := Pi.uniform_space _,
  uniformity_edist := begin
    simp only [Pi.uniformity, emetric_space.uniformity_edist, comap_infi, gt_iff_lt,
      preimage_set_of_eq, comap_principal],
    rw infi_comm, congr, funext Îµ,
    rw infi_comm, congr, funext Îµpos,
    change 0 < Îµ at Îµpos,
    simp [set.ext_iff, Îµpos]
  end }

end pi

namespace emetric
variables {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : ennreal} {s : set Î±}

/-- `emetric.ball x Îµ` is the set of all points `y` with `edist y x < Îµ` -/
def ball (x : Î±) (Îµ : ennreal) : set Î± := {y | edist y x < Îµ}

@[simp] theorem mem_ball : y âˆˆ ball x Îµ â†” edist y x < Îµ := iff.rfl

theorem mem_ball' : y âˆˆ ball x Îµ â†” edist x y < Îµ := by rw edist_comm; refl

/-- `emetric.closed_ball x Îµ` is the set of all points `y` with `edist y x â‰¤ Îµ` -/
def closed_ball (x : Î±) (Îµ : ennreal) := {y | edist y x â‰¤ Îµ}

@[simp] theorem mem_closed_ball : y âˆˆ closed_ball x Îµ â†” edist y x â‰¤ Îµ := iff.rfl

theorem ball_subset_closed_ball : ball x Îµ âŠ† closed_ball x Îµ :=
assume y, by simp; intros h; apply le_of_lt h

theorem pos_of_mem_ball (hy : y âˆˆ ball x Îµ) : 0 < Îµ :=
lt_of_le_of_lt (zero_le _) hy

theorem mem_ball_self (h : 0 < Îµ) : x âˆˆ ball x Îµ :=
show edist x x < Îµ, by rw edist_self; assumption

theorem mem_closed_ball_self : x âˆˆ closed_ball x Îµ :=
show edist x x â‰¤ Îµ, by rw edist_self; exact bot_le

theorem mem_ball_comm : x âˆˆ ball y Îµ â†” y âˆˆ ball x Îµ :=
by simp [edist_comm]

theorem ball_subset_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) : ball x Îµâ‚ âŠ† ball x Îµâ‚‚ :=
Î» y (yx : _ < Îµâ‚), lt_of_lt_of_le yx h

theorem closed_ball_subset_closed_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) :
  closed_ball x Îµâ‚ âŠ† closed_ball x Îµâ‚‚ :=
Î» y (yx : _ â‰¤ Îµâ‚), le_trans yx h

theorem ball_disjoint (h : Îµâ‚ + Îµâ‚‚ â‰¤ edist x y) : ball x Îµâ‚ âˆ© ball y Îµâ‚‚ = âˆ… :=
eq_empty_iff_forall_not_mem.2 $ Î» z âŸ¨hâ‚, hâ‚‚âŸ©,
not_lt_of_le (edist_triangle_left x y z)
  (lt_of_lt_of_le (ennreal.add_lt_add hâ‚ hâ‚‚) h)

theorem ball_subset (h : edist x y + Îµâ‚ â‰¤ Îµâ‚‚) (h' : edist x y < âŠ¤) : ball x Îµâ‚ âŠ† ball y Îµâ‚‚ :=
Î» z zx, calc
  edist z y â‰¤ edist z x + edist x y : edist_triangle _ _ _
  ... = edist x y + edist z x : add_comm _ _
  ... < edist x y + Îµâ‚ : (ennreal.add_lt_add_iff_left h').2 zx
  ... â‰¤ Îµâ‚‚ : h

@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem exists_ball_subset_ball (h : y âˆˆ ball x Îµ) : âˆƒ Îµ' > 0, ball y Îµ' âŠ† ball x Îµ :=
begin
  have : 0 < Îµ - edist y x := by simpa using h,
  refine âŸ¨Îµ - edist y x, this, ball_subset _ _âŸ©,
  { rw ennreal.add_sub_cancel_of_le (le_of_lt h), apply le_refl _},
  { have : edist y x â‰  âŠ¤ := ne_top_of_lt h, apply lt_top_iff_ne_top.2 this }
end

theorem ball_eq_empty_iff : ball x Îµ = âˆ… â†” Îµ = 0 :=
eq_empty_iff_forall_not_mem.trans
âŸ¨Î»h, le_bot_iff.1 (le_of_not_gt (Î» Îµ0, h _ (mem_ball_self Îµ0))),
Î»Îµ0 y h, not_lt_of_le (le_of_eq Îµ0) (pos_of_mem_ball h)âŸ©

/-- Relation â€œtwo points are at a finite edistanceâ€ is an equivalence relation. -/
def edist_lt_top_setoid : setoid Î± :=
{ r := Î» x y, edist x y < âŠ¤,
  iseqv := âŸ¨Î» x, by { rw edist_self, exact ennreal.coe_lt_top },
    Î» x y h, by rwa edist_comm,
    Î» x y z hxy hyz, lt_of_le_of_lt (edist_triangle x y z) (ennreal.add_lt_top.2 âŸ¨hxy, hyzâŸ©)âŸ© }

@[simp] lemma ball_zero : ball x 0 = âˆ… :=
by rw [emetric.ball_eq_empty_iff]

theorem nhds_basis_eball : (ğ“ x).has_basis (Î» Îµ:ennreal, 0 < Îµ) (ball x) :=
nhds_basis_uniformity uniformity_basis_edist

theorem nhds_basis_closed_eball : (ğ“ x).has_basis (Î» Îµ:ennreal, 0 < Îµ) (closed_ball x) :=
nhds_basis_uniformity uniformity_basis_edist_le

@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem nhds_eq : ğ“ x = (â¨…Îµ>0, ğ“Ÿ (ball x Îµ)) :=
nhds_basis_eball.eq_binfi

@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem mem_nhds_iff : s âˆˆ ğ“ x â†” âˆƒÎµ>0, ball x Îµ âŠ† s := nhds_basis_eball.mem_iff

@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem is_open_iff : is_open s â†” âˆ€xâˆˆs, âˆƒÎµ>0, ball x Îµ âŠ† s :=
by simp [is_open_iff_nhds, mem_nhds_iff]

theorem is_open_ball : is_open (ball x Îµ) :=
is_open_iff.2 $ Î» y, exists_ball_subset_ball

theorem is_closed_ball_top : is_closed (ball x âŠ¤) :=
is_open_iff.2 $ Î» y hy, âŸ¨âŠ¤, ennreal.coe_lt_top, subset_compl_iff_disjoint.2 $
  ball_disjoint $ by { rw ennreal.top_add, exact le_of_not_lt hy }âŸ©

theorem ball_mem_nhds (x : Î±) {Îµ : ennreal} (Îµ0 : 0 < Îµ) : ball x Îµ âˆˆ ğ“ x :=
mem_nhds_sets is_open_ball (mem_ball_self Îµ0)

/-- Îµ-characterization of the closure in emetric spaces -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem mem_closure_iff :
  x âˆˆ closure s â†” âˆ€Îµ>0, âˆƒy âˆˆ s, edist x y < Îµ :=
(mem_closure_iff_nhds_basis nhds_basis_eball).trans $
  by simp only [mem_ball, edist_comm x]

@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem tendsto_nhds {f : filter Î²} {u : Î² â†’ Î±} {a : Î±} :
  tendsto u f (ğ“ a) â†” âˆ€ Îµ > 0, âˆ€á¶  x in f, edist (u x) a < Îµ :=
nhds_basis_eball.tendsto_right_iff

@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem tendsto_at_top [nonempty Î²] [semilattice_sup Î²] (u : Î² â†’ Î±) {a : Î±} :
  tendsto u at_top (ğ“ a) â†” âˆ€Îµ>0, âˆƒN, âˆ€nâ‰¥N, edist (u n) a < Îµ :=
(at_top_basis.tendsto_iff nhds_basis_eball).trans $
  by simp only [exists_prop, true_and, mem_Ici, mem_ball]

/-- In an emetric space, Cauchy sequences are characterized by the fact that, eventually,
the edistance between its elements is arbitrarily small -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem cauchy_seq_iff [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>0, âˆƒN, âˆ€m nâ‰¥N, edist (u m) (u n) < Îµ :=
uniformity_basis_edist.cauchy_seq_iff

/-- A variation around the emetric characterization of Cauchy sequences -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem cauchy_seq_iff' [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€Îµ>(0 : ennreal), âˆƒN, âˆ€nâ‰¥N, edist (u n) (u N) < Îµ :=
uniformity_basis_edist.cauchy_seq_iff'

/-- A variation of the emetric characterization of Cauchy sequences that deals with
`nnreal` upper bounds. -/
theorem cauchy_seq_iff_nnreal [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” âˆ€ Îµ : nnreal, 0 < Îµ â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ edist (u n) (u N) < Îµ :=
uniformity_basis_edist_nnreal.cauchy_seq_iff'

@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem totally_bounded_iff {s : set Î±} :
  totally_bounded s â†” âˆ€ Îµ > 0, âˆƒt : set Î±, finite t âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
âŸ¨Î» H Îµ Îµ0, H _ (edist_mem_uniformity Îµ0),
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru,
               âŸ¨t, ft, hâŸ© := H Îµ Îµ0 in
  âŸ¨t, ft, subset.trans h $ Union_subset_Union $ Î» y, Union_subset_Union $ Î» yt z, hÎµâŸ©âŸ©

@[nolint ge_or_gt] -- see Note [nolint_ge]
theorem totally_bounded_iff' {s : set Î±} :
  totally_bounded s â†” âˆ€ Îµ > 0, âˆƒtâŠ†s, finite t âˆ§ s âŠ† â‹ƒyâˆˆt, ball y Îµ :=
âŸ¨Î» H Îµ Îµ0, (totally_bounded_iff_subset.1 H) _ (edist_mem_uniformity Îµ0),
 Î» H r ru, let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru,
               âŸ¨t, _, ft, hâŸ© := H Îµ Îµ0 in
  âŸ¨t, ft, subset.trans h $ Union_subset_Union $ Î» y, Union_subset_Union $ Î» yt z, hÎµâŸ©âŸ©

section compact

/-- A compact set in an emetric space is separable, i.e., it is the closure of a countable set -/
lemma countable_closure_of_compact {Î± : Type u} [emetric_space Î±] {s : set Î±} (hs : is_compact s) :
  âˆƒ t âŠ† s, (countable t âˆ§ s = closure t) :=
begin
  have A : âˆ€ (e:ennreal), e > 0 â†’ âˆƒ t âŠ† s, (finite t âˆ§ s âŠ† (â‹ƒxâˆˆt, ball x e)) :=
    totally_bounded_iff'.1 (compact_iff_totally_bounded_complete.1 hs).1,
--    assume e, finite_cover_balls_of_compact hs,
  have B : âˆ€ (e:ennreal), âˆƒ t âŠ† s, finite t âˆ§ (e > 0 â†’ s âŠ† (â‹ƒxâˆˆt, ball x e)),
  { intro e,
    cases le_or_gt e 0 with h,
    { exact âŸ¨âˆ…, by finishâŸ© },
    { rcases A e h with âŸ¨s, âŸ¨finite_s, closure_sâŸ©âŸ©, existsi s, finish }},
  /-The desired countable set is obtained by taking for each `n` the centers of a finite cover
  by balls of radius `1/n`, and then the union over `n`. -/
  choose T T_in_s finite_T using B,
  let t := â‹ƒn:â„•, T nâ»Â¹,
  have Tâ‚ : t âŠ† s := begin apply Union_subset, assume n, apply T_in_s end,
  have Tâ‚‚ : countable t := by finish [countable_Union, finite.countable],
  have Tâ‚ƒ : s âŠ† closure t,
  { intros x x_in_s,
    apply mem_closure_iff.2,
    intros Îµ Îµpos,
    rcases ennreal.exists_inv_nat_lt (bot_lt_iff_ne_bot.1 Îµpos) with âŸ¨n, hnâŸ©,
    have inv_n_pos : (0 : ennreal) < (n : â„•)â»Â¹ := by simp [ennreal.bot_lt_iff_ne_bot],
    have C : x âˆˆ (â‹ƒyâˆˆ T (n : â„•)â»Â¹, ball y (n : â„•)â»Â¹) :=
      mem_of_mem_of_subset x_in_s ((finite_T (n : â„•)â»Â¹).2 inv_n_pos),
    rcases mem_Union.1 C with âŸ¨y, _, âŸ¨y_in_T, rflâŸ©, DxyâŸ©,
    simp at Dxy,  -- Dxy : edist x y < 1 / â†‘n
    have : y âˆˆ t := mem_of_mem_of_subset y_in_T (by apply subset_Union (Î» (n:â„•), T (n : â„•)â»Â¹)),
    have : edist x y < Îµ := lt_trans Dxy hn,
    exact âŸ¨y, â€¹y âˆˆ tâ€º, â€¹edist x y < Îµâ€ºâŸ© },
  have Tâ‚„ : closure t âŠ† s := calc
    closure t âŠ† closure s : closure_mono Tâ‚
    ... = s : hs.is_closed.closure_eq,
  exact âŸ¨t, âŸ¨Tâ‚, Tâ‚‚, subset.antisymm Tâ‚ƒ Tâ‚„âŸ©âŸ©
end

end compact

section first_countable

@[priority 100] -- see Note [lower instance priority]
instance (Î± : Type u) [emetric_space Î±] :
  topological_space.first_countable_topology Î± :=
uniform_space.first_countable_topology uniformity_has_countable_basis

end first_countable

section second_countable
open topological_space

/-- A separable emetric space is second countable: one obtains a countable basis by taking
the balls centered at points in a dense subset, and with rational radii. We do not register
this as an instance, as there is already an instance going in the other direction
from second countable spaces to separable spaces, and we want to avoid loops. -/
lemma second_countable_of_separable (Î± : Type u) [emetric_space Î±] [separable_space Î±] :
  second_countable_topology Î± :=
let âŸ¨S, âŸ¨S_countable, S_denseâŸ©âŸ© := separable_space.exists_countable_closure_eq_univ in
âŸ¨âŸ¨â‹ƒx âˆˆ S, â‹ƒ (n : nat), {ball x (nâ»Â¹)},
âŸ¨show countable â‹ƒx âˆˆ S, â‹ƒ (n : nat), {ball x (nâ»Â¹)},
{ apply S_countable.bUnion,
  intros a aS,
  apply countable_Union,
  simp },
show uniform_space.to_topological_space = generate_from (â‹ƒx âˆˆ S, â‹ƒ (n : nat), {ball x (nâ»Â¹)}),
{ have A : âˆ€ (u : set Î±), (u âˆˆ â‹ƒx âˆˆ S, â‹ƒ (n : nat), ({ball x ((n : ennreal)â»Â¹)} : set (set Î±))) â†’ is_open u,
  { simp only [and_imp, exists_prop, set.mem_Union, set.mem_singleton_iff, exists_imp_distrib],
    intros u x hx i u_ball,
    rw [u_ball],
    exact is_open_ball },
  have B : is_topological_basis (â‹ƒx âˆˆ S, â‹ƒ (n : nat), ({ball x (nâ»Â¹)} : set (set Î±))),
  { refine is_topological_basis_of_open_of_nhds A (Î»a u au open_u, _),
    rcases is_open_iff.1 open_u a au with âŸ¨Îµ, Îµpos, ÎµballâŸ©,
    have : Îµ / 2 > 0 := ennreal.half_pos Îµpos,
    /- The ball `ball a Îµ` is included in `u`. We need to find one of our balls `ball x (nâ»Â¹)`
    containing `a` and contained in `ball a Îµ`. For this, we take `n` larger than `2/Îµ`, and
    then `x` in `S` at distance at most `nâ»Â¹` of `a` -/
    rcases ennreal.exists_inv_nat_lt (bot_lt_iff_ne_bot.1 (ennreal.half_pos Îµpos)) with âŸ¨n, ÎµnâŸ©,
    have : (0 : ennreal) < nâ»Â¹ := by simp [ennreal.bot_lt_iff_ne_bot],
    have : (a : Î±) âˆˆ closure (S : set Î±) := by rw [S_dense]; simp,
    rcases mem_closure_iff.1 this _ â€¹(0 : ennreal) < nâ»Â¹â€º with âŸ¨x, xS, xdistâŸ©,
    existsi ball x (â†‘n)â»Â¹,
    have I : ball x (nâ»Â¹) âŠ† ball a Îµ := Î»y ydist, calc
      edist y a = edist a y : edist_comm _ _
      ... â‰¤ edist a x + edist y x : edist_triangle_right _ _ _
      ... < nâ»Â¹ + nâ»Â¹ : ennreal.add_lt_add xdist ydist
      ... < Îµ/2 + Îµ/2 : ennreal.add_lt_add Îµn Îµn
      ... = Îµ : ennreal.add_halves _,
    simp only [emetric.mem_ball, exists_prop, set.mem_Union, set.mem_singleton_iff],
    exact âŸ¨âŸ¨x, âŸ¨xS, âŸ¨n, rflâŸ©âŸ©âŸ©, âŸ¨by simpa, subset.trans I ÎµballâŸ©âŸ© },
  exact B.2.2 }âŸ©âŸ©âŸ©

end second_countable

section diam

/-- The diameter of a set in an emetric space, named `emetric.diam` -/
def diam (s : set Î±) := â¨† (x âˆˆ s) (y âˆˆ s), edist x y

lemma diam_le_iff_forall_edist_le {d : ennreal} :
  diam s â‰¤ d â†” âˆ€ (x âˆˆ s) (y âˆˆ s), edist x y â‰¤ d :=
by simp only [diam, supr_le_iff]

/-- If two points belong to some set, their edistance is bounded by the diameter of the set -/
lemma edist_le_diam_of_mem (hx : x âˆˆ s) (hy : y âˆˆ s) : edist x y â‰¤ diam s :=
diam_le_iff_forall_edist_le.1 (le_refl _) x hx y hy

/-- If the distance between any two points in a set is bounded by some constant, this constant
bounds the diameter. -/
lemma diam_le_of_forall_edist_le {d : ennreal} (h : âˆ€ (x âˆˆ s) (y âˆˆ s), edist x y â‰¤ d) :
  diam s â‰¤ d :=
diam_le_iff_forall_edist_le.2 h

/-- The diameter of a subsingleton vanishes. -/
lemma diam_subsingleton (hs : s.subsingleton) : diam s = 0 :=
le_zero_iff_eq.1 $ diam_le_of_forall_edist_le $
Î» x hx y hy, (hs hx hy).symm â–¸ edist_self y â–¸ le_refl _

/-- The diameter of the empty set vanishes -/
@[simp] lemma diam_empty : diam (âˆ… : set Î±) = 0 :=
diam_subsingleton subsingleton_empty

/-- The diameter of a singleton vanishes -/
@[simp] lemma diam_singleton : diam ({x} : set Î±) = 0 :=
diam_subsingleton subsingleton_singleton

lemma diam_eq_zero_iff : diam s = 0 â†” s.subsingleton :=
âŸ¨Î» h x hx y hy, edist_le_zero.1 $ h â–¸ edist_le_diam_of_mem hx hy, diam_subsingletonâŸ©

lemma diam_pos_iff : 0 < diam s â†” âˆƒ (x âˆˆ s) (y âˆˆ s), x â‰  y :=
begin
  have := not_congr (@diam_eq_zero_iff _ _ s),
  dunfold set.subsingleton at this,
  push_neg at this,
  simpa only [zero_lt_iff_ne_zero, exists_prop] using this
end

lemma diam_insert : diam (insert x s) = max (â¨† y âˆˆ s, edist x y) (diam s) :=
eq_of_forall_ge_iff $ Î» d, by simp only [diam_le_iff_forall_edist_le, ball_insert_iff,
  edist_self, edist_comm x, max_le_iff, supr_le_iff, zero_le, true_and,
  forall_and_distrib, and_self, â† and_assoc]

lemma diam_pair : diam ({x, y} : set Î±) = edist x y :=
by simp only [supr_singleton, diam_insert, diam_singleton, ennreal.max_zero_right]

lemma diam_triple :
  diam ({x, y, z} : set Î±) = max (max (edist x y) (edist x z)) (edist y z) :=
by simp only [diam_insert, supr_insert, supr_singleton, diam_singleton,
  ennreal.max_zero_right, ennreal.sup_eq_max]

/-- The diameter is monotonous with respect to inclusion -/
lemma diam_mono {s t : set Î±} (h : s âŠ† t) : diam s â‰¤ diam t :=
diam_le_of_forall_edist_le $ Î» x hx y hy, edist_le_diam_of_mem (h hx) (h hy)

/-- The diameter of a union is controlled by the diameter of the sets, and the edistance
between two points in the sets. -/
lemma diam_union {t : set Î±} (xs : x âˆˆ s) (yt : y âˆˆ t) : diam (s âˆª t) â‰¤ diam s + edist x y + diam t :=
begin
  have A : âˆ€a âˆˆ s, âˆ€b âˆˆ t, edist a b â‰¤ diam s + edist x y + diam t := Î»a ha b hb, calc
    edist a b â‰¤ edist a x + edist x y + edist y b : edist_triangle4 _ _ _ _
    ... â‰¤ diam s + edist x y + diam t :
      add_le_add (add_le_add (edist_le_diam_of_mem ha xs) (le_refl _)) (edist_le_diam_of_mem yt hb),
  refine diam_le_of_forall_edist_le (Î»a ha b hb, _),
  cases (mem_union _ _ _).1 ha with h'a h'a; cases (mem_union _ _ _).1 hb with h'b h'b,
  { calc edist a b â‰¤ diam s : edist_le_diam_of_mem h'a h'b
        ... â‰¤ diam s + (edist x y + diam t) : le_add_right (le_refl _)
        ... = diam s + edist x y + diam t : by simp only [add_comm, eq_self_iff_true, add_left_comm] },
  { exact A a h'a b h'b },
  { have Z := A b h'b a h'a, rwa [edist_comm] at Z },
  { calc edist a b â‰¤ diam t : edist_le_diam_of_mem h'a h'b
        ... â‰¤ (diam s + edist x y) + diam t : le_add_left (le_refl _) }
end

lemma diam_union' {t : set Î±} (h : (s âˆ© t).nonempty) : diam (s âˆª t) â‰¤ diam s + diam t :=
let âŸ¨x, âŸ¨xs, xtâŸ©âŸ© := h in by simpa using diam_union xs xt

lemma diam_closed_ball {r : ennreal} : diam (closed_ball x r) â‰¤ 2 * r :=
diam_le_of_forall_edist_le $ Î»a ha b hb, calc
  edist a b â‰¤ edist a x + edist b x : edist_triangle_right _ _ _
  ... â‰¤ r + r : add_le_add ha hb
  ... = 2 * r : by simp [mul_two, mul_comm]

lemma diam_ball {r : ennreal} : diam (ball x r) â‰¤ 2 * r :=
le_trans (diam_mono ball_subset_closed_ball) diam_closed_ball

end diam

end emetric --namespace
