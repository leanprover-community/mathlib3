/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/

import topology.metric_space.basic
import topology.uniform_space.equicontinuity
/-!
# Equicontinuity in metric spaces

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This files contains various facts about (uniform) equicontinuity in metric spaces. Most
importantly, we prove the usual characterization of equicontinuity of `F` at `xâ‚€` in the case of
(pseudo) metric spaces: `âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x, dist x xâ‚€ < Î´ â†’ âˆ€ i, dist (F i xâ‚€) (F i x) < Îµ`,
and we prove that functions sharing a common (local or global) continuity modulus are
(locally or uniformly) equicontinuous.

## Main statements

* `equicontinuous_at_iff`: characterization of equicontinuity for families of functions between
  (pseudo) metric spaces.
* `equicontinuous_at_of_continuity_modulus`: convenient way to prove equicontinuity at a point of
  a family of functions to a (pseudo) metric space by showing that they share a common *local*
  continuity modulus.
* `uniform_equicontinuous_of_continuity_modulus`: convenient way to prove uniform equicontinuity
  of a family of functions to a (pseudo) metric space by showing that they share a common *global*
  continuity modulus.

## Tags

equicontinuity, continuity modulus
-/

open filter
open_locale topology uniformity

variables {Î± Î² Î¹ : Type*} [pseudo_metric_space Î±]

namespace metric

/-- Characterization of equicontinuity for families of functions taking values in a (pseudo) metric
space. -/
lemma equicontinuous_at_iff_right {Î¹ : Type*} [topological_space Î²] {F : Î¹ â†’ Î² â†’ Î±} {xâ‚€ : Î²} :
  equicontinuous_at F xâ‚€ â†” âˆ€ Îµ > 0, âˆ€á¶  x in ğ“ xâ‚€, âˆ€ i, dist (F i xâ‚€) (F i x) < Îµ :=
uniformity_basis_dist.equicontinuous_at_iff_right

/-- Characterization of equicontinuity for families of functions between (pseudo) metric spaces. -/
lemma equicontinuous_at_iff {Î¹ : Type*} [pseudo_metric_space Î²] {F : Î¹ â†’ Î² â†’ Î±} {xâ‚€ : Î²} :
  equicontinuous_at F xâ‚€ â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x, dist x xâ‚€ < Î´ â†’ âˆ€ i, dist (F i xâ‚€) (F i x) < Îµ :=
nhds_basis_ball.equicontinuous_at_iff uniformity_basis_dist

/-- Reformulation of `equicontinuous_at_iff_pair` for families of functions taking values in a
(pseudo) metric space. -/
protected lemma equicontinuous_at_iff_pair {Î¹ : Type*} [topological_space Î²] {F : Î¹ â†’ Î² â†’ Î±}
  {xâ‚€ : Î²} :
  equicontinuous_at F xâ‚€ â†” âˆ€ Îµ > 0, âˆƒ U âˆˆ ğ“ xâ‚€, âˆ€ (x x' âˆˆ U), âˆ€ i, dist (F i x) (F i x') < Îµ :=
begin
  rw equicontinuous_at_iff_pair,
  split; intros H,
  { intros Îµ hÎµ,
    refine exists_imp_exists (Î» V, exists_imp_exists $ Î» hV h, _) (H _ (dist_mem_uniformity hÎµ)),
    exact Î» x hx x' hx', h _ hx _ hx' },
  { intros U hU,
    rcases mem_uniformity_dist.mp hU with âŸ¨Îµ, hÎµ, hÎµUâŸ©,
    refine exists_imp_exists (Î» V, exists_imp_exists $ Î» hV h, _) (H _ hÎµ),
    exact Î» x hx x' hx' i, hÎµU (h _ hx _ hx' i) }
end

/-- Characterization of uniform equicontinuity for families of functions taking values in a
(pseudo) metric space. -/
lemma uniform_equicontinuous_iff_right {Î¹ : Type*} [uniform_space Î²] {F : Î¹ â†’ Î² â†’ Î±} :
  uniform_equicontinuous F â†”
  âˆ€ Îµ > 0, âˆ€á¶  (xy : Î² Ã— Î²) in ğ“¤ Î², âˆ€ i, dist (F i xy.1) (F i xy.2) < Îµ :=
uniformity_basis_dist.uniform_equicontinuous_iff_right

/-- Characterization of uniform equicontinuity for families of functions between
(pseudo) metric spaces. -/
lemma uniform_equicontinuous_iff {Î¹ : Type*} [pseudo_metric_space Î²] {F : Î¹ â†’ Î² â†’ Î±} :
  uniform_equicontinuous F â†”
  âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x y, dist x y < Î´ â†’ âˆ€ i, dist (F i x) (F i y) < Îµ :=
uniformity_basis_dist.uniform_equicontinuous_iff uniformity_basis_dist

/-- For a family of functions to a (pseudo) metric spaces, a convenient way to prove
equicontinuity at a point is to show that all of the functions share a common *local* continuity
modulus. -/
lemma equicontinuous_at_of_continuity_modulus {Î¹ : Type*} [topological_space Î²] {xâ‚€ : Î²}
  (b : Î² â†’ â„)
  (b_lim : tendsto b (ğ“ xâ‚€) (ğ“ 0))
  (F : Î¹ â†’ Î² â†’ Î±)
  (H : âˆ€á¶  x in ğ“ xâ‚€, âˆ€ i, dist (F i xâ‚€) (F i x) â‰¤ b x) :
  equicontinuous_at F xâ‚€ :=
begin
  rw metric.equicontinuous_at_iff_right,
  intros Îµ Îµ0,
  filter_upwards [b_lim (Iio_mem_nhds Îµ0), H] using Î» x hxâ‚ hxâ‚‚ i, (hxâ‚‚ i).trans_lt hxâ‚
end

/-- For a family of functions between (pseudo) metric spaces, a convenient way to prove
uniform equicontinuity is to show that all of the functions share a common *global* continuity
modulus. -/
lemma uniform_equicontinuous_of_continuity_modulus {Î¹ : Type*} [pseudo_metric_space Î²] (b : â„ â†’ â„)
  (b_lim : tendsto b (ğ“ 0) (ğ“ 0))
  (F : Î¹ â†’ Î² â†’ Î±)
  (H : âˆ€ (x y : Î²) i, dist (F i x) (F i y) â‰¤ b (dist x y)) :
  uniform_equicontinuous F :=
begin
  rw metric.uniform_equicontinuous_iff,
  intros Îµ Îµ0,
  rcases tendsto_nhds_nhds.1 b_lim Îµ Îµ0 with âŸ¨Î´, Î´0, hÎ´âŸ©,
  refine âŸ¨Î´, Î´0, Î» x y hxy i, _âŸ©,
  calc
    dist (F i x) (F i y) â‰¤ b (dist x y) : H x y i
    ... â‰¤ |b (dist x y)| : le_abs_self _
    ... = dist (b (dist x y)) 0 : by simp [real.dist_eq]
    ... < Îµ : hÎ´ (by simpa only [real.dist_eq, tsub_zero, abs_dist] using hxy)
end

/-- For a family of functions between (pseudo) metric spaces, a convenient way to prove
equicontinuity is to show that all of the functions share a common *global* continuity modulus. -/
lemma equicontinuous_of_continuity_modulus {Î¹ : Type*} [pseudo_metric_space Î²] (b : â„ â†’ â„)
  (b_lim : tendsto b (ğ“ 0) (ğ“ 0))
  (F : Î¹ â†’ Î² â†’ Î±)
  (H : âˆ€ (x y : Î²) i, dist (F i x) (F i y) â‰¤ b (dist x y)) :
  equicontinuous F :=
(uniform_equicontinuous_of_continuity_modulus b b_lim F H).equicontinuous

end metric
