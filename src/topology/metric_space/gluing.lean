/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import topology.metric_space.isometry

/-!
# Metric space gluing

Gluing two metric spaces along a common subset. Formally, we are given

```
     Œ¶
  Z ---> X
  |
  |Œ®
  v
  Y
```
where `hŒ¶ : isometry Œ¶` and `hŒ® : isometry Œ®`.
We want to complete the square by a space `glue_space hŒ¶ hŒ®` and two isometries
`to_glue_l hŒ¶ hŒ®` and `to_glue_r hŒ¶ hŒ®` that make the square commute.
We start by defining a predistance on the disjoint union `X ‚äï Y`, for which
points `Œ¶ p` and `Œ® p` are at distance 0. The (quotient) metric space associated
to this predistance is the desired space.

This is an instance of a more general construction, where `Œ¶` and `Œ®` do not have to be isometries,
but the distances in the image almost coincide, up to `2Œµ` say. Then one can almost glue the two
spaces so that the images of a point under `Œ¶` and `Œ®` are `Œµ`-close. If `Œµ > 0`, this yields a
metric space structure on `X ‚äï Y`, without the need to take a quotient. In particular, when `X`
and `Y` are inhabited, this gives a natural metric space structure on `X ‚äï Y`, where the basepoints
are at distance 1, say, and the distances between other points are obtained by going through the two
basepoints.

We also define the inductive limit of metric spaces. Given
```
     f 0        f 1        f 2        f 3
X 0 -----> X 1 -----> X 2 -----> X 3 -----> ...
```
where the `X n` are metric spaces and `f n` isometric embeddings, we define the inductive
limit of the `X n`, also known as the increasing union of the `X n` in this context, if we
identify `X n` and `X (n+1)` through `f n`. This is a metric space in which all `X n` embed
isometrically and in a way compatible with `f n`.

-/

noncomputable theory

universes u v w
open function set
open_locale uniformity

namespace metric
section approx_gluing

variables {X : Type u} {Y : Type v} {Z : Type w}

variables [metric_space X] [metric_space Y]
          {Œ¶ : Z ‚Üí X} {Œ® : Z ‚Üí Y} {Œµ : ‚Ñù}
open _root_.sum (inl inr)

/-- Define a predistance on `X ‚äï Y`, for which `Œ¶ p` and `Œ® p` are at distance `Œµ` -/
def glue_dist (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) : X ‚äï Y ‚Üí X ‚äï Y ‚Üí ‚Ñù
| (inl x) (inl y) := dist x y
| (inr x) (inr y) := dist x y
| (inl x) (inr y) := (‚®Ö p, dist x (Œ¶ p) + dist y (Œ® p)) + Œµ
| (inr x) (inl y) := (‚®Ö p, dist y (Œ¶ p) + dist x (Œ® p)) + Œµ

private lemma glue_dist_self (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) : ‚àÄ x, glue_dist Œ¶ Œ® Œµ x x = 0
| (inl x) := dist_self _
| (inr x) := dist_self _

lemma glue_dist_glued_points [nonempty Z] (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (p : Z) :
  glue_dist Œ¶ Œ® Œµ (inl (Œ¶ p)) (inr (Œ® p)) = Œµ :=
begin
  have : (‚®Ö q, dist (Œ¶ p) (Œ¶ q) + dist (Œ® p) (Œ® q)) = 0,
  { have A : ‚àÄ q, 0 ‚â§ dist (Œ¶ p) (Œ¶ q) + dist (Œ® p) (Œ® q) :=
      Œªq, by rw ‚Üê add_zero (0 : ‚Ñù); exact add_le_add dist_nonneg dist_nonneg,
    refine le_antisymm _ (le_cinfi A),
    have : 0 = dist (Œ¶ p) (Œ¶ p) + dist (Œ® p) (Œ® p), by simp,
    rw this,
    exact cinfi_le ‚ü®0, forall_range_iff.2 A‚ü© p },
  rw [glue_dist, this, zero_add]
end

private lemma glue_dist_comm (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) :
  ‚àÄ x y, glue_dist Œ¶ Œ® Œµ x y = glue_dist Œ¶ Œ® Œµ y x
| (inl x) (inl y) := dist_comm _ _
| (inr x) (inr y) := dist_comm _ _
| (inl x) (inr y) := rfl
| (inr x) (inl y) := rfl

variable [nonempty Z]

private lemma glue_dist_triangle (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù)
  (H : ‚àÄ p q, |dist (Œ¶ p) (Œ¶ q) - dist (Œ® p) (Œ® q)| ‚â§ 2 * Œµ) :
  ‚àÄ x y z, glue_dist Œ¶ Œ® Œµ x z ‚â§ glue_dist Œ¶ Œ® Œµ x y + glue_dist Œ¶ Œ® Œµ y z
| (inl x) (inl y) (inl z) := dist_triangle _ _ _
| (inr x) (inr y) (inr z) := dist_triangle _ _ _
| (inr x) (inl y) (inl z) := begin
    have B : ‚àÄ a b, bdd_below (range (Œª (p : Z), dist a (Œ¶ p) + dist b (Œ® p))) :=
      Œªa b, ‚ü®0, forall_range_iff.2 (Œªp, add_nonneg dist_nonneg dist_nonneg)‚ü©,
    unfold glue_dist,
    have : (‚®Ö p, dist z (Œ¶ p) + dist x (Œ® p)) ‚â§ (‚®Ö p, dist y (Œ¶ p) + dist x (Œ® p)) + dist y z,
    { have : (‚®Ö p, dist y (Œ¶ p) + dist x (Œ® p)) + dist y z =
            infi ((Œªt, t + dist y z) ‚àò (Œªp, dist y (Œ¶ p) + dist x (Œ® p))),
      { refine map_cinfi_of_continuous_at_of_monotone (continuous_at_id.add continuous_at_const) _
          (B _ _),
        intros x y hx, simpa },
      rw [this, comp],
      refine cinfi_le_cinfi (B _ _) (Œªp, _),
      calc
        dist z (Œ¶ p) + dist x (Œ® p) ‚â§ (dist y z + dist y (Œ¶ p)) + dist x (Œ® p) :
          add_le_add (dist_triangle_left _ _ _) (le_refl _)
        ... = dist y (Œ¶ p) + dist x (Œ® p) + dist y z : by ring },
    linarith
  end
| (inr x) (inr y) (inl z) := begin
    have B : ‚àÄ a b, bdd_below (range (Œª (p : Z), dist a (Œ¶ p) + dist b (Œ® p))) :=
      Œªa b, ‚ü®0, forall_range_iff.2 (Œªp, add_nonneg dist_nonneg dist_nonneg)‚ü©,
    unfold glue_dist,
    have : (‚®Ö p, dist z (Œ¶ p) + dist x (Œ® p)) ‚â§ dist x y + ‚®Ö p, dist z (Œ¶ p) + dist y (Œ® p),
    { have : dist x y + (‚®Ö p, dist z (Œ¶ p) + dist y (Œ® p)) =
            infi ((Œªt, dist x y + t) ‚àò (Œªp, dist z (Œ¶ p) + dist y (Œ® p))),
      { refine map_cinfi_of_continuous_at_of_monotone (continuous_at_const.add continuous_at_id) _
          (B _ _),
        intros x y hx, simpa },
      rw [this, comp],
      refine cinfi_le_cinfi (B _ _) (Œªp, _),
      calc
        dist z (Œ¶ p) + dist x (Œ® p) ‚â§ dist z (Œ¶ p) + (dist x y + dist y (Œ® p)) :
          add_le_add (le_refl _) (dist_triangle _ _ _)
        ... = dist x y + (dist z (Œ¶ p) + dist y (Œ® p)) : by ring },
    linarith
  end
| (inl x) (inl y) (inr z) := begin
    have B : ‚àÄ a b, bdd_below (range (Œª (p : Z), dist a (Œ¶ p) + dist b (Œ® p))) :=
      Œªa b, ‚ü®0, forall_range_iff.2 (Œªp, add_nonneg dist_nonneg dist_nonneg)‚ü©,
    unfold glue_dist,
    have : (‚®Ö p, dist x (Œ¶ p) + dist z (Œ® p)) ‚â§ dist x y + ‚®Ö p, dist y (Œ¶ p) + dist z (Œ® p),
    { have : dist x y + (‚®Ö p, dist y (Œ¶ p) + dist z (Œ® p)) =
            infi ((Œªt, dist x y + t) ‚àò (Œªp, dist y (Œ¶ p) + dist z (Œ® p))),
      { refine map_cinfi_of_continuous_at_of_monotone (continuous_at_const.add continuous_at_id) _
          (B _ _),
        intros x y hx, simpa },
      rw [this, comp],
      refine cinfi_le_cinfi (B _ _) (Œªp, _),
      calc
        dist x (Œ¶ p) + dist z (Œ® p) ‚â§ (dist x y + dist y (Œ¶ p)) + dist z (Œ® p) :
          add_le_add (dist_triangle _ _ _) (le_refl _)
        ... = dist x y + (dist y (Œ¶ p) + dist z (Œ® p)) : by ring },
    linarith
  end
| (inl x) (inr y) (inr z) := begin
    have B : ‚àÄ a b, bdd_below (range (Œª (p : Z), dist a (Œ¶ p) + dist b (Œ® p))) :=
      Œªa b, ‚ü®0, forall_range_iff.2 (Œªp, add_nonneg dist_nonneg dist_nonneg)‚ü©,
    unfold glue_dist,
    have : (‚®Ö p, dist x (Œ¶ p) + dist z (Œ® p)) ‚â§ (‚®Ö p, dist x (Œ¶ p) + dist y (Œ® p)) + dist y z,
    { have : (‚®Ö p, dist x (Œ¶ p) + dist y (Œ® p)) + dist y z =
            infi ((Œªt, t + dist y z) ‚àò (Œªp, dist x (Œ¶ p) + dist y (Œ® p))),
      { refine map_cinfi_of_continuous_at_of_monotone (continuous_at_id.add continuous_at_const) _
          (B _ _),
        intros x y hx, simpa },
      rw [this, comp],
      refine cinfi_le_cinfi (B _ _) (Œªp, _),
      calc
        dist x (Œ¶ p) + dist z (Œ® p) ‚â§ dist x (Œ¶ p) + (dist y z + dist y (Œ® p)) :
          add_le_add (le_refl _) (dist_triangle_left _ _ _)
        ... = dist x (Œ¶ p) + dist y (Œ® p) + dist y z : by ring },
    linarith
  end
| (inl x) (inr y) (inl z) := le_of_forall_pos_le_add $ ŒªŒ¥ Œ¥pos, begin
    obtain ‚ü®p, hp‚ü© : ‚àÉ p, dist x (Œ¶ p) + dist y (Œ® p) < (‚®Ö p, dist x (Œ¶ p) + dist y (Œ® p)) + Œ¥ / 2,
      from exists_lt_of_cinfi_lt (by linarith),
    obtain ‚ü®q, hq‚ü© : ‚àÉ q, dist z (Œ¶ q) + dist y (Œ® q) < (‚®Ö p, dist z (Œ¶ p) + dist y (Œ® p)) + Œ¥ / 2,
      from exists_lt_of_cinfi_lt (by linarith),
    have : dist (Œ¶ p) (Œ¶ q) ‚â§ dist (Œ® p) (Œ® q) + 2 * Œµ,
      { have := le_trans (le_abs_self _) (H p q), by linarith },
    calc dist x z ‚â§ dist x (Œ¶ p) + dist (Œ¶ p) (Œ¶ q) + dist (Œ¶ q) z : dist_triangle4 _ _ _ _
      ... ‚â§ dist x (Œ¶ p) + dist (Œ® p) (Œ® q) + dist z (Œ¶ q) + 2 * Œµ : by rw [dist_comm z]; linarith
      ... ‚â§ dist x (Œ¶ p) + (dist y (Œ® p) + dist y (Œ® q)) + dist z (Œ¶ q) + 2 * Œµ :
        add_le_add (add_le_add (add_le_add (le_refl _) (dist_triangle_left _ _ _)) le_rfl) le_rfl
      ... ‚â§ ((‚®Ö p, dist x (Œ¶ p) + dist y (Œ® p)) + Œµ) +
            ((‚®Ö p, dist z (Œ¶ p) + dist y (Œ® p)) + Œµ) + Œ¥ : by linarith
  end
| (inr x) (inl y) (inr z) := le_of_forall_pos_le_add $ ŒªŒ¥ Œ¥pos, begin
    obtain ‚ü®p, hp‚ü© : ‚àÉ p, dist y (Œ¶ p) + dist x (Œ® p) < (‚®Ö p, dist y (Œ¶ p) + dist x (Œ® p)) + Œ¥ / 2,
      from exists_lt_of_cinfi_lt (by linarith),
    obtain ‚ü®q, hq‚ü© : ‚àÉ q, dist y (Œ¶ q) + dist z (Œ® q) < (‚®Ö p, dist y (Œ¶ p) + dist z (Œ® p)) + Œ¥ / 2,
      from exists_lt_of_cinfi_lt (by linarith),
    have : dist (Œ® p) (Œ® q) ‚â§ dist (Œ¶ p) (Œ¶ q) + 2 * Œµ,
      { have := le_trans (neg_le_abs_self _) (H p q), by linarith },
    calc dist x z ‚â§ dist x (Œ® p) + dist (Œ® p) (Œ® q) + dist (Œ® q) z : dist_triangle4 _ _ _ _
      ... ‚â§ dist x (Œ® p) + dist (Œ¶ p) (Œ¶ q) + dist z (Œ® q) + 2 * Œµ : by rw [dist_comm z]; linarith
      ... ‚â§ dist x (Œ® p) + (dist y (Œ¶ p) + dist y (Œ¶ q)) + dist z (Œ® q) + 2 * Œµ :
        add_le_add (add_le_add (add_le_add le_rfl (dist_triangle_left _ _ _)) le_rfl) le_rfl
      ... ‚â§ ((‚®Ö p, dist y (Œ¶ p) + dist x (Œ® p)) + Œµ) +
            ((‚®Ö p, dist y (Œ¶ p) + dist z (Œ® p)) + Œµ) + Œ¥ : by linarith
  end

private lemma glue_eq_of_dist_eq_zero (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (Œµ0 : 0 < Œµ) :
  ‚àÄ p q : X ‚äï Y, glue_dist Œ¶ Œ® Œµ p q = 0 ‚Üí p = q
| (inl x) (inl y) h := by rw eq_of_dist_eq_zero h
| (inl x) (inr y) h := begin
    have : 0 ‚â§ (‚®Ö p, dist x (Œ¶ p) + dist y (Œ® p)) :=
      le_cinfi (Œªp, by simpa using add_le_add (@dist_nonneg _ _ x _) (@dist_nonneg _ _ y _)),
    have : 0 + Œµ ‚â§ glue_dist Œ¶ Œ® Œµ (inl x) (inr y) := add_le_add this (le_refl Œµ),
    exfalso,
    linarith
  end
| (inr x) (inl y) h := begin
    have : 0 ‚â§ ‚®Ö p, dist y (Œ¶ p) + dist x (Œ® p) :=
      le_cinfi (Œªp, by simpa [add_comm]
                         using add_le_add (@dist_nonneg _ _ x _) (@dist_nonneg _ _ y _)),
    have : 0 + Œµ ‚â§ glue_dist Œ¶ Œ® Œµ (inr x) (inl y) := add_le_add this (le_refl Œµ),
    exfalso,
    linarith
  end
| (inr x) (inr y) h := by rw eq_of_dist_eq_zero h

/-- Given two maps `Œ¶` and `Œ®` intro metric spaces `X` and `Y` such that the distances between
`Œ¶ p` and `Œ¶ q`, and between `Œ® p` and `Œ® q`, coincide up to `2 Œµ` where `Œµ > 0`, one can almost
glue the two spaces `X` and `Y` along the images of `Œ¶` and `Œ®`, so that `Œ¶ p` and `Œ® p` are
at distance `Œµ`. -/
def glue_metric_approx (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (Œµ0 : 0 < Œµ)
  (H : ‚àÄ p q, |dist (Œ¶ p) (Œ¶ q) - dist (Œ® p) (Œ® q)| ‚â§ 2 * Œµ) : metric_space (X ‚äï Y) :=
{ dist               := glue_dist Œ¶ Œ® Œµ,
  dist_self          := glue_dist_self Œ¶ Œ® Œµ,
  dist_comm          := glue_dist_comm Œ¶ Œ® Œµ,
  dist_triangle      := glue_dist_triangle Œ¶ Œ® Œµ H,
  eq_of_dist_eq_zero := glue_eq_of_dist_eq_zero Œ¶ Œ® Œµ Œµ0 }

end approx_gluing

section sum
/- A particular case of the previous construction is when one uses basepoints in `X` and `Y` and one
glues only along the basepoints, putting them at distance 1. We give a direct definition of
the distance, without infi, as it is easier to use in applications, and show that it is equal to
the gluing distance defined above to take advantage of the lemmas we have already proved. -/


variables {X : Type u} {Y : Type v} {Z : Type w}
variables [metric_space X] [metric_space Y] [inhabited X] [inhabited Y]
open sum (inl inr)

/-- Distance on a disjoint union. There are many (noncanonical) ways to put a distance compatible
with each factor.
If the two spaces are bounded, one can say for instance that each point in the first is at distance
`diam X + diam Y + 1` of each point in the second.
Instead, we choose a construction that works for unbounded spaces, but requires basepoints.
We embed isometrically each factor, set the basepoints at distance 1,
arbitrarily, and say that the distance from `a` to `b` is the sum of the distances of `a` and `b` to
their respective basepoints, plus the distance 1 between the basepoints.
Since there is an arbitrary choice in this construction, it is not an instance by default. -/
def sum.dist : X ‚äï Y ‚Üí X ‚äï Y ‚Üí ‚Ñù
| (inl a) (inl a') := dist a a'
| (inr b) (inr b') := dist b b'
| (inl a) (inr b)  := dist a (default X) + 1 + dist (default Y) b
| (inr b) (inl a)  := dist b (default Y) + 1 + dist (default X) a

lemma sum.dist_eq_glue_dist {p q : X ‚äï Y} :
  sum.dist p q = glue_dist (Œª_ : unit, default X) (Œª_ : unit, default Y) 1 p q :=
by cases p; cases q; refl <|> simp [sum.dist, glue_dist, dist_comm, add_comm, add_left_comm]

private lemma sum.dist_comm (x y : X ‚äï Y) : sum.dist x y = sum.dist y x :=
by cases x; cases y; simp only [sum.dist, dist_comm, add_comm, add_left_comm]

lemma sum.one_dist_le {x : X} {y : Y} : 1 ‚â§ sum.dist (inl x) (inr y) :=
le_trans (le_add_of_nonneg_right dist_nonneg) $
add_le_add_right (le_add_of_nonneg_left dist_nonneg) _

lemma sum.one_dist_le' {x : X} {y : Y} : 1 ‚â§ sum.dist (inr y) (inl x) :=
by rw sum.dist_comm; exact sum.one_dist_le

private lemma sum.mem_uniformity (s : set ((X ‚äï Y) √ó (X ‚äï Y))) :
  s ‚àà ùì§ (X ‚äï Y) ‚Üî ‚àÉ Œµ > 0, ‚àÄ a b, sum.dist a b < Œµ ‚Üí (a, b) ‚àà s :=
begin
  split,
  { rintro ‚ü®hsX, hsY‚ü©,
    rcases mem_uniformity_dist.1 hsX with ‚ü®ŒµX, ŒµX0, hX‚ü©,
    rcases mem_uniformity_dist.1 hsY with ‚ü®ŒµY, ŒµY0, hY‚ü©,
    refine ‚ü®min (min ŒµX ŒµY) 1, lt_min (lt_min ŒµX0 ŒµY0) zero_lt_one, _‚ü©,
    rintro (a|a) (b|b) h,
    { exact hX (lt_of_lt_of_le h (le_trans (min_le_left _ _) (min_le_left _ _))) },
    { cases not_le_of_lt (lt_of_lt_of_le h (min_le_right _ _)) sum.one_dist_le },
    { cases not_le_of_lt (lt_of_lt_of_le h (min_le_right _ _)) sum.one_dist_le' },
    { exact hY (lt_of_lt_of_le h (le_trans (min_le_left _ _) (min_le_right _ _))) } },
  { rintro ‚ü®Œµ, Œµ0, H‚ü©,
    split; rw [filter.mem_sets, filter.mem_map, mem_uniformity_dist];
      exact ‚ü®Œµ, Œµ0, Œª x y h, H _ _ (by exact h)‚ü© }
end

/-- The distance on the disjoint union indeed defines a metric space. All the distance properties
follow from our choice of the distance. The harder work is to show that the uniform structure
defined by the distance coincides with the disjoint union uniform structure. -/
def metric_space_sum : metric_space (X ‚äï Y) :=
{ dist               := sum.dist,
  dist_self          := Œªx, by cases x; simp only [sum.dist, dist_self],
  dist_comm          := sum.dist_comm,
  dist_triangle      := Œªp q r,
    by simp only [dist, sum.dist_eq_glue_dist]; exact glue_dist_triangle _ _ _ (by norm_num) _ _ _,
  eq_of_dist_eq_zero := Œªp q,
    by simp only [dist, sum.dist_eq_glue_dist]; exact glue_eq_of_dist_eq_zero _ _ _ zero_lt_one _ _,
  to_uniform_space   := sum.uniform_space,
  uniformity_dist    := uniformity_dist_of_mem_uniformity _ _ sum.mem_uniformity }

local attribute [instance] metric_space_sum

lemma sum.dist_eq {x y : X ‚äï Y} : dist x y = sum.dist x y := rfl

/-- The left injection of a space in a disjoint union in an isometry -/
lemma isometry_on_inl : isometry (sum.inl : X ‚Üí (X ‚äï Y)) :=
isometry_emetric_iff_metric.2 $ Œªx y, rfl

/-- The right injection of a space in a disjoint union in an isometry -/
lemma isometry_on_inr : isometry (sum.inr : Y ‚Üí (X ‚äï Y)) :=
isometry_emetric_iff_metric.2 $ Œªx y, rfl

end sum

section gluing
/- Exact gluing of two metric spaces along isometric subsets. -/

variables {X : Type u} {Y : Type v} {Z : Type w}
variables [nonempty Z] [metric_space Z] [metric_space X] [metric_space Y]
          {Œ¶ : Z ‚Üí X} {Œ® : Z ‚Üí Y} {Œµ : ‚Ñù}
open _root_.sum (inl inr)
local attribute [instance] pseudo_metric.dist_setoid

/-- Given two isometric embeddings `Œ¶ : Z ‚Üí X` and `Œ® : Z ‚Üí Y`, we define a pseudo metric space
structure on `X ‚äï Y` by declaring that `Œ¶ x` and `Œ® x` are at distance `0`. -/
def glue_premetric (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) : pseudo_metric_space (X ‚äï Y) :=
{ dist          := glue_dist Œ¶ Œ® 0,
  dist_self     := glue_dist_self Œ¶ Œ® 0,
  dist_comm     := glue_dist_comm Œ¶ Œ® 0,
  dist_triangle := glue_dist_triangle Œ¶ Œ® 0 $ Œªp q, by rw [hŒ¶.dist_eq, hŒ®.dist_eq]; simp }

/-- Given two isometric embeddings `Œ¶ : Z ‚Üí X` and `Œ® : Z ‚Üí Y`, we define a
space  `glue_space hŒ¶ hŒ®` by identifying in `X ‚äï Y` the points `Œ¶ x` and `Œ® x`. -/
def glue_space (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) : Type* :=
@pseudo_metric_quot _ (glue_premetric hŒ¶ hŒ®)

instance metric_space_glue_space (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) :
  metric_space (glue_space hŒ¶ hŒ®) :=
@metric_space_quot _ (glue_premetric hŒ¶ hŒ®)

/-- The canonical map from `X` to the space obtained by gluing isometric subsets in `X` and `Y`. -/
def to_glue_l (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) (x : X) : glue_space hŒ¶ hŒ® :=
by letI : pseudo_metric_space (X ‚äï Y) := glue_premetric hŒ¶ hŒ®; exact ‚ü¶inl x‚üß

/-- The canonical map from `Y` to the space obtained by gluing isometric subsets in `X` and `Y`. -/
def to_glue_r (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) (y : Y) : glue_space hŒ¶ hŒ® :=
by letI : pseudo_metric_space (X ‚äï Y) := glue_premetric hŒ¶ hŒ®; exact ‚ü¶inr y‚üß

instance inhabited_left (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) [inhabited X] :
  inhabited (glue_space hŒ¶ hŒ®) :=
‚ü®to_glue_l _ _ (default _)‚ü©

instance inhabited_right (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) [inhabited Y] :
  inhabited (glue_space hŒ¶ hŒ®) :=
‚ü®to_glue_r _ _ (default _)‚ü©

lemma to_glue_commute (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) :
  (to_glue_l hŒ¶ hŒ®) ‚àò Œ¶ = (to_glue_r hŒ¶ hŒ®) ‚àò Œ® :=
begin
  letI : pseudo_metric_space (X ‚äï Y) := glue_premetric hŒ¶ hŒ®,
  funext,
  simp only [comp, to_glue_l, to_glue_r, quotient.eq],
  exact glue_dist_glued_points Œ¶ Œ® 0 x
end

lemma to_glue_l_isometry (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) : isometry (to_glue_l hŒ¶ hŒ®) :=
isometry_emetric_iff_metric.2 $ Œª_ _, rfl

lemma to_glue_r_isometry (hŒ¶ : isometry Œ¶) (hŒ® : isometry Œ®) : isometry (to_glue_r hŒ¶ hŒ®) :=
isometry_emetric_iff_metric.2 $ Œª_ _, rfl

end gluing --section

section inductive_limit
/- In this section, we define the inductive limit of
     f 0        f 1        f 2        f 3
X 0 -----> X 1 -----> X 2 -----> X 3 -----> ...
where the X n are metric spaces and f n isometric embeddings. We do it by defining a premetric
space structure on Œ£ n, X n, where the predistance dist x y is obtained by pushing x and y in a
common X k using composition by the f n, and taking the distance there. This does not depend on
the choice of k as the f n are isometries. The metric space associated to this premetric space
is the desired inductive limit.-/
open nat

variables {X : ‚Ñï ‚Üí Type u} [‚àÄ n, metric_space (X n)] {f : Œ† n, X n ‚Üí X (n+1)}

/-- Predistance on the disjoint union `Œ£ n, X n`. -/
def inductive_limit_dist (f : Œ† n, X n ‚Üí X (n+1)) (x y : Œ£ n, X n) : ‚Ñù :=
dist (le_rec_on (le_max_left  x.1 y.1) f x.2 : X (max x.1 y.1))
     (le_rec_on (le_max_right x.1 y.1) f y.2 : X (max x.1 y.1))

/-- The predistance on the disjoint union `Œ£ n, X n` can be computed in any `X k` for large
enough `k`. -/
lemma inductive_limit_dist_eq_dist (I : ‚àÄ n, isometry (f n))
  (x y : Œ£ n, X n) (m : ‚Ñï) : ‚àÄ hx : x.1 ‚â§ m, ‚àÄ hy : y.1 ‚â§ m,
  inductive_limit_dist f x y = dist (le_rec_on hx f x.2 : X m) (le_rec_on hy f y.2 : X m) :=
begin
  induction m with m hm,
  { assume hx hy,
    have A : max x.1 y.1 = 0, { rw [nonpos_iff_eq_zero.1 hx, nonpos_iff_eq_zero.1 hy], simp },
    unfold inductive_limit_dist,
    congr; simp only [A] },
  { assume hx hy,
    by_cases h : max x.1 y.1 = m.succ,
    { unfold inductive_limit_dist,
      congr; simp only [h] },
    { have : max x.1 y.1 ‚â§ succ m := by simp [hx, hy],
      have : max x.1 y.1 ‚â§ m := by simpa [h] using of_le_succ this,
      have xm : x.1 ‚â§ m := le_trans (le_max_left _ _) this,
      have ym : y.1 ‚â§ m := le_trans (le_max_right _ _) this,
      rw [le_rec_on_succ xm, le_rec_on_succ ym, (I m).dist_eq],
      exact hm xm ym }}
end

/-- Premetric space structure on `Œ£ n, X n`.-/
def inductive_premetric (I : ‚àÄ n, isometry (f n)) :
  pseudo_metric_space (Œ£ n, X n) :=
{ dist          := inductive_limit_dist f,
  dist_self     := Œªx, by simp [dist, inductive_limit_dist],
  dist_comm     := Œªx y, begin
    let m := max x.1 y.1,
    have hx : x.1 ‚â§ m := le_max_left _ _,
    have hy : y.1 ‚â§ m := le_max_right _ _,
    unfold dist,
    rw [inductive_limit_dist_eq_dist I x y m hx hy, inductive_limit_dist_eq_dist I y x m hy hx,
        dist_comm]
  end,
  dist_triangle := Œªx y z, begin
    let m := max (max x.1 y.1) z.1,
    have hx : x.1 ‚â§ m := le_trans (le_max_left _ _) (le_max_left _ _),
    have hy : y.1 ‚â§ m := le_trans (le_max_right _ _) (le_max_left _ _),
    have hz : z.1 ‚â§ m := le_max_right _ _,
    calc inductive_limit_dist f x z
      = dist (le_rec_on hx f x.2 : X m) (le_rec_on hz f z.2 : X m) :
        inductive_limit_dist_eq_dist I x z m hx hz
      ... ‚â§ dist (le_rec_on hx f x.2 : X m) (le_rec_on hy f y.2 : X m)
          + dist (le_rec_on hy f y.2 : X m) (le_rec_on hz f z.2 : X m) :
        dist_triangle _ _ _
      ... = inductive_limit_dist f x y + inductive_limit_dist f y z :
         by rw [inductive_limit_dist_eq_dist I x y m hx hy,
                inductive_limit_dist_eq_dist I y z m hy hz]
  end }

local attribute [instance] inductive_premetric pseudo_metric.dist_setoid

/-- The type giving the inductive limit in a metric space context. -/
def inductive_limit (I : ‚àÄ n, isometry (f n)) : Type* :=
@pseudo_metric_quot _ (inductive_premetric I)

/-- Metric space structure on the inductive limit. -/
instance metric_space_inductive_limit (I : ‚àÄ n, isometry (f n)) :
  metric_space (inductive_limit I) :=
@metric_space_quot _ (inductive_premetric I)

/-- Mapping each `X n` to the inductive limit. -/
def to_inductive_limit (I : ‚àÄ n, isometry (f n)) (n : ‚Ñï) (x : X n) : metric.inductive_limit I :=
by letI : pseudo_metric_space (Œ£ n, X n) := inductive_premetric I; exact ‚ü¶sigma.mk n x‚üß

instance (I : ‚àÄ n, isometry (f n)) [inhabited (X 0)] : inhabited (inductive_limit I) :=
‚ü®to_inductive_limit _ 0 (default _)‚ü©

/-- The map `to_inductive_limit n` mapping `X n` to the inductive limit is an isometry. -/
lemma to_inductive_limit_isometry (I : ‚àÄ n, isometry (f n)) (n : ‚Ñï) :
  isometry (to_inductive_limit I n) := isometry_emetric_iff_metric.2 $ Œªx y,
begin
  change inductive_limit_dist f ‚ü®n, x‚ü© ‚ü®n, y‚ü© = dist x y,
  rw [inductive_limit_dist_eq_dist I ‚ü®n, x‚ü© ‚ü®n, y‚ü© n (le_refl n) (le_refl n),
      le_rec_on_self, le_rec_on_self]
end

/-- The maps `to_inductive_limit n` are compatible with the maps `f n`. -/
lemma to_inductive_limit_commute (I : ‚àÄ n, isometry (f n)) (n : ‚Ñï) :
  (to_inductive_limit I n.succ) ‚àò (f n) = to_inductive_limit I n :=
begin
  funext,
  simp only [comp, to_inductive_limit, quotient.eq],
  show inductive_limit_dist f ‚ü®n.succ, f n x‚ü© ‚ü®n, x‚ü© = 0,
  { rw [inductive_limit_dist_eq_dist I ‚ü®n.succ, f n x‚ü© ‚ü®n, x‚ü© n.succ,
        le_rec_on_self, le_rec_on_succ, le_rec_on_self, dist_self],
    exact le_refl _,
    exact le_refl _,
    exact le_succ _ }
end

end inductive_limit --section
end metric --namespace
