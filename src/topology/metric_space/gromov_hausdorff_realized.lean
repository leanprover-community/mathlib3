/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: S√©bastien Gou√´zel

Construction of a good coupling between nonempty compact metric spaces, minimizing
their Hausdorff distance. This construction is instrumental to study the Gromov-Hausdorff
distance between nonempty compact metric spaces -/
import topology.metric_space.gluing
import topology.metric_space.hausdorff_distance

noncomputable theory
open_locale classical
open_locale topological_space
universes u v w

open classical set function topological_space filter metric quotient
open bounded_continuous_function
open sum (inl inr)

local attribute [instance] metric_space_sum

namespace Gromov_Hausdorff

section Gromov_Hausdorff_realized
/- This section shows that the Gromov-Hausdorff distance
is realized. For this, we consider candidate distances on the disjoint union
Œ± ‚äï Œ≤ of two compact nonempty metric spaces, almost realizing the Gromov-Hausdorff
distance, and show that they form a compact family by applying Arzela-Ascoli
theorem. The existence of a minimizer follows. -/

section definitions
variables (Œ± : Type u) (Œ≤ : Type v)
  [metric_space Œ±] [compact_space Œ±] [nonempty Œ±]
  [metric_space Œ≤] [compact_space Œ≤] [nonempty Œ≤]

@[reducible] private def prod_space_fun : Type* := ((Œ± ‚äï Œ≤) √ó (Œ± ‚äï Œ≤)) ‚Üí ‚Ñù
@[reducible] private def Cb : Type* := bounded_continuous_function ((Œ± ‚äï Œ≤) √ó (Œ± ‚äï Œ≤)) ‚Ñù

private def max_var : nnreal :=
2 * ‚ü®diam (univ : set Œ±), diam_nonneg‚ü© + 1 + 2 * ‚ü®diam (univ : set Œ≤), diam_nonneg‚ü©

private lemma one_le_max_var : 1 ‚â§ max_var Œ± Œ≤ := calc
  (1 : real) = 2 * 0 + 1 + 2 * 0 : by simp
  ... ‚â§ 2 * diam (univ : set Œ±) + 1 + 2 * diam (univ : set Œ≤) :
  by apply_rules [add_le_add, mul_le_mul_of_nonneg_left, diam_nonneg]; norm_num

/-- The set of functions on Œ± ‚äï Œ≤ that are candidates distances to realize the
minimum of the Hausdorff distances between Œ± and Œ≤ in a coupling -/
def candidates : set (prod_space_fun Œ± Œ≤) :=
  {f | (((((‚àÄx y : Œ±, f (sum.inl x, sum.inl y) = dist x y)
    ‚àß (‚àÄx y : Œ≤, f (sum.inr x, sum.inr y) = dist x y))
    ‚àß (‚àÄx y,     f (x, y) = f (y, x)))
    ‚àß (‚àÄx y z,   f (x, z) ‚â§ f (x, y) + f (y, z)))
    ‚àß (‚àÄx,       f (x, x) = 0))
    ‚àß (‚àÄx y,     f (x, y) ‚â§ max_var Œ± Œ≤) }

/-- Version of the set of candidates in bounded_continuous_functions, to apply
Arzela-Ascoli -/
private def candidates_b : set (Cb Œ± Œ≤) := {f : Cb Œ± Œ≤ | f.val ‚àà candidates Œ± Œ≤}

end definitions --section

section constructions

variables {Œ± : Type u} {Œ≤ : Type v}
[metric_space Œ±] [compact_space Œ±] [nonempty Œ±] [metric_space Œ≤] [compact_space Œ≤] [nonempty Œ≤]
{f : prod_space_fun Œ± Œ≤} {x y z t : Œ± ‚äï Œ≤}
local attribute [instance, priority 10] inhabited_of_nonempty'

private lemma max_var_bound : dist x y ‚â§ max_var Œ± Œ≤ := calc
  dist x y ‚â§ diam (univ : set (Œ± ‚äï Œ≤)) :
    dist_le_diam_of_mem (bounded_of_compact compact_univ) (mem_univ _) (mem_univ _)
  ... = diam (inl '' (univ : set Œ±) ‚à™ inr '' (univ : set Œ≤)) :
    by apply congr_arg; ext x y z; cases x; simp [mem_univ, mem_range_self]
  ... ‚â§ diam (inl '' (univ : set Œ±)) + dist (inl (default Œ±)) (inr (default Œ≤)) + diam (inr '' (univ : set Œ≤)) :
    diam_union (mem_image_of_mem _ (mem_univ _)) (mem_image_of_mem _ (mem_univ _))
  ... = diam (univ : set Œ±) + (dist (default Œ±) (default Œ±) + 1 + dist (default Œ≤) (default Œ≤)) + diam (univ : set Œ≤) :
    by { rw [isometry_on_inl.diam_image, isometry_on_inr.diam_image], refl }
  ... = 1 * diam (univ : set Œ±) + 1 + 1 * diam (univ : set Œ≤) : by simp
  ... ‚â§ 2 * diam (univ : set Œ±) + 1 + 2 * diam (univ : set Œ≤) :
  begin
    apply_rules [add_le_add, mul_le_mul_of_nonneg_right, diam_nonneg, le_refl],
    norm_num, norm_num
  end

private lemma candidates_symm (fA : f ‚àà candidates Œ± Œ≤) : f (x, y) = f (y ,x) := fA.1.1.1.2 x y

private lemma candidates_triangle (fA : f ‚àà candidates Œ± Œ≤) : f (x, z) ‚â§ f (x, y) + f (y, z) :=
  fA.1.1.2 x y z

private lemma candidates_refl (fA : f ‚àà candidates Œ± Œ≤) : f (x, x) = 0 := fA.1.2 x

private lemma candidates_nonneg (fA : f ‚àà candidates Œ± Œ≤) : 0 ‚â§ f (x, y) :=
begin
  have : 0 ‚â§ 2 * f (x, y) := calc
    0 = f (x, x) : (candidates_refl fA).symm
    ... ‚â§ f (x, y) + f (y, x) : candidates_triangle fA
    ... = f (x, y) + f (x, y) : by rw [candidates_symm fA]
    ... = 2 * f (x, y) : by ring,
  by linarith
end

private lemma candidates_dist_inl (fA : f ‚àà candidates Œ± Œ≤) (x y: Œ±) : f (inl x, inl y) = dist x y :=
fA.1.1.1.1.1 x y

private lemma candidates_dist_inr (fA : f ‚àà candidates Œ± Œ≤) (x y : Œ≤) : f (inr x, inr y) = dist x y :=
fA.1.1.1.1.2 x y

private lemma candidates_le_max_var (fA : f ‚àà candidates Œ± Œ≤) : f (x, y) ‚â§ max_var Œ± Œ≤ :=
fA.2 x y

/-- candidates are bounded by max_var Œ± Œ≤ -/
private lemma candidates_dist_bound  (fA : f ‚àà candidates Œ± Œ≤) :
  ‚àÄ {x y : Œ± ‚äï Œ≤}, f (x, y) ‚â§ max_var Œ± Œ≤ * dist x y
| (inl x) (inl y) := calc
    f (inl x, inl y) = dist x y : candidates_dist_inl fA x y
    ... = dist (inl x) (inl y) : by { rw @sum.dist_eq Œ± Œ≤, refl }
    ... = 1 * dist (inl x) (inl y) : by simp
    ... ‚â§ max_var Œ± Œ≤ * dist (inl x) (inl y) :
      mul_le_mul_of_nonneg_right (one_le_max_var Œ± Œ≤) dist_nonneg
| (inl x) (inr y) := calc
    f (inl x, inr y) ‚â§ max_var Œ± Œ≤ : candidates_le_max_var fA
    ... = max_var Œ± Œ≤ * 1 : by simp
    ... ‚â§ max_var Œ± Œ≤ * dist (inl x) (inr y) :
      mul_le_mul_of_nonneg_left sum.one_dist_le (le_trans (zero_le_one) (one_le_max_var Œ± Œ≤))
| (inr x) (inl y) := calc
    f (inr x, inl y) ‚â§ max_var Œ± Œ≤ : candidates_le_max_var fA
    ... = max_var Œ± Œ≤ * 1 : by simp
    ... ‚â§ max_var Œ± Œ≤ * dist (inl x) (inr y) :
      mul_le_mul_of_nonneg_left sum.one_dist_le (le_trans (zero_le_one) (one_le_max_var Œ± Œ≤))
| (inr x) (inr y) := calc
    f (inr x, inr y) = dist x y : candidates_dist_inr fA x y
    ... = dist (inr x) (inr y) : by { rw @sum.dist_eq Œ± Œ≤, refl }
    ... = 1 * dist (inr x) (inr y) : by simp
    ... ‚â§ max_var Œ± Œ≤ * dist (inr x) (inr y) :
      mul_le_mul_of_nonneg_right (one_le_max_var Œ± Œ≤) dist_nonneg

/-- Technical lemma to prove that candidates are Lipschitz -/
private lemma candidates_lipschitz_aux (fA : f ‚àà candidates Œ± Œ≤) : f (x, y) - f (z, t) ‚â§ 2 * max_var Œ± Œ≤ * dist (x, y) (z, t) :=
calc
  f (x, y) - f(z, t) ‚â§ f (x, t) + f (t, y) - f (z, t) : add_le_add_right (candidates_triangle fA) _
  ... ‚â§ (f (x, z) + f (z, t) + f(t, y)) - f (z, t) :
    add_le_add_right (add_le_add_right (candidates_triangle fA) _ ) _
  ... = f (x, z) + f (t, y) : by simp [sub_eq_add_neg, add_assoc]
  ... ‚â§ max_var Œ± Œ≤ * dist x z + max_var Œ± Œ≤ * dist t y :
    add_le_add (candidates_dist_bound fA) (candidates_dist_bound fA)
  ... ‚â§ max_var Œ± Œ≤ * max (dist x z) (dist t y) + max_var Œ± Œ≤ * max (dist x z) (dist t y) :
  begin
    apply add_le_add,
    apply mul_le_mul_of_nonneg_left (le_max_left (dist x z) (dist t y)) (le_trans zero_le_one (one_le_max_var Œ± Œ≤)),
    apply mul_le_mul_of_nonneg_left (le_max_right (dist x z) (dist t y)) (le_trans zero_le_one (one_le_max_var Œ± Œ≤)),
  end
  ... = 2 * max_var Œ± Œ≤ * max (dist x z) (dist y t) :
    by { simp [dist_comm], ring }
  ... = 2 * max_var Œ± Œ≤ * dist (x, y) (z, t) : by refl

/-- Candidates are Lipschitz -/
private lemma candidates_lipschitz (fA : f ‚àà candidates Œ± Œ≤) :
  lipschitz_with (2 * max_var Œ± Œ≤) f :=
begin
  apply lipschitz_with.of_dist_le_mul,
  rintros ‚ü®x, y‚ü© ‚ü®z, t‚ü©,
  rw real.dist_eq,
  apply abs_le_of_le_of_neg_le,
  { exact candidates_lipschitz_aux fA },
  { have : -(f (x, y) - f (z, t)) = f (z, t) - f (x, y), by ring,
    rw [this, dist_comm],
    exact candidates_lipschitz_aux fA }
end

/-- candidates give rise to elements of bounded_continuous_functions -/
def candidates_b_of_candidates (f : prod_space_fun Œ± Œ≤) (fA : f ‚àà candidates Œ± Œ≤) : Cb Œ± Œ≤ :=
bounded_continuous_function.mk_of_compact f (candidates_lipschitz fA).continuous

lemma candidates_b_of_candidates_mem (f : prod_space_fun Œ± Œ≤) (fA : f ‚àà candidates Œ± Œ≤) :
  candidates_b_of_candidates f fA ‚àà candidates_b Œ± Œ≤ := fA

/-- The distance on Œ± ‚äï Œ≤ is a candidate -/
private lemma dist_mem_candidates : (Œªp : (Œ± ‚äï Œ≤) √ó (Œ± ‚äï Œ≤), dist p.1 p.2) ‚àà candidates Œ± Œ≤ :=
begin
  simp only [candidates, dist_comm, forall_const, and_true, add_comm, eq_self_iff_true,
             and_self, sum.forall, set.mem_set_of_eq, dist_self],
  repeat { split
    <|> exact (Œªa y z, dist_triangle_left _ _ _)
    <|> exact (Œªx y, by refl)
    <|> exact (Œªx y, max_var_bound) }
end

def candidates_b_dist (Œ± : Type u) (Œ≤ : Type v) [metric_space Œ±] [compact_space Œ±] [inhabited Œ±]
  [metric_space Œ≤] [compact_space Œ≤] [inhabited Œ≤] : Cb Œ± Œ≤ := candidates_b_of_candidates _ dist_mem_candidates

lemma candidates_b_dist_mem_candidates_b : candidates_b_dist Œ± Œ≤ ‚àà candidates_b Œ± Œ≤ :=
candidates_b_of_candidates_mem _ _

private lemma candidates_b_nonempty : (candidates_b Œ± Œ≤).nonempty :=
‚ü®_,  candidates_b_dist_mem_candidates_b‚ü©

/-- To apply Arzela-Ascoli, we need to check that the set of candidates is closed and equicontinuous.
Equicontinuity follows from the Lipschitz control, we check closedness -/
private lemma closed_candidates_b : is_closed (candidates_b Œ± Œ≤) :=
begin
  have I1 : ‚àÄx y, is_closed {f : Cb Œ± Œ≤ | f (inl x, inl y) = dist x y} :=
    Œªx y, is_closed_eq continuous_evalx continuous_const,
  have I2 : ‚àÄx y, is_closed {f : Cb Œ± Œ≤ | f (inr x, inr y) = dist x y } :=
    Œªx y, is_closed_eq continuous_evalx continuous_const,
  have I3 : ‚àÄx y, is_closed {f : Cb Œ± Œ≤ | f (x, y) = f (y, x)} :=
    Œªx y, is_closed_eq continuous_evalx continuous_evalx,
  have I4 : ‚àÄx y z, is_closed {f : Cb Œ± Œ≤ | f (x, z) ‚â§ f (x, y) + f (y, z)} :=
    Œªx y z, is_closed_le continuous_evalx (continuous_evalx.add continuous_evalx),
  have I5 : ‚àÄx, is_closed {f : Cb Œ± Œ≤ | f (x, x) = 0} :=
    Œªx, is_closed_eq continuous_evalx continuous_const,
  have I6 : ‚àÄx y, is_closed {f : Cb Œ± Œ≤ | f (x, y) ‚â§ max_var Œ± Œ≤} :=
    Œªx y, is_closed_le continuous_evalx continuous_const,
  have : candidates_b Œ± Œ≤ = (‚ãÇx y, {f : Cb Œ± Œ≤ | f ((@inl Œ± Œ≤ x), (@inl Œ± Œ≤ y)) = dist x y})
               ‚à© (‚ãÇx y, {f : Cb Œ± Œ≤ | f ((@inr Œ± Œ≤ x), (@inr Œ± Œ≤ y)) = dist x y})
               ‚à© (‚ãÇx y, {f : Cb Œ± Œ≤ | f (x, y) = f (y, x)})
               ‚à© (‚ãÇx y z, {f : Cb Œ± Œ≤ | f (x, z) ‚â§ f (x, y) + f (y, z)})
               ‚à© (‚ãÇx, {f : Cb Œ± Œ≤ | f (x, x) = 0})
               ‚à© (‚ãÇx y, {f : Cb Œ± Œ≤ | f (x, y) ‚â§ max_var Œ± Œ≤}) :=
    begin ext, unfold candidates_b, unfold candidates, simp [-sum.forall], refl end,
  rw this,
  repeat { apply is_closed_inter _ _
       <|> apply is_closed_Inter _
       <|> apply I1 _ _
       <|> apply I2 _ _
       <|> apply I3 _ _
       <|> apply I4 _ _ _
       <|> apply I5 _
       <|> apply I6 _ _
       <|> assume x },
end

/-- Compactness of candidates (in bounded_continuous_functions) follows -/
private lemma compact_candidates_b : is_compact (candidates_b Œ± Œ≤) :=
begin
  refine arzela_ascoli‚ÇÇ (Icc 0 (max_var Œ± Œ≤)) compact_Icc (candidates_b Œ± Œ≤) closed_candidates_b _ _,
  { rintros f ‚ü®x1, x2‚ü© hf,
    simp only [set.mem_Icc],
    exact ‚ü®candidates_nonneg hf, candidates_le_max_var hf‚ü© },
  { refine equicontinuous_of_continuity_modulus (Œªt, 2 * max_var Œ± Œ≤ * t) _ _ _,
    { have : tendsto (Œª (t : ‚Ñù), 2 * (max_var Œ± Œ≤ : ‚Ñù) * t) (ùìù 0) (ùìù (2 * max_var Œ± Œ≤ * 0)) :=
        tendsto_const_nhds.mul tendsto_id,
      simpa using this },
    { assume x y f hf,
      exact (candidates_lipschitz hf).dist_le_mul _ _ } }
end

/-- We will then choose the candidate minimizing the Hausdorff distance. Except that we are not
in a metric space setting, so we need to define our custom version of Hausdorff distance,
called HD, and prove its basic properties. -/
def HD (f : Cb Œ± Œ≤) := max (‚®Ü x, ‚®Ö y, f (inl x, inr y)) (‚®Ü y, ‚®Ö x, f (inl x, inr y))

/- We will show that HD is continuous on bounded_continuous_functions, to deduce that its
minimum on the compact set candidates_b is attained. Since it is defined in terms of
infimum and supremum on ‚Ñù, which is only conditionnally complete, we will need all the time
to check that the defining sets are bounded below or above. This is done in the next few
technical lemmas -/

lemma HD_below_aux1 {f : Cb Œ± Œ≤} (C : ‚Ñù) {x : Œ±} :
  bdd_below (range (Œª (y : Œ≤), f (inl x, inr y) + C)) :=
let ‚ü®cf, hcf‚ü© := (real.bounded_iff_bdd_below_bdd_above.1 bounded_range).1 in
‚ü®cf + C, forall_range_iff.2 (Œªi, add_le_add_right ((Œªx, hcf (mem_range_self x)) _) _)‚ü©

private lemma HD_bound_aux1 (f : Cb Œ± Œ≤) (C : ‚Ñù) :
  bdd_above (range (Œª (x : Œ±), ‚®Ö y, f (inl x, inr y) + C)) :=
begin
  rcases (real.bounded_iff_bdd_below_bdd_above.1 bounded_range).2 with ‚ü®Cf, hCf‚ü©,
  refine ‚ü®Cf + C, forall_range_iff.2 (Œªx, _)‚ü©,
  calc (‚®Ö y, f (inl x, inr y) + C) ‚â§ f (inl x, inr (default Œ≤)) + C :
    cinfi_le (HD_below_aux1 C) (default Œ≤)
    ... ‚â§ Cf + C : add_le_add ((Œªx, hCf (mem_range_self x)) _) (le_refl _)
end

lemma HD_below_aux2 {f : Cb Œ± Œ≤} (C : ‚Ñù) {y : Œ≤} :
  bdd_below (range (Œª (x : Œ±), f (inl x, inr y) + C)) :=
let ‚ü®cf, hcf‚ü© := (real.bounded_iff_bdd_below_bdd_above.1 bounded_range).1 in
‚ü®cf + C, forall_range_iff.2 (Œªi, add_le_add_right ((Œªx, hcf (mem_range_self x)) _) _)‚ü©

private lemma HD_bound_aux2 (f : Cb Œ± Œ≤) (C : ‚Ñù) :
  bdd_above (range (Œª (y : Œ≤), ‚®Ö x, f (inl x, inr y) + C)) :=
begin
  rcases (real.bounded_iff_bdd_below_bdd_above.1 bounded_range).2 with ‚ü®Cf, hCf‚ü©,
  refine ‚ü®Cf + C, forall_range_iff.2 (Œªy, _)‚ü©,
  calc (‚®Ö x, f (inl x, inr y) + C) ‚â§ f (inl (default Œ±), inr y) + C :
    cinfi_le (HD_below_aux2 C) (default Œ±)
  ... ‚â§ Cf + C : add_le_add ((Œªx, hCf (mem_range_self x)) _) (le_refl _)
end

/-- Explicit bound on HD (dist). This means that when looking for minimizers it will
be sufficient to look for functions with HD(f) bounded by this bound. -/
lemma HD_candidates_b_dist_le :
  HD (candidates_b_dist Œ± Œ≤) ‚â§ diam (univ : set Œ±) + 1 + diam (univ : set Œ≤) :=
begin
  refine max_le (csupr_le (Œªx, _)) (csupr_le (Œªy, _)),
  { have A : (‚®Ö y, candidates_b_dist Œ± Œ≤ (inl x, inr y)) ‚â§
      candidates_b_dist Œ± Œ≤ (inl x, inr (default Œ≤)) :=
      cinfi_le (by simpa using HD_below_aux1 0) (default Œ≤),
    have B : dist (inl x) (inr (default Œ≤)) ‚â§ diam (univ : set Œ±) + 1 + diam (univ : set Œ≤) := calc
      dist (inl x) (inr (default Œ≤)) = dist x (default Œ±) + 1 + dist (default Œ≤) (default Œ≤) : rfl
      ... ‚â§ diam (univ : set Œ±) + 1 + diam (univ : set Œ≤) :
      begin
        apply add_le_add (add_le_add _ (le_refl _)),
        exact dist_le_diam_of_mem (bounded_of_compact (compact_univ)) (mem_univ _) (mem_univ _),
        exact dist_le_diam_of_mem (bounded_of_compact (compact_univ)) (mem_univ _) (mem_univ _)
      end,
    exact le_trans A B },
  { have A : (‚®Ö x, candidates_b_dist Œ± Œ≤ (inl x, inr y)) ‚â§
      candidates_b_dist Œ± Œ≤ (inl (default Œ±), inr y) :=
      cinfi_le (by simpa using HD_below_aux2 0) (default Œ±),
    have B : dist (inl (default Œ±)) (inr y) ‚â§ diam (univ : set Œ±) + 1 + diam (univ : set Œ≤) := calc
      dist (inl (default Œ±)) (inr y) = dist (default Œ±) (default Œ±) + 1 + dist (default Œ≤) y : rfl
      ... ‚â§ diam (univ : set Œ±) + 1 + diam (univ : set Œ≤) :
      begin
        apply add_le_add (add_le_add _ (le_refl _)),
        exact dist_le_diam_of_mem (bounded_of_compact (compact_univ)) (mem_univ _) (mem_univ _),
        exact dist_le_diam_of_mem (bounded_of_compact (compact_univ)) (mem_univ _) (mem_univ _)
      end,
    exact le_trans A B },
end

/- To check that HD is continuous, we check that it is Lipschitz. As HD is a max, we
prove separately inequalities controlling the two terms (relying too heavily on copy-paste...) -/
private lemma HD_lipschitz_aux1 (f g : Cb Œ± Œ≤) :
  (‚®Ü x, ‚®Ö y, f (inl x, inr y)) ‚â§ (‚®Ü x, ‚®Ö y, g (inl x, inr y)) + dist f g :=
begin
  rcases (real.bounded_iff_bdd_below_bdd_above.1 bounded_range).1 with ‚ü®cg, hcg‚ü©,
  have Hcg : ‚àÄx, cg ‚â§ g x := Œªx, hcg (mem_range_self x),
  rcases (real.bounded_iff_bdd_below_bdd_above.1 bounded_range).1 with ‚ü®cf, hcf‚ü©,
  have Hcf : ‚àÄx, cf ‚â§ f x := Œªx, hcf (mem_range_self x),

  -- prove the inequality but with `dist f g` inside, by using inequalities comparing
  -- supr to supr and infi to infi
  have Z : (‚®Ü x, ‚®Ö y, f (inl x, inr y)) ‚â§ ‚®Ü x, ‚®Ö y, g (inl x, inr y) + dist f g :=
    csupr_le_csupr (HD_bound_aux1 _ (dist f g))
      (Œªx, cinfi_le_cinfi ‚ü®cf, forall_range_iff.2(Œªi, Hcf _)‚ü© (Œªy, coe_le_coe_add_dist)),
  -- move the `dist f g` out of the infimum and the supremum, arguing that continuous monotone maps
  -- (here the addition of `dist f g`) preserve infimum and supremum
  have E1 : ‚àÄx, (‚®Ö y, g (inl x, inr y)) + dist f g = ‚®Ö y, g (inl x, inr y) + dist f g,
  { assume x,
    refine map_cinfi_of_continuous_at_of_monotone (continuous_at_id.add continuous_at_const) _ _,
    { assume x y hx, simpa },
    { show bdd_below (range (Œª (y : Œ≤), g (inl x, inr y))),
        from ‚ü®cg, forall_range_iff.2(Œªi, Hcg _)‚ü© } },
  have E2 : (‚®Ü x, ‚®Ö y, g (inl x, inr y)) + dist f g = ‚®Ü x, (‚®Ö y, g (inl x, inr y)) + dist f g,
  { refine map_csupr_of_continuous_at_of_monotone (continuous_at_id.add continuous_at_const) _ _,
    { assume x y hx, simpa },
    { by simpa using HD_bound_aux1 _ 0 } },
  -- deduce the result from the above two steps
  simpa [E2, E1, function.comp]
end

private lemma HD_lipschitz_aux2 (f g : Cb Œ± Œ≤) :
  (‚®Ü y, ‚®Ö x, f (inl x, inr y)) ‚â§ (‚®Ü y, ‚®Ö x, g (inl x, inr y)) + dist f g :=
begin
  rcases (real.bounded_iff_bdd_below_bdd_above.1 bounded_range).1 with ‚ü®cg, hcg‚ü©,
  have Hcg : ‚àÄx, cg ‚â§ g x := Œªx, hcg (mem_range_self x),
  rcases (real.bounded_iff_bdd_below_bdd_above.1 bounded_range).1 with ‚ü®cf, hcf‚ü©,
  have Hcf : ‚àÄx, cf ‚â§ f x := Œªx, hcf (mem_range_self x),

  -- prove the inequality but with `dist f g` inside, by using inequalities comparing
  -- supr to supr and infi to infi
  have Z : (‚®Ü y, ‚®Ö x, f (inl x, inr y)) ‚â§ ‚®Ü y, ‚®Ö x, g (inl x, inr y) + dist f g :=
    csupr_le_csupr (HD_bound_aux2 _ (dist f g))
      (Œªy, cinfi_le_cinfi  ‚ü®cf, forall_range_iff.2(Œªi, Hcf _)‚ü© (Œªy, coe_le_coe_add_dist)),
  -- move the `dist f g` out of the infimum and the supremum, arguing that continuous monotone maps
  -- (here the addition of `dist f g`) preserve infimum and supremum
  have E1 : ‚àÄy, (‚®Ö x, g (inl x, inr y)) + dist f g = ‚®Ö x, g (inl x, inr y) + dist f g,
  { assume y,
    refine map_cinfi_of_continuous_at_of_monotone (continuous_at_id.add continuous_at_const) _ _,
    { assume x y hx, simpa },
    { show bdd_below (range (Œªx:Œ±, g (inl x, inr y))),
        from ‚ü®cg, forall_range_iff.2 (Œªi, Hcg _)‚ü© } },
  have E2 : (‚®Ü y, ‚®Ö x, g (inl x, inr y)) + dist f g = ‚®Ü y, (‚®Ö x, g (inl x, inr y)) + dist f g,
  { refine map_csupr_of_continuous_at_of_monotone (continuous_at_id.add continuous_at_const) _ _,
    { assume x y hx, simpa },
    { by simpa using HD_bound_aux2 _ 0 } },
  -- deduce the result from the above two steps
  simpa [E2, E1]
end

private lemma HD_lipschitz_aux3 (f g : Cb Œ± Œ≤) : HD f ‚â§ HD g + dist f g :=
max_le (le_trans (HD_lipschitz_aux1 f g) (add_le_add_right (le_max_left _ _) _))
       (le_trans (HD_lipschitz_aux2 f g) (add_le_add_right (le_max_right _ _) _))

/-- Conclude that HD, being Lipschitz, is continuous -/
private lemma HD_continuous : continuous (HD : Cb Œ± Œ≤ ‚Üí ‚Ñù) :=
lipschitz_with.continuous (lipschitz_with.of_le_add HD_lipschitz_aux3)

end constructions --section

section consequences
variables (Œ± : Type u) (Œ≤ : Type v) [metric_space Œ±] [compact_space Œ±] [nonempty Œ±] [metric_space Œ≤]
  [compact_space Œ≤] [nonempty Œ≤]

/- Now that we have proved that the set of candidates is compact, and that HD is continuous,
we can finally select a candidate minimizing HD. This will be the candidate realizing the
optimal coupling. -/
private lemma exists_minimizer : ‚àÉf ‚àà candidates_b Œ± Œ≤, ‚àÄg ‚àà candidates_b Œ± Œ≤, HD f ‚â§ HD g :=
compact_candidates_b.exists_forall_le candidates_b_nonempty HD_continuous.continuous_on

private definition optimal_GH_dist : Cb Œ± Œ≤ := classical.some (exists_minimizer Œ± Œ≤)

private lemma optimal_GH_dist_mem_candidates_b : optimal_GH_dist Œ± Œ≤ ‚àà candidates_b Œ± Œ≤ :=
by cases (classical.some_spec (exists_minimizer Œ± Œ≤)); assumption

private lemma HD_optimal_GH_dist_le (g : Cb Œ± Œ≤) (hg : g ‚àà candidates_b Œ± Œ≤) : HD (optimal_GH_dist Œ± Œ≤) ‚â§ HD g :=
let ‚ü®Z1, Z2‚ü© := classical.some_spec (exists_minimizer Œ± Œ≤) in Z2 g hg

/-- With the optimal candidate, construct a premetric space structure on Œ± ‚äï Œ≤, on which the
predistance is given by the candidate. Then, we will identify points at 0 predistance
to obtain a genuine metric space -/
def premetric_optimal_GH_dist : premetric_space (Œ± ‚äï Œ≤) :=
{ dist := Œªp q, optimal_GH_dist Œ± Œ≤ (p, q),
  dist_self := Œªx, candidates_refl (optimal_GH_dist_mem_candidates_b Œ± Œ≤),
  dist_comm := Œªx y, candidates_symm (optimal_GH_dist_mem_candidates_b Œ± Œ≤),
  dist_triangle := Œªx y z, candidates_triangle (optimal_GH_dist_mem_candidates_b Œ± Œ≤) }

local attribute [instance] premetric_optimal_GH_dist premetric.dist_setoid

/-- A metric space which realizes the optimal coupling between Œ± and Œ≤ -/
@[derive [metric_space]] definition optimal_GH_coupling : Type* :=
premetric.metric_quot (Œ± ‚äï Œ≤)

/-- Injection of Œ± in the optimal coupling between Œ± and Œ≤ -/
def optimal_GH_injl (x : Œ±) : optimal_GH_coupling Œ± Œ≤ := ‚ü¶inl x‚üß

/-- The injection of Œ± in the optimal coupling between Œ± and Œ≤ is an isometry. -/
lemma isometry_optimal_GH_injl : isometry (optimal_GH_injl Œ± Œ≤) :=
begin
  refine isometry_emetric_iff_metric.2 (Œªx y, _),
  change dist ‚ü¶inl x‚üß ‚ü¶inl y‚üß = dist x y,
  exact candidates_dist_inl (optimal_GH_dist_mem_candidates_b Œ± Œ≤) _ _,
end

/-- Injection of Œ≤  in the optimal coupling between Œ± and Œ≤ -/
def optimal_GH_injr (y : Œ≤) : optimal_GH_coupling Œ± Œ≤ := ‚ü¶inr y‚üß

/-- The injection of Œ≤ in the optimal coupling between Œ± and Œ≤ is an isometry. -/
lemma isometry_optimal_GH_injr : isometry (optimal_GH_injr Œ± Œ≤) :=
begin
  refine isometry_emetric_iff_metric.2 (Œªx y, _),
  change dist ‚ü¶inr x‚üß ‚ü¶inr y‚üß = dist x y,
  exact candidates_dist_inr (optimal_GH_dist_mem_candidates_b Œ± Œ≤) _ _,
end

/-- The optimal coupling between two compact spaces Œ± and Œ≤ is still a compact space -/
instance compact_space_optimal_GH_coupling : compact_space (optimal_GH_coupling Œ± Œ≤) :=
‚ü®begin
  have : (univ : set (optimal_GH_coupling Œ± Œ≤)) =
           (optimal_GH_injl Œ± Œ≤ '' univ) ‚à™ (optimal_GH_injr Œ± Œ≤ '' univ),
  { refine subset.antisymm (Œªxc hxc, _) (subset_univ _),
    rcases quotient.exists_rep xc with ‚ü®x, hx‚ü©,
    cases x; rw ‚Üê hx,
    { have : ‚ü¶inl x‚üß = optimal_GH_injl Œ± Œ≤ x := rfl,
      rw this,
      exact mem_union_left _ (mem_image_of_mem _ (mem_univ _)) },
    { have : ‚ü¶inr x‚üß = optimal_GH_injr Œ± Œ≤ x := rfl,
      rw this,
      exact mem_union_right _ (mem_image_of_mem _ (mem_univ _)) } },
  rw this,
  exact (compact_univ.image (isometry_optimal_GH_injl Œ± Œ≤).continuous).union
    (compact_univ.image (isometry_optimal_GH_injr Œ± Œ≤).continuous)
end‚ü©

/-- For any candidate f, HD(f) is larger than or equal to the Hausdorff distance in the
optimal coupling. This follows from the fact that HD of the optimal candidate is exactly
the Hausdorff distance in the optimal coupling, although we only prove here the inequality
we need. -/
lemma Hausdorff_dist_optimal_le_HD {f} (h : f ‚àà candidates_b Œ± Œ≤) :
  Hausdorff_dist (range (optimal_GH_injl Œ± Œ≤)) (range (optimal_GH_injr Œ± Œ≤)) ‚â§ HD f :=
begin
  refine le_trans (le_of_forall_le_of_dense (Œªr hr, _)) (HD_optimal_GH_dist_le Œ± Œ≤ f h),
  have A : ‚àÄ x ‚àà range (optimal_GH_injl Œ± Œ≤), ‚àÉ y ‚àà range (optimal_GH_injr Œ± Œ≤), dist x y ‚â§ r,
  { assume x hx,
    rcases mem_range.1 hx with ‚ü®z, hz‚ü©,
    rw ‚Üê hz,
    have I1 : (‚®Ü x, ‚®Ö y, optimal_GH_dist Œ± Œ≤ (inl x, inr y)) < r :=
      lt_of_le_of_lt (le_max_left _ _) hr,
    have I2 : (‚®Ö y, optimal_GH_dist Œ± Œ≤ (inl z, inr y)) ‚â§
        ‚®Ü x, ‚®Ö y, optimal_GH_dist Œ± Œ≤ (inl x, inr y) :=
      le_cSup (by simpa using HD_bound_aux1 _ 0) (mem_range_self _),
    have I : (‚®Ö y, optimal_GH_dist Œ± Œ≤ (inl z, inr y)) < r := lt_of_le_of_lt I2 I1,
    rcases exists_lt_of_cInf_lt (range_nonempty _) I with ‚ü®r', r'range, hr'‚ü©,
    rcases mem_range.1 r'range with ‚ü®z', hz'‚ü©,
    existsi [optimal_GH_injr Œ± Œ≤ z', mem_range_self _],
    have : (optimal_GH_dist Œ± Œ≤) (inl z, inr z') ‚â§ r := begin rw hz', exact le_of_lt hr' end,
    exact this },
  refine Hausdorff_dist_le_of_mem_dist _ A _,
  { rcases exists_mem_of_nonempty Œ± with ‚ü®xŒ±, _‚ü©,
    have : optimal_GH_injl Œ± Œ≤ xŒ± ‚àà range (optimal_GH_injl Œ± Œ≤) := mem_range_self _,
    rcases A _ this with ‚ü®y, yrange, hy‚ü©,
    exact le_trans dist_nonneg hy },
  { assume y hy,
    rcases mem_range.1 hy with ‚ü®z, hz‚ü©,
    rw ‚Üê hz,
    have I1 : (‚®Ü y, ‚®Ö x, optimal_GH_dist Œ± Œ≤ (inl x, inr y)) < r :=
      lt_of_le_of_lt (le_max_right _ _) hr,
    have I2 : (‚®Ö x, optimal_GH_dist Œ± Œ≤ (inl x, inr z)) ‚â§
        ‚®Ü y, ‚®Ö x, optimal_GH_dist Œ± Œ≤ (inl x, inr y) :=
      le_cSup (by simpa using HD_bound_aux2 _ 0) (mem_range_self _),
    have I : (‚®Ö x, optimal_GH_dist Œ± Œ≤ (inl x, inr z)) < r := lt_of_le_of_lt I2 I1,
    rcases exists_lt_of_cInf_lt (range_nonempty _) I with ‚ü®r', r'range, hr'‚ü©,
    rcases mem_range.1 r'range with ‚ü®z', hz'‚ü©,
    existsi [optimal_GH_injl Œ± Œ≤ z', mem_range_self _],
    have : (optimal_GH_dist Œ± Œ≤) (inl z', inr z) ‚â§ r := begin rw hz', exact le_of_lt hr' end,
    rw dist_comm,
    exact this }
end

end consequences
/- We are done with the construction of the optimal coupling -/
end Gromov_Hausdorff_realized

end Gromov_Hausdorff
