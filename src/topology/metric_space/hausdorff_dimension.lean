/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import measure_theory.measure.hausdorff

/-!
# Hausdorff dimension

The Hausdorff dimension of a set `X` in an (extended) metric space is the unique number
`dimH s : ‚Ñù‚â•0‚àû` such that for any `d : ‚Ñù‚â•0` we have

- `ŒºH[d] s = 0` if `dimH s < d`, and
- `ŒºH[d] s = ‚àû` if `d < dimH s`.

In this file we define `dimH s` to be the Hausdorff dimension of `s`, then prove some basic
properties of Hausdorff dimension.

## Main definitions

* `measure_theory.dimH`: the Hausdorff dimension of a set. For the Hausdorff dimension of the whole
  space we use `measure_theory.dimH (set.univ : set X)`.

## Main results

### Basic properties of Hausdorff dimension

* `hausdorff_measure_of_lt_dimH`, `dimH_le_of_hausdorff_measure_ne_top`,
  `le_dimH_of_hausdorff_measure_eq_top`, `hausdorff_measure_of_dimH_lt`, `measure_zero_of_dimH_lt`,
  `le_dimH_of_hausdorff_measure_ne_zero`, `dimH_of_hausdorff_measure_ne_zero_ne_top`: various forms
  of the characteristic property of the Hausdorff dimension;
* `dimH_union`: the Hausdorff dimension of the union of two sets is the maximum of their Hausdorff
  dimensions.
* `dimH_Union`, `dimH_bUnion`, `dimH_sUnion`: the Hausdorff dimension of a countable union of sets
  is the supremum of their Hausdorff dimensions;
* `dimH_empty`, `dimH_singleton`, `set.subsingleton.dimH_zero`, `set.countable.dimH_zero` : `dimH s
  = 0` whenever `s` is countable;

### (Pre)images under (anti)lipschitz and H√∂lder continuous maps

* `holder_with.dimH_image_le` etc: if `f : X ‚Üí Y` is H√∂lder continuous with exponent `r > 0`, then
  for any `s`, `dimH (f '' s) ‚â§ dimH s / r`. We prove versions of this statement for `holder_with`,
  `holder_on_with`, and locally H√∂lder maps, as well as for `set.image` and `set.range`.
* `lipschitz_with.dimH_image_le` etc: Lipschitz continuous maps do not increase the Hausdorff
  dimension of sets.
* for a map that is known to be both Lipschitz and antilipschitz (e.g., for an `isometry` or
  a `continuous_linear_equiv`) we also prove `dimH (f '' s) = dimH s`.

### Hausdorff measure in `‚Ñù‚Åø`

* `real.dimH_of_nonempty_interior`: if `s` is a set in a finite dimensional real vector space `E`
  with nonempty interior, then the Hausdorff dimension of `s` is equal to the dimension of `E`.
* `dense_compl_of_dimH_lt_finrank`: if `s` is a set in a finite dimensional real vector space `E`
  with Hausdorff dimension strictly less than the dimension of `E`, the `s` has a dense complement.
* `times_cont_diff.dense_compl_range_of_finrank_lt_finrank`: the complement to the range of a `C¬π`
  smooth map is dense provided that the dimension of the domain is strictly less than the dimension
  of the codomain.

## Notations

We use the following notation localized in `measure_theory`. It is defined in
`measure_theory.measure.hausdorff`.

- `ŒºH[d]` : `measure_theory.measure.hausdorff_measure d`

## Implementation notes

* The definition of `dimH` explicitly uses `borel X` as a measurable space structure. This way we
  can formulate lemmas about Hausdorff dimension without assuming that the environment has a
  `[measurable_space X]` instance that is equal but possibly not defeq to `borel X`.

  Lemma `dimH_def` unfolds this definition using whatever `[measurable_space X]` instance we have in
  the environment (as long as it is equal to `borel X`).

* The definition `dimH` is irreducible; use API lemmas or `dimH_def` instead.

## Tags

Hausdorff measure, Hausdorff dimension, dimension
-/
open_locale measure_theory ennreal nnreal topological_space
open measure_theory measure_theory.measure set topological_space finite_dimensional filter

variables {Œπ X Y : Type*} [emetric_space X] [emetric_space Y]

/-- Hausdorff dimension of a set in an (e)metric space. -/
@[irreducible] noncomputable def dimH (s : set X) : ‚Ñù‚â•0‚àû :=
by letI := borel X; exact ‚®Ü (d : ‚Ñù‚â•0) (hd : @hausdorff_measure X _ _ ‚ü®rfl‚ü© d s = ‚àû), d

/-!
### Basic properties
-/
section measurable

variables [measurable_space X] [borel_space X]

/-- Unfold the definition of `dimH` using `[measurable_space X] [borel_space X]` from the
environment. -/
lemma dimH_def (s : set X) : dimH s = ‚®Ü (d : ‚Ñù‚â•0) (hd : ŒºH[d] s = ‚àû), d :=
begin
  unfreezingI { obtain rfl : ‚Äπmeasurable_space X‚Ä∫ = borel X := borel_space.measurable_eq },
  rw dimH
end

lemma hausdorff_measure_of_lt_dimH {s : set X} {d : ‚Ñù‚â•0} (h : ‚Üëd < dimH s) : ŒºH[d] s = ‚àû :=
begin
  simp only [dimH_def, lt_supr_iff] at h,
  rcases h with ‚ü®d', hsd', hdd'‚ü©,
  rw [ennreal.coe_lt_coe, ‚Üê nnreal.coe_lt_coe] at hdd',
  exact top_unique (hsd' ‚ñ∏ hausdorff_measure_mono hdd'.le _)
end

lemma dimH_le {s : set X} {d : ‚Ñù‚â•0‚àû} (H : ‚àÄ d' : ‚Ñù‚â•0, ŒºH[d'] s = ‚àû ‚Üí ‚Üëd' ‚â§ d) : dimH s ‚â§ d :=
(dimH_def s).trans_le $ bsupr_le H

lemma dimH_le_of_hausdorff_measure_ne_top {s : set X} {d : ‚Ñù‚â•0} (h : ŒºH[d] s ‚â† ‚àû) :
  dimH s ‚â§ d :=
le_of_not_lt $ mt hausdorff_measure_of_lt_dimH h

lemma le_dimH_of_hausdorff_measure_eq_top {s : set X} {d : ‚Ñù‚â•0} (h : ŒºH[d] s = ‚àû) :
  ‚Üëd ‚â§ dimH s :=
by { rw dimH_def, exact le_bsupr d h }

lemma hausdorff_measure_of_dimH_lt {s : set X} {d : ‚Ñù‚â•0}
  (h : dimH s < d) : ŒºH[d] s = 0 :=
begin
  rw dimH_def at h,
  rcases ennreal.lt_iff_exists_nnreal_btwn.1 h with ‚ü®d', hsd', hd'd‚ü©,
  rw [ennreal.coe_lt_coe, ‚Üê nnreal.coe_lt_coe] at hd'd,
  exact (hausdorff_measure_zero_or_top hd'd s).resolve_right
    (Œª h, hsd'.not_le (le_bsupr d' h))
end

lemma measure_zero_of_dimH_lt {Œº : measure X} {d : ‚Ñù‚â•0}
  (h : Œº ‚â™ ŒºH[d]) {s : set X} (hd : dimH s < d) :
  Œº s = 0 :=
h $ hausdorff_measure_of_dimH_lt hd

lemma le_dimH_of_hausdorff_measure_ne_zero {s : set X} {d : ‚Ñù‚â•0} (h : ŒºH[d] s ‚â† 0) :
  ‚Üëd ‚â§ dimH s :=
le_of_not_lt $ mt hausdorff_measure_of_dimH_lt h

lemma dimH_of_hausdorff_measure_ne_zero_ne_top {d : ‚Ñù‚â•0} {s : set X} (h : ŒºH[d] s ‚â† 0)
  (h' : ŒºH[d] s ‚â† ‚àû) : dimH s = d :=
le_antisymm (dimH_le_of_hausdorff_measure_ne_top h') (le_dimH_of_hausdorff_measure_ne_zero h)

end measurable

@[mono] lemma dimH_mono {s t : set X} (h : s ‚äÜ t) : dimH s ‚â§ dimH t :=
begin
  letI := borel X, haveI : borel_space X := ‚ü®rfl‚ü©,
  exact dimH_le (Œª d hd, le_dimH_of_hausdorff_measure_eq_top $
    top_unique $ hd ‚ñ∏ measure_mono h)
end

lemma dimH_subsingleton {s : set X} (h : s.subsingleton) : dimH s = 0 :=
by simp [dimH, h.measure_zero]

alias dimH_subsingleton ‚Üê set.subsingleton.dimH_zero

@[simp] lemma dimH_empty : dimH (‚àÖ : set X) = 0 := subsingleton_empty.dimH_zero

@[simp] lemma dimH_singleton (x : X) : dimH ({x} : set X) = 0 := subsingleton_singleton.dimH_zero

@[simp] lemma dimH_Union [encodable Œπ] (s : Œπ ‚Üí set X) :
  dimH (‚ãÉ i, s i) = ‚®Ü i, dimH (s i) :=
begin
  letI := borel X, haveI : borel_space X := ‚ü®rfl‚ü©,
  refine le_antisymm (dimH_le $ Œª d hd, _) (supr_le $ Œª i, dimH_mono $ subset_Union _ _),
  contrapose! hd,
  have : ‚àÄ i, ŒºH[d] (s i) = 0,
    from Œª i, hausdorff_measure_of_dimH_lt ((le_supr (Œª i, dimH (s i)) i).trans_lt hd),
  rw measure_Union_null this,
  exact ennreal.zero_ne_top
end

@[simp] lemma dimH_bUnion {s : set Œπ} (hs : countable s) (t : Œπ ‚Üí set X) :
  dimH (‚ãÉ i ‚àà s, t i) = ‚®Ü i ‚àà s, dimH (t i) :=
begin
  haveI := hs.to_encodable,
  rw [bUnion_eq_Union, dimH_Union, ‚Üê supr_subtype'']
end

@[simp] lemma dimH_sUnion {S : set (set X)} (hS : countable S) : dimH (‚ãÉ‚ÇÄ S) = ‚®Ü s ‚àà S, dimH s :=
by rw [sUnion_eq_bUnion, dimH_bUnion hS]

@[simp] lemma dimH_union (s t : set X) : dimH (s ‚à™ t) = max (dimH s) (dimH t) :=
by rw [union_eq_Union, dimH_Union, supr_bool_eq, cond, cond, ennreal.sup_eq_max]

lemma dimH_countable {s : set X} (hs : countable s) : dimH s = 0 :=
bUnion_of_singleton s ‚ñ∏ by simp only [dimH_bUnion hs, dimH_singleton, ennreal.supr_zero_eq_zero]

alias dimH_countable ‚Üê set.countable.dimH_zero

lemma dimH_finite {s : set X} (hs : finite s) : dimH s = 0 := hs.countable.dimH_zero

alias dimH_finite ‚Üê set.finite.dimH_zero

@[simp] lemma dimH_coe_finset (s : finset X) : dimH (s : set X) = 0 := s.finite_to_set.dimH_zero

alias dimH_coe_finset ‚Üê finset.dimH_zero

/-!
### Hausdorff dimension as the supremum of local Hausdorff dimensions
-/

section

variables [second_countable_topology X]

/-- If `r` is less than the Hausdorff dimension of a set `s` in an (extended) metric space with
second countable topology, then there exists a point `x ‚àà s` such that every neighborhood
`t` of `x` within `s` has Hausdorff dimension greater than `r`. -/
lemma exists_mem_nhds_within_lt_dimH_of_lt_dimH {s : set X} {r : ‚Ñù‚â•0‚àû} (h : r < dimH s) :
  ‚àÉ x ‚àà s, ‚àÄ t ‚àà ùìù[s] x, r < dimH t :=
begin
  contrapose! h, choose! t htx htr using h,
  rcases countable_cover_nhds_within htx with ‚ü®S, hSs, hSc, hSU‚ü©,
  calc dimH s ‚â§ dimH (‚ãÉ x ‚àà S, t x) : dimH_mono hSU
  ... = ‚®Ü x ‚àà S, dimH (t x) : dimH_bUnion hSc _
  ... ‚â§ r : bsupr_le (Œª x hx, htr x (hSs hx))
end

/-- In an (extended) metric space with second countable topology, the Hausdorff dimension
of a set `s` is the supremum over `x ‚àà s` of the limit superiors of `dimH t` along
`(ùìù[s] x).lift' powerset`. -/
lemma bsupr_limsup_dimH (s : set X) : (‚®Ü x ‚àà s, limsup ((ùìù[s] x).lift' powerset) dimH) = dimH s :=
begin
  refine le_antisymm (bsupr_le $ Œª x hx, _) _,
  { refine Limsup_le_of_le (by apply_auto_param) (eventually_map.2 _),
    exact eventually_lift'_powerset.2 ‚ü®s, self_mem_nhds_within, Œª t, dimH_mono‚ü© },
  { refine le_of_forall_ge_of_dense (Œª r hr, _),
    rcases exists_mem_nhds_within_lt_dimH_of_lt_dimH hr with ‚ü®x, hxs, hxr‚ü©,
    refine le_bsupr_of_le x hxs _, rw limsup_eq, refine le_Inf (Œª b hb, _),
    rcases eventually_lift'_powerset.1 hb with ‚ü®t, htx, ht‚ü©,
    exact (hxr t htx).le.trans (ht t subset.rfl) }
end

/-- In an (extended) metric space with second countable topology, the Hausdorff dimension
of a set `s` is the supremum over all `x` of the limit superiors of `dimH t` along
`(ùìù[s] x).lift' powerset`. -/
lemma supr_limsup_dimH (s : set X) : (‚®Ü x, limsup ((ùìù[s] x).lift' powerset) dimH) = dimH s :=
begin
  refine le_antisymm (supr_le $ Œª x, _) _,
  { refine Limsup_le_of_le (by apply_auto_param) (eventually_map.2 _),
    exact eventually_lift'_powerset.2 ‚ü®s, self_mem_nhds_within, Œª t, dimH_mono‚ü© },
  { rw ‚Üê bsupr_limsup_dimH, exact bsupr_le_supr _ _ }
end

end

/-!
### Hausdorff dimension and H√∂lder continuity
-/

variables {C K r : ‚Ñù‚â•0} {f : X ‚Üí Y} {s t : set X}

/-- If `f` is a H√∂lder continuous map with exponent `r > 0`, then `dimH (f '' s) ‚â§ dimH s / r`. -/
lemma holder_on_with.dimH_image_le (h : holder_on_with C r f s) (hr : 0 < r) :
  dimH (f '' s) ‚â§ dimH s / r :=
begin
  letI := borel X, haveI : borel_space X := ‚ü®rfl‚ü©,
  letI := borel Y, haveI : borel_space Y := ‚ü®rfl‚ü©,
  refine dimH_le (Œª d hd, _),
  have := h.hausdorff_measure_image_le hr d.coe_nonneg,
  rw [hd, ennreal.coe_rpow_of_nonneg _ d.coe_nonneg, top_le_iff] at this,
  have Hrd : ŒºH[(r * d : ‚Ñù‚â•0)] s = ‚ä§,
  { contrapose this, exact ennreal.mul_ne_top ennreal.coe_ne_top this },
  rw [ennreal.le_div_iff_mul_le, mul_comm, ‚Üê ennreal.coe_mul],
  exacts [le_dimH_of_hausdorff_measure_eq_top Hrd, or.inl (mt ennreal.coe_eq_zero.1 hr.ne'),
    or.inl ennreal.coe_ne_top]
end

namespace holder_with

/-- If `f : X ‚Üí Y` is H√∂lder continuous with a positive exponent `r`, then the Hausdorff dimension
of the image of a set `s` is at most `dimH s / r`. -/
lemma dimH_image_le (h : holder_with C r f) (hr : 0 < r) (s : set X) :
  dimH (f '' s) ‚â§ dimH s / r :=
(h.holder_on_with s).dimH_image_le hr

/-- If `f` is a H√∂lder continuous map with exponent `r > 0`, then the Hausdorff dimension of its
range is at most the Hausdorff dimension of its domain divided by `r`. -/
lemma dimH_range_le (h : holder_with C r f) (hr : 0 < r) :
  dimH (range f) ‚â§ dimH (univ : set X) / r :=
@image_univ _ _ f ‚ñ∏ h.dimH_image_le hr univ

end holder_with

/-- If `s` is a set in a space `X` with second countable topology and `f : X ‚Üí Y` is H√∂lder
continuous in a neighborhood within `s` of every point `x ‚àà s` with the same positive exponent `r`
but possibly different coefficients, then the Hausdorff dimension of the image `f '' s` is at most
the Hausdorff dimension of `s` divided by `r`. -/
lemma dimH_image_le_of_locally_holder_on [second_countable_topology X] {r : ‚Ñù‚â•0} {f : X ‚Üí Y}
  (hr : 0 < r) {s : set X} (hf : ‚àÄ x ‚àà s, ‚àÉ (C : ‚Ñù‚â•0) (t ‚àà ùìù[s] x), holder_on_with C r f t) :
  dimH (f '' s) ‚â§ dimH s / r :=
begin
  choose! C t htn hC using hf,
  rcases countable_cover_nhds_within htn with ‚ü®u, hus, huc, huU‚ü©,
  replace huU := inter_eq_self_of_subset_left huU, rw inter_bUnion at huU,
  rw [‚Üê huU, image_bUnion, dimH_bUnion huc, dimH_bUnion huc], simp only [ennreal.supr_div],
  exact bsupr_le_bsupr (Œª x hx, ((hC x (hus hx)).mono (inter_subset_right _ _)).dimH_image_le hr)
end

/-- If `f : X ‚Üí Y` is H√∂lder continuous in a neighborhood of every point `x : X` with the same
positive exponent `r` but possibly different coefficients, then the Hausdorff dimension of the range
of `f` is at most the Hausdorff dimension of `X` divided by `r`. -/
lemma dimH_range_le_of_locally_holder_on [second_countable_topology X] {r : ‚Ñù‚â•0} {f : X ‚Üí Y}
  (hr : 0 < r) (hf : ‚àÄ x : X, ‚àÉ (C : ‚Ñù‚â•0) (s ‚àà ùìù x), holder_on_with C r f s) :
  dimH (range f) ‚â§ dimH (univ : set X) / r :=
begin
  rw ‚Üê image_univ,
  refine dimH_image_le_of_locally_holder_on hr (Œª x _, _),
  simpa only [exists_prop, nhds_within_univ] using hf x
end

/-!
### Hausdorff dimension and Lipschitz continuity
-/

/-- If `f : X ‚Üí Y` is Lipschitz continuous on `s`, then `dimH (f '' s) ‚â§ dimH s`. -/
lemma lipschitz_on_with.dimH_image_le (h : lipschitz_on_with K f s) : dimH (f '' s) ‚â§ dimH s :=
by simpa using h.holder_on_with.dimH_image_le zero_lt_one

namespace lipschitz_with

/-- If `f` is a Lipschitz continuous map, then `dimH (f '' s) ‚â§ dimH s`. -/
lemma dimH_image_le (h : lipschitz_with K f) (s : set X) : dimH (f '' s) ‚â§ dimH s :=
(h.lipschitz_on_with s).dimH_image_le

/-- If `f` is a Lipschitz continuous map, then the Hausdorff dimension of its range is at most the
Hausdorff dimension of its domain. -/
lemma dimH_range_le (h : lipschitz_with K f) : dimH (range f) ‚â§ dimH (univ : set X) :=
@image_univ _ _ f ‚ñ∏ h.dimH_image_le univ

end lipschitz_with

/-- If `s` is a set in an extended metric space `X` with second countable topology and `f : X ‚Üí Y`
is Lipschitz in a neighborhood within `s` of every point `x ‚àà s`, then the Hausdorff dimension of
the image `f '' s` is at most the Hausdorff dimension of `s`. -/
lemma dimH_image_le_of_locally_lipschitz_on [second_countable_topology X] {f : X ‚Üí Y}
  {s : set X} (hf : ‚àÄ x ‚àà s, ‚àÉ (C : ‚Ñù‚â•0) (t ‚àà ùìù[s] x), lipschitz_on_with C f t) :
  dimH (f '' s) ‚â§ dimH s :=
begin
  have : ‚àÄ x ‚àà s, ‚àÉ (C : ‚Ñù‚â•0) (t ‚àà ùìù[s] x), holder_on_with C 1 f t,
    by simpa only [holder_on_with_one] using hf,
  simpa only [ennreal.coe_one, ennreal.div_one]
    using dimH_image_le_of_locally_holder_on zero_lt_one this
end

/-- If `f : X ‚Üí Y` is Lipschitz in a neighborhood of each point `x : X`, then the Hausdorff
dimension of `range f` is at most the Hausdorff dimension of `X`. -/
lemma dimH_range_le_of_locally_lipschitz_on [second_countable_topology X] {f : X ‚Üí Y}
  (hf : ‚àÄ x : X, ‚àÉ (C : ‚Ñù‚â•0) (s ‚àà ùìù x), lipschitz_on_with C f s) :
  dimH (range f) ‚â§ dimH (univ : set X) :=
begin
  rw ‚Üê image_univ,
  refine dimH_image_le_of_locally_lipschitz_on (Œª x _, _),
  simpa only [exists_prop, nhds_within_univ] using hf x
end

namespace antilipschitz_with

lemma dimH_preimage_le (hf : antilipschitz_with K f) (s : set Y) :
  dimH (f ‚Åª¬π' s) ‚â§ dimH s :=
begin
  letI := borel X, haveI : borel_space X := ‚ü®rfl‚ü©,
  letI := borel Y, haveI : borel_space Y := ‚ü®rfl‚ü©,
  refine dimH_le (Œª d hd, le_dimH_of_hausdorff_measure_eq_top _),
  have := hf.hausdorff_measure_preimage_le d.coe_nonneg s,
  rw [hd, top_le_iff] at this,
  contrapose! this,
  exact ennreal.mul_ne_top (by simp) this
end

lemma le_dimH_image (hf : antilipschitz_with K f) (s : set X) :
  dimH s ‚â§ dimH (f '' s) :=
calc dimH s ‚â§ dimH (f ‚Åª¬π' (f '' s)) : dimH_mono (subset_preimage_image _ _)
        ... ‚â§ dimH (f '' s)         : hf.dimH_preimage_le _

end antilipschitz_with

/-!
### Isometries preserve Hausdorff dimension
-/

lemma isometry.dimH_image (hf : isometry f) (s : set X) : dimH (f '' s) = dimH s :=
le_antisymm (hf.lipschitz.dimH_image_le _) (hf.antilipschitz.le_dimH_image _)

namespace isometric

@[simp] lemma dimH_image (e : X ‚âÉ·µ¢ Y) (s : set X) : dimH (e '' s) = dimH s :=
e.isometry.dimH_image s

@[simp] lemma dimH_preimage (e : X ‚âÉ·µ¢ Y) (s : set Y) : dimH (e ‚Åª¬π' s) = dimH s :=
by rw [‚Üê e.image_symm, e.symm.dimH_image]

lemma dimH_univ (e : X ‚âÉ·µ¢ Y) : dimH (univ : set X) = dimH (univ : set Y) :=
by rw [‚Üê e.dimH_preimage univ, preimage_univ]

end isometric

namespace continuous_linear_equiv

variables {ùïú E F : Type*} [nondiscrete_normed_field ùïú]
  [normed_group E] [normed_space ùïú E] [normed_group F] [normed_space ùïú F]

@[simp] lemma dimH_image (e : E ‚âÉL[ùïú] F) (s : set E) : dimH (e '' s) = dimH s :=
le_antisymm (e.lipschitz.dimH_image_le s) $
  by simpa only [e.symm_image_image] using e.symm.lipschitz.dimH_image_le (e '' s)

@[simp] lemma dimH_preimage (e : E ‚âÉL[ùïú] F) (s : set F) : dimH (e ‚Åª¬π' s) = dimH s :=
by rw [‚Üê e.image_symm_eq_preimage, e.symm.dimH_image]

lemma dimH_univ (e : E ‚âÉL[ùïú] F) : dimH (univ : set E) = dimH (univ : set F) :=
by rw [‚Üê e.dimH_preimage, preimage_univ]

end continuous_linear_equiv

/-!
### Hausdorff dimension in a real vector space
-/

namespace real

variables {E : Type*} [fintype Œπ] [normed_group E] [normed_space ‚Ñù E] [finite_dimensional ‚Ñù E]

theorem dimH_ball_pi (x : Œπ ‚Üí ‚Ñù) {r : ‚Ñù} (hr : 0 < r) :
  dimH (metric.ball x r) = fintype.card Œπ :=
begin
  casesI is_empty_or_nonempty Œπ,
  { rwa [dimH_subsingleton, eq_comm, nat.cast_eq_zero, fintype.card_eq_zero_iff],
    exact Œª x _ y _, subsingleton.elim x y },
  { rw ‚Üê ennreal.coe_nat,
    have : ŒºH[fintype.card Œπ] (metric.ball x r) = ennreal.of_real ((2 * r) ^ fintype.card Œπ),
      by rw [hausdorff_measure_pi_real, real.volume_pi_ball _ hr],
    refine dimH_of_hausdorff_measure_ne_zero_ne_top _ _; rw [nnreal.coe_nat_cast, this],
    { simp [pow_pos (mul_pos zero_lt_two hr)] },
    { exact ennreal.of_real_ne_top } }
end

theorem dimH_ball_pi_fin {n : ‚Ñï} (x : fin n ‚Üí ‚Ñù) {r : ‚Ñù} (hr : 0 < r) :
  dimH (metric.ball x r) = n :=
by rw [dimH_ball_pi x hr, fintype.card_fin]

theorem dimH_univ_pi (Œπ : Type*) [fintype Œπ] : dimH (univ : set (Œπ ‚Üí ‚Ñù)) = fintype.card Œπ :=
by simp only [‚Üê metric.Union_ball_nat_succ (0 : Œπ ‚Üí ‚Ñù), dimH_Union,
  dimH_ball_pi _ (nat.cast_add_one_pos _), supr_const]

theorem dimH_univ_pi_fin (n : ‚Ñï) : dimH (univ : set (fin n ‚Üí ‚Ñù)) = n :=
by rw [dimH_univ_pi, fintype.card_fin]

theorem dimH_of_mem_nhds {x : E} {s : set E} (h : s ‚àà ùìù x) :
  dimH s = finrank ‚Ñù E :=
begin
  haveI : finite_dimensional ‚Ñù (fin (finrank ‚Ñù E) ‚Üí ‚Ñù), from is_noetherian_pi',
  have e : E ‚âÉL[‚Ñù] (fin (finrank ‚Ñù E) ‚Üí ‚Ñù),
    from continuous_linear_equiv.of_finrank_eq (finite_dimensional.finrank_fin_fun ‚Ñù).symm,
  rw ‚Üê e.dimH_image,
  refine le_antisymm _ _,
  { exact (dimH_mono (subset_univ _)).trans_eq (dimH_univ_pi_fin _) },
  { have : e '' s ‚àà ùìù (e x), by { rw ‚Üê e.map_nhds_eq, exact image_mem_map h },
    rcases metric.nhds_basis_ball.mem_iff.1 this with ‚ü®r, hr0, hr‚ü©,
    simpa only [dimH_ball_pi_fin (e x) hr0] using dimH_mono hr }
end

theorem dimH_of_nonempty_interior {s : set E} (h : (interior s).nonempty) :
  dimH s = finrank ‚Ñù E :=
let ‚ü®x, hx‚ü© := h in dimH_of_mem_nhds (mem_interior_iff_mem_nhds.1 hx)

variable (E)

theorem dimH_univ_eq_finrank : dimH (univ : set E) = finrank ‚Ñù E :=
dimH_of_mem_nhds (@univ_mem _ (ùìù 0))

theorem dimH_univ : dimH (univ : set ‚Ñù) = 1 :=
by rw [dimH_univ_eq_finrank ‚Ñù, finite_dimensional.finrank_self, nat.cast_one]

end real

variables {E F : Type*}
  [normed_group E] [normed_space ‚Ñù E] [finite_dimensional ‚Ñù E]
  [normed_group F] [normed_space ‚Ñù F]

theorem dense_compl_of_dimH_lt_finrank {s : set E} (hs : dimH s < finrank ‚Ñù E) : dense s·∂ú :=
begin
  refine Œª x, mem_closure_iff_nhds.2 (Œª t ht, ne_empty_iff_nonempty.1 $ Œª he, hs.not_le _),
  rw [‚Üê diff_eq, diff_eq_empty] at he,
  rw [‚Üê real.dimH_of_mem_nhds ht],
  exact dimH_mono he
end

/-!
### Hausdorff dimension and `C¬π`-smooth maps

`C¬π`-smooth maps are locally Lipschitz continuous, hence they do not increase the Hausdorff
dimension of sets.
-/

/-- Let `f` be a function defined on a finite dimensional real normed space. If `f` is `C¬π`-smooth
on a convex set `s`, then the Hausdorff dimension of `f '' s` is less than or equal to the Hausdorff
dimension of `s`.

TODO: do we actually need `convex ‚Ñù s`? -/
lemma times_cont_diff_on.dimH_image_le {f : E ‚Üí F} {s t : set E} (hf : times_cont_diff_on ‚Ñù 1 f s)
  (hc : convex ‚Ñù s) (ht : t ‚äÜ s) :
  dimH (f '' t) ‚â§ dimH t :=
dimH_image_le_of_locally_lipschitz_on $ Œª x hx,
  let ‚ü®C, u, hu, hf‚ü© := (hf x (ht hx)).exists_lipschitz_on_with hc
  in ‚ü®C, u, nhds_within_mono _ ht hu, hf‚ü©

/-- The Hausdorff dimension of the range of a `C¬π`-smooth function defined on a finite dimensional
real normed space is at most the dimension of its domain as a vector space over `‚Ñù`. -/
lemma times_cont_diff.dimH_range_le {f : E ‚Üí F} (h : times_cont_diff ‚Ñù 1 f) :
  dimH (range f) ‚â§ finrank ‚Ñù E :=
calc dimH (range f) = dimH (f '' univ) : by rw image_univ
... ‚â§ dimH (univ : set E) : h.times_cont_diff_on.dimH_image_le convex_univ subset.rfl
... = finrank ‚Ñù E : real.dimH_univ_eq_finrank E

/-- A particular case of Sard's Theorem. Let `f : E ‚Üí F` be a map between finite dimensional real
vector spaces. Suppose that `f` is `C¬π` smooth on a convex set `s` of Hausdorff dimension strictly
less than the dimension of `F`. Then the complement of the image `f '' s` is dense in `F`. -/
lemma times_cont_diff_on.dense_compl_image_of_dimH_lt_finrank [finite_dimensional ‚Ñù F] {f : E ‚Üí F}
  {s t : set E} (h : times_cont_diff_on ‚Ñù 1 f s) (hc : convex ‚Ñù s) (ht : t ‚äÜ s)
  (htF : dimH t < finrank ‚Ñù F) :
  dense (f '' t)·∂ú :=
dense_compl_of_dimH_lt_finrank $ (h.dimH_image_le hc ht).trans_lt htF

/-- A particular case of Sard's Theorem. If `f` is a `C¬π` smooth map from a real vector space to a
real vector space `F` of strictly larger dimension, then the complement of the range of `f` is dense
in `F`. -/
lemma times_cont_diff.dense_compl_range_of_finrank_lt_finrank [finite_dimensional ‚Ñù F] {f : E ‚Üí F}
  (h : times_cont_diff ‚Ñù 1 f) (hEF : finrank ‚Ñù E < finrank ‚Ñù F) :
  dense (range f)·∂ú :=
dense_compl_of_dimH_lt_finrank $ h.dimH_range_le.trans_lt $ ennreal.coe_nat_lt_coe_nat.2 hEF
