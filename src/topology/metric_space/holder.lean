/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import topology.metric_space.lipschitz
import analysis.special_functions.pow

/-!
# Holder continuous functions

In this file we define `f : X ‚Üí Y` to be *H√∂lder continuous* with constant `C : ‚Ñù‚â•0` and exponent
`r : ‚Ñù‚â•0`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y : X`. We also prove some basic
facts about this definition.

## Implementation notes

We use the type `‚Ñù‚â•0` (a.k.a. `nnreal`) for `C` because this type has coercion both to `‚Ñù` and
`‚Ñù‚â•0‚àû`, so it can be easily used both in inequalities about `dist` and `edist`. We also use `‚Ñù‚â•0`
for `r` to ensure that `d ^ r` is monotonically increasing in `d`. It might be a good idea to use
`‚Ñù>0` for `r` but we don't have this type in `mathlib` (yet).

## Tags

H√∂lder continuity, Lipschitz continuity

 -/

variables {X Y Z : Type*}

open filter
open_locale nnreal ennreal topological_space

section emetric

variables [pseudo_emetric_space X] [pseudo_emetric_space Y] [pseudo_emetric_space Z]

/-- A function `f : X ‚Üí Y` between two `pseudo_emeteric_space`s is H√∂lder continuous with constant
`C : ‚Ñù‚â•0` and exponent `r : ‚Ñù‚â•0`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y : X`. -/
def holder_with (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) : Prop :=
‚àÄ x y, edist (f x) (f y) ‚â§ C * edist x y ^ (r : ‚Ñù)

@[simp] lemma holder_with_one {C : ‚Ñù‚â•0} {f : X ‚Üí Y} :
  holder_with C 1 f ‚Üî lipschitz_with C f :=
by simp only [holder_with, lipschitz_with, nnreal.coe_one, ennreal.rpow_one]

lemma holder_with_id : holder_with 1 1 (id : X ‚Üí X) :=
holder_with_one.mpr lipschitz_with.id

namespace holder_with

variables {C r : ‚Ñù‚â•0} {f : X ‚Üí Y}

lemma edist_le (h : holder_with C r f) (x y : X) :
  edist (f x) (f y) ‚â§ C * edist x y ^ (r : ‚Ñù) :=
h x y

lemma edist_le_of_le (h : holder_with C r f) {x y : X} {d : ‚Ñù‚â•0‚àû} (hd : edist x y ‚â§ d) :
  edist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
(h _ _).trans (ennreal.mul_left_mono (ennreal.rpow_le_rpow hd r.coe_nonneg))

lemma comp {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} (hg : holder_with Cg rg g)
  {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y} (hf : holder_with Cf rf f) :
  holder_with (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) :=
begin
  intros x y,
  rw [ennreal.coe_mul, mul_comm rg, nnreal.coe_mul, ennreal.rpow_mul, mul_assoc,
    ‚Üê ennreal.coe_rpow_of_nonneg _ rg.coe_nonneg, ‚Üê ennreal.mul_rpow_of_nonneg _ _ rg.coe_nonneg],
  exact hg.edist_le_of_le (hf _ _)
end

/-- A H√∂lder continuous function is uniformly continuous -/
protected lemma uniform_continuous (hf : holder_with C r f) (h0 : 0 < r) : uniform_continuous f :=
begin
  refine emetric.uniform_continuous_iff.2 (ŒªŒµ Œµpos, _),
  have : tendsto (Œª d : ‚Ñù‚â•0‚àû, (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù)) (ùìù 0) (ùìù 0),
  { convert ennreal.tendsto.const_mul (ennreal.continuous_rpow_const.tendsto 0) _,
    { simp [h0] },
    { exact or.inr ennreal.coe_ne_top } },
  rcases ennreal.nhds_zero_basis.mem_iff.1 (this (gt_mem_nhds Œµpos)) with ‚ü®Œ¥, Œ¥0, H‚ü©,
  exact ‚ü®Œ¥, Œ¥0, Œª x y h, (hf x y).trans_lt (H h)‚ü©,
end

protected lemma continuous (hf : holder_with C r f) (h0 : 0 < r) : continuous f :=
(hf.uniform_continuous h0).continuous

lemma ediam_image_le (hf : holder_with C r f) (s : set X) :
  emetric.diam (f '' s) ‚â§ C * emetric.diam s ^ (r : ‚Ñù) :=
emetric.diam_image_le_iff.2 $ Œª x hx y hy, hf.edist_le_of_le $ emetric.edist_le_diam_of_mem hx hy

end holder_with

end emetric

section metric

variables [pseudo_metric_space X] [pseudo_metric_space Y] {C r : ‚Ñù‚â•0} {f : X ‚Üí Y}

namespace holder_with

lemma nndist_le_of_le (hf : holder_with C r f) {x y : X} {d : ‚Ñù‚â•0} (hd : nndist x y ‚â§ d) :
  nndist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
begin
  rw [‚Üê ennreal.coe_le_coe, ‚Üê edist_nndist, ennreal.coe_mul,
    ‚Üê ennreal.coe_rpow_of_nonneg _ r.coe_nonneg],
  apply hf.edist_le_of_le,
  rwa [edist_nndist, ennreal.coe_le_coe],
end

lemma nndist_le (hf : holder_with C r f) (x y : X) :
  nndist (f x) (f y) ‚â§ C * nndist x y ^ (r : ‚Ñù) :=
hf.nndist_le_of_le le_rfl

lemma dist_le_of_le (hf : holder_with C r f) {x y : X} {d : ‚Ñù} (hd : dist x y ‚â§ d) :
  dist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
begin
  lift d to ‚Ñù‚â•0 using dist_nonneg.trans hd,
  rw dist_nndist at hd ‚ä¢,
  norm_cast at hd ‚ä¢,
  exact hf.nndist_le_of_le hd
end

lemma dist_le (hf : holder_with C r f) (x y : X) :
  dist (f x) (f y) ‚â§ C * dist x y ^ (r : ‚Ñù) :=
hf.dist_le_of_le le_rfl

end holder_with

end metric
