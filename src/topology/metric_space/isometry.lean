/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Isometries of emetric and metric spaces
Authors: S√©bastien Gou√´zel
-/

import topology.metric_space.basic topology.bounded_continuous_function
  analysis.normed_space.basic topology.opens topology.metric_space.contracting

/-!
# Isometries

We define isometries, i.e., maps between emetric spaces that preserve
the edistance (on metric spaces, these are exactly the maps that preserve distances),
and prove their basic properties. We also introduce isometric bijections.
-/

noncomputable theory

universes u v w
variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

open function set

/-- An isometry (also known as isometric embedding) is a map preserving the edistance
between emetric spaces, or equivalently the distance between metric space.  -/
def isometry [emetric_space Œ±] [emetric_space Œ≤] (f : Œ± ‚Üí Œ≤) : Prop :=
‚àÄx1 x2 : Œ±, edist (f x1) (f x2) = edist x1 x2

/-- On metric spaces, a map is an isometry if and only if it preserves distances. -/
lemma isometry_emetric_iff_metric [metric_space Œ±] [metric_space Œ≤] {f : Œ± ‚Üí Œ≤} :
  isometry f ‚Üî (‚àÄx y, dist (f x) (f y) = dist x y) :=
‚ü®assume H x y, by simp [dist_edist, H x y],
assume H x y, by simp [edist_dist, H x y]‚ü©

/-- An isometry preserves edistances. -/
theorem isometry.edist_eq [emetric_space Œ±] [emetric_space Œ≤] {f : Œ± ‚Üí Œ≤} {x y : Œ±} (hf : isometry f) :
  edist (f x) (f y) = edist x y :=
hf x y

/-- An isometry preserves distances. -/
theorem isometry.dist_eq [metric_space Œ±] [metric_space Œ≤] {f : Œ± ‚Üí Œ≤} {x y : Œ±} (hf : isometry f) :
  dist (f x) (f y) = dist x y :=
by rw [dist_edist, dist_edist, hf]

section emetric_isometry

variables [emetric_space Œ±] [emetric_space Œ≤] [emetric_space Œ≥]
variables {f : Œ± ‚Üí Œ≤} {x y z : Œ±}  {s : set Œ±}

/-- An isometry is injective -/
lemma isometry.injective (h : isometry f) : injective f :=
Œªx y hxy, edist_eq_zero.1 $
calc edist x y = edist (f x) (f y) : (h x y).symm
         ...   = 0 : by rw [hxy]; simp

/-- Any map on a subsingleton is an isometry -/
theorem isometry_subsingleton [subsingleton Œ±] : isometry f :=
Œªx y, by rw subsingleton.elim x y; simp

/-- The identity is an isometry -/
lemma isometry_id : isometry (id : Œ± ‚Üí Œ±) :=
Œªx y, rfl

/-- The composition of isometries is an isometry -/
theorem isometry.comp {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : isometry g) (hf : isometry f) : isometry (g ‚àò f) :=
assume x y, calc
  edist ((g ‚àò f) x) ((g ‚àò f) y) = edist (f x) (f y) : hg _ _
                            ... = edist x y : hf _ _

/-- An isometry is an embedding -/
theorem isometry.uniform_embedding (hf : isometry f) : uniform_embedding f :=
begin
  refine emetric.uniform_embedding_iff'.2 ‚ü®_, _‚ü©,
  { assume Œµ Œµpos,
    existsi [Œµ, Œµpos],
    simp [hf.edist_eq] },
  { assume Œ¥ Œ¥pos,
    existsi [Œ¥, Œ¥pos],
    simp [hf.edist_eq] }
end

/-- An isometry is continuous. -/
lemma isometry.continuous (hf : isometry f) : continuous f :=
hf.uniform_embedding.embedding.continuous

/-- The inverse of an isometry is an isometry. -/
lemma isometry.inv (e : Œ± ‚âÉ Œ≤) (h : isometry e.to_fun) : isometry e.inv_fun :=
Œªx y, by rw [‚Üê h, e.right_inv _, e.right_inv _]

/-- Isometries preserve the diameter -/
lemma emetric.isometry.diam_image (hf : isometry f) {s : set Œ±}:
  emetric.diam (f '' s) = emetric.diam s :=
begin
  refine le_antisymm _ _,
  { apply lattice.Sup_le _,
    simp only [and_imp, set.mem_image, set.mem_prod, exists_imp_distrib, prod.exists],
    assume b x x' z zs xz z' z's x'z' hb,
    rw [‚Üê hb, ‚Üê xz, ‚Üê x'z', hf z z'],
    exact emetric.edist_le_diam_of_mem zs z's },
  { apply lattice.Sup_le _,
    simp only [and_imp, set.mem_image, set.mem_prod, exists_imp_distrib, prod.exists],
    assume b x x' xs x's hb,
    rw [‚Üê hb, ‚Üê hf x x'],
    exact emetric.edist_le_diam_of_mem (mem_image_of_mem _ xs) (mem_image_of_mem _ x's) }
end

/-- The injection from a subtype is an isometry -/
lemma isometry_subtype_val {s : set Œ±} : isometry (subtype.val : s ‚Üí Œ±) :=
Œªx y, rfl

lemma isometry.restrict (hf : isometry f) (s : set Œ±) : isometry (restrict f s) :=
hf.comp isometry_subtype_val

end emetric_isometry --section

/-- An isometry preserves the diameter in metric spaces -/
lemma metric.isometry.diam_image [metric_space Œ±] [metric_space Œ≤]
  {f : Œ± ‚Üí Œ≤} {s : set Œ±} (hf : isometry f) : metric.diam (f '' s) = metric.diam s :=
by rw [metric.diam, metric.diam, emetric.isometry.diam_image hf]

/-- Œ± and Œ≤ are isometric if there is an isometric bijection between them. -/
structure isometric (Œ± : Type*) (Œ≤ : Type*) [emetric_space Œ±] [emetric_space Œ≤]
  extends Œ± ‚âÉ Œ≤ :=
(isometry_to_fun  : isometry to_fun)
(isometry_inv_fun : isometry inv_fun)

infix ` ‚âÉ·µ¢ `:25 := isometric

namespace isometric
variables [emetric_space Œ±] [emetric_space Œ≤] [emetric_space Œ≥]

instance : has_coe_to_fun (Œ± ‚âÉ·µ¢ Œ≤) := ‚ü®Œª_, Œ± ‚Üí Œ≤, Œªe, e.to_equiv‚ü©

lemma coe_eq_to_equiv (h : Œ± ‚âÉ·µ¢ Œ≤) (a : Œ±) : h a = h.to_equiv a := rfl

/-- The (bundled) homeomorphism associated to an isometric isomorphism. -/
protected def to_homeomorph (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ± ‚âÉ‚Çú Œ≤ :=
{ continuous_to_fun  := (isometry_to_fun h).continuous,
  continuous_inv_fun := (isometry_inv_fun h).continuous,
  .. h.to_equiv }

lemma coe_eq_to_homeomorph (h : Œ± ‚âÉ·µ¢ Œ≤) (a : Œ±) :
  h a = h.to_homeomorph a := rfl

lemma to_homeomorph_to_equiv (h : Œ± ‚âÉ·µ¢ Œ≤) :
  h.to_homeomorph.to_equiv = h.to_equiv :=
by ext; refl

/-- The identity isometry of a space. -/
protected def refl (Œ± : Type*) [emetric_space Œ±] : Œ± ‚âÉ·µ¢ Œ± :=
{ isometry_to_fun := isometry_id, isometry_inv_fun := isometry_id, .. equiv.refl Œ± }

/-- The composition of two isometric isomorphisms, as an isometric isomorphism. -/
protected def trans (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) : Œ± ‚âÉ·µ¢ Œ≥ :=
{ isometry_to_fun  := h‚ÇÇ.isometry_to_fun.comp h‚ÇÅ.isometry_to_fun,
  isometry_inv_fun := h‚ÇÅ.isometry_inv_fun.comp h‚ÇÇ.isometry_inv_fun,
  .. equiv.trans h‚ÇÅ.to_equiv h‚ÇÇ.to_equiv }

/-- The inverse of an isometric isomorphism, as an isometric isomorphism. -/
protected def symm (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ≤ ‚âÉ·µ¢ Œ± :=
{ isometry_to_fun  := h.isometry_inv_fun,
  isometry_inv_fun := h.isometry_to_fun,
  .. h.to_equiv.symm }

protected lemma isometry (h : Œ± ‚âÉ·µ¢ Œ≤) : isometry h := h.isometry_to_fun

lemma symm_comp_self (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.symm ‚àò ‚áëh = id :=
funext $ assume a, h.to_equiv.left_inv a

lemma self_comp_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh ‚àò ‚áëh.symm = id :=
funext $ assume a, h.to_equiv.right_inv a

lemma range_coe (h : Œ± ‚âÉ·µ¢ Œ≤) : range h = univ :=
h.to_equiv.range_eq_univ

lemma image_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : image h.symm = preimage h :=
funext h.symm.to_equiv.image_eq_preimage

lemma preimage_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : preimage h.symm = image h :=
(funext h.to_equiv.image_eq_preimage).symm

end isometric

/-- An isometry induces an isometric isomorphism between the source space and the
range of the isometry. -/
def isometry.isometric_on_range [emetric_space Œ±] [emetric_space Œ≤] {f : Œ± ‚Üí Œ≤} (h : isometry f) :
  Œ± ‚âÉ·µ¢ range f :=
{ isometry_to_fun := Œªx y,
  begin
    change edist ((equiv.set.range f _) x) ((equiv.set.range f _) y) = edist x y,
    rw [equiv.set.range_apply f h.injective, equiv.set.range_apply f h.injective],
    exact h x y
  end,
  isometry_inv_fun :=
  begin
    apply isometry.inv,
    assume x y,
    change edist ((equiv.set.range f _) x) ((equiv.set.range f _) y) = edist x y,
    rw [equiv.set.range_apply f h.injective, equiv.set.range_apply f h.injective],
    exact h x y
  end,
  .. equiv.set.range f h.injective }

lemma isometry.isometric_on_range_apply [emetric_space Œ±] [emetric_space Œ≤]
  {f : Œ± ‚Üí Œ≤} (h : isometry f) (x : Œ±) : h.isometric_on_range x = ‚ü®f x, mem_range_self _‚ü© :=
begin
  dunfold isometry.isometric_on_range,
  rw ‚Üê equiv.set.range_apply f h.injective x,
  refl
end

/-- In a normed algebra, the inclusion of the base field in the extended field is an isometry. -/
lemma algebra_map_isometry (ùïú : Type*) (ùïú' : Type*) [normed_field ùïú] [normed_ring ùïú']
  [h : normed_algebra ùïú ùïú'] : isometry (@algebra_map ùïú ùïú' _ _ _) :=
begin
  refine isometry_emetric_iff_metric.2 (Œªx y, _),
  rw [dist_eq_norm, dist_eq_norm, ‚Üê algebra.map_sub, norm_algebra_map_eq],
end

lemma lipschitz_with.injective_add_isometry [metric_space Œ±] [normed_group Œ≤] {f : Œ± ‚Üí Œ≤}
  {c : nnreal} (hf : lipschitz_with c f) (hc : c < 1) {e : Œ± ‚Üí Œ≤} (he : isometry e) :
  injective (Œª x, f x + e x) :=
begin
  assume x y hxy,
  have A : dist (f x) (f y) ‚â§ c * dist x y := hf x y,
  have B : dist (f x) (f y) = dist (e x) (e y),
  { rw [dist_eq_norm, dist_eq_norm, norm_sub_rev],
    apply congr_arg,
    rw [‚Üê sub_eq_iff_eq_add] at hxy,
    simp only [hxy.symm],
    abel },
  have : 0 ‚â§ ((c:‚Ñù) - 1) * dist x y,
    by simpa only [sub_mul, one_mul, sub_nonneg, B, he.dist_eq] using A,
  exact dist_le_zero.1 (nonpos_of_mul_nonneg_right this (sub_lt_zero.2 hc))
end

lemma lipschitz_with.inj_on_add_isometry [metric_space Œ±] [normed_group Œ≤] {f : Œ± ‚Üí Œ≤}
  {s : set Œ±} {c : nnreal} (hf : lipschitz_with c (restrict f s)) (hc : c < 1)
  {e : Œ± ‚Üí Œ≤} (he : isometry e) :
  inj_on (Œª x, f x + e x) s :=
inj_on_iff_injective.2 $ hf.injective_add_isometry hc (he.restrict s)

lemma contracting_with.injective_add_id [normed_group Œ±] {f : Œ± ‚Üí Œ±}
  {c : nnreal} (hf : contracting_with c f) :
  injective (Œª x, f x + x) :=
hf.2.injective_add_isometry hf.1 isometry_id

lemma lipschitz_with.inj_on_add_id [normed_group Œ±] {f : Œ± ‚Üí Œ±} {s : set Œ±}
  {c : nnreal} (hf : lipschitz_with c (restrict f s)) (hc : c < 1) :
  inj_on (Œª x, f x + x) s :=
hf.inj_on_add_isometry hc isometry_id

/-- The space of bounded sequences, with its sup norm -/
@[reducible] def ‚Ñì_infty_‚Ñù : Type := bounded_continuous_function ‚Ñï ‚Ñù
open bounded_continuous_function metric topological_space

namespace Kuratowski_embedding

/-! ### In this section, we show that any separable metric space can be embedded isometrically in ‚Ñì^‚àû(‚Ñù) -/

variables {f g : ‚Ñì_infty_‚Ñù} {n : ‚Ñï} {C : ‚Ñù} [metric_space Œ±] (x : ‚Ñï ‚Üí Œ±) (a b : Œ±)

/-- A metric space can be embedded in `l^‚àû(‚Ñù)` via the distances to points in
a fixed countable set, if this set is dense. This map is given in the next definition,
without density assumptions. -/
def embedding_of_subset : ‚Ñì_infty_‚Ñù :=
of_normed_group_discrete (Œªn, dist a (x n) - dist (x 0) (x n)) (dist a (x 0)) (Œª_, abs_dist_sub_le _ _ _)

lemma embedding_of_subset_coe : embedding_of_subset x a n = dist a (x n) - dist (x 0) (x n) := rfl

/-- The embedding map is always a semi-contraction. -/
lemma embedding_of_subset_dist_le (a b : Œ±) :
  dist (embedding_of_subset x a) (embedding_of_subset x b) ‚â§ dist a b :=
begin
  refine (dist_le dist_nonneg).2 (Œªn, _),
  have A : dist a (x n) + (dist (x 0) (x n) + (-dist b (x n) + -dist (x 0) (x n)))
    = dist a (x n) - dist b (x n), by ring,
  simp only [embedding_of_subset_coe, real.dist_eq, A, add_comm, neg_add_rev, _root_.neg_neg,
             sub_eq_add_neg, add_left_comm],
  exact abs_dist_sub_le _ _ _
end

/-- When the reference set is dense, the embedding map is an isometry on its image. -/
lemma embedding_of_subset_isometry (H : closure (range x) = univ) : isometry (embedding_of_subset x) :=
begin
  refine isometry_emetric_iff_metric.2 (Œªa b, _),
  refine le_antisymm (embedding_of_subset_dist_le x a b) (real.le_of_forall_epsilon_le (Œªe epos, _)),
  /- First step: find n with dist a (x n) < e -/
  have A : a ‚àà closure (range x), by { have B := mem_univ a, rwa [‚Üê H] at B },
  rcases mem_closure_iff'.1 A (e/2) (half_pos epos) with ‚ü®d, ‚ü®drange, hd‚ü©‚ü©,
  cases drange with n dn,
  rw [‚Üê dn] at hd,
  /- Second step: use the norm control at index n to conclude -/
  have C : dist b (x n) - dist a (x n) = embedding_of_subset x b n - embedding_of_subset x a n :=
    by { simp [embedding_of_subset_coe] },
  have := calc
    dist a b ‚â§ dist a (x n) + dist (x n) b : dist_triangle _ _ _
    ...    = 2 * dist a (x n) + (dist b (x n) - dist a (x n)) : by { simp [dist_comm], ring }
    ...    ‚â§ 2 * dist a (x n) + abs (dist b (x n) - dist a (x n)) :
      by apply_rules [add_le_add_left, le_abs_self]
    ...    ‚â§ 2 * (e/2) + abs (embedding_of_subset x b n - embedding_of_subset x a n) :
      begin rw [C], apply_rules [add_le_add, mul_le_mul_of_nonneg_left, le_of_lt hd, le_refl], norm_num end
    ...    ‚â§ 2 * (e/2) + dist (embedding_of_subset x b) (embedding_of_subset x a) :
      begin rw [‚Üê coe_diff], apply add_le_add_left, rw [coe_diff, ‚Üêreal.dist_eq], apply dist_coe_le_dist end
    ...    = dist (embedding_of_subset x b) (embedding_of_subset x a) + e : by ring,
  simpa [dist_comm] using this
end

/-- Every separable metric space embeds isometrically in ‚Ñì_infty_‚Ñù. -/
theorem exists_isometric_embedding (Œ± : Type u) [metric_space Œ±] [separable_space Œ±] :
  ‚àÉ(f : Œ± ‚Üí ‚Ñì_infty_‚Ñù), isometry f :=
begin
  classical,
  by_cases h : (univ : set Œ±) = ‚àÖ,
  { use (Œª_, 0), assume x, exact (ne_empty_of_mem (mem_univ x) h).elim },
  { /- We construct a map x : ‚Ñï ‚Üí Œ± with dense image -/
    rcases exists_mem_of_ne_empty h with basepoint,
    haveI : inhabited Œ± := ‚ü®basepoint‚ü©,
    have : ‚àÉs:set Œ±, countable s ‚àß closure s = univ := separable_space.exists_countable_closure_eq_univ _,
    rcases this with ‚ü®S, ‚ü®S_countable, S_dense‚ü©‚ü©,
    rcases countable_iff_exists_surjective.1 S_countable with ‚ü®x, x_range‚ü©,
    have : closure (range x) = univ :=
      univ_subset_iff.1 (by { rw [‚Üê S_dense], apply closure_mono, assumption }),
    /- Use embedding_of_subset to construct the desired isometry -/
    exact ‚ü®embedding_of_subset x, embedding_of_subset_isometry x this‚ü© }
end
end Kuratowski_embedding

open topological_space Kuratowski_embedding

/-- The Kuratowski embedding is an isometric embedding of a separable metric space in ‚Ñì^‚àû(‚Ñù) -/
def Kuratowski_embedding (Œ± : Type u) [metric_space Œ±] [separable_space Œ±] : Œ± ‚Üí ‚Ñì_infty_‚Ñù :=
  classical.some (Kuratowski_embedding.exists_isometric_embedding Œ±)

/-- The Kuratowski embedding is an isometry -/
protected lemma Kuratowski_embedding.isometry (Œ± : Type u) [metric_space Œ±] [separable_space Œ±] :
  isometry (Kuratowski_embedding Œ±) :=
classical.some_spec (exists_isometric_embedding Œ±)

/-- Version of the Kuratowski embedding for nonempty compacts -/
def nonempty_compacts.Kuratowski_embedding (Œ± : Type u) [metric_space Œ±] [compact_space Œ±] [nonempty Œ±] :
  nonempty_compacts ‚Ñì_infty_‚Ñù :=
‚ü®range (Kuratowski_embedding Œ±),
begin
  split,
  { rcases exists_mem_of_nonempty Œ± with ‚ü®x, hx‚ü©,
    have A : Kuratowski_embedding Œ± x ‚àà range (Kuratowski_embedding Œ±) := ‚ü®x, by simp‚ü©,
    apply ne_empty_of_mem A },
  { rw ‚Üê image_univ,
    exact compact_univ.image (Kuratowski_embedding.isometry Œ±).continuous },
end‚ü©
