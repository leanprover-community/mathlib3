/-
Copyright (c) 2018 Rohan Mitta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Rohan Mitta, Kevin Buzzard, Alistair Tucker, Johannes HÃ¶lzl

Lipschitz functions and the Banach fixed-point theorem
-/
import topology.metric_space.basic analysis.specific_limits
open filter
open_locale topological_space

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*}

lemma fixed_point_of_tendsto_iterate [topological_space Î±] [t2_space Î±] {f : Î± â†’ Î±} {x : Î±}
  (hf : tendsto f (ğ“ x) (ğ“ (f x))) (hx : âˆƒ xâ‚€ : Î±, tendsto (Î» n, f^[n] xâ‚€) at_top (ğ“ x)) :
  f x = x :=
begin
  rcases hx with âŸ¨xâ‚€, hxâŸ©,
  refine tendsto_nhds_unique at_top_ne_bot _ hx,
  rw [â† tendsto_add_at_top_iff_nat 1, funext (assume n, nat.iterate_succ' f n xâ‚€)],
  exact hf.comp hx
end

/-- A Lipschitz function is uniformly continuous -/
lemma uniform_continuous_of_lipschitz [metric_space Î±] [metric_space Î²] {K : â„}
  {f : Î± â†’ Î²} (H : âˆ€x y, dist (f x) (f y) â‰¤ K * dist x y) : uniform_continuous f :=
begin
  have : 0 < max K 1 := lt_of_lt_of_le zero_lt_one (le_max_right K 1),
  refine metric.uniform_continuous_iff.2 (Î»Îµ Îµpos, _),
  exact âŸ¨Îµ/max K 1, div_pos Îµpos this, assume y x Dyx, calc
    dist (f y) (f x) â‰¤ K * dist y x : H y x
    ... â‰¤ max K 1 * dist y x : mul_le_mul_of_nonneg_right (le_max_left K 1) (dist_nonneg)
    ... < max K 1 * (Îµ/max K 1) : mul_lt_mul_of_pos_left Dyx this
    ... = Îµ : mul_div_cancel' _ (ne_of_gt this)âŸ©
end

/-- A Lipschitz function is continuous -/
lemma continuous_of_lipschitz [metric_space Î±] [metric_space Î²] {K : â„}
  {f : Î± â†’ Î²} (H : âˆ€x y, dist (f x) (f y) â‰¤ K * dist x y) : continuous f :=
uniform_continuous.continuous (uniform_continuous_of_lipschitz H)

lemma uniform_continuous_of_le_add [metric_space Î±] {f : Î± â†’ â„} (K : â„)
  (h : âˆ€x y, f x â‰¤ f y + K * dist x y) : uniform_continuous f :=
begin
  have I : âˆ€ (x y : Î±), f x - f y â‰¤ K * dist x y := Î»x y, calc
    f x - f y â‰¤ (f y + K * dist x y) - f y : add_le_add (h x y) (le_refl _)
    ... = K * dist x y : by ring,
  refine @uniform_continuous_of_lipschitz _ _ _ _ K _ (Î»x y, _),
  rw real.dist_eq,
  refine abs_sub_le_iff.2 âŸ¨_, _âŸ©,
  { exact I x y },
  { rw dist_comm, exact I y x }
end

/-- `lipschitz_with K f`: the function `f` is Lipschitz continuous w.r.t. the Lipschitz
constant `K`. -/
def lipschitz_with [metric_space Î±] [metric_space Î²] (K : â„) (f : Î± â†’ Î²) :=
0 â‰¤ K âˆ§ âˆ€x y, dist (f x) (f y) â‰¤ K * dist x y

namespace lipschitz_with

variables [metric_space Î±] [metric_space Î²] [metric_space Î³] {K : â„}

protected lemma weaken (K' : â„) {f : Î± â†’ Î²} (hf : lipschitz_with K f) (h : K â‰¤ K') :
  lipschitz_with K' f :=
âŸ¨le_trans hf.1 h, assume x y, le_trans (hf.2 x y) $ mul_le_mul_of_nonneg_right h dist_nonnegâŸ©

protected lemma to_uniform_continuous {f : Î± â†’ Î²} (hf : lipschitz_with K f) : uniform_continuous f :=
uniform_continuous_of_lipschitz hf.2

protected lemma to_continuous {f : Î± â†’ Î²} (hf : lipschitz_with K f) : continuous f :=
continuous_of_lipschitz hf.2

protected lemma const (b : Î²) : lipschitz_with 0 (Î»a:Î±, b) :=
âŸ¨le_refl 0, assume x y, by simpâŸ©

protected lemma id : lipschitz_with 1 (@id Î±) :=
âŸ¨zero_le_one, by simp [le_refl]âŸ©

protected lemma comp {Kf Kg : â„} {f : Î² â†’ Î³} {g : Î± â†’ Î²}
  (hf : lipschitz_with Kf f) (hg : lipschitz_with Kg g) : lipschitz_with (Kf * Kg) (f âˆ˜ g) :=
âŸ¨mul_nonneg hf.1 hg.1, assume x y,
  calc dist (f (g x)) (f (g y)) â‰¤ Kf * dist (g x) (g y) : hf.2 _ _
    ... â‰¤ Kf * (Kg * dist x y) : mul_le_mul_of_nonneg_left (hg.2 _ _) hf.1
    ... = (Kf * Kg) * dist x y : by rw mul_assocâŸ©

protected lemma iterate {f : Î± â†’ Î±} (hf : lipschitz_with K f) : âˆ€n, lipschitz_with (K ^ n) (f^[n])
| 0       := lipschitz_with.id
| (n + 1) := by rw [â† nat.succ_eq_add_one, pow_succ, mul_comm]; exact (iterate n).comp hf

section contraction
variables {f : Î± â†’ Î±} {x y : Î±}

lemma dist_inequality_of_contraction (hKâ‚ : K < 1) (hf : lipschitz_with K f) :
   dist x y â‰¤ (dist x (f x) + dist y (f y)) / (1 - K) :=
suffices dist x y â‰¤ dist x (f x) + (dist y (f y) + K * dist x y),
  by rwa [le_div_iff (sub_pos_of_lt hKâ‚), mul_comm, sub_mul, one_mul, sub_le_iff_le_add, add_assoc],
calc dist x y â‰¤ dist x (f x) + dist y (f x) :
    dist_triangle_right x y (f x)
  ... â‰¤ dist x (f x) + (dist y (f y) + dist (f x) (f y)) :
    add_le_add_left (dist_triangle_right y (f x) (f y)) _
  ... â‰¤ dist x (f x) + (dist y (f y) + K * dist x y) :
    add_le_add_left (add_le_add_left (hf.2 _ _) _) _

theorem fixed_point_unique_of_contraction (hK : K < 1) (hf : lipschitz_with K f)
  (hx : f x = x) (hy : f y = y) : x = y :=
dist_le_zero.1 $ le_trans (dist_inequality_of_contraction hK hf) $
  by rewrite [iff.mpr dist_eq_zero hx.symm, iff.mpr dist_eq_zero hy.symm]; simp

/-- Banach fixed-point theorem, contraction mapping theorem -/
theorem exists_fixed_point_of_contraction [hÎ± : nonempty Î±] [complete_space Î±]
  (hK : K < 1) (hf : lipschitz_with K f) : âˆƒx, f x = x :=
let âŸ¨xâ‚€âŸ© := hÎ± in
have cauchy_seq (Î» n, f^[n] xâ‚€) := begin
  refine cauchy_seq_of_le_geometric K (dist xâ‚€ (f xâ‚€)) hK (Î»n, _),
  rw [nat.iterate_succ f n xâ‚€, mul_comm],
  exact and.right (hf.iterate n) xâ‚€ (f xâ‚€)
end,
let âŸ¨x, hxâŸ© := cauchy_seq_tendsto_of_complete this in
âŸ¨x, fixed_point_of_tendsto_iterate (hf.to_uniform_continuous.continuous.tendsto x) âŸ¨xâ‚€, hxâŸ©âŸ©

end contraction

end lipschitz_with
