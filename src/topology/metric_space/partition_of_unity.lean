/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import topology.metric_space.emetric_paracompact
import analysis.convex.partition_of_unity

/-!
# Lemmas about (e)metric spaces that need partition of unity

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

The main lemma in this file (see `metric.exists_continuous_real_forall_closed_ball_subset`) says the
following. Let `X` be a metric space. Let `K : Î¹ â†’ set X` be a locally finite family of closed sets,
let `U : Î¹ â†’ set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there exists a
positive continuous function `Î´ : C(X, â†’ â„)` such that for any `i` and `x âˆˆ K i`, we have
`metric.closed_ball x (Î´ x) âŠ† U i`. We also formulate versions of this lemma for extended metric
spaces and for different codomains (`â„`, `â„â‰¥0`, and `â„â‰¥0âˆ`).

We also prove a few auxiliary lemmas to be used later in a proof of the smooth version of this
lemma.

## Tags

metric space, partition of unity, locally finite
-/

open_locale topology ennreal big_operators nnreal filter
open set function filter topological_space

variables {Î¹ X : Type*}

namespace emetric

variables [emetric_space X] {K : Î¹ â†’ set X} {U : Î¹ â†’ set X}

/-- Let `K : Î¹ â†’ set X` be a locally finitie family of closed sets in an emetric space. Let
`U : Î¹ â†’ set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then for any point
`x : X`, for sufficiently small `r : â„â‰¥0âˆ` and for `y` sufficiently close to `x`, for all `i`, if
`y âˆˆ K i`, then `emetric.closed_ball y r âŠ† U i`. -/
lemma eventually_nhds_zero_forall_closed_ball_subset (hK : âˆ€ i, is_closed (K i))
  (hU : âˆ€ i, is_open (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : locally_finite K) (x : X) :
  âˆ€á¶  p : â„â‰¥0âˆ Ã— X in ğ“ 0 Ã—á¶  ğ“ x, âˆ€ i, p.2 âˆˆ K i â†’ closed_ball p.2 p.1 âŠ† U i :=
begin
  suffices : âˆ€ i, x âˆˆ K i â†’ âˆ€á¶  p : â„â‰¥0âˆ Ã— X in ğ“ 0 Ã—á¶  ğ“ x, closed_ball p.2 p.1 âŠ† U i,
  { filter_upwards [tendsto_snd (hfin.Inter_compl_mem_nhds hK x),
      (eventually_all_finite (hfin.point_finite x)).2 this],
    rintro âŸ¨r, yâŸ© hxy hyU i hi,
    simp only [mem_Interâ‚‚, mem_compl_iff, not_imp_not, mem_preimage] at hxy,
    exact hyU _ (hxy _ hi) },
  intros i hi,
  rcases nhds_basis_closed_eball.mem_iff.1 ((hU i).mem_nhds $ hKU i hi) with âŸ¨R, hRâ‚€, hRâŸ©,
  rcases ennreal.lt_iff_exists_nnreal_btwn.mp hRâ‚€ with âŸ¨r, hrâ‚€, hrRâŸ©,
  filter_upwards [prod_mem_prod (eventually_lt_nhds hrâ‚€)
    (closed_ball_mem_nhds x (tsub_pos_iff_lt.2 hrR))] with p hp z hz,
  apply hR,
  calc edist z x â‰¤ edist z p.2 + edist p.2 x : edist_triangle _ _ _
  ... â‰¤ p.1 + (R - p.1) : add_le_add hz $ le_trans hp.2 $ tsub_le_tsub_left hp.1.out.le _
  ... = R : add_tsub_cancel_of_le (lt_trans hp.1 hrR).le,
end

lemma exists_forall_closed_ball_subset_auxâ‚ (hK : âˆ€ i, is_closed (K i))
  (hU : âˆ€ i, is_open (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : locally_finite K) (x : X) :
  âˆƒ r : â„, âˆ€á¶  y in ğ“ x, r âˆˆ Ioi (0 : â„) âˆ©
    ennreal.of_real â»Â¹' â‹‚ i (hi : y âˆˆ K i), {r | closed_ball y r âŠ† U i} :=
begin
  have := (ennreal.continuous_of_real.tendsto' 0 0 ennreal.of_real_zero).eventually
    (eventually_nhds_zero_forall_closed_ball_subset hK hU hKU hfin x).curry,
  rcases this.exists_gt with âŸ¨r, hr0, hrâŸ©,
  refine âŸ¨r, hr.mono (Î» y hy, âŸ¨hr0, _âŸ©)âŸ©,
  rwa [mem_preimage, mem_Interâ‚‚]
end

lemma exists_forall_closed_ball_subset_auxâ‚‚ (y : X) :
  convex â„ (Ioi (0 : â„) âˆ© ennreal.of_real â»Â¹' â‹‚ i (hi : y âˆˆ K i), {r | closed_ball y r âŠ† U i}) :=
(convex_Ioi _).inter $ ord_connected.convex $ ord_connected.preimage_ennreal_of_real $
  ord_connected_Inter $ Î» i, ord_connected_Inter $
    Î» hi, ord_connected_set_of_closed_ball_subset y (U i)

/-- Let `X` be an extended metric space. Let `K : Î¹ â†’ set X` be a locally finite family of closed
sets, let `U : Î¹ â†’ set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there
exists a positive continuous function `Î´ : C(X, â„)` such that for any `i` and `x âˆˆ K i`,
we have `emetric.closed_ball x (ennreal.of_real (Î´ x)) âŠ† U i`. -/
lemma exists_continuous_real_forall_closed_ball_subset (hK : âˆ€ i, is_closed (K i))
  (hU : âˆ€ i, is_open (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : locally_finite K) :
  âˆƒ Î´ : C(X, â„), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ i (x âˆˆ K i), closed_ball x (ennreal.of_real $ Î´ x) âŠ† U i :=
by simpa only [mem_inter_iff, forall_and_distrib, mem_preimage, mem_Inter, @forall_swap Î¹ X]
  using exists_continuous_forall_mem_convex_of_local_const exists_forall_closed_ball_subset_auxâ‚‚
    (exists_forall_closed_ball_subset_auxâ‚ hK hU hKU hfin)

/-- Let `X` be an extended metric space. Let `K : Î¹ â†’ set X` be a locally finite family of closed
sets, let `U : Î¹ â†’ set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there
exists a positive continuous function `Î´ : C(X, â„â‰¥0)` such that for any `i` and `x âˆˆ K i`,
we have `emetric.closed_ball x (Î´ x) âŠ† U i`. -/
lemma exists_continuous_nnreal_forall_closed_ball_subset (hK : âˆ€ i, is_closed (K i))
  (hU : âˆ€ i, is_open (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : locally_finite K) :
  âˆƒ Î´ : C(X, â„â‰¥0), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ i (x âˆˆ K i), closed_ball x (Î´ x) âŠ† U i :=
begin
  rcases exists_continuous_real_forall_closed_ball_subset hK hU hKU hfin with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©,
  lift Î´ to C(X, â„â‰¥0) using Î» x, (hÎ´â‚€ x).le,
  refine âŸ¨Î´, hÎ´â‚€, Î» i x hi, _âŸ©,
  simpa only [â† ennreal.of_real_coe_nnreal] using hÎ´ i x hi
end

/-- Let `X` be an extended metric space. Let `K : Î¹ â†’ set X` be a locally finite family of closed
sets, let `U : Î¹ â†’ set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there
exists a positive continuous function `Î´ : C(X, â„â‰¥0âˆ)` such that for any `i` and `x âˆˆ K i`,
we have `emetric.closed_ball x (Î´ x) âŠ† U i`. -/
lemma exists_continuous_ennreal_forall_closed_ball_subset (hK : âˆ€ i, is_closed (K i))
  (hU : âˆ€ i, is_open (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : locally_finite K) :
  âˆƒ Î´ : C(X, â„â‰¥0âˆ), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ i (x âˆˆ K i), closed_ball x (Î´ x) âŠ† U i :=
let âŸ¨Î´, hÎ´â‚€, hÎ´âŸ© := exists_continuous_nnreal_forall_closed_ball_subset hK hU hKU hfin
in âŸ¨continuous_map.comp âŸ¨coe, ennreal.continuous_coeâŸ© Î´, Î» x, ennreal.coe_pos.2 (hÎ´â‚€ x), hÎ´âŸ©

end emetric

namespace metric

variables [metric_space X] {K : Î¹ â†’ set X} {U : Î¹ â†’ set X}

/-- Let `X` be a metric space. Let `K : Î¹ â†’ set X` be a locally finite family of closed sets, let
`U : Î¹ â†’ set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there exists a
positive continuous function `Î´ : C(X, â„â‰¥0)` such that for any `i` and `x âˆˆ K i`, we have
`metric.closed_ball x (Î´ x) âŠ† U i`. -/
lemma exists_continuous_nnreal_forall_closed_ball_subset (hK : âˆ€ i, is_closed (K i))
  (hU : âˆ€ i, is_open (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : locally_finite K) :
  âˆƒ Î´ : C(X, â„â‰¥0), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ i (x âˆˆ K i), closed_ball x (Î´ x) âŠ† U i :=
begin
  rcases emetric.exists_continuous_nnreal_forall_closed_ball_subset hK hU hKU hfin
    with âŸ¨Î´, hÎ´0, hÎ´âŸ©,
  refine âŸ¨Î´, hÎ´0, Î» i x hx, _âŸ©,
  rw [â† emetric_closed_ball_nnreal],
  exact hÎ´ i x hx
end

/-- Let `X` be a metric space. Let `K : Î¹ â†’ set X` be a locally finite family of closed sets, let
`U : Î¹ â†’ set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there exists a
positive continuous function `Î´ : C(X, â„)` such that for any `i` and `x âˆˆ K i`, we have
`metric.closed_ball x (Î´ x) âŠ† U i`. -/
lemma exists_continuous_real_forall_closed_ball_subset (hK : âˆ€ i, is_closed (K i))
  (hU : âˆ€ i, is_open (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : locally_finite K) :
  âˆƒ Î´ : C(X, â„), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ i (x âˆˆ K i), closed_ball x (Î´ x) âŠ† U i :=
let âŸ¨Î´, hÎ´â‚€, hÎ´âŸ© := exists_continuous_nnreal_forall_closed_ball_subset hK hU hKU hfin
in âŸ¨continuous_map.comp âŸ¨coe, nnreal.continuous_coeâŸ© Î´, hÎ´â‚€, hÎ´âŸ©

end metric
