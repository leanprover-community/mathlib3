/-
Copyright (c) 2023 Junyan Xu, R√©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu, R√©mi Bottinelli
-/
import analysis.bounded_variation
import topology.path_connected
import topology.metric_space.arclength
import topology.metric_space.lipschitz

/-!
# Path Metric



-/


open_locale nnreal ennreal big_operators
open set unit_interval
noncomputable theory


local notation `ùïÄ` := unit_interval

alias evariation_on.eq_of_eq_on ‚Üê set.eq_on.evariation_on_eq

namespace path

variables {E : Type*} [pseudo_emetric_space E] {x y z : E} (p : path x y) (q : path y z)

def length (p : path x y) : ‚Ñù‚â•0‚àû := evariation_on p set.univ --arclength p 0 1

lemma edist_le_length (p : path x y) : edist x y ‚â§ p.length :=
by { simp_rw [length, ‚Üê p.source, ‚Üê p.target], exact evariation_on.edist_le _ trivial trivial }

-- TODO : move to `data/set/image.lean#870` after `set.subsingleton.image` ?
lemma _root_.set.subsingleton_image_of_const {Œ± Œ≤ : Type*} (b : Œ≤) (s : set Œ±) :
  ((Œª _, b) '' s).subsingleton := by
{ rintro _ ‚ü®_, _, rfl‚ü© _ ‚ü®_, _, rfl‚ü©, refl }

lemma length_refl (x : E) : (path.refl x).length = 0 :=
evariation_on.constant_on (set.subsingleton_image_of_const _ _)

lemma length_symm (p : path x y) : p.symm.length = p.length :=
evariation_on.comp_eq_of_antitone_on _ _ (unit_interval.antitone_symm.antitone_on _)
  (set.maps_to_univ _ _) $
  set.surjective_iff_surj_on_univ.1 $ unit_interval.bijective_symm.surjective

lemma length_trans (p : path x y) (q : path y z) : (p.trans q).length = p.length + q.length :=
begin
  rw [length, evariation_on.split_univ _ (div_two 1),
    (p.trans_eq_on_left q).evariation_on_eq, (p.trans_eq_on_right q).evariation_on_eq],
  congr' 1; apply evariation_on.comp_eq_of_monotone_on _ _ _ (set.maps_to_univ _ _),
  { refine Œª t ht, ‚ü®div_two t, div_two_mem_Iic t, _‚ü©,
    dsimp only, rw [two_mul_div_two, set.proj_Icc_of_mem _ t.prop],
    ext, refl },
  work_on_goal 2 { refine Œª t ht, ‚ü®unit_interval.symm
      (div_two $ unit_interval.symm t), (symm_mem_Ici_iff _).2 (div_two_mem_Iic _), _‚ü©,
    dsimp only, rw [coe_symm_eq, mul_sub, mul_one, two_mul_div_two, sub_right_comm,
      (_ : (2 - 1 : ‚Ñù) = 1), ‚Üê coe_symm_eq, unit_interval.symm_symm, set.proj_Icc_of_mem _ t.prop],
    { ext, refl }, norm_num },
  all_goals { apply ((set.monotone_proj_Icc _).comp $ Œª x y h, _).monotone_on },
  { exact mul_le_mul_of_nonneg_left h zero_le_two },
  { exact sub_le_sub_right (mul_le_mul_of_nonneg_left h zero_le_two) _ },
end

lemma comp_length_le {F : Type*} [pseudo_emetric_space F] (p : path x y) {œÜ : E ‚Üí F} {K : ‚Ñù‚â•0}
  (hœÜ : lipschitz_with K œÜ) : (p.map hœÜ.continuous).length ‚â§ ‚ÜëK * p.length :=
lipschitz_on_with.comp_evariation_on_le (hœÜ.lipschitz_on_with set.univ) (set.maps_to_univ _ _)

/- Two definitions agree. -/
lemma evariation_on_extend_unit_interval_eq_length (p : path x y) :
  evariation_on p.extend ùïÄ = p.length := arclength_Icc_extend zero_le_one p

lemma length_of_continuous_on {X : Type*} {f : ‚Ñù ‚Üí X} {s t : ‚Ñù} (hst : s ‚â§ t)
  [pseudo_emetric_space X] (hf : continuous_on f (set.Icc s t)) :
  (of_continuous_on hst hf).length = arclength f s t :=
begin
  apply evariation_on.comp_eq_of_monotone_on _ _ (monotone.monotone_on _ _) _ (Œª x hx, _),
  { exact (monotone_affine_map_of_le hst).comp (Œª _ _, id) },
  { exact Œª x hx, affine_map_maps_to_I hst x.2 },
  { obtain ‚ü®y, hy, h'‚ü© := affine_map_surj_on_I hst hx, exact ‚ü®‚ü®y, hy‚ü©, trivial, h'‚ü© },
end

end path

section path_emetric

def path_emetric (E : Type*) [pseudo_emetric_space E] := E

variables {E : Type*} [pseudo_emetric_space E]

/-- Casting from `E` to `path_emetric E`. -/
def to_path_emetric : E ‚âÉ path_emetric E := equiv.refl _

/-- Casting from `path_emetric E` to `E`. -/
def from_path_emetric : path_emetric E ‚âÉ E := equiv.refl _
/- TODO: should make from_path_metric a bundled continuous map ... more useful than equiv/bij? -/

lemma from_to_path_emetric (x : E) : from_path_emetric (to_path_emetric x) = x := rfl
lemma to_from_path_emetric (x : path_emetric E) : to_path_emetric (from_path_emetric x) = x := rfl

local notation `of` := to_path_emetric
local notation `fo` := from_path_emetric

instance : pseudo_emetric_space (path_emetric E) :=
{ edist := Œª x y, ‚®Ö p : path (fo x) (fo y), p.length,
  edist_self := Œª x, le_antisymm (infi_le_of_le _ $ (path.length_refl _).le) zero_le',
  edist_comm := Œª x y, by apply le_antisymm;
    exact le_infi (Œª p, (infi_le _ _).trans_eq p.length_symm),
  edist_triangle := Œª x y z, begin
    simp_rw [ennreal.infi_add, ennreal.add_infi],
    exact le_infi‚ÇÇ (Œª p q, (infi_le _ _).trans_eq $ p.length_trans q),
  end }

/-- `E` is a length space if the path emetric on `E` is equal to its original metric. -/
class length_space (E) [pseudo_emetric_space E] : Prop :=
(isom : isometry (of : E ‚Üí path_emetric E))

lemma edist_le_edist_of (x y : E) : edist x y ‚â§ edist (of x) (of y) :=
le_infi $ Œª p, p.edist_le_length

lemma from_length_emetric_nonexpanding :
  lipschitz_with 1 (from_path_emetric : path_emetric E ‚Üí E) :=
Œª x y, by { rw [ennreal.coe_one, one_mul], exact edist_le_edist_of x y }

lemma continuous_from_length_emetric : continuous (from_path_emetric : path_emetric E ‚Üí E) :=
from_length_emetric_nonexpanding.continuous

lemma path_emetric.edist_le_arclength {f : ‚Ñù ‚Üí E} {s t : ‚Ñù} (hst : s ‚â§ t)
  (hf : continuous_on f (set.Icc s t)) : edist (of $ f s) (of $ f t) ‚â§ arclength f s t :=
(infi_le _ $ path.of_continuous_on hst hf).trans_eq $ path.length_of_continuous_on _ _

lemma path_emetric.edist_le_max {f : ‚Ñù ‚Üí E} (s t : ‚Ñù) (hf : continuous_on f (set.uIcc s t)) :
  edist (of $ f s) (of $ f t) ‚â§ max (arclength f s t) (arclength f t s) :=
begin
  obtain hst | hts := le_total s t,
  { rw max_eq_left ((arclength_eq_zero f hst).trans_le $ zero_le _),
    exact path_emetric.edist_le_arclength hst (hf.mono set.Icc_subset_uIcc) },
  { rw [max_eq_right ((arclength_eq_zero f hts).trans_le $ zero_le _), edist_comm],
    exact path_emetric.edist_le_arclength hts (hf.mono set.Icc_subset_uIcc') },
end

lemma path_emetric.continuous_of_locally_bounded_variation
  {f : ‚Ñù ‚Üí E} {s : set ‚Ñù} (hconn : s.ord_connected)
  (hcont : continuous_on f s) (fbdd : has_locally_bounded_variation_on f s) :
  continuous_on (of ‚àò f) s :=
Œª x hx, begin
  simp_rw [continuous_within_at, emetric.tendsto_nhds, edist_comm],
  have := (continuous_on_arclength f x hconn fbdd hcont x hx).max
    (continuous_on_arclength' f x hconn fbdd hcont x hx),
  dsimp only at this, rw [arclength_self, max_self] at this,
  exact Œª Œµ hŒµ, (ennreal.nhds_zero_basis.tendsto_right_iff.1 this Œµ hŒµ).mp
    (filter.eventually_inf_principal.2 $ filter.eventually_of_forall $ Œª y hy h,
    (path_emetric.edist_le_max x y $ hcont.mono $ hconn.uIcc_subset hx hy).trans_lt h),
end

@[simps] def path.of_length_ne_top {x y : E} (p : path x y) (hp : p.length ‚â† ‚ä§) : path (of x) (of y) :=
begin
  refine ‚ü®‚ü®of ‚àò p, _‚ü©, p.source, p.target‚ü©,
  convert continuous_on_iff_continuous_restrict.1
    (path_emetric.continuous_of_locally_bounded_variation
      set.ord_connected_Icc p.continuous_extend.continuous_on $
      has_bounded_variation_on.has_locally_bounded_variation_on _),
  { ext, rw p.extend_extends' },
  { rwa ‚Üê p.evariation_on_extend_unit_interval_eq_length at hp },
end

/-- Every continuous curve, rectifiable or not, has the same length in the path metric. -/
lemma path_emetric.evariation_of_eq_evariation {f : ‚Ñù ‚Üí E}
  {s : set ‚Ñù} (hconn : s.ord_connected) (hcont : continuous_on f s) :
  evariation_on (of ‚àò f) s = evariation_on f s :=
begin
  refine (supr_le _).antisymm
    (supr_le $ Œª u, trans (finset.sum_le_sum $ Œª i hi, edist_le_edist_of _ _) $ le_supr _ u),
  rintro ‚ü®n, u, um, us‚ü©,
  refine (finset.sum_le_sum $ Œª i hi, _).trans _,
  { exact Œª i, arclength f (u i) (u $ i+1) },
  { rw edist_comm, exact path_emetric.edist_le_arclength
      (um $ i.le_succ) (hcont.mono $ hconn.out (us i) (us $ i+1)) },
  { rw arclength_sum f um, apply evariation_on.mono f (hconn.out (us 0) (us n)) },
end

lemma path.length_of_length_ne_top {x y : E} (p : path x y) (hp : p.length ‚â† ‚ä§) :
  (p.of_length_ne_top hp).length = p.length :=
begin
  simp_rw [‚Üê path.evariation_on_extend_unit_interval_eq_length,
           ‚Üê path_emetric.evariation_of_eq_evariation
          set.ord_connected_Icc p.continuous_extend.continuous_on],
  refl,
end

lemma path.length_eq {x y : E} (p : path (of x) (of y)) :
  (p.map continuous_from_length_emetric).length = p.length :=
begin
  by_cases h : (p.map continuous_from_length_emetric).length = ‚ä§,
  { symmetry,
    simpa only [h, ennreal.coe_one, one_mul, top_le_iff] using
      path.comp_length_le p from_length_emetric_nonexpanding, },
  { rw ‚Üê(path.length_of_length_ne_top _ h),
    congr,
    ext,
    refl, },
end

instance (E) [pseudo_emetric_space E] : length_space (path_emetric E) :=
‚ü®Œª x y, begin
  refine (le_infi $ Œª p, _).antisymm (edist_le_edist_of x y),
  by_cases p.length = ‚ä§, { rw h, exact le_top },
  rw ‚Üê p.length_of_length_ne_top h,
  exact infi_le _ _,
end‚ü©

end path_emetric
