/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import topology.metric_space.basic
import topology.metric_space.emetric_paracompact
import topology.shrinking_lemma

/-!
# Shrinking lemma in a proper metric space

In this file we prove a few versions of the shrinking lemma for coverings by balls in a proper
(pseudo) metric space.

## Tags

shrinking lemma, metric space
-/

universes u v
open set metric
open_locale topological_space

variables {Œ± : Type u} {Œπ : Type v} [metric_space Œ±] [proper_space Œ±] {c : Œπ ‚Üí Œ±}
variables {x : Œ±} {r : ‚Ñù} {s : set Œ±}

/-- Shrinking lemma for coverings by open balls in a proper metric space. A point-finite open cover
of a closed subset of a proper metric space by open balls can be shrunk to a new cover by open balls
so that each of the new balls has strictly smaller radius than the old one. This version assumes
that `Œª x, ball (c i) (r i)` is a locally finite covering and provides a covering indexed by the
same type. -/
lemma exists_subset_Union_ball_radius_lt {r : Œπ ‚Üí ‚Ñù} (hs : is_closed s)
  (uf : ‚àÄ x ‚àà s, finite {i | x ‚àà ball (c i) (r i)}) (us : s ‚äÜ ‚ãÉ i, ball (c i) (r i)) :
  ‚àÉ r' : Œπ ‚Üí ‚Ñù, s ‚äÜ (‚ãÉ i, ball (c i) (r' i)) ‚àß ‚àÄ i, r' i < r i :=
begin
  rcases exists_subset_Union_closed_subset hs (Œª i, @is_open_ball _ _ (c i) (r i)) uf us
    with ‚ü®v, hsv, hvc, hcv‚ü©,
  have := Œª i, exists_lt_subset_ball (hvc i) (hcv i),
  choose r' hlt hsub,
  exact ‚ü®r', subset.trans hsv $ Union_subset_Union $ hsub, hlt‚ü©
end

/-- Shrinking lemma for coverings by open balls in a proper metric space. A point-finite open cover
of a proper metric space by open balls can be shrunk to a new cover by open balls so that each of
the new balls has strictly smaller radius than the old one. -/
lemma exists_Union_ball_eq_radius_lt {r : Œπ ‚Üí ‚Ñù} (uf : ‚àÄ x, finite {i | x ‚àà ball (c i) (r i)})
  (uU : (‚ãÉ i, ball (c i) (r i)) = univ) :
  ‚àÉ r' : Œπ ‚Üí ‚Ñù, (‚ãÉ i, ball (c i) (r' i)) = univ ‚àß ‚àÄ i, r' i < r i :=
let ‚ü®r', hU, hv‚ü© := exists_subset_Union_ball_radius_lt is_closed_univ (Œª x _, uf x) uU.ge
in ‚ü®r', univ_subset_iff.1 hU, hv‚ü©

/-- Shrinking lemma for coverings by open balls in a proper metric space. A point-finite open cover
of a closed subset of a proper metric space by nonempty open balls can be shrunk to a new cover by
nonempty open balls so that each of the new balls has strictly smaller radius than the old one. -/
lemma exists_subset_Union_ball_radius_pos_lt {r : Œπ ‚Üí ‚Ñù} (hr : ‚àÄ i, 0 < r i) (hs : is_closed s)
  (uf : ‚àÄ x ‚àà s, finite {i | x ‚àà ball (c i) (r i)}) (us : s ‚äÜ ‚ãÉ i, ball (c i) (r i)) :
  ‚àÉ r' : Œπ ‚Üí ‚Ñù, s ‚äÜ (‚ãÉ i, ball (c i) (r' i)) ‚àß ‚àÄ i, r' i ‚àà Ioo 0 (r i) :=
begin
  rcases exists_subset_Union_closed_subset hs (Œª i, @is_open_ball _ _ (c i) (r i)) uf us
    with ‚ü®v, hsv, hvc, hcv‚ü©,
  have := Œª i, exists_pos_lt_subset_ball (hr i) (hvc i) (hcv i),
  choose r' hlt hsub,
  exact ‚ü®r', subset.trans hsv $ Union_subset_Union hsub, hlt‚ü©
end

/-- Shrinking lemma for coverings by open balls in a proper metric space. A point-finite open cover
of a proper metric space by nonempty open balls can be shrunk to a new cover by nonempty open balls
so that each of the new balls has strictly smaller radius than the old one. -/
lemma exists_Union_ball_eq_radius_pos_lt {r : Œπ ‚Üí ‚Ñù} (hr : ‚àÄ i, 0 < r i)
  (uf : ‚àÄ x, finite {i | x ‚àà ball (c i) (r i)}) (uU : (‚ãÉ i, ball (c i) (r i)) = univ) :
  ‚àÉ r' : Œπ ‚Üí ‚Ñù, (‚ãÉ i, ball (c i) (r' i)) = univ ‚àß ‚àÄ i, r' i ‚àà Ioo 0 (r i) :=
let ‚ü®r', hU, hv‚ü© := exists_subset_Union_ball_radius_pos_lt hr is_closed_univ (Œª x _, uf x) uU.ge
in ‚ü®r', univ_subset_iff.1 hU, hv‚ü©

/-- Let `R : Œ± ‚Üí ‚Ñù` be a (possibly discontinuous) function on a proper metric space.
Let `s` be a closed set in `Œ±` such that `R` is positive on `s`. Then there exists a collection of
pairs of balls `metric.ball (c i) (r i)`, `metric.ball (c i) (r' i)` such that

* all centers belong to `s`;
* for all `i` we have `0 < r i < r' i < R (c i)`;
* the family of balls `metric.ball (c i) (r' i)` is locally finite;
* the balls `metric.ball (c i) (r i)` cover `s`.

This is a simple corollary of `refinement_of_locally_compact_sigma_compact_of_nhds_basis_set`
and `exists_subset_Union_ball_radius_pos_lt`. -/
lemma exists_locally_finite_subset_Union_ball_radius_lt (hs : is_closed s)
  {R : Œ± ‚Üí ‚Ñù} (hR : ‚àÄ x ‚àà s, 0 < R x) :
  ‚àÉ (Œπ : Type u) (c : Œπ ‚Üí Œ±) (r r' : Œπ ‚Üí ‚Ñù),
    (‚àÄ i, c i ‚àà s ‚àß 0 < r i ‚àß r i < r' i ‚àß r' i < R (c i)) ‚àß
    locally_finite (Œª i, ball (c i) (r' i)) ‚àß s ‚äÜ ‚ãÉ i, ball (c i) (r i) :=
begin
  have : ‚àÄ x ‚àà s, (ùìù x).has_basis (Œª r : ‚Ñù, 0 < r ‚àß r < R x) (Œª r, ball x r),
    from Œª x hx, nhds_basis_uniformity (uniformity_basis_dist_lt (hR x hx)),
  rcases refinement_of_locally_compact_sigma_compact_of_nhds_basis_set hs this
    with ‚ü®Œπ, c, r', hr', hsub', hfin‚ü©,
  rcases exists_subset_Union_ball_radius_pos_lt (Œª i, (hr' i).2.1) hs
    (Œª x hx, hfin.point_finite x) hsub' with ‚ü®r, hsub, hlt‚ü©,
  exact ‚ü®Œπ, c, r, r', Œª i, ‚ü®(hr' i).1, (hlt i).1, (hlt i).2, (hr' i).2.2‚ü©, hfin, hsub‚ü©
end

/-- Let `R : Œ± ‚Üí ‚Ñù` be a (possibly discontinuous) positive function on a proper metric space. Then
there exists a collection of pairs of balls `metric.ball (c i) (r i)`, `metric.ball (c i) (r' i)`
such that

* for all `i` we have `0 < r i < r' i < R (c i)`;
* the family of balls `metric.ball (c i) (r' i)` is locally finite;
* the balls `metric.ball (c i) (r i)` cover the whole space.

This is a simple corollary of `refinement_of_locally_compact_sigma_compact_of_nhds_basis`
and `exists_Union_ball_eq_radius_pos_lt` or `exists_locally_finite_subset_Union_ball_radius_lt`. -/
lemma exists_locally_finite_Union_eq_ball_radius_lt {R : Œ± ‚Üí ‚Ñù} (hR : ‚àÄ x, 0 < R x) :
  ‚àÉ (Œπ : Type u) (c : Œπ ‚Üí Œ±) (r r' : Œπ ‚Üí ‚Ñù), (‚àÄ i, 0 < r i ‚àß r i < r' i ‚àß r' i < R (c i)) ‚àß
    locally_finite (Œª i, ball (c i) (r' i)) ‚àß (‚ãÉ i, ball (c i) (r i)) = univ :=
let ‚ü®Œπ, c, r, r', hlt, hfin, hsub‚ü© := exists_locally_finite_subset_Union_ball_radius_lt
  is_closed_univ (Œª x _, hR x)
in ‚ü®Œπ, c, r, r', Œª i, (hlt i).2, hfin, univ_subset_iff.1 hsub‚ü©
