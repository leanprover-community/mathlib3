/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Patrick Massot
-/
import topology.basic
/-!
# Neighborhoods of a set

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define the filter `ğ“Ë¢ s` or `nhds_set s` consisting of all neighborhoods of a set
`s`.

## Main Properties

There are a couple different notions equivalent to `s âˆˆ ğ“Ë¢ t`:
* `s âŠ† interior t` using `subset_interior_iff_mem_nhds_set`
* `âˆ€ (x : Î±), x âˆˆ t â†’ s âˆˆ ğ“ x` using `mem_nhds_set_iff_forall`
* `âˆƒ U : set Î±, is_open U âˆ§ t âŠ† U âˆ§ U âŠ† s` using `mem_nhds_set_iff_exists`

Furthermore, we have the following results:
* `monotone_nhds_set`: `ğ“Ë¢` is monotone
* In Tâ‚-spaces, `ğ“Ë¢`is strictly monotone and hence injective:
  `strict_mono_nhds_set`/`injective_nhds_set`. These results are in `topology.separation`.
-/

open set filter
open_locale topology filter

variables {Î± Î² : Type*} [topological_space Î±] [topological_space Î²]
  {s t sâ‚ sâ‚‚ tâ‚ tâ‚‚ : set Î±} {x : Î±}

/-- The filter of neighborhoods of a set in a topological space. -/
def nhds_set (s : set Î±) : filter Î± :=
Sup (nhds '' s)

localized "notation (name := nhds_set) `ğ“Ë¢` := nhds_set" in topology

lemma nhds_set_diagonal (Î±) [topological_space (Î± Ã— Î±)] : ğ“Ë¢ (diagonal Î±) = â¨† x, ğ“ (x, x) :=
by { rw [nhds_set, â† range_diag, â† range_comp], refl }

lemma mem_nhds_set_iff_forall : s âˆˆ ğ“Ë¢ t â†” âˆ€ (x : Î±), x âˆˆ t â†’ s âˆˆ ğ“ x :=
by simp_rw [nhds_set, filter.mem_Sup, ball_image_iff]

lemma bUnion_mem_nhds_set {t : Î± â†’ set Î±} (h : âˆ€ x âˆˆ s, t x âˆˆ ğ“ x) : (â‹ƒ x âˆˆ s, t x) âˆˆ ğ“Ë¢ s :=
mem_nhds_set_iff_forall.2 $ Î» x hx, mem_of_superset (h x hx) (subset_Unionâ‚‚ x hx)

lemma subset_interior_iff_mem_nhds_set : s âŠ† interior t â†” t âˆˆ ğ“Ë¢ s :=
by simp_rw [mem_nhds_set_iff_forall, subset_interior_iff_nhds]

lemma mem_nhds_set_iff_exists : s âˆˆ ğ“Ë¢ t â†” âˆƒ U : set Î±, is_open U âˆ§ t âŠ† U âˆ§ U âŠ† s :=
by { rw [â† subset_interior_iff_mem_nhds_set, subset_interior_iff] }

lemma has_basis_nhds_set (s : set Î±) : (ğ“Ë¢ s).has_basis (Î» U, is_open U âˆ§ s âŠ† U) (Î» U, U) :=
âŸ¨Î» t, by simp [mem_nhds_set_iff_exists, and_assoc]âŸ©

lemma is_open.mem_nhds_set (hU : is_open s) : s âˆˆ ğ“Ë¢ t â†” t âŠ† s :=
by rw [â† subset_interior_iff_mem_nhds_set, interior_eq_iff_is_open.mpr hU]

lemma principal_le_nhds_set : ğ“Ÿ s â‰¤ ğ“Ë¢ s :=
Î» s hs, (subset_interior_iff_mem_nhds_set.mpr hs).trans interior_subset

@[simp] lemma nhds_set_eq_principal_iff : ğ“Ë¢ s = ğ“Ÿ s â†” is_open s :=
by rw [â† principal_le_nhds_set.le_iff_eq, le_principal_iff, mem_nhds_set_iff_forall,
  is_open_iff_mem_nhds]

alias nhds_set_eq_principal_iff â†” _ is_open.nhds_set_eq

@[simp] lemma nhds_set_interior : ğ“Ë¢ (interior s) = ğ“Ÿ (interior s) :=
is_open_interior.nhds_set_eq

@[simp] lemma nhds_set_singleton : ğ“Ë¢ {x} = ğ“ x :=
by { ext,
     rw [â† subset_interior_iff_mem_nhds_set, â† mem_interior_iff_mem_nhds, singleton_subset_iff] }

lemma mem_nhds_set_interior : s âˆˆ ğ“Ë¢ (interior s) :=
subset_interior_iff_mem_nhds_set.mp subset.rfl

@[simp] lemma nhds_set_empty : ğ“Ë¢ (âˆ… : set Î±) = âŠ¥ :=
by rw [is_open_empty.nhds_set_eq, principal_empty]

lemma mem_nhds_set_empty : s âˆˆ ğ“Ë¢ (âˆ… : set Î±) := by simp

@[simp] lemma nhds_set_univ : ğ“Ë¢ (univ : set Î±) = âŠ¤ :=
by rw [is_open_univ.nhds_set_eq, principal_univ]

@[mono] lemma nhds_set_mono (h : s âŠ† t) : ğ“Ë¢ s â‰¤ ğ“Ë¢ t :=  Sup_le_Sup $ image_subset _ h

lemma monotone_nhds_set : monotone (ğ“Ë¢ : set Î± â†’ filter Î±) := Î» s t, nhds_set_mono

lemma nhds_le_nhds_set (h : x âˆˆ s) : ğ“ x â‰¤ ğ“Ë¢ s := le_Sup $ mem_image_of_mem _ h

@[simp] lemma nhds_set_union (s t : set Î±) : ğ“Ë¢ (s âˆª t) = ğ“Ë¢ s âŠ” ğ“Ë¢ t :=
by simp only [nhds_set, image_union, Sup_union]

lemma union_mem_nhds_set (hâ‚ : sâ‚ âˆˆ ğ“Ë¢ tâ‚) (hâ‚‚ : sâ‚‚ âˆˆ ğ“Ë¢ tâ‚‚) : sâ‚ âˆª sâ‚‚ âˆˆ ğ“Ë¢ (tâ‚ âˆª tâ‚‚) :=
by { rw nhds_set_union, exact union_mem_sup hâ‚ hâ‚‚ }

/-- Preimage of a set neighborhood of `t` under a continuous map `f` is a set neighborhood of `s`
provided that `f` maps `s` to `t`.  -/
lemma continuous.tendsto_nhds_set {f : Î± â†’ Î²} {t : set Î²} (hf : continuous f)
  (hst : maps_to f s t) : tendsto f (ğ“Ë¢ s) (ğ“Ë¢ t) :=
((has_basis_nhds_set s).tendsto_iff (has_basis_nhds_set t)).mpr $ Î» U hU,
  âŸ¨f â»Â¹' U, âŸ¨hU.1.preimage hf, hst.mono subset.rfl hU.2âŸ©, Î» x, idâŸ©
