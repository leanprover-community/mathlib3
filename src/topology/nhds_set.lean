/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Patrick Massot
-/
import topology.basic
/-!
# Neighborhoods of a set

In this file we define the filter `ğ“Ë¢ s` or `nhds_set s` consisting of all neighborhoods of a set
`s`.

## Main Properties

There are a couple different notions equivalent to `s âˆˆ ğ“Ë¢ t`:
* `s âŠ† interior t` using `subset_interior_iff_mem_nhds_set`
* `âˆ€ (x : Î±), x âˆˆ t â†’ s âˆˆ ğ“ x` using `mem_nhds_set_iff_forall`
* `âˆƒ U : set Î±, is_open U âˆ§ t âŠ† U âˆ§ U âŠ† s` using `mem_nhds_set_iff_exists`

Furthermore, we have the following results:
* `monotone_nhds_set`: `ğ“Ë¢` is monotone
* In Tâ‚-spaces, `ğ“Ë¢`is strictly monotone and hence injective:
  `strict_mono_nhds_set`/`injective_nhds_set`. These results are in `topology.separation`.
-/

open set filter
open_locale topological_space

variables {Î± Î² : Type*} [topological_space Î±] [topological_space Î²]
  {s t sâ‚ sâ‚‚ tâ‚ tâ‚‚ : set Î±} {x : Î±}

/-- The filter of neighborhoods of a set in a topological space. -/
def nhds_set (s : set Î±) : filter Î± :=
Sup (nhds '' s)

localized "notation `ğ“Ë¢` := nhds_set" in topological_space

lemma mem_nhds_set_iff_forall : s âˆˆ ğ“Ë¢ t â†” âˆ€ (x : Î±), x âˆˆ t â†’ s âˆˆ ğ“ x :=
by simp_rw [nhds_set, filter.mem_Sup, ball_image_iff]

lemma subset_interior_iff_mem_nhds_set : s âŠ† interior t â†” t âˆˆ ğ“Ë¢ s :=
by simp_rw [mem_nhds_set_iff_forall, subset_interior_iff_nhds]

lemma mem_nhds_set_iff_exists : s âˆˆ ğ“Ë¢ t â†” âˆƒ U : set Î±, is_open U âˆ§ t âŠ† U âˆ§ U âŠ† s :=
by { rw [â† subset_interior_iff_mem_nhds_set, subset_interior_iff] }

lemma has_basis_nhds_set (s : set Î±) : (ğ“Ë¢ s).has_basis (Î» U, is_open U âˆ§ s âŠ† U) (Î» U, U) :=
âŸ¨Î» t, by simp [mem_nhds_set_iff_exists, and_assoc]âŸ©

lemma is_open.mem_nhds_set (hU : is_open s) : s âˆˆ ğ“Ë¢ t â†” t âŠ† s :=
by rw [â† subset_interior_iff_mem_nhds_set, interior_eq_iff_open.mpr hU]

@[simp] lemma nhds_set_singleton : ğ“Ë¢ {x} = ğ“ x :=
by { ext,
     rw [â† subset_interior_iff_mem_nhds_set, â† mem_interior_iff_mem_nhds, singleton_subset_iff] }

lemma mem_nhds_set_interior : s âˆˆ ğ“Ë¢ (interior s) :=
subset_interior_iff_mem_nhds_set.mp subset.rfl

lemma mem_nhds_set_empty : s âˆˆ ğ“Ë¢ (âˆ… : set Î±) :=
subset_interior_iff_mem_nhds_set.mp $ empty_subset _

@[simp] lemma nhds_set_empty : ğ“Ë¢ (âˆ… : set Î±) = âŠ¥ :=
by { ext, simp [mem_nhds_set_empty] }

@[simp] lemma nhds_set_univ : ğ“Ë¢ (univ : set Î±) = âŠ¤ :=
by { ext, rw [â† subset_interior_iff_mem_nhds_set, univ_subset_iff, interior_eq_univ, mem_top] }

lemma monotone_nhds_set : monotone (ğ“Ë¢ : set Î± â†’ filter Î±) :=
Î» s t hst, Sup_le_Sup $ image_subset _ hst

@[simp] lemma nhds_set_union (s t : set Î±) : ğ“Ë¢ (s âˆª t) = ğ“Ë¢ s âŠ” ğ“Ë¢ t :=
by simp only [nhds_set, image_union, Sup_union]

lemma union_mem_nhds_set (hâ‚ : sâ‚ âˆˆ ğ“Ë¢ tâ‚) (hâ‚‚ : sâ‚‚ âˆˆ ğ“Ë¢ tâ‚‚) : sâ‚ âˆª sâ‚‚ âˆˆ ğ“Ë¢ (tâ‚ âˆª tâ‚‚) :=
by { rw nhds_set_union, exact union_mem_sup hâ‚ hâ‚‚ }

/-- Preimage of a set neighborhood of `t` under a continuous map `f` is a set neighborhood of `s`
provided that `f` maps `s` to `t`.  -/
lemma continuous.tendsto_nhds_set {f : Î± â†’ Î²} {t : set Î²} (hf : continuous f)
  (hst : maps_to f s t) : tendsto f (ğ“Ë¢ s) (ğ“Ë¢ t) :=
((has_basis_nhds_set s).tendsto_iff (has_basis_nhds_set t)).mpr $ Î» U hU,
  âŸ¨f â»Â¹' U, âŸ¨hU.1.preimage hf, hst.mono subset.rfl hU.2âŸ©, Î» x, idâŸ©
