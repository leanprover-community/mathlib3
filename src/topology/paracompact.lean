/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Yury Kudryashov
-/
import topology.subset_properties
import topology.separation
import data.option.basic

/-!
# Paracompact topological spaces

A topological space `X` is said to be paracompact if every open covering of `X` admits a locally
finite refinement.

The definition requires that each set of the new covering is a subset of one of the sets of the
initial covering. However, one can ensure that each open covering `s : Œπ ‚Üí set X` admits a *precise*
locally finite refinement, i.e., an open covering `t : Œπ ‚Üí set X` with the same index set such that
`‚àÄ i, t i ‚äÜ s i`, see lemma `precise_refinement`. We also provide a convenience lemma
`precise_refinement_set` that deals with open coverings of a closed subset of `X` instead of the
whole space.

We also prove the following facts.

* Every compact space is paracompact, see instance `paracompact_of_compact`.

* A locally compact sigma compact Hausdorff space is paracompact, see instance
  `paracompact_of_locally_compact_sigma_compact`. Moreover, we can choose a locally finite
  refinement with sets in a given collection of filter bases of `ùìù x, `x : X`, see
  `refinement_of_locally_compact_sigma_compact_of_nhds_basis`. For example, in a proper metric space
  every open covering `‚ãÉ i, s i` admits a refinement `‚ãÉ i, metric.ball (c i) (r i)`.

* Every paracompact Hausdorff space is normal. This statement is not an instance to avoid loops in
  the instance graph.

* Every `emetric_space` is a paracompact space, see instance `emetric_space.paracompact_space` in
  `topology/metric_space/emetric_space`.

## TODO

Prove (some of) [Michael's theorems](https://ncatlab.org/nlab/show/Michael%27s+theorem).

## Tags

compact space, paracompact space, locally finite covering
-/

open set filter function
open_locale filter topological_space

universes u v

/-- A topological space is called paracompact, if every open covering of this space admits a locally
finite refinement. We use the same universe for all types in the definition to avoid creating a
class like `paracompact_space.{u v}`. Due to lemma `precise_refinement` below, every open covering
`s : Œ± ‚Üí set X` indexed on `Œ± : Type v` has a *precise* locally finite refinement, i.e., a locally
finite refinement `t : Œ± ‚Üí set X` indexed on the same type such that each `‚àÄ i, t i ‚äÜ s i`. -/
class paracompact_space (X : Type v) [topological_space X] : Prop :=
(locally_finite_refinement :
  ‚àÄ (Œ± : Type v) (s : Œ± ‚Üí set X) (ho : ‚àÄ a, is_open (s a)) (hc : (‚ãÉ a, s a) = univ),
  ‚àÉ (Œ≤ : Type v) (t : Œ≤ ‚Üí set X) (ho : ‚àÄ b, is_open (t b)) (hc : (‚ãÉ b, t b) = univ),
    locally_finite t ‚àß ‚àÄ b, ‚àÉ a, t b ‚äÜ s a)

variables {Œπ : Type u} {X : Type v} [topological_space X]

/-- Any open cover of a paracompact space has a locally finite *precise* refinement, that is,
one indexed on the same type with each open set contained in the corresponding original one. -/
lemma precise_refinement [paracompact_space X] (u : Œπ ‚Üí set X) (uo : ‚àÄ a, is_open (u a))
  (uc : (‚ãÉ i, u i) = univ) :
  ‚àÉ v : Œπ ‚Üí set X, (‚àÄ a, is_open (v a)) ‚àß (‚ãÉ i, v i) = univ ‚àß locally_finite v ‚àß (‚àÄ a, v a ‚äÜ u a) :=
begin
  -- Apply definition to `range u`, then turn existence quantifiers into functions using `choose`
  have := paracompact_space.locally_finite_refinement (range u) coe
    (set_coe.forall.2 $ forall_range_iff.2 uo) (by rwa [‚Üê sUnion_range, subtype.range_coe]),
  simp only [set_coe.exists, subtype.coe_mk, exists_range_iff', Union_eq_univ_iff,
    exists_prop] at this,
  choose Œ± t hto hXt htf ind hind, choose t_inv ht_inv using hXt, choose U hxU hU using htf,
  -- Send each `i` to the union of `t a` over `a ‚àà ind ‚Åª¬π' {i}`
  refine ‚ü®Œª i, ‚ãÉ (a : Œ±) (ha : ind a = i), t a, _, _, _, _‚ü©,
  { exact Œª a, is_open_Union (Œª a, is_open_Union $ Œª ha, hto a) },
  { simp only [eq_univ_iff_forall, mem_Union],
    exact Œª x, ‚ü®ind (t_inv x), _, rfl, ht_inv _‚ü© },
  { refine Œª x, ‚ü®U x, hxU x, ((hU x).image ind).subset _‚ü©,
    simp only [subset_def, mem_Union, mem_set_of_eq, set.nonempty, mem_inter_eq],
    rintro i ‚ü®y, ‚ü®a, rfl, hya‚ü©, hyU‚ü©,
    exact mem_image_of_mem _ ‚ü®y, hya, hyU‚ü© },
  { simp only [subset_def, mem_Union],
    rintro i x ‚ü®a, rfl, hxa‚ü©,
    exact hind _ hxa }
end

/-- In a paracompact space, every open covering of a closed set admits a locally finite refinement
indexed by the same type. -/
lemma precise_refinement_set [paracompact_space X] {s : set X} (hs : is_closed s)
  (u : Œπ ‚Üí set X) (uo : ‚àÄ i, is_open (u i)) (us : s ‚äÜ ‚ãÉ i, u i) :
  ‚àÉ v : Œπ ‚Üí set X, (‚àÄ i, is_open (v i)) ‚àß (s ‚äÜ ‚ãÉ i, v i) ‚àß locally_finite v ‚àß (‚àÄ i, v i ‚äÜ u i) :=
begin
  rcases precise_refinement (Œª i, option.elim i s·∂ú u)
    (option.forall.2 ‚ü®is_open_compl_iff.2 hs, uo‚ü©) _ with ‚ü®v, vo, vc, vf, vu‚ü©,
  refine ‚ü®v ‚àò some, Œª i, vo _, _, vf.comp_injective (option.some_injective _), Œª i, vu _‚ü©,
  { simp only [Union_option, ‚Üê compl_subset_iff_union] at vc,
    exact subset.trans (subset_compl_comm.1 $ vu option.none) vc },
  { simpa only [Union_option, option.elim, ‚Üê compl_subset_iff_union, compl_compl] }
end

/-- A compact space is paracompact. -/
@[priority 100] -- See note [lower instance priority]
instance paracompact_of_compact [compact_space X] : paracompact_space X :=
begin
  -- the proof is trivial: we choose a finite subcover using compactness, and use it
  refine ‚ü®Œª Œπ s ho hu, _‚ü©,
  rcases compact_univ.elim_finite_subcover _ ho hu.ge with ‚ü®T, hT‚ü©,
  have := hT, simp only [subset_def, mem_Union] at this,
  choose i hiT hi using Œª x, this x (mem_univ x),
  refine ‚ü®(T : set Œπ), Œª t, s t, Œª t, ho _, _, locally_finite_of_fintype _, Œª t, ‚ü®t, subset.rfl‚ü©‚ü©,
  rwa [Union_coe_set, finset.set_bUnion_coe, ‚Üê univ_subset_iff],
end

/-- Let `X` be a locally compact sigma compact Hausdorff topological space, let `s` be a closed set
in `X`. Suppose that for each `x ‚àà s` the sets `B x : Œπ x ‚Üí set X` with the predicate
`p x : Œπ x ‚Üí Prop` form a basis of the filter `ùìù x`. Then there exists a locally finite covering
`Œª i, B (c i) (r i)` of `s` such that all ‚Äúcenters‚Äù `c i` belong to `s` and each `r i` satisfies
`p (c i)`.

The notation is inspired by the case `B x r = metric.ball x r` but the theorem applies to
`nhds_basis_opens` as well. If the covering must be subordinate to some open covering of `s`, then
the user should use a basis obtained by `filter.has_basis.restrict_subset` or a similar lemma, see
the proof of `paracompact_of_locally_compact_sigma_compact` for an example.

The formalization is based on two [ncatlab](https://ncatlab.org/) proofs:
* [locally compact and sigma compact spaces are paracompact](https://ncatlab.org/nlab/show/locally+compact+and+sigma-compact+spaces+are+paracompact);
* [open cover of smooth manifold admits locally finite refinement by closed balls](https://ncatlab.org/nlab/show/partition+of+unity#ExistenceOnSmoothManifolds).

See also `refinement_of_locally_compact_sigma_compact_of_nhds_basis` for a version of this lemma
dealing with a covering of the whole space.

In most cases (namely, if `B c r ‚à™ B c r'` is again a set of the form `B c r''`) it is possible
to choose `Œ± = X`. This fact is not yet formalized in `mathlib`. -/
theorem refinement_of_locally_compact_sigma_compact_of_nhds_basis_set
  [locally_compact_space X] [sigma_compact_space X] [t2_space X]
  {Œπ : X ‚Üí Type u} {p : Œ† x, Œπ x ‚Üí Prop} {B : Œ† x, Œπ x ‚Üí set X} {s : set X}
  (hs : is_closed s) (hB : ‚àÄ x ‚àà s, (ùìù x).has_basis (p x) (B x)) :
  ‚àÉ (Œ± : Type v) (c : Œ± ‚Üí X) (r : Œ† a, Œπ (c a)), (‚àÄ a, c a ‚àà s ‚àß p (c a) (r a)) ‚àß
    (s ‚äÜ ‚ãÉ a, B (c a) (r a)) ‚àß locally_finite (Œª a, B (c a) (r a)) :=
begin
  classical,
  -- For technical reasons we prepend two empty sets to the sequence `compact_exhaustion.choice X`
  set K' : compact_exhaustion X := compact_exhaustion.choice X,
  set K : compact_exhaustion X := K'.shiftr.shiftr,
  set Kdiff := Œª n, K (n + 1) \ interior (K n),
  -- Now we restate some properties of `compact_exhaustion` for `K`/`Kdiff`
  have hKcov : ‚àÄ x, x ‚àà Kdiff (K'.find x + 1),
  { intro x,
    simpa only [K'.find_shiftr]
      using diff_subset_diff_right interior_subset (K'.shiftr.mem_diff_shiftr_find x) },
  have Kdiffc : ‚àÄ n, is_compact (Kdiff n ‚à© s),
    from Œª n, ((K.is_compact _).diff is_open_interior).inter_right hs,
  -- Next we choose a finite covering `B (c n i) (r n i)` of each
  -- `Kdiff (n + 1) ‚à© s` such that `B (c n i) (r n i) ‚à© s` is disjoint with `K n`
  have : ‚àÄ n (x : Kdiff (n + 1) ‚à© s), (K n)·∂ú ‚àà ùìù (x : X),
    from Œª n x, is_open.mem_nhds (K.is_closed n).is_open_compl
      (Œª hx', x.2.1.2 $ K.subset_interior_succ _ hx'),
  haveI : ‚àÄ n (x : Kdiff n ‚à© s), nonempty (Œπ x) := Œª n x, (hB x x.2.2).nonempty,
  choose! r hrp hr using (Œª n (x : Kdiff (n + 1) ‚à© s), (hB x x.2.2).mem_iff.1 (this n x)),
  have hxr : ‚àÄ n x (hx : x ‚àà Kdiff (n + 1) ‚à© s), B x (r n ‚ü®x, hx‚ü©) ‚àà ùìù x,
    from Œª n x hx, (hB x hx.2).mem_of_mem (hrp _ ‚ü®x, hx‚ü©),
  choose T hT using Œª n, (Kdiffc (n + 1)).elim_nhds_subcover' _ (hxr n),
  set T' : Œ† n, set ‚Ü•(Kdiff (n + 1) ‚à© s) := Œª n, T n,
  -- Finally, we take the union of all these coverings
  refine ‚ü®Œ£ n, T' n, Œª a, a.2, Œª a, r a.1 a.2, _, _, _‚ü©,
  { rintro ‚ü®n, x, hx‚ü©, exact ‚ü®x.2.2, hrp _ _‚ü© },
  { refine (Œª x hx, mem_Union.2 _),
    rcases mem_bUnion_iff.1 (hT _ ‚ü®hKcov x, hx‚ü©) with ‚ü®‚ü®c, hc‚ü©, hcT, hcx‚ü©,
    exact ‚ü®‚ü®_, ‚ü®c, hc‚ü©, hcT‚ü©, hcx‚ü© },
  { intro x,
    refine ‚ü®interior (K (K'.find x + 3)),
      is_open.mem_nhds is_open_interior (K.subset_interior_succ _ (hKcov x).1), _‚ü©,
    have : (‚ãÉ k ‚â§ K'.find x + 2, (range $ sigma.mk k) : set (Œ£ n, T' n)).finite,
      from (finite_le_nat _).bUnion (Œª k hk, finite_range _),
    apply this.subset, rintro ‚ü®k, c, hc‚ü©,
    simp only [mem_Union, mem_set_of_eq, mem_image_eq, subtype.coe_mk],
    rintro ‚ü®x, hxB : x ‚àà B c (r k c), hxK‚ü©,
    refine ‚ü®k, _, ‚ü®c, hc‚ü©, rfl‚ü©,
    have := (mem_compl_iff _ _).1 (hr k c hxB),
    contrapose! this with hnk,
    exact K.subset hnk (interior_subset hxK) },
end

/-- Let `X` be a locally compact sigma compact Hausdorff topological space. Suppose that for each
`x` the sets `B x : Œπ x ‚Üí set X` with the predicate `p x : Œπ x ‚Üí Prop` form a basis of the filter
`ùìù x`. Then there exists a locally finite covering `Œª i, B (c i) (r i)` of `X` such that each `r i`
satisfies `p (c i)`

The notation is inspired by the case `B x r = metric.ball x r` but the theorem applies to
`nhds_basis_opens` as well. If the covering must be subordinate to some open covering of `s`, then
the user should use a basis obtained by `filter.has_basis.restrict_subset` or a similar lemma, see
the proof of `paracompact_of_locally_compact_sigma_compact` for an example.

The formalization is based on two [ncatlab](https://ncatlab.org/) proofs:
* [locally compact and sigma compact spaces are paracompact](https://ncatlab.org/nlab/show/locally+compact+and+sigma-compact+spaces+are+paracompact);
* [open cover of smooth manifold admits locally finite refinement by closed balls](https://ncatlab.org/nlab/show/partition+of+unity#ExistenceOnSmoothManifolds).

See also `refinement_of_locally_compact_sigma_compact_of_nhds_basis_set` for a version of this lemma
dealing with a covering of a closed set.

In most cases (namely, if `B c r ‚à™ B c r'` is again a set of the form `B c r''`) it is possible
to choose `Œ± = X`. This fact is not yet formalized in `mathlib`. -/
theorem refinement_of_locally_compact_sigma_compact_of_nhds_basis
  [locally_compact_space X] [sigma_compact_space X] [t2_space X]
  {Œπ : X ‚Üí Type u} {p : Œ† x, Œπ x ‚Üí Prop} {B : Œ† x, Œπ x ‚Üí set X}
  (hB : ‚àÄ x, (ùìù x).has_basis (p x) (B x)) :
  ‚àÉ (Œ± : Type v) (c : Œ± ‚Üí X) (r : Œ† a, Œπ (c a)), (‚àÄ a, p (c a) (r a)) ‚àß
    (‚ãÉ a, B (c a) (r a)) = univ ‚àß locally_finite (Œª a, B (c a) (r a)) :=
let ‚ü®Œ±, c, r, hp, hU, hfin‚ü© := refinement_of_locally_compact_sigma_compact_of_nhds_basis_set
  is_closed_univ (Œª x _, hB x)
in ‚ü®Œ±, c, r, Œª a, (hp a).2, univ_subset_iff.1 hU, hfin‚ü©

/-- A locally compact sigma compact Hausdorff space is paracompact. See also
`refinement_of_locally_compact_sigma_compact_of_nhds_basis` for a more precise statement. -/
@[priority 100] -- See note [lower instance priority]
instance paracompact_of_locally_compact_sigma_compact [locally_compact_space X]
  [sigma_compact_space X] [t2_space X] : paracompact_space X :=
begin
  refine ‚ü®Œª Œ± s ho hc, _‚ü©,
  choose i hi using Union_eq_univ_iff.1 hc,
  have : ‚àÄ x : X, (ùìù x).has_basis (Œª t : set X, (x ‚àà t ‚àß is_open t) ‚àß t ‚äÜ s (i x)) id,
    from Œª x : X, (nhds_basis_opens x).restrict_subset (is_open.mem_nhds (ho (i x)) (hi x)),
  rcases refinement_of_locally_compact_sigma_compact_of_nhds_basis this
    with ‚ü®Œ≤, c, t, hto, htc, htf‚ü©,
  exact ‚ü®Œ≤, t, Œª x, (hto x).1.2, htc, htf, Œª b, ‚ü®i $ c b, (hto b).2‚ü©‚ü©
end

/- Dieudonn√©‚Äòs theorem: a paracompact Hausdorff space is normal. Formalization is based on the proof
at [ncatlab](https://ncatlab.org/nlab/show/paracompact+Hausdorff+spaces+are+normal). -/
lemma normal_of_paracompact_t2 [t2_space X] [paracompact_space X] : normal_space X :=
begin
  /- First we show how to go from points to a set on one side. -/
  have : ‚àÄ (s t : set X), is_closed s ‚Üí is_closed t ‚Üí
    (‚àÄ x ‚àà s, ‚àÉ u v, is_open u ‚àß is_open v ‚àß x ‚àà u ‚àß t ‚äÜ v ‚àß disjoint u v) ‚Üí
    ‚àÉ u v, is_open u ‚àß is_open v ‚àß s ‚äÜ u ‚àß t ‚äÜ v ‚àß disjoint u v,
  { /- For each `x ‚àà s` we choose open disjoint `u x ‚àã x` and `v x ‚äá t`. The sets `u x` form an
    open covering of `s`. We choose a locally finite refinement `u' : s ‚Üí set X`, then `‚ãÉ i, u' i`
    and `(closure (‚ãÉ i, u' i))·∂ú` are disjoint open neighborhoods of `s` and `t`. -/
    intros s t hs ht H, choose u v hu hv hxu htv huv using set_coe.forall'.1 H,
    rcases precise_refinement_set hs u hu (Œª x hx, mem_Union.2 ‚ü®‚ü®x, hx‚ü©, hxu _‚ü©)
      with ‚ü®u', hu'o, hcov', hu'fin, hsub‚ü©,
    refine ‚ü®‚ãÉ i, u' i, (closure (‚ãÉ i, u' i))·∂ú, is_open_Union hu'o, is_closed_closure.is_open_compl,
      hcov', _, disjoint_compl_right.mono le_rfl (compl_le_compl subset_closure)‚ü©,
    rw [hu'fin.closure_Union, compl_Union, subset_Inter_iff],
    refine Œª i x hxt hxu, absurd (htv i hxt) (closure_minimal _ (is_closed_compl_iff.2 $ hv _) hxu),
    exact Œª y hyu hyv, huv i ‚ü®hsub _ hyu, hyv‚ü© },
  /- Now we apply the lemma twice: first to `s` and `t`, then to `t` and each point of `s`. -/
  refine ‚ü®Œª s t hs ht hst, this s t hs ht (Œª x hx, _)‚ü©,
  rcases this t {x} ht is_closed_singleton (Œª y hyt, _) with ‚ü®v, u, hv, hu, htv, hxu, huv‚ü©,
  { exact ‚ü®u, v, hu, hv, singleton_subset_iff.1 hxu, htv, huv.symm‚ü© },
  { have : x ‚â† y, by { rintro rfl, exact hst ‚ü®hx, hyt‚ü© },
    rcases t2_separation this with ‚ü®v, u, hv, hu, hxv, hyu, hd‚ü©,
    exact ‚ü®u, v, hu, hv, hyu, singleton_subset_iff.2 hxv, disjoint.symm hd.le‚ü© }
end
