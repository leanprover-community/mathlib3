/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import topology.subset_properties
import topology.connected
import topology.nhds_set

/-!
# Separation properties of topological spaces.

This file defines the predicate `separated`, and common separation axioms
(under the Kolmogorov classification).

## Main definitions

* `separated`: Two `set`s are separated if they are contained in disjoint open sets.
* `t0_space`: A Tâ‚€/Kolmogorov space is a space where, for every two points `x â‰  y`,
  there is an open set that contains one, but not the other.
* `t1_space`: A Tâ‚/FrÃ©chet space is a space where every singleton set is closed.
  This is equivalent to, for every pair `x â‰  y`, there existing an open set containing `x`
  but not `y` (`t1_space_iff_exists_open` shows that these conditions are equivalent.)
* `t2_space`: A Tâ‚‚/Hausdorff space is a space where, for every two points `x â‰  y`,
  there is two disjoint open sets, one containing `x`, and the other `y`.
* `t2_5_space`: A Tâ‚‚.â‚…/Urysohn space is a space where, for every two points `x â‰  y`,
  there is two open sets, one containing `x`, and the other `y`, whose closures are disjoint.
* `regular_space`: A Tâ‚ƒ space (sometimes referred to as regular, but authors vary on
  whether this includes Tâ‚‚; `mathlib` does), is one where given any closed `C` and `x âˆ‰ C`,
  there is disjoint open sets containing `x` and `C` respectively. In `mathlib`, Tâ‚ƒ implies Tâ‚‚.â‚….
* `normal_space`: A Tâ‚„ space (sometimes referred to as normal, but authors vary on
  whether this includes Tâ‚‚; `mathlib` does), is one where given two disjoint closed sets,
  we can find two open sets that separate them. In `mathlib`, Tâ‚„ implies Tâ‚ƒ.

## Main results

### Tâ‚€ spaces

* `is_closed.exists_closed_singleton` Given a closed set `S` in a compact Tâ‚€ space,
  there is some `x âˆˆ S` such that `{x}` is closed.
* `exists_open_singleton_of_open_finset` Given an open `finset` `S` in a Tâ‚€ space,
  there is some `x âˆˆ S` such that `{x}` is open.

### Tâ‚ spaces

* `is_closed_map_const`: The constant map is a closed map.
* `discrete_of_t1_of_finite`: A finite Tâ‚ space must have the discrete topology.

### Tâ‚‚ spaces

* `t2_iff_nhds`: A space is Tâ‚‚ iff the neighbourhoods of distinct points generate the bottom filter.
* `t2_iff_is_closed_diagonal`: A space is Tâ‚‚ iff the `diagonal` of `Î±` (that is, the set of all
  points of the form `(a, a) : Î± Ã— Î±`) is closed under the product topology.
* `finset_disjoint_finset_opens_of_t2`: Any two disjoint finsets are `separated`.
* Most topological constructions preserve Hausdorffness;
  these results are part of the typeclass inference system (e.g. `embedding.t2_space`)
* `set.eq_on.closure`: If two functions are equal on some set `s`, they are equal on its closure.
* `is_compact.is_closed`: All compact sets are closed.
* `locally_compact_of_compact_nhds`: If every point has a compact neighbourhood,
  then the space is locally compact.
* `tot_sep_of_zero_dim`: If `Î±` has a clopen basis, it is a `totally_separated_space`.
* `loc_compact_t2_tot_disc_iff_tot_sep`: A locally compact Tâ‚‚ space is totally disconnected iff
  it is totally separated.

If the space is also compact:

* `normal_of_compact_t2`: A compact Tâ‚‚ space is a `normal_space`.
* `connected_components_eq_Inter_clopen`: The connected component of a point
  is the intersection of all its clopen neighbourhoods.
* `compact_t2_tot_disc_iff_tot_sep`: Being a `totally_disconnected_space`
  is equivalent to being a `totally_separated_space`.
* `connected_components.t2`: `connected_components Î±` is Tâ‚‚ for `Î±` Tâ‚‚ and compact.

### Tâ‚ƒ spaces

* `disjoint_nested_nhds`: Given two points `x â‰  y`, we can find neighbourhoods `x âˆˆ Vâ‚ âŠ† Uâ‚` and
  `y âˆˆ Vâ‚‚ âŠ† Uâ‚‚`, with the `Vâ‚–` closed and the `Uâ‚–` open, such that the `Uâ‚–` are disjoint.

### Discrete spaces

* `discrete_topology_iff_nhds`: Discrete topological spaces are those whose neighbourhood
  filters are the `pure` filter (which is the principal filter at a singleton).
* `induced_bot`/`discrete_topology_induced`: The pullback of the discrete topology
  under an inclusion is the discrete topology.

## References

https://en.wikipedia.org/wiki/Separation_axiom
-/

open set filter topological_space
open_locale topological_space filter classical

universes u v
variables {Î± : Type u} {Î² : Type v} [topological_space Î±]

section separation

/--
`separated` is a predicate on pairs of sub`set`s of a topological space.  It holds if the two
sub`set`s are contained in disjoint open sets.
-/
def separated : set Î± â†’ set Î± â†’ Prop :=
  Î» (s t : set Î±), âˆƒ U V : (set Î±), (is_open U) âˆ§ is_open V âˆ§
  (s âŠ† U) âˆ§ (t âŠ† V) âˆ§ disjoint U V

namespace separated

open separated

@[symm] lemma symm {s t : set Î±} : separated s t â†’ separated t s :=
Î» âŸ¨U, V, oU, oV, aU, bV, UVâŸ©, âŸ¨V, U, oV, oU, bV, aU, disjoint.symm UVâŸ©

lemma comm (s t : set Î±) : separated s t â†” separated t s :=
âŸ¨symm, symmâŸ©

lemma preimage [topological_space Î²] {f : Î± â†’ Î²} {s t : set Î²} (h : separated s t)
  (hf : continuous f) : separated (f â»Â¹' s) (f â»Â¹' t) :=
let âŸ¨U, V, oU, oV, sU, tV, UVâŸ© := h in
âŸ¨f â»Â¹' U, f â»Â¹' V, oU.preimage hf, oV.preimage hf, preimage_mono sU, preimage_mono tV,
  UV.preimage fâŸ©

protected lemma disjoint {s t : set Î±} (h : separated s t) : disjoint s t :=
let âŸ¨U, V, hU, hV, hsU, htV, hdâŸ© := h in hd.mono hsU htV

lemma disjoint_closure_left {s t : set Î±} (h : separated s t) : disjoint (closure s) t :=
let âŸ¨U, V, hU, hV, hsU, htV, hdâŸ© := h
in (hd.closure_left hV).mono (closure_mono hsU) htV

lemma disjoint_closure_right {s t : set Î±} (h : separated s t) : disjoint s (closure t) :=
h.symm.disjoint_closure_left.symm

lemma empty_right (a : set Î±) : separated a âˆ… :=
âŸ¨_, _, is_open_univ, is_open_empty, Î» a h, mem_univ a, Î» a h, by cases h, disjoint_empty _âŸ©

lemma empty_left (a : set Î±) : separated âˆ… a :=
(empty_right _).symm

lemma mono {sâ‚ sâ‚‚ tâ‚ tâ‚‚ : set Î±} (h : separated sâ‚‚ tâ‚‚) (hs : sâ‚ âŠ† sâ‚‚) (ht : tâ‚ âŠ† tâ‚‚) :
  separated sâ‚ tâ‚ :=
let âŸ¨U, V, hU, hV, hsU, htV, hdâŸ© := h in âŸ¨U, V, hU, hV, hs.trans hsU, ht.trans htV, hdâŸ©

lemma union_left {a b c : set Î±} : separated a c â†’ separated b c â†’ separated (a âˆª b) c :=
Î» âŸ¨U, V, oU, oV, aU, bV, UVâŸ© âŸ¨W, X, oW, oX, aW, bX, WXâŸ©,
  âŸ¨U âˆª W, V âˆ© X, is_open.union oU oW, is_open.inter oV oX,
    union_subset_union aU aW, subset_inter bV bX, set.disjoint_union_left.mpr
    âŸ¨disjoint_of_subset_right (inter_subset_left _ _) UV,
      disjoint_of_subset_right (inter_subset_right _ _) WXâŸ©âŸ©

lemma union_right {a b c : set Î±} (ab : separated a b) (ac : separated a c) :
  separated a (b âˆª c) :=
(ab.symm.union_left ac.symm).symm

end separated

/-- A Tâ‚€ space, also known as a Kolmogorov space, is a topological space
  where for every pair `x â‰  y`, there is an open set containing one but not the other. -/
class t0_space (Î± : Type u) [topological_space Î±] : Prop :=
(t0 : âˆ€ x y, x â‰  y â†’ âˆƒ U:set Î±, is_open U âˆ§ (xor (x âˆˆ U) (y âˆˆ U)))

lemma t0_space_def (Î± : Type u) [topological_space Î±] :
  t0_space Î± â†” âˆ€ x y, x â‰  y â†’ âˆƒ U:set Î±, is_open U âˆ§ (xor (x âˆˆ U) (y âˆˆ U)) :=
by { split, apply @t0_space.t0, apply t0_space.mk }

/-- Two points are topologically indistinguishable if no open set separates them. -/
def indistinguishable {Î± : Type u} [topological_space Î±] (x y : Î±) : Prop :=
âˆ€ (U : set Î±) (hU : is_open U), x âˆˆ U â†” y âˆˆ U

lemma indistinguishable_iff_nhds_eq {x y : Î±} : indistinguishable x y â†” ğ“ x = ğ“ y :=
âŸ¨Î» h, by simp only [nhds_def', h _] { contextual := tt },
  Î» h U hU, by simp only [â† hU.mem_nhds_iff, h]âŸ©

alias indistinguishable_iff_nhds_eq â†” indistinguishable.nhds_eq _

lemma t0_space_iff_distinguishable (Î± : Type u) [topological_space Î±] :
  t0_space Î± â†” âˆ€ (x y : Î±), x â‰  y â†’ Â¬ indistinguishable x y :=
begin
  delta indistinguishable,
  rw t0_space_def,
  push_neg,
  simp_rw xor_iff_not_iff,
end

@[simp] lemma nhds_eq_nhds_iff [t0_space Î±] {a b : Î±} : ğ“ a = ğ“ b â†” a = b :=
function.injective.eq_iff $ Î» x y h, of_not_not $
  Î» hne, (t0_space_iff_distinguishable Î±).mp â€¹_â€º x y hne (indistinguishable_iff_nhds_eq.mpr h)

lemma indistinguishable.eq [t0_space Î±] {x y : Î±} (h : indistinguishable x y) : x = y :=
nhds_eq_nhds_iff.mp h.nhds_eq

lemma indistinguishable_iff_closed {x y : Î±} :
  indistinguishable x y â†” âˆ€ (U : set Î±) (hU : is_closed U), x âˆˆ U â†” y âˆˆ U :=
âŸ¨Î» h U hU, not_iff_not.mp (h _ hU.1), Î» h U hU, not_iff_not.mp (h _ (is_closed_compl_iff.mpr hU))âŸ©

lemma indistinguishable_iff_closure (x y : Î±) :
  indistinguishable x y â†” x âˆˆ closure ({y} : set Î±) âˆ§ y âˆˆ closure ({x} : set Î±) :=
begin
  rw indistinguishable_iff_closed,
  exact âŸ¨Î» h, âŸ¨(h _ is_closed_closure).mpr (subset_closure $ set.mem_singleton y),
      (h _ is_closed_closure).mp (subset_closure $ set.mem_singleton x)âŸ©,
    Î» h U hU, âŸ¨Î» hx, (is_closed.closure_subset_iff hU).mpr (set.singleton_subset_iff.mpr hx) h.2,
      Î» hy, (is_closed.closure_subset_iff hU).mpr (set.singleton_subset_iff.mpr hy) h.1âŸ©âŸ©
end

lemma subtype_indistinguishable_iff {Î± : Type u} [topological_space Î±] {U : set Î±} (x y : U) :
  indistinguishable x y â†” indistinguishable (x : Î±) y :=
by { simp_rw [indistinguishable_iff_closure, closure_subtype, image_singleton] }

/-- Given a closed set `S` in a compact Tâ‚€ space,
there is some `x âˆˆ S` such that `{x}` is closed. -/
theorem is_closed.exists_closed_singleton {Î± : Type*} [topological_space Î±]
  [t0_space Î±] [compact_space Î±] {S : set Î±} (hS : is_closed S) (hne : S.nonempty) :
  âˆƒ (x : Î±), x âˆˆ S âˆ§ is_closed ({x} : set Î±) :=
begin
  obtain âŸ¨V, Vsub, Vne, Vcls, hVâŸ© := hS.exists_minimal_nonempty_closed_subset hne,
  by_cases hnt : âˆƒ (x y : Î±) (hx : x âˆˆ V) (hy : y âˆˆ V), x â‰  y,
  { exfalso,
    obtain âŸ¨x, y, hx, hy, hneâŸ© := hnt,
    obtain âŸ¨U, hU, hsepâŸ© := t0_space.t0 _ _ hne,
    have : âˆ€ (z w : Î±) (hz : z âˆˆ V) (hw : w âˆˆ V) (hz' : z âˆˆ U) (hw' : Â¬ w âˆˆ U), false,
    { intros z w hz hw hz' hw',
      have uvne : (V âˆ© Uá¶œ).nonempty,
      { use w, simp only [hw, hw', set.mem_inter_eq, not_false_iff, and_self, set.mem_compl_eq], },
      specialize hV (V âˆ© Uá¶œ) (set.inter_subset_left _ _) uvne
        (is_closed.inter Vcls (is_closed_compl_iff.mpr hU)),
      have : V âŠ† Uá¶œ,
      { rw â†hV, exact set.inter_subset_right _ _ },
      exact this hz hz', },
    cases hsep,
    { exact this x y hx hy hsep.1 hsep.2 },
    { exact this y x hy hx hsep.1 hsep.2 } },
  { push_neg at hnt,
    obtain âŸ¨z, hzâŸ© := Vne,
    refine âŸ¨z, Vsub hz, _âŸ©,
    convert Vcls,
    ext,
    simp only [set.mem_singleton_iff, set.mem_compl_eq],
    split,
    { rintro rfl, exact hz, },
    { exact Î» hx, hnt x z hx hz, }, },
end

/-- Given an open `finset` `S` in a Tâ‚€ space, there is some `x âˆˆ S` such that `{x}` is open. -/
theorem exists_open_singleton_of_open_finset [t0_space Î±] (s : finset Î±) (sne : s.nonempty)
  (hso : is_open (s : set Î±)) :
  âˆƒ x âˆˆ s, is_open ({x} : set Î±):=
begin
  induction s using finset.strong_induction_on with s ihs,
  by_cases hs : set.subsingleton (s : set Î±),
  { rcases sne with âŸ¨x, hxâŸ©,
    refine âŸ¨x, hx, _âŸ©,
    have : (s : set Î±) = {x}, from hs.eq_singleton_of_mem hx,
    rwa this at hso },
  { dunfold set.subsingleton at hs,
    push_neg at hs,
    rcases hs with âŸ¨x, hx, y, hy, hxyâŸ©,
    rcases t0_space.t0 x y hxy with âŸ¨U, hU, hxyUâŸ©,
    wlog H : x âˆˆ U âˆ§ y âˆ‰ U := hxyU using [x y, y x],
    obtain âŸ¨z, hzs, hzâŸ© : âˆƒ z âˆˆ s.filter (Î» z, z âˆˆ U), is_open ({z} : set Î±),
    { refine ihs _ (finset.filter_ssubset.2 âŸ¨y, hy, H.2âŸ©) âŸ¨x, finset.mem_filter.2 âŸ¨hx, H.1âŸ©âŸ© _,
      rw [finset.coe_filter],
      exact is_open.inter hso hU },
    exact âŸ¨z, (finset.mem_filter.1 hzs).1, hzâŸ© }
end

theorem exists_open_singleton_of_fintype [t0_space Î±] [f : fintype Î±] [ha : nonempty Î±] :
  âˆƒ x:Î±, is_open ({x}:set Î±) :=
begin
  refine ha.elim (Î» x, _),
  have : is_open ((finset.univ : finset Î±) : set Î±), { simp },
  rcases exists_open_singleton_of_open_finset _ âŸ¨x, finset.mem_univ xâŸ© this with âŸ¨x, _, hxâŸ©,
  exact âŸ¨x, hxâŸ©
end

lemma t0_space_of_injective_of_continuous [topological_space Î²] {f : Î± â†’ Î²}
  (hf : function.injective f) (hf' : continuous f) [t0_space Î²] : t0_space Î± :=
âŸ¨Î» x y hxy, let âŸ¨U, hU, hxyUâŸ© := t0_space.t0 (f x) (f y) (hf.ne hxy) in
  âŸ¨f â»Â¹' U, hU.preimage hf', hxyUâŸ©âŸ©

protected lemma embedding.t0_space [topological_space Î²] [t0_space Î²] {f : Î± â†’ Î²}
  (hf : embedding f) : t0_space Î± :=
t0_space_of_injective_of_continuous hf.inj hf.continuous

instance subtype.t0_space [t0_space Î±] {p : Î± â†’ Prop} : t0_space (subtype p) :=
embedding_subtype_coe.t0_space

theorem t0_space_iff_or_not_mem_closure (Î± : Type u) [topological_space Î±] :
  t0_space Î± â†” (âˆ€ a b : Î±, (a â‰  b) â†’ (a âˆ‰ closure ({b} : set Î±) âˆ¨ b âˆ‰ closure ({a} : set Î±))) :=
begin
  simp only [â† not_and_distrib, t0_space_def, not_and],
  refine forallâ‚ƒ_congr (Î» a b _, âŸ¨_, Î» h, _âŸ©),
  { rintro âŸ¨s, hâ‚, (âŸ¨hâ‚‚, hâ‚ƒ : b âˆˆ sá¶œâŸ©|âŸ¨hâ‚‚, hâ‚ƒ : a âˆˆ sá¶œâŸ©)âŸ© ha hb; rw â† is_closed_compl_iff at hâ‚,
    { exact (is_closed.closure_subset_iff hâ‚).mpr (set.singleton_subset_iff.mpr hâ‚ƒ) ha hâ‚‚ },
    { exact (is_closed.closure_subset_iff hâ‚).mpr (set.singleton_subset_iff.mpr hâ‚ƒ) hb hâ‚‚ } },
  { by_cases h' : a âˆˆ closure ({b} : set Î±),
    { exact âŸ¨(closure {a})á¶œ, is_closed_closure.1,
        or.inr âŸ¨h h', not_not.mpr (subset_closure (set.mem_singleton a))âŸ©âŸ© },
    { exact âŸ¨(closure {b})á¶œ, is_closed_closure.1,
        or.inl âŸ¨h', not_not.mpr (subset_closure (set.mem_singleton b))âŸ©âŸ© } }
end

/-- A Tâ‚ space, also known as a FrÃ©chet space, is a topological space
  where every singleton set is closed. Equivalently, for every pair
  `x â‰  y`, there is an open set containing `x` and not `y`. -/
class t1_space (Î± : Type u) [topological_space Î±] : Prop :=
(t1 : âˆ€x, is_closed ({x} : set Î±))

lemma is_closed_singleton [t1_space Î±] {x : Î±} : is_closed ({x} : set Î±) :=
t1_space.t1 x

lemma is_open_compl_singleton [t1_space Î±] {x : Î±} : is_open ({x}á¶œ : set Î±) :=
is_closed_singleton.is_open_compl

lemma is_open_ne [t1_space Î±] {x : Î±} : is_open {y | y â‰  x} :=
is_open_compl_singleton

lemma ne.nhds_within_compl_singleton [t1_space Î±] {x y : Î±} (h : x â‰  y) :
  ğ“[{y}á¶œ] x = ğ“ x :=
is_open_ne.nhds_within_eq h

lemma ne.nhds_within_diff_singleton [t1_space Î±] {x y : Î±} (h : x â‰  y) (s : set Î±) :
  ğ“[s \ {y}] x = ğ“[s] x :=
begin
  rw [diff_eq, inter_comm, nhds_within_inter_of_mem],
  exact mem_nhds_within_of_mem_nhds (is_open_ne.mem_nhds h)
end

protected lemma set.finite.is_closed [t1_space Î±] {s : set Î±} (hs : set.finite s) :
  is_closed s :=
begin
  rw â† bUnion_of_singleton s,
  exact is_closed_bUnion hs (Î» i hi, is_closed_singleton)
end

lemma filter.coclosed_compact_le_cofinite [t1_space Î±] :
  filter.coclosed_compact Î± â‰¤ filter.cofinite :=
Î» s hs, compl_compl s â–¸ hs.is_compact.compl_mem_coclosed_compact_of_is_closed hs.is_closed

variable (Î±)

/-- In a `t1_space`, relatively compact sets form a bornology. Its cobounded filter is
`filter.coclosed_compact`. See also `bornology.in_compact` the bornology of sets contained
in a compact set. -/
def bornology.relatively_compact [t1_space Î±] : bornology Î± :=
{ cobounded := filter.coclosed_compact Î±,
  le_cofinite := filter.coclosed_compact_le_cofinite }

variable {Î±}

lemma bornology.relatively_compact.is_bounded_iff [t1_space Î±] {s : set Î±} :
  @bornology.is_bounded _ (bornology.relatively_compact Î±) s â†” is_compact (closure s) :=
begin
  change sá¶œ âˆˆ filter.coclosed_compact Î± â†” _,
  rw filter.mem_coclosed_compact,
  split,
  { rintros âŸ¨t, htâ‚, htâ‚‚, hstâŸ©,
    rw compl_subset_compl at hst,
    exact compact_of_is_closed_subset htâ‚‚ is_closed_closure (closure_minimal hst htâ‚) },
  { intros h,
    exact âŸ¨closure s, is_closed_closure, h, compl_subset_compl.mpr subset_closureâŸ© }
end

protected lemma finset.is_closed [t1_space Î±] (s : finset Î±) : is_closed (s : set Î±) :=
s.finite_to_set.is_closed

lemma t1_space_tfae (Î± : Type u) [topological_space Î±] :
  tfae [t1_space Î±,
    âˆ€ x, is_closed ({x} : set Î±),
    âˆ€ x, is_open ({x}á¶œ : set Î±),
    continuous (@cofinite_topology.of Î±),
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ {y}á¶œ âˆˆ ğ“ x,
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ âˆƒ s âˆˆ ğ“ x, y âˆ‰ s,
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ âˆƒ (U : set Î±) (hU : is_open U), x âˆˆ U âˆ§ y âˆ‰ U,
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ disjoint (ğ“ x) (pure y),
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ disjoint (pure x) (ğ“ y)] :=
begin
  tfae_have : 1 â†” 2, from âŸ¨Î» h, h.1, Î» h, âŸ¨hâŸ©âŸ©,
  tfae_have : 2 â†” 3, by simp only [is_open_compl_iff],
  tfae_have : 5 â†” 3,
  { refine forall_swap.trans _,
    simp only [is_open_iff_mem_nhds, mem_compl_iff, mem_singleton_iff] },
  tfae_have : 5 â†” 6,
    by simp only [â† subset_compl_singleton_iff, exists_mem_subset_iff],
  tfae_have : 5 â†” 7,
    by simp only [(nhds_basis_opens _).mem_iff, subset_compl_singleton_iff, exists_prop, and.assoc,
      and.left_comm],
  tfae_have : 5 â†” 8,
    by simp only [â† principal_singleton, disjoint_principal_right],
  tfae_have : 8 â†” 9, from forall_swap.trans (by simp only [disjoint.comm, ne_comm]),
  tfae_have : 1 â†’ 4,
  { simp only [continuous_def, cofinite_topology.is_open_iff'],
    rintro H s (rfl|hs),
    exacts [is_open_empty, compl_compl s â–¸ (@set.finite.is_closed _ _ H _ hs).is_open_compl] },
  tfae_have : 4 â†’ 2,
    from Î» h x, (cofinite_topology.is_closed_iff.2 $ or.inr (finite_singleton _)).preimage h,
  tfae_finish
end

lemma t1_space_iff_continuous_cofinite_of {Î± : Type*} [topological_space Î±] :
  t1_space Î± â†” continuous (@cofinite_topology.of Î±) :=
(t1_space_tfae Î±).out 0 3

lemma cofinite_topology.continuous_of [t1_space Î±] : continuous (@cofinite_topology.of Î±) :=
t1_space_iff_continuous_cofinite_of.mp â€¹_â€º

lemma t1_space_iff_exists_open : t1_space Î± â†”
  âˆ€ (x y), x â‰  y â†’ (âˆƒ (U : set Î±) (hU : is_open U), x âˆˆ U âˆ§ y âˆ‰ U) :=
(t1_space_tfae Î±).out 0 6

lemma t1_space_iff_disjoint_pure_nhds : t1_space Î± â†” âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ disjoint (pure x) (ğ“ y) :=
(t1_space_tfae Î±).out 0 8

lemma t1_space_iff_disjoint_nhds_pure : t1_space Î± â†” âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ disjoint (ğ“ x) (pure y) :=
(t1_space_tfae Î±).out 0 7

lemma disjoint_pure_nhds [t1_space Î±] {x y : Î±} (h : x â‰  y) : disjoint (pure x) (ğ“ y) :=
t1_space_iff_disjoint_pure_nhds.mp â€¹_â€º h

lemma disjoint_nhds_pure [t1_space Î±] {x y : Î±} (h : x â‰  y) : disjoint (ğ“ x) (pure y) :=
t1_space_iff_disjoint_nhds_pure.mp â€¹_â€º h

instance {Î± : Type*} : t1_space (cofinite_topology Î±) :=
t1_space_iff_continuous_cofinite_of.mpr continuous_id

lemma t1_space_antitone {Î± : Type*} : antitone (@t1_space Î±) :=
begin
  simp only [antitone, t1_space_iff_continuous_cofinite_of, continuous_iff_le_induced],
  exact Î» tâ‚ tâ‚‚ h, h.trans
end

lemma continuous_within_at_update_of_ne [t1_space Î±] [decidable_eq Î±] [topological_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {x y : Î±} {z : Î²} (hne : y â‰  x) :
  continuous_within_at (function.update f x z) s y â†” continuous_within_at f s y :=
eventually_eq.congr_continuous_within_at
  (mem_nhds_within_of_mem_nhds $ mem_of_superset (is_open_ne.mem_nhds hne) $
    Î» y' hy', function.update_noteq hy' _ _)
  (function.update_noteq hne _ _)

lemma continuous_at_update_of_ne [t1_space Î±] [decidable_eq Î±] [topological_space Î²]
  {f : Î± â†’ Î²} {x y : Î±} {z : Î²} (hne : y â‰  x) :
  continuous_at (function.update f x z) y â†” continuous_at f y :=
by simp only [â† continuous_within_at_univ, continuous_within_at_update_of_ne hne]

lemma continuous_on_update_iff [t1_space Î±] [decidable_eq Î±] [topological_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {x : Î±} {y : Î²} :
  continuous_on (function.update f x y) s â†”
    continuous_on f (s \ {x}) âˆ§ (x âˆˆ s â†’ tendsto f (ğ“[s \ {x}] x) (ğ“ y)) :=
begin
  rw [continuous_on, â† and_forall_ne x, and_comm],
  refine and_congr âŸ¨Î» H z hz, _, Î» H z hzx hzs, _âŸ© (forall_congr $ Î» hxs, _),
  { specialize H z hz.2 hz.1,
    rw continuous_within_at_update_of_ne hz.2 at H,
    exact H.mono (diff_subset _ _) },
  { rw continuous_within_at_update_of_ne hzx,
    refine (H z âŸ¨hzs, hzxâŸ©).mono_of_mem (inter_mem_nhds_within _ _),
    exact is_open_ne.mem_nhds hzx },
  { exact continuous_within_at_update_same }
end

lemma t1_space_of_injective_of_continuous [topological_space Î²] {f : Î± â†’ Î²}
  (hf : function.injective f) (hf' : continuous f) [t1_space Î²] : t1_space Î± :=
{ t1 :=
  begin
    intros x,
    rw [â† function.injective.preimage_image hf {x}, image_singleton],
    exact (t1_space.t1 $ f x).preimage hf'
  end }

protected lemma embedding.t1_space [topological_space Î²] [t1_space Î²] {f : Î± â†’ Î²}
  (hf : embedding f) : t1_space Î± :=
t1_space_of_injective_of_continuous hf.inj hf.continuous

instance subtype.t1_space {Î± : Type u} [topological_space Î±] [t1_space Î±] {p : Î± â†’ Prop} :
  t1_space (subtype p) :=
embedding_subtype_coe.t1_space

@[priority 100] -- see Note [lower instance priority]
instance t1_space.t0_space [t1_space Î±] : t0_space Î± :=
âŸ¨Î» x y h, âŸ¨{z | z â‰  y}, is_open_ne, or.inl âŸ¨h, not_not_intro rflâŸ©âŸ©âŸ©

@[simp] lemma compl_singleton_mem_nhds_iff [t1_space Î±] {x y : Î±} : {x}á¶œ âˆˆ ğ“ y â†” y â‰  x :=
is_open_compl_singleton.mem_nhds_iff

lemma compl_singleton_mem_nhds [t1_space Î±] {x y : Î±} (h : y â‰  x) : {x}á¶œ âˆˆ ğ“ y :=
compl_singleton_mem_nhds_iff.mpr h

@[simp] lemma closure_singleton [t1_space Î±] {a : Î±} :
  closure ({a} : set Î±) = {a} :=
is_closed_singleton.closure_eq

lemma set.subsingleton.closure [t1_space Î±] {s : set Î±} (hs : s.subsingleton) :
  (closure s).subsingleton :=
hs.induction_on (by simp) $ Î» x, by simp

@[simp] lemma subsingleton_closure [t1_space Î±] {s : set Î±} :
  (closure s).subsingleton â†” s.subsingleton :=
âŸ¨Î» h, h.mono subset_closure, Î» h, h.closureâŸ©

lemma is_closed_map_const {Î± Î²} [topological_space Î±] [topological_space Î²] [t1_space Î²] {y : Î²} :
  is_closed_map (function.const Î± y) :=
begin
  apply is_closed_map.of_nonempty, intros s hs h2s, simp_rw [h2s.image_const, is_closed_singleton]
end

lemma bInter_basis_nhds [t1_space Î±] {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {x : Î±}
  (h : (ğ“ x).has_basis p s) : (â‹‚ i (h : p i), s i) = {x} :=
begin
  simp only [eq_singleton_iff_unique_mem, mem_Inter],
  refine âŸ¨Î» i hi, mem_of_mem_nhds $ h.mem_of_mem hi, Î» y hy, _âŸ©,
  contrapose! hy,
  rcases h.mem_iff.1 (compl_singleton_mem_nhds hy.symm) with âŸ¨i, hi, hsubâŸ©,
  exact âŸ¨i, hi, Î» h, hsub h rflâŸ©
end

@[simp] lemma pure_le_nhds_iff [t1_space Î±] {a b : Î±} : pure a â‰¤ ğ“ b â†” a = b :=
begin
  refine âŸ¨Î» h, _, Î» h, h â–¸ pure_le_nhds aâŸ©,
  by_contra hab,
  simpa only [mem_pure, mem_compl_iff, mem_singleton, not_true] using
    h (compl_singleton_mem_nhds $ ne.symm hab)
end

@[simp] lemma nhds_le_nhds_iff [t1_space Î±] {a b : Î±} : ğ“ a â‰¤ ğ“ b â†” a = b :=
âŸ¨Î» h, pure_le_nhds_iff.mp $ (pure_le_nhds a).trans h, Î» h, h â–¸ le_rflâŸ©

@[simp] lemma compl_singleton_mem_nhds_set_iff [t1_space Î±] {x : Î±} {s : set Î±} :
  {x}á¶œ âˆˆ ğ“Ë¢ s â†” x âˆ‰ s :=
by rwa [is_open_compl_singleton.mem_nhds_set, subset_compl_singleton_iff]

@[simp] lemma nhds_set_le_iff [t1_space Î±] {s t : set Î±} : ğ“Ë¢ s â‰¤ ğ“Ë¢ t â†” s âŠ† t :=
begin
  refine âŸ¨_, Î» h, monotone_nhds_set hâŸ©,
  simp_rw [filter.le_def], intros h x hx,
  specialize h {x}á¶œ,
  simp_rw [compl_singleton_mem_nhds_set_iff] at h,
  by_contra hxt,
  exact h hxt hx,
end

@[simp] lemma nhds_set_inj_iff [t1_space Î±] {s t : set Î±} : ğ“Ë¢ s = ğ“Ë¢ t â†” s = t :=
by { simp_rw [le_antisymm_iff], exact and_congr nhds_set_le_iff nhds_set_le_iff }

lemma injective_nhds_set [t1_space Î±] : function.injective (ğ“Ë¢ : set Î± â†’ filter Î±) :=
Î» s t hst, nhds_set_inj_iff.mp hst

lemma strict_mono_nhds_set [t1_space Î±] : strict_mono (ğ“Ë¢ : set Î± â†’ filter Î±) :=
monotone_nhds_set.strict_mono_of_injective injective_nhds_set

@[simp] lemma nhds_le_nhds_set [t1_space Î±] {s : set Î±} {x : Î±} : ğ“ x â‰¤ ğ“Ë¢ s â†” x âˆˆ s :=
by rw [â† nhds_set_singleton, nhds_set_le_iff, singleton_subset_iff]

/-- Removing a non-isolated point from a dense set, one still obtains a dense set. -/
lemma dense.diff_singleton [t1_space Î±] {s : set Î±} (hs : dense s) (x : Î±) [ne_bot (ğ“[â‰ ] x)] :
  dense (s \ {x}) :=
hs.inter_of_open_right (dense_compl_singleton x) is_open_compl_singleton

/-- Removing a finset from a dense set in a space without isolated points, one still
obtains a dense set. -/
lemma dense.diff_finset [t1_space Î±] [âˆ€ (x : Î±), ne_bot (ğ“[â‰ ] x)]
  {s : set Î±} (hs : dense s) (t : finset Î±) :
  dense (s \ t) :=
begin
  induction t using finset.induction_on with x s hxs ih hd,
  { simpa using hs },
  { rw [finset.coe_insert, â† union_singleton, â† diff_diff],
    exact ih.diff_singleton _, }
end

/-- Removing a finite set from a dense set in a space without isolated points, one still
obtains a dense set. -/
lemma dense.diff_finite [t1_space Î±] [âˆ€ (x : Î±), ne_bot (ğ“[â‰ ] x)]
  {s : set Î±} (hs : dense s) {t : set Î±} (ht : finite t) :
  dense (s \ t) :=
begin
  convert hs.diff_finset ht.to_finset,
  exact (finite.coe_to_finset _).symm,
end

/-- If a function to a `t1_space` tends to some limit `b` at some point `a`, then necessarily
`b = f a`. -/
lemma eq_of_tendsto_nhds [topological_space Î²] [t1_space Î²] {f : Î± â†’ Î²} {a : Î±} {b : Î²}
  (h : tendsto f (ğ“ a) (ğ“ b)) : f a = b :=
by_contra $ assume (hfa : f a â‰  b),
have factâ‚ : {f a}á¶œ âˆˆ ğ“ b := compl_singleton_mem_nhds hfa.symm,
have factâ‚‚ : tendsto f (pure a) (ğ“ b) := h.comp (tendsto_id' $ pure_le_nhds a),
factâ‚‚ factâ‚ (eq.refl $ f a)

/-- To prove a function to a `t1_space` is continuous at some point `a`, it suffices to prove that
`f` admits *some* limit at `a`. -/
lemma continuous_at_of_tendsto_nhds [topological_space Î²] [t1_space Î²] {f : Î± â†’ Î²} {a : Î±} {b : Î²}
  (h : tendsto f (ğ“ a) (ğ“ b)) : continuous_at f a :=
show tendsto f (ğ“ a) (ğ“ $ f a), by rwa eq_of_tendsto_nhds h

lemma tendsto_const_nhds_iff [t1_space Î±] {l : filter Î±} [ne_bot l] {c d : Î±} :
  tendsto (Î» x, c) l (ğ“ d) â†” c = d :=
by simp_rw [tendsto, filter.map_const, pure_le_nhds_iff]

/-- If the punctured neighborhoods of a point form a nontrivial filter, then any neighborhood is
infinite. -/
lemma infinite_of_mem_nhds {Î±} [topological_space Î±] [t1_space Î±] (x : Î±) [hx : ne_bot (ğ“[â‰ ] x)]
  {s : set Î±} (hs : s âˆˆ ğ“ x) : set.infinite s :=
begin
  intro hsf,
  have A : {x} âŠ† s, by simp only [singleton_subset_iff, mem_of_mem_nhds hs],
  have B : is_closed (s \ {x}) := (hsf.subset (diff_subset _ _)).is_closed,
  have C : (s \ {x})á¶œ âˆˆ ğ“ x, from B.is_open_compl.mem_nhds (Î» h, h.2 rfl),
  have D : {x} âˆˆ ğ“ x, by simpa only [â† diff_eq, diff_diff_cancel_left A] using inter_mem hs C,
  rwa [â† mem_interior_iff_mem_nhds, interior_singleton] at D
end

lemma discrete_of_t1_of_finite {X : Type*} [topological_space X] [t1_space X] [fintype X] :
  discrete_topology X :=
begin
  apply singletons_open_iff_discrete.mp,
  intros x,
  rw [â† is_closed_compl_iff],
  exact (finite.of_fintype _).is_closed
end

lemma singleton_mem_nhds_within_of_mem_discrete {s : set Î±} [discrete_topology s]
  {x : Î±} (hx : x âˆˆ s) :
  {x} âˆˆ ğ“[s] x :=
begin
  have : ({âŸ¨x, hxâŸ©} : set s) âˆˆ ğ“ (âŸ¨x, hxâŸ© : s), by simp [nhds_discrete],
  simpa only [nhds_within_eq_map_subtype_coe hx, image_singleton]
    using @image_mem_map _ _ _ (coe : s â†’ Î±) _ this
end

/-- The neighbourhoods filter of `x` within `s`, under the discrete topology, is equal to
the pure `x` filter (which is the principal filter at the singleton `{x}`.) -/
lemma nhds_within_of_mem_discrete {s : set Î±} [discrete_topology s] {x : Î±} (hx : x âˆˆ s) :
  ğ“[s] x = pure x :=
le_antisymm (le_pure_iff.2 $ singleton_mem_nhds_within_of_mem_discrete hx) (pure_le_nhds_within hx)

lemma filter.has_basis.exists_inter_eq_singleton_of_mem_discrete
  {Î¹ : Type*} {p : Î¹ â†’ Prop} {t : Î¹ â†’ set Î±} {s : set Î±} [discrete_topology s] {x : Î±}
  (hb : (ğ“ x).has_basis p t) (hx : x âˆˆ s) :
  âˆƒ i (hi : p i), t i âˆ© s = {x} :=
begin
  rcases (nhds_within_has_basis hb s).mem_iff.1 (singleton_mem_nhds_within_of_mem_discrete hx)
    with âŸ¨i, hi, hixâŸ©,
  exact âŸ¨i, hi, subset.antisymm hix $ singleton_subset_iff.2
    âŸ¨mem_of_mem_nhds $ hb.mem_of_mem hi, hxâŸ©âŸ©
end

/-- A point `x` in a discrete subset `s` of a topological space admits a neighbourhood
that only meets `s` at `x`.  -/
lemma nhds_inter_eq_singleton_of_mem_discrete {s : set Î±} [discrete_topology s]
  {x : Î±} (hx : x âˆˆ s) :
  âˆƒ U âˆˆ ğ“ x, U âˆ© s = {x} :=
by simpa using (ğ“ x).basis_sets.exists_inter_eq_singleton_of_mem_discrete hx

/-- For point `x` in a discrete subset `s` of a topological space, there is a set `U`
such that
1. `U` is a punctured neighborhood of `x` (ie. `U âˆª {x}` is a neighbourhood of `x`),
2. `U` is disjoint from `s`.
-/
lemma disjoint_nhds_within_of_mem_discrete {s : set Î±} [discrete_topology s] {x : Î±} (hx : x âˆˆ s) :
  âˆƒ U âˆˆ ğ“[â‰ ] x, disjoint U s :=
let âŸ¨V, h, h'âŸ© := nhds_inter_eq_singleton_of_mem_discrete hx in
  âŸ¨{x}á¶œ âˆ© V, inter_mem_nhds_within _ h,
    (disjoint_iff_inter_eq_empty.mpr (by { rw [inter_assoc, h', compl_inter_self] }))âŸ©

/-- Let `X` be a topological space and let `s, t âŠ† X` be two subsets.  If there is an inclusion
`t âŠ† s`, then the topological space structure on `t` induced by `X` is the same as the one
obtained by the induced topological space structure on `s`. -/
lemma topological_space.subset_trans {X : Type*} [tX : topological_space X]
  {s t : set X} (ts : t âŠ† s) :
  (subtype.topological_space : topological_space t) =
    (subtype.topological_space : topological_space s).induced (set.inclusion ts) :=
begin
  change tX.induced ((coe : s â†’ X) âˆ˜ (set.inclusion ts)) =
    topological_space.induced (set.inclusion ts) (tX.induced _),
  rw â† induced_compose,
end

/-- This lemma characterizes discrete topological spaces as those whose singletons are
neighbourhoods. -/
lemma discrete_topology_iff_nhds {X : Type*} [topological_space X] :
  discrete_topology X â†” (nhds : X â†’ filter X) = pure :=
begin
  split,
  { introI hX,
    exact nhds_discrete X },
  { intro h,
    constructor,
    apply eq_of_nhds_eq_nhds,
    simp [h, nhds_bot] }
end

/-- The topology pulled-back under an inclusion `f : X â†’ Y` from the discrete topology (`âŠ¥`) is the
discrete topology.
This version does not assume the choice of a topology on either the source `X`
nor the target `Y` of the inclusion `f`. -/
lemma induced_bot {X Y : Type*} {f : X â†’ Y} (hf : function.injective f) :
  topological_space.induced f âŠ¥ = âŠ¥ :=
eq_of_nhds_eq_nhds (by simp [nhds_induced, â† set.image_singleton, hf.preimage_image, nhds_bot])

/-- The topology induced under an inclusion `f : X â†’ Y` from the discrete topological space `Y`
is the discrete topology on `X`. -/
lemma discrete_topology_induced {X Y : Type*} [tY : topological_space Y] [discrete_topology Y]
  {f : X â†’ Y} (hf : function.injective f) : @discrete_topology X (topological_space.induced f tY) :=
begin
  constructor,
  rw discrete_topology.eq_bot Y,
  exact induced_bot hf
end

/-- Let `s, t âŠ† X` be two subsets of a topological space `X`.  If `t âŠ† s` and the topology induced
by `X`on `s` is discrete, then also the topology induces on `t` is discrete.  -/
lemma discrete_topology.of_subset {X : Type*} [topological_space X] {s t : set X}
  (ds : discrete_topology s) (ts : t âŠ† s) :
  discrete_topology t :=
begin
  rw [topological_space.subset_trans ts, ds.eq_bot],
  exact {eq_bot := induced_bot (set.inclusion_injective ts)}
end

/-- A Tâ‚‚ space, also known as a Hausdorff space, is one in which for every
  `x â‰  y` there exists disjoint open sets around `x` and `y`. This is
  the most widely used of the separation axioms. -/
@[mk_iff] class t2_space (Î± : Type u) [topological_space Î±] : Prop :=
(t2 : âˆ€ x y, x â‰  y â†’ âˆƒ u v : set Î±, is_open u âˆ§ is_open v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ u âˆ© v = âˆ…)

/-- Two different points can be separated by open sets. -/
lemma t2_separation [t2_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ u v : set Î±, is_open u âˆ§ is_open v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ u âˆ© v = âˆ… :=
t2_space.t2 x y h

/-- A finite set can be separated by open sets. -/
lemma t2_separation_finset [t2_space Î±] (s : finset Î±) :
  âˆƒ f : Î± â†’ set Î±, set.pairwise_disjoint â†‘s f âˆ§ âˆ€ x âˆˆ s, x âˆˆ f x âˆ§ is_open (f x) :=
finset.induction_on s (by simp) begin
  rintros t s ht âŸ¨f, hf, hf'âŸ©,
  have hty : âˆ€ y : s, t â‰  y := by { rintros y rfl, exact ht y.2 },
  choose u v hu hv htu hxv huv using Î» {x} (h : t â‰  x), t2_separation h,
  refine âŸ¨Î» x, if ht : t = x then â‹‚ y : s, u (hty y) else f x âˆ© v ht, _, _âŸ©,
  { rintros x hxâ‚ y hyâ‚ hxy a âŸ¨hx, hyâŸ©,
    rw [finset.mem_coe, finset.mem_insert, eq_comm] at hxâ‚ hyâ‚,
    rcases eq_or_ne t x with rfl | hxâ‚‚;
    rcases eq_or_ne t y with rfl | hyâ‚‚,
    { exact hxy rfl },
    { simp_rw [dif_pos rfl, mem_Inter] at hx,
      simp_rw [dif_neg hyâ‚‚] at hy,
      rw [bot_eq_empty, â†huv hyâ‚‚],
      exact âŸ¨hx âŸ¨y, hyâ‚.resolve_left hyâ‚‚âŸ©, hy.2âŸ© },
    { simp_rw [dif_neg hxâ‚‚] at hx,
      simp_rw [dif_pos rfl, mem_Inter] at hy,
      rw [bot_eq_empty, â†huv hxâ‚‚],
      exact âŸ¨hy âŸ¨x, hxâ‚.resolve_left hxâ‚‚âŸ©, hx.2âŸ© },
    { simp_rw [dif_neg hxâ‚‚] at hx,
      simp_rw [dif_neg hyâ‚‚] at hy,
      exact hf (hxâ‚.resolve_left hxâ‚‚) (hyâ‚.resolve_left hyâ‚‚) hxy âŸ¨hx.1, hy.1âŸ© } },
  { intros x hx,
    split_ifs with ht,
    { refine âŸ¨mem_Inter.2 (Î» y, _), is_open_Inter (Î» y, hu (hty y))âŸ©,
      rw â†ht,
      exact htu (hty y) },
    { have hx := hf' x ((finset.mem_insert.1 hx).resolve_left (ne.symm ht)),
      exact âŸ¨âŸ¨hx.1, hxv htâŸ©, is_open.inter hx.2 (hv ht)âŸ© } }
end

@[priority 100] -- see Note [lower instance priority]
instance t2_space.t1_space [t2_space Î±] : t1_space Î± :=
âŸ¨Î» x, is_open_compl_iff.1 $ is_open_iff_forall_mem_open.2 $ Î» y hxy,
let âŸ¨u, v, hu, hv, hyu, hxv, huvâŸ© := t2_separation (mt mem_singleton_of_eq hxy) in
âŸ¨u, Î» z hz1 hz2, (ext_iff.1 huv x).1 âŸ¨mem_singleton_iff.1 hz2 â–¸ hz1, hxvâŸ©, hu, hyuâŸ©âŸ©

lemma eq_of_nhds_ne_bot [ht : t2_space Î±] {x y : Î±} (h : ne_bot (ğ“ x âŠ“ ğ“ y)) : x = y :=
classical.by_contradiction $ assume : x â‰  y,
let âŸ¨u, v, hu, hv, hx, hy, huvâŸ© := t2_space.t2 x y this in
absurd huv $ (inf_ne_bot_iff.1 h (is_open.mem_nhds hu hx) (is_open.mem_nhds hv hy)).ne_empty

/-- A space is Tâ‚‚ iff the neighbourhoods of distinct points generate the bottom filter. -/
lemma t2_iff_nhds : t2_space Î± â†” âˆ€ {x y : Î±}, ne_bot (ğ“ x âŠ“ ğ“ y) â†’ x = y :=
âŸ¨assume h, by exactI Î» x y, eq_of_nhds_ne_bot,
 assume h, âŸ¨assume x y xy,
   have ğ“ x âŠ“ ğ“ y = âŠ¥ := not_ne_bot.1 $ mt h xy,
   let âŸ¨u', hu', v', hv', u'v'âŸ© := empty_mem_iff_bot.mpr this,
       âŸ¨u, uu', uo, huâŸ© := mem_nhds_iff.mp hu',
       âŸ¨v, vv', vo, hvâŸ© := mem_nhds_iff.mp hv' in
   âŸ¨u, v, uo, vo, hu, hv, by { rw [â† subset_empty_iff, u'v'], exact inter_subset_inter uu' vv' }âŸ©âŸ©âŸ©

lemma t2_space_iff_nhds : t2_space Î± â†” âˆ€ {x y : Î±}, x â‰  y â†’ âˆƒ (U âˆˆ ğ“ x) (V âˆˆ ğ“ y), U âˆ© V = âˆ… :=
begin
  split,
  { rintro âŸ¨hâŸ© x y hxy,
    rcases h x y hxy with âŸ¨u, v, u_op, v_op, hx, hy, HâŸ©,
    exact âŸ¨u, u_op.mem_nhds hx, v, v_op.mem_nhds hy, HâŸ© },
  { refine Î» h, âŸ¨Î» x y hxy, _âŸ©,
    rcases h hxy with âŸ¨u, u_in, v, v_in, HâŸ©,
    rcases mem_nhds_iff.mp u_in with âŸ¨U, hUu, U_op, hxUâŸ©,
    rcases mem_nhds_iff.mp v_in with âŸ¨V, hVv, V_op, hyVâŸ©,
    refine âŸ¨U, V, U_op, V_op, hxU, hyV, set.eq_empty_of_subset_empty _âŸ©,
    rw â† H,
    exact set.inter_subset_inter hUu hVv }
end

lemma t2_separation_nhds [t2_space Î±] {x y : Î±} (h : x â‰  y) :
   âˆƒ u v, u âˆˆ ğ“ x âˆ§ v âˆˆ ğ“ y âˆ§ u âˆ© v = âˆ… :=
let âŸ¨u, v, open_u, open_v, x_in, y_in, huvâŸ© := t2_separation h in
âŸ¨u, v, open_u.mem_nhds x_in, open_v.mem_nhds y_in, huvâŸ©

lemma t2_separation_compact_nhds [locally_compact_space Î±]
  [t2_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ u v, u âˆˆ ğ“ x âˆ§ v âˆˆ ğ“ y âˆ§ is_compact u âˆ§ is_compact v âˆ§ u âˆ© v = âˆ… :=
begin
  obtain âŸ¨uâ‚€, vâ‚€, uâ‚€_in, vâ‚€_in, huâ‚€vâ‚€âŸ© := t2_separation_nhds h,
  obtain âŸ¨Kâ‚€, Kâ‚€_in, Kâ‚€_uâ‚€, hKâ‚€âŸ© := local_compact_nhds uâ‚€_in,
  obtain âŸ¨Lâ‚€, Lâ‚€_in, Lâ‚€_uâ‚€, hLâ‚€âŸ© := local_compact_nhds vâ‚€_in,
  use [Kâ‚€, Lâ‚€, Kâ‚€_in, Lâ‚€_in, hKâ‚€, hLâ‚€],
  apply set.eq_empty_of_subset_empty,
  rw â† huâ‚€vâ‚€,
  exact set.inter_subset_inter Kâ‚€_uâ‚€ Lâ‚€_uâ‚€
end

lemma t2_iff_ultrafilter :
  t2_space Î± â†” âˆ€ {x y : Î±} (f : ultrafilter Î±), â†‘f â‰¤ ğ“ x â†’ â†‘f â‰¤ ğ“ y â†’ x = y :=
t2_iff_nhds.trans $ by simp only [â†exists_ultrafilter_iff, and_imp, le_inf_iff, exists_imp_distrib]

lemma is_closed_diagonal [t2_space Î±] : is_closed (diagonal Î±) :=
begin
  refine is_closed_iff_cluster_pt.mpr _,
  rintro âŸ¨aâ‚, aâ‚‚âŸ© h,
  refine eq_of_nhds_ne_bot âŸ¨Î» this : ğ“ aâ‚ âŠ“ ğ“ aâ‚‚ = âŠ¥, h.ne _âŸ©,
  obtain âŸ¨tâ‚, (htâ‚ : tâ‚ âˆˆ ğ“ aâ‚), tâ‚‚, (htâ‚‚ : tâ‚‚ âˆˆ ğ“ aâ‚‚), (h' : tâ‚ âˆ© tâ‚‚ = âˆ…)âŸ© :=
    inf_eq_bot_iff.1 this,
  rw [inf_principal_eq_bot, nhds_prod_eq],
  apply mem_of_superset (prod_mem_prod htâ‚ htâ‚‚),
  rintro âŸ¨x, yâŸ© âŸ¨x_in, y_inâŸ© (heq : x = y),
  rw â† heq at *,
  have : x âˆˆ tâ‚ âˆ© tâ‚‚ := âŸ¨x_in, y_inâŸ©,
  rwa h' at this
end

lemma t2_iff_is_closed_diagonal : t2_space Î± â†” is_closed (diagonal Î±) :=
begin
  split,
  { introI h,
    exact is_closed_diagonal },
  { intro h,
    constructor,
    intros x y hxy,
    have : (x, y) âˆˆ (diagonal Î±)á¶œ, by rwa [mem_compl_iff],
    obtain âŸ¨t, t_sub, t_op, xytâŸ© : âˆƒ t âŠ† (diagonal Î±)á¶œ, is_open t âˆ§ (x, y) âˆˆ t :=
      is_open_iff_forall_mem_open.mp h.is_open_compl _ this,
    rcases is_open_prod_iff.mp t_op x y xyt with âŸ¨U, V, U_op, V_op, xU, yV, HâŸ©,
    use [U, V, U_op, V_op, xU, yV],
    have := subset.trans H t_sub,
    rw eq_empty_iff_forall_not_mem,
    rintros z âŸ¨zU, zVâŸ©,
    have : Â¬ (z, z) âˆˆ diagonal Î± := this (mk_mem_prod zU zV),
    exact this rfl },
end

section separated

open separated finset

lemma finset_disjoint_finset_opens_of_t2 [t2_space Î±] :
  âˆ€ (s t : finset Î±), disjoint s t â†’ separated (s : set Î±) t :=
begin
  refine induction_on_union _ (Î» a b hi d, (hi d.symm).symm) (Î» a d, empty_right a) (Î» a b ab, _) _,
  { obtain âŸ¨U, V, oU, oV, aU, bV, UVâŸ© := t2_separation (finset.disjoint_singleton.1 ab),
    refine âŸ¨U, V, oU, oV, _, _, set.disjoint_iff_inter_eq_empty.mpr UVâŸ©;
    exact singleton_subset_set_iff.mpr â€¹_â€º },
  { intros a b c ac bc d,
    apply_mod_cast union_left (ac (disjoint_of_subset_left (a.subset_union_left b) d)) (bc _),
    exact disjoint_of_subset_left (a.subset_union_right b) d },
end

lemma point_disjoint_finset_opens_of_t2 [t2_space Î±] {x : Î±} {s : finset Î±} (h : x âˆ‰ s) :
  separated ({x} : set Î±) s :=
by exact_mod_cast finset_disjoint_finset_opens_of_t2 {x} s (finset.disjoint_singleton_left.mpr h)

end separated

lemma tendsto_nhds_unique [t2_space Î±] {f : Î² â†’ Î±} {l : filter Î²} {a b : Î±}
  [ne_bot l] (ha : tendsto f l (ğ“ a)) (hb : tendsto f l (ğ“ b)) : a = b :=
eq_of_nhds_ne_bot $ ne_bot_of_le $ le_inf ha hb

lemma tendsto_nhds_unique' [t2_space Î±] {f : Î² â†’ Î±} {l : filter Î²} {a b : Î±}
  (hl : ne_bot l) (ha : tendsto f l (ğ“ a)) (hb : tendsto f l (ğ“ b)) : a = b :=
eq_of_nhds_ne_bot $ ne_bot_of_le $ le_inf ha hb

lemma tendsto_nhds_unique_of_eventually_eq [t2_space Î±] {f g : Î² â†’ Î±} {l : filter Î²} {a b : Î±}
  [ne_bot l] (ha : tendsto f l (ğ“ a)) (hb : tendsto g l (ğ“ b)) (hfg : f =á¶ [l] g) :
  a = b :=
tendsto_nhds_unique (ha.congr' hfg) hb

lemma tendsto_nhds_unique_of_frequently_eq [t2_space Î±] {f g : Î² â†’ Î±} {l : filter Î²} {a b : Î±}
  (ha : tendsto f l (ğ“ a)) (hb : tendsto g l (ğ“ b)) (hfg : âˆƒá¶  x in l, f x = g x) :
  a = b :=
have âˆƒá¶  z : Î± Ã— Î± in ğ“ (a, b), z.1 = z.2 := (ha.prod_mk_nhds hb).frequently hfg,
not_not.1 $ Î» hne, this (is_closed_diagonal.is_open_compl.mem_nhds hne)

/-- A Tâ‚‚.â‚… space, also known as a Urysohn space, is a topological space
  where for every pair `x â‰  y`, there are two open sets, with the intersection of closures
  empty, one containing `x` and the other `y` . -/
class t2_5_space (Î± : Type u) [topological_space Î±]: Prop :=
(t2_5 : âˆ€ x y  (h : x â‰  y), âˆƒ (U V: set Î±), is_open U âˆ§  is_open V âˆ§
                                            closure U âˆ© closure V = âˆ… âˆ§ x âˆˆ U âˆ§ y âˆˆ V)

@[priority 100] -- see Note [lower instance priority]
instance t2_5_space.t2_space [t2_5_space Î±] : t2_space Î± :=
âŸ¨Î» x y hxy,
  let âŸ¨U, V, hU, hV, hUV, hhâŸ© := t2_5_space.t2_5 x y hxy in
  âŸ¨U, V, hU, hV, hh.1, hh.2, subset_eq_empty (powerset_mono.mpr
    (closure_inter_subset_inter_closure U V) subset_closure) hUVâŸ©âŸ©

section lim
variables [t2_space Î±] {f : filter Î±}

/-!
### Properties of `Lim` and `lim`

In this section we use explicit `nonempty Î±` instances for `Lim` and `lim`. This way the lemmas
are useful without a `nonempty Î±` instance.
-/

lemma Lim_eq {a : Î±} [ne_bot f] (h : f â‰¤ ğ“ a) :
  @Lim _ _ âŸ¨aâŸ© f = a :=
tendsto_nhds_unique (le_nhds_Lim âŸ¨a, hâŸ©) h

lemma Lim_eq_iff [ne_bot f] (h : âˆƒ (a : Î±), f â‰¤ nhds a) {a} : @Lim _ _ âŸ¨aâŸ© f = a â†” f â‰¤ ğ“ a :=
âŸ¨Î» c, c â–¸ le_nhds_Lim h, Lim_eqâŸ©

lemma ultrafilter.Lim_eq_iff_le_nhds [compact_space Î±] {x : Î±} {F : ultrafilter Î±} :
  F.Lim = x â†” â†‘F â‰¤ ğ“ x :=
âŸ¨Î» h, h â–¸ F.le_nhds_Lim, Lim_eqâŸ©

lemma is_open_iff_ultrafilter' [compact_space Î±] (U : set Î±) :
  is_open U â†” (âˆ€ F : ultrafilter Î±, F.Lim âˆˆ U â†’ U âˆˆ F.1) :=
begin
  rw is_open_iff_ultrafilter,
  refine âŸ¨Î» h F hF, h F.Lim hF F F.le_nhds_Lim, _âŸ©,
  intros cond x hx f h,
  rw [â† (ultrafilter.Lim_eq_iff_le_nhds.2 h)] at hx,
  exact cond _ hx
end

lemma filter.tendsto.lim_eq {a : Î±} {f : filter Î²} [ne_bot f] {g : Î² â†’ Î±} (h : tendsto g f (ğ“ a)) :
  @lim _ _ _ âŸ¨aâŸ© f g = a :=
Lim_eq h

lemma filter.lim_eq_iff {f : filter Î²} [ne_bot f] {g : Î² â†’ Î±} (h : âˆƒ a, tendsto g f (ğ“ a)) {a} :
  @lim _ _ _ âŸ¨aâŸ© f g = a â†” tendsto g f (ğ“ a) :=
âŸ¨Î» c, c â–¸ tendsto_nhds_lim h, filter.tendsto.lim_eqâŸ©

lemma continuous.lim_eq [topological_space Î²] {f : Î² â†’ Î±} (h : continuous f) (a : Î²) :
  @lim _ _ _ âŸ¨f aâŸ© (ğ“ a) f = f a :=
(h.tendsto a).lim_eq

@[simp] lemma Lim_nhds (a : Î±) : @Lim _ _ âŸ¨aâŸ© (ğ“ a) = a :=
Lim_eq le_rfl

@[simp] lemma lim_nhds_id (a : Î±) : @lim _ _ _ âŸ¨aâŸ© (ğ“ a) id = a :=
Lim_nhds a

@[simp] lemma Lim_nhds_within {a : Î±} {s : set Î±} (h : a âˆˆ closure s) :
  @Lim _ _ âŸ¨aâŸ© (ğ“[s] a) = a :=
by haveI : ne_bot (ğ“[s] a) := mem_closure_iff_cluster_pt.1 h;
exact Lim_eq inf_le_left

@[simp] lemma lim_nhds_within_id {a : Î±} {s : set Î±} (h : a âˆˆ closure s) :
  @lim _ _ _ âŸ¨aâŸ© (ğ“[s] a) id = a :=
Lim_nhds_within h

end lim

/-!
### `t2_space` constructions

We use two lemmas to prove that various standard constructions generate Hausdorff spaces from
Hausdorff spaces:

* `separated_by_continuous` says that two points `x y : Î±` can be separated by open neighborhoods
  provided that there exists a continuous map `f : Î± â†’ Î²` with a Hausdorff codomain such that
  `f x â‰  f y`. We use this lemma to prove that topological spaces defined using `induced` are
  Hausdorff spaces.

* `separated_by_open_embedding` says that for an open embedding `f : Î± â†’ Î²` of a Hausdorff space
  `Î±`, the images of two distinct points `x y : Î±`, `x â‰  y` can be separated by open neighborhoods.
  We use this lemma to prove that topological spaces defined using `coinduced` are Hausdorff spaces.
-/

@[priority 100] -- see Note [lower instance priority]
instance t2_space_discrete {Î± : Type*} [topological_space Î±] [discrete_topology Î±] : t2_space Î± :=
{ t2 := assume x y hxy, âŸ¨{x}, {y}, is_open_discrete _, is_open_discrete _, rfl, rfl,
  eq_empty_iff_forall_not_mem.2 $ by intros z hz;
    cases eq_of_mem_singleton hz.1; cases eq_of_mem_singleton hz.2; ccâŸ© }

lemma separated_by_continuous {Î± : Type*} {Î² : Type*}
  [topological_space Î±] [topological_space Î²] [t2_space Î²]
  {f : Î± â†’ Î²} (hf : continuous f) {x y : Î±} (h : f x â‰  f y) :
  âˆƒu v : set Î±, is_open u âˆ§ is_open v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ u âˆ© v = âˆ… :=
let âŸ¨u, v, uo, vo, xu, yv, uvâŸ© := t2_separation h in
âŸ¨f â»Â¹' u, f â»Â¹' v, uo.preimage hf, vo.preimage hf, xu, yv,
  by rw [â†preimage_inter, uv, preimage_empty]âŸ©

lemma separated_by_open_embedding {Î± Î² : Type*} [topological_space Î±] [topological_space Î²]
  [t2_space Î±] {f : Î± â†’ Î²} (hf : open_embedding f) {x y : Î±} (h : x â‰  y) :
  âˆƒ u v : set Î², is_open u âˆ§ is_open v âˆ§ f x âˆˆ u âˆ§ f y âˆˆ v âˆ§ u âˆ© v = âˆ… :=
let âŸ¨u, v, uo, vo, xu, yv, uvâŸ© := t2_separation h in
âŸ¨f '' u, f '' v, hf.is_open_map _ uo, hf.is_open_map _ vo,
  mem_image_of_mem _ xu, mem_image_of_mem _ yv, by rw [image_inter hf.inj, uv, image_empty]âŸ©

instance {Î± : Type*} {p : Î± â†’ Prop} [t : topological_space Î±] [t2_space Î±] : t2_space (subtype p) :=
âŸ¨assume x y h, separated_by_continuous continuous_subtype_val (mt subtype.eq h)âŸ©

instance {Î± : Type*} {Î² : Type*} [tâ‚ : topological_space Î±] [t2_space Î±]
  [tâ‚‚ : topological_space Î²] [t2_space Î²] : t2_space (Î± Ã— Î²) :=
âŸ¨assume âŸ¨xâ‚,xâ‚‚âŸ© âŸ¨yâ‚,yâ‚‚âŸ© h,
  or.elim (not_and_distrib.mp (mt prod.ext_iff.mpr h))
    (Î» hâ‚, separated_by_continuous continuous_fst hâ‚)
    (Î» hâ‚‚, separated_by_continuous continuous_snd hâ‚‚)âŸ©

lemma embedding.t2_space [topological_space Î²] [t2_space Î²] {f : Î± â†’ Î²} (hf : embedding f) :
  t2_space Î± :=
âŸ¨Î» x y h, separated_by_continuous hf.continuous (hf.inj.ne h)âŸ©

instance {Î± : Type*} {Î² : Type*} [tâ‚ : topological_space Î±] [t2_space Î±]
  [tâ‚‚ : topological_space Î²] [t2_space Î²] : t2_space (Î± âŠ• Î²) :=
begin
  constructor,
  rintros (x|x) (y|y) h,
  { replace h : x â‰  y := Î» c, (c.subst h) rfl,
    exact separated_by_open_embedding open_embedding_inl h },
  { exact âŸ¨_, _, is_open_range_inl, is_open_range_inr, âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©,
      range_inl_inter_range_inrâŸ© },
  { exact âŸ¨_, _, is_open_range_inr, is_open_range_inl, âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©,
      range_inr_inter_range_inlâŸ© },
  { replace h : x â‰  y := Î» c, (c.subst h) rfl,
    exact separated_by_open_embedding open_embedding_inr h }
end

instance Pi.t2_space {Î± : Type*} {Î² : Î± â†’ Type v} [tâ‚‚ : Î a, topological_space (Î² a)]
  [âˆ€a, t2_space (Î² a)] :
  t2_space (Î a, Î² a) :=
âŸ¨assume x y h,
  let âŸ¨i, hiâŸ© := not_forall.mp (mt funext h) in
  separated_by_continuous (continuous_apply i) hiâŸ©

instance sigma.t2_space {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î i, topological_space (Î± i)]
  [âˆ€a, t2_space (Î± a)] :
  t2_space (Î£i, Î± i) :=
begin
  constructor,
  rintros âŸ¨i, xâŸ© âŸ¨j, yâŸ© neq,
  rcases em (i = j) with (rfl|h),
  { replace neq : x â‰  y := Î» c, (c.subst neq) rfl,
    exact separated_by_open_embedding open_embedding_sigma_mk neq },
  { exact âŸ¨_, _, is_open_range_sigma_mk, is_open_range_sigma_mk, âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©, by tidyâŸ© }
end

variables [topological_space Î²]

lemma is_closed_eq [t2_space Î±] {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) : is_closed {x:Î² | f x = g x} :=
continuous_iff_is_closed.mp (hf.prod_mk hg) _ is_closed_diagonal

/-- If two continuous maps are equal on `s`, then they are equal on the closure of `s`. See also
`set.eq_on.of_subset_closure` for a more general version. -/
lemma set.eq_on.closure [t2_space Î±] {s : set Î²} {f g : Î² â†’ Î±} (h : eq_on f g s)
  (hf : continuous f) (hg : continuous g) :
  eq_on f g (closure s) :=
closure_minimal h (is_closed_eq hf hg)

/-- If two continuous functions are equal on a dense set, then they are equal. -/
lemma continuous.ext_on [t2_space Î±] {s : set Î²} (hs : dense s) {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) (h : eq_on f g s) :
  f = g :=
funext $ Î» x, h.closure hf hg (hs x)

/-- If `f x = g x` for all `x âˆˆ s` and `f`, `g` are continuous on `t`, `s âŠ† t âŠ† closure s`, then
`f x = g x` for all `x âˆˆ t`. See also `set.eq_on.closure`. -/
lemma set.eq_on.of_subset_closure [t2_space Î±] {s t : set Î²} {f g : Î² â†’ Î±} (h : eq_on f g s)
  (hf : continuous_on f t) (hg : continuous_on g t) (hst : s âŠ† t) (hts : t âŠ† closure s) :
  eq_on f g t :=
begin
  intros x hx,
  haveI : (ğ“[s] x).ne_bot, from mem_closure_iff_cluster_pt.mp (hts hx),
  exact tendsto_nhds_unique_of_eventually_eq ((hf x hx).mono_left $ nhds_within_mono _ hst)
    ((hg x hx).mono_left $ nhds_within_mono _ hst) (h.eventually_eq_of_mem self_mem_nhds_within)
end

lemma function.left_inverse.closed_range [t2_space Î±] {f : Î± â†’ Î²} {g : Î² â†’ Î±}
  (h : function.left_inverse f g) (hf : continuous f) (hg : continuous g) :
  is_closed (range g) :=
have eq_on (g âˆ˜ f) id (closure $ range g),
  from h.right_inv_on_range.eq_on.closure (hg.comp hf) continuous_id,
is_closed_of_closure_subset $ Î» x hx,
calc x = g (f x) : (this hx).symm
   ... âˆˆ _ : mem_range_self _

lemma function.left_inverse.closed_embedding [t2_space Î±] {f : Î± â†’ Î²} {g : Î² â†’ Î±}
  (h : function.left_inverse f g) (hf : continuous f) (hg : continuous g) :
  closed_embedding g :=
âŸ¨h.embedding hf hg, h.closed_range hf hgâŸ©

lemma diagonal_eq_range_diagonal_map {Î± : Type*} : {p:Î±Ã—Î± | p.1 = p.2} = range (Î»x, (x,x)) :=
ext $ assume p, iff.intro
  (assume h, âŸ¨p.1, prod.ext_iff.2 âŸ¨rfl, hâŸ©âŸ©)
  (assume âŸ¨x, hxâŸ©, show p.1 = p.2, by rw â†hx)

lemma prod_subset_compl_diagonal_iff_disjoint {Î± : Type*} {s t : set Î±} :
  s Ã—Ë¢ t âŠ† {p:Î±Ã—Î± | p.1 = p.2}á¶œ â†” s âˆ© t = âˆ… :=
by rw [eq_empty_iff_forall_not_mem, subset_compl_comm,
       diagonal_eq_range_diagonal_map, range_subset_iff]; simp

lemma compact_compact_separated [t2_space Î±] {s t : set Î±}
  (hs : is_compact s) (ht : is_compact t) (hst : s âˆ© t = âˆ…) :
  âˆƒu v : set Î±, is_open u âˆ§ is_open v âˆ§ s âŠ† u âˆ§ t âŠ† v âˆ§ u âˆ© v = âˆ… :=
by simp only [prod_subset_compl_diagonal_iff_disjoint.symm] at âŠ¢ hst;
   exact generalized_tube_lemma hs ht is_closed_diagonal.is_open_compl hst

/-- In a `t2_space`, every compact set is closed. -/
lemma is_compact.is_closed [t2_space Î±] {s : set Î±} (hs : is_compact s) : is_closed s :=
is_open_compl_iff.1 $ is_open_iff_forall_mem_open.mpr $ assume x hx,
  let âŸ¨u, v, uo, vo, su, xv, uvâŸ© :=
    compact_compact_separated hs (is_compact_singleton : is_compact {x})
      (by rwa [inter_comm, â†subset_compl_iff_disjoint, singleton_subset_iff]) in
  have v âŠ† sá¶œ, from
    subset_compl_comm.mp (subset.trans su (subset_compl_iff_disjoint.mpr uv)),
âŸ¨v, this, vo, by simpa using xvâŸ©

@[simp] lemma filter.coclosed_compact_eq_cocompact [t2_space Î±] :
  coclosed_compact Î± = cocompact Î± :=
by simp [coclosed_compact, cocompact, infi_and', and_iff_right_of_imp is_compact.is_closed]

@[simp] lemma bornology.relatively_compact_eq_in_compact [t2_space Î±] :
  bornology.relatively_compact Î± = bornology.in_compact Î± :=
by rw bornology.ext_iff; exact filter.coclosed_compact_eq_cocompact

/-- If `V : Î¹ â†’ set Î±` is a decreasing family of compact sets then any neighborhood of
`â‹‚ i, V i` contains some `V i`. This is a version of `exists_subset_nhd_of_compact'` where we
don't need to assume each `V i` closed because it follows from compactness since `Î±` is
assumed to be Hausdorff. -/
lemma exists_subset_nhd_of_compact [t2_space Î±] {Î¹ : Type*} [nonempty Î¹] {V : Î¹ â†’ set Î±}
  (hV : directed (âŠ‡) V) (hV_cpct : âˆ€ i, is_compact (V i)) {U : set Î±}
  (hU : âˆ€ x âˆˆ â‹‚ i, V i, U âˆˆ ğ“ x) : âˆƒ i, V i âŠ† U :=
exists_subset_nhd_of_compact' hV hV_cpct (Î» i, (hV_cpct i).is_closed) hU

lemma compact_exhaustion.is_closed [t2_space Î±] (K : compact_exhaustion Î±) (n : â„•) :
  is_closed (K n) :=
(K.is_compact n).is_closed

lemma is_compact.inter [t2_space Î±] {s t : set Î±} (hs : is_compact s) (ht : is_compact t) :
  is_compact (s âˆ© t) :=
hs.inter_right $ ht.is_closed

lemma compact_closure_of_subset_compact [t2_space Î±] {s t : set Î±} (ht : is_compact t) (h : s âŠ† t) :
  is_compact (closure s) :=
compact_of_is_closed_subset ht is_closed_closure (closure_minimal h ht.is_closed)

@[simp]
lemma exists_compact_superset_iff [t2_space Î±] {s : set Î±} :
  (âˆƒ K, is_compact K âˆ§ s âŠ† K) â†” is_compact (closure s) :=
âŸ¨Î» âŸ¨K, hK, hsKâŸ©, compact_closure_of_subset_compact hK hsK, Î» h, âŸ¨closure s, h, subset_closureâŸ©âŸ©

lemma image_closure_of_compact [t2_space Î²]
  {s : set Î±} (hs : is_compact (closure s)) {f : Î± â†’ Î²} (hf : continuous_on f (closure s)) :
  f '' closure s = closure (f '' s) :=
subset.antisymm hf.image_closure $ closure_minimal (image_subset f subset_closure)
  (hs.image_of_continuous_on hf).is_closed

/-- If a compact set is covered by two open sets, then we can cover it by two compact subsets. -/
lemma is_compact.binary_compact_cover [t2_space Î±] {K U V : set Î±} (hK : is_compact K)
  (hU : is_open U) (hV : is_open V) (h2K : K âŠ† U âˆª V) :
  âˆƒ Kâ‚ Kâ‚‚ : set Î±, is_compact Kâ‚ âˆ§ is_compact Kâ‚‚ âˆ§ Kâ‚ âŠ† U âˆ§ Kâ‚‚ âŠ† V âˆ§ K = Kâ‚ âˆª Kâ‚‚ :=
begin
  rcases compact_compact_separated (hK.diff hU) (hK.diff hV)
    (by rwa [diff_inter_diff, diff_eq_empty]) with âŸ¨Oâ‚, Oâ‚‚, h1Oâ‚, h1Oâ‚‚, h2Oâ‚, h2Oâ‚‚, hOâŸ©,
  refine âŸ¨_, _, hK.diff h1Oâ‚, hK.diff h1Oâ‚‚,
    by rwa [diff_subset_comm], by rwa [diff_subset_comm], by rw [â† diff_inter, hO, diff_empty]âŸ©
end

lemma continuous.is_closed_map [compact_space Î±] [t2_space Î²] {f : Î± â†’ Î²} (h : continuous f) :
  is_closed_map f :=
Î» s hs, (hs.is_compact.image h).is_closed

lemma continuous.closed_embedding [compact_space Î±] [t2_space Î²] {f : Î± â†’ Î²} (h : continuous f)
  (hf : function.injective f) : closed_embedding f :=
closed_embedding_of_continuous_injective_closed h hf h.is_closed_map

section
open finset function
/-- For every finite open cover `Uáµ¢` of a compact set, there exists a compact cover `Káµ¢ âŠ† Uáµ¢`. -/
lemma is_compact.finite_compact_cover [t2_space Î±] {s : set Î±} (hs : is_compact s)
  {Î¹} (t : finset Î¹) (U : Î¹ â†’ set Î±) (hU : âˆ€ i âˆˆ t, is_open (U i)) (hsC : s âŠ† â‹ƒ i âˆˆ t, U i) :
  âˆƒ K : Î¹ â†’ set Î±, (âˆ€ i, is_compact (K i)) âˆ§ (âˆ€i, K i âŠ† U i) âˆ§ s = â‹ƒ i âˆˆ t, K i :=
begin
  classical,
  induction t using finset.induction with x t hx ih generalizing U hU s hs hsC,
  { refine âŸ¨Î» _, âˆ…, Î» i, is_compact_empty, Î» i, empty_subset _, _âŸ©,
    simpa only [subset_empty_iff, Union_false, Union_empty] using hsC },
  simp only [finset.set_bUnion_insert] at hsC,
  simp only [finset.mem_insert] at hU,
  have hU' : âˆ€ i âˆˆ t, is_open (U i) := Î» i hi, hU i (or.inr hi),
  rcases hs.binary_compact_cover (hU x (or.inl rfl)) (is_open_bUnion hU') hsC
    with âŸ¨Kâ‚, Kâ‚‚, h1Kâ‚, h1Kâ‚‚, h2Kâ‚, h2Kâ‚‚, hKâŸ©,
  rcases ih U hU' h1Kâ‚‚ h2Kâ‚‚ with âŸ¨K, h1K, h2K, h3KâŸ©,
  refine âŸ¨update K x Kâ‚, _, _, _âŸ©,
  { intros i, by_cases hi : i = x,
    { simp only [update_same, hi, h1Kâ‚] },
    { rw [â† ne.def] at hi, simp only [update_noteq hi, h1K] }},
  { intros i, by_cases hi : i = x,
    { simp only [update_same, hi, h2Kâ‚] },
    { rw [â† ne.def] at hi, simp only [update_noteq hi, h2K] }},
  { simp only [set_bUnion_insert_update _ hx, hK, h3K] }
end
end

lemma locally_compact_of_compact_nhds [t2_space Î±] (h : âˆ€ x : Î±, âˆƒ s, s âˆˆ ğ“ x âˆ§ is_compact s) :
  locally_compact_space Î± :=
âŸ¨assume x n hn,
  let âŸ¨u, un, uo, xuâŸ© := mem_nhds_iff.mp hn in
  let âŸ¨k, kx, kcâŸ© := h x in
  -- K is compact but not necessarily contained in N.
  -- K \ U is again compact and doesn't contain x, so
  -- we may find open sets V, W separating x from K \ U.
  -- Then K \ W is a compact neighborhood of x contained in U.
  let âŸ¨v, w, vo, wo, xv, kuw, vwâŸ© :=
    compact_compact_separated is_compact_singleton (is_compact.diff kc uo)
      (by rw [singleton_inter_eq_empty]; exact Î» h, h.2 xu) in
  have wn : wá¶œ âˆˆ ğ“ x, from
   mem_nhds_iff.mpr
     âŸ¨v, subset_compl_iff_disjoint.mpr vw, vo, singleton_subset_iff.mp xvâŸ©,
  âŸ¨k \ w,
   filter.inter_mem kx wn,
   subset.trans (diff_subset_comm.mp kuw) un,
   kc.diff woâŸ©âŸ©

@[priority 100] -- see Note [lower instance priority]
instance locally_compact_of_compact [t2_space Î±] [compact_space Î±] : locally_compact_space Î± :=
locally_compact_of_compact_nhds (assume x, âŸ¨univ, is_open_univ.mem_nhds trivial, compact_univâŸ©)

/-- In a locally compact Tâ‚‚ space, every point has an open neighborhood with compact closure -/
lemma exists_open_with_compact_closure [locally_compact_space Î±] [t2_space Î±] (x : Î±) :
  âˆƒ (U : set Î±), is_open U âˆ§ x âˆˆ U âˆ§ is_compact (closure U) :=
begin
  rcases exists_compact_mem_nhds x with âŸ¨K, hKc, hxKâŸ©,
  rcases mem_nhds_iff.1 hxK with âŸ¨t, h1t, h2t, h3tâŸ©,
  exact âŸ¨t, h2t, h3t, compact_closure_of_subset_compact hKc h1tâŸ©
end

/--
In a locally compact Tâ‚‚ space, every compact set has an open neighborhood with compact closure.
-/
lemma exists_open_superset_and_is_compact_closure [locally_compact_space Î±] [t2_space Î±]
  {K : set Î±} (hK : is_compact K) : âˆƒ V, is_open V âˆ§ K âŠ† V âˆ§ is_compact (closure V) :=
begin
  rcases exists_compact_superset hK with âŸ¨K', hK', hKK'âŸ©,
  refine âŸ¨interior K', is_open_interior, hKK',
    compact_closure_of_subset_compact hK' interior_subsetâŸ©,
end

lemma is_preirreducible_iff_subsingleton [t2_space Î±] (S : set Î±) :
  is_preirreducible S â†” set.subsingleton S :=
begin
  split,
  { intros h x hx y hy,
    by_contradiction e,
    obtain âŸ¨U, V, hU, hV, hxU, hyV, h'âŸ© := t2_separation e,
    have := h U V hU hV âŸ¨x, hx, hxUâŸ© âŸ¨y, hy, hyVâŸ©,
    rw [h', inter_empty] at this,
    exact this.some_spec },
  { exact set.subsingleton.is_preirreducible }
end

lemma is_irreducible_iff_singleton [t2_space Î±] (S : set Î±) :
  is_irreducible S â†” âˆƒ x, S = {x} :=
by rw [is_irreducible, is_preirreducible_iff_subsingleton,
  exists_eq_singleton_iff_nonempty_subsingleton]

end separation

section regularity

/-- A Tâ‚ƒ space, also known as a regular space (although this condition sometimes
  omits Tâ‚‚), is one in which for every closed `C` and `x âˆ‰ C`, there exist
  disjoint open sets containing `x` and `C` respectively. -/
class regular_space (Î± : Type u) [topological_space Î±] extends t0_space Î± : Prop :=
(regular : âˆ€{s:set Î±} {a}, is_closed s â†’ a âˆ‰ s â†’ âˆƒt, is_open t âˆ§ s âŠ† t âˆ§ ğ“[t] a = âŠ¥)

@[priority 100] -- see Note [lower instance priority]
instance regular_space.t1_space [regular_space Î±] : t1_space Î± :=
begin
  rw t1_space_iff_exists_open,
  intros x y hxy,
  obtain âŸ¨U, hU, hâŸ© := t0_space.t0 x y hxy,
  cases h,
  { exact âŸ¨U, hU, hâŸ© },
  { obtain âŸ¨R, hR, hhâŸ© := regular_space.regular (is_closed_compl_iff.mpr hU) (not_not.mpr h.1),
    obtain âŸ¨V, hV, hhhâŸ© := mem_nhds_iff.1 (filter.inf_principal_eq_bot.1 hh.2),
    exact âŸ¨R, hR, hh.1 (mem_compl h.2), hV hhh.2âŸ© }
end

lemma nhds_is_closed [regular_space Î±] {a : Î±} {s : set Î±} (h : s âˆˆ ğ“ a) :
  âˆƒ t âˆˆ ğ“ a, t âŠ† s âˆ§ is_closed t :=
let âŸ¨s', hâ‚, hâ‚‚, hâ‚ƒâŸ© := mem_nhds_iff.mp h in
have âˆƒt, is_open t âˆ§ s'á¶œ âŠ† t âˆ§ ğ“[t] a = âŠ¥,
  from regular_space.regular hâ‚‚.is_closed_compl (not_not_intro hâ‚ƒ),
let âŸ¨t, htâ‚, htâ‚‚, htâ‚ƒâŸ© := this in
âŸ¨tá¶œ,
  mem_of_eq_bot $ by rwa [compl_compl],
  subset.trans (compl_subset_comm.1 htâ‚‚) hâ‚,
  is_closed_compl_iff.mpr htâ‚âŸ©

lemma closed_nhds_basis [regular_space Î±] (a : Î±) :
  (ğ“ a).has_basis (Î» s : set Î±, s âˆˆ ğ“ a âˆ§ is_closed s) id :=
âŸ¨Î» t, âŸ¨Î» t_in, let âŸ¨s, s_in, h_st, hâŸ© := nhds_is_closed t_in in âŸ¨s, âŸ¨s_in, hâŸ©, h_stâŸ©,
       Î» âŸ¨s, âŸ¨s_in, hsâŸ©, hstâŸ©, mem_of_superset s_in hstâŸ©âŸ©

lemma topological_space.is_topological_basis.exists_closure_subset [regular_space Î±]
  {B : set (set Î±)} (hB : topological_space.is_topological_basis B) {a : Î±} {s : set Î±}
  (h : s âˆˆ ğ“ a) :
  âˆƒ t âˆˆ B, a âˆˆ t âˆ§ closure t âŠ† s :=
begin
  rcases nhds_is_closed h with âŸ¨t, hat, hts, htcâŸ©,
  rcases hB.mem_nhds_iff.1 hat with âŸ¨u, huB, hau, hutâŸ©,
  exact âŸ¨u, huB, hau, (closure_minimal hut htc).trans htsâŸ©
end

lemma topological_space.is_topological_basis.nhds_basis_closure [regular_space Î±]
  {B : set (set Î±)} (hB : topological_space.is_topological_basis B) (a : Î±) :
  (ğ“ a).has_basis (Î» s : set Î±, a âˆˆ s âˆ§ s âˆˆ B) closure :=
âŸ¨Î» s, âŸ¨Î» h, let âŸ¨t, htB, hat, htsâŸ© := hB.exists_closure_subset h in âŸ¨t, âŸ¨hat, htBâŸ©, htsâŸ©,
  Î» âŸ¨t, âŸ¨hat, htBâŸ©, htsâŸ©, mem_of_superset (hB.mem_nhds htB hat) (subset_closure.trans hts)âŸ©âŸ©

protected lemma embedding.regular_space [topological_space Î²] [regular_space Î²] {f : Î± â†’ Î²}
  (hf : embedding f) : regular_space Î± :=
{ to_t0_space := hf.t0_space,
  regular :=
  begin
    intros s a hs ha,
    rcases hf.to_inducing.is_closed_iff.1 hs with âŸ¨s, hs', rflâŸ©,
    rcases regular_space.regular hs' ha with âŸ¨t, ht, hst, hatâŸ©,
    refine âŸ¨f â»Â¹' t, ht.preimage hf.continuous, preimage_mono hst, _âŸ©,
    rw [nhds_within, hf.to_inducing.nhds_eq_comap, â† comap_principal, â† comap_inf,
        â† nhds_within, hat, comap_bot]
  end }

instance subtype.regular_space [regular_space Î±] {p : Î± â†’ Prop} : regular_space (subtype p) :=
embedding_subtype_coe.regular_space

variable (Î±)
@[priority 100] -- see Note [lower instance priority]
instance regular_space.t2_space [regular_space Î±] : t2_space Î± :=
âŸ¨Î» x y hxy,
let âŸ¨s, hs, hys, hxsâŸ© := regular_space.regular is_closed_singleton
    (mt mem_singleton_iff.1 hxy),
  âŸ¨t, hxt, u, hsu, htuâŸ© := empty_mem_iff_bot.2 hxs,
  âŸ¨v, hvt, hv, hxvâŸ© := mem_nhds_iff.1 hxt in
âŸ¨v, s, hv, hs, hxv, singleton_subset_iff.1 hys,
eq_empty_of_subset_empty $ Î» z âŸ¨hzv, hzsâŸ©, by { rw htu, exact âŸ¨hvt hzv, hsu hzsâŸ© }âŸ©âŸ©

@[priority 100] -- see Note [lower instance priority]
instance regular_space.t2_5_space [regular_space Î±] : t2_5_space Î± :=
âŸ¨Î» x y hxy,
let âŸ¨U, V, hU, hV, hh_1, hh_2, hUVâŸ© := t2_space.t2 x y hxy,
  hxcV := not_not.mpr ((interior_maximal (subset_compl_iff_disjoint.mpr hUV) hU) hh_1),
  âŸ¨R, hR, hhâŸ© := regular_space.regular is_closed_closure (by rwa closure_eq_compl_interior_compl),
  âŸ¨A, hA, hhhâŸ© := mem_nhds_iff.1 (filter.inf_principal_eq_bot.1 hh.2) in
âŸ¨A, V, hhh.1, hV, subset_eq_empty ((closure V).inter_subset_inter_left
  (subset.trans (closure_minimal hA (is_closed_compl_iff.mpr hR)) (compl_subset_compl.mpr hh.1)))
  (compl_inter_self (closure V)), hhh.2, hh_2âŸ©âŸ©

variable {Î±}

/-- Given two points `x â‰  y`, we can find neighbourhoods `x âˆˆ Vâ‚ âŠ† Uâ‚` and `y âˆˆ Vâ‚‚ âŠ† Uâ‚‚`,
with the `Vâ‚–` closed and the `Uâ‚–` open, such that the `Uâ‚–` are disjoint. -/
lemma disjoint_nested_nhds [regular_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ (Uâ‚ Vâ‚ âˆˆ ğ“ x) (Uâ‚‚ Vâ‚‚ âˆˆ ğ“ y), is_closed Vâ‚ âˆ§ is_closed Vâ‚‚ âˆ§ is_open Uâ‚ âˆ§ is_open Uâ‚‚ âˆ§
  Vâ‚ âŠ† Uâ‚ âˆ§ Vâ‚‚ âŠ† Uâ‚‚ âˆ§ Uâ‚ âˆ© Uâ‚‚ = âˆ… :=
begin
  rcases t2_separation h with âŸ¨Uâ‚, Uâ‚‚, Uâ‚_op, Uâ‚‚_op, x_in, y_in, HâŸ©,
  rcases nhds_is_closed (is_open.mem_nhds Uâ‚_op x_in) with âŸ¨Vâ‚, Vâ‚_in, hâ‚, Vâ‚_closedâŸ©,
  rcases nhds_is_closed (is_open.mem_nhds Uâ‚‚_op y_in) with âŸ¨Vâ‚‚, Vâ‚‚_in, hâ‚‚, Vâ‚‚_closedâŸ©,
  use [Uâ‚, mem_of_superset Vâ‚_in hâ‚, Vâ‚, Vâ‚_in,
       Uâ‚‚, mem_of_superset Vâ‚‚_in hâ‚‚, Vâ‚‚, Vâ‚‚_in],
  tauto
end

/--
In a locally compact regular space, given a compact set `K` inside an open set `U`, we can find a
compact set `K'` between these sets: `K` is inside the interior of `K'` and `K' âŠ† U`.
-/
lemma exists_compact_between [locally_compact_space Î±] [regular_space Î±]
  {K U : set Î±} (hK : is_compact K) (hU : is_open U) (hKU : K âŠ† U) :
  âˆƒ K', is_compact K' âˆ§ K âŠ† interior K' âˆ§ K' âŠ† U :=
begin
  choose C hxC hCU hC using Î» x : K, nhds_is_closed (hU.mem_nhds $ hKU x.2),
  choose L hL hxL using Î» x : K, exists_compact_mem_nhds (x : Î±),
  have : K âŠ† â‹ƒ x, interior (L x) âˆ© interior (C x), from
  Î» x hx, mem_Union.mpr âŸ¨âŸ¨x, hxâŸ©,
    âŸ¨mem_interior_iff_mem_nhds.mpr (hxL _), mem_interior_iff_mem_nhds.mpr (hxC _)âŸ©âŸ©,
  rcases hK.elim_finite_subcover _ _ this with âŸ¨t, htâŸ©,
  { refine âŸ¨â‹ƒ x âˆˆ t, L x âˆ© C x, t.compact_bUnion (Î» x _, (hL x).inter_right (hC x)), Î» x hx, _, _âŸ©,
    { obtain âŸ¨y, hyt, hy : x âˆˆ interior (L y) âˆ© interior (C y)âŸ© := mem_Unionâ‚‚.mp (ht hx),
      rw [â† interior_inter] at hy,
      refine interior_mono (subset_bUnion_of_mem hyt) hy },
    { simp_rw [Union_subset_iff], rintro x -, exact (inter_subset_right _ _).trans (hCU _) } },
  { exact Î» _, is_open_interior.inter is_open_interior }
end

/--
In a locally compact regular space, given a compact set `K` inside an open set `U`, we can find a
open set `V` between these sets with compact closure: `K âŠ† V` and the closure of `V` is inside `U`.
-/
lemma exists_open_between_and_is_compact_closure [locally_compact_space Î±] [regular_space Î±]
  {K U : set Î±} (hK : is_compact K) (hU : is_open U) (hKU : K âŠ† U) :
  âˆƒ V, is_open V âˆ§ K âŠ† V âˆ§ closure V âŠ† U âˆ§ is_compact (closure V) :=
begin
  rcases exists_compact_between hK hU hKU with âŸ¨V, hV, hKV, hVUâŸ©,
  refine âŸ¨interior V, is_open_interior, hKV,
    (closure_minimal interior_subset hV.is_closed).trans hVU,
    compact_closure_of_subset_compact hV interior_subsetâŸ©,
end

end regularity

section normality

/-- A Tâ‚„ space, also known as a normal space (although this condition sometimes
  omits Tâ‚‚), is one in which for every pair of disjoint closed sets `C` and `D`,
  there exist disjoint open sets containing `C` and `D` respectively. -/
class normal_space (Î± : Type u) [topological_space Î±] extends t1_space Î± : Prop :=
(normal : âˆ€ s t : set Î±, is_closed s â†’ is_closed t â†’ disjoint s t â†’
  âˆƒ u v, is_open u âˆ§ is_open v âˆ§ s âŠ† u âˆ§ t âŠ† v âˆ§ disjoint u v)

theorem normal_separation [normal_space Î±] {s t : set Î±}
  (H1 : is_closed s) (H2 : is_closed t) (H3 : disjoint s t) :
  âˆƒ u v, is_open u âˆ§ is_open v âˆ§ s âŠ† u âˆ§ t âŠ† v âˆ§ disjoint u v :=
normal_space.normal s t H1 H2 H3

theorem normal_exists_closure_subset [normal_space Î±] {s t : set Î±} (hs : is_closed s)
  (ht : is_open t) (hst : s âŠ† t) :
  âˆƒ u, is_open u âˆ§ s âŠ† u âˆ§ closure u âŠ† t :=
begin
  have : disjoint s tá¶œ, from Î» x âŸ¨hxs, hxtâŸ©, hxt (hst hxs),
  rcases normal_separation hs (is_closed_compl_iff.2 ht) this
    with âŸ¨s', t', hs', ht', hss', htt', hs't'âŸ©,
  refine âŸ¨s', hs', hss',
    subset.trans (closure_minimal _ (is_closed_compl_iff.2 ht')) (compl_subset_comm.1 htt')âŸ©,
  exact Î» x hxs hxt, hs't' âŸ¨hxs, hxtâŸ©
end

@[priority 100] -- see Note [lower instance priority]
instance normal_space.regular_space [normal_space Î±] : regular_space Î± :=
{ regular := Î» s x hs hxs, let âŸ¨u, v, hu, hv, hsu, hxv, huvâŸ© :=
    normal_separation hs is_closed_singleton
      (Î» _ âŸ¨hx, hyâŸ©, hxs $ mem_of_eq_of_mem (eq_of_mem_singleton hy).symm hx) in
    âŸ¨u, hu, hsu, filter.empty_mem_iff_bot.1 $ filter.mem_inf_iff.2
      âŸ¨v, is_open.mem_nhds hv (singleton_subset_iff.1 hxv), u, filter.mem_principal_self u,
       by rwa [eq_comm, inter_comm, â† disjoint_iff_inter_eq_empty]âŸ©âŸ© }

-- We can't make this an instance because it could cause an instance loop.
lemma normal_of_compact_t2 [compact_space Î±] [t2_space Î±] : normal_space Î± :=
begin
  refine âŸ¨assume s t hs ht st, _âŸ©,
  simp only [disjoint_iff],
  exact compact_compact_separated hs.is_compact ht.is_compact st.eq_bot
end

protected lemma closed_embedding.normal_space [topological_space Î²] [normal_space Î²] {f : Î± â†’ Î²}
  (hf : closed_embedding f) : normal_space Î± :=
{ to_t1_space := hf.to_embedding.t1_space,
  normal :=
  begin
    intros s t hs ht hst,
    rcases normal_space.normal (f '' s) (f '' t) (hf.is_closed_map s hs) (hf.is_closed_map t ht)
      (disjoint_image_of_injective hf.inj hst) with âŸ¨u, v, hu, hv, hsu, htv, huvâŸ©,
    rw image_subset_iff at hsu htv,
    exact âŸ¨f â»Â¹' u, f â»Â¹' v, hu.preimage hf.continuous, hv.preimage hf.continuous,
            hsu, htv, huv.preimage fâŸ©
  end }

variable (Î±)

/-- A regular topological space with second countable topology is a normal space.
This lemma is not an instance to avoid a loop. -/
lemma normal_space_of_regular_second_countable [second_countable_topology Î±] [regular_space Î±] :
  normal_space Î± :=
begin
  have key : âˆ€ {s t : set Î±}, is_closed t â†’ disjoint s t â†’
    âˆƒ U : set (countable_basis Î±), (s âŠ† â‹ƒ u âˆˆ U, â†‘u) âˆ§
      (âˆ€ u âˆˆ U, disjoint (closure â†‘u) t) âˆ§
      âˆ€ n : â„•, is_closed (â‹ƒ (u âˆˆ U) (h : encodable.encode u â‰¤ n), closure (u : set Î±)),
  { intros s t hc hd,
    rw disjoint_left at hd,
    have : âˆ€ x âˆˆ s, âˆƒ U âˆˆ countable_basis Î±, x âˆˆ U âˆ§ disjoint (closure U) t,
    { intros x hx,
      rcases (is_basis_countable_basis Î±).exists_closure_subset (hc.is_open_compl.mem_nhds (hd hx))
        with âŸ¨u, hu, hxu, hutâŸ©,
      exact âŸ¨u, hu, hxu, disjoint_left.2 hutâŸ© },
    choose! U hu hxu hd,
    set V : s â†’ countable_basis Î± := maps_to.restrict _ _ _ hu,
    refine âŸ¨range V, _, forall_range_iff.2 $ subtype.forall.2 hd, Î» n, _âŸ©,
    { rw bUnion_range,
      exact Î» x hx, mem_Union.2 âŸ¨âŸ¨x, hxâŸ©, hxu x hxâŸ© },
    { simp only [â† supr_eq_Union, supr_and'],
      exact is_closed_bUnion (((finite_le_nat n).preimage_embedding (encodable.encode' _)).subset $
        inter_subset_right _ _) (Î» u hu, is_closed_closure) } },
  refine âŸ¨Î» s t hs ht hd, _âŸ©,
  rcases key ht hd with âŸ¨U, hsU, hUd, hUcâŸ©,
  rcases key hs hd.symm with âŸ¨V, htV, hVd, hVcâŸ©,
  refine âŸ¨â‹ƒ u âˆˆ U, â†‘u \ â‹ƒ (v âˆˆ V) (hv : encodable.encode v â‰¤ encodable.encode u), closure â†‘v,
    â‹ƒ v âˆˆ V, â†‘v \ â‹ƒ (u âˆˆ U) (hu : encodable.encode u â‰¤ encodable.encode v), closure â†‘u,
    is_open_bUnion $ Î» u hu, (is_open_of_mem_countable_basis u.2).sdiff (hVc _),
    is_open_bUnion $ Î» v hv, (is_open_of_mem_countable_basis v.2).sdiff (hUc _),
    Î» x hx, _, Î» x hx, _, _âŸ©,
  { rcases mem_Unionâ‚‚.1 (hsU hx) with âŸ¨u, huU, hxuâŸ©,
    refine mem_bUnion huU âŸ¨hxu, _âŸ©,
    simp only [mem_Union],
    rintro âŸ¨v, hvV, -, hxvâŸ©,
    exact hVd v hvV âŸ¨hxv, hxâŸ© },
  { rcases mem_Unionâ‚‚.1 (htV hx) with âŸ¨v, hvV, hxvâŸ©,
    refine mem_bUnion hvV âŸ¨hxv, _âŸ©,
    simp only [mem_Union],
    rintro âŸ¨u, huU, -, hxuâŸ©,
    exact hUd u huU âŸ¨hxu, hxâŸ© },
  { simp only [disjoint_left, mem_Union, mem_diff, not_exists, not_and, not_forall, not_not],
    rintro a âŸ¨u, huU, hau, haVâŸ© v hvV hav,
    cases le_total (encodable.encode u) (encodable.encode v) with hle hle,
    exacts [âŸ¨u, huU, hle, subset_closure hauâŸ©, (haV _ hvV hle $ subset_closure hav).elim] }
end

end normality

/-- In a compact t2 space, the connected component of a point equals the intersection of all
its clopen neighbourhoods. -/
lemma connected_component_eq_Inter_clopen [t2_space Î±] [compact_space Î±] (x : Î±) :
  connected_component x = â‹‚ Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}, Z :=
begin
  apply eq_of_subset_of_subset connected_component_subset_Inter_clopen,
  -- Reduce to showing that the clopen intersection is connected.
  refine is_preconnected.subset_connected_component _ (mem_Inter.2 (Î» Z, Z.2.2)),
  -- We do this by showing that any disjoint cover by two closed sets implies
  -- that one of these closed sets must contain our whole thing.
  -- To reduce to the case where the cover is disjoint on all of `Î±` we need that `s` is closed
  have hs : @is_closed _ _inst_1 (â‹‚ (Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}), Z) :=
    is_closed_Inter (Î» Z, Z.2.1.2),
  rw (is_preconnected_iff_subset_of_fully_disjoint_closed hs),
  intros a b ha hb hab ab_empty,
  haveI := @normal_of_compact_t2 Î± _ _ _,
  -- Since our space is normal, we get two larger disjoint open sets containing the disjoint
  -- closed sets. If we can show that our intersection is a subset of any of these we can then
  -- "descend" this to show that it is a subset of either a or b.
  rcases normal_separation ha hb (disjoint_iff.2 ab_empty) with âŸ¨u, v, hu, hv, hau, hbv, huvâŸ©,
  -- If we can find a clopen set around x, contained in u âˆª v, we get a disjoint decomposition
  -- Z = Z âˆ© u âˆª Z âˆ© v of clopen sets. The intersection of all clopen neighbourhoods will then lie
  -- in whichever of u or v x lies in and hence will be a subset of either a or b.
  suffices : âˆƒ (Z : set Î±), is_clopen Z âˆ§ x âˆˆ Z âˆ§ Z âŠ† u âˆª v,
  { cases this with Z H,
    rw [disjoint_iff_inter_eq_empty] at huv,
    have H1 := is_clopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hu hv huv,
    rw [union_comm] at H,
    have H2 := is_clopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hv hu (inter_comm u v â–¸ huv),
    by_cases (x âˆˆ u),
    -- The x âˆˆ u case.
    { left,
      suffices : (â‹‚ (Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) âŠ† u,
      { rw â†set.disjoint_iff_inter_eq_empty at huv,
        replace hab : (â‹‚ (Z : {Z // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) â‰¤ a âˆª b := hab,
        replace this : (â‹‚ (Z : {Z // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) â‰¤ u := this,
        exact disjoint.left_le_of_le_sup_right hab (huv.mono this hbv) },
      { apply subset.trans _ (inter_subset_right Z u),
        apply Inter_subset (Î» Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}, â†‘Z)
          âŸ¨Z âˆ© u, H1, mem_inter H.2.1 hâŸ© } },
    -- If x âˆ‰ u, we get x âˆˆ v since x âˆˆ u âˆª v. The rest is then like the x âˆˆ u case.
    have h1 : x âˆˆ v,
    { cases (mem_union x u v).1 (mem_of_subset_of_mem (subset.trans hab
        (union_subset_union hau hbv)) (mem_Inter.2 (Î» i, i.2.2))) with h1 h1,
      { exfalso, exact h h1},
      { exact h1} },
    right,
    suffices : (â‹‚ (Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) âŠ† v,
    { rw [inter_comm, â†set.disjoint_iff_inter_eq_empty] at huv,
      replace hab : (â‹‚ (Z : {Z // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) â‰¤ a âˆª b := hab,
      replace this : (â‹‚ (Z : {Z // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) â‰¤ v := this,
      exact disjoint.left_le_of_le_sup_left hab (huv.mono this hau) },
    { apply subset.trans _ (inter_subset_right Z v),
      apply Inter_subset (Î» Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}, â†‘Z)
        âŸ¨Z âˆ© v, H2, mem_inter H.2.1 h1âŸ© } },
  -- Now we find the required Z. We utilize the fact that X \ u âˆª v will be compact,
  -- so there must be some finite intersection of clopen neighbourhoods of X disjoint to it,
  -- but a finite intersection of clopen sets is clopen so we let this be our Z.
  have H1 := ((is_closed_compl_iff.2 (hu.union hv)).is_compact.inter_Inter_nonempty
    (Î» Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}, Z) (Î» Z, Z.2.1.2)),
  rw [â†not_imp_not, not_forall, not_nonempty_iff_eq_empty, inter_comm] at H1,
  have huv_union := subset.trans hab (union_subset_union hau hbv),
  rw [â† compl_compl (u âˆª v), subset_compl_iff_disjoint] at huv_union,
  cases H1 huv_union with Zi H2,
  refine âŸ¨(â‹‚ (U âˆˆ Zi), subtype.val U), _, _, _âŸ©,
  { exact is_clopen_bInter (Î» Z hZ, Z.2.1) },
  { exact mem_Interâ‚‚.2 (Î» Z hZ, Z.2.2) },
  { rwa [not_nonempty_iff_eq_empty, inter_comm, â†subset_compl_iff_disjoint, compl_compl] at H2 }
end

section profinite

variables [t2_space Î±]

/-- A Hausdorff space with a clopen basis is totally separated. -/
lemma tot_sep_of_zero_dim (h : is_topological_basis {s : set Î± | is_clopen s}) :
  totally_separated_space Î± :=
begin
  constructor,
  rintros x - y - hxy,
  obtain âŸ¨u, v, hu, hv, xu, yv, disjâŸ© := t2_separation hxy,
  obtain âŸ¨w, hw : is_clopen w, xw, wuâŸ© := (is_topological_basis.mem_nhds_iff h).1
    (is_open.mem_nhds hu xu),
  refine âŸ¨w, wá¶œ, hw.1, (is_clopen_compl_iff.2 hw).1, xw, _, _, set.inter_compl_self wâŸ©,
  { intro h,
    have : y âˆˆ u âˆ© v := âŸ¨wu h, yvâŸ©,
    rwa disj at this },
  rw set.union_compl_self,
end

variables [compact_space Î±]

/-- A compact Hausdorff space is totally disconnected if and only if it is totally separated, this
  is also true for locally compact spaces. -/
theorem compact_t2_tot_disc_iff_tot_sep :
  totally_disconnected_space Î± â†” totally_separated_space Î± :=
begin
  split,
  { intro h, constructor,
    rintros x - y -,
    contrapose!,
    intros hyp,
    suffices : x âˆˆ connected_component y,
      by simpa [totally_disconnected_space_iff_connected_component_singleton.1 h y,
                mem_singleton_iff],
    rw [connected_component_eq_Inter_clopen, mem_Inter],
    rintro âŸ¨w : set Î±, hw : is_clopen w, hy : y âˆˆ wâŸ©,
    by_contra hx,
    simpa using hyp wá¶œ w (is_open_compl_iff.mpr hw.2) hw.1 hx hy },
  apply totally_separated_space.totally_disconnected_space,
end

variables [totally_disconnected_space Î±]

lemma nhds_basis_clopen (x : Î±) : (ğ“ x).has_basis (Î» s : set Î±, x âˆˆ s âˆ§ is_clopen s) id :=
âŸ¨Î» U, begin
  split,
  { have : connected_component x = {x},
      from totally_disconnected_space_iff_connected_component_singleton.mp â€¹_â€º x,
    rw connected_component_eq_Inter_clopen at this,
    intros hU,
    let N := {Z // is_clopen Z âˆ§ x âˆˆ Z},
    suffices : âˆƒ Z : N, Z.val âŠ† U,
    { rcases this with âŸ¨âŸ¨s, hs, hs'âŸ©, hs''âŸ©,
      exact âŸ¨s, âŸ¨hs', hsâŸ©, hs''âŸ© },
    haveI : nonempty N := âŸ¨âŸ¨univ, is_clopen_univ, mem_univ xâŸ©âŸ©,
    have hNcl : âˆ€ Z : N, is_closed Z.val := (Î» Z, Z.property.1.2),
    have hdir : directed superset (Î» Z : N, Z.val),
    { rintros âŸ¨s, hs, hxsâŸ© âŸ¨t, ht, hxtâŸ©,
      exact âŸ¨âŸ¨s âˆ© t, hs.inter ht, âŸ¨hxs, hxtâŸ©âŸ©, inter_subset_left s t, inter_subset_right s tâŸ© },
    have h_nhd: âˆ€ y âˆˆ (â‹‚ Z : N, Z.val), U âˆˆ ğ“ y,
    { intros y y_in,
      erw [this, mem_singleton_iff] at y_in,
      rwa y_in },
    exact exists_subset_nhd_of_compact_space hdir hNcl h_nhd },
  { rintro âŸ¨V, âŸ¨hxV, V_op, -âŸ©, hUV : V âŠ† UâŸ©,
    rw mem_nhds_iff,
    exact âŸ¨V, hUV, V_op, hxVâŸ© }
endâŸ©

lemma is_topological_basis_clopen : is_topological_basis {s : set Î± | is_clopen s} :=
begin
  apply is_topological_basis_of_open_of_nhds (Î» U (hU : is_clopen U), hU.1),
  intros x U hxU U_op,
  have : U âˆˆ ğ“ x,
  from is_open.mem_nhds U_op hxU,
  rcases (nhds_basis_clopen x).mem_iff.mp this with âŸ¨V, âŸ¨hxV, hVâŸ©, hVU : V âŠ† UâŸ©,
  use V,
  tauto
end

/-- Every member of an open set in a compact Hausdorff totally disconnected space
  is contained in a clopen set contained in the open set.  -/
lemma compact_exists_clopen_in_open {x : Î±} {U : set Î±} (is_open : is_open U) (memU : x âˆˆ U) :
    âˆƒ (V : set Î±) (hV : is_clopen V), x âˆˆ V âˆ§ V âŠ† U :=
  (is_topological_basis.mem_nhds_iff is_topological_basis_clopen).1 (is_open.mem_nhds memU)

end profinite

section locally_compact

variables {H : Type*} [topological_space H] [locally_compact_space H] [t2_space H]

/-- A locally compact Hausdorff totally disconnected space has a basis with clopen elements. -/
lemma loc_compact_Haus_tot_disc_of_zero_dim [totally_disconnected_space H] :
  is_topological_basis {s : set H | is_clopen s} :=
begin
  refine is_topological_basis_of_open_of_nhds (Î» u hu, hu.1) _,
  rintros x U memU hU,
  obtain âŸ¨s, comp, xs, sUâŸ© := exists_compact_subset hU memU,
  obtain âŸ¨t, h, ht, xtâŸ© := mem_interior.1 xs,
  let u : set s := (coe : s â†’ H)â»Â¹' (interior s),
  have u_open_in_s : is_open u := is_open_interior.preimage continuous_subtype_coe,
  let X : s := âŸ¨x, h xtâŸ©,
  have Xu : X âˆˆ u := xs,
  haveI : compact_space s := is_compact_iff_compact_space.1 comp,
  obtain âŸ¨V : set s, clopen_in_s, Vx, V_subâŸ© := compact_exists_clopen_in_open u_open_in_s Xu,
  have V_clopen : is_clopen ((coe : s â†’ H) '' V),
  { refine âŸ¨_, (comp.is_closed.closed_embedding_subtype_coe.closed_iff_image_closed).1
               clopen_in_s.2âŸ©,
    let v : set u := (coe : u â†’ s)â»Â¹' V,
    have : (coe : u â†’ H) = (coe : s â†’ H) âˆ˜ (coe : u â†’ s) := rfl,
    have f0 : embedding (coe : u â†’ H) := embedding_subtype_coe.comp embedding_subtype_coe,
    have f1 : open_embedding (coe : u â†’ H),
    { refine âŸ¨f0, _âŸ©,
      { have : set.range (coe : u â†’ H) = interior s,
        { rw [this, set.range_comp, subtype.range_coe, subtype.image_preimage_coe],
          apply set.inter_eq_self_of_subset_left interior_subset, },
        rw this,
        apply is_open_interior } },
    have f2 : is_open v := clopen_in_s.1.preimage continuous_subtype_coe,
    have f3 : (coe : s â†’ H) '' V = (coe : u â†’ H) '' v,
    { rw [this, image_comp coe coe, subtype.image_preimage_coe,
          inter_eq_self_of_subset_left V_sub] },
    rw f3,
    apply f1.is_open_map v f2 },
  refine âŸ¨coe '' V, V_clopen, by simp [Vx, h xt], _âŸ©,
  transitivity s,
  { simp },
  assumption
end

/-- A locally compact Hausdorff space is totally disconnected
  if and only if it is totally separated. -/
theorem loc_compact_t2_tot_disc_iff_tot_sep :
  totally_disconnected_space H â†” totally_separated_space H :=
begin
  split,
  { introI h,
    exact tot_sep_of_zero_dim loc_compact_Haus_tot_disc_of_zero_dim, },
  apply totally_separated_space.totally_disconnected_space,
end

end locally_compact

/-- `connected_components Î±` is Hausdorff when `Î±` is Hausdorff and compact -/
instance connected_components.t2 [t2_space Î±] [compact_space Î±] :
  t2_space (connected_components Î±) :=
begin
  -- Proof follows that of: https://stacks.math.columbia.edu/tag/0900
  -- Fix 2 distinct connected components, with points a and b
  refine âŸ¨connected_components.surjective_coe.forallâ‚‚.2 $ Î» a b ne, _âŸ©,
  rw connected_components.coe_ne_coe at ne,
  have h := connected_component_disjoint ne,
  -- write â†‘b as the intersection of all clopen subsets containing it
  rw [connected_component_eq_Inter_clopen b, disjoint_iff_inter_eq_empty] at h,
  -- Now we show that this can be reduced to some clopen containing `â†‘b` being disjoint to `â†‘a`
  obtain âŸ¨U, V, hU, ha, hb, rflâŸ© : âˆƒ (U : set Î±) (V : set (connected_components Î±)), is_clopen U âˆ§
    connected_component a âˆ© U = âˆ… âˆ§ connected_component b âŠ† U âˆ§ coe â»Â¹' V = U,
  { cases is_closed_connected_component.is_compact.elim_finite_subfamily_closed _ _ h with fin_a ha,
    swap, { exact Î» Z, Z.2.1.2 },
    -- This clopen and its complement will separate the connected components of `a` and `b`
    set U : set Î± := (â‹‚ (i : {Z // is_clopen Z âˆ§ b âˆˆ Z}) (H : i âˆˆ fin_a), i),
    have hU : is_clopen U := is_clopen_bInter (Î» i j, i.2.1),
    exact âŸ¨U, coe '' U, hU, ha, subset_Interâ‚‚ (Î» Z _, Z.2.1.connected_component_subset Z.2.2),
      (connected_components_preimage_image U).symm â–¸ hU.bUnion_connected_component_eqâŸ© },
  rw connected_components.quotient_map_coe.is_clopen_preimage at hU,
  refine âŸ¨Vá¶œ, V, hU.compl.is_open, hU.is_open, _, hb mem_connected_component, compl_inter_self _âŸ©,
  exact Î» h, flip set.nonempty.ne_empty ha âŸ¨a, mem_connected_component, hâŸ©,
end
