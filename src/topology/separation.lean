/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import topology.subset_properties
import topology.connected
import topology.nhds_set
import topology.inseparable

/-!
# Separation properties of topological spaces.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the predicate `separated_nhds`, and common separation axioms
(under the Kolmogorov classification).

## Main definitions

* `separated_nhds`: Two `set`s are separated by neighbourhoods if they are contained in disjoint
  open sets.
* `t0_space`: A Tâ‚€/Kolmogorov space is a space where, for every two points `x â‰  y`,
  there is an open set that contains one, but not the other.
* `t1_space`: A Tâ‚/FrÃ©chet space is a space where every singleton set is closed.
  This is equivalent to, for every pair `x â‰  y`, there existing an open set containing `x`
  but not `y` (`t1_space_iff_exists_open` shows that these conditions are equivalent.)
* `t2_space`: A Tâ‚‚/Hausdorff space is a space where, for every two points `x â‰  y`,
  there is two disjoint open sets, one containing `x`, and the other `y`.
* `t2_5_space`: A Tâ‚‚.â‚…/Urysohn space is a space where, for every two points `x â‰  y`,
  there is two open sets, one containing `x`, and the other `y`, whose closures are disjoint.
* `t3_space`: A Tâ‚ƒ space, is one where given any closed `C` and `x âˆ‰ C`,
  there is disjoint open sets containing `x` and `C` respectively. In `mathlib`, Tâ‚ƒ implies Tâ‚‚.â‚….
* `normal_space`: A Tâ‚„ space (sometimes referred to as normal, but authors vary on
  whether this includes Tâ‚‚; `mathlib` does), is one where given two disjoint closed sets,
  we can find two open sets that separate them. In `mathlib`, Tâ‚„ implies Tâ‚ƒ.
* `t5_space`: A Tâ‚… space, also known as a *completely normal Hausdorff space*

## Main results

### Tâ‚€ spaces

* `is_closed.exists_closed_singleton` Given a closed set `S` in a compact Tâ‚€ space,
  there is some `x âˆˆ S` such that `{x}` is closed.
* `exists_open_singleton_of_open_finset` Given an open `finset` `S` in a Tâ‚€ space,
  there is some `x âˆˆ S` such that `{x}` is open.

### Tâ‚ spaces

* `is_closed_map_const`: The constant map is a closed map.
* `discrete_of_t1_of_finite`: A finite Tâ‚ space must have the discrete topology.

### Tâ‚‚ spaces

* `t2_iff_nhds`: A space is Tâ‚‚ iff the neighbourhoods of distinct points generate the bottom filter.
* `t2_iff_is_closed_diagonal`: A space is Tâ‚‚ iff the `diagonal` of `Î±` (that is, the set of all
  points of the form `(a, a) : Î± Ã— Î±`) is closed under the product topology.
* `finset_disjoint_finset_opens_of_t2`: Any two disjoint finsets are `separated_nhds`.
* Most topological constructions preserve Hausdorffness;
  these results are part of the typeclass inference system (e.g. `embedding.t2_space`)
* `set.eq_on.closure`: If two functions are equal on some set `s`, they are equal on its closure.
* `is_compact.is_closed`: All compact sets are closed.
* `locally_compact_of_compact_nhds`: If every point has a compact neighbourhood,
  then the space is locally compact.
* `totally_separated_space_of_t1_of_basis_clopen`: If `Î±` has a clopen basis, then
  it is a `totally_separated_space`.
* `loc_compact_t2_tot_disc_iff_tot_sep`: A locally compact Tâ‚‚ space is totally disconnected iff
  it is totally separated.

If the space is also compact:

* `normal_of_compact_t2`: A compact Tâ‚‚ space is a `normal_space`.
* `connected_components_eq_Inter_clopen`: The connected component of a point
  is the intersection of all its clopen neighbourhoods.
* `compact_t2_tot_disc_iff_tot_sep`: Being a `totally_disconnected_space`
  is equivalent to being a `totally_separated_space`.
* `connected_components.t2`: `connected_components Î±` is Tâ‚‚ for `Î±` Tâ‚‚ and compact.

### Tâ‚ƒ spaces

* `disjoint_nested_nhds`: Given two points `x â‰  y`, we can find neighbourhoods `x âˆˆ Vâ‚ âŠ† Uâ‚` and
  `y âˆˆ Vâ‚‚ âŠ† Uâ‚‚`, with the `Vâ‚–` closed and the `Uâ‚–` open, such that the `Uâ‚–` are disjoint.

## References

https://en.wikipedia.org/wiki/Separation_axiom
-/

open function set filter topological_space
open_locale topology filter classical

universes u v
variables {Î± : Type u} {Î² : Type v} [topological_space Î±]

section separation

/--
`separated_nhds` is a predicate on pairs of sub`set`s of a topological space.  It holds if the two
sub`set`s are contained in disjoint open sets.
-/
def separated_nhds : set Î± â†’ set Î± â†’ Prop :=
  Î» (s t : set Î±), âˆƒ U V : (set Î±), (is_open U) âˆ§ is_open V âˆ§
  (s âŠ† U) âˆ§ (t âŠ† V) âˆ§ disjoint U V

lemma separated_nhds_iff_disjoint {s t : set Î±} :
  separated_nhds s t â†” disjoint (ğ“Ë¢ s) (ğ“Ë¢ t) :=
by simp only [(has_basis_nhds_set s).disjoint_iff (has_basis_nhds_set t), separated_nhds,
  exists_prop, â† exists_and_distrib_left, and.assoc, and.comm, and.left_comm]

namespace separated_nhds

variables {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ u : set Î±}

@[symm] lemma symm : separated_nhds s t â†’ separated_nhds t s :=
Î» âŸ¨U, V, oU, oV, aU, bV, UVâŸ©, âŸ¨V, U, oV, oU, bV, aU, disjoint.symm UVâŸ©

lemma comm (s t : set Î±) : separated_nhds s t â†” separated_nhds t s := âŸ¨symm, symmâŸ©

lemma preimage [topological_space Î²] {f : Î± â†’ Î²} {s t : set Î²} (h : separated_nhds s t)
  (hf : continuous f) : separated_nhds (f â»Â¹' s) (f â»Â¹' t) :=
let âŸ¨U, V, oU, oV, sU, tV, UVâŸ© := h in
âŸ¨f â»Â¹' U, f â»Â¹' V, oU.preimage hf, oV.preimage hf, preimage_mono sU, preimage_mono tV,
  UV.preimage fâŸ©

protected lemma disjoint (h : separated_nhds s t) : disjoint s t :=
let âŸ¨U, V, hU, hV, hsU, htV, hdâŸ© := h in hd.mono hsU htV

lemma disjoint_closure_left (h : separated_nhds s t) : disjoint (closure s) t :=
let âŸ¨U, V, hU, hV, hsU, htV, hdâŸ© := h
in (hd.closure_left hV).mono (closure_mono hsU) htV

lemma disjoint_closure_right (h : separated_nhds s t) : disjoint s (closure t) :=
h.symm.disjoint_closure_left.symm

lemma empty_right (s : set Î±) : separated_nhds s âˆ… :=
âŸ¨_, _, is_open_univ, is_open_empty, Î» a h, mem_univ a, Î» a h, by cases h, disjoint_empty _âŸ©

lemma empty_left (s : set Î±) : separated_nhds âˆ… s :=
(empty_right _).symm

lemma mono (h : separated_nhds sâ‚‚ tâ‚‚) (hs : sâ‚ âŠ† sâ‚‚) (ht : tâ‚ âŠ† tâ‚‚) : separated_nhds sâ‚ tâ‚ :=
let âŸ¨U, V, hU, hV, hsU, htV, hdâŸ© := h in âŸ¨U, V, hU, hV, hs.trans hsU, ht.trans htV, hdâŸ©

lemma union_left : separated_nhds s u â†’ separated_nhds t u â†’ separated_nhds (s âˆª t) u :=
by simpa only [separated_nhds_iff_disjoint, nhds_set_union, disjoint_sup_left] using and.intro

lemma union_right (ht : separated_nhds s t) (hu : separated_nhds s u) :
  separated_nhds s (t âˆª u) :=
(ht.symm.union_left hu.symm).symm

end separated_nhds

/-- A Tâ‚€ space, also known as a Kolmogorov space, is a topological space such that for every pair
`x â‰  y`, there is an open set containing one but not the other. We formulate the definition in terms
of the `inseparable` relation.  -/
class t0_space (Î± : Type u) [topological_space Î±] : Prop :=
(t0 : âˆ€ â¦ƒx y : Î±â¦„, inseparable x y â†’ x = y)

lemma t0_space_iff_inseparable (Î± : Type u) [topological_space Î±] :
  t0_space Î± â†” âˆ€ (x y : Î±), inseparable x y â†’ x = y :=
âŸ¨Î» âŸ¨hâŸ©, h, Î» h, âŸ¨hâŸ©âŸ©

lemma t0_space_iff_not_inseparable (Î± : Type u) [topological_space Î±] :
  t0_space Î± â†” âˆ€ (x y : Î±), x â‰  y â†’ Â¬inseparable x y :=
by simp only [t0_space_iff_inseparable, ne.def, not_imp_not]

lemma inseparable.eq [t0_space Î±] {x y : Î±} (h : inseparable x y) : x = y :=
t0_space.t0 h

protected lemma inducing.injective [topological_space Î²] [t0_space Î±] {f : Î± â†’ Î²}
  (hf : inducing f) : injective f :=
Î» x y h, inseparable.eq $ hf.inseparable_iff.1 $ h â–¸ inseparable.refl _

protected lemma inducing.embedding [topological_space Î²] [t0_space Î±] {f : Î± â†’ Î²}
  (hf : inducing f) : embedding f :=
âŸ¨hf, hf.injectiveâŸ©

lemma embedding_iff_inducing [topological_space Î²] [t0_space Î±] {f : Î± â†’ Î²} :
  embedding f â†” inducing f :=
âŸ¨embedding.to_inducing, inducing.embeddingâŸ©

lemma t0_space_iff_nhds_injective (Î± : Type u) [topological_space Î±] :
  t0_space Î± â†” injective (ğ“ : Î± â†’ filter Î±) :=
t0_space_iff_inseparable Î±

lemma nhds_injective [t0_space Î±] : injective (ğ“ : Î± â†’ filter Î±) :=
(t0_space_iff_nhds_injective Î±).1 â€¹_â€º

lemma inseparable_iff_eq [t0_space Î±] {x y : Î±} : inseparable x y â†” x = y :=
nhds_injective.eq_iff

@[simp] lemma nhds_eq_nhds_iff [t0_space Î±] {a b : Î±} : ğ“ a = ğ“ b â†” a = b :=
nhds_injective.eq_iff

@[simp] lemma inseparable_eq_eq [t0_space Î±] : inseparable = @eq Î± :=
funextâ‚‚ $ Î» x y, propext inseparable_iff_eq

lemma t0_space_iff_exists_is_open_xor_mem (Î± : Type u) [topological_space Î±] :
  t0_space Î± â†” âˆ€ x y, x â‰  y â†’ âˆƒ U:set Î±, is_open U âˆ§ (xor (x âˆˆ U) (y âˆˆ U)) :=
by simp only [t0_space_iff_not_inseparable, xor_iff_not_iff, not_forall, exists_prop,
  inseparable_iff_forall_open]

lemma exists_is_open_xor_mem [t0_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ U : set Î±, is_open U âˆ§ xor (x âˆˆ U) (y âˆˆ U) :=
(t0_space_iff_exists_is_open_xor_mem Î±).1 â€¹_â€º x y h

/-- Specialization forms a partial order on a t0 topological space. -/
def specialization_order (Î± : Type*) [topological_space Î±] [t0_space Î±] : partial_order Î± :=
{ .. specialization_preorder Î±,
  .. partial_order.lift (order_dual.to_dual âˆ˜ ğ“) nhds_injective }

instance : t0_space (separation_quotient Î±) :=
âŸ¨Î» x' y', quotient.induction_onâ‚‚' x' y' $
  Î» x y h, separation_quotient.mk_eq_mk.2 $ separation_quotient.inducing_mk.inseparable_iff.1 hâŸ©

theorem minimal_nonempty_closed_subsingleton [t0_space Î±] {s : set Î±} (hs : is_closed s)
  (hmin : âˆ€ t âŠ† s, t.nonempty â†’ is_closed t â†’ t = s) :
  s.subsingleton :=
begin
  refine Î» x hx y hy, of_not_not (Î» hxy, _),
  rcases exists_is_open_xor_mem hxy with âŸ¨U, hUo, hUâŸ©,
  wlog h : x âˆˆ U âˆ§ y âˆ‰ U,
  { exact this hmin y hy x hx (ne.symm hxy) U hUo hU.symm (hU.resolve_left h), },
  cases h with hxU hyU,
  have : s \ U = s := hmin (s \ U) (diff_subset _ _) âŸ¨y, hy, hyUâŸ© (hs.sdiff hUo),
  exact (this.symm.subset hx).2 hxU
end

theorem minimal_nonempty_closed_eq_singleton [t0_space Î±] {s : set Î±} (hs : is_closed s)
  (hne : s.nonempty) (hmin : âˆ€ t âŠ† s, t.nonempty â†’ is_closed t â†’ t = s) :
  âˆƒ x, s = {x} :=
exists_eq_singleton_iff_nonempty_subsingleton.2 âŸ¨hne, minimal_nonempty_closed_subsingleton hs hminâŸ©

/-- Given a closed set `S` in a compact Tâ‚€ space,
there is some `x âˆˆ S` such that `{x}` is closed. -/
theorem is_closed.exists_closed_singleton {Î± : Type*} [topological_space Î±]
  [t0_space Î±] [compact_space Î±] {S : set Î±} (hS : is_closed S) (hne : S.nonempty) :
  âˆƒ (x : Î±), x âˆˆ S âˆ§ is_closed ({x} : set Î±) :=
begin
  obtain âŸ¨V, Vsub, Vne, Vcls, hVâŸ© := hS.exists_minimal_nonempty_closed_subset hne,
  rcases minimal_nonempty_closed_eq_singleton Vcls Vne hV with âŸ¨x, rflâŸ©,
  exact âŸ¨x, Vsub (mem_singleton x), VclsâŸ©
end

theorem minimal_nonempty_open_subsingleton [t0_space Î±] {s : set Î±} (hs : is_open s)
  (hmin : âˆ€ t âŠ† s, t.nonempty â†’ is_open t â†’ t = s) :
  s.subsingleton :=
begin
  refine Î» x hx y hy, of_not_not (Î» hxy, _),
  rcases exists_is_open_xor_mem hxy with âŸ¨U, hUo, hUâŸ©,
  wlog h : x âˆˆ U âˆ§ y âˆ‰ U,
  { exact this hs hmin y hy x hx (ne.symm hxy) U hUo hU.symm (hU.resolve_left h), },
  cases h with hxU hyU,
  have : s âˆ© U = s := hmin (s âˆ© U) (inter_subset_left _ _) âŸ¨x, hx, hxUâŸ© (hs.inter hUo),
  exact hyU (this.symm.subset hy).2
end

theorem minimal_nonempty_open_eq_singleton [t0_space Î±] {s : set Î±} (hs : is_open s)
  (hne : s.nonempty) (hmin : âˆ€ t âŠ† s, t.nonempty â†’ is_open t â†’ t = s) :
  âˆƒ x, s = {x} :=
exists_eq_singleton_iff_nonempty_subsingleton.2 âŸ¨hne, minimal_nonempty_open_subsingleton hs hminâŸ©

/-- Given an open finite set `S` in a Tâ‚€ space, there is some `x âˆˆ S` such that `{x}` is open. -/
theorem exists_open_singleton_of_open_finite [t0_space Î±] {s : set Î±} (hfin : s.finite)
  (hne : s.nonempty) (ho : is_open s) :
  âˆƒ x âˆˆ s, is_open ({x} : set Î±) :=
begin
  lift s to finset Î± using hfin,
  induction s using finset.strong_induction_on with s ihs,
  rcases em (âˆƒ t âŠ‚ s, t.nonempty âˆ§ is_open (t : set Î±)) with âŸ¨t, hts, htne, htoâŸ©|ht,
  { rcases ihs t hts htne hto with âŸ¨x, hxt, hxoâŸ©,
    exact âŸ¨x, hts.1 hxt, hxoâŸ© },
  { rcases minimal_nonempty_open_eq_singleton ho hne _ with âŸ¨x, hxâŸ©,
    { exact âŸ¨x, hx.symm â–¸ rfl, hx â–¸ hoâŸ© },
    refine Î» t hts htne hto, of_not_not (Î» hts', ht _),
    lift t to finset Î± using s.finite_to_set.subset hts,
    exact âŸ¨t, ssubset_iff_subset_ne.2 âŸ¨hts, mt finset.coe_inj.2 hts'âŸ©, htne, htoâŸ© }
end

theorem exists_open_singleton_of_fintype [t0_space Î±] [finite Î±] [nonempty Î±] :
  âˆƒ x : Î±, is_open ({x} : set Î±) :=
let âŸ¨x, _, hâŸ© := exists_open_singleton_of_open_finite (set.to_finite _) univ_nonempty
  is_open_univ in âŸ¨x, hâŸ©

lemma t0_space_of_injective_of_continuous [topological_space Î²] {f : Î± â†’ Î²}
  (hf : function.injective f) (hf' : continuous f) [t0_space Î²] : t0_space Î± :=
âŸ¨Î» x y h, hf $ (h.map hf').eqâŸ©

protected lemma embedding.t0_space [topological_space Î²] [t0_space Î²] {f : Î± â†’ Î²}
  (hf : embedding f) : t0_space Î± :=
t0_space_of_injective_of_continuous hf.inj hf.continuous

instance subtype.t0_space [t0_space Î±] {p : Î± â†’ Prop} : t0_space (subtype p) :=
embedding_subtype_coe.t0_space

theorem t0_space_iff_or_not_mem_closure (Î± : Type u) [topological_space Î±] :
  t0_space Î± â†” (âˆ€ a b : Î±, a â‰  b â†’ (a âˆ‰ closure ({b} : set Î±) âˆ¨ b âˆ‰ closure ({a} : set Î±))) :=
by simp only [t0_space_iff_not_inseparable, inseparable_iff_mem_closure, not_and_distrib]

instance [topological_space Î²] [t0_space Î±] [t0_space Î²] : t0_space (Î± Ã— Î²) :=
âŸ¨Î» x y h, prod.ext (h.map continuous_fst).eq (h.map continuous_snd).eqâŸ©

instance {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [Î  i, topological_space (Ï€ i)] [Î  i, t0_space (Ï€ i)] :
  t0_space (Î  i, Ï€ i) :=
âŸ¨Î» x y h, funext $ Î» i, (h.map (continuous_apply i)).eqâŸ©

lemma t0_space.of_cover (h : âˆ€ x y, inseparable x y â†’ âˆƒ s : set Î±, x âˆˆ s âˆ§ y âˆˆ s âˆ§ t0_space s) :
  t0_space Î± :=
begin
  refine âŸ¨Î» x y hxy, _âŸ©,
  rcases h x y hxy with âŸ¨s, hxs, hys, hsâŸ©, resetI,
  lift x to s using hxs, lift y to s using hys,
  rw â† subtype_inseparable_iff at hxy,
  exact congr_arg coe hxy.eq
end

lemma t0_space.of_open_cover (h : âˆ€ x, âˆƒ s : set Î±, x âˆˆ s âˆ§ is_open s âˆ§ t0_space s) : t0_space Î± :=
t0_space.of_cover $ Î» x y hxy,
  let âŸ¨s, hxs, hso, hsâŸ© := h x in âŸ¨s, hxs, (hxy.mem_open_iff hso).1 hxs, hsâŸ©

/-- A Tâ‚ space, also known as a FrÃ©chet space, is a topological space
  where every singleton set is closed. Equivalently, for every pair
  `x â‰  y`, there is an open set containing `x` and not `y`. -/
class t1_space (Î± : Type u) [topological_space Î±] : Prop :=
(t1 : âˆ€x, is_closed ({x} : set Î±))

lemma is_closed_singleton [t1_space Î±] {x : Î±} : is_closed ({x} : set Î±) :=
t1_space.t1 x

lemma is_open_compl_singleton [t1_space Î±] {x : Î±} : is_open ({x}á¶œ : set Î±) :=
is_closed_singleton.is_open_compl

lemma is_open_ne [t1_space Î±] {x : Î±} : is_open {y | y â‰  x} :=
is_open_compl_singleton

@[to_additive]
lemma continuous.is_open_mul_support [t1_space Î±] [has_one Î±] [topological_space Î²]
  {f : Î² â†’ Î±} (hf : continuous f) : is_open (mul_support f) :=
is_open_ne.preimage hf

lemma ne.nhds_within_compl_singleton [t1_space Î±] {x y : Î±} (h : x â‰  y) :
  ğ“[{y}á¶œ] x = ğ“ x :=
is_open_ne.nhds_within_eq h

lemma ne.nhds_within_diff_singleton [t1_space Î±] {x y : Î±} (h : x â‰  y) (s : set Î±) :
  ğ“[s \ {y}] x = ğ“[s] x :=
begin
  rw [diff_eq, inter_comm, nhds_within_inter_of_mem],
  exact mem_nhds_within_of_mem_nhds (is_open_ne.mem_nhds h)
end

lemma is_open_set_of_eventually_nhds_within [t1_space Î±] {p : Î± â†’ Prop} :
  is_open {x | âˆ€á¶  y in ğ“[â‰ ] x, p y} :=
begin
  refine is_open_iff_mem_nhds.mpr (Î» a ha, _),
  filter_upwards [eventually_nhds_nhds_within.mpr ha] with b hb,
  by_cases a = b,
  { subst h, exact hb },
  { rw (ne.symm h).nhds_within_compl_singleton at hb,
    exact hb.filter_mono nhds_within_le_nhds }
end

protected lemma set.finite.is_closed [t1_space Î±] {s : set Î±} (hs : set.finite s) :
  is_closed s :=
begin
  rw â† bUnion_of_singleton s,
  exact is_closed_bUnion hs (Î» i hi, is_closed_singleton)
end

lemma topological_space.is_topological_basis.exists_mem_of_ne
  [t1_space Î±] {b : set (set Î±)} (hb : is_topological_basis b) {x y : Î±} (h : x â‰  y) :
  âˆƒ a âˆˆ b, x âˆˆ a âˆ§ y âˆ‰ a :=
begin
  rcases hb.is_open_iff.1 is_open_ne x h with âŸ¨a, ab, xa, haâŸ©,
  exact âŸ¨a, ab, xa, Î» h, ha h rflâŸ©,
end

lemma filter.coclosed_compact_le_cofinite [t1_space Î±] :
  filter.coclosed_compact Î± â‰¤ filter.cofinite :=
Î» s hs, compl_compl s â–¸ hs.is_compact.compl_mem_coclosed_compact_of_is_closed hs.is_closed

variable (Î±)

/-- In a `t1_space`, relatively compact sets form a bornology. Its cobounded filter is
`filter.coclosed_compact`. See also `bornology.in_compact` the bornology of sets contained
in a compact set. -/
def bornology.relatively_compact [t1_space Î±] : bornology Î± :=
{ cobounded := filter.coclosed_compact Î±,
  le_cofinite := filter.coclosed_compact_le_cofinite }

variable {Î±}

lemma bornology.relatively_compact.is_bounded_iff [t1_space Î±] {s : set Î±} :
  @bornology.is_bounded _ (bornology.relatively_compact Î±) s â†” is_compact (closure s) :=
begin
  change sá¶œ âˆˆ filter.coclosed_compact Î± â†” _,
  rw filter.mem_coclosed_compact,
  split,
  { rintros âŸ¨t, htâ‚, htâ‚‚, hstâŸ©,
    rw compl_subset_compl at hst,
    exact is_compact_of_is_closed_subset htâ‚‚ is_closed_closure (closure_minimal hst htâ‚) },
  { intros h,
    exact âŸ¨closure s, is_closed_closure, h, compl_subset_compl.mpr subset_closureâŸ© }
end

protected lemma finset.is_closed [t1_space Î±] (s : finset Î±) : is_closed (s : set Î±) :=
s.finite_to_set.is_closed

lemma t1_space_tfae (Î± : Type u) [topological_space Î±] :
  tfae [t1_space Î±,
    âˆ€ x, is_closed ({x} : set Î±),
    âˆ€ x, is_open ({x}á¶œ : set Î±),
    continuous (@cofinite_topology.of Î±),
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ {y}á¶œ âˆˆ ğ“ x,
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ âˆƒ s âˆˆ ğ“ x, y âˆ‰ s,
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ âˆƒ (U : set Î±) (hU : is_open U), x âˆˆ U âˆ§ y âˆ‰ U,
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ disjoint (ğ“ x) (pure y),
    âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ disjoint (pure x) (ğ“ y),
    âˆ€ â¦ƒx y : Î±â¦„, x â¤³ y â†’ x = y] :=
begin
  tfae_have : 1 â†” 2, from âŸ¨Î» h, h.1, Î» h, âŸ¨hâŸ©âŸ©,
  tfae_have : 2 â†” 3, by simp only [is_open_compl_iff],
  tfae_have : 5 â†” 3,
  { refine forall_swap.trans _,
    simp only [is_open_iff_mem_nhds, mem_compl_iff, mem_singleton_iff] },
  tfae_have : 5 â†” 6,
    by simp only [â† subset_compl_singleton_iff, exists_mem_subset_iff],
  tfae_have : 5 â†” 7,
    by simp only [(nhds_basis_opens _).mem_iff, subset_compl_singleton_iff, exists_prop, and.assoc,
      and.left_comm],
  tfae_have : 5 â†” 8,
    by simp only [â† principal_singleton, disjoint_principal_right],
  tfae_have : 8 â†” 9, from forall_swap.trans (by simp only [disjoint.comm, ne_comm]),
  tfae_have : 1 â†’ 4,
  { simp only [continuous_def, cofinite_topology.is_open_iff'],
    rintro H s (rfl|hs),
    exacts [is_open_empty, compl_compl s â–¸ (@set.finite.is_closed _ _ H _ hs).is_open_compl] },
  tfae_have : 4 â†’ 2,
    from Î» h x, (cofinite_topology.is_closed_iff.2 $ or.inr (finite_singleton _)).preimage h,
  tfae_have : 2 â†” 10,
  { simp only [â† closure_subset_iff_is_closed, specializes_iff_mem_closure, subset_def,
      mem_singleton_iff, eq_comm] },
  tfae_finish
end

lemma t1_space_iff_continuous_cofinite_of {Î± : Type*} [topological_space Î±] :
  t1_space Î± â†” continuous (@cofinite_topology.of Î±) :=
(t1_space_tfae Î±).out 0 3

lemma cofinite_topology.continuous_of [t1_space Î±] : continuous (@cofinite_topology.of Î±) :=
t1_space_iff_continuous_cofinite_of.mp â€¹_â€º

lemma t1_space_iff_exists_open : t1_space Î± â†”
  âˆ€ (x y), x â‰  y â†’ (âˆƒ (U : set Î±) (hU : is_open U), x âˆˆ U âˆ§ y âˆ‰ U) :=
(t1_space_tfae Î±).out 0 6

lemma t1_space_iff_disjoint_pure_nhds : t1_space Î± â†” âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ disjoint (pure x) (ğ“ y) :=
(t1_space_tfae Î±).out 0 8

lemma t1_space_iff_disjoint_nhds_pure : t1_space Î± â†” âˆ€ â¦ƒx y : Î±â¦„, x â‰  y â†’ disjoint (ğ“ x) (pure y) :=
(t1_space_tfae Î±).out 0 7

lemma t1_space_iff_specializes_imp_eq : t1_space Î± â†” âˆ€ â¦ƒx y : Î±â¦„, x â¤³ y â†’ x = y :=
(t1_space_tfae Î±).out 0 9

lemma disjoint_pure_nhds [t1_space Î±] {x y : Î±} (h : x â‰  y) : disjoint (pure x) (ğ“ y) :=
t1_space_iff_disjoint_pure_nhds.mp â€¹_â€º h

lemma disjoint_nhds_pure [t1_space Î±] {x y : Î±} (h : x â‰  y) : disjoint (ğ“ x) (pure y) :=
t1_space_iff_disjoint_nhds_pure.mp â€¹_â€º h

lemma specializes.eq [t1_space Î±] {x y : Î±} (h : x â¤³ y) : x = y :=
t1_space_iff_specializes_imp_eq.1 â€¹_â€º h

lemma specializes_iff_eq [t1_space Î±] {x y : Î±} : x â¤³ y â†” x = y :=
âŸ¨specializes.eq, Î» h, h â–¸ specializes_rflâŸ©

@[simp] lemma specializes_eq_eq [t1_space Î±] : (â¤³) = @eq Î± :=
funextâ‚‚ $ Î» x y, propext specializes_iff_eq

@[simp] lemma pure_le_nhds_iff [t1_space Î±] {a b : Î±} : pure a â‰¤ ğ“ b â†” a = b :=
specializes_iff_pure.symm.trans specializes_iff_eq

@[simp] lemma nhds_le_nhds_iff [t1_space Î±] {a b : Î±} : ğ“ a â‰¤ ğ“ b â†” a = b :=
specializes_iff_eq

instance {Î± : Type*} : t1_space (cofinite_topology Î±) :=
t1_space_iff_continuous_cofinite_of.mpr continuous_id

lemma t1_space_antitone {Î± : Type*} : antitone (@t1_space Î±) :=
begin
  simp only [antitone, t1_space_iff_continuous_cofinite_of, continuous_iff_le_induced],
  exact Î» tâ‚ tâ‚‚ h, h.trans
end

lemma continuous_within_at_update_of_ne [t1_space Î±] [decidable_eq Î±] [topological_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {x y : Î±} {z : Î²} (hne : y â‰  x) :
  continuous_within_at (function.update f x z) s y â†” continuous_within_at f s y :=
eventually_eq.congr_continuous_within_at
  (mem_nhds_within_of_mem_nhds $ mem_of_superset (is_open_ne.mem_nhds hne) $
    Î» y' hy', function.update_noteq hy' _ _)
  (function.update_noteq hne _ _)

lemma continuous_at_update_of_ne [t1_space Î±] [decidable_eq Î±] [topological_space Î²]
  {f : Î± â†’ Î²} {x y : Î±} {z : Î²} (hne : y â‰  x) :
  continuous_at (function.update f x z) y â†” continuous_at f y :=
by simp only [â† continuous_within_at_univ, continuous_within_at_update_of_ne hne]

lemma continuous_on_update_iff [t1_space Î±] [decidable_eq Î±] [topological_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {x : Î±} {y : Î²} :
  continuous_on (function.update f x y) s â†”
    continuous_on f (s \ {x}) âˆ§ (x âˆˆ s â†’ tendsto f (ğ“[s \ {x}] x) (ğ“ y)) :=
begin
  rw [continuous_on, â† and_forall_ne x, and_comm],
  refine and_congr âŸ¨Î» H z hz, _, Î» H z hzx hzs, _âŸ© (forall_congr $ Î» hxs, _),
  { specialize H z hz.2 hz.1,
    rw continuous_within_at_update_of_ne hz.2 at H,
    exact H.mono (diff_subset _ _) },
  { rw continuous_within_at_update_of_ne hzx,
    refine (H z âŸ¨hzs, hzxâŸ©).mono_of_mem (inter_mem_nhds_within _ _),
    exact is_open_ne.mem_nhds hzx },
  { exact continuous_within_at_update_same }
end

lemma t1_space_of_injective_of_continuous [topological_space Î²] {f : Î± â†’ Î²}
  (hf : function.injective f) (hf' : continuous f) [t1_space Î²] : t1_space Î± :=
t1_space_iff_specializes_imp_eq.2 $ Î» x y h, hf (h.map hf').eq

protected lemma embedding.t1_space [topological_space Î²] [t1_space Î²] {f : Î± â†’ Î²}
  (hf : embedding f) : t1_space Î± :=
t1_space_of_injective_of_continuous hf.inj hf.continuous

instance subtype.t1_space {Î± : Type u} [topological_space Î±] [t1_space Î±] {p : Î± â†’ Prop} :
  t1_space (subtype p) :=
embedding_subtype_coe.t1_space

instance [topological_space Î²] [t1_space Î±] [t1_space Î²] : t1_space (Î± Ã— Î²) :=
âŸ¨Î» âŸ¨a, bâŸ©, @singleton_prod_singleton _ _ a b â–¸ is_closed_singleton.prod is_closed_singletonâŸ©

instance {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [Î  i, topological_space (Ï€ i)] [Î  i, t1_space (Ï€ i)] :
  t1_space (Î  i, Ï€ i) :=
âŸ¨Î» f, univ_pi_singleton f â–¸ is_closed_set_pi (Î» i hi, is_closed_singleton)âŸ©

@[priority 100] -- see Note [lower instance priority]
instance t1_space.t0_space [t1_space Î±] : t0_space Î± := âŸ¨Î» x y h, h.specializes.eqâŸ©

@[simp] lemma compl_singleton_mem_nhds_iff [t1_space Î±] {x y : Î±} : {x}á¶œ âˆˆ ğ“ y â†” y â‰  x :=
is_open_compl_singleton.mem_nhds_iff

lemma compl_singleton_mem_nhds [t1_space Î±] {x y : Î±} (h : y â‰  x) : {x}á¶œ âˆˆ ğ“ y :=
compl_singleton_mem_nhds_iff.mpr h

@[simp] lemma closure_singleton [t1_space Î±] {a : Î±} :
  closure ({a} : set Î±) = {a} :=
is_closed_singleton.closure_eq

lemma set.subsingleton.closure [t1_space Î±] {s : set Î±} (hs : s.subsingleton) :
  (closure s).subsingleton :=
hs.induction_on (by simp) $ Î» x, by simp

@[simp] lemma subsingleton_closure [t1_space Î±] {s : set Î±} :
  (closure s).subsingleton â†” s.subsingleton :=
âŸ¨Î» h, h.anti subset_closure, Î» h, h.closureâŸ©

lemma is_closed_map_const {Î± Î²} [topological_space Î±] [topological_space Î²] [t1_space Î²] {y : Î²} :
  is_closed_map (function.const Î± y) :=
is_closed_map.of_nonempty $ Î» s hs h2s, by simp_rw [h2s.image_const, is_closed_singleton]

lemma nhds_within_insert_of_ne [t1_space Î±] {x y : Î±} {s : set Î±} (hxy : x â‰  y) :
  ğ“[insert y s] x = ğ“[s] x :=
begin
  refine le_antisymm (Î» t ht, _) (nhds_within_mono x $ subset_insert y s),
  obtain âŸ¨o, ho, hxo, hostâŸ© := mem_nhds_within.mp ht,
  refine mem_nhds_within.mpr âŸ¨o \ {y}, ho.sdiff is_closed_singleton, âŸ¨hxo, hxyâŸ©, _âŸ©,
  rw [inter_insert_of_not_mem $ not_mem_diff_of_mem (mem_singleton y)],
  exact (inter_subset_inter (diff_subset _ _) subset.rfl).trans host
end

/-- If `t` is a subset of `s`, except for one point,
then `insert x s` is a neighborhood of `x` within `t`. -/
lemma insert_mem_nhds_within_of_subset_insert [t1_space Î±] {x y : Î±} {s t : set Î±}
  (hu : t âŠ† insert y s) :
  insert x s âˆˆ ğ“[t] x :=
begin
  rcases eq_or_ne x y with rfl|h,
  { exact mem_of_superset self_mem_nhds_within hu },
  refine nhds_within_mono x hu _,
  rw [nhds_within_insert_of_ne h],
  exact mem_of_superset self_mem_nhds_within (subset_insert x s)
end

lemma bInter_basis_nhds [t1_space Î±] {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {x : Î±}
  (h : (ğ“ x).has_basis p s) : (â‹‚ i (h : p i), s i) = {x} :=
begin
  simp only [eq_singleton_iff_unique_mem, mem_Inter],
  refine âŸ¨Î» i hi, mem_of_mem_nhds $ h.mem_of_mem hi, Î» y hy, _âŸ©,
  contrapose! hy,
  rcases h.mem_iff.1 (compl_singleton_mem_nhds hy.symm) with âŸ¨i, hi, hsubâŸ©,
  exact âŸ¨i, hi, Î» h, hsub h rflâŸ©
end

@[simp] lemma compl_singleton_mem_nhds_set_iff [t1_space Î±] {x : Î±} {s : set Î±} :
  {x}á¶œ âˆˆ ğ“Ë¢ s â†” x âˆ‰ s :=
by rwa [is_open_compl_singleton.mem_nhds_set, subset_compl_singleton_iff]

@[simp] lemma nhds_set_le_iff [t1_space Î±] {s t : set Î±} : ğ“Ë¢ s â‰¤ ğ“Ë¢ t â†” s âŠ† t :=
begin
  refine âŸ¨_, Î» h, monotone_nhds_set hâŸ©,
  simp_rw [filter.le_def], intros h x hx,
  specialize h {x}á¶œ,
  simp_rw [compl_singleton_mem_nhds_set_iff] at h,
  by_contra hxt,
  exact h hxt hx,
end

@[simp] lemma nhds_set_inj_iff [t1_space Î±] {s t : set Î±} : ğ“Ë¢ s = ğ“Ë¢ t â†” s = t :=
by { simp_rw [le_antisymm_iff], exact and_congr nhds_set_le_iff nhds_set_le_iff }

lemma injective_nhds_set [t1_space Î±] : function.injective (ğ“Ë¢ : set Î± â†’ filter Î±) :=
Î» s t hst, nhds_set_inj_iff.mp hst

lemma strict_mono_nhds_set [t1_space Î±] : strict_mono (ğ“Ë¢ : set Î± â†’ filter Î±) :=
monotone_nhds_set.strict_mono_of_injective injective_nhds_set

@[simp] lemma nhds_le_nhds_set_iff [t1_space Î±] {s : set Î±} {x : Î±} : ğ“ x â‰¤ ğ“Ë¢ s â†” x âˆˆ s :=
by rw [â† nhds_set_singleton, nhds_set_le_iff, singleton_subset_iff]

/-- Removing a non-isolated point from a dense set, one still obtains a dense set. -/
lemma dense.diff_singleton [t1_space Î±] {s : set Î±} (hs : dense s) (x : Î±) [ne_bot (ğ“[â‰ ] x)] :
  dense (s \ {x}) :=
hs.inter_of_open_right (dense_compl_singleton x) is_open_compl_singleton

/-- Removing a finset from a dense set in a space without isolated points, one still
obtains a dense set. -/
lemma dense.diff_finset [t1_space Î±] [âˆ€ (x : Î±), ne_bot (ğ“[â‰ ] x)]
  {s : set Î±} (hs : dense s) (t : finset Î±) :
  dense (s \ t) :=
begin
  induction t using finset.induction_on with x s hxs ih hd,
  { simpa using hs },
  { rw [finset.coe_insert, â† union_singleton, â† diff_diff],
    exact ih.diff_singleton _, }
end

/-- Removing a finite set from a dense set in a space without isolated points, one still
obtains a dense set. -/
lemma dense.diff_finite [t1_space Î±] [âˆ€ (x : Î±), ne_bot (ğ“[â‰ ] x)]
  {s : set Î±} (hs : dense s) {t : set Î±} (ht : t.finite) :
  dense (s \ t) :=
begin
  convert hs.diff_finset ht.to_finset,
  exact (finite.coe_to_finset _).symm,
end

/-- If a function to a `t1_space` tends to some limit `b` at some point `a`, then necessarily
`b = f a`. -/
lemma eq_of_tendsto_nhds [topological_space Î²] [t1_space Î²] {f : Î± â†’ Î²} {a : Î±} {b : Î²}
  (h : tendsto f (ğ“ a) (ğ“ b)) : f a = b :=
by_contra $ assume (hfa : f a â‰  b),
have factâ‚ : {f a}á¶œ âˆˆ ğ“ b := compl_singleton_mem_nhds hfa.symm,
have factâ‚‚ : tendsto f (pure a) (ğ“ b) := h.comp (tendsto_id'.2 $ pure_le_nhds a),
factâ‚‚ factâ‚ (eq.refl $ f a)

lemma filter.tendsto.eventually_ne [topological_space Î²] [t1_space Î²] {Î± : Type*} {g : Î± â†’ Î²}
  {l : filter Î±} {bâ‚ bâ‚‚ : Î²} (hg : tendsto g l (ğ“ bâ‚)) (hb : bâ‚ â‰  bâ‚‚) :
  âˆ€á¶  z in l, g z â‰  bâ‚‚ :=
hg.eventually (is_open_compl_singleton.eventually_mem hb)

lemma continuous_at.eventually_ne [topological_space Î²] [t1_space Î²] {g : Î± â†’ Î²}
  {a : Î±} {b : Î²} (hg1 : continuous_at g a) (hg2 : g a â‰  b) :
  âˆ€á¶  z in ğ“ a, g z â‰  b :=
hg1.tendsto.eventually_ne hg2

/-- To prove a function to a `t1_space` is continuous at some point `a`, it suffices to prove that
`f` admits *some* limit at `a`. -/
lemma continuous_at_of_tendsto_nhds [topological_space Î²] [t1_space Î²] {f : Î± â†’ Î²} {a : Î±} {b : Î²}
  (h : tendsto f (ğ“ a) (ğ“ b)) : continuous_at f a :=
show tendsto f (ğ“ a) (ğ“ $ f a), by rwa eq_of_tendsto_nhds h

@[simp] lemma tendsto_const_nhds_iff [t1_space Î±] {l : filter Î²} [ne_bot l] {c d : Î±} :
  tendsto (Î» x, c) l (ğ“ d) â†” c = d :=
by simp_rw [tendsto, filter.map_const, pure_le_nhds_iff]

/-- A point with a finite neighborhood has to be isolated. -/
lemma is_open_singleton_of_finite_mem_nhds {Î± : Type*} [topological_space Î±] [t1_space Î±]
  (x : Î±) {s : set Î±} (hs : s âˆˆ ğ“ x) (hsf : s.finite) : is_open ({x} : set Î±) :=
begin
  have A : {x} âŠ† s, by simp only [singleton_subset_iff, mem_of_mem_nhds hs],
  have B : is_closed (s \ {x}) := (hsf.subset (diff_subset _ _)).is_closed,
  have C : (s \ {x})á¶œ âˆˆ ğ“ x, from B.is_open_compl.mem_nhds (Î» h, h.2 rfl),
  have D : {x} âˆˆ ğ“ x, by simpa only [â† diff_eq, diff_diff_cancel_left A] using inter_mem hs C,
  rwa [â† mem_interior_iff_mem_nhds, â† singleton_subset_iff, subset_interior_iff_is_open] at D
end

/-- If the punctured neighborhoods of a point form a nontrivial filter, then any neighborhood is
infinite. -/
lemma infinite_of_mem_nhds {Î±} [topological_space Î±] [t1_space Î±] (x : Î±) [hx : ne_bot (ğ“[â‰ ] x)]
  {s : set Î±} (hs : s âˆˆ ğ“ x) : set.infinite s :=
begin
  refine Î» hsf, hx.1 _,
  rw [â† is_open_singleton_iff_punctured_nhds],
  exact is_open_singleton_of_finite_mem_nhds x hs hsf
end

lemma discrete_of_t1_of_finite {X : Type*} [topological_space X] [t1_space X] [finite X] :
  discrete_topology X :=
begin
  apply singletons_open_iff_discrete.mp,
  intros x,
  rw [â† is_closed_compl_iff],
  exact (set.to_finite _).is_closed
end

lemma preconnected_space.trivial_of_discrete [preconnected_space Î±] [discrete_topology Î±] :
  subsingleton Î± :=
begin
  rw â†not_nontrivial_iff_subsingleton,
  rintro âŸ¨x, y, hxyâŸ©,
  rw [ne.def, â†mem_singleton_iff, (is_clopen_discrete _).eq_univ $ singleton_nonempty y] at hxy,
  exact hxy (mem_univ x)
end

lemma is_preconnected.infinite_of_nontrivial [t1_space Î±] {s : set Î±} (h : is_preconnected s)
  (hs : s.nontrivial) : s.infinite :=
begin
  refine mt (Î» hf, (subsingleton_coe s).mp _) (not_subsingleton_iff.mpr hs),
  haveI := @discrete_of_t1_of_finite s _ _ hf.to_subtype,
  exact @preconnected_space.trivial_of_discrete _ _ (subtype.preconnected_space h) _
end

lemma connected_space.infinite [connected_space Î±] [nontrivial Î±] [t1_space Î±] : infinite Î± :=
infinite_univ_iff.mp $ is_preconnected_univ.infinite_of_nontrivial nontrivial_univ

lemma singleton_mem_nhds_within_of_mem_discrete {s : set Î±} [discrete_topology s]
  {x : Î±} (hx : x âˆˆ s) :
  {x} âˆˆ ğ“[s] x :=
begin
  have : ({âŸ¨x, hxâŸ©} : set s) âˆˆ ğ“ (âŸ¨x, hxâŸ© : s), by simp [nhds_discrete],
  simpa only [nhds_within_eq_map_subtype_coe hx, image_singleton]
    using @image_mem_map _ _ _ (coe : s â†’ Î±) _ this
end

/-- The neighbourhoods filter of `x` within `s`, under the discrete topology, is equal to
the pure `x` filter (which is the principal filter at the singleton `{x}`.) -/
lemma nhds_within_of_mem_discrete {s : set Î±} [discrete_topology s] {x : Î±} (hx : x âˆˆ s) :
  ğ“[s] x = pure x :=
le_antisymm (le_pure_iff.2 $ singleton_mem_nhds_within_of_mem_discrete hx) (pure_le_nhds_within hx)

lemma filter.has_basis.exists_inter_eq_singleton_of_mem_discrete
  {Î¹ : Type*} {p : Î¹ â†’ Prop} {t : Î¹ â†’ set Î±} {s : set Î±} [discrete_topology s] {x : Î±}
  (hb : (ğ“ x).has_basis p t) (hx : x âˆˆ s) :
  âˆƒ i (hi : p i), t i âˆ© s = {x} :=
begin
  rcases (nhds_within_has_basis hb s).mem_iff.1 (singleton_mem_nhds_within_of_mem_discrete hx)
    with âŸ¨i, hi, hixâŸ©,
  exact âŸ¨i, hi, subset.antisymm hix $ singleton_subset_iff.2
    âŸ¨mem_of_mem_nhds $ hb.mem_of_mem hi, hxâŸ©âŸ©
end

/-- A point `x` in a discrete subset `s` of a topological space admits a neighbourhood
that only meets `s` at `x`.  -/
lemma nhds_inter_eq_singleton_of_mem_discrete {s : set Î±} [discrete_topology s]
  {x : Î±} (hx : x âˆˆ s) :
  âˆƒ U âˆˆ ğ“ x, U âˆ© s = {x} :=
by simpa using (ğ“ x).basis_sets.exists_inter_eq_singleton_of_mem_discrete hx

/-- For point `x` in a discrete subset `s` of a topological space, there is a set `U`
such that
1. `U` is a punctured neighborhood of `x` (ie. `U âˆª {x}` is a neighbourhood of `x`),
2. `U` is disjoint from `s`.
-/
lemma disjoint_nhds_within_of_mem_discrete {s : set Î±} [discrete_topology s] {x : Î±} (hx : x âˆˆ s) :
  âˆƒ U âˆˆ ğ“[â‰ ] x, disjoint U s :=
let âŸ¨V, h, h'âŸ© := nhds_inter_eq_singleton_of_mem_discrete hx in
  âŸ¨{x}á¶œ âˆ© V, inter_mem_nhds_within _ h,
    (disjoint_iff_inter_eq_empty.mpr (by { rw [inter_assoc, h', compl_inter_self] }))âŸ©

/-- Let `X` be a topological space and let `s, t âŠ† X` be two subsets.  If there is an inclusion
`t âŠ† s`, then the topological space structure on `t` induced by `X` is the same as the one
obtained by the induced topological space structure on `s`. -/
lemma topological_space.subset_trans {X : Type*} [tX : topological_space X]
  {s t : set X} (ts : t âŠ† s) :
  (subtype.topological_space : topological_space t) =
    (subtype.topological_space : topological_space s).induced (set.inclusion ts) :=
begin
  change tX.induced ((coe : s â†’ X) âˆ˜ (set.inclusion ts)) =
    topological_space.induced (set.inclusion ts) (tX.induced _),
  rw â† induced_compose,
end

/-- A Tâ‚‚ space, also known as a Hausdorff space, is one in which for every
  `x â‰  y` there exists disjoint open sets around `x` and `y`. This is
  the most widely used of the separation axioms. -/
@[mk_iff] class t2_space (Î± : Type u) [topological_space Î±] : Prop :=
(t2 : âˆ€ x y, x â‰  y â†’ âˆƒ u v : set Î±, is_open u âˆ§ is_open v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ disjoint u v)

/-- Two different points can be separated by open sets. -/
lemma t2_separation [t2_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ u v : set Î±, is_open u âˆ§ is_open v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ disjoint u v :=
t2_space.t2 x y h

lemma t2_space_iff_disjoint_nhds : t2_space Î± â†” âˆ€ x y : Î±, x â‰  y â†’ disjoint (ğ“ x) (ğ“ y) :=
begin
  refine (t2_space_iff Î±).trans (forallâ‚ƒ_congr $ Î» x y hne, _),
  simp only [(nhds_basis_opens x).disjoint_iff (nhds_basis_opens y), exists_prop,
    â† exists_and_distrib_left, and.assoc, and_comm, and.left_comm]
end

@[simp] lemma disjoint_nhds_nhds [t2_space Î±] {x y : Î±} : disjoint (ğ“ x) (ğ“ y) â†” x â‰  y :=
âŸ¨Î» hd he, by simpa [he, nhds_ne_bot.ne] using hd, t2_space_iff_disjoint_nhds.mp â€¹_â€º x yâŸ©

lemma pairwise_disjoint_nhds [t2_space Î±] : pairwise (disjoint on (ğ“ : Î± â†’ filter Î±)) :=
Î» x y, disjoint_nhds_nhds.2

protected lemma set.pairwise_disjoint_nhds [t2_space Î±] (s : set Î±) : s.pairwise_disjoint ğ“ :=
pairwise_disjoint_nhds.set_pairwise s

/-- Points of a finite set can be separated by open sets from each other. -/
lemma set.finite.t2_separation [t2_space Î±] {s : set Î±} (hs : s.finite) :
  âˆƒ U : Î± â†’ set Î±, (âˆ€ x, x âˆˆ U x âˆ§ is_open (U x)) âˆ§ s.pairwise_disjoint U :=
s.pairwise_disjoint_nhds.exists_mem_filter_basis hs nhds_basis_opens

lemma is_open_set_of_disjoint_nhds_nhds :
  is_open {p : Î± Ã— Î± | disjoint (ğ“ p.1) (ğ“ p.2)} :=
begin
  simp only [is_open_iff_mem_nhds, prod.forall, mem_set_of_eq],
  intros x y h,
  obtain âŸ¨U, hU, V, hV, hdâŸ© := ((nhds_basis_opens x).disjoint_iff (nhds_basis_opens y)).mp h,
  exact mem_nhds_prod_iff.mpr âŸ¨U, hU.2.mem_nhds hU.1, V, hV.2.mem_nhds hV.1,
    Î» âŸ¨x', y'âŸ© âŸ¨hx', hy'âŸ©, disjoint_of_disjoint_of_mem hd (hU.2.mem_nhds hx') (hV.2.mem_nhds hy')âŸ©
end

@[priority 100] -- see Note [lower instance priority]
instance t2_space.t1_space [t2_space Î±] : t1_space Î± :=
t1_space_iff_disjoint_pure_nhds.mpr $ Î» x y hne, (disjoint_nhds_nhds.2 hne).mono_left $
  pure_le_nhds _

/-- A space is Tâ‚‚ iff the neighbourhoods of distinct points generate the bottom filter. -/
lemma t2_iff_nhds : t2_space Î± â†” âˆ€ {x y : Î±}, ne_bot (ğ“ x âŠ“ ğ“ y) â†’ x = y :=
by simp only [t2_space_iff_disjoint_nhds, disjoint_iff, ne_bot_iff, ne.def, not_imp_comm]

lemma eq_of_nhds_ne_bot [t2_space Î±] {x y : Î±} (h : ne_bot (ğ“ x âŠ“ ğ“ y)) : x = y :=
t2_iff_nhds.mp â€¹_â€º h

lemma t2_space_iff_nhds : t2_space Î± â†” âˆ€ {x y : Î±}, x â‰  y â†’ âˆƒ (U âˆˆ ğ“ x) (V âˆˆ ğ“ y), disjoint U V :=
by simp only [t2_space_iff_disjoint_nhds, filter.disjoint_iff]

lemma t2_separation_nhds [t2_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ u v, u âˆˆ ğ“ x âˆ§ v âˆˆ ğ“ y âˆ§ disjoint u v :=
let âŸ¨u, v, open_u, open_v, x_in, y_in, huvâŸ© := t2_separation h in
âŸ¨u, v, open_u.mem_nhds x_in, open_v.mem_nhds y_in, huvâŸ©

lemma t2_separation_compact_nhds [locally_compact_space Î±] [t2_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ u v, u âˆˆ ğ“ x âˆ§ v âˆˆ ğ“ y âˆ§ is_compact u âˆ§ is_compact v âˆ§ disjoint u v :=
by simpa only [exists_prop, â† exists_and_distrib_left, and_comm, and.assoc, and.left_comm]
  using ((compact_basis_nhds x).disjoint_iff (compact_basis_nhds y)).1 (disjoint_nhds_nhds.2 h)

lemma t2_iff_ultrafilter :
  t2_space Î± â†” âˆ€ {x y : Î±} (f : ultrafilter Î±), â†‘f â‰¤ ğ“ x â†’ â†‘f â‰¤ ğ“ y â†’ x = y :=
t2_iff_nhds.trans $ by simp only [â†exists_ultrafilter_iff, and_imp, le_inf_iff, exists_imp_distrib]

lemma t2_iff_is_closed_diagonal : t2_space Î± â†” is_closed (diagonal Î±) :=
by simp only [t2_space_iff_disjoint_nhds, â† is_open_compl_iff, is_open_iff_mem_nhds, prod.forall,
  nhds_prod_eq, compl_diagonal_mem_prod, mem_compl_iff, mem_diagonal_iff]

lemma is_closed_diagonal [t2_space Î±] : is_closed (diagonal Î±) :=
t2_iff_is_closed_diagonal.mp â€¹_â€º

section separated

open separated_nhds finset

lemma finset_disjoint_finset_opens_of_t2 [t2_space Î±] :
  âˆ€ (s t : finset Î±), disjoint s t â†’ separated_nhds (s : set Î±) t :=
begin
  refine induction_on_union _ (Î» a b hi d, (hi d.symm).symm) (Î» a d, empty_right a) (Î» a b ab, _) _,
  { obtain âŸ¨U, V, oU, oV, aU, bV, UVâŸ© := t2_separation (finset.disjoint_singleton.1 ab),
    refine âŸ¨U, V, oU, oV, _, _, UVâŸ©;
    exact singleton_subset_set_iff.mpr â€¹_â€º },
  { intros a b c ac bc d,
    apply_mod_cast union_left (ac (disjoint_of_subset_left (a.subset_union_left b) d)) (bc _),
    exact disjoint_of_subset_left (a.subset_union_right b) d },
end

lemma point_disjoint_finset_opens_of_t2 [t2_space Î±] {x : Î±} {s : finset Î±} (h : x âˆ‰ s) :
  separated_nhds ({x} : set Î±) s :=
by exact_mod_cast finset_disjoint_finset_opens_of_t2 {x} s (finset.disjoint_singleton_left.mpr h)

end separated

lemma tendsto_nhds_unique [t2_space Î±] {f : Î² â†’ Î±} {l : filter Î²} {a b : Î±}
  [ne_bot l] (ha : tendsto f l (ğ“ a)) (hb : tendsto f l (ğ“ b)) : a = b :=
eq_of_nhds_ne_bot $ ne_bot_of_le $ le_inf ha hb

lemma tendsto_nhds_unique' [t2_space Î±] {f : Î² â†’ Î±} {l : filter Î²} {a b : Î±}
  (hl : ne_bot l) (ha : tendsto f l (ğ“ a)) (hb : tendsto f l (ğ“ b)) : a = b :=
eq_of_nhds_ne_bot $ ne_bot_of_le $ le_inf ha hb

lemma tendsto_nhds_unique_of_eventually_eq [t2_space Î±] {f g : Î² â†’ Î±} {l : filter Î²} {a b : Î±}
  [ne_bot l] (ha : tendsto f l (ğ“ a)) (hb : tendsto g l (ğ“ b)) (hfg : f =á¶ [l] g) :
  a = b :=
tendsto_nhds_unique (ha.congr' hfg) hb

lemma tendsto_nhds_unique_of_frequently_eq [t2_space Î±] {f g : Î² â†’ Î±} {l : filter Î²} {a b : Î±}
  (ha : tendsto f l (ğ“ a)) (hb : tendsto g l (ğ“ b)) (hfg : âˆƒá¶  x in l, f x = g x) :
  a = b :=
have âˆƒá¶  z : Î± Ã— Î± in ğ“ (a, b), z.1 = z.2 := (ha.prod_mk_nhds hb).frequently hfg,
not_not.1 $ Î» hne, this (is_closed_diagonal.is_open_compl.mem_nhds hne)

/-- A Tâ‚‚.â‚… space, also known as a Urysohn space, is a topological space
  where for every pair `x â‰  y`, there are two open sets, with the intersection of closures
  empty, one containing `x` and the other `y` . -/
class t2_5_space (Î± : Type u) [topological_space Î±]: Prop :=
(t2_5 : âˆ€ â¦ƒx y : Î±â¦„  (h : x â‰  y), disjoint ((ğ“ x).lift' closure) ((ğ“ y).lift' closure))

@[simp] lemma disjoint_lift'_closure_nhds [t2_5_space Î±] {x y : Î±} :
  disjoint ((ğ“ x).lift' closure) ((ğ“ y).lift' closure) â†” x â‰  y :=
âŸ¨Î» h hxy, by simpa [hxy, nhds_ne_bot.ne] using h, Î» h, t2_5_space.t2_5 hâŸ©

@[priority 100] -- see Note [lower instance priority]
instance t2_5_space.t2_space [t2_5_space Î±] : t2_space Î± :=
t2_space_iff_disjoint_nhds.2 $
  Î» x y hne, (disjoint_lift'_closure_nhds.2 hne).mono (le_lift'_closure _) (le_lift'_closure _)

lemma exists_nhds_disjoint_closure [t2_5_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ (s âˆˆ ğ“ x) (t âˆˆ ğ“ y), disjoint (closure s) (closure t) :=
((ğ“ x).basis_sets.lift'_closure.disjoint_iff (ğ“ y).basis_sets.lift'_closure).1 $
  disjoint_lift'_closure_nhds.2 h

lemma exists_open_nhds_disjoint_closure [t2_5_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ u : set Î±, x âˆˆ u âˆ§ is_open u âˆ§ âˆƒ v : set Î±, y âˆˆ v âˆ§ is_open v âˆ§
    disjoint (closure u) (closure v) :=
by simpa only [exists_prop, and.assoc] using ((nhds_basis_opens x).lift'_closure.disjoint_iff
  (nhds_basis_opens y).lift'_closure).1 (disjoint_lift'_closure_nhds.2 h)

section lim
variables [t2_space Î±] {f : filter Î±}

/-!
### Properties of `Lim` and `lim`

In this section we use explicit `nonempty Î±` instances for `Lim` and `lim`. This way the lemmas
are useful without a `nonempty Î±` instance.
-/

lemma Lim_eq {a : Î±} [ne_bot f] (h : f â‰¤ ğ“ a) :
  @Lim _ _ âŸ¨aâŸ© f = a :=
tendsto_nhds_unique (le_nhds_Lim âŸ¨a, hâŸ©) h

lemma Lim_eq_iff [ne_bot f] (h : âˆƒ (a : Î±), f â‰¤ nhds a) {a} : @Lim _ _ âŸ¨aâŸ© f = a â†” f â‰¤ ğ“ a :=
âŸ¨Î» c, c â–¸ le_nhds_Lim h, Lim_eqâŸ©

lemma ultrafilter.Lim_eq_iff_le_nhds [compact_space Î±] {x : Î±} {F : ultrafilter Î±} :
  F.Lim = x â†” â†‘F â‰¤ ğ“ x :=
âŸ¨Î» h, h â–¸ F.le_nhds_Lim, Lim_eqâŸ©

lemma is_open_iff_ultrafilter' [compact_space Î±] (U : set Î±) :
  is_open U â†” (âˆ€ F : ultrafilter Î±, F.Lim âˆˆ U â†’ U âˆˆ F.1) :=
begin
  rw is_open_iff_ultrafilter,
  refine âŸ¨Î» h F hF, h F.Lim hF F F.le_nhds_Lim, _âŸ©,
  intros cond x hx f h,
  rw [â† (ultrafilter.Lim_eq_iff_le_nhds.2 h)] at hx,
  exact cond _ hx
end

lemma filter.tendsto.lim_eq {a : Î±} {f : filter Î²} [ne_bot f] {g : Î² â†’ Î±} (h : tendsto g f (ğ“ a)) :
  @lim _ _ _ âŸ¨aâŸ© f g = a :=
Lim_eq h

lemma filter.lim_eq_iff {f : filter Î²} [ne_bot f] {g : Î² â†’ Î±} (h : âˆƒ a, tendsto g f (ğ“ a)) {a} :
  @lim _ _ _ âŸ¨aâŸ© f g = a â†” tendsto g f (ğ“ a) :=
âŸ¨Î» c, c â–¸ tendsto_nhds_lim h, filter.tendsto.lim_eqâŸ©

lemma continuous.lim_eq [topological_space Î²] {f : Î² â†’ Î±} (h : continuous f) (a : Î²) :
  @lim _ _ _ âŸ¨f aâŸ© (ğ“ a) f = f a :=
(h.tendsto a).lim_eq

@[simp] lemma Lim_nhds (a : Î±) : @Lim _ _ âŸ¨aâŸ© (ğ“ a) = a :=
Lim_eq le_rfl

@[simp] lemma lim_nhds_id (a : Î±) : @lim _ _ _ âŸ¨aâŸ© (ğ“ a) id = a :=
Lim_nhds a

@[simp] lemma Lim_nhds_within {a : Î±} {s : set Î±} (h : a âˆˆ closure s) :
  @Lim _ _ âŸ¨aâŸ© (ğ“[s] a) = a :=
by haveI : ne_bot (ğ“[s] a) := mem_closure_iff_cluster_pt.1 h;
exact Lim_eq inf_le_left

@[simp] lemma lim_nhds_within_id {a : Î±} {s : set Î±} (h : a âˆˆ closure s) :
  @lim _ _ _ âŸ¨aâŸ© (ğ“[s] a) id = a :=
Lim_nhds_within h

end lim

/-!
### `t2_space` constructions

We use two lemmas to prove that various standard constructions generate Hausdorff spaces from
Hausdorff spaces:

* `separated_by_continuous` says that two points `x y : Î±` can be separated by open neighborhoods
  provided that there exists a continuous map `f : Î± â†’ Î²` with a Hausdorff codomain such that
  `f x â‰  f y`. We use this lemma to prove that topological spaces defined using `induced` are
  Hausdorff spaces.

* `separated_by_open_embedding` says that for an open embedding `f : Î± â†’ Î²` of a Hausdorff space
  `Î±`, the images of two distinct points `x y : Î±`, `x â‰  y` can be separated by open neighborhoods.
  We use this lemma to prove that topological spaces defined using `coinduced` are Hausdorff spaces.
-/

@[priority 100] -- see Note [lower instance priority]
instance discrete_topology.to_t2_space {Î± : Type*} [topological_space Î±] [discrete_topology Î±] :
  t2_space Î± :=
âŸ¨Î» x y h, âŸ¨{x}, {y}, is_open_discrete _, is_open_discrete _, rfl, rfl, disjoint_singleton.2 hâŸ©âŸ©

lemma separated_by_continuous {Î± : Type*} {Î² : Type*}
  [topological_space Î±] [topological_space Î²] [t2_space Î²]
  {f : Î± â†’ Î²} (hf : continuous f) {x y : Î±} (h : f x â‰  f y) :
  âˆƒu v : set Î±, is_open u âˆ§ is_open v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ disjoint u v :=
let âŸ¨u, v, uo, vo, xu, yv, uvâŸ© := t2_separation h in
âŸ¨f â»Â¹' u, f â»Â¹' v, uo.preimage hf, vo.preimage hf, xu, yv, uv.preimage _âŸ©

lemma separated_by_open_embedding {Î± Î² : Type*} [topological_space Î±] [topological_space Î²]
  [t2_space Î±] {f : Î± â†’ Î²} (hf : open_embedding f) {x y : Î±} (h : x â‰  y) :
  âˆƒ u v : set Î², is_open u âˆ§ is_open v âˆ§ f x âˆˆ u âˆ§ f y âˆˆ v âˆ§ disjoint u v :=
let âŸ¨u, v, uo, vo, xu, yv, uvâŸ© := t2_separation h in
âŸ¨f '' u, f '' v, hf.is_open_map _ uo, hf.is_open_map _ vo,
  mem_image_of_mem _ xu, mem_image_of_mem _ yv, disjoint_image_of_injective hf.inj uvâŸ©

instance {Î± : Type*} {p : Î± â†’ Prop} [t : topological_space Î±] [t2_space Î±] : t2_space (subtype p) :=
âŸ¨assume x y h, separated_by_continuous continuous_subtype_val (mt subtype.eq h)âŸ©

instance {Î± : Type*} {Î² : Type*} [tâ‚ : topological_space Î±] [t2_space Î±]
  [tâ‚‚ : topological_space Î²] [t2_space Î²] : t2_space (Î± Ã— Î²) :=
âŸ¨assume âŸ¨xâ‚,xâ‚‚âŸ© âŸ¨yâ‚,yâ‚‚âŸ© h,
  or.elim (not_and_distrib.mp (mt prod.ext_iff.mpr h))
    (Î» hâ‚, separated_by_continuous continuous_fst hâ‚)
    (Î» hâ‚‚, separated_by_continuous continuous_snd hâ‚‚)âŸ©

lemma embedding.t2_space [topological_space Î²] [t2_space Î²] {f : Î± â†’ Î²} (hf : embedding f) :
  t2_space Î± :=
âŸ¨Î» x y h, separated_by_continuous hf.continuous (hf.inj.ne h)âŸ©

instance {Î± : Type*} {Î² : Type*} [tâ‚ : topological_space Î±] [t2_space Î±]
  [tâ‚‚ : topological_space Î²] [t2_space Î²] : t2_space (Î± âŠ• Î²) :=
begin
  constructor,
  rintros (x|x) (y|y) h,
  { replace h : x â‰  y := Î» c, (c.subst h) rfl,
    exact separated_by_open_embedding open_embedding_inl h },
  { exact âŸ¨_, _, is_open_range_inl, is_open_range_inr, âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©,
      is_compl_range_inl_range_inr.disjointâŸ© },
  { exact âŸ¨_, _, is_open_range_inr, is_open_range_inl, âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©,
      is_compl_range_inl_range_inr.disjoint.symmâŸ© },
  { replace h : x â‰  y := Î» c, (c.subst h) rfl,
    exact separated_by_open_embedding open_embedding_inr h }
end

instance Pi.t2_space {Î± : Type*} {Î² : Î± â†’ Type v} [tâ‚‚ : Î a, topological_space (Î² a)]
  [âˆ€a, t2_space (Î² a)] :
  t2_space (Î a, Î² a) :=
âŸ¨assume x y h,
  let âŸ¨i, hiâŸ© := not_forall.mp (mt funext h) in
  separated_by_continuous (continuous_apply i) hiâŸ©

instance sigma.t2_space {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [Î i, topological_space (Î± i)]
  [âˆ€a, t2_space (Î± a)] :
  t2_space (Î£i, Î± i) :=
begin
  constructor,
  rintros âŸ¨i, xâŸ© âŸ¨j, yâŸ© neq,
  rcases em (i = j) with (rfl|h),
  { replace neq : x â‰  y := Î» c, (c.subst neq) rfl,
    exact separated_by_open_embedding open_embedding_sigma_mk neq },
  { exact âŸ¨_, _, is_open_range_sigma_mk, is_open_range_sigma_mk, âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©,
      set.disjoint_left.mpr $ by tidyâŸ© }
end

variables {Î³ : Type*} [topological_space Î²] [topological_space Î³]

lemma is_closed_eq [t2_space Î±] {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) : is_closed {x:Î² | f x = g x} :=
continuous_iff_is_closed.mp (hf.prod_mk hg) _ is_closed_diagonal

lemma is_open_ne_fun [t2_space Î±] {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) : is_open {x:Î² | f x â‰  g x} :=
is_open_compl_iff.mpr $ is_closed_eq hf hg

/-- If two continuous maps are equal on `s`, then they are equal on the closure of `s`. See also
`set.eq_on.of_subset_closure` for a more general version. -/
lemma set.eq_on.closure [t2_space Î±] {s : set Î²} {f g : Î² â†’ Î±} (h : eq_on f g s)
  (hf : continuous f) (hg : continuous g) :
  eq_on f g (closure s) :=
closure_minimal h (is_closed_eq hf hg)

/-- If two continuous functions are equal on a dense set, then they are equal. -/
lemma continuous.ext_on [t2_space Î±] {s : set Î²} (hs : dense s) {f g : Î² â†’ Î±}
  (hf : continuous f) (hg : continuous g) (h : eq_on f g s) :
  f = g :=
funext $ Î» x, h.closure hf hg (hs x)

lemma eq_on_closureâ‚‚' [t2_space Î±] {s : set Î²} {t : set Î³} {f g : Î² â†’ Î³ â†’ Î±}
  (h : âˆ€ (x âˆˆ s) (y âˆˆ t), f x y = g x y)
  (hfâ‚ : âˆ€ x, continuous (f x)) (hfâ‚‚ : âˆ€ y, continuous (Î» x, f x y))
  (hgâ‚ : âˆ€ x, continuous (g x)) (hgâ‚‚ : âˆ€ y, continuous (Î» x, g x y)) :
  âˆ€ (x âˆˆ closure s) (y âˆˆ closure t), f x y = g x y :=
suffices closure s âŠ† â‹‚ y âˆˆ closure t, {x | f x y = g x y}, by simpa only [subset_def, mem_Inter],
closure_minimal (Î» x hx, mem_Interâ‚‚.2 $ set.eq_on.closure (h x hx) (hfâ‚ _) (hgâ‚ _)) $
  is_closed_bInter $ Î» y hy, is_closed_eq (hfâ‚‚ _) (hgâ‚‚ _)

lemma eq_on_closureâ‚‚ [t2_space Î±] {s : set Î²} {t : set Î³} {f g : Î² â†’ Î³ â†’ Î±}
  (h : âˆ€ (x âˆˆ s) (y âˆˆ t), f x y = g x y)
  (hf : continuous (uncurry f)) (hg : continuous (uncurry g)) :
  âˆ€ (x âˆˆ closure s) (y âˆˆ closure t), f x y = g x y :=
eq_on_closureâ‚‚' h (Î» x, continuous_uncurry_left x hf) (Î» x, continuous_uncurry_right x hf)
  (Î» y, continuous_uncurry_left y hg) (Î» y, continuous_uncurry_right y hg)

/-- If `f x = g x` for all `x âˆˆ s` and `f`, `g` are continuous on `t`, `s âŠ† t âŠ† closure s`, then
`f x = g x` for all `x âˆˆ t`. See also `set.eq_on.closure`. -/
lemma set.eq_on.of_subset_closure [t2_space Î±] {s t : set Î²} {f g : Î² â†’ Î±} (h : eq_on f g s)
  (hf : continuous_on f t) (hg : continuous_on g t) (hst : s âŠ† t) (hts : t âŠ† closure s) :
  eq_on f g t :=
begin
  intros x hx,
  haveI : (ğ“[s] x).ne_bot, from mem_closure_iff_cluster_pt.mp (hts hx),
  exact tendsto_nhds_unique_of_eventually_eq ((hf x hx).mono_left $ nhds_within_mono _ hst)
    ((hg x hx).mono_left $ nhds_within_mono _ hst) (h.eventually_eq_of_mem self_mem_nhds_within)
end

lemma function.left_inverse.closed_range [t2_space Î±] {f : Î± â†’ Î²} {g : Î² â†’ Î±}
  (h : function.left_inverse f g) (hf : continuous f) (hg : continuous g) :
  is_closed (range g) :=
have eq_on (g âˆ˜ f) id (closure $ range g),
  from h.right_inv_on_range.eq_on.closure (hg.comp hf) continuous_id,
is_closed_of_closure_subset $ Î» x hx,
calc x = g (f x) : (this hx).symm
   ... âˆˆ _ : mem_range_self _

lemma function.left_inverse.closed_embedding [t2_space Î±] {f : Î± â†’ Î²} {g : Î² â†’ Î±}
  (h : function.left_inverse f g) (hf : continuous f) (hg : continuous g) :
  closed_embedding g :=
âŸ¨h.embedding hf hg, h.closed_range hf hgâŸ©

lemma is_compact_is_compact_separated [t2_space Î±] {s t : set Î±}
  (hs : is_compact s) (ht : is_compact t) (hst : disjoint s t) :
  separated_nhds s t :=
by simp only [separated_nhds, prod_subset_compl_diagonal_iff_disjoint.symm] at âŠ¢ hst;
   exact generalized_tube_lemma hs ht is_closed_diagonal.is_open_compl hst

/-- In a `t2_space`, every compact set is closed. -/
lemma is_compact.is_closed [t2_space Î±] {s : set Î±} (hs : is_compact s) : is_closed s :=
is_open_compl_iff.1 $ is_open_iff_forall_mem_open.mpr $ assume x hx,
  let âŸ¨u, v, uo, vo, su, xv, uvâŸ© :=
    is_compact_is_compact_separated hs is_compact_singleton (disjoint_singleton_right.2 hx) in
âŸ¨v, (uv.mono_left $ show s â‰¤ u, from su).subset_compl_left, vo, by simpa using xvâŸ©

@[simp] lemma filter.coclosed_compact_eq_cocompact [t2_space Î±] :
  coclosed_compact Î± = cocompact Î± :=
by simp [coclosed_compact, cocompact, infi_and', and_iff_right_of_imp is_compact.is_closed]

@[simp] lemma bornology.relatively_compact_eq_in_compact [t2_space Î±] :
  bornology.relatively_compact Î± = bornology.in_compact Î± :=
by rw bornology.ext_iff; exact filter.coclosed_compact_eq_cocompact

/-- If `V : Î¹ â†’ set Î±` is a decreasing family of compact sets then any neighborhood of
`â‹‚ i, V i` contains some `V i`. This is a version of `exists_subset_nhd_of_compact'` where we
don't need to assume each `V i` closed because it follows from compactness since `Î±` is
assumed to be Hausdorff. -/
lemma exists_subset_nhds_of_is_compact [t2_space Î±] {Î¹ : Type*} [nonempty Î¹] {V : Î¹ â†’ set Î±}
  (hV : directed (âŠ‡) V) (hV_cpct : âˆ€ i, is_compact (V i)) {U : set Î±}
  (hU : âˆ€ x âˆˆ â‹‚ i, V i, U âˆˆ ğ“ x) : âˆƒ i, V i âŠ† U :=
exists_subset_nhds_of_is_compact' hV hV_cpct (Î» i, (hV_cpct i).is_closed) hU

lemma compact_exhaustion.is_closed [t2_space Î±] (K : compact_exhaustion Î±) (n : â„•) :
  is_closed (K n) :=
(K.is_compact n).is_closed

lemma is_compact.inter [t2_space Î±] {s t : set Î±} (hs : is_compact s) (ht : is_compact t) :
  is_compact (s âˆ© t) :=
hs.inter_right $ ht.is_closed

lemma is_compact_closure_of_subset_compact [t2_space Î±] {s t : set Î±}
  (ht : is_compact t) (h : s âŠ† t) : is_compact (closure s) :=
is_compact_of_is_closed_subset ht is_closed_closure (closure_minimal h ht.is_closed)

@[simp]
lemma exists_compact_superset_iff [t2_space Î±] {s : set Î±} :
  (âˆƒ K, is_compact K âˆ§ s âŠ† K) â†” is_compact (closure s) :=
âŸ¨Î» âŸ¨K, hK, hsKâŸ©, is_compact_closure_of_subset_compact hK hsK, Î» h, âŸ¨closure s, h, subset_closureâŸ©âŸ©

lemma image_closure_of_is_compact [t2_space Î²]
  {s : set Î±} (hs : is_compact (closure s)) {f : Î± â†’ Î²} (hf : continuous_on f (closure s)) :
  f '' closure s = closure (f '' s) :=
subset.antisymm hf.image_closure $ closure_minimal (image_subset f subset_closure)
  (hs.image_of_continuous_on hf).is_closed

/-- If a compact set is covered by two open sets, then we can cover it by two compact subsets. -/
lemma is_compact.binary_compact_cover [t2_space Î±] {K U V : set Î±} (hK : is_compact K)
  (hU : is_open U) (hV : is_open V) (h2K : K âŠ† U âˆª V) :
  âˆƒ Kâ‚ Kâ‚‚ : set Î±, is_compact Kâ‚ âˆ§ is_compact Kâ‚‚ âˆ§ Kâ‚ âŠ† U âˆ§ Kâ‚‚ âŠ† V âˆ§ K = Kâ‚ âˆª Kâ‚‚ :=
begin
  obtain âŸ¨Oâ‚, Oâ‚‚, h1Oâ‚, h1Oâ‚‚, h2Oâ‚, h2Oâ‚‚, hOâŸ© :=
    is_compact_is_compact_separated (hK.diff hU) (hK.diff hV)
    (by rwa [disjoint_iff_inter_eq_empty, diff_inter_diff, diff_eq_empty]),
  exact âŸ¨_, _, hK.diff h1Oâ‚, hK.diff h1Oâ‚‚, by rwa [diff_subset_comm], by rwa [diff_subset_comm],
    by rw [â† diff_inter, hO.inter_eq, diff_empty]âŸ©
end

lemma continuous.is_closed_map [compact_space Î±] [t2_space Î²] {f : Î± â†’ Î²} (h : continuous f) :
  is_closed_map f :=
Î» s hs, (hs.is_compact.image h).is_closed

lemma continuous.closed_embedding [compact_space Î±] [t2_space Î²] {f : Î± â†’ Î²} (h : continuous f)
  (hf : function.injective f) : closed_embedding f :=
closed_embedding_of_continuous_injective_closed h hf h.is_closed_map

section
open finset function
/-- For every finite open cover `Uáµ¢` of a compact set, there exists a compact cover `Káµ¢ âŠ† Uáµ¢`. -/
lemma is_compact.finite_compact_cover [t2_space Î±] {s : set Î±} (hs : is_compact s)
  {Î¹} (t : finset Î¹) (U : Î¹ â†’ set Î±) (hU : âˆ€ i âˆˆ t, is_open (U i)) (hsC : s âŠ† â‹ƒ i âˆˆ t, U i) :
  âˆƒ K : Î¹ â†’ set Î±, (âˆ€ i, is_compact (K i)) âˆ§ (âˆ€i, K i âŠ† U i) âˆ§ s = â‹ƒ i âˆˆ t, K i :=
begin
  classical,
  induction t using finset.induction with x t hx ih generalizing U hU s hs hsC,
  { refine âŸ¨Î» _, âˆ…, Î» i, is_compact_empty, Î» i, empty_subset _, _âŸ©,
    simpa only [subset_empty_iff, Union_false, Union_empty] using hsC },
  simp only [finset.set_bUnion_insert] at hsC,
  simp only [finset.mem_insert] at hU,
  have hU' : âˆ€ i âˆˆ t, is_open (U i) := Î» i hi, hU i (or.inr hi),
  rcases hs.binary_compact_cover (hU x (or.inl rfl)) (is_open_bUnion hU') hsC
    with âŸ¨Kâ‚, Kâ‚‚, h1Kâ‚, h1Kâ‚‚, h2Kâ‚, h2Kâ‚‚, hKâŸ©,
  rcases ih U hU' h1Kâ‚‚ h2Kâ‚‚ with âŸ¨K, h1K, h2K, h3KâŸ©,
  refine âŸ¨update K x Kâ‚, _, _, _âŸ©,
  { intros i, by_cases hi : i = x,
    { simp only [update_same, hi, h1Kâ‚] },
    { rw [â† ne.def] at hi, simp only [update_noteq hi, h1K] }},
  { intros i, by_cases hi : i = x,
    { simp only [update_same, hi, h2Kâ‚] },
    { rw [â† ne.def] at hi, simp only [update_noteq hi, h2K] }},
  { simp only [set_bUnion_insert_update _ hx, hK, h3K] }
end
end

lemma locally_compact_of_compact_nhds [t2_space Î±] (h : âˆ€ x : Î±, âˆƒ s, s âˆˆ ğ“ x âˆ§ is_compact s) :
  locally_compact_space Î± :=
âŸ¨assume x n hn,
  let âŸ¨u, un, uo, xuâŸ© := mem_nhds_iff.mp hn in
  let âŸ¨k, kx, kcâŸ© := h x in
  -- K is compact but not necessarily contained in N.
  -- K \ U is again compact and doesn't contain x, so
  -- we may find open sets V, W separating x from K \ U.
  -- Then K \ W is a compact neighborhood of x contained in U.
  let âŸ¨v, w, vo, wo, xv, kuw, vwâŸ© :=
    is_compact_is_compact_separated is_compact_singleton (kc.diff uo)
      (disjoint_singleton_left.2 $ Î» h, h.2 xu) in
  have wn : wá¶œ âˆˆ ğ“ x, from
   mem_nhds_iff.mpr âŸ¨v, vw.subset_compl_right, vo, singleton_subset_iff.mp xvâŸ©,
  âŸ¨k \ w,
   filter.inter_mem kx wn,
   subset.trans (diff_subset_comm.mp kuw) un,
   kc.diff woâŸ©âŸ©

@[priority 100] -- see Note [lower instance priority]
instance locally_compact_of_compact [t2_space Î±] [compact_space Î±] : locally_compact_space Î± :=
locally_compact_of_compact_nhds (assume x, âŸ¨univ, is_open_univ.mem_nhds trivial, is_compact_univâŸ©)

/-- In a locally compact Tâ‚‚ space, every point has an open neighborhood with compact closure -/
lemma exists_open_with_compact_closure [locally_compact_space Î±] [t2_space Î±] (x : Î±) :
  âˆƒ (U : set Î±), is_open U âˆ§ x âˆˆ U âˆ§ is_compact (closure U) :=
begin
  rcases exists_compact_mem_nhds x with âŸ¨K, hKc, hxKâŸ©,
  rcases mem_nhds_iff.1 hxK with âŸ¨t, h1t, h2t, h3tâŸ©,
  exact âŸ¨t, h2t, h3t, is_compact_closure_of_subset_compact hKc h1tâŸ©
end

/--
In a locally compact Tâ‚‚ space, every compact set has an open neighborhood with compact closure.
-/
lemma exists_open_superset_and_is_compact_closure [locally_compact_space Î±] [t2_space Î±]
  {K : set Î±} (hK : is_compact K) : âˆƒ V, is_open V âˆ§ K âŠ† V âˆ§ is_compact (closure V) :=
begin
  rcases exists_compact_superset hK with âŸ¨K', hK', hKK'âŸ©,
  refine âŸ¨interior K', is_open_interior, hKK',
    is_compact_closure_of_subset_compact hK' interior_subsetâŸ©,
end

/--
In a locally compact Tâ‚‚ space, given a compact set `K` inside an open set `U`, we can find a
open set `V` between these sets with compact closure: `K âŠ† V` and the closure of `V` is inside `U`.
-/
lemma exists_open_between_and_is_compact_closure [locally_compact_space Î±] [t2_space Î±]
  {K U : set Î±} (hK : is_compact K) (hU : is_open U) (hKU : K âŠ† U) :
  âˆƒ V, is_open V âˆ§ K âŠ† V âˆ§ closure V âŠ† U âˆ§ is_compact (closure V) :=
begin
  rcases exists_compact_between hK hU hKU with âŸ¨V, hV, hKV, hVUâŸ©,
  exact âŸ¨interior V, is_open_interior, hKV,
    (closure_minimal interior_subset hV.is_closed).trans hVU,
    is_compact_closure_of_subset_compact hV interior_subsetâŸ©,
end

lemma is_preirreducible_iff_subsingleton [t2_space Î±] {S : set Î±} :
  is_preirreducible S â†” S.subsingleton :=
begin
  refine âŸ¨Î» h x hx y hy, _, set.subsingleton.is_preirreducibleâŸ©,
  by_contradiction e,
  obtain âŸ¨U, V, hU, hV, hxU, hyV, h'âŸ© := t2_separation e,
  exact ((h U V hU hV âŸ¨x, hx, hxUâŸ© âŸ¨y, hy, hyVâŸ©).mono $ inter_subset_right _ _).not_disjoint h',
end

alias is_preirreducible_iff_subsingleton â†” is_preirreducible.subsingleton _
attribute [protected] is_preirreducible.subsingleton

lemma is_irreducible_iff_singleton [t2_space Î±] {S : set Î±} :
  is_irreducible S â†” âˆƒ x, S = {x} :=
by rw [is_irreducible, is_preirreducible_iff_subsingleton,
  exists_eq_singleton_iff_nonempty_subsingleton]

/-- There does not exist a nontrivial preirreducible Tâ‚‚ space. -/
lemma not_preirreducible_nontrivial_t2 (Î±) [topological_space Î±] [preirreducible_space Î±]
  [nontrivial Î±] [t2_space Î±] : false :=
(preirreducible_space.is_preirreducible_univ Î±).subsingleton.not_nontrivial nontrivial_univ

end separation

section regular_space

/-- A topological space is called a *regular space* if for any closed set `s` and `a âˆ‰ s`, there
exist disjoint open sets `U âŠ‡ s` and `V âˆ‹ a`. We formulate this condition in terms of `disjoint`ness
of filters `ğ“Ë¢ s` and `ğ“ a`. -/
@[mk_iff] class regular_space (X : Type u) [topological_space X] : Prop :=
(regular : âˆ€ {s : set X} {a}, is_closed s â†’ a âˆ‰ s â†’ disjoint (ğ“Ë¢ s) (ğ“ a))

lemma regular_space_tfae (X : Type u) [topological_space X] :
  tfae [regular_space X,
    âˆ€ (s : set X) (a âˆ‰ closure s), disjoint (ğ“Ë¢ s) (ğ“ a),
    âˆ€ (a : X) (s : set X), disjoint (ğ“Ë¢ s) (ğ“ a) â†” a âˆ‰ closure s,
    âˆ€ (a : X) (s âˆˆ ğ“ a), âˆƒ t âˆˆ ğ“ a, is_closed t âˆ§ t âŠ† s,
    âˆ€ a : X, (ğ“ a).lift' closure â‰¤ ğ“ a,
    âˆ€ a : X, (ğ“ a).lift' closure = ğ“ a] :=
begin
  tfae_have : 1 â†” 5,
  { rw [regular_space_iff, (@compl_surjective (set X) _).forall, forall_swap],
    simp only [is_closed_compl_iff, mem_compl_iff, not_not, @and_comm (_ âˆˆ _),
      (nhds_basis_opens _).lift'_closure.le_basis_iff (nhds_basis_opens _), and_imp,
      (nhds_basis_opens _).disjoint_iff_right, exists_prop, â† subset_interior_iff_mem_nhds_set,
      interior_compl, compl_subset_compl] },
  tfae_have : 5 â†’ 6, from Î» h a, (h a).antisymm (ğ“ _).le_lift'_closure,
  tfae_have : 6 â†’ 4,
  { intros H a s hs,
    rw [â† H] at hs,
    rcases (ğ“ a).basis_sets.lift'_closure.mem_iff.mp hs with âŸ¨U, hU, hUsâŸ©,
    exact âŸ¨closure U, mem_of_superset hU subset_closure, is_closed_closure, hUsâŸ© },
  tfae_have : 4 â†’ 2,
  { intros H s a ha,
    have ha' : sá¶œ âˆˆ ğ“ a, by rwa [â† mem_interior_iff_mem_nhds, interior_compl],
    rcases H _ _ ha' with âŸ¨U, hU, hUc, hUsâŸ©,
    refine disjoint_of_disjoint_of_mem disjoint_compl_left _ hU,
    rwa [â† subset_interior_iff_mem_nhds_set, hUc.is_open_compl.interior_eq, subset_compl_comm] },
  tfae_have : 2 â†’ 3,
  { refine Î» H a s, âŸ¨Î» hd has, mem_closure_iff_nhds_ne_bot.mp has _, H s aâŸ©,
    exact (hd.symm.mono_right $ @principal_le_nhds_set _ _ s).eq_bot },
  tfae_have : 3 â†’ 1, from Î» H, âŸ¨Î» s a hs ha, (H _ _).mpr $ hs.closure_eq.symm â–¸ haâŸ©,
  tfae_finish
end

lemma regular_space.of_lift'_closure (h : âˆ€ a : Î±, (ğ“ a).lift' closure = ğ“ a) : regular_space Î± :=
iff.mpr ((regular_space_tfae Î±).out 0 5) h

lemma regular_space.of_basis {Î¹ : Î± â†’ Sort*} {p : Î  a, Î¹ a â†’ Prop} {s : Î  a, Î¹ a â†’ set Î±}
  (hâ‚ : âˆ€ a, (ğ“ a).has_basis (p a) (s a)) (hâ‚‚ : âˆ€ a i, p a i â†’ is_closed (s a i)) :
  regular_space Î± :=
regular_space.of_lift'_closure $ Î» a, (hâ‚ a).lift'_closure_eq_self (hâ‚‚ a)

lemma regular_space.of_exists_mem_nhds_is_closed_subset
  (h : âˆ€ (a : Î±) (s âˆˆ ğ“ a), âˆƒ t âˆˆ ğ“ a, is_closed t âˆ§ t âŠ† s) : regular_space Î± :=
iff.mpr ((regular_space_tfae Î±).out 0 3) h

variables [regular_space Î±] {a : Î±} {s : set Î±}

lemma disjoint_nhds_set_nhds : disjoint (ğ“Ë¢ s) (ğ“ a) â†” a âˆ‰ closure s :=
iff.mp ((regular_space_tfae Î±).out 0 2) â€¹_â€º _ _

lemma disjoint_nhds_nhds_set : disjoint (ğ“ a) (ğ“Ë¢ s) â†” a âˆ‰ closure s :=
disjoint.comm.trans disjoint_nhds_set_nhds

lemma exists_mem_nhds_is_closed_subset {a : Î±} {s : set Î±} (h : s âˆˆ ğ“ a) :
  âˆƒ t âˆˆ ğ“ a, is_closed t âˆ§ t âŠ† s :=
iff.mp ((regular_space_tfae Î±).out 0 3) â€¹_â€º _ _ h

lemma closed_nhds_basis (a : Î±) : (ğ“ a).has_basis (Î» s : set Î±, s âˆˆ ğ“ a âˆ§ is_closed s) id :=
has_basis_self.2 (Î» _, exists_mem_nhds_is_closed_subset)

lemma lift'_nhds_closure (a : Î±) : (ğ“ a).lift' closure = ğ“ a :=
(closed_nhds_basis a).lift'_closure_eq_self (Î» s hs, hs.2)

lemma filter.has_basis.nhds_closure {Î¹ : Sort*} {a : Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±}
  (h : (ğ“ a).has_basis p s) : (ğ“ a).has_basis p (Î» i, closure (s i)) :=
lift'_nhds_closure a â–¸ h.lift'_closure

lemma has_basis_nhds_closure (a : Î±) : (ğ“ a).has_basis (Î» s, s âˆˆ ğ“ a) closure :=
(ğ“ a).basis_sets.nhds_closure

lemma has_basis_opens_closure (a : Î±) : (ğ“ a).has_basis (Î» s, a âˆˆ s âˆ§ is_open s) closure :=
(nhds_basis_opens a).nhds_closure

lemma topological_space.is_topological_basis.nhds_basis_closure
  {B : set (set Î±)} (hB : topological_space.is_topological_basis B) (a : Î±) :
  (ğ“ a).has_basis (Î» s : set Î±, a âˆˆ s âˆ§ s âˆˆ B) closure :=
by simpa only [and_comm] using hB.nhds_has_basis.nhds_closure

lemma topological_space.is_topological_basis.exists_closure_subset
  {B : set (set Î±)} (hB : topological_space.is_topological_basis B) {a : Î±} {s : set Î±}
  (h : s âˆˆ ğ“ a) :
  âˆƒ t âˆˆ B, a âˆˆ t âˆ§ closure t âŠ† s :=
by simpa only [exists_prop, and.assoc] using hB.nhds_has_basis.nhds_closure.mem_iff.mp h

lemma disjoint_nhds_nhds_iff_not_specializes {a b : Î±} :
  disjoint (ğ“ a) (ğ“ b) â†” Â¬a â¤³ b :=
by rw [â† nhds_set_singleton, disjoint_nhds_set_nhds, specializes_iff_mem_closure]

lemma specializes_comm {a b : Î±} : a â¤³ b â†” b â¤³ a :=
by simp only [â† disjoint_nhds_nhds_iff_not_specializes.not_left, disjoint.comm]

alias specializes_comm â†” specializes.symm _

lemma specializes_iff_inseparable {a b : Î±} : a â¤³ b â†” inseparable a b :=
âŸ¨Î» h, h.antisymm h.symm, le_of_eqâŸ©

lemma is_closed_set_of_specializes : is_closed {p : Î± Ã— Î± | p.1 â¤³ p.2} :=
by simp only [â† is_open_compl_iff, compl_set_of, â† disjoint_nhds_nhds_iff_not_specializes,
  is_open_set_of_disjoint_nhds_nhds]

lemma is_closed_set_of_inseparable : is_closed {p : Î± Ã— Î± | inseparable p.1 p.2} :=
by simp only [â† specializes_iff_inseparable, is_closed_set_of_specializes]

protected lemma inducing.regular_space [topological_space Î²] {f : Î² â†’ Î±} (hf : inducing f) :
  regular_space Î² :=
regular_space.of_basis (Î» b, by { rw [hf.nhds_eq_comap b], exact (closed_nhds_basis _).comap _ }) $
  Î» b s hs, hs.2.preimage hf.continuous

lemma regular_space_induced (f : Î² â†’ Î±) : @regular_space Î² (induced f â€¹_â€º) :=
by { letI := induced f â€¹_â€º, exact inducing.regular_space âŸ¨rflâŸ© }

lemma regular_space_Inf {X} {T : set (topological_space X)} (h : âˆ€ t âˆˆ T, @regular_space X t) :
  @regular_space X (Inf T) :=
begin
  letI := Inf T,
  have : âˆ€ a, (ğ“ a).has_basis
    (Î» If : Î£ I : set T, I â†’ set X,
      If.1.finite âˆ§ âˆ€ i : If.1, If.2 i âˆˆ @nhds X i a âˆ§ is_closed[â†‘i] (If.2 i))
    (Î» If, â‹‚ i : If.1, If.snd i),
  { intro a,
    rw [nhds_Inf, â† infi_subtype''],
    exact has_basis_infi (Î» t : T, @closed_nhds_basis X t (h t t.2) a) },
  refine regular_space.of_basis this (Î» a If hIf, is_closed_Inter $ Î» i, _),
  exact (hIf.2 i).2.mono (Inf_le (i : T).2)
end

lemma regular_space_infi {Î¹ X} {t : Î¹ â†’ topological_space X} (h : âˆ€ i, @regular_space X (t i)) :
  @regular_space X (infi t) :=
regular_space_Inf $ forall_range_iff.mpr h

lemma regular_space.inf {X} {tâ‚ tâ‚‚ : topological_space X} (hâ‚ : @regular_space X tâ‚)
  (hâ‚‚ : @regular_space X tâ‚‚) : @regular_space X (tâ‚ âŠ“ tâ‚‚) :=
by { rw [inf_eq_infi], exact regular_space_infi (bool.forall_bool.2 âŸ¨hâ‚‚, hâ‚âŸ©) }

instance {p : Î± â†’ Prop} : regular_space (subtype p) :=
embedding_subtype_coe.to_inducing.regular_space

instance [topological_space Î²] [regular_space Î²] : regular_space (Î± Ã— Î²) :=
(regular_space_induced prod.fst).inf (regular_space_induced prod.snd)

instance {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [Î  i, topological_space (Ï€ i)] [âˆ€ i, regular_space (Ï€ i)] :
  regular_space (Î  i, Ï€ i) :=
regular_space_infi $ Î» i, regular_space_induced _

end regular_space

section t3

/-- A Tâ‚ƒ space is a Tâ‚€ space which is a regular space. Any Tâ‚ƒ space is a Tâ‚ space, a Tâ‚‚ space, and
a Tâ‚‚.â‚… space.  -/
class t3_space (Î± : Type u) [topological_space Î±] extends t0_space Î±, regular_space Î± : Prop

@[priority 100] -- see Note [lower instance priority]
instance t3_space.t2_5_space [t3_space Î±] : t2_5_space Î± :=
begin
  refine âŸ¨Î» x y hne, _âŸ©,
  rw [lift'_nhds_closure, lift'_nhds_closure],
  have aux : x âˆ‰ closure {y} âˆ¨ y âˆ‰ closure {x},
    from (t0_space_iff_or_not_mem_closure Î±).mp infer_instance x y hne,
  wlog H : x âˆ‰ closure ({y} : set Î±),
  { refine (this y x hne.symm aux.symm (aux.resolve_left H)).symm },
  { rwa [â† disjoint_nhds_nhds_set, nhds_set_singleton] at H },
end

protected lemma embedding.t3_space [topological_space Î²] [t3_space Î²] {f : Î± â†’ Î²}
  (hf : embedding f) : t3_space Î± :=
{ to_t0_space := hf.t0_space,
  to_regular_space := hf.to_inducing.regular_space }

instance subtype.t3_space [t3_space Î±] {p : Î± â†’ Prop} : t3_space (subtype p) :=
embedding_subtype_coe.t3_space

instance [topological_space Î²] [t3_space Î±] [t3_space Î²] : t3_space (Î± Ã— Î²) := âŸ¨âŸ©

instance {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [Î  i, topological_space (Ï€ i)] [Î  i, t3_space (Ï€ i)] :
  t3_space (Î  i, Ï€ i) := âŸ¨âŸ©

/-- Given two points `x â‰  y`, we can find neighbourhoods `x âˆˆ Vâ‚ âŠ† Uâ‚` and `y âˆˆ Vâ‚‚ âŠ† Uâ‚‚`,
with the `Vâ‚–` closed and the `Uâ‚–` open, such that the `Uâ‚–` are disjoint. -/
lemma disjoint_nested_nhds [t3_space Î±] {x y : Î±} (h : x â‰  y) :
  âˆƒ (Uâ‚ Vâ‚ âˆˆ ğ“ x) (Uâ‚‚ Vâ‚‚ âˆˆ ğ“ y), is_closed Vâ‚ âˆ§ is_closed Vâ‚‚ âˆ§ is_open Uâ‚ âˆ§ is_open Uâ‚‚ âˆ§
  Vâ‚ âŠ† Uâ‚ âˆ§ Vâ‚‚ âŠ† Uâ‚‚ âˆ§ disjoint Uâ‚ Uâ‚‚ :=
begin
  rcases t2_separation h with âŸ¨Uâ‚, Uâ‚‚, Uâ‚_op, Uâ‚‚_op, x_in, y_in, HâŸ©,
  rcases exists_mem_nhds_is_closed_subset (Uâ‚_op.mem_nhds x_in) with âŸ¨Vâ‚, Vâ‚_in, Vâ‚_closed, hâ‚âŸ©,
  rcases exists_mem_nhds_is_closed_subset (Uâ‚‚_op.mem_nhds y_in) with âŸ¨Vâ‚‚, Vâ‚‚_in, Vâ‚‚_closed, hâ‚‚âŸ©,
  exact âŸ¨Uâ‚, mem_of_superset Vâ‚_in hâ‚, Vâ‚, Vâ‚_in, Uâ‚‚, mem_of_superset Vâ‚‚_in hâ‚‚, Vâ‚‚, Vâ‚‚_in,
    Vâ‚_closed, Vâ‚‚_closed, Uâ‚_op, Uâ‚‚_op, hâ‚, hâ‚‚, HâŸ©
end

open separation_quotient

/-- The `separation_quotient` of a regular space is a Tâ‚ƒ space. -/
instance [regular_space Î±] : t3_space (separation_quotient Î±) :=
{ regular := Î» s, surjective_mk.forall.2 $ Î» a hs ha,
    by { rw [â† disjoint_comap_iff surjective_mk, comap_mk_nhds_mk, comap_mk_nhds_set],
         exact regular_space.regular (hs.preimage continuous_mk) ha } }

end t3

section normality

/-- A Tâ‚„ space, also known as a normal space (although this condition sometimes
  omits Tâ‚‚), is one in which for every pair of disjoint closed sets `C` and `D`,
  there exist disjoint open sets containing `C` and `D` respectively. -/
class normal_space (Î± : Type u) [topological_space Î±] extends t1_space Î± : Prop :=
(normal : âˆ€ s t : set Î±, is_closed s â†’ is_closed t â†’ disjoint s t â†’ separated_nhds s t)

theorem normal_separation [normal_space Î±] {s t : set Î±}
  (H1 : is_closed s) (H2 : is_closed t) (H3 : disjoint s t) :
  separated_nhds s t :=
normal_space.normal s t H1 H2 H3

theorem normal_exists_closure_subset [normal_space Î±] {s t : set Î±} (hs : is_closed s)
  (ht : is_open t) (hst : s âŠ† t) :
  âˆƒ u, is_open u âˆ§ s âŠ† u âˆ§ closure u âŠ† t :=
begin
  have : disjoint s tá¶œ, from set.disjoint_left.mpr (Î» x hxs hxt, hxt (hst hxs)),
  rcases normal_separation hs (is_closed_compl_iff.2 ht) this
    with âŸ¨s', t', hs', ht', hss', htt', hs't'âŸ©,
  refine âŸ¨s', hs', hss',
    subset.trans (closure_minimal _ (is_closed_compl_iff.2 ht')) (compl_subset_comm.1 htt')âŸ©,
  exact Î» x hxs hxt, hs't'.le_bot âŸ¨hxs, hxtâŸ©
end

@[priority 100] -- see Note [lower instance priority]
instance normal_space.t3_space [normal_space Î±] : t3_space Î± :=
{ regular := Î» s x hs hxs, let âŸ¨u, v, hu, hv, hsu, hxv, huvâŸ© :=
    normal_separation hs is_closed_singleton (disjoint_singleton_right.mpr hxs) in
    disjoint_of_disjoint_of_mem huv (hu.mem_nhds_set.2 hsu) (hv.mem_nhds $ hxv rfl) }

-- We can't make this an instance because it could cause an instance loop.
lemma normal_of_compact_t2 [compact_space Î±] [t2_space Î±] : normal_space Î± :=
âŸ¨Î» s t hs ht, is_compact_is_compact_separated hs.is_compact ht.is_compactâŸ©

protected lemma closed_embedding.normal_space [topological_space Î²] [normal_space Î²] {f : Î± â†’ Î²}
  (hf : closed_embedding f) : normal_space Î± :=
{ to_t1_space := hf.to_embedding.t1_space,
  normal :=
  begin
    intros s t hs ht hst,
    have H : separated_nhds (f '' s) (f '' t),
      from normal_space.normal (f '' s) (f '' t) (hf.is_closed_map s hs) (hf.is_closed_map t ht)
        (disjoint_image_of_injective hf.inj hst),
    exact (H.preimage hf.continuous).mono (subset_preimage_image _ _) (subset_preimage_image _ _)
  end }

namespace separation_quotient

/-- The `separation_quotient` of a normal space is a Tâ‚„ space. We don't have separate typeclasses
for normal spaces (without Tâ‚ assumption) and Tâ‚„ spaces, so we use the same class for assumption
and for conclusion.

One can prove this using a homeomorphism between `Î±` and `separation_quotient Î±`. We give an
alternative proof that works without assuming that `Î±` is a Tâ‚ space. -/
instance [normal_space Î±] : normal_space (separation_quotient Î±) :=
{ normal := Î» s t hs ht hd, separated_nhds_iff_disjoint.2 $
    begin
      rw [â† disjoint_comap_iff surjective_mk, comap_mk_nhds_set, comap_mk_nhds_set],
      exact separated_nhds_iff_disjoint.1 (normal_separation (hs.preimage continuous_mk)
        (ht.preimage continuous_mk) (hd.preimage mk))
    end }

end separation_quotient

variable (Î±)

/-- A Tâ‚ƒ topological space with second countable topology is a normal space.
This lemma is not an instance to avoid a loop. -/
lemma normal_space_of_t3_second_countable [second_countable_topology Î±] [t3_space Î±] :
  normal_space Î± :=
begin
  have key : âˆ€ {s t : set Î±}, is_closed t â†’ disjoint s t â†’
    âˆƒ U : set (countable_basis Î±), (s âŠ† â‹ƒ u âˆˆ U, â†‘u) âˆ§
      (âˆ€ u âˆˆ U, disjoint (closure â†‘u) t) âˆ§
      âˆ€ n : â„•, is_closed (â‹ƒ (u âˆˆ U) (h : encodable.encode u â‰¤ n), closure (u : set Î±)),
  { intros s t hc hd,
    rw disjoint_left at hd,
    have : âˆ€ x âˆˆ s, âˆƒ U âˆˆ countable_basis Î±, x âˆˆ U âˆ§ disjoint (closure U) t,
    { intros x hx,
      rcases (is_basis_countable_basis Î±).exists_closure_subset (hc.is_open_compl.mem_nhds (hd hx))
        with âŸ¨u, hu, hxu, hutâŸ©,
      exact âŸ¨u, hu, hxu, disjoint_left.2 hutâŸ© },
    choose! U hu hxu hd,
    set V : s â†’ countable_basis Î± := maps_to.restrict _ _ _ hu,
    refine âŸ¨range V, _, forall_range_iff.2 $ subtype.forall.2 hd, Î» n, _âŸ©,
    { rw bUnion_range,
      exact Î» x hx, mem_Union.2 âŸ¨âŸ¨x, hxâŸ©, hxu x hxâŸ© },
    { simp only [â† supr_eq_Union, supr_and'],
      exact is_closed_bUnion (((finite_le_nat n).preimage_embedding (encodable.encode' _)).subset $
        inter_subset_right _ _) (Î» u hu, is_closed_closure) } },
  refine âŸ¨Î» s t hs ht hd, _âŸ©,
  rcases key ht hd with âŸ¨U, hsU, hUd, hUcâŸ©,
  rcases key hs hd.symm with âŸ¨V, htV, hVd, hVcâŸ©,
  refine âŸ¨â‹ƒ u âˆˆ U, â†‘u \ â‹ƒ (v âˆˆ V) (hv : encodable.encode v â‰¤ encodable.encode u), closure â†‘v,
    â‹ƒ v âˆˆ V, â†‘v \ â‹ƒ (u âˆˆ U) (hu : encodable.encode u â‰¤ encodable.encode v), closure â†‘u,
    is_open_bUnion $ Î» u hu, (is_open_of_mem_countable_basis u.2).sdiff (hVc _),
    is_open_bUnion $ Î» v hv, (is_open_of_mem_countable_basis v.2).sdiff (hUc _),
    Î» x hx, _, Î» x hx, _, _âŸ©,
  { rcases mem_Unionâ‚‚.1 (hsU hx) with âŸ¨u, huU, hxuâŸ©,
    refine mem_bUnion huU âŸ¨hxu, _âŸ©,
    simp only [mem_Union],
    rintro âŸ¨v, hvV, -, hxvâŸ©,
    exact (hVd v hvV).le_bot âŸ¨hxv, hxâŸ© },
  { rcases mem_Unionâ‚‚.1 (htV hx) with âŸ¨v, hvV, hxvâŸ©,
    refine mem_bUnion hvV âŸ¨hxv, _âŸ©,
    simp only [mem_Union],
    rintro âŸ¨u, huU, -, hxuâŸ©,
    exact (hUd u huU).le_bot âŸ¨hxu, hxâŸ© },
  { simp only [disjoint_left, mem_Union, mem_diff, not_exists, not_and, not_forall, not_not],
    rintro a âŸ¨u, huU, hau, haVâŸ© v hvV hav,
    cases le_total (encodable.encode u) (encodable.encode v) with hle hle,
    exacts [âŸ¨u, huU, hle, subset_closure hauâŸ©, (haV _ hvV hle $ subset_closure hav).elim] }
end

end normality

section completely_normal

/-- A topological space `Î±` is a *completely normal Hausdorff space* if each subspace `s : set Î±` is
a normal Hausdorff space. Equivalently, `Î±` is a `Tâ‚` space and for any two sets `s`, `t` such that
`closure s` is disjoint with `t` and `s` is disjoint with `closure t`, there exist disjoint
neighbourhoods of `s` and `t`. -/
class t5_space (Î± : Type u) [topological_space Î±] extends t1_space Î± : Prop :=
(completely_normal : âˆ€ â¦ƒs t : set Î±â¦„, disjoint (closure s) t â†’ disjoint s (closure t) â†’
  disjoint (ğ“Ë¢ s) (ğ“Ë¢ t))

export t5_space (completely_normal)

lemma embedding.t5_space [topological_space Î²] [t5_space Î²] {e : Î± â†’ Î²} (he : embedding e) :
  t5_space Î± :=
begin
  haveI := he.t1_space,
  refine âŸ¨Î» s t hdâ‚ hdâ‚‚, _âŸ©,
  simp only [he.to_inducing.nhds_set_eq_comap],
  refine disjoint_comap (completely_normal _ _),
  { rwa [â† subset_compl_iff_disjoint_left, image_subset_iff, preimage_compl,
      â† he.closure_eq_preimage_closure_image, subset_compl_iff_disjoint_left] },
  { rwa [â† subset_compl_iff_disjoint_right, image_subset_iff, preimage_compl,
      â† he.closure_eq_preimage_closure_image, subset_compl_iff_disjoint_right] }
end

/-- A subspace of a `Tâ‚…` space is a `Tâ‚…` space. -/
instance [t5_space Î±] {p : Î± â†’ Prop} : t5_space {x // p x} := embedding_subtype_coe.t5_space

/-- A `Tâ‚…` space is a `Tâ‚„` space. -/
@[priority 100] -- see Note [lower instance priority]
instance t5_space.to_normal_space [t5_space Î±] : normal_space Î± :=
âŸ¨Î» s t hs ht hd, separated_nhds_iff_disjoint.2 $
  completely_normal (by rwa [hs.closure_eq]) (by rwa [ht.closure_eq])âŸ©

open separation_quotient

/-- The `separation_quotient` of a completely normal space is a Tâ‚… space. We don't have separate
typeclasses for completely normal spaces (without Tâ‚ assumption) and Tâ‚… spaces, so we use the same
class for assumption and for conclusion.

One can prove this using a homeomorphism between `Î±` and `separation_quotient Î±`. We give an
alternative proof that works without assuming that `Î±` is a Tâ‚ space. -/
instance [t5_space Î±] : t5_space (separation_quotient Î±) :=
{ completely_normal := Î» s t hdâ‚ hdâ‚‚,
    begin
      rw [â† disjoint_comap_iff surjective_mk, comap_mk_nhds_set, comap_mk_nhds_set],
      apply t5_space.completely_normal; rw [â† preimage_mk_closure],
      exacts [hdâ‚.preimage mk, hdâ‚‚.preimage mk]
    end }

end completely_normal

/-- In a compact t2 space, the connected component of a point equals the intersection of all
its clopen neighbourhoods. -/
lemma connected_component_eq_Inter_clopen [t2_space Î±] [compact_space Î±] (x : Î±) :
  connected_component x = â‹‚ Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}, Z :=
begin
  apply eq_of_subset_of_subset connected_component_subset_Inter_clopen,
  -- Reduce to showing that the clopen intersection is connected.
  refine is_preconnected.subset_connected_component _ (mem_Inter.2 (Î» Z, Z.2.2)),
  -- We do this by showing that any disjoint cover by two closed sets implies
  -- that one of these closed sets must contain our whole thing.
  -- To reduce to the case where the cover is disjoint on all of `Î±` we need that `s` is closed
  have hs : is_closed (â‹‚ (Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}), Z : set Î±) :=
    is_closed_Inter (Î» Z, Z.2.1.2),
  rw (is_preconnected_iff_subset_of_fully_disjoint_closed hs),
  intros a b ha hb hab ab_disj,
  haveI := @normal_of_compact_t2 Î± _ _ _,
  -- Since our space is normal, we get two larger disjoint open sets containing the disjoint
  -- closed sets. If we can show that our intersection is a subset of any of these we can then
  -- "descend" this to show that it is a subset of either a or b.
  rcases normal_separation ha hb ab_disj with âŸ¨u, v, hu, hv, hau, hbv, huvâŸ©,
  -- If we can find a clopen set around x, contained in u âˆª v, we get a disjoint decomposition
  -- Z = Z âˆ© u âˆª Z âˆ© v of clopen sets. The intersection of all clopen neighbourhoods will then lie
  -- in whichever of u or v x lies in and hence will be a subset of either a or b.
  rsuffices âŸ¨Z, HâŸ© : âˆƒ (Z : set Î±), is_clopen Z âˆ§ x âˆˆ Z âˆ§ Z âŠ† u âˆª v,
  { have H1 := is_clopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hu hv huv,
    rw [union_comm] at H,
    have H2 := is_clopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hv hu huv.symm,
    by_cases (x âˆˆ u),
    -- The x âˆˆ u case.
    { left,
      suffices : (â‹‚ (Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) âŠ† u,
      { replace hab : (â‹‚ (Z : {Z // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) â‰¤ a âˆª b := hab,
        replace this : (â‹‚ (Z : {Z // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) â‰¤ u := this,
        exact disjoint.left_le_of_le_sup_right hab (huv.mono this hbv) },
      { apply subset.trans _ (inter_subset_right Z u),
        apply Inter_subset (Î» Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}, â†‘Z)
          âŸ¨Z âˆ© u, H1, mem_inter H.2.1 hâŸ© } },
    -- If x âˆ‰ u, we get x âˆˆ v since x âˆˆ u âˆª v. The rest is then like the x âˆˆ u case.
    have h1 : x âˆˆ v,
    { cases (mem_union x u v).1 (mem_of_subset_of_mem (subset.trans hab
        (union_subset_union hau hbv)) (mem_Inter.2 (Î» i, i.2.2))) with h1 h1,
      { exfalso, exact h h1},
      { exact h1} },
    right,
    suffices : (â‹‚ (Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) âŠ† v,
    { replace this : (â‹‚ (Z : {Z // is_clopen Z âˆ§ x âˆˆ Z}), â†‘Z) â‰¤ v := this,
      exact (huv.symm.mono this hau).left_le_of_le_sup_left hab },
    { apply subset.trans _ (inter_subset_right Z v),
      apply Inter_subset (Î» Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}, â†‘Z)
        âŸ¨Z âˆ© v, H2, mem_inter H.2.1 h1âŸ© } },
  -- Now we find the required Z. We utilize the fact that X \ u âˆª v will be compact,
  -- so there must be some finite intersection of clopen neighbourhoods of X disjoint to it,
  -- but a finite intersection of clopen sets is clopen so we let this be our Z.
  have H1 := (hu.union hv).is_closed_compl.is_compact.inter_Inter_nonempty
    (Î» Z : {Z : set Î± // is_clopen Z âˆ§ x âˆˆ Z}, Z) (Î» Z, Z.2.1.2),
  rw [â†not_disjoint_iff_nonempty_inter, imp_not_comm, not_forall] at H1,
  cases H1 (disjoint_compl_left_iff_subset.2 $ hab.trans $ union_subset_union hau hbv) with Zi H2,
  refine âŸ¨(â‹‚ (U âˆˆ Zi), subtype.val U), _, _, _âŸ©,
  { exact is_clopen_bInter_finset (Î» Z hZ, Z.2.1) },
  { exact mem_Interâ‚‚.2 (Î» Z hZ, Z.2.2) },
  { rwa [â†disjoint_compl_left_iff_subset, disjoint_iff_inter_eq_empty, â†not_nonempty_iff_eq_empty] }
end

section profinite

/-- A T1 space with a clopen basis is totally separated. -/
lemma totally_separated_space_of_t1_of_basis_clopen [t1_space Î±]
  (h : is_topological_basis {s : set Î± | is_clopen s}) :
  totally_separated_space Î± :=
begin
  constructor,
  rintros x - y - hxy,
  rcases h.mem_nhds_iff.mp (is_open_ne.mem_nhds hxy) with âŸ¨U, hU, hxU, hyUâŸ©,
  exact âŸ¨U, Uá¶œ, hU.is_open, hU.compl.is_open, hxU, Î» h, hyU h rfl,
    (union_compl_self U).superset, disjoint_compl_rightâŸ©
end

variables [t2_space Î±] [compact_space Î±]

/-- A compact Hausdorff space is totally disconnected if and only if it is totally separated, this
  is also true for locally compact spaces. -/
theorem compact_t2_tot_disc_iff_tot_sep :
  totally_disconnected_space Î± â†” totally_separated_space Î± :=
begin
  split,
  { intro h, constructor,
    rintros x - y -,
    contrapose!,
    intros hyp,
    suffices : x âˆˆ connected_component y,
      by simpa [totally_disconnected_space_iff_connected_component_singleton.1 h y,
                mem_singleton_iff],
    rw [connected_component_eq_Inter_clopen, mem_Inter],
    rintro âŸ¨w : set Î±, hw : is_clopen w, hy : y âˆˆ wâŸ©,
    by_contra hx,
    exact hyp wá¶œ w hw.2.is_open_compl hw.1 hx hy (@is_compl_compl _ w _).symm.codisjoint.top_le
      disjoint_compl_left },
  apply totally_separated_space.totally_disconnected_space,
end

variables [totally_disconnected_space Î±]

lemma nhds_basis_clopen (x : Î±) : (ğ“ x).has_basis (Î» s : set Î±, x âˆˆ s âˆ§ is_clopen s) id :=
âŸ¨Î» U, begin
  split,
  { have : connected_component x = {x},
      from totally_disconnected_space_iff_connected_component_singleton.mp â€¹_â€º x,
    rw connected_component_eq_Inter_clopen at this,
    intros hU,
    let N := {Z // is_clopen Z âˆ§ x âˆˆ Z},
    rsuffices âŸ¨âŸ¨s, hs, hs'âŸ©, hs''âŸ© : âˆƒ Z : N, Z.val âŠ† U,
    { exact âŸ¨s, âŸ¨hs', hsâŸ©, hs''âŸ© },
    haveI : nonempty N := âŸ¨âŸ¨univ, is_clopen_univ, mem_univ xâŸ©âŸ©,
    have hNcl : âˆ€ Z : N, is_closed Z.val := (Î» Z, Z.property.1.2),
    have hdir : directed superset (Î» Z : N, Z.val),
    { rintros âŸ¨s, hs, hxsâŸ© âŸ¨t, ht, hxtâŸ©,
      exact âŸ¨âŸ¨s âˆ© t, hs.inter ht, âŸ¨hxs, hxtâŸ©âŸ©, inter_subset_left s t, inter_subset_right s tâŸ© },
    have h_nhd: âˆ€ y âˆˆ (â‹‚ Z : N, Z.val), U âˆˆ ğ“ y,
    { intros y y_in,
      erw [this, mem_singleton_iff] at y_in,
      rwa y_in },
    exact exists_subset_nhds_of_compact_space hdir hNcl h_nhd },
  { rintro âŸ¨V, âŸ¨hxV, V_op, -âŸ©, hUV : V âŠ† UâŸ©,
    rw mem_nhds_iff,
    exact âŸ¨V, hUV, V_op, hxVâŸ© }
endâŸ©

lemma is_topological_basis_clopen : is_topological_basis {s : set Î± | is_clopen s} :=
begin
  apply is_topological_basis_of_open_of_nhds (Î» U (hU : is_clopen U), hU.1),
  intros x U hxU U_op,
  have : U âˆˆ ğ“ x,
  from is_open.mem_nhds U_op hxU,
  rcases (nhds_basis_clopen x).mem_iff.mp this with âŸ¨V, âŸ¨hxV, hVâŸ©, hVU : V âŠ† UâŸ©,
  use V,
  tauto
end

/-- Every member of an open set in a compact Hausdorff totally disconnected space
  is contained in a clopen set contained in the open set.  -/
lemma compact_exists_clopen_in_open {x : Î±} {U : set Î±} (is_open : is_open U) (memU : x âˆˆ U) :
  âˆƒ (V : set Î±) (hV : is_clopen V), x âˆˆ V âˆ§ V âŠ† U :=
(is_topological_basis.mem_nhds_iff is_topological_basis_clopen).1 (is_open.mem_nhds memU)

end profinite

section locally_compact

variables {H : Type*} [topological_space H] [locally_compact_space H] [t2_space H]

/-- A locally compact Hausdorff totally disconnected space has a basis with clopen elements. -/
lemma loc_compact_Haus_tot_disc_of_zero_dim [totally_disconnected_space H] :
  is_topological_basis {s : set H | is_clopen s} :=
begin
  refine is_topological_basis_of_open_of_nhds (Î» u hu, hu.1) _,
  rintros x U memU hU,
  obtain âŸ¨s, comp, xs, sUâŸ© := exists_compact_subset hU memU,
  obtain âŸ¨t, h, ht, xtâŸ© := mem_interior.1 xs,
  let u : set s := (coe : s â†’ H)â»Â¹' (interior s),
  have u_open_in_s : is_open u := is_open_interior.preimage continuous_subtype_coe,
  let X : s := âŸ¨x, h xtâŸ©,
  have Xu : X âˆˆ u := xs,
  haveI : compact_space s := is_compact_iff_compact_space.1 comp,
  obtain âŸ¨V : set s, clopen_in_s, Vx, V_subâŸ© := compact_exists_clopen_in_open u_open_in_s Xu,
  have V_clopen : is_clopen ((coe : s â†’ H) '' V),
  { refine âŸ¨_, (comp.is_closed.closed_embedding_subtype_coe.closed_iff_image_closed).1
               clopen_in_s.2âŸ©,
    let v : set u := (coe : u â†’ s)â»Â¹' V,
    have : (coe : u â†’ H) = (coe : s â†’ H) âˆ˜ (coe : u â†’ s) := rfl,
    have f0 : embedding (coe : u â†’ H) := embedding_subtype_coe.comp embedding_subtype_coe,
    have f1 : open_embedding (coe : u â†’ H),
    { refine âŸ¨f0, _âŸ©,
      { have : set.range (coe : u â†’ H) = interior s,
        { rw [this, set.range_comp, subtype.range_coe, subtype.image_preimage_coe],
          apply set.inter_eq_self_of_subset_left interior_subset, },
        rw this,
        apply is_open_interior } },
    have f2 : is_open v := clopen_in_s.1.preimage continuous_subtype_coe,
    have f3 : (coe : s â†’ H) '' V = (coe : u â†’ H) '' v,
    { rw [this, image_comp coe coe, subtype.image_preimage_coe,
          inter_eq_self_of_subset_left V_sub] },
    rw f3,
    apply f1.is_open_map v f2 },
  refine âŸ¨coe '' V, V_clopen, by simp [Vx, h xt], _âŸ©,
  transitivity s,
  { simp },
  assumption
end

/-- A locally compact Hausdorff space is totally disconnected
  if and only if it is totally separated. -/
theorem loc_compact_t2_tot_disc_iff_tot_sep :
  totally_disconnected_space H â†” totally_separated_space H :=
begin
  split,
  { introI h,
    exact totally_separated_space_of_t1_of_basis_clopen loc_compact_Haus_tot_disc_of_zero_dim },
  apply totally_separated_space.totally_disconnected_space,
end

end locally_compact

/-- `connected_components Î±` is Hausdorff when `Î±` is Hausdorff and compact -/
instance connected_components.t2 [t2_space Î±] [compact_space Î±] :
  t2_space (connected_components Î±) :=
begin
  -- Proof follows that of: https://stacks.math.columbia.edu/tag/0900
  -- Fix 2 distinct connected components, with points a and b
  refine âŸ¨connected_components.surjective_coe.forallâ‚‚.2 $ Î» a b ne, _âŸ©,
  rw connected_components.coe_ne_coe at ne,
  have h := connected_component_disjoint ne,
  -- write â†‘b as the intersection of all clopen subsets containing it
  rw [connected_component_eq_Inter_clopen b, disjoint_iff_inter_eq_empty] at h,
  -- Now we show that this can be reduced to some clopen containing `â†‘b` being disjoint to `â†‘a`
  obtain âŸ¨U, V, hU, ha, hb, rflâŸ© : âˆƒ (U : set Î±) (V : set (connected_components Î±)), is_clopen U âˆ§
    connected_component a âˆ© U = âˆ… âˆ§ connected_component b âŠ† U âˆ§ coe â»Â¹' V = U,
  { cases is_closed_connected_component.is_compact.elim_finite_subfamily_closed _ _ h with fin_a ha,
    swap, { exact Î» Z, Z.2.1.2 },
    -- This clopen and its complement will separate the connected components of `a` and `b`
    set U : set Î± := (â‹‚ (i : {Z // is_clopen Z âˆ§ b âˆˆ Z}) (H : i âˆˆ fin_a), i),
    have hU : is_clopen U := is_clopen_bInter_finset (Î» i j, i.2.1),
    exact âŸ¨U, coe '' U, hU, ha, subset_Interâ‚‚ (Î» Z _, Z.2.1.connected_component_subset Z.2.2),
      (connected_components_preimage_image U).symm â–¸ hU.bUnion_connected_component_eqâŸ© },
  rw connected_components.quotient_map_coe.is_clopen_preimage at hU,
  refine âŸ¨Vá¶œ, V, hU.compl.is_open, hU.is_open, _, hb mem_connected_component, disjoint_compl_leftâŸ©,
  exact Î» h, flip set.nonempty.ne_empty ha âŸ¨a, mem_connected_component, hâŸ©,
end
