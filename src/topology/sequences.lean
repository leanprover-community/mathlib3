/-
Copyright (c) 2018 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, Patrick Massot
-/
import topology.subset_properties
import topology.metric_space.basic

/-!
# Sequences in topological spaces

In this file we define sequences in topological spaces and show how they are related to
filters and the topology. In particular, we
* define the sequential closure of a set and prove that it's contained in the closure,
* define a type class "sequential_space" in which closure and sequential closure agree,
* define sequential continuity and show that it coincides with continuity in sequential spaces,
* provide an instance that shows that every first-countable (and in particular metric) space is
  a sequential space.
* define sequential compactness, prove that compactness implies sequential compactness in first
  countable spaces, and prove they are equivalent for uniform spaces having a countable uniformity
  basis (in particular metric spaces).
-/

open set filter
open_locale topological_space

variables {Œ± : Type*} {Œ≤ : Type*}

local notation f ` ‚ü∂ ` limit := tendsto f at_top (ùìù limit)

/-! ### Sequential closures, sequential continuity, and sequential spaces. -/
section topological_space
variables [topological_space Œ±] [topological_space Œ≤]

/-- A sequence converges in the sence of topological spaces iff the associated statement for filter
holds. -/
lemma topological_space.seq_tendsto_iff {x : ‚Ñï ‚Üí Œ±} {limit : Œ±} :
  tendsto x at_top (ùìù limit) ‚Üî
    ‚àÄ U : set Œ±, limit ‚àà U ‚Üí is_open U ‚Üí ‚àÉ N, ‚àÄ n ‚â• N, (x n) ‚àà U :=
(at_top_basis.tendsto_iff (nhds_basis_opens limit)).trans $
  by simp only [and_imp, exists_prop, true_and, set.mem_Ici, ge_iff_le, id]

/-- The sequential closure of a subset M ‚äÜ Œ± of a topological space Œ± is
the set of all p ‚àà Œ± which arise as limit of sequences in M. -/
def sequential_closure (M : set Œ±) : set Œ± :=
{p | ‚àÉ x : ‚Ñï ‚Üí Œ±, (‚àÄ n : ‚Ñï, x n ‚àà M) ‚àß (x ‚ü∂ p)}

lemma subset_sequential_closure (M : set Œ±) : M ‚äÜ sequential_closure M :=
assume p (_ : p ‚àà M), show p ‚àà sequential_closure M, from
  ‚ü®Œª n, p, assume n, ‚Äπp ‚àà M‚Ä∫, tendsto_const_nhds‚ü©

/-- A set `s` is sequentially closed if for any converging sequence `x n` of elements of `s`,
the limit belongs to `s` as well. -/
def is_seq_closed (s : set Œ±) : Prop := s = sequential_closure s

/-- A convenience lemma for showing that a set is sequentially closed. -/
lemma is_seq_closed_of_def {A : set Œ±}
  (h : ‚àÄ(x : ‚Ñï ‚Üí Œ±) (p : Œ±), (‚àÄ n : ‚Ñï, x n ‚àà A) ‚Üí (x ‚ü∂ p) ‚Üí p ‚àà A) : is_seq_closed A :=
show A = sequential_closure A, from subset.antisymm
  (subset_sequential_closure A)
  (show ‚àÄ p, p ‚àà sequential_closure A ‚Üí p ‚àà A, from
    (assume p ‚ü®x, _, _‚ü©, show p ‚àà A, from h x p ‚Äπ‚àÄ n : ‚Ñï, ((x n) ‚àà A)‚Ä∫ ‚Äπ(x ‚ü∂ p)‚Ä∫))

/-- The sequential closure of a set is contained in the closure of that set.
The converse is not true. -/
lemma sequential_closure_subset_closure (M : set Œ±) : sequential_closure M ‚äÜ closure M :=
assume p ‚ü®x, xM, xp‚ü©,
mem_closure_of_tendsto xp (univ_mem' xM)

/-- A set is sequentially closed if it is closed. -/
lemma is_seq_closed_of_is_closed (M : set Œ±) (_ : is_closed M) : is_seq_closed M :=
suffices sequential_closure M ‚äÜ M, from
  set.eq_of_subset_of_subset (subset_sequential_closure M) this,
calc sequential_closure M ‚äÜ closure M : sequential_closure_subset_closure M
  ... = M : is_closed.closure_eq ‚Äπis_closed M‚Ä∫

/-- The limit of a convergent sequence in a sequentially closed set is in that set.-/
lemma mem_of_is_seq_closed {A : set Œ±} (_ : is_seq_closed A) {x : ‚Ñï ‚Üí Œ±}
  (_ : ‚àÄ n, x n ‚àà A) {limit : Œ±} (_ : (x ‚ü∂ limit)) : limit ‚àà A :=
have limit ‚àà sequential_closure A, from
  show ‚àÉ x : ‚Ñï ‚Üí Œ±, (‚àÄ n : ‚Ñï, x n ‚àà A) ‚àß (x ‚ü∂ limit), from ‚ü®x, ‚Äπ‚àÄ n, x n ‚àà A‚Ä∫, ‚Äπ(x ‚ü∂ limit)‚Ä∫‚ü©,
eq.subst (eq.symm ‚Äπis_seq_closed A‚Ä∫) ‚Äπlimit ‚àà sequential_closure A‚Ä∫

/-- The limit of a convergent sequence in a closed set is in that set.-/
lemma mem_of_is_closed_sequential {A : set Œ±} (_ : is_closed A) {x : ‚Ñï ‚Üí Œ±}
  (_ : ‚àÄ n, x n ‚àà A) {limit : Œ±} (_ : x ‚ü∂ limit) : limit ‚àà A :=
mem_of_is_seq_closed (is_seq_closed_of_is_closed A ‚Äπis_closed A‚Ä∫) ‚Äπ‚àÄ n, x n ‚àà A‚Ä∫ ‚Äπ(x ‚ü∂ limit)‚Ä∫

/-- A sequential space is a space in which 'sequences are enough to probe the topology'. This can be
 formalised by demanding that the sequential closure and the closure coincide. The following
 statements show that other topological properties can be deduced from sequences in sequential
 spaces. -/
class sequential_space (Œ± : Type*) [topological_space Œ±] : Prop :=
(sequential_closure_eq_closure : ‚àÄ M : set Œ±, sequential_closure M = closure M)

/-- In a sequential space, a set is closed iff it's sequentially closed. -/
lemma is_seq_closed_iff_is_closed [sequential_space Œ±] {M : set Œ±} :
  is_seq_closed M ‚Üî is_closed M :=
iff.intro
  (assume _, closure_eq_iff_is_closed.mp (eq.symm
    (calc M = sequential_closure M : by assumption
        ... = closure M            : sequential_space.sequential_closure_eq_closure M)))
  (is_seq_closed_of_is_closed M)

/-- In a sequential space, a point belongs to the closure of a set iff it is a limit of a sequence
taking values in this set. -/
lemma mem_closure_iff_seq_limit [sequential_space Œ±] {s : set Œ±} {a : Œ±} :
  a ‚àà closure s ‚Üî ‚àÉ x : ‚Ñï ‚Üí Œ±, (‚àÄ n : ‚Ñï, x n ‚àà s) ‚àß (x ‚ü∂ a) :=
by { rw ‚Üê sequential_space.sequential_closure_eq_closure, exact iff.rfl }

/-- A function between topological spaces is sequentially continuous if it commutes with limit of
 convergent sequences. -/
def sequentially_continuous (f : Œ± ‚Üí Œ≤) : Prop :=
‚àÄ (x : ‚Ñï ‚Üí Œ±), ‚àÄ {limit : Œ±}, (x ‚ü∂ limit) ‚Üí (f‚àòx ‚ü∂ f limit)

/- A continuous function is sequentially continuous. -/
lemma continuous.to_sequentially_continuous {f : Œ± ‚Üí Œ≤} (_ : continuous f) :
  sequentially_continuous f :=
assume x limit (_ : x ‚ü∂ limit),
have tendsto f (ùìù limit) (ùìù (f limit)), from continuous.tendsto ‚Äπcontinuous f‚Ä∫ limit,
show (f ‚àò x) ‚ü∂ (f limit), from tendsto.comp this ‚Äπ(x ‚ü∂ limit)‚Ä∫

/-- In a sequential space, continuity and sequential continuity coincide. -/
lemma continuous_iff_sequentially_continuous {f : Œ± ‚Üí Œ≤} [sequential_space Œ±] :
  continuous f ‚Üî sequentially_continuous f :=
iff.intro
  (assume _, ‚Äπcontinuous f‚Ä∫.to_sequentially_continuous)
  (assume : sequentially_continuous f, show continuous f, from
    suffices h : ‚àÄ {A : set Œ≤}, is_closed A ‚Üí is_seq_closed (f ‚Åª¬π' A), from
      continuous_iff_is_closed.mpr (assume A _, is_seq_closed_iff_is_closed.mp $ h ‚Äπis_closed A‚Ä∫),
    assume A (_ : is_closed A),
      is_seq_closed_of_def $
        assume (x : ‚Ñï ‚Üí Œ±) p (_ : ‚àÄ n, f (x n) ‚àà A) (_ : x ‚ü∂ p),
        have (f ‚àò x) ‚ü∂ (f p), from ‚Äπsequentially_continuous f‚Ä∫ x ‚Äπ(x ‚ü∂ p)‚Ä∫,
        show f p ‚àà A, from
          mem_of_is_closed_sequential ‚Äπis_closed A‚Ä∫ ‚Äπ‚àÄ n, f (x n) ‚àà A‚Ä∫ ‚Äπ(f‚àòx ‚ü∂ f p)‚Ä∫)

end topological_space

namespace topological_space

namespace first_countable_topology

variables [topological_space Œ±] [first_countable_topology Œ±]

/-- Every first-countable space is sequential. -/
@[priority 100] -- see Note [lower instance priority]
instance : sequential_space Œ± :=
‚ü®show ‚àÄ M, sequential_closure M = closure M, from assume M,
  suffices closure M ‚äÜ sequential_closure M,
    from set.subset.antisymm (sequential_closure_subset_closure M) this,
  -- For every p ‚àà closure M, we need to construct a sequence x in M that converges to p:
  assume (p : Œ±) (hp : p ‚àà closure M),
  -- Since we are in a first-countable space, the neighborhood filter around `p` has a decreasing
  -- basis `U` indexed by `‚Ñï`.
  let ‚ü®U, hU‚ü© := (ùìù p).exists_antitone_basis in
  -- Since `p ‚àà closure M`, there is an element in each `M ‚à© U i`
  have hp : ‚àÄ (i : ‚Ñï), ‚àÉ (y : Œ±), y ‚àà M ‚àß y ‚àà U i,
    by simpa using (mem_closure_iff_nhds_basis hU.1).mp hp,
  begin
    -- The axiom of (countable) choice builds our sequence from the later fact
    choose u hu using hp,
    rw forall_and_distrib at hu,
    -- It clearly takes values in `M`
    use [u, hu.1],
    -- and converges to `p` because the basis is decreasing.
    apply hU.tendsto hu.2,
  end‚ü©


end first_countable_topology

end topological_space

section seq_compact
open topological_space topological_space.first_countable_topology
variables [topological_space Œ±]

/-- A set `s` is sequentially compact if every sequence taking values in `s` has a
converging subsequence. -/
def is_seq_compact (s : set Œ±) :=
  ‚àÄ ‚¶Éu : ‚Ñï ‚Üí Œ±‚¶Ñ, (‚àÄ n, u n ‚àà s) ‚Üí
    ‚àÉ (x ‚àà s) (œÜ : ‚Ñï ‚Üí ‚Ñï), strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù x)

/-- A space `Œ±` is sequentially compact if every sequence in `Œ±` has a
converging subsequence. -/
class seq_compact_space (Œ± : Type*) [topological_space Œ±] : Prop :=
(seq_compact_univ : is_seq_compact (univ : set Œ±))

lemma is_seq_compact.subseq_of_frequently_in {s : set Œ±} (hs : is_seq_compact s) {u : ‚Ñï ‚Üí Œ±}
  (hu : ‚àÉ·∂† n in at_top, u n ‚àà s) :
  ‚àÉ (x ‚àà s) (œÜ : ‚Ñï ‚Üí ‚Ñï), strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù x) :=
let ‚ü®œà, hœà, huœà‚ü© := extraction_of_frequently_at_top hu, ‚ü®x, x_in, œÜ, hœÜ, h‚ü© := hs huœà in
‚ü®x, x_in, œà ‚àò œÜ, hœà.comp hœÜ, h‚ü©

lemma seq_compact_space.tendsto_subseq [seq_compact_space Œ±] (u : ‚Ñï ‚Üí Œ±) :
  ‚àÉ x (œÜ : ‚Ñï ‚Üí ‚Ñï), strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù x) :=
let ‚ü®x, _, œÜ, mono, h‚ü© := seq_compact_space.seq_compact_univ (by simp : ‚àÄ n, u n ‚àà univ) in
‚ü®x, œÜ, mono, h‚ü©

section first_countable_topology
variables [first_countable_topology Œ±]
open topological_space.first_countable_topology

lemma is_compact.is_seq_compact {s : set Œ±} (hs : is_compact s) : is_seq_compact s :=
Œª u u_in,
let ‚ü®x, x_in, hx‚ü© := @hs (map u at_top) _
  (le_principal_iff.mpr (univ_mem' u_in : _)) in ‚ü®x, x_in, tendsto_subseq hx‚ü©

lemma is_compact.tendsto_subseq' {s : set Œ±} {u : ‚Ñï ‚Üí Œ±} (hs : is_compact s)
  (hu : ‚àÉ·∂† n in at_top, u n ‚àà s) :
‚àÉ (x ‚àà s) (œÜ : ‚Ñï ‚Üí ‚Ñï), strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù x) :=
hs.is_seq_compact.subseq_of_frequently_in hu

lemma is_compact.tendsto_subseq {s : set Œ±} {u : ‚Ñï ‚Üí Œ±} (hs : is_compact s) (hu : ‚àÄ n, u n ‚àà s) :
‚àÉ (x ‚àà s) (œÜ : ‚Ñï ‚Üí ‚Ñï), strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù x) :=
hs.is_seq_compact hu

@[priority 100] -- see Note [lower instance priority]
instance first_countable_topology.seq_compact_of_compact [compact_space Œ±] : seq_compact_space Œ± :=
‚ü®compact_univ.is_seq_compact‚ü©

lemma compact_space.tendsto_subseq [compact_space Œ±] (u : ‚Ñï ‚Üí Œ±) :
  ‚àÉ x (œÜ : ‚Ñï ‚Üí ‚Ñï), strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù x) :=
seq_compact_space.tendsto_subseq u

end first_countable_topology
end seq_compact

section uniform_space_seq_compact

open_locale uniformity
open uniform_space prod

variables [uniform_space Œ≤] {s : set Œ≤}

lemma lebesgue_number_lemma_seq {Œπ : Type*} [is_countably_generated (ùì§ Œ≤)] {c : Œπ ‚Üí set Œ≤}
  (hs : is_seq_compact s) (hc‚ÇÅ : ‚àÄ i, is_open (c i)) (hc‚ÇÇ : s ‚äÜ ‚ãÉ i, c i)
  :
  ‚àÉ V ‚àà ùì§ Œ≤, symmetric_rel V ‚àß ‚àÄ x ‚àà s, ‚àÉ i, ball x V ‚äÜ c i :=
begin
  classical,
  obtain ‚ü®V, hV, Vsymm‚ü© :
    ‚àÉ V : ‚Ñï ‚Üí set (Œ≤ √ó Œ≤), (ùì§ Œ≤).has_antitone_basis (Œª _, true) V ‚àß  ‚àÄ n, swap ‚Åª¬π' V n = V n,
      from uniform_space.has_seq_basis Œ≤,
  suffices : ‚àÉ n, ‚àÄ x ‚àà s, ‚àÉ i, ball x (V n) ‚äÜ c i,
  { cases this with n hn,
    exact ‚ü®V n, hV.to_has_basis.mem_of_mem trivial, Vsymm n, hn‚ü© },
  by_contradiction H,
  obtain ‚ü®x, x_in, hx‚ü© : ‚àÉ x : ‚Ñï ‚Üí Œ≤, (‚àÄ n, x n ‚àà s) ‚àß ‚àÄ n i, ¬¨ ball (x n) (V n) ‚äÜ c i,
  { push_neg at H,
    choose x hx using H,
    exact ‚ü®x, forall_and_distrib.mp hx‚ü© }, clear H,
  obtain ‚ü®x‚ÇÄ, x‚ÇÄ_in, œÜ, œÜ_mono, hlim‚ü© : ‚àÉ (x‚ÇÄ ‚àà s) (œÜ : ‚Ñï ‚Üí ‚Ñï), strict_mono œÜ ‚àß (x ‚àò œÜ ‚ü∂ x‚ÇÄ),
    from hs x_in, clear hs,
  obtain ‚ü®i‚ÇÄ, x‚ÇÄ_in‚ü© : ‚àÉ i‚ÇÄ, x‚ÇÄ ‚àà c i‚ÇÄ,
  { rcases hc‚ÇÇ x‚ÇÄ_in with ‚ü®_, ‚ü®i‚ÇÄ, rfl‚ü©, x‚ÇÄ_in_c‚ü©,
    exact ‚ü®i‚ÇÄ, x‚ÇÄ_in_c‚ü© }, clear hc‚ÇÇ,
  obtain ‚ü®n‚ÇÄ, hn‚ÇÄ‚ü© : ‚àÉ n‚ÇÄ, ball x‚ÇÄ (V n‚ÇÄ) ‚äÜ c i‚ÇÄ,
  { rcases (nhds_basis_uniformity hV.to_has_basis).mem_iff.mp
      (is_open_iff_mem_nhds.mp (hc‚ÇÅ i‚ÇÄ) _ x‚ÇÄ_in) with ‚ü®n‚ÇÄ, _, h‚ü©,
    use n‚ÇÄ,
    rwa ‚Üê ball_eq_of_symmetry (Vsymm n‚ÇÄ) at h }, clear hc‚ÇÅ,
  obtain ‚ü®W, W_in, hWW‚ü© : ‚àÉ W ‚àà ùì§ Œ≤, W ‚óã W ‚äÜ V n‚ÇÄ,
    from comp_mem_uniformity_sets (hV.to_has_basis.mem_of_mem trivial),
  obtain ‚ü®N, x_œÜ_N_in, hVNW‚ü© : ‚àÉ N, x (œÜ N) ‚àà ball x‚ÇÄ W ‚àß V (œÜ N) ‚äÜ W,
  { obtain ‚ü®N‚ÇÅ, h‚ÇÅ‚ü© : ‚àÉ N‚ÇÅ, ‚àÄ n ‚â• N‚ÇÅ, x (œÜ n) ‚àà ball x‚ÇÄ W,
      from tendsto_at_top'.mp hlim _ (mem_nhds_left x‚ÇÄ W_in),
    obtain ‚ü®N‚ÇÇ, h‚ÇÇ‚ü© : ‚àÉ N‚ÇÇ, V (œÜ N‚ÇÇ) ‚äÜ W,
    { rcases hV.to_has_basis.mem_iff.mp W_in with ‚ü®N, _, hN‚ü©,
      use N,
      exact subset.trans (hV.decreasing trivial trivial $  œÜ_mono.id_le _) hN },
    have : œÜ N‚ÇÇ ‚â§ œÜ (max N‚ÇÅ N‚ÇÇ),
      from œÜ_mono.le_iff_le.mpr (le_max_right _ _),
    exact ‚ü®max N‚ÇÅ N‚ÇÇ, h‚ÇÅ _ (le_max_left _ _), trans (hV.decreasing trivial trivial this) h‚ÇÇ‚ü© },
  suffices : ball (x (œÜ N)) (V (œÜ N)) ‚äÜ c i‚ÇÄ,
    from hx (œÜ N) i‚ÇÄ this,
  calc
    ball (x $ œÜ N) (V $ œÜ N) ‚äÜ ball (x $ œÜ N) W : preimage_mono hVNW
                         ... ‚äÜ ball x‚ÇÄ (V n‚ÇÄ)   : ball_subset_of_comp_subset x_œÜ_N_in hWW
                         ... ‚äÜ c i‚ÇÄ             : hn‚ÇÄ,
end

lemma is_seq_compact.totally_bounded (h : is_seq_compact s) : totally_bounded s :=
begin
  classical,
  apply totally_bounded_of_forall_symm,
  unfold is_seq_compact at h,
  contrapose! h,
  rcases h with ‚ü®V, V_in, V_symm, h‚ü©,
  simp_rw [not_subset] at h,
  have : ‚àÄ (t : set Œ≤), finite t ‚Üí ‚àÉ a, a ‚àà s ‚àß a ‚àâ ‚ãÉ y ‚àà t, ball y V,
  { intros t ht,
    obtain ‚ü®a, a_in, H‚ü© : ‚àÉ a ‚àà s, ‚àÄ (x : Œ≤), x ‚àà t ‚Üí (x, a) ‚àâ V,
      by simpa [ht] using h t,
    use [a, a_in],
    intro H',
    obtain ‚ü®x, x_in, hx‚ü© := mem_bUnion_iff.mp H',
    exact H x x_in hx },
  cases seq_of_forall_finite_exists this with u hu, clear h this,
  simp [forall_and_distrib] at hu,
  cases hu with u_in hu,
  use [u, u_in], clear u_in,
  intros x x_in œÜ,
  intros hœÜ huœÜ,
  obtain ‚ü®N, hN‚ü© : ‚àÉ N, ‚àÄ p q, p ‚â• N ‚Üí q ‚â• N ‚Üí (u (œÜ p), u (œÜ q)) ‚àà V,
    from huœÜ.cauchy_seq.mem_entourage V_in,
  specialize hN N (N+1) (le_refl N) (nat.le_succ N),
  specialize hu (œÜ $ N+1) (œÜ N) (hœÜ $ lt_add_one N),
  exact hu hN,
end

protected lemma is_seq_compact.is_compact [is_countably_generated $ ùì§ Œ≤] (hs : is_seq_compact s) :
  is_compact s :=
begin
  classical,
  rw is_compact_iff_finite_subcover,
  intros Œπ U Uop s_sub,
  rcases lebesgue_number_lemma_seq hs Uop s_sub with ‚ü®V, V_in, Vsymm, H‚ü©,
  rcases totally_bounded_iff_subset.mp hs.totally_bounded V V_in with ‚ü®t,t_sub, tfin,  ht‚ü©,
  have : ‚àÄ x : t, ‚àÉ (i : Œπ), ball x.val V ‚äÜ U i,
  { rintros ‚ü®x, x_in‚ü©,
    exact H x (t_sub x_in) },
  choose i hi using this,
  haveI : fintype t := tfin.fintype,
  use finset.image i finset.univ,
  transitivity ‚ãÉ y ‚àà t, ball y V,
  { intros x x_in,
    specialize ht x_in,
    rw mem_bUnion_iff at *,
    simp_rw ball_eq_of_symmetry Vsymm,
    exact ht },
  { apply bUnion_subset_bUnion,
    intros x x_in,
    exact ‚ü®i ‚ü®x, x_in‚ü©, finset.mem_image_of_mem _ (finset.mem_univ _), hi ‚ü®x, x_in‚ü©‚ü© },
end

/-- A version of Bolzano-Weistrass: in a uniform space with countably generated uniformity filter
(e.g., in a metric space), a set is compact if and only if it is sequentially compact. -/
protected lemma uniform_space.compact_iff_seq_compact [is_countably_generated $ ùì§ Œ≤] :
 is_compact s ‚Üî is_seq_compact s :=
‚ü®Œª H, H.is_seq_compact, Œª H, H.is_compact‚ü©

lemma uniform_space.compact_space_iff_seq_compact_space [is_countably_generated $ ùì§ Œ≤] :
  compact_space Œ≤ ‚Üî seq_compact_space Œ≤ :=
have key : is_compact (univ : set Œ≤) ‚Üî is_seq_compact univ := uniform_space.compact_iff_seq_compact,
‚ü®Œª ‚ü®h‚ü©, ‚ü®key.mp h‚ü©, Œª ‚ü®h‚ü©, ‚ü®key.mpr h‚ü©‚ü©

end uniform_space_seq_compact

section metric_seq_compact

variables [metric_space Œ≤] {s : set Œ≤}
open metric

/-- A version of Bolzano-Weistrass: in a proper metric space (eg. $‚Ñù^n$),
every bounded sequence has a converging subsequence. This version assumes only
that the sequence is frequently in some bounded set. -/
lemma tendsto_subseq_of_frequently_bounded [proper_space Œ≤] (hs : bounded s)
  {u : ‚Ñï ‚Üí Œ≤} (hu : ‚àÉ·∂† n in at_top, u n ‚àà s) :
  ‚àÉ b ‚àà closure s, ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù b) :=
begin
  have hcs : is_compact (closure s) :=
    compact_iff_closed_bounded.mpr ‚ü®is_closed_closure, bounded_closure_of_bounded hs‚ü©,
  replace hcs : is_seq_compact (closure s),
    from uniform_space.compact_iff_seq_compact.mp hcs,
  have hu' : ‚àÉ·∂† n in at_top, u n ‚àà closure s,
  { apply frequently.mono hu,
    intro n,
    apply subset_closure },
  exact hcs.subseq_of_frequently_in hu',
end

/-- A version of Bolzano-Weistrass: in a proper metric space (eg. $‚Ñù^n$),
every bounded sequence has a converging subsequence. -/
lemma tendsto_subseq_of_bounded [proper_space Œ≤] (hs : bounded s)
  {u : ‚Ñï ‚Üí Œ≤} (hu : ‚àÄ n, u n ‚àà s) :
‚àÉ b ‚àà closure s, ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù b) :=
tendsto_subseq_of_frequently_bounded hs $ frequently_of_forall hu

lemma seq_compact.lebesgue_number_lemma_of_metric
  {Œπ : Type*} {c : Œπ ‚Üí set Œ≤} (hs : is_seq_compact s)
  (hc‚ÇÅ : ‚àÄ i, is_open (c i)) (hc‚ÇÇ : s ‚äÜ ‚ãÉ i, c i) :
  ‚àÉ Œ¥ > 0, ‚àÄ x ‚àà s, ‚àÉ i, ball x Œ¥ ‚äÜ c i :=
begin
  rcases lebesgue_number_lemma_seq hs hc‚ÇÅ hc‚ÇÇ with ‚ü®V, V_in, _, hV‚ü©,
  rcases uniformity_basis_dist.mem_iff.mp V_in with ‚ü®Œ¥, Œ¥_pos, h‚ü©,
  use [Œ¥, Œ¥_pos],
  intros x x_in,
  rcases hV x x_in with ‚ü®i, hi‚ü©,
  use i,
  have := ball_mono h x,
  rw ball_eq_ball' at this,
  exact subset.trans this hi,
end

end metric_seq_compact
