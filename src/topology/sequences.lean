/-
Copyright (c) 2018 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, Patrick Massot, Yury Kudryashov
-/
import topology.subset_properties
import topology.metric_space.basic

/-!
# Sequences in topological spaces

In this file we define sequences in topological spaces and show how they are related to
filters and the topology. In particular, we
* define the sequential closure of a set and prove that it's contained in the closure,
* define a type class "sequential_space" in which closure and sequential closure agree,
* define sequential continuity and show that it coincides with continuity in sequential spaces,
* provide an instance that shows that every first-countable (and in particular metric) space is
  a sequential space.
* define sequential compactness, prove that compactness implies sequential compactness in first
  countable spaces, and prove they are equivalent for uniform spaces having a countable uniformity
  basis (in particular metric spaces).
-/

open set function filter bornology
open_locale topological_space filter

variables {Î± : Type*} {Î² : Type*}

local notation x ` âŸ¶ ` p := tendsto x at_top (ğ“ p)

/-! ### Sequential closures, sequential continuity, and sequential spaces. -/
section topological_space
variables [topological_space Î±] [topological_space Î²]

/-- The sequential closure of a set `s : set Î±` in a topological space `Î±` is the set of all `p âˆˆ Î±`
which arise as limit of sequences in `s`. Note that it does not need to be sequentially closed. -/
def seq_closure (s : set Î±) : set Î± :=
{p | âˆƒ x : â„• â†’ Î±, (âˆ€ n : â„•, x n âˆˆ s) âˆ§ (x âŸ¶ p)}

lemma subset_seq_closure {s : set Î±} : s âŠ† seq_closure s :=
Î» p hp, âŸ¨const â„• p, Î» _, hp, tendsto_const_nhdsâŸ©

/-- The sequential closure of a set is contained in the closure of that set.
The converse is not true. -/
lemma seq_closure_subset_closure {s : set Î±} : seq_closure s âŠ† closure s :=
Î» p âŸ¨x, xM, xpâŸ©, mem_closure_of_tendsto xp (univ_mem' xM)

/-- A set `s` is sequentially closed if for any converging sequence `x n` of elements of `s`,
the limit belongs to `s` as well. -/
def is_seq_closed (s : set Î±) : Prop :=
âˆ€ â¦ƒx : â„• â†’ Î±â¦„ â¦ƒp : Î±â¦„, (âˆ€ n, x n âˆˆ s) â†’ (x âŸ¶ p) â†’ p âˆˆ s

/-- The sequential closure of a sequentially closed set is the set itself. -/
lemma is_seq_closed.seq_closure_eq {s : set Î±} (hs : is_seq_closed s) :
  seq_closure s = s :=
subset.antisymm (Î» p âŸ¨x, hx, hpâŸ©, hs hx hp) subset_seq_closure

/-- A set is sequentially closed if it is closed. -/
protected lemma is_closed.is_seq_closed {s : set Î±} (hc : is_closed s) : is_seq_closed s :=
Î» u x hu hx, hc.mem_of_tendsto hx (eventually_of_forall hu)

/-- A sequential space is a space in which 'sequences are enough to probe the topology'. This can be
 formalised by demanding that the sequential closure and the closure coincide. The following
 statements show that other topological properties can be deduced from sequences in sequential
 spaces. -/
class sequential_space (Î± : Type*) [topological_space Î±] : Prop :=
(is_closed_of_seq : âˆ€ s : set Î±, is_seq_closed s â†’ is_closed s)

/-- In a sequential space, a sequentially closed set is closed. -/
protected lemma is_seq_closed.is_closed [sequential_space Î±] {s : set Î±} (hs : is_seq_closed s) :
  is_closed s :=
sequential_space.is_closed_of_seq s hs

/-- In a sequential space, a set is closed iff it's sequentially closed. -/
lemma is_seq_closed_iff_is_closed [sequential_space Î±] {M : set Î±} :
  is_seq_closed M â†” is_closed M :=
âŸ¨is_seq_closed.is_closed, is_closed.is_seq_closedâŸ©

/-
TODO: add FrÃ©chet-Urysohn spaces and this lemma?
lemma tendsto_nhds_iff_seq_tendsto [frechet_urysohn_space Î±] {f : Î± â†’ Î²} {a : Î±} {b : Î²} :
  tendsto f (ğ“ a) (ğ“ b) â†” âˆ€ u : â„• â†’ Î±, (u âŸ¶ a) â†’ (f âˆ˜ u âŸ¶ b) :=
begin
  refine âŸ¨Î» hf u hu, hf.comp hu,
    Î» h, ((nhds_basis_closeds _).tendsto_iff (nhds_basis_closeds _)).2 _âŸ©,
  rintro s âŸ¨hbs, hscâŸ©,
  refine âŸ¨closure (f â»Â¹' s), âŸ¨mt _ hbs, is_closed_closureâŸ©, Î» x, mt $ Î» hx, subset_closure hxâŸ©,
  rw [â† sequential_space.sequential_closure_eq_closure],
  rintro âŸ¨u, hus, huâŸ©,
  exact hsc.mem_of_tendsto (h u hu) (eventually_of_forall hus)
end
-/

/-- A function between topological spaces is sequentially continuous if it commutes with limit of
 convergent sequences. -/
def seq_continuous (f : Î± â†’ Î²) : Prop :=
âˆ€ â¦ƒx : â„• â†’ Î±â¦„ â¦ƒp : Î±â¦„, (x âŸ¶ p) â†’ (f âˆ˜ x âŸ¶ f p)

/-- The preimage of a sequentially closed set under a sequentially continuous map is sequentially
closed. -/
lemma is_seq_closed.preimage {f : Î± â†’ Î²} {s : set Î²} (hs : is_seq_closed s)
  (hf : seq_continuous f) :
  is_seq_closed (f â»Â¹' s) :=
Î» x p hx hp, hs hx (hf hp)

/- A continuous function is sequentially continuous. -/
protected lemma continuous.seq_continuous {f : Î± â†’ Î²} (hf : continuous f) :
  seq_continuous f :=
Î» x p hx, (hf.tendsto p).comp hx

/-- A sequentially continuous function defined on a sequential space is continuous. -/
protected lemma seq_continuous.continuous [sequential_space Î±] {f : Î± â†’ Î²} (hf : seq_continuous f) :
  continuous f :=
continuous_iff_is_closed.mpr $ Î» s hs, (hs.is_seq_closed.preimage hf).is_closed

/-- If the domain of a function is a sequential space, then continuity of this function is
equivalent to its sequential continuity. -/
lemma continuous_iff_seq_continuous [sequential_space Î±] {f : Î± â†’ Î²} :
  continuous f â†” seq_continuous f :=
âŸ¨continuous.seq_continuous, seq_continuous.continuousâŸ©

end topological_space

namespace topological_space

namespace first_countable_topology

variables [topological_space Î±] [first_countable_topology Î±]

/-- Every first-countable space is sequential. -/
@[priority 100] -- see Note [lower instance priority]
instance : sequential_space Î± :=
begin
  -- Consider a closed set `s`
  refine âŸ¨Î» s hs, _âŸ©,
  -- It suffices to show that `s` contains every cluster point of `ğ“Ÿ s`.
  -- Consider a cluster point `p` of `s`.
  refine is_closed_iff_cluster_pt.mpr (Î» p hp, _),
  haveI := hp.ne_bot,
  -- Fix an antitone basis `U : â„• â†’ set Î±` of `ğ“ p`, then choose a sequence `x : â„• â†’ Î±` such that
  -- `x n âˆˆ U n âˆ© s`.
  rcases exists_antitone_basis (ğ“ p) with âŸ¨U, hUâŸ©,
  have : âˆ€ n, (U n âˆ© s).nonempty,
    from Î» n, filter.nonempty_of_mem ((hU.to_has_basis.inf_principal _).mem_of_mem trivial),
  choose x hxU hxs,
  -- Then `x âŸ¶ p`, thus `p âˆˆ s`
  exact hs hxs (hU.tendsto hxU)
end


end first_countable_topology

end topological_space

section seq_compact
open topological_space topological_space.first_countable_topology
variables [topological_space Î±]

/-- A set `s` is sequentially compact if every sequence taking values in `s` has a
converging subsequence. -/
def is_seq_compact (s : set Î±) :=
âˆ€ â¦ƒu : â„• â†’ Î±â¦„, (âˆ€ n, u n âˆˆ s) â†’ âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x)

/-- A space `Î±` is sequentially compact if every sequence in `Î±` has a
converging subsequence. -/
class seq_compact_space (Î± : Type*) [topological_space Î±] : Prop :=
(seq_compact_univ : is_seq_compact (univ : set Î±))

lemma is_seq_compact.subseq_of_frequently_in {s : set Î±} (hs : is_seq_compact s) {u : â„• â†’ Î±}
  (hu : âˆƒá¶  n in at_top, u n âˆˆ s) :
  âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
let âŸ¨Ïˆ, hÏˆ, huÏˆâŸ© := extraction_of_frequently_at_top hu, âŸ¨x, x_in, Ï†, hÏ†, hâŸ© := hs huÏˆ in
âŸ¨x, x_in, Ïˆ âˆ˜ Ï†, hÏˆ.comp hÏ†, hâŸ©

lemma seq_compact_space.tendsto_subseq [seq_compact_space Î±] (u : â„• â†’ Î±) :
  âˆƒ x (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
let âŸ¨x, _, Ï†, mono, hâŸ© := seq_compact_space.seq_compact_univ (Î» n, mem_univ (u n)) in
âŸ¨x, Ï†, mono, hâŸ©

section first_countable_topology
variables [first_countable_topology Î±]
open topological_space.first_countable_topology

protected lemma is_compact.is_seq_compact {s : set Î±} (hs : is_compact s) : is_seq_compact s :=
Î» u u_in,
let âŸ¨x, x_in, hxâŸ© := @hs (map u at_top) _ (le_principal_iff.mpr (mem_map.2 $ univ_mem' u_in))
in âŸ¨x, x_in, tendsto_subseq hxâŸ©

lemma is_compact.tendsto_subseq' {s : set Î±} {u : â„• â†’ Î±} (hs : is_compact s)
  (hu : âˆƒá¶  n in at_top, u n âˆˆ s) :
  âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
hs.is_seq_compact.subseq_of_frequently_in hu

lemma is_compact.tendsto_subseq {s : set Î±} {u : â„• â†’ Î±} (hs : is_compact s) (hu : âˆ€ n, u n âˆˆ s) :
  âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
hs.is_seq_compact hu

@[priority 100] -- see Note [lower instance priority]
instance first_countable_topology.seq_compact_of_compact [compact_space Î±] : seq_compact_space Î± :=
âŸ¨compact_univ.is_seq_compactâŸ©

lemma compact_space.tendsto_subseq [compact_space Î±] (u : â„• â†’ Î±) :
  âˆƒ x (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
seq_compact_space.tendsto_subseq u

end first_countable_topology
end seq_compact

section uniform_space_seq_compact

open_locale uniformity
open uniform_space prod

variables [uniform_space Î²] {s : set Î²}

lemma is_seq_compact.exists_tendsto_of_frequently_mem (hs : is_seq_compact s) {u : â„• â†’ Î²}
  (hu : âˆƒá¶  n in at_top, u n âˆˆ s) (huc : cauchy_seq u) :
  âˆƒ x âˆˆ s, u âŸ¶ x :=
begin
  rcases hs.subseq_of_frequently_in hu with âŸ¨x, hxs, Ï†, Ï†_mono, hxâŸ©,
  refine âŸ¨x, hxs, le_nhds_of_cauchy_adhp huc ((cluster_pt.of_le_nhds hx).mono _)âŸ©,
  rw [â† filter.map_map],
  exact map_mono Ï†_mono.tendsto_at_top
end

lemma is_seq_compact.exists_tendsto (hs : is_seq_compact s) {u : â„• â†’ Î²} (hu : âˆ€ n, u n âˆˆ s)
  (huc : cauchy_seq u) : âˆƒ x âˆˆ s, u âŸ¶ x :=
hs.exists_tendsto_of_frequently_mem (frequently_of_forall hu) huc

/-- A sequentially compact set in a uniform space is totally bounded. -/
protected lemma is_seq_compact.totally_bounded (h : is_seq_compact s) : totally_bounded s :=
begin
  intros V V_in,
  unfold is_seq_compact at h,
  contrapose! h,
  obtain âŸ¨u, u_in, huâŸ© : âˆƒ u : â„• â†’ Î², (âˆ€ n, u n âˆˆ s) âˆ§ âˆ€ n m, m < n â†’ u m âˆ‰ ball (u n) V,
  { simp only [not_subset, mem_Unionâ‚‚, not_exists, exists_prop] at h,
    simpa only [forall_and_distrib, ball_image_iff, not_and] using seq_of_forall_finite_exists h },
  refine âŸ¨u, u_in, Î» x x_in Ï† hÏ† huÏ†, _âŸ©,
  obtain âŸ¨N, hNâŸ© : âˆƒ N, âˆ€ p q, p â‰¥ N â†’ q â‰¥ N â†’ (u (Ï† p), u (Ï† q)) âˆˆ V,
    from huÏ†.cauchy_seq.mem_entourage V_in,
  exact hu (Ï† $ N + 1) (Ï† N) (hÏ† $ lt_add_one N) (hN (N + 1) N N.le_succ le_rfl)
end

variables [is_countably_generated (ğ“¤ Î²)]

/-- A sequentially compact set in a uniform set with countably generated uniformity filter
is complete. -/
protected lemma is_seq_compact.is_complete (hs : is_seq_compact s) : is_complete s :=
begin
  intros l hl hls,
  haveI := hl.1,
  have Hâ‚‚ : l Ã—á¶  l â‰¤ ğ“¤ Î² âŠ“ ğ“Ÿ (s Ã—Ë¢ s),
  { rw â† prod_principal_principal,
    exact le_inf hl.2 (prod_mono hls hls) },
  rcases exists_antitone_basis (ğ“¤ Î²) with âŸ¨V, hVâŸ©,
  choose W hW hWV using Î» n, comp_mem_uniformity_sets (hV.mem n),
  obtain âŸ¨t, ht_anti, htl, htW, htV, htsâŸ© : âˆƒ t : â„• â†’ set Î², antitone t âˆ§ (âˆ€ n, t n âˆˆ l) âˆ§
    (âˆ€ n, t n Ã—Ë¢ t n âŠ† W n) âˆ§ (âˆ€ n, t n Ã—Ë¢ t n âŠ† V n) âˆ§ (âˆ€ n, t n âŠ† s),
  { have : âˆ€ n, âˆƒ t âˆˆ l, t Ã—Ë¢ t âŠ† W n âˆ§ t Ã—Ë¢ t âŠ† V n âˆ§ t âŠ† s,
    { simpa only [l.basis_sets.prod_self.mem_iff, true_implies_iff, subset_inter_iff,
        prod_self_subset_prod_self, and.assoc]
        using Î» n, Hâ‚‚ (inter_mem_inf (inter_mem (hW n) (hV.mem n)) subset.rfl) },
    choose t htl htW htV hts,
    have : âˆ€ n, (â‹‚ k â‰¤ n, t k) âŠ† t n, from Î» n, Interâ‚‚_subset _ le_rfl,
    exact âŸ¨Î» n, â‹‚ k â‰¤ n, t k, Î» m n h, bInter_subset_bInter_left (Î» k (hk : k â‰¤ m), hk.trans h),
      Î» n, (bInter_mem (finite_le_nat n)).2 (Î» k hk, htl k),
      Î» n, (prod_mono (this n) (this n)).trans (htW n),
      Î» n, (prod_mono (this n) (this n)).trans (htV n), Î» n, (this n).trans (hts n)âŸ© },
  choose u hu using Î» n, filter.nonempty_of_mem (htl n),
  have huc : cauchy_seq u,
    from hV.to_has_basis.cauchy_seq_iff.2
      (Î» N hN, âŸ¨N, Î» m hm n hn, htV N (mk_mem_prod (ht_anti hm (hu _)) (ht_anti hn (hu _)))âŸ©),
  rcases hs.exists_tendsto (Î» n, hts n (hu n)) huc with âŸ¨x, hxs, hxâŸ©,
  refine âŸ¨x, hxs, (nhds_basis_uniformity' hV.to_has_basis).ge_iff.2 $ Î» N hN, _âŸ©,
  obtain âŸ¨n, hNn, hnâŸ© : âˆƒ n, N â‰¤ n âˆ§ u n âˆˆ ball x (W N),
    from ((eventually_ge_at_top N).and (hx $ ball_mem_nhds x (hW N))).exists,
  refine mem_of_superset (htl n) (Î» y hy, hWV N âŸ¨u n, _, htW N âŸ¨_, _âŸ©âŸ©),
  exacts [hn, ht_anti hNn (hu n), ht_anti hNn hy]
end

/-- If `ğ“¤ Î²` is countably generated, then any sequentially compact set is compact. -/
protected lemma is_seq_compact.is_compact (hs : is_seq_compact s) : is_compact s :=
compact_iff_totally_bounded_complete.2 âŸ¨hs.totally_bounded, hs.is_completeâŸ©

/-- A version of Bolzano-Weistrass: in a uniform space with countably generated uniformity filter
(e.g., in a metric space), a set is compact if and only if it is sequentially compact. -/
protected lemma uniform_space.compact_iff_seq_compact : is_compact s â†” is_seq_compact s :=
âŸ¨Î» H, H.is_seq_compact, Î» H, H.is_compactâŸ©

lemma uniform_space.compact_space_iff_seq_compact_space : compact_space Î² â†” seq_compact_space Î² :=
have key : is_compact (univ : set Î²) â†” is_seq_compact univ := uniform_space.compact_iff_seq_compact,
âŸ¨Î» âŸ¨hâŸ©, âŸ¨key.mp hâŸ©, Î» âŸ¨hâŸ©, âŸ¨key.mpr hâŸ©âŸ©

end uniform_space_seq_compact

section metric_seq_compact

variables [metric_space Î²] {s : set Î²}
open metric

/-- A version of **Bolzano-Weistrass**: in a proper metric space (eg. $â„^n$),
every bounded sequence has a converging subsequence. This version assumes only
that the sequence is frequently in some bounded set. -/
lemma tendsto_subseq_of_frequently_bounded [proper_space Î²] (hs : bounded s)
  {u : â„• â†’ Î²} (hu : âˆƒá¶  n in at_top, u n âˆˆ s) :
  âˆƒ b âˆˆ closure s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ b) :=
have hcs : is_seq_compact (closure s), from hs.is_compact_closure.is_seq_compact,
have hu' : âˆƒá¶  n in at_top, u n âˆˆ closure s, from hu.mono (Î» n hn, subset_closure hn),
hcs.subseq_of_frequently_in hu'

/-- A version of Bolzano-Weistrass: in a proper metric space (eg. $â„^n$),
every bounded sequence has a converging subsequence. -/
lemma tendsto_subseq_of_bounded [proper_space Î²] (hs : bounded s)
  {u : â„• â†’ Î²} (hu : âˆ€ n, u n âˆˆ s) :
  âˆƒ b âˆˆ closure s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ b) :=
tendsto_subseq_of_frequently_bounded hs $ frequently_of_forall hu

end metric_seq_compact
