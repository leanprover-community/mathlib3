/-
Copyright (c) 2018 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, Patrick Massot, Yury Kudryashov
-/
import topology.subset_properties
import topology.metric_space.basic

/-!
# Sequences in topological spaces

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define sequences in topological spaces and show how they are related to
filters and the topology.

## Main definitions

### Set operation
* `seq_closure s`: sequential closure of a set, the set of limits of sequences of points of `s`;

### Predicates

* `is_seq_closed s`: predicate saying that a set is sequentially closed, i.e., `seq_closure s âŠ† s`;
* `seq_continuous f`: predicate saying that a function is sequentially continuous, i.e.,
  for any sequence `u : â„• â†’ X` that converges to a point `x`, the sequence `f âˆ˜ u` converges to
  `f x`;
* `is_seq_compact s`: predicate saying that a set is sequentially compact, i.e., every sequence
  taking values in `s` has a converging subsequence.

### Type classes

* `frechet_urysohn_space X`: a typeclass saying that a topological space is a *FrÃ©chet-Urysohn
  space*, i.e., the sequential closure of any set is equal to its closure.
* `sequential_space X`: a typeclass saying that a topological space is a *sequential space*, i.e.,
  any sequentially closed set in this space is closed. This condition is weaker than being a
  FrÃ©chet-Urysohn space.
* `seq_compact_space X`: a typeclass saying that a topological space is sequentially compact, i.e.,
  every sequence in `X` has a converging subsequence.

## Main results

* `seq_closure_subset_closure`: closure of a set includes its sequential closure;
* `is_closed.is_seq_closed`: a closed set is sequentially closed;
* `is_seq_closed.seq_closure_eq`: sequential closure of a sequentially closed set `s` is equal
  to `s`;
* `seq_closure_eq_closure`: in a FrÃ©chet-Urysohn space, the sequential closure of a set is equal to
  its closure;
* `tendsto_nhds_iff_seq_tendsto`, `frechet_urysohn_space.of_seq_tendsto_imp_tendsto`: a topological
  space is a FrÃ©chet-Urysohn space if and only if sequential convergence implies convergence;
* `topological_space.first_countable_topology.frechet_urysohn_space`: every topological space with
  first countable topology is a FrÃ©chet-Urysohn space;
* `frechet_urysohn_space.to_sequential_space`: every FrÃ©chet-Urysohn space is a sequential space;
* `is_seq_compact.is_compact`: a sequentially compact set in a uniform space with countably
  generated uniformity is compact.

## Tags

sequentially closed, sequentially compact, sequential space
-/

open set function filter topological_space
open_locale topology filter

variables {X Y : Type*}

/-! ### Sequential closures, sequential continuity, and sequential spaces. -/
section topological_space
variables [topological_space X] [topological_space Y]

/-- The sequential closure of a set `s : set X` in a topological space `X` is the set of all `a : X`
which arise as limit of sequences in `s`. Note that the sequential closure of a set is not
guaranteed to be sequentially closed. -/
def seq_closure (s : set X) : set X :=
{a | âˆƒ x : â„• â†’ X, (âˆ€ n : â„•, x n âˆˆ s) âˆ§ tendsto x at_top (ğ“ a)}

lemma subset_seq_closure {s : set X} : s âŠ† seq_closure s :=
Î» p hp, âŸ¨const â„• p, Î» _, hp, tendsto_const_nhdsâŸ©

/-- The sequential closure of a set is contained in the closure of that set.
The converse is not true. -/
lemma seq_closure_subset_closure {s : set X} : seq_closure s âŠ† closure s :=
Î» p âŸ¨x, xM, xpâŸ©, mem_closure_of_tendsto xp (univ_mem' xM)

/-- A set `s` is sequentially closed if for any converging sequence `x n` of elements of `s`, the
limit belongs to `s` as well. Note that the sequential closure of a set is not guaranteed to be
sequentially closed. -/
def is_seq_closed (s : set X) : Prop :=
âˆ€ â¦ƒx : â„• â†’ Xâ¦„ â¦ƒp : Xâ¦„, (âˆ€ n, x n âˆˆ s) â†’ tendsto x at_top (ğ“ p) â†’ p âˆˆ s

/-- The sequential closure of a sequentially closed set is the set itself. -/
lemma is_seq_closed.seq_closure_eq {s : set X} (hs : is_seq_closed s) :
  seq_closure s = s :=
subset.antisymm (Î» p âŸ¨x, hx, hpâŸ©, hs hx hp) subset_seq_closure

/-- If a set is equal to its sequential closure, then it is sequentially closed. -/
lemma is_seq_closed_of_seq_closure_eq {s : set X} (hs : seq_closure s = s) :
  is_seq_closed s :=
Î» x p hxs hxp, hs â–¸ âŸ¨x, hxs, hxpâŸ©

/-- A set is sequentially closed iff it is equal to its sequential closure. -/
lemma is_seq_closed_iff {s : set X} :
  is_seq_closed s â†” seq_closure s = s :=
âŸ¨is_seq_closed.seq_closure_eq, is_seq_closed_of_seq_closure_eqâŸ©

/-- A set is sequentially closed if it is closed. -/
protected lemma is_closed.is_seq_closed {s : set X} (hc : is_closed s) : is_seq_closed s :=
Î» u x hu hx, hc.mem_of_tendsto hx (eventually_of_forall hu)

/-- A topological space is called a *FrÃ©chet-Urysohn space*, if the sequential closure of any set
is equal to its closure. Since one of the inclusions is trivial, we require only the non-trivial one
in the definition. -/
class frechet_urysohn_space (X : Type*) [topological_space X] : Prop :=
(closure_subset_seq_closure : âˆ€ s : set X, closure s âŠ† seq_closure s)

lemma seq_closure_eq_closure [frechet_urysohn_space X] (s : set X) :
  seq_closure s = closure s :=
seq_closure_subset_closure.antisymm $ frechet_urysohn_space.closure_subset_seq_closure s

/-- In a FrÃ©chet-Urysohn space, a point belongs to the closure of a set iff it is a limit
of a sequence taking values in this set. -/
lemma mem_closure_iff_seq_limit [frechet_urysohn_space X] {s : set X} {a : X} :
  a âˆˆ closure s â†” âˆƒ x : â„• â†’ X, (âˆ€ n : â„•, x n âˆˆ s) âˆ§ tendsto x at_top (ğ“ a) :=
by { rw [â† seq_closure_eq_closure], refl }

/-- If the domain of a function `f : Î± â†’ Î²` is a FrÃ©chet-Urysohn space, then convergence
is equivalent to sequential convergence. See also `filter.tendsto_iff_seq_tendsto` for a version
that works for any pair of filters assuming that the filter in the domain is countably generated.

This property is equivalent to the definition of `frechet_urysohn_space`, see
`frechet_urysohn_space.of_seq_tendsto_imp_tendsto`. -/
lemma tendsto_nhds_iff_seq_tendsto [frechet_urysohn_space X] {f : X â†’ Y} {a : X} {b : Y} :
  tendsto f (ğ“ a) (ğ“ b) â†” âˆ€ u : â„• â†’ X, tendsto u at_top (ğ“ a) â†’ tendsto (f âˆ˜ u) at_top (ğ“ b) :=
begin
  refine âŸ¨Î» hf u hu, hf.comp hu,
    Î» h, ((nhds_basis_closeds _).tendsto_iff (nhds_basis_closeds _)).2 _âŸ©,
  rintro s âŸ¨hbs, hscâŸ©,
  refine âŸ¨closure (f â»Â¹' s), âŸ¨mt _ hbs, is_closed_closureâŸ©, Î» x, mt $ Î» hx, subset_closure hxâŸ©,
  rw [â† seq_closure_eq_closure],
  rintro âŸ¨u, hus, huâŸ©,
  exact hsc.mem_of_tendsto (h u hu) (eventually_of_forall hus)
end

/-- An alternative construction for `frechet_urysohn_space`: if sequential convergence implies
convergence, then the space is a FrÃ©chet-Urysohn space. -/
lemma frechet_urysohn_space.of_seq_tendsto_imp_tendsto
  (h : âˆ€ (f : X â†’ Prop) (a : X),
    (âˆ€ u : â„• â†’ X, tendsto u at_top (ğ“ a) â†’ tendsto (f âˆ˜ u) at_top (ğ“ (f a))) â†’ continuous_at f a) :
  frechet_urysohn_space X :=
begin
  refine âŸ¨Î» s x hcx, _âŸ©,
  specialize h (âˆ‰ s) x,
  by_cases hx : x âˆˆ s, { exact subset_seq_closure hx },
  simp_rw [(âˆ˜), continuous_at, hx, not_false_iff, nhds_true, tendsto_pure, eq_true_iff,
    â† mem_compl_iff, eventually_mem_set, â† mem_interior_iff_mem_nhds, interior_compl] at h,
  rw [mem_compl_iff, imp_not_comm] at h,
  simp only [not_forall, not_eventually, mem_compl_iff, not_not] at h,
  rcases h hcx with âŸ¨u, hux, husâŸ©,
  rcases extraction_of_frequently_at_top hus with âŸ¨Ï†, Ï†_mono, hÏ†âŸ©,
  exact âŸ¨u âˆ˜ Ï†, hÏ†, hux.comp Ï†_mono.tendsto_at_topâŸ©
end

/-- Every first-countable space is a FrÃ©chet-Urysohn space. -/
@[priority 100] -- see Note [lower instance priority]
instance topological_space.first_countable_topology.frechet_urysohn_space
  [first_countable_topology X] : frechet_urysohn_space X :=
frechet_urysohn_space.of_seq_tendsto_imp_tendsto $ Î» f a, tendsto_iff_seq_tendsto.2

/-- A topological space is said to be a *sequential space* if any sequentially closed set in this
space is closed. This condition is weaker than being a FrÃ©chet-Urysohn space. -/
class sequential_space (X : Type*) [topological_space X] : Prop :=
(is_closed_of_seq : âˆ€ s : set X, is_seq_closed s â†’ is_closed s)

/-- Every FrÃ©chet-Urysohn space is a sequential space. -/
@[priority 100] -- see Note [lower instance priority]
instance frechet_urysohn_space.to_sequential_space [frechet_urysohn_space X] :
  sequential_space X :=
âŸ¨Î» s hs, by rw [â† closure_eq_iff_is_closed, â† seq_closure_eq_closure, hs.seq_closure_eq]âŸ©

/-- In a sequential space, a sequentially closed set is closed. -/
protected lemma is_seq_closed.is_closed [sequential_space X] {s : set X} (hs : is_seq_closed s) :
  is_closed s :=
sequential_space.is_closed_of_seq s hs

/-- In a sequential space, a set is closed iff it's sequentially closed. -/
lemma is_seq_closed_iff_is_closed [sequential_space X] {M : set X} :
  is_seq_closed M â†” is_closed M :=
âŸ¨is_seq_closed.is_closed, is_closed.is_seq_closedâŸ©

/-- A function between topological spaces is sequentially continuous if it commutes with limit of
 convergent sequences. -/
def seq_continuous (f : X â†’ Y) : Prop :=
âˆ€ â¦ƒx : â„• â†’ Xâ¦„ â¦ƒp : Xâ¦„, tendsto x at_top (ğ“ p) â†’ tendsto (f âˆ˜ x) at_top (ğ“ (f p))

/-- The preimage of a sequentially closed set under a sequentially continuous map is sequentially
closed. -/
lemma is_seq_closed.preimage {f : X â†’ Y} {s : set Y} (hs : is_seq_closed s)
  (hf : seq_continuous f) :
  is_seq_closed (f â»Â¹' s) :=
Î» x p hx hp, hs hx (hf hp)

/- A continuous function is sequentially continuous. -/
protected lemma continuous.seq_continuous {f : X â†’ Y} (hf : continuous f) :
  seq_continuous f :=
Î» x p hx, (hf.tendsto p).comp hx

/-- A sequentially continuous function defined on a sequential space is continuous. -/
protected lemma seq_continuous.continuous [sequential_space X] {f : X â†’ Y} (hf : seq_continuous f) :
  continuous f :=
continuous_iff_is_closed.mpr $ Î» s hs, (hs.is_seq_closed.preimage hf).is_closed

/-- If the domain of a function is a sequential space, then continuity of this function is
equivalent to its sequential continuity. -/
lemma continuous_iff_seq_continuous [sequential_space X] {f : X â†’ Y} :
  continuous f â†” seq_continuous f :=
âŸ¨continuous.seq_continuous, seq_continuous.continuousâŸ©

lemma quotient_map.sequential_space [sequential_space X] {f : X â†’ Y} (hf : quotient_map f) :
  sequential_space Y :=
âŸ¨Î» s hs, hf.is_closed_preimage.mp $ (hs.preimage $ hf.continuous.seq_continuous).is_closedâŸ©

/-- The quotient of a sequential space is a sequential space. -/
instance [sequential_space X] {s : setoid X} : sequential_space (quotient s) :=
quotient_map_quot_mk.sequential_space

end topological_space

section seq_compact
open topological_space topological_space.first_countable_topology
variables [topological_space X]

/-- A set `s` is sequentially compact if every sequence taking values in `s` has a
converging subsequence. -/
def is_seq_compact (s : set X) :=
âˆ€ â¦ƒx : â„• â†’ Xâ¦„, (âˆ€ n, x n âˆˆ s) â†’ âˆƒ (a âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a)

/-- A space `X` is sequentially compact if every sequence in `X` has a
converging subsequence. -/
@[mk_iff] class seq_compact_space (X : Type*) [topological_space X] : Prop :=
(seq_compact_univ : is_seq_compact (univ : set X))

export seq_compact_space (seq_compact_univ)

lemma is_seq_compact.subseq_of_frequently_in {s : set X} (hs : is_seq_compact s) {x : â„• â†’ X}
  (hx : âˆƒá¶  n in at_top, x n âˆˆ s) :
  âˆƒ (a âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
let âŸ¨Ïˆ, hÏˆ, huÏˆâŸ© := extraction_of_frequently_at_top hx, âŸ¨a, a_in, Ï†, hÏ†, hâŸ© := hs huÏˆ in
âŸ¨a, a_in, Ïˆ âˆ˜ Ï†, hÏˆ.comp hÏ†, hâŸ©

lemma seq_compact_space.tendsto_subseq [seq_compact_space X] (x : â„• â†’ X) :
  âˆƒ a (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
let âŸ¨a, _, Ï†, mono, hâŸ© := seq_compact_univ (Î» n, mem_univ (x n)) in âŸ¨a, Ï†, mono, hâŸ©

section first_countable_topology
variables [first_countable_topology X]
open topological_space.first_countable_topology

protected lemma is_compact.is_seq_compact {s : set X} (hs : is_compact s) : is_seq_compact s :=
Î» x x_in, let âŸ¨a, a_in, haâŸ© := hs (tendsto_principal.mpr (eventually_of_forall x_in))
in âŸ¨a, a_in, tendsto_subseq haâŸ©

lemma is_compact.tendsto_subseq' {s : set X} {x : â„• â†’ X} (hs : is_compact s)
  (hx : âˆƒá¶  n in at_top, x n âˆˆ s) :
  âˆƒ (a âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
hs.is_seq_compact.subseq_of_frequently_in hx

lemma is_compact.tendsto_subseq {s : set X} {x : â„• â†’ X} (hs : is_compact s) (hx : âˆ€ n, x n âˆˆ s) :
  âˆƒ (a âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
hs.is_seq_compact hx

@[priority 100] -- see Note [lower instance priority]
instance first_countable_topology.seq_compact_of_compact [compact_space X] : seq_compact_space X :=
âŸ¨is_compact_univ.is_seq_compactâŸ©

lemma compact_space.tendsto_subseq [compact_space X] (x : â„• â†’ X) :
  âˆƒ a (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
seq_compact_space.tendsto_subseq x

end first_countable_topology
end seq_compact

section uniform_space_seq_compact

open_locale uniformity
open uniform_space prod

variables [uniform_space X] {s : set X}

lemma is_seq_compact.exists_tendsto_of_frequently_mem (hs : is_seq_compact s) {u : â„• â†’ X}
  (hu : âˆƒá¶  n in at_top, u n âˆˆ s) (huc : cauchy_seq u) :
  âˆƒ x âˆˆ s, tendsto u at_top (ğ“ x) :=
let âŸ¨x, hxs, Ï†, Ï†_mono, hxâŸ© := hs.subseq_of_frequently_in hu
in âŸ¨x, hxs, tendsto_nhds_of_cauchy_seq_of_subseq huc Ï†_mono.tendsto_at_top hxâŸ©

lemma is_seq_compact.exists_tendsto (hs : is_seq_compact s) {u : â„• â†’ X} (hu : âˆ€ n, u n âˆˆ s)
  (huc : cauchy_seq u) : âˆƒ x âˆˆ s, tendsto u at_top (ğ“ x) :=
hs.exists_tendsto_of_frequently_mem (frequently_of_forall hu) huc

/-- A sequentially compact set in a uniform space is totally bounded. -/
protected lemma is_seq_compact.totally_bounded (h : is_seq_compact s) : totally_bounded s :=
begin
  intros V V_in,
  unfold is_seq_compact at h,
  contrapose! h,
  obtain âŸ¨u, u_in, huâŸ© : âˆƒ u : â„• â†’ X, (âˆ€ n, u n âˆˆ s) âˆ§ âˆ€ n m, m < n â†’ u m âˆ‰ ball (u n) V,
  { simp only [not_subset, mem_Unionâ‚‚, not_exists, exists_prop] at h,
    simpa only [forall_and_distrib, ball_image_iff, not_and] using seq_of_forall_finite_exists h },
  refine âŸ¨u, u_in, Î» x x_in Ï† hÏ† huÏ†, _âŸ©,
  obtain âŸ¨N, hNâŸ© : âˆƒ N, âˆ€ p q, p â‰¥ N â†’ q â‰¥ N â†’ (u (Ï† p), u (Ï† q)) âˆˆ V,
    from huÏ†.cauchy_seq.mem_entourage V_in,
  exact hu (Ï† $ N + 1) (Ï† N) (hÏ† $ lt_add_one N) (hN (N + 1) N N.le_succ le_rfl)
end

variables [is_countably_generated (ğ“¤ X)]

/-- A sequentially compact set in a uniform set with countably generated uniformity filter
is complete. -/
protected lemma is_seq_compact.is_complete (hs : is_seq_compact s) : is_complete s :=
begin
  intros l hl hls,
  haveI := hl.1,
  rcases exists_antitone_basis (ğ“¤ X) with âŸ¨V, hVâŸ©,
  choose W hW hWV using Î» n, comp_mem_uniformity_sets (hV.mem n),
  have hWV' : âˆ€ n, W n âŠ† V n, from Î» n âŸ¨x, yâŸ© hx, @hWV n (x, y) âŸ¨x, refl_mem_uniformity $ hW _, hxâŸ©,
  obtain âŸ¨t, ht_anti, htl, htW, htsâŸ© : âˆƒ t : â„• â†’ set X, antitone t âˆ§ (âˆ€ n, t n âˆˆ l) âˆ§
    (âˆ€ n, t n Ã—Ë¢ t n âŠ† W n) âˆ§ (âˆ€ n, t n âŠ† s),
  { have : âˆ€ n, âˆƒ t âˆˆ l, t Ã—Ë¢ t âŠ† W n âˆ§ t âŠ† s,
    { rw [le_principal_iff] at hls,
      have : âˆ€ n, W n âˆ© s Ã—Ë¢ s âˆˆ l Ã—á¶  l := Î» n, inter_mem (hl.2 (hW n)) (prod_mem_prod hls hls),
      simpa only [l.basis_sets.prod_self.mem_iff, true_implies_iff, subset_inter_iff,
        prod_self_subset_prod_self, and.assoc] using this },
    choose t htl htW hts,
    have : âˆ€ n, (â‹‚ k â‰¤ n, t k) âŠ† t n, from Î» n, Interâ‚‚_subset _ le_rfl,
    exact âŸ¨Î» n, â‹‚ k â‰¤ n, t k, Î» m n h, bInter_subset_bInter_left (Î» k (hk : k â‰¤ m), hk.trans h),
      Î» n, (bInter_mem (finite_le_nat n)).2 (Î» k hk, htl k),
      Î» n, (prod_mono (this n) (this n)).trans (htW n), Î» n, (this n).trans (hts n)âŸ© },
  choose u hu using Î» n, filter.nonempty_of_mem (htl n),
  have huc : cauchy_seq u := hV.to_has_basis.cauchy_seq_iff.2
    (Î» N hN, âŸ¨N, Î» m hm n hn, hWV' _ $ @htW N (_, _) âŸ¨ht_anti hm (hu _), (ht_anti hn (hu _))âŸ©âŸ©),
  rcases hs.exists_tendsto (Î» n, hts n (hu n)) huc with âŸ¨x, hxs, hxâŸ©,
  refine âŸ¨x, hxs, (nhds_basis_uniformity' hV.to_has_basis).ge_iff.2 $ Î» N hN, _âŸ©,
  obtain âŸ¨n, hNn, hnâŸ© : âˆƒ n, N â‰¤ n âˆ§ u n âˆˆ ball x (W N),
    from ((eventually_ge_at_top N).and (hx $ ball_mem_nhds x (hW N))).exists,
  refine mem_of_superset (htl n) (Î» y hy, hWV N âŸ¨u n, _, htW N âŸ¨_, _âŸ©âŸ©),
  exacts [hn, ht_anti hNn (hu n), ht_anti hNn hy]
end

/-- If `ğ“¤ Î²` is countably generated, then any sequentially compact set is compact. -/
protected lemma is_seq_compact.is_compact (hs : is_seq_compact s) : is_compact s :=
is_compact_iff_totally_bounded_is_complete.2 âŸ¨hs.totally_bounded, hs.is_completeâŸ©

/-- A version of Bolzano-Weistrass: in a uniform space with countably generated uniformity filter
(e.g., in a metric space), a set is compact if and only if it is sequentially compact. -/
protected lemma uniform_space.is_compact_iff_is_seq_compact : is_compact s â†” is_seq_compact s :=
âŸ¨Î» H, H.is_seq_compact, Î» H, H.is_compactâŸ©

lemma uniform_space.compact_space_iff_seq_compact_space : compact_space X â†” seq_compact_space X :=
by simp only [â† is_compact_univ_iff, seq_compact_space_iff,
  uniform_space.is_compact_iff_is_seq_compact]

end uniform_space_seq_compact

section metric_seq_compact

variables [pseudo_metric_space X]
open metric

lemma seq_compact.lebesgue_number_lemma_of_metric {Î¹ : Sort*} {c : Î¹ â†’ set X}
  {s : set X} (hs : is_seq_compact s) (hcâ‚ : âˆ€ i, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) :
  âˆƒ Î´ > 0, âˆ€ a âˆˆ s, âˆƒ i, ball a Î´ âŠ† c i :=
lebesgue_number_lemma_of_metric hs.is_compact hcâ‚ hcâ‚‚

variables [proper_space X] {s : set X}

/-- A version of **Bolzano-Weistrass**: in a proper metric space (eg. $â„^n$),
every bounded sequence has a converging subsequence. This version assumes only
that the sequence is frequently in some bounded set. -/
lemma tendsto_subseq_of_frequently_bounded (hs : bounded s)
  {x : â„• â†’ X} (hx : âˆƒá¶  n in at_top, x n âˆˆ s) :
  âˆƒ a âˆˆ closure s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
have hcs : is_seq_compact (closure s), from hs.is_compact_closure.is_seq_compact,
have hu' : âˆƒá¶  n in at_top, x n âˆˆ closure s, from hx.mono (Î» n hn, subset_closure hn),
hcs.subseq_of_frequently_in hu'

/-- A version of Bolzano-Weistrass: in a proper metric space (eg. $â„^n$),
every bounded sequence has a converging subsequence. -/
lemma tendsto_subseq_of_bounded (hs : bounded s) {x : â„• â†’ X} (hx : âˆ€ n, x n âˆˆ s) :
  âˆƒ a âˆˆ closure s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
tendsto_subseq_of_frequently_bounded hs $ frequently_of_forall hx

end metric_seq_compact
