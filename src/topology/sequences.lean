/-
Copyright (c) 2018 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, Patrick Massot, Yury Kudryashov
-/
import topology.subset_properties
import topology.metric_space.basic

/-!
# Sequences in topological spaces

In this file we define sequences in topological spaces and show how they are related to
filters and the topology. In particular, we
* define the sequential closure of a set and prove that it's contained in the closure,
* define a type class "sequential_space" in which closure and sequential closure agree,
* define sequential continuity and show that it coincides with continuity in sequential spaces,
* provide an instance that shows that every first-countable (and in particular metric) space is
  a sequential space.
* define sequential compactness, prove that compactness implies sequential compactness in first
  countable spaces, and prove they are equivalent for uniform spaces having a countable uniformity
  basis (in particular metric spaces).
-/

open set function filter bornology topological_space
open_locale topological_space filter

variables {X Y : Type*}

/-! ### Sequential closures, sequential continuity, and sequential spaces. -/
section topological_space
variables [topological_space X] [topological_space Y]

/-- The sequential closure of a set `s : set X` in a topological space `X` is
the set of all `a : X` which arise as limit of sequences in `s`. -/
def seq_closure (s : set X) : set X :=
{a | âˆƒ x : â„• â†’ X, (âˆ€ n : â„•, x n âˆˆ s) âˆ§ tendsto x at_top (ğ“ a)}

lemma subset_seq_closure {s : set X} : s âŠ† seq_closure s :=
Î» p hp, âŸ¨const â„• p, Î» _, hp, tendsto_const_nhdsâŸ©

/-- The sequential closure of a set is contained in the closure of that set.
The converse is not true. -/
lemma seq_closure_subset_closure {s : set X} : seq_closure s âŠ† closure s :=
Î» p âŸ¨x, xM, xpâŸ©, mem_closure_of_tendsto xp (univ_mem' xM)

/-- A set `s` is sequentially closed if for any converging sequence `x n` of elements of `s`,
the limit belongs to `s` as well. -/
def is_seq_closed (s : set X) : Prop :=
âˆ€ â¦ƒx : â„• â†’ Xâ¦„ â¦ƒp : Xâ¦„, (âˆ€ n, x n âˆˆ s) â†’ (tendsto x at_top (ğ“ p)) â†’ p âˆˆ s

/-- The sequential closure of a sequentially closed set is the set itself. -/
lemma is_seq_closed.seq_closure_eq {s : set X} (hs : is_seq_closed s) :
  seq_closure s = s :=
subset.antisymm (Î» p âŸ¨x, hx, hpâŸ©, hs hx hp) subset_seq_closure

/-- A set is sequentially closed if it is closed. -/
protected lemma is_closed.is_seq_closed {s : set X} (hc : is_closed s) : is_seq_closed s :=
Î» u x hu hx, hc.mem_of_tendsto hx (eventually_of_forall hu)

/-- A topological space is called a *FrÃ©chet-Urysohn space*, if the sequential closure of any set
is equal to its closure. Since one of the inclusions is trivial, we require only the non-trivial one
in the definition. -/
class frechet_urysohn_space (X : Type*) [topological_space X] : Prop :=
(closure_subset_seq_closure : âˆ€ s : set X, closure s âŠ† seq_closure s)

lemma seq_closure_eq_closure [frechet_urysohn_space X] (s : set X) :
  seq_closure s = closure s :=
seq_closure_subset_closure.antisymm $ frechet_urysohn_space.closure_subset_seq_closure s

/-- If the domain of a function `f : Î± â†’ Î²` is a FrÃ©chet-Urysohn space, then convergence
is equivalent to sequential convergence. See also `filter.tendsto_iff_seq_tendsto` for a version
that works for any pair of filters assuming that the filter in the domain is countably generated.

This property is equivalent to the definition of `frechet_urysohn_space`, see
`frechet_urysohn_space.of_seq_tendsto_imp_tendsto`. -/
lemma tendsto_nhds_iff_seq_tendsto [frechet_urysohn_space X] {f : X â†’ Y} {a : X} {b : Y} :
  tendsto f (ğ“ a) (ğ“ b) â†” âˆ€ u : â„• â†’ X, tendsto u at_top (ğ“ a) â†’ tendsto (f âˆ˜ u) at_top (ğ“ b) :=
begin
  refine âŸ¨Î» hf u hu, hf.comp hu,
    Î» h, ((nhds_basis_closeds _).tendsto_iff (nhds_basis_closeds _)).2 _âŸ©,
  rintro s âŸ¨hbs, hscâŸ©,
  refine âŸ¨closure (f â»Â¹' s), âŸ¨mt _ hbs, is_closed_closureâŸ©, Î» x, mt $ Î» hx, subset_closure hxâŸ©,
  rw [â† seq_closure_eq_closure],
  rintro âŸ¨u, hus, huâŸ©,
  exact hsc.mem_of_tendsto (h u hu) (eventually_of_forall hus)
end

/-- An alternative construction for `frechet_urysohn_space`: if sequential convergence implies
convergence, then the space is a FrÃ©chet-Urysohn space. -/
lemma frechet_urysohn_space.of_seq_tendsto_imp_tendsto
  (h : âˆ€ (f : X â†’ Prop) (a : X),
    (âˆ€ u : â„• â†’ X, tendsto u at_top (ğ“ a) â†’ tendsto (f âˆ˜ u) at_top (ğ“ (f a))) â†’ continuous_at f a) :
  frechet_urysohn_space X :=
begin
  refine âŸ¨Î» s x hcx, _âŸ©,
  specialize h (âˆ‰ s) x,
  by_cases hx : x âˆˆ s, { exact subset_seq_closure hx },
  simp_rw [(âˆ˜), continuous_at, hx, not_false_iff, nhds_true, tendsto_pure, eq_true,
    â† mem_compl_iff, eventually_mem_set, â† mem_interior_iff_mem_nhds, interior_compl] at h,
  rw [mem_compl_iff, imp_not_comm] at h,
  simp only [not_forall, not_eventually, mem_compl_iff, not_not] at h,
  rcases h hcx with âŸ¨u, hux, husâŸ©,
  rcases extraction_of_frequently_at_top hus with âŸ¨Ï†, Ï†_mono, hÏ†âŸ©,
  exact âŸ¨u âˆ˜ Ï†, hÏ†, hux.comp Ï†_mono.tendsto_at_topâŸ©
end

/-- Every first-countable space is a FrÃ©chet-Urysohn space. -/
@[priority 100] -- see Note [lower instance priority]
instance topological_space.first_countable_topology.frechet_urysohn_space
  [first_countable_topology X] : frechet_urysohn_space X :=
frechet_urysohn_space.of_seq_tendsto_imp_tendsto $ Î» f a, tendsto_iff_seq_tendsto.2

/-- A sequential space is a space in which 'sequences are enough to probe the topology'. This can be
 formalised by demanding that the sequential closure and the closure coincide. The following
 statements show that other topological properties can be deduced from sequences in sequential
 spaces. -/
class sequential_space (X : Type*) [topological_space X] : Prop :=
(is_closed_of_seq : âˆ€ s : set X, is_seq_closed s â†’ is_closed s)

/-- Every FrÃ©chet-Urysohn space is a sequential space. -/
@[priority 100] -- see Note [lower instance priority]
instance frechet_urysohn_space.to_sequential_space [frechet_urysohn_space X] :
  sequential_space X :=
âŸ¨Î» s hs, by rw [â† closure_eq_iff_is_closed, â† seq_closure_eq_closure, hs.seq_closure_eq]âŸ©

/-- In a sequential space, a sequentially closed set is closed. -/
protected lemma is_seq_closed.is_closed [sequential_space X] {s : set X} (hs : is_seq_closed s) :
  is_closed s :=
sequential_space.is_closed_of_seq s hs

/-- In a sequential space, a set is closed iff it's sequentially closed. -/
lemma is_seq_closed_iff_is_closed [sequential_space X] {M : set X} :
  is_seq_closed M â†” is_closed M :=
âŸ¨is_seq_closed.is_closed, is_closed.is_seq_closedâŸ©

/-- A function between topological spaces is sequentially continuous if it commutes with limit of
 convergent sequences. -/
def seq_continuous (f : X â†’ Y) : Prop :=
âˆ€ â¦ƒx : â„• â†’ Xâ¦„ â¦ƒp : Xâ¦„, tendsto x at_top (ğ“ p) â†’ tendsto (f âˆ˜ x) at_top (ğ“ (f p))

/-- The preimage of a sequentially closed set under a sequentially continuous map is sequentially
closed. -/
lemma is_seq_closed.preimage {f : X â†’ Y} {s : set Y} (hs : is_seq_closed s)
  (hf : seq_continuous f) :
  is_seq_closed (f â»Â¹' s) :=
Î» x p hx hp, hs hx (hf hp)

/- A continuous function is sequentially continuous. -/
protected lemma continuous.seq_continuous {f : X â†’ Y} (hf : continuous f) :
  seq_continuous f :=
Î» x p hx, (hf.tendsto p).comp hx

/-- A sequentially continuous function defined on a sequential space is continuous. -/
protected lemma seq_continuous.continuous [sequential_space X] {f : X â†’ Y} (hf : seq_continuous f) :
  continuous f :=
continuous_iff_is_closed.mpr $ Î» s hs, (hs.is_seq_closed.preimage hf).is_closed

/-- If the domain of a function is a sequential space, then continuity of this function is
equivalent to its sequential continuity. -/
lemma continuous_iff_seq_continuous [sequential_space X] {f : X â†’ Y} :
  continuous f â†” seq_continuous f :=
âŸ¨continuous.seq_continuous, seq_continuous.continuousâŸ©

lemma quotient_map.sequential_space [sequential_space X] {f : X â†’ Y} (hf : quotient_map f) :
  sequential_space Y :=
âŸ¨Î» s hs, hf.is_closed_preimage.mp $ (hs.preimage $ hf.continuous.seq_continuous).is_closedâŸ©

/-- The quotient of a sequential space is a sequential space. -/
instance [sequential_space X] {s : setoid X} : sequential_space (quotient s) :=
quotient_map_quot_mk.sequential_space

end topological_space

section seq_compact
open topological_space topological_space.first_countable_topology
variables [topological_space X]

/-- A set `s` is sequentially compact if every sequence taking values in `s` has a
converging subsequence. -/
def is_seq_compact (s : set X) :=
âˆ€ â¦ƒx : â„• â†’ Xâ¦„, (âˆ€ n, x n âˆˆ s) â†’ âˆƒ (a âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a)

/-- A space `X` is sequentially compact if every sequence in `X` has a
converging subsequence. -/
class seq_compact_space (X : Type*) [topological_space X] : Prop :=
(seq_compact_univ : is_seq_compact (univ : set X))

lemma is_seq_compact.subseq_of_frequently_in {s : set X} (hs : is_seq_compact s) {x : â„• â†’ X}
  (hx : âˆƒá¶  n in at_top, x n âˆˆ s) :
  âˆƒ (a âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
let âŸ¨Ïˆ, hÏˆ, huÏˆâŸ© := extraction_of_frequently_at_top hx, âŸ¨a, a_in, Ï†, hÏ†, hâŸ© := hs huÏˆ in
âŸ¨a, a_in, Ïˆ âˆ˜ Ï†, hÏˆ.comp hÏ†, hâŸ©

lemma seq_compact_space.tendsto_subseq [seq_compact_space X] (x : â„• â†’ X) :
  âˆƒ a (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
let âŸ¨a, _, Ï†, mono, hâŸ© := seq_compact_space.seq_compact_univ (Î» n, mem_univ (x n)) in
âŸ¨a, Ï†, mono, hâŸ©

section first_countable_topology
variables [first_countable_topology X]
open topological_space.first_countable_topology

lemma is_compact.is_seq_compact {s : set X} (hs : is_compact s) : is_seq_compact s :=
Î» x x_in,
let âŸ¨a, a_in, haâŸ© := @hs (map x at_top) _
  (le_principal_iff.mpr (univ_mem' x_in : _)) in âŸ¨a, a_in, tendsto_subseq haâŸ©

lemma is_compact.tendsto_subseq' {s : set X} {x : â„• â†’ X} (hs : is_compact s)
  (hx : âˆƒá¶  n in at_top, x n âˆˆ s) :
  âˆƒ (a âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
hs.is_seq_compact.subseq_of_frequently_in hx

lemma is_compact.tendsto_subseq {s : set X} {x : â„• â†’ X} (hs : is_compact s) (hx : âˆ€ n, x n âˆˆ s) :
  âˆƒ (a âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
hs.is_seq_compact hx

@[priority 100] -- see Note [lower instance priority]
instance first_countable_topology.seq_compact_of_compact [compact_space X] : seq_compact_space X :=
âŸ¨compact_univ.is_seq_compactâŸ©

lemma compact_space.tendsto_subseq [compact_space X] (x : â„• â†’ X) :
  âˆƒ a (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
seq_compact_space.tendsto_subseq x

end first_countable_topology
end seq_compact

section uniform_space_seq_compact

open_locale uniformity
open uniform_space prod

variables [uniform_space X] {s : set X}

lemma lebesgue_number_lemma_seq {Î¹ : Type*} [is_countably_generated (ğ“¤ X)] {c : Î¹ â†’ set X}
  (hs : is_seq_compact s) (hcâ‚ : âˆ€ i, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) :
  âˆƒ V âˆˆ ğ“¤ X, symmetric_rel V âˆ§ âˆ€ x âˆˆ s, âˆƒ i, ball x V âŠ† c i :=
begin
  classical,
  obtain âŸ¨V, hV, VsymmâŸ© :
    âˆƒ V : â„• â†’ set (X Ã— X), (ğ“¤ X).has_antitone_basis V âˆ§ âˆ€ n, swap â»Â¹' V n = V n,
      from uniform_space.has_seq_basis X,
  suffices : âˆƒ n, âˆ€ x âˆˆ s, âˆƒ i, ball x (V n) âŠ† c i,
  { cases this with n hn,
    exact âŸ¨V n, hV.to_has_basis.mem_of_mem trivial, Vsymm n, hnâŸ© },
  by_contradiction H,
  obtain âŸ¨x, x_in, hxâŸ© : âˆƒ x : â„• â†’ X, (âˆ€ n, x n âˆˆ s) âˆ§ âˆ€ n i, Â¬ ball (x n) (V n) âŠ† c i,
  { push_neg at H,
    choose x hx using H,
    exact âŸ¨x, forall_and_distrib.mp hxâŸ© }, clear H,
  obtain âŸ¨xâ‚€, xâ‚€_in, Ï†, Ï†_mono, hlimâŸ© :
    âˆƒ (xâ‚€ âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ xâ‚€),
    from hs x_in, clear hs,
  obtain âŸ¨iâ‚€, xâ‚€_inâŸ© : âˆƒ iâ‚€, xâ‚€ âˆˆ c iâ‚€,
  { rcases hcâ‚‚ xâ‚€_in with âŸ¨_, âŸ¨iâ‚€, rflâŸ©, xâ‚€_in_câŸ©,
    exact âŸ¨iâ‚€, xâ‚€_in_câŸ© }, clear hcâ‚‚,
  obtain âŸ¨nâ‚€, hnâ‚€âŸ© : âˆƒ nâ‚€, ball xâ‚€ (V nâ‚€) âŠ† c iâ‚€,
  { rcases (nhds_basis_uniformity hV.to_has_basis).mem_iff.mp
      (is_open_iff_mem_nhds.mp (hcâ‚ iâ‚€) _ xâ‚€_in) with âŸ¨nâ‚€, _, hâŸ©,
    use nâ‚€,
    rwa â† ball_eq_of_symmetry (Vsymm nâ‚€) at h }, clear hcâ‚,
  obtain âŸ¨W, W_in, hWWâŸ© : âˆƒ W âˆˆ ğ“¤ X, W â—‹ W âŠ† V nâ‚€,
    from comp_mem_uniformity_sets (hV.to_has_basis.mem_of_mem trivial),
  obtain âŸ¨N, x_Ï†_N_in, hVNWâŸ© : âˆƒ N, x (Ï† N) âˆˆ ball xâ‚€ W âˆ§ V (Ï† N) âŠ† W,
  { obtain âŸ¨Nâ‚, hâ‚âŸ© : âˆƒ Nâ‚, âˆ€ n â‰¥ Nâ‚, x (Ï† n) âˆˆ ball xâ‚€ W,
      from tendsto_at_top'.mp hlim _ (mem_nhds_left xâ‚€ W_in),
    obtain âŸ¨Nâ‚‚, hâ‚‚âŸ© : âˆƒ Nâ‚‚, V (Ï† Nâ‚‚) âŠ† W,
    { rcases hV.to_has_basis.mem_iff.mp W_in with âŸ¨N, _, hNâŸ©,
      use N,
      exact subset.trans (hV.antitone $ Ï†_mono.id_le _) hN },
    have : Ï† Nâ‚‚ â‰¤ Ï† (max Nâ‚ Nâ‚‚),
      from Ï†_mono.le_iff_le.mpr (le_max_right _ _),
    exact âŸ¨max Nâ‚ Nâ‚‚, hâ‚ _ (le_max_left _ _), trans (hV.antitone this) hâ‚‚âŸ© },
  suffices : ball (x (Ï† N)) (V (Ï† N)) âŠ† c iâ‚€,
    from hx (Ï† N) iâ‚€ this,
  calc
    ball (x $ Ï† N) (V $ Ï† N) âŠ† ball (x $ Ï† N) W : preimage_mono hVNW
                         ... âŠ† ball xâ‚€ (V nâ‚€)   : ball_subset_of_comp_subset x_Ï†_N_in hWW
                         ... âŠ† c iâ‚€             : hnâ‚€,
end

lemma is_seq_compact.totally_bounded (h : is_seq_compact s) : totally_bounded s :=
begin
  classical,
  apply totally_bounded_of_forall_symm,
  unfold is_seq_compact at h,
  contrapose! h,
  rcases h with âŸ¨V, V_in, V_symm, hâŸ©,
  simp_rw [not_subset] at h,
  have : âˆ€ (t : set X), t.finite â†’ âˆƒ a, a âˆˆ s âˆ§ a âˆ‰ â‹ƒ y âˆˆ t, ball y V,
  { intros t ht,
    obtain âŸ¨a, a_in, HâŸ© : âˆƒ a âˆˆ s, âˆ€ x âˆˆ t, (x, a) âˆ‰ V,
      by simpa [ht] using h t,
    use [a, a_in],
    intro H',
    obtain âŸ¨x, x_in, hxâŸ© := mem_Unionâ‚‚.mp H',
    exact H x x_in hx },
  cases seq_of_forall_finite_exists this with u hu, clear h this,
  simp [forall_and_distrib] at hu,
  cases hu with u_in hu,
  use [u, u_in], clear u_in,
  intros x x_in Ï†,
  intros hÏ† huÏ†,
  obtain âŸ¨N, hNâŸ© : âˆƒ N, âˆ€ p q, p â‰¥ N â†’ q â‰¥ N â†’ (u (Ï† p), u (Ï† q)) âˆˆ V,
    from huÏ†.cauchy_seq.mem_entourage V_in,
  specialize hN N (N+1) (le_refl N) (nat.le_succ N),
  specialize hu (Ï† $ N+1) (Ï† N) (hÏ† $ lt_add_one N),
  exact hu hN,
end

protected lemma is_seq_compact.is_compact [is_countably_generated $ ğ“¤ X] (hs : is_seq_compact s) :
  is_compact s :=
begin
  classical,
  rw is_compact_iff_finite_subcover,
  intros Î¹ U Uop s_sub,
  rcases lebesgue_number_lemma_seq hs Uop s_sub with âŸ¨V, V_in, Vsymm, HâŸ©,
  rcases totally_bounded_iff_subset.mp hs.totally_bounded V V_in with âŸ¨t,t_sub, tfin,  htâŸ©,
  have : âˆ€ x : t, âˆƒ (i : Î¹), ball x.val V âŠ† U i,
  { rintros âŸ¨x, x_inâŸ©,
    exact H x (t_sub x_in) },
  choose i hi using this,
  haveI : fintype t := tfin.fintype,
  use finset.image i finset.univ,
  transitivity â‹ƒ y âˆˆ t, ball y V,
  { intros x x_in,
    specialize ht x_in,
    rw mem_Unionâ‚‚ at *,
    simp_rw ball_eq_of_symmetry Vsymm,
    exact ht },
  { refine Unionâ‚‚_mono' (Î» x x_in, _),
    exact âŸ¨i âŸ¨x, x_inâŸ©, finset.mem_image_of_mem _ (finset.mem_univ _), hi âŸ¨x, x_inâŸ©âŸ© },
end

/-- A version of Bolzano-Weistrass: in a uniform space with countably generated uniformity filter
(e.g., in a metric space), a set is compact if and only if it is sequentially compact. -/
protected lemma uniform_space.compact_iff_seq_compact [is_countably_generated $ ğ“¤ X] :
 is_compact s â†” is_seq_compact s :=
âŸ¨Î» H, H.is_seq_compact, Î» H, H.is_compactâŸ©

lemma uniform_space.compact_space_iff_seq_compact_space [is_countably_generated $ ğ“¤ X] :
  compact_space X â†” seq_compact_space X :=
have key : is_compact (univ : set X) â†” is_seq_compact univ := uniform_space.compact_iff_seq_compact,
âŸ¨Î» âŸ¨hâŸ©, âŸ¨key.mp hâŸ©, Î» âŸ¨hâŸ©, âŸ¨key.mpr hâŸ©âŸ©

end uniform_space_seq_compact

section metric_seq_compact

variables [pseudo_metric_space X]
open metric

lemma seq_compact.lebesgue_number_lemma_of_metric {Î¹ : Sort*} {c : Î¹ â†’ set X}
  {s : set X} (hs : is_seq_compact s) (hcâ‚ : âˆ€ i, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) :
  âˆƒ Î´ > 0, âˆ€ a âˆˆ s, âˆƒ i, ball a Î´ âŠ† c i :=
lebesgue_number_lemma_of_metric hs.is_compact hcâ‚ hcâ‚‚

variables [proper_space X] {s : set X}

/-- A version of **Bolzano-Weistrass**: in a proper metric space (eg. $â„^n$),
every bounded sequence has a converging subsequence. This version assumes only
that the sequence is frequently in some bounded set. -/
lemma tendsto_subseq_of_frequently_bounded (hs : bounded s)
  {x : â„• â†’ X} (hx : âˆƒá¶  n in at_top, x n âˆˆ s) :
  âˆƒ a âˆˆ closure s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
have hcs : is_seq_compact (closure s), from hs.is_compact_closure.is_seq_compact,
have hu' : âˆƒá¶  n in at_top, x n âˆˆ closure s, from hx.mono (Î» n hn, subset_closure hn),
hcs.subseq_of_frequently_in hu'

/-- A version of Bolzano-Weistrass: in a proper metric space (eg. $â„^n$),
every bounded sequence has a converging subsequence. -/
lemma tendsto_subseq_of_bounded (hs : bounded s) {x : â„• â†’ X} (hx : âˆ€ n, x n âˆˆ s) :
  âˆƒ a âˆˆ closure s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (x âˆ˜ Ï†) at_top (ğ“ a) :=
tendsto_subseq_of_frequently_bounded hs $ frequently_of_forall hx

end metric_seq_compact
