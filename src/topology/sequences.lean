/-
Copyright (c) 2018 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, Patrick Massot
-/
import topology.subset_properties
import topology.metric_space.basic

/-!
# Sequences in topological spaces

In this file we define sequences in topological spaces and show how they are related to
filters and the topology. In particular, we
* define the sequential closure of a set and prove that it's contained in the closure,
* define a type class "sequential_space" in which closure and sequential closure agree,
* define sequential continuity and show that it coincides with continuity in sequential spaces,
* provide an instance that shows that every first-countable (and in particular metric) space is
  a sequential space.
* define sequential compactness, prove that compactness implies sequential compactness in first
  countable spaces, and prove they are equivalent for uniform spaces having a countable uniformity
  basis (in particular metric spaces).
-/

open set filter
open_locale topological_space

variables {Î± : Type*} {Î² : Type*}

local notation f ` âŸ¶ ` limit := tendsto f at_top (ğ“ limit)

/-! ### Sequential closures, sequential continuity, and sequential spaces. -/
section topological_space
variables [topological_space Î±] [topological_space Î²]

/-- The sequential closure of a subset M âŠ† Î± of a topological space Î± is
the set of all p âˆˆ Î± which arise as limit of sequences in M. -/
def sequential_closure (M : set Î±) : set Î± :=
{p | âˆƒ x : â„• â†’ Î±, (âˆ€ n : â„•, x n âˆˆ M) âˆ§ (x âŸ¶ p)}

lemma subset_sequential_closure (M : set Î±) : M âŠ† sequential_closure M :=
assume p (_ : p âˆˆ M), show p âˆˆ sequential_closure M, from
  âŸ¨Î» n, p, assume n, â€¹p âˆˆ Mâ€º, tendsto_const_nhdsâŸ©

/-- A set `s` is sequentially closed if for any converging sequence `x n` of elements of `s`,
the limit belongs to `s` as well. -/
def is_seq_closed (s : set Î±) : Prop := s = sequential_closure s

/-- A convenience lemma for showing that a set is sequentially closed. -/
lemma is_seq_closed_of_def {A : set Î±}
  (h : âˆ€(x : â„• â†’ Î±) (p : Î±), (âˆ€ n : â„•, x n âˆˆ A) â†’ (x âŸ¶ p) â†’ p âˆˆ A) : is_seq_closed A :=
show A = sequential_closure A, from subset.antisymm
  (subset_sequential_closure A)
  (show âˆ€ p, p âˆˆ sequential_closure A â†’ p âˆˆ A, from
    (assume p âŸ¨x, _, _âŸ©, show p âˆˆ A, from h x p â€¹âˆ€ n : â„•, ((x n) âˆˆ A)â€º â€¹(x âŸ¶ p)â€º))

/-- The sequential closure of a set is contained in the closure of that set.
The converse is not true. -/
lemma sequential_closure_subset_closure (M : set Î±) : sequential_closure M âŠ† closure M :=
assume p âŸ¨x, xM, xpâŸ©,
mem_closure_of_tendsto xp (univ_mem' xM)

/-- A set is sequentially closed if it is closed. -/
lemma is_seq_closed_of_is_closed (M : set Î±) (_ : is_closed M) : is_seq_closed M :=
suffices sequential_closure M âŠ† M, from
  set.eq_of_subset_of_subset (subset_sequential_closure M) this,
calc sequential_closure M âŠ† closure M : sequential_closure_subset_closure M
  ... = M : is_closed.closure_eq â€¹is_closed Mâ€º

/-- The limit of a convergent sequence in a sequentially closed set is in that set.-/
lemma mem_of_is_seq_closed {A : set Î±} (_ : is_seq_closed A) {x : â„• â†’ Î±}
  (_ : âˆ€ n, x n âˆˆ A) {limit : Î±} (_ : (x âŸ¶ limit)) : limit âˆˆ A :=
have limit âˆˆ sequential_closure A, from
  show âˆƒ x : â„• â†’ Î±, (âˆ€ n : â„•, x n âˆˆ A) âˆ§ (x âŸ¶ limit), from âŸ¨x, â€¹âˆ€ n, x n âˆˆ Aâ€º, â€¹(x âŸ¶ limit)â€ºâŸ©,
eq.subst (eq.symm â€¹is_seq_closed Aâ€º) â€¹limit âˆˆ sequential_closure Aâ€º

/-- The limit of a convergent sequence in a closed set is in that set.-/
lemma mem_of_is_closed_sequential {A : set Î±} (_ : is_closed A) {x : â„• â†’ Î±}
  (_ : âˆ€ n, x n âˆˆ A) {limit : Î±} (_ : x âŸ¶ limit) : limit âˆˆ A :=
mem_of_is_seq_closed (is_seq_closed_of_is_closed A â€¹is_closed Aâ€º) â€¹âˆ€ n, x n âˆˆ Aâ€º â€¹(x âŸ¶ limit)â€º

/-- A sequential space is a space in which 'sequences are enough to probe the topology'. This can be
 formalised by demanding that the sequential closure and the closure coincide. The following
 statements show that other topological properties can be deduced from sequences in sequential
 spaces. -/
class sequential_space (Î± : Type*) [topological_space Î±] : Prop :=
(sequential_closure_eq_closure : âˆ€ M : set Î±, sequential_closure M = closure M)

/-- In a sequential space, a set is closed iff it's sequentially closed. -/
lemma is_seq_closed_iff_is_closed [sequential_space Î±] {M : set Î±} :
  is_seq_closed M â†” is_closed M :=
iff.intro
  (assume _, closure_eq_iff_is_closed.mp (eq.symm
    (calc M = sequential_closure M : by assumption
        ... = closure M            : sequential_space.sequential_closure_eq_closure M)))
  (is_seq_closed_of_is_closed M)

/-- In a sequential space, a point belongs to the closure of a set iff it is a limit of a sequence
taking values in this set. -/
lemma mem_closure_iff_seq_limit [sequential_space Î±] {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆƒ x : â„• â†’ Î±, (âˆ€ n : â„•, x n âˆˆ s) âˆ§ (x âŸ¶ a) :=
by { rw â† sequential_space.sequential_closure_eq_closure, exact iff.rfl }

/-- A function between topological spaces is sequentially continuous if it commutes with limit of
 convergent sequences. -/
def sequentially_continuous (f : Î± â†’ Î²) : Prop :=
âˆ€ (x : â„• â†’ Î±), âˆ€ {limit : Î±}, (x âŸ¶ limit) â†’ (fâˆ˜x âŸ¶ f limit)

/- A continuous function is sequentially continuous. -/
lemma continuous.to_sequentially_continuous {f : Î± â†’ Î²} (_ : continuous f) :
  sequentially_continuous f :=
assume x limit (_ : x âŸ¶ limit),
have tendsto f (ğ“ limit) (ğ“ (f limit)), from continuous.tendsto â€¹continuous fâ€º limit,
show (f âˆ˜ x) âŸ¶ (f limit), from tendsto.comp this â€¹(x âŸ¶ limit)â€º

/-- In a sequential space, continuity and sequential continuity coincide. -/
lemma continuous_iff_sequentially_continuous {f : Î± â†’ Î²} [sequential_space Î±] :
  continuous f â†” sequentially_continuous f :=
iff.intro
  (assume _, â€¹continuous fâ€º.to_sequentially_continuous)
  (assume : sequentially_continuous f, show continuous f, from
    suffices h : âˆ€ {A : set Î²}, is_closed A â†’ is_seq_closed (f â»Â¹' A), from
      continuous_iff_is_closed.mpr (assume A _, is_seq_closed_iff_is_closed.mp $ h â€¹is_closed Aâ€º),
    assume A (_ : is_closed A),
      is_seq_closed_of_def $
        assume (x : â„• â†’ Î±) p (_ : âˆ€ n, f (x n) âˆˆ A) (_ : x âŸ¶ p),
        have (f âˆ˜ x) âŸ¶ (f p), from â€¹sequentially_continuous fâ€º x â€¹(x âŸ¶ p)â€º,
        show f p âˆˆ A, from
          mem_of_is_closed_sequential â€¹is_closed Aâ€º â€¹âˆ€ n, f (x n) âˆˆ Aâ€º â€¹(fâˆ˜x âŸ¶ f p)â€º)

end topological_space

namespace topological_space

namespace first_countable_topology

variables [topological_space Î±] [first_countable_topology Î±]

/-- Every first-countable space is sequential. -/
@[priority 100] -- see Note [lower instance priority]
instance : sequential_space Î± :=
âŸ¨show âˆ€ M, sequential_closure M = closure M, from assume M,
  suffices closure M âŠ† sequential_closure M,
    from set.subset.antisymm (sequential_closure_subset_closure M) this,
  -- For every p âˆˆ closure M, we need to construct a sequence x in M that converges to p:
  assume (p : Î±) (hp : p âˆˆ closure M),
  -- Since we are in a first-countable space, the neighborhood filter around `p` has a decreasing
  -- basis `U` indexed by `â„•`.
  let âŸ¨U, hUâŸ© := (ğ“ p).exists_antitone_basis in
  -- Since `p âˆˆ closure M`, there is an element in each `M âˆ© U i`
  have hp : âˆ€ (i : â„•), âˆƒ (y : Î±), y âˆˆ M âˆ§ y âˆˆ U i,
    by simpa using (mem_closure_iff_nhds_basis hU.1).mp hp,
  begin
    -- The axiom of (countable) choice builds our sequence from the later fact
    choose u hu using hp,
    rw forall_and_distrib at hu,
    -- It clearly takes values in `M`
    use [u, hu.1],
    -- and converges to `p` because the basis is decreasing.
    apply hU.tendsto hu.2,
  endâŸ©


end first_countable_topology

end topological_space

section seq_compact
open topological_space topological_space.first_countable_topology
variables [topological_space Î±]

/-- A set `s` is sequentially compact if every sequence taking values in `s` has a
converging subsequence. -/
def is_seq_compact (s : set Î±) :=
  âˆ€ â¦ƒu : â„• â†’ Î±â¦„, (âˆ€ n, u n âˆˆ s) â†’
    âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x)

/-- A space `Î±` is sequentially compact if every sequence in `Î±` has a
converging subsequence. -/
class seq_compact_space (Î± : Type*) [topological_space Î±] : Prop :=
(seq_compact_univ : is_seq_compact (univ : set Î±))

lemma is_seq_compact.subseq_of_frequently_in {s : set Î±} (hs : is_seq_compact s) {u : â„• â†’ Î±}
  (hu : âˆƒá¶  n in at_top, u n âˆˆ s) :
  âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
let âŸ¨Ïˆ, hÏˆ, huÏˆâŸ© := extraction_of_frequently_at_top hu, âŸ¨x, x_in, Ï†, hÏ†, hâŸ© := hs huÏˆ in
âŸ¨x, x_in, Ïˆ âˆ˜ Ï†, hÏˆ.comp hÏ†, hâŸ©

lemma seq_compact_space.tendsto_subseq [seq_compact_space Î±] (u : â„• â†’ Î±) :
  âˆƒ x (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
let âŸ¨x, _, Ï†, mono, hâŸ© := seq_compact_space.seq_compact_univ (by simp : âˆ€ n, u n âˆˆ univ) in
âŸ¨x, Ï†, mono, hâŸ©

section first_countable_topology
variables [first_countable_topology Î±]
open topological_space.first_countable_topology

lemma is_compact.is_seq_compact {s : set Î±} (hs : is_compact s) : is_seq_compact s :=
Î» u u_in,
let âŸ¨x, x_in, hxâŸ© := @hs (map u at_top) _
  (le_principal_iff.mpr (univ_mem' u_in : _)) in âŸ¨x, x_in, tendsto_subseq hxâŸ©

lemma is_compact.tendsto_subseq' {s : set Î±} {u : â„• â†’ Î±} (hs : is_compact s)
  (hu : âˆƒá¶  n in at_top, u n âˆˆ s) :
âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
hs.is_seq_compact.subseq_of_frequently_in hu

lemma is_compact.tendsto_subseq {s : set Î±} {u : â„• â†’ Î±} (hs : is_compact s) (hu : âˆ€ n, u n âˆˆ s) :
âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
hs.is_seq_compact hu

@[priority 100] -- see Note [lower instance priority]
instance first_countable_topology.seq_compact_of_compact [compact_space Î±] : seq_compact_space Î± :=
âŸ¨compact_univ.is_seq_compactâŸ©

lemma compact_space.tendsto_subseq [compact_space Î±] (u : â„• â†’ Î±) :
  âˆƒ x (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ x) :=
seq_compact_space.tendsto_subseq u

end first_countable_topology
end seq_compact

section uniform_space_seq_compact

open_locale uniformity
open uniform_space prod

variables [uniform_space Î²] {s : set Î²}

lemma lebesgue_number_lemma_seq {Î¹ : Type*} [is_countably_generated (ğ“¤ Î²)] {c : Î¹ â†’ set Î²}
  (hs : is_seq_compact s) (hcâ‚ : âˆ€ i, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) :
  âˆƒ V âˆˆ ğ“¤ Î², symmetric_rel V âˆ§ âˆ€ x âˆˆ s, âˆƒ i, ball x V âŠ† c i :=
begin
  classical,
  obtain âŸ¨V, hV, VsymmâŸ© :
    âˆƒ V : â„• â†’ set (Î² Ã— Î²), (ğ“¤ Î²).has_antitone_basis V âˆ§ âˆ€ n, swap â»Â¹' V n = V n,
      from uniform_space.has_seq_basis Î²,
  suffices : âˆƒ n, âˆ€ x âˆˆ s, âˆƒ i, ball x (V n) âŠ† c i,
  { cases this with n hn,
    exact âŸ¨V n, hV.to_has_basis.mem_of_mem trivial, Vsymm n, hnâŸ© },
  by_contradiction H,
  obtain âŸ¨x, x_in, hxâŸ© : âˆƒ x : â„• â†’ Î², (âˆ€ n, x n âˆˆ s) âˆ§ âˆ€ n i, Â¬ ball (x n) (V n) âŠ† c i,
  { push_neg at H,
    choose x hx using H,
    exact âŸ¨x, forall_and_distrib.mp hxâŸ© }, clear H,
  obtain âŸ¨xâ‚€, xâ‚€_in, Ï†, Ï†_mono, hlimâŸ© : âˆƒ (xâ‚€ âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ (x âˆ˜ Ï† âŸ¶ xâ‚€),
    from hs x_in, clear hs,
  obtain âŸ¨iâ‚€, xâ‚€_inâŸ© : âˆƒ iâ‚€, xâ‚€ âˆˆ c iâ‚€,
  { rcases hcâ‚‚ xâ‚€_in with âŸ¨_, âŸ¨iâ‚€, rflâŸ©, xâ‚€_in_câŸ©,
    exact âŸ¨iâ‚€, xâ‚€_in_câŸ© }, clear hcâ‚‚,
  obtain âŸ¨nâ‚€, hnâ‚€âŸ© : âˆƒ nâ‚€, ball xâ‚€ (V nâ‚€) âŠ† c iâ‚€,
  { rcases (nhds_basis_uniformity hV.to_has_basis).mem_iff.mp
      (is_open_iff_mem_nhds.mp (hcâ‚ iâ‚€) _ xâ‚€_in) with âŸ¨nâ‚€, _, hâŸ©,
    use nâ‚€,
    rwa â† ball_eq_of_symmetry (Vsymm nâ‚€) at h }, clear hcâ‚,
  obtain âŸ¨W, W_in, hWWâŸ© : âˆƒ W âˆˆ ğ“¤ Î², W â—‹ W âŠ† V nâ‚€,
    from comp_mem_uniformity_sets (hV.to_has_basis.mem_of_mem trivial),
  obtain âŸ¨N, x_Ï†_N_in, hVNWâŸ© : âˆƒ N, x (Ï† N) âˆˆ ball xâ‚€ W âˆ§ V (Ï† N) âŠ† W,
  { obtain âŸ¨Nâ‚, hâ‚âŸ© : âˆƒ Nâ‚, âˆ€ n â‰¥ Nâ‚, x (Ï† n) âˆˆ ball xâ‚€ W,
      from tendsto_at_top'.mp hlim _ (mem_nhds_left xâ‚€ W_in),
    obtain âŸ¨Nâ‚‚, hâ‚‚âŸ© : âˆƒ Nâ‚‚, V (Ï† Nâ‚‚) âŠ† W,
    { rcases hV.to_has_basis.mem_iff.mp W_in with âŸ¨N, _, hNâŸ©,
      use N,
      exact subset.trans (hV.antitone $ Ï†_mono.id_le _) hN },
    have : Ï† Nâ‚‚ â‰¤ Ï† (max Nâ‚ Nâ‚‚),
      from Ï†_mono.le_iff_le.mpr (le_max_right _ _),
    exact âŸ¨max Nâ‚ Nâ‚‚, hâ‚ _ (le_max_left _ _), trans (hV.antitone this) hâ‚‚âŸ© },
  suffices : ball (x (Ï† N)) (V (Ï† N)) âŠ† c iâ‚€,
    from hx (Ï† N) iâ‚€ this,
  calc
    ball (x $ Ï† N) (V $ Ï† N) âŠ† ball (x $ Ï† N) W : preimage_mono hVNW
                         ... âŠ† ball xâ‚€ (V nâ‚€)   : ball_subset_of_comp_subset x_Ï†_N_in hWW
                         ... âŠ† c iâ‚€             : hnâ‚€,
end

lemma is_seq_compact.totally_bounded (h : is_seq_compact s) : totally_bounded s :=
begin
  classical,
  apply totally_bounded_of_forall_symm,
  unfold is_seq_compact at h,
  contrapose! h,
  rcases h with âŸ¨V, V_in, V_symm, hâŸ©,
  simp_rw [not_subset] at h,
  have : âˆ€ (t : set Î²), finite t â†’ âˆƒ a, a âˆˆ s âˆ§ a âˆ‰ â‹ƒ y âˆˆ t, ball y V,
  { intros t ht,
    obtain âŸ¨a, a_in, HâŸ© : âˆƒ a âˆˆ s, âˆ€ (x : Î²), x âˆˆ t â†’ (x, a) âˆ‰ V,
      by simpa [ht] using h t,
    use [a, a_in],
    intro H',
    obtain âŸ¨x, x_in, hxâŸ© := mem_Unionâ‚‚.mp H',
    exact H x x_in hx },
  cases seq_of_forall_finite_exists this with u hu, clear h this,
  simp [forall_and_distrib] at hu,
  cases hu with u_in hu,
  use [u, u_in], clear u_in,
  intros x x_in Ï†,
  intros hÏ† huÏ†,
  obtain âŸ¨N, hNâŸ© : âˆƒ N, âˆ€ p q, p â‰¥ N â†’ q â‰¥ N â†’ (u (Ï† p), u (Ï† q)) âˆˆ V,
    from huÏ†.cauchy_seq.mem_entourage V_in,
  specialize hN N (N+1) (le_refl N) (nat.le_succ N),
  specialize hu (Ï† $ N+1) (Ï† N) (hÏ† $ lt_add_one N),
  exact hu hN,
end

protected lemma is_seq_compact.is_compact [is_countably_generated $ ğ“¤ Î²] (hs : is_seq_compact s) :
  is_compact s :=
begin
  classical,
  rw is_compact_iff_finite_subcover,
  intros Î¹ U Uop s_sub,
  rcases lebesgue_number_lemma_seq hs Uop s_sub with âŸ¨V, V_in, Vsymm, HâŸ©,
  rcases totally_bounded_iff_subset.mp hs.totally_bounded V V_in with âŸ¨t,t_sub, tfin,  htâŸ©,
  have : âˆ€ x : t, âˆƒ (i : Î¹), ball x.val V âŠ† U i,
  { rintros âŸ¨x, x_inâŸ©,
    exact H x (t_sub x_in) },
  choose i hi using this,
  haveI : fintype t := tfin.fintype,
  use finset.image i finset.univ,
  transitivity â‹ƒ y âˆˆ t, ball y V,
  { intros x x_in,
    specialize ht x_in,
    rw mem_Unionâ‚‚ at *,
    simp_rw ball_eq_of_symmetry Vsymm,
    exact ht },
  { refine Unionâ‚‚_mono' (Î» x x_in, _),
    exact âŸ¨i âŸ¨x, x_inâŸ©, finset.mem_image_of_mem _ (finset.mem_univ _), hi âŸ¨x, x_inâŸ©âŸ© },
end

/-- A version of Bolzano-Weistrass: in a uniform space with countably generated uniformity filter
(e.g., in a metric space), a set is compact if and only if it is sequentially compact. -/
protected lemma uniform_space.compact_iff_seq_compact [is_countably_generated $ ğ“¤ Î²] :
 is_compact s â†” is_seq_compact s :=
âŸ¨Î» H, H.is_seq_compact, Î» H, H.is_compactâŸ©

lemma uniform_space.compact_space_iff_seq_compact_space [is_countably_generated $ ğ“¤ Î²] :
  compact_space Î² â†” seq_compact_space Î² :=
have key : is_compact (univ : set Î²) â†” is_seq_compact univ := uniform_space.compact_iff_seq_compact,
âŸ¨Î» âŸ¨hâŸ©, âŸ¨key.mp hâŸ©, Î» âŸ¨hâŸ©, âŸ¨key.mpr hâŸ©âŸ©

end uniform_space_seq_compact

section metric_seq_compact

variables [pseudo_metric_space Î²]
open metric

lemma seq_compact.lebesgue_number_lemma_of_metric {Î¹ : Sort*} {c : Î¹ â†’ set Î²}
  {s : set Î²}(hs : is_seq_compact s) (hcâ‚ : âˆ€ i, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) :
  âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ i, ball x Î´ âŠ† c i :=
lebesgue_number_lemma_of_metric hs.is_compact hcâ‚ hcâ‚‚

variables [proper_space Î²] {s : set Î²}

/-- A version of **Bolzano-Weistrass**: in a proper metric space (eg. $â„^n$),
every bounded sequence has a converging subsequence. This version assumes only
that the sequence is frequently in some bounded set. -/
lemma tendsto_subseq_of_frequently_bounded (hs : bounded s)
  {u : â„• â†’ Î²} (hu : âˆƒá¶  n in at_top, u n âˆˆ s) :
  âˆƒ b âˆˆ closure s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ b) :=
have hcs : is_seq_compact (closure s), from hs.is_compact_closure.is_seq_compact,
have hu' : âˆƒá¶  n in at_top, u n âˆˆ closure s, from hu.mono (Î» n hn, subset_closure hn),
hcs.subseq_of_frequently_in hu'

/-- A version of Bolzano-Weistrass: in a proper metric space (eg. $â„^n$),
every bounded sequence has a converging subsequence. -/
lemma tendsto_subseq_of_bounded (hs : bounded s)
  {u : â„• â†’ Î²} (hu : âˆ€ n, u n âˆˆ s) :
  âˆƒ b âˆˆ closure s, âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ğ“ b) :=
tendsto_subseq_of_frequently_bounded hs $ frequently_of_forall hu

end metric_seq_compact
