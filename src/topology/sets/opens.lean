/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Floris van Doorn
-/
import order.hom.complete_lattice
import topology.bases
import topology.homeomorph
import topology.continuous_function.basic
import order.compactly_generated
import tactic.auto_cases

/-!
# Open sets

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

## Summary

We define the subtype of open sets in a topological space.

## Main Definitions

### Bundled open sets

- `opens Œ±` is the type of open subsets of a topological space `Œ±`.
- `opens.is_basis` is a predicate saying that a set of `opens`s form a topological basis.
- `opens.comap`: preimage of an open set under a continuous map as a `frame_hom`.
- `homeomorph.opens_congr`: order-preserving equivalence between open sets in the domain and the
  codomain of a homeomorphism.

### Bundled open neighborhoods

- `open_nhds_of x` is the type of open subsets of a topological space `Œ±` containing `x : Œ±`.
- `open_nhds_of.comap f x U` is the preimage of open neighborhood `U` of `f x` under `f : C(Œ±, Œ≤)`.

## Main results

We define order structures on both `opens Œ±` (`complete_structure`, `frame`) and `open_nhds_of x`
(`order_top`, `distrib_lattice`).
-/

open filter function order set
open_locale topology

variables {Œπ Œ± Œ≤ Œ≥ : Type*} [topological_space Œ±] [topological_space Œ≤] [topological_space Œ≥]

namespace topological_space

variable (Œ±)

/-- The type of open subsets of a topological space. -/
structure opens :=
(carrier : set Œ±)
(is_open' : is_open carrier)

variable {Œ±}

namespace opens

instance : set_like (opens Œ±) Œ± :=
{ coe := opens.carrier,
  coe_injective' := Œª ‚ü®_, _‚ü© ‚ü®_, _‚ü© _, by congr; assumption }

instance : can_lift (set Œ±) (opens Œ±) coe is_open :=
‚ü®Œª s h, ‚ü®‚ü®s, h‚ü©, rfl‚ü©‚ü©

lemma ¬´forall¬ª {p : opens Œ± ‚Üí Prop} : (‚àÄ U, p U) ‚Üî ‚àÄ (U : set Œ±) (hU : is_open U), p ‚ü®U, hU‚ü© :=
‚ü®Œª h _ _, h _, Œª h ‚ü®U, hU‚ü©, h _ _‚ü©

@[simp] lemma carrier_eq_coe (U : opens Œ±) : U.1 = ‚ÜëU := rfl

/-- the coercion `opens Œ± ‚Üí set Œ±` applied to a pair is the same as taking the first component -/
@[simp] lemma coe_mk {U : set Œ±} {hU : is_open U} : ‚Üë(‚ü®U, hU‚ü© : opens Œ±) = U := rfl

@[simp] lemma mem_mk {x : Œ±} {U : set Œ±} {h : is_open U} :
  @has_mem.mem _ (opens Œ±) _ x ‚ü®U, h‚ü© ‚Üî x ‚àà U := iff.rfl

-- todo: make it `simp` for a `set_like`?
@[simp] protected lemma nonempty_coe_sort {U : opens Œ±} : nonempty U ‚Üî (U : set Œ±).nonempty :=
set.nonempty_coe_sort

@[ext] lemma ext {U V : opens Œ±} (h : (U : set Œ±) = V) : U = V := set_like.coe_injective h
@[simp] lemma coe_inj {U V : opens Œ±} : (U : set Œ±) = V ‚Üî U = V := set_like.ext'_iff.symm

protected lemma is_open (U : opens Œ±) : is_open (U : set Œ±) := U.is_open'

@[simp] lemma mk_coe (U : opens Œ±) : mk ‚ÜëU U.is_open = U := by { cases U, refl }

/-- See Note [custom simps projection]. -/
def simps.coe (U : opens Œ±) : set Œ± := U

initialize_simps_projections opens (carrier ‚Üí coe)

/-- The interior of a set, as an element of `opens`. -/
def interior (s : set Œ±) : opens Œ± := ‚ü®interior s, is_open_interior‚ü©

lemma gc : galois_connection (coe : opens Œ± ‚Üí set Œ±) interior :=
Œª U s, ‚ü®Œª h, interior_maximal h U.is_open, Œª h, le_trans h interior_subset‚ü©

/-- The galois coinsertion between sets and opens. -/
def gi : galois_coinsertion coe (@interior Œ± _) :=
{ choice := Œª s hs, ‚ü®s, interior_eq_iff_is_open.mp $ le_antisymm interior_subset hs‚ü©,
  gc := gc,
  u_l_le := Œª _, interior_subset,
  choice_eq := Œª s hs, le_antisymm hs interior_subset }

instance : complete_lattice (opens Œ±) :=
complete_lattice.copy (galois_coinsertion.lift_complete_lattice gi)
/- le  -/ (Œª U V, (U : set Œ±) ‚äÜ V) rfl
/- top -/ ‚ü®univ, is_open_univ‚ü© (ext interior_univ.symm)
/- bot -/ ‚ü®‚àÖ, is_open_empty‚ü© rfl
/- sup -/ (Œª U V, ‚ü®‚ÜëU ‚à™ ‚ÜëV, U.2.union V.2‚ü©) rfl
/- inf -/ (Œª U V, ‚ü®‚ÜëU ‚à© ‚ÜëV, U.2.inter V.2‚ü©) (funext‚ÇÇ $ Œª U V, ext (U.2.inter V.2).interior_eq.symm)
/- Sup -/ (Œª S, ‚ü®‚ãÉ s ‚àà S, ‚Üës, is_open_bUnion $ Œª s _, s.2‚ü©) (funext $ Œª S, ext Sup_image.symm)
/- Inf -/ _ rfl

@[simp] lemma mk_inf_mk {U V : set Œ±} {hU : is_open U} {hV : is_open V} :
  (‚ü®U, hU‚ü© ‚äì ‚ü®V, hV‚ü© : opens Œ±) = ‚ü®U ‚äì V, is_open.inter hU hV‚ü© := rfl
@[simp, norm_cast] lemma coe_inf (s t : opens Œ±) : (‚Üë(s ‚äì t) : set Œ±) = s ‚à© t := rfl
@[simp, norm_cast] lemma coe_sup (s t : opens Œ±) : (‚Üë(s ‚äî t) : set Œ±) = s ‚à™ t := rfl
@[simp, norm_cast] lemma coe_bot : ((‚ä• : opens Œ±) : set Œ±) = ‚àÖ := rfl
@[simp, norm_cast] lemma coe_top : ((‚ä§ : opens Œ±) : set Œ±) = set.univ := rfl
@[simp, norm_cast] lemma coe_Sup {S : set (opens Œ±)} : (‚Üë(Sup S) : set Œ±) = ‚ãÉ i ‚àà S, ‚Üëi := rfl

@[simp, norm_cast] lemma coe_finset_sup (f : Œπ ‚Üí opens Œ±) (s : finset Œπ) :
  (‚Üë(s.sup f) : set Œ±) = s.sup (coe ‚àò f) :=
map_finset_sup (‚ü®‚ü®coe, coe_sup‚ü©, coe_bot‚ü© : sup_bot_hom (opens Œ±) (set Œ±)) _ _

@[simp, norm_cast] lemma coe_finset_inf (f : Œπ ‚Üí opens Œ±) (s : finset Œπ) :
  (‚Üë(s.inf f) : set Œ±) = s.inf (coe ‚àò f) :=
map_finset_inf (‚ü®‚ü®coe, coe_inf‚ü©, coe_top‚ü© : inf_top_hom (opens Œ±) (set Œ±)) _ _

instance : inhabited (opens Œ±) := ‚ü®‚ä•‚ü©

lemma supr_def {Œπ} (s : Œπ ‚Üí opens Œ±) : (‚®Ü i, s i) = ‚ü®‚ãÉ i, s i, is_open_Union $ Œª i, (s i).2‚ü© :=
by { ext, simp only [supr, coe_Sup, bUnion_range], refl }

@[simp] lemma supr_mk {Œπ} (s : Œπ ‚Üí set Œ±) (h : Œ† i, is_open (s i)) :
  (‚®Ü i, ‚ü®s i, h i‚ü© : opens Œ±) = ‚ü®‚ãÉ i, s i, is_open_Union h‚ü© :=
by { rw supr_def, simp }

@[simp, norm_cast] lemma coe_supr {Œπ} (s : Œπ ‚Üí opens Œ±) :
  ((‚®Ü i, s i : opens Œ±) : set Œ±) = ‚ãÉ i, s i :=
by simp [supr_def]

@[simp] theorem mem_supr {Œπ} {x : Œ±} {s : Œπ ‚Üí opens Œ±} : x ‚àà supr s ‚Üî ‚àÉ i, x ‚àà s i :=
by { rw [‚Üê set_like.mem_coe], simp, }

@[simp] lemma mem_Sup {Us : set (opens Œ±)} {x : Œ±} : x ‚àà Sup Us ‚Üî ‚àÉ u ‚àà Us, x ‚àà u :=
by simp_rw [Sup_eq_supr, mem_supr]

instance : frame (opens Œ±) :=
{ Sup := Sup,
  inf_Sup_le_supr_inf := Œª a s,
    (ext $ by simp only [coe_inf, coe_supr, coe_Sup, set.inter_Union‚ÇÇ]).le,
  ..opens.complete_lattice }

lemma open_embedding_of_le {U V : opens Œ±} (i : U ‚â§ V) :
  open_embedding (set.inclusion i) :=
{ inj := set.inclusion_injective i,
  induced := (@induced_compose _ _ _ _ (set.inclusion i) coe).symm,
  open_range :=
  begin
    rw set.range_inclusion i,
    exact U.is_open.preimage continuous_subtype_val
  end, }

lemma not_nonempty_iff_eq_bot (U : opens Œ±) : ¬¨ set.nonempty (U : set Œ±) ‚Üî U = ‚ä• :=
by rw [‚Üê coe_inj, opens.coe_bot, ‚Üê set.not_nonempty_iff_eq_empty]

lemma ne_bot_iff_nonempty (U : opens Œ±) : U ‚â† ‚ä• ‚Üî set.nonempty (U : set Œ±) :=
by rw [ne.def, ‚Üê opens.not_nonempty_iff_eq_bot, not_not]

/-- An open set in the indiscrete topology is either empty or the whole space. -/
lemma eq_bot_or_top {Œ±} [t : topological_space Œ±] (h : t = ‚ä§) (U : opens Œ±) : U = ‚ä• ‚à® U = ‚ä§ :=
begin
  simp only [‚Üê coe_inj],
  unfreezingI { subst h }, letI : topological_space Œ± := ‚ä§,
  exact (is_open_top_iff _).1 U.2
end

/-- A set of `opens Œ±` is a basis if the set of corresponding sets is a topological basis. -/
def is_basis (B : set (opens Œ±)) : Prop := is_topological_basis ((coe : _ ‚Üí set Œ±) '' B)

lemma is_basis_iff_nbhd {B : set (opens Œ±)} :
  is_basis B ‚Üî ‚àÄ {U : opens Œ±} {x}, x ‚àà U ‚Üí ‚àÉ U' ‚àà B, x ‚àà U' ‚àß U' ‚â§ U :=
begin
  split; intro h,
  { rintros ‚ü®sU, hU‚ü© x hx,
    rcases h.mem_nhds_iff.mp (is_open.mem_nhds hU hx)
      with ‚ü®sV, ‚ü®‚ü®V, H‚ÇÅ, H‚ÇÇ‚ü©, hsV‚ü©‚ü©,
    refine ‚ü®V, H‚ÇÅ, _‚ü©,
    cases V, dsimp at H‚ÇÇ, subst H‚ÇÇ, exact hsV },
  { refine is_topological_basis_of_open_of_nhds _ _,
    { rintros sU ‚ü®U, ‚ü®H‚ÇÅ, rfl‚ü©‚ü©, exact U.2 },
    { intros x sU hx hsU,
      rcases @h (‚ü®sU, hsU‚ü© : opens Œ±) x hx with ‚ü®V, hV, H‚ü©,
      exact ‚ü®V, ‚ü®V, hV, rfl‚ü©, H‚ü© } }
end

lemma is_basis_iff_cover {B : set (opens Œ±)} :
  is_basis B ‚Üî ‚àÄ U : opens Œ±, ‚àÉ Us ‚äÜ B, U = Sup Us :=
begin
  split,
  { intros hB U,
    refine ‚ü®{V : opens Œ± | V ‚àà B ‚àß V ‚â§ U}, Œª U hU, hU.left, _‚ü©,
    apply ext,
    rw [coe_Sup, hB.open_eq_sUnion' U.is_open],
    simp_rw [sUnion_eq_bUnion, Union, supr_and, supr_image],
    refl },
  { intro h,
    rw is_basis_iff_nbhd,
    intros U x hx,
    rcases h U with ‚ü®Us, hUs, rfl‚ü©,
    rcases mem_Sup.1 hx with ‚ü®U, Us, xU‚ü©,
    exact ‚ü®U, hUs Us, xU, le_Sup Us‚ü© }
end

/-- If `Œ±` has a basis consisting of compact opens, then an open set in `Œ±` is compact open iff
  it is a finite union of some elements in the basis -/
lemma is_basis.is_compact_open_iff_eq_finite_Union
  {Œπ : Type*} (b : Œπ ‚Üí opens Œ±) (hb : is_basis (set.range b))
  (hb' : ‚àÄ i, is_compact (b i : set Œ±)) (U : set Œ±) :
  is_compact U ‚àß is_open U ‚Üî ‚àÉ (s : set Œπ), s.finite ‚àß U = ‚ãÉ i ‚àà s, b i :=
begin
  apply is_compact_open_iff_eq_finite_Union_of_is_topological_basis
    (Œª i : Œπ, (b i).1),
  { convert hb, ext, simp },
  { exact hb' }
end

@[simp] lemma is_compact_element_iff (s : opens Œ±) :
  complete_lattice.is_compact_element s ‚Üî is_compact (s : set Œ±) :=
begin
  rw [is_compact_iff_finite_subcover, complete_lattice.is_compact_element_iff],
  refine ‚ü®_, Œª H Œπ U hU, _‚ü©,
  { introv H hU hU',
    obtain ‚ü®t, ht‚ü© := H Œπ (Œª i, ‚ü®U i, hU i‚ü©) (by simpa),
    refine ‚ü®t, set.subset.trans ht _‚ü©,
    rw [coe_finset_sup, finset.sup_eq_supr],
    refl },
  { obtain ‚ü®t, ht‚ü© := H (Œª i, U i) (Œª i, (U i).is_open)
      (by simpa using (show (s : set Œ±) ‚äÜ ‚Üë(supr U), from hU)),
    refine ‚ü®t, set.subset.trans ht _‚ü©,
    simp only [set.Union_subset_iff],
    show ‚àÄ i ‚àà t, U i ‚â§ t.sup U, from Œª i, finset.le_sup }
end

/-- The preimage of an open set, as an open set. -/
def comap (f : C(Œ±, Œ≤)) : frame_hom (opens Œ≤) (opens Œ±) :=
{ to_fun := Œª s, ‚ü®f ‚Åª¬π' s, s.2.preimage f.continuous‚ü©,
  map_Sup' := Œª s, ext $ by simp only [coe_Sup, preimage_Union, bUnion_image, coe_mk],
  map_inf' := Œª a b, rfl,
  map_top' := rfl }

@[simp] lemma comap_id : comap (continuous_map.id Œ±) = frame_hom.id _ :=
frame_hom.ext $ Œª a, ext rfl

lemma comap_mono (f : C(Œ±, Œ≤)) {s t : opens Œ≤} (h : s ‚â§ t) : comap f s ‚â§ comap f t :=
order_hom_class.mono (comap f) h

@[simp] lemma coe_comap (f : C(Œ±, Œ≤)) (U : opens Œ≤) : ‚Üë(comap f U) = f ‚Åª¬π' U := rfl

protected lemma comap_comp (g : C(Œ≤, Œ≥)) (f : C(Œ±, Œ≤)) :
  comap (g.comp f) = (comap f).comp (comap g) := rfl

protected lemma comap_comap (g : C(Œ≤, Œ≥)) (f : C(Œ±, Œ≤)) (U : opens Œ≥) :
  comap f (comap g U) = comap (g.comp f) U := rfl

lemma comap_injective [t0_space Œ≤] : injective (comap : C(Œ±, Œ≤) ‚Üí frame_hom (opens Œ≤) (opens Œ±)) :=
Œª f g h, continuous_map.ext $ Œª a, inseparable.eq $ inseparable_iff_forall_open.2 $ Œª s hs,
have comap f ‚ü®s, hs‚ü© = comap g ‚ü®s, hs‚ü©, from fun_like.congr_fun h ‚ü®_, hs‚ü©,
show a ‚àà f ‚Åª¬π' s ‚Üî a ‚àà g ‚Åª¬π' s, from set.ext_iff.1 (coe_inj.2 this) a

/-- A homeomorphism induces an order-preserving equivalence on open sets, by taking comaps. -/
@[simps apply { fully_applied := ff }]
def _root_.homeomorph.opens_congr (f : Œ± ‚âÉ‚Çú Œ≤) : opens Œ± ‚âÉo opens Œ≤ :=
{ to_fun := opens.comap f.symm.to_continuous_map,
  inv_fun := opens.comap f.to_continuous_map,
  left_inv := by { intro U, ext1, exact f.to_equiv.preimage_symm_preimage _ },
  right_inv := by { intro U, ext1, exact f.to_equiv.symm_preimage_preimage _ },
  map_rel_iff' := Œª U V, by simp only [‚Üê set_like.coe_subset_coe];
    exact f.symm.surjective.preimage_subset_preimage_iff }

@[simp] lemma _root_.homeomorph.opens_congr_symm (f : Œ± ‚âÉ‚Çú Œ≤) :
  f.opens_congr.symm = f.symm.opens_congr :=
rfl

instance [finite Œ±] : finite (opens Œ±) := finite.of_injective _ set_like.coe_injective

end opens

/-- The open neighborhoods of a point. See also `opens` or `nhds`. -/
structure open_nhds_of (x : Œ±) extends opens Œ± :=
(mem' : x ‚àà carrier)

namespace open_nhds_of

variables {x : Œ±}

lemma to_opens_injective : injective (to_opens : open_nhds_of x ‚Üí opens Œ±)
| ‚ü®_, _‚ü© ‚ü®_, _‚ü© rfl := rfl

instance : set_like (open_nhds_of x) Œ± :=
{ coe := Œª U, U.1,
  coe_injective' := set_like.coe_injective.comp to_opens_injective }

instance can_lift_set : can_lift (set Œ±) (open_nhds_of x) coe (Œª s, is_open s ‚àß x ‚àà s) :=
‚ü®Œª s hs, ‚ü®‚ü®‚ü®s, hs.1‚ü©, hs.2‚ü©, rfl‚ü©‚ü©

protected lemma mem (U : open_nhds_of x) : x ‚àà U := U.mem'
protected lemma is_open (U : open_nhds_of x) : is_open (U : set Œ±) := U.is_open'

instance : order_top (open_nhds_of x) :=
{ top := ‚ü®‚ä§, set.mem_univ _‚ü©,
  le_top := Œª _, subset_univ _ }

instance : inhabited (open_nhds_of x) := ‚ü®‚ä§‚ü©

instance : has_inf (open_nhds_of x) := ‚ü®Œª U V, ‚ü®U.1 ‚äì V.1, U.2, V.2‚ü©‚ü©

instance : has_sup (open_nhds_of x) := ‚ü®Œª U V, ‚ü®U.1 ‚äî V.1, or.inl U.2‚ü©‚ü©

instance : distrib_lattice (open_nhds_of x) :=
to_opens_injective.distrib_lattice _ (Œª _ _, rfl) (Œª _ _, rfl)

lemma basis_nhds : (ùìù x).has_basis (Œª U : open_nhds_of x, true) coe :=
(nhds_basis_opens x).to_has_basis (Œª U hU, ‚ü®‚ü®‚ü®U, hU.2‚ü©, hU.1‚ü©, trivial, subset.rfl‚ü©)
  (Œª U _, ‚ü®U, ‚ü®‚ü®U.mem, U.is_open‚ü©, subset.rfl‚ü©‚ü©)

/-- Preimage of an open neighborhood of `f x` under a continuous map `f` as a `lattice_hom`. -/
def comap (f : C(Œ±, Œ≤)) (x : Œ±) : lattice_hom (open_nhds_of (f x)) (open_nhds_of x) :=
{ to_fun := Œª U, ‚ü®opens.comap f U.1, U.mem‚ü©,
  map_sup' := Œª U V, rfl,
  map_inf' := Œª U V, rfl }

end open_nhds_of

end topological_space

namespace tactic

namespace auto_cases

/-- Find an `auto_cases_tac` which matches `topological_space.opens`. -/
meta def opens_find_tac : expr ‚Üí option auto_cases_tac
| `(topological_space.opens _)     := tac_cases
| _ := none

end auto_cases

/-- A version of `tactic.auto_cases` that works for `topological_space.opens`. -/
@[hint_tactic]
meta def auto_cases_opens : tactic string :=
auto_cases tactic.auto_cases.opens_find_tac

end tactic
