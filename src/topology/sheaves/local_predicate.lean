/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison, Adam Topaz
-/
import topology.sheaves.sheaf_of_functions
import topology.sheaves.stalks
import topology.local_homeomorph
import topology.sheaves.sheaf_condition.unique_gluing

/-!
# Functions satisfying a local predicate form a sheaf.

At this stage, in `topology/sheaves/sheaf_of_functions.lean`
we've proved that not-necessarily-continuous functions from a topological space
into some type (or type family) form a sheaf.

Why do the continuous functions form a sheaf?
The point is just that continuity is a local condition,
so one can use the lifting condition for functions to provide a candidate lift,
then verify that the lift is actually continuous by using the factorisation condition for the lift
(which guarantees that on each open set it agrees with the functions being lifted,
which were assumed to be continuous).

This file abstracts this argument to work for
any collection of dependent functions on a topological space
satisfying a "local predicate".

As an application, we check that continuity is a local predicate in this sense, and provide
* `Top.sheaf_to_Top`: continuous functions into a topological space form a sheaf

A sheaf constructed in this way has a natural map `stalk_to_fiber` from the stalks
to the types in the ambient type family.

We give conditions sufficient to show that this map is injective and/or surjective.
-/

universe v

noncomputable theory

variables {X : Top.{v}}
variables (T : X ‚Üí Type v)

open topological_space
open opposite
open category_theory
open category_theory.limits
open category_theory.limits.types

namespace Top

/--
Given a topological space `X : Top` and a type family `T : X ‚Üí Type`,
a `P : prelocal_predicate T` consists of:
* a family of predicates `P.pred`, one for each `U : opens X`, of the form `(Œ† x : U, T x) ‚Üí Prop`
* a proof that if `f : Œ† x : V, T x` satisfies the predicate on `V : opens X`, then
  the restriction of `f` to any open subset `U` also satisfies the predicate.
-/
structure prelocal_predicate :=
(pred : Œ† {U : opens X}, (Œ† x : U, T x) ‚Üí Prop)
(res : ‚àÄ {U V : opens X} (i : U ‚ü∂ V) (f : Œ† x : V, T x) (h : pred f), pred (Œª x : U, f (i x)))

variables (X)
/--
Continuity is a "prelocal" predicate on functions to a fixed topological space `T`.
-/
@[simps]
def continuous_prelocal (T : Top.{v}) : prelocal_predicate (Œª x : X, T) :=
{ pred := Œª U f, continuous f,
  res := Œª U V i f h, continuous.comp h (opens.open_embedding_of_le i.le).continuous, }

/-- Satisfying the inhabited linter. -/
instance inhabited_prelocal_predicate (T : Top.{v}) : inhabited (prelocal_predicate (Œª x : X, T)) :=
‚ü®continuous_prelocal X T‚ü©

variables {X}

/--
Given a topological space `X : Top` and a type family `T : X ‚Üí Type`,
a `P : local_predicate T` consists of:
* a family of predicates `P.pred`, one for each `U : opens X`, of the form `(Œ† x : U, T x) ‚Üí Prop`
* a proof that if `f : Œ† x : V, T x` satisfies the predicate on `V : opens X`, then
  the restriction of `f` to any open subset `U` also satisfies the predicate, and
* a proof that given some `f : Œ† x : U, T x`,
  if for every `x : U` we can find an open set `x ‚àà V ‚â§ U`
  so that the restriction of `f` to `V` satisfies the predicate,
  then `f` itself satisfies the predicate.
-/
structure local_predicate extends prelocal_predicate T :=
(locality : ‚àÄ {U : opens X} (f : Œ† x : U, T x)
  (w : ‚àÄ x : U, ‚àÉ (V : opens X) (m : x.1 ‚àà V) (i : V ‚ü∂ U), pred (Œª x : V, f (i x : U))), pred f)

variables (X)

/--
Continuity is a "local" predicate on functions to a fixed topological space `T`.
-/
def continuous_local (T : Top.{v}) : local_predicate (Œª x : X, T) :=
{ locality := Œª U f w,
   begin
     apply continuous_iff_continuous_at.2,
     intro x,
     specialize w x,
     rcases w with ‚ü®V, m, i, w‚ü©,
     dsimp at w,
     rw continuous_iff_continuous_at at w,
     specialize w ‚ü®x, m‚ü©,
     simpa using (opens.open_embedding_of_le i.le).continuous_at_iff.1 w,
   end,
  ..continuous_prelocal X T }

/-- Satisfying the inhabited linter. -/
instance inhabited_local_predicate (T : Top.{v}) : inhabited (local_predicate _) :=
‚ü®continuous_local X T‚ü©

variables {X T}

/--
Given a `P : prelocal_predicate`, we can always construct a `local_predicate`
by asking that the condition from `P` holds locally near every point.
-/
def prelocal_predicate.sheafify {T : X ‚Üí Type v} (P : prelocal_predicate T) : local_predicate T :=
{ pred := Œª U f, ‚àÄ x : U, ‚àÉ (V : opens X) (m : x.1 ‚àà V) (i : V ‚ü∂ U), P.pred (Œª x : V, f (i x : U)),
  res := Œª V U i f w x,
  begin
    specialize w (i x),
    rcases w with ‚ü®V', m', i', p‚ü©,
    refine ‚ü®V ‚äì V', ‚ü®x.2,m'‚ü©, opens.inf_le_left _ _, _‚ü©,
    convert P.res (opens.inf_le_right V V') _ p,
  end,
  locality := Œª U f w x,
  begin
    specialize w x,
    rcases w with ‚ü®V, m, i, p‚ü©,
    specialize p ‚ü®x.1, m‚ü©,
    rcases p with ‚ü®V', m', i', p'‚ü©,
    exact ‚ü®V', m', i' ‚â´ i, p'‚ü©,
  end }

lemma prelocal_predicate.sheafify_of {T : X ‚Üí Type v} {P : prelocal_predicate T}
  {U : opens X} {f : Œ† x : U, T x} (h : P.pred f) :
  P.sheafify.pred f :=
Œª x, ‚ü®U, x.2, ùüô _, by { convert h, ext ‚ü®y, w‚ü©, refl, }‚ü©

/--
The subpresheaf of dependent functions on `X` satisfying the "pre-local" predicate `P`.
-/
@[simps]
def subpresheaf_to_Types (P : prelocal_predicate T) : presheaf (Type v) X :=
{ obj := Œª U, { f : Œ† x : unop U, T x // P.pred f },
  map := Œª U V i f, ‚ü®Œª x, f.1 (i.unop x), P.res i.unop f.1 f.2‚ü© }.

namespace subpresheaf_to_Types

variables (P : prelocal_predicate T)

/--
The natural transformation including the subpresheaf of functions satisfying a local predicate
into the presheaf of all functions.
-/
def subtype : subpresheaf_to_Types P ‚ü∂ presheaf_to_Types X T :=
{ app := Œª U f, f.1 }

open Top.presheaf

/--
The functions satisfying a local predicate satisfy the sheaf condition.
-/
lemma is_sheaf (P : local_predicate T) :
  (subpresheaf_to_Types P.to_prelocal_predicate).is_sheaf :=
presheaf.is_sheaf_of_is_sheaf_unique_gluing_types _ $ Œª Œπ U sf sf_comp, begin
  -- We show the sheaf condition in terms of unique gluing.
  -- First we obtain a family of sections for the underlying sheaf of functions,
  -- by forgetting that the prediacte holds
  let sf' : Œ† i : Œπ, (presheaf_to_Types X T).obj (op (U i)) := Œª i, (sf i).val,
  -- Since our original family is compatible, this one is as well
  have sf'_comp : (presheaf_to_Types X T).is_compatible U sf' := Œª i j,
    congr_arg subtype.val (sf_comp i j),
  -- So, we can obtain a unique gluing
  obtain ‚ü®gl,gl_spec,gl_uniq‚ü© := (sheaf_to_Types X T).exists_unique_gluing U sf' sf'_comp,
  refine ‚ü®‚ü®gl,_‚ü©,_,_‚ü©,
  { -- Our first goal is to show that this chosen gluing satisfies the
    -- predicate. Of course, we use locality of the predicate.
    apply P.locality,
    rintros ‚ü®x, mem‚ü©,
    -- Once we're at a particular point `x`, we can select some open set `x ‚àà U i`.
    choose i hi using opens.mem_supr.mp mem,
    -- We claim that the predicate holds in `U i`
    use [U i, hi, opens.le_supr U i],
    -- This follows, since our original family `sf` satisfies the predicate
    convert (sf i).property,
    exact gl_spec i },
  -- It remains to show that the chosen lift is really a gluing for the subsheaf and
  -- that it is unique. Both of which follow immediately from the corresponding facts
  -- in the sheaf of functions without the local predicate.
  { intro i,
    ext1,
    exact (gl_spec i) },
  { intros gl' hgl',
    ext1,
    exact gl_uniq gl'.1 (Œª i, congr_arg subtype.val (hgl' i)) },
end

end subpresheaf_to_Types

/--
The subsheaf of the sheaf of all dependently typed functions satisfying the local predicate `P`.
-/
@[simps]
def subsheaf_to_Types (P : local_predicate T) : sheaf (Type v) X :=
‚ü®subpresheaf_to_Types P.to_prelocal_predicate, subpresheaf_to_Types.is_sheaf P‚ü©

/--
There is a canonical map from the stalk to the original fiber, given by evaluating sections.
-/
def stalk_to_fiber (P : local_predicate T) (x : X) :
  (subsheaf_to_Types P).1.stalk x ‚ü∂ T x :=
begin
  refine colimit.desc _
    { X := T x, Œπ := { app := Œª U f, _, naturality' := _ } },
  { exact f.1 ‚ü®x, (unop U).2‚ü©, },
  { tidy, }
end

@[simp] lemma stalk_to_fiber_germ (P : local_predicate T) (U : opens X) (x : U) (f) :
  stalk_to_fiber P x ((subsheaf_to_Types P).1.germ x f) = f.1 x :=
begin
  dsimp [presheaf.germ, stalk_to_fiber],
  cases x,
  simp,
  refl,
end

/--
The `stalk_to_fiber` map is surjective at `x` if
every point in the fiber `T x` has an allowed section passing through it.
-/
lemma stalk_to_fiber_surjective (P : local_predicate T) (x : X)
  (w : ‚àÄ (t : T x), ‚àÉ (U : open_nhds x) (f : Œ† y : U.1, T y) (h : P.pred f), f ‚ü®x, U.2‚ü© = t) :
  function.surjective (stalk_to_fiber P x) :=
Œª t,
begin
  rcases w t with ‚ü®U, f, h, rfl‚ü©,
  fsplit,
  { exact (subsheaf_to_Types P).1.germ ‚ü®x, U.2‚ü© ‚ü®f, h‚ü©, },
  { exact stalk_to_fiber_germ _ U.1 ‚ü®x, U.2‚ü© ‚ü®f, h‚ü©, }
end

/--
The `stalk_to_fiber` map is injective at `x` if any two allowed sections which agree at `x`
agree on some neighborhood of `x`.
-/
lemma stalk_to_fiber_injective (P : local_predicate T) (x : X)
  (w : ‚àÄ (U V : open_nhds x) (fU : Œ† y : U.1, T y) (hU : P.pred fU)
    (fV : Œ† y : V.1, T y) (hV : P.pred fV) (e : fU ‚ü®x, U.2‚ü© = fV ‚ü®x, V.2‚ü©),
    ‚àÉ (W : open_nhds x) (iU : W ‚ü∂ U) (iV : W ‚ü∂ V), ‚àÄ (w : W.1), fU (iU w : U.1) = fV (iV w : V.1)) :
  function.injective (stalk_to_fiber P x) :=
Œª tU tV h,
begin
  -- We promise to provide all the ingredients of the proof later:
  let Q :
    ‚àÉ (W : (open_nhds x)·µí·µñ) (s : Œ† w : (unop W).1, T w) (hW : P.pred s),
      tU = (subsheaf_to_Types P).1.germ ‚ü®x, (unop W).2‚ü© ‚ü®s, hW‚ü© ‚àß
      tV = (subsheaf_to_Types P).1.germ ‚ü®x, (unop W).2‚ü© ‚ü®s, hW‚ü© := _,
  { choose W s hW e using Q,
    exact e.1.trans e.2.symm, },
  -- Then use induction to pick particular representatives of `tU tV : stalk x`
  obtain ‚ü®U, ‚ü®fU, hU‚ü©, rfl‚ü© := jointly_surjective'.{v v} tU,
  obtain ‚ü®V, ‚ü®fV, hV‚ü©, rfl‚ü© := jointly_surjective'.{v v} tV,
  { -- Decompose everything into its constituent parts:
    dsimp,
    simp only [stalk_to_fiber, types.colimit.Œπ_desc_apply'] at h,
    specialize w (unop U) (unop V) fU hU fV hV h,
    rcases w with ‚ü®W, iU, iV, w‚ü©,
    -- and put it back together again in the correct order.
    refine ‚ü®(op W), (Œª w, fU (iU w : (unop U).1)), P.res _ _ hU, _‚ü©,
    rcases W with ‚ü®W, m‚ü©,
    exact ‚ü®colimit_sound iU.op (subtype.eq rfl),
           colimit_sound iV.op (subtype.eq (funext w).symm)‚ü©, },
end

/--
Some repackaging:
the presheaf of functions satisfying `continuous_prelocal` is just the same thing as
the presheaf of continuous functions.
-/
def subpresheaf_continuous_prelocal_iso_presheaf_to_Top (T : Top.{v}) :
  subpresheaf_to_Types (continuous_prelocal X T) ‚âÖ presheaf_to_Top X T :=
nat_iso.of_components
  (Œª X,
  { hom := by { rintro ‚ü®f, c‚ü©, exact ‚ü®f, c‚ü©, },
    inv := by { rintro ‚ü®f, c‚ü©, exact ‚ü®f, c‚ü©, },
    hom_inv_id' := by { ext ‚ü®f, p‚ü© x, refl, },
    inv_hom_id' := by { ext ‚ü®f, p‚ü© x, refl, }, })
  (by tidy)

/--
The sheaf of continuous functions on `X` with values in a space `T`.
-/
def sheaf_to_Top (T : Top.{v}) : sheaf (Type v) X :=
‚ü®presheaf_to_Top X T,
  presheaf.is_sheaf_of_iso (subpresheaf_continuous_prelocal_iso_presheaf_to_Top T)
    (subpresheaf_to_Types.is_sheaf (continuous_local X T))‚ü©

end Top
