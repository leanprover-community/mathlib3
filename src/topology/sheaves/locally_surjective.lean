/-
Copyright (c) 2022 Sam van Gool and Jake Levinson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sam van Gool, Jake Levinson
-/

import topology.sheaves.presheaf
import topology.sheaves.stalks
import category_theory.sites.surjective

/-!

# Locally surjective maps of presheaves.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Let `X` be a topological space, `â„±` and `ğ’¢` presheaves on `X`, `T : â„± âŸ¶ ğ’¢` a map.

In this file we formulate two notions for what it means for
`T` to be locally surjective:

  1. For each open set `U`, each section `t : ğ’¢(U)` is in the image of `T`
     after passing to some open cover of `U`.

  2. For each `x : X`, the map of *stalks* `Tâ‚“ : â„±â‚“ âŸ¶ ğ’¢â‚“` is surjective.

We prove that these are equivalent.

-/

universes v u

noncomputable theory

open category_theory
open topological_space
open opposite

namespace Top.presheaf

section locally_surjective

local attribute [instance] concrete_category.has_coe_to_fun
local attribute [instance] concrete_category.has_coe_to_sort

open_locale algebraic_geometry

/-- Let `C` be a concrete category, `X` a topological space. -/
variables {C : Type u} [category.{v} C] [concrete_category.{v} C] {X : Top.{v}}

/-- Let `â„±, ğ’¢ : (opens X)áµ’áµ– â¥¤ C` be `C`-valued presheaves on `X`. -/
variables {â„± ğ’¢ : X.presheaf C}

/--
A map of presheaves `T : â„± âŸ¶ ğ’¢` is **locally surjective** if for any open set `U`,
section `t` over `U`, and `x âˆˆ U`, there exists an open set `x âˆˆ V âŠ† U` and a section `s` over `V`
such that `$T_*(s_V) = t|_V$`.

See `is_locally_surjective_iff` below.
-/
def is_locally_surjective (T : â„± âŸ¶ ğ’¢) :=
  category_theory.is_locally_surjective (opens.grothendieck_topology X) T

lemma is_locally_surjective_iff (T : â„± âŸ¶ ğ’¢) :
  is_locally_surjective T â†”
    âˆ€ U t (x âˆˆ U), âˆƒ V (Î¹ : V âŸ¶ U), (âˆƒ s, T.app _ s = t |_â‚• Î¹) âˆ§ x âˆˆ V :=
iff.rfl

section surjective_on_stalks

variables [limits.has_colimits C] [limits.preserves_filtered_colimits (forget C)]

/-- An equivalent condition for a map of presheaves to be locally surjective
is for all the induced maps on stalks to be surjective. -/
lemma locally_surjective_iff_surjective_on_stalks (T : â„± âŸ¶ ğ’¢) :
  is_locally_surjective T â†”
  âˆ€ (x : X), function.surjective ((stalk_functor C x).map T) :=
begin
  split; intro hT,
  { /- human proof:
    Let g âˆˆ Î“â‚›â‚œ ğ’¢ x be a germ. Represent it on an open set U âŠ† X
    as âŸ¨t, UâŸ©. By local surjectivity, pass to a smaller open set V
    on which there exists s âˆˆ Î“_ â„± V mapping to t |_ V.
    Then the germ of s maps to g -/

    -- Let g âˆˆ Î“â‚›â‚œ ğ’¢ x be a germ.
    intros x g,
    -- Represent it on an open set U âŠ† X as âŸ¨t, UâŸ©.
    obtain âŸ¨U, hxU, t, rflâŸ© :=  ğ’¢.germ_exist x g,
    -- By local surjectivity, pass to a smaller open set V
    -- on which there exists s âˆˆ Î“_ â„± V mapping to t |_ V.
    rcases hT U t x hxU with âŸ¨V, Î¹, âŸ¨s, h_eqâŸ©, hxVâŸ©,

    -- Then the germ of s maps to g.
    use â„±.germ âŸ¨x, hxVâŸ© s,
    convert stalk_functor_map_germ_apply V âŸ¨x, hxVâŸ© T s,

    simpa [h_eq] using germ_res_apply ğ’¢ Î¹ âŸ¨x,hxVâŸ© t, },

  { /- human proof:
    Let U be an open set, t âˆˆ Î“ â„± U a section, x âˆˆ U a point.
    By surjectivity on stalks, the germ of t is the image of
    some germ f âˆˆ Î“â‚›â‚œ â„± x. Represent f on some open set V âŠ† X as âŸ¨s, VâŸ©.
    Then there is some possibly smaller open set x âˆˆ W âŠ† V âˆ© U on which
    we have T(s) |_ W = t |_ W. -/
    intros U t x hxU,
    set t_x := ğ’¢.germ âŸ¨x, hxUâŸ© t with ht_x,
    obtain âŸ¨s_x, hs_x : ((stalk_functor C x).map T) s_x = t_xâŸ© := hT x t_x,
    obtain âŸ¨V, hxV, s, rflâŸ© := â„±.germ_exist x s_x,
    -- rfl : â„±.germ x s = s_x
    have key_W := ğ’¢.germ_eq x hxV hxU (T.app _ s) t
      (by { convert hs_x,
            symmetry,
            convert stalk_functor_map_germ_apply _ _ _ s, }),
    obtain âŸ¨W, hxW, hWV, hWU, h_eqâŸ© := key_W,

    refine âŸ¨W, hWU, âŸ¨â„±.map hWV.op s, _âŸ©, hxWâŸ©,
    convert h_eq,
    simp only [â† comp_apply, T.naturality], },
end

end surjective_on_stalks

end locally_surjective

end Top.presheaf
