/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Mario Carneiro, Reid Barton, Andrew Yang
-/
import topology.category.Top.opens
import category_theory.limits.kan_extension
import category_theory.adjunction.opposites

/-!
# Presheaves on a topological space

We define `presheaf C X` simply as `(opens X)·µí·µñ ‚•§ C`,
and inherit the category structure with natural transformations as morphisms.

We define
* `pushforward_obj {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) : Y.presheaf C`
with notation `f _* ‚Ñ±`
and for `‚Ñ± : X.presheaf C` provide the natural isomorphisms
* `pushforward.id : (ùüô X) _* ‚Ñ± ‚âÖ ‚Ñ±`
* `pushforward.comp : (f ‚â´ g) _* ‚Ñ± ‚âÖ g _* (f _* ‚Ñ±)`
along with their `@[simp]` lemmas.

We also define the functors `pushforward` and `pullback` between the categories
`X.presheaf C` and `Y.presheaf C`, and provide their adjunction at
`pushforward_pullback_adjunction`.
-/

universes v u

open category_theory
open topological_space
open opposite

variables (C : Type u) [category.{v} C]

namespace Top

/-- The category of `C`-valued presheaves on a (bundled) topological space `X`. -/
@[derive category, nolint has_inhabited_instance]
def presheaf (X : Top.{v}) := (opens X)·µí·µñ ‚•§ C

variables {C}

namespace presheaf

/-- Pushforward a presheaf on `X` along a continuous map `f : X ‚ü∂ Y`, obtaining a presheaf
on `Y`. -/
def pushforward_obj {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) : Y.presheaf C :=
(opens.map f).op ‚ãô ‚Ñ±

infix ` _* `: 80 := pushforward_obj

@[simp] lemma pushforward_obj_obj {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) (U : (opens Y)·µí·µñ) :
  (f _* ‚Ñ±).obj U = ‚Ñ±.obj ((opens.map f).op.obj U) := rfl

@[simp] lemma pushforward_obj_map {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C)
  {U V : (opens Y)·µí·µñ} (i : U ‚ü∂ V) :
  (f _* ‚Ñ±).map i = ‚Ñ±.map ((opens.map f).op.map i) := rfl

/--
An equality of continuous maps induces a natural isomorphism between the pushforwards of a presheaf
along those maps.
-/
def pushforward_eq {X Y : Top.{v}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.presheaf C) :
  f _* ‚Ñ± ‚âÖ g _* ‚Ñ± :=
iso_whisker_right (nat_iso.op (opens.map_iso f g h).symm) ‚Ñ±

lemma pushforward_eq' {X Y : Top.{v}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.presheaf C) :
  f _* ‚Ñ± = g _* ‚Ñ± :=
by rw h

@[simp] lemma pushforward_eq_hom_app
  {X Y : Top.{v}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.presheaf C) (U) :
  (pushforward_eq h ‚Ñ±).hom.app U =
    ‚Ñ±.map (begin dsimp [functor.op], apply quiver.hom.op, apply eq_to_hom, rw h, end) :=
by simp [pushforward_eq]

@[simp]
lemma pushforward_eq_rfl {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) (U) :
  (pushforward_eq (rfl : f = f) ‚Ñ±).hom.app (op U) = ùüô _ :=
begin
  dsimp [pushforward_eq],
  simp,
end

lemma pushforward_eq_eq {X Y : Top.{v}} {f g : X ‚ü∂ Y} (h‚ÇÅ h‚ÇÇ : f = g) (‚Ñ± : X.presheaf C) :
  ‚Ñ±.pushforward_eq h‚ÇÅ = ‚Ñ±.pushforward_eq h‚ÇÇ :=
rfl

namespace pushforward
variables {X : Top.{v}} (‚Ñ± : X.presheaf C)

/-- The natural isomorphism between the pushforward of a presheaf along the identity continuous map
and the original presheaf. -/
def id : (ùüô X) _* ‚Ñ± ‚âÖ ‚Ñ± :=
(iso_whisker_right (nat_iso.op (opens.map_id X).symm) ‚Ñ±) ‚â™‚â´ functor.left_unitor _

lemma id_eq : (ùüô X) _* ‚Ñ± = ‚Ñ± :=
by { unfold pushforward_obj, rw opens.map_id_eq, erw functor.id_comp }

@[simp] lemma id_hom_app' (U) (p) :
  (id ‚Ñ±).hom.app (op ‚ü®U, p‚ü©) = ‚Ñ±.map (ùüô (op ‚ü®U, p‚ü©)) :=
by { dsimp [id], simp, }

local attribute [tidy] tactic.op_induction'

@[simp, priority 990] lemma id_hom_app (U) :
  (id ‚Ñ±).hom.app U = ‚Ñ±.map (eq_to_hom (opens.op_map_id_obj U)) := by tidy

@[simp] lemma id_inv_app' (U) (p) : (id ‚Ñ±).inv.app (op ‚ü®U, p‚ü©) = ‚Ñ±.map (ùüô (op ‚ü®U, p‚ü©)) :=
by { dsimp [id], simp, }

/-- The natural isomorphism between
the pushforward of a presheaf along the composition of two continuous maps and
the corresponding pushforward of a pushforward. -/
def comp {Y Z : Top.{v}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g) _* ‚Ñ± ‚âÖ g _* (f _* ‚Ñ±) :=
iso_whisker_right (nat_iso.op (opens.map_comp f g).symm) ‚Ñ±

lemma comp_eq {Y Z : Top.{v}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : (f ‚â´ g) _* ‚Ñ± = g _* (f _* ‚Ñ±) :=
rfl

@[simp] lemma comp_hom_app {Y Z : Top.{v}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U) :
  (comp ‚Ñ± f g).hom.app U = ùüô _ :=
by { dsimp [comp], tidy, }

@[simp] lemma comp_inv_app {Y Z : Top.{v}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (U) :
  (comp ‚Ñ± f g).inv.app U = ùüô _ :=
by { dsimp [comp], tidy, }

end pushforward

/--
A morphism of presheaves gives rise to a morphisms of the pushforwards of those presheaves.
-/
@[simps]
def pushforward_map {X Y : Top.{v}} (f : X ‚ü∂ Y) {‚Ñ± ùí¢ : X.presheaf C} (Œ± : ‚Ñ± ‚ü∂ ùí¢) :
  f _* ‚Ñ± ‚ü∂ f _* ùí¢ :=
{ app := Œª U, Œ±.app _,
  naturality' := Œª U V i, by { erw Œ±.naturality, refl, } }

open category_theory.limits
section pullback
variable [has_colimits C]
noncomputable theory

/--
Pullback a presheaf on `Y` along a continuous map `f : X ‚ü∂ Y`, obtaining a presheaf on `X`.

This is defined in terms of left Kan extensions, which is just a fancy way of saying
"take the colimits over the open sets whose preimage contains U".
-/
@[simps]
def pullback_obj {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : Y.presheaf C) : X.presheaf C :=
(Lan (opens.map f).op).obj ‚Ñ±

/-- Pulling back along continuous maps is functorial. -/
def pullback_map {X Y : Top.{v}} (f : X ‚ü∂ Y) {‚Ñ± ùí¢ : Y.presheaf C} (Œ± : ‚Ñ± ‚ü∂ ùí¢) :
  pullback_obj f ‚Ñ± ‚ü∂ pullback_obj f ùí¢ :=
(Lan (opens.map f).op).map Œ±

/-- If `f '' U` is open, then `f‚Åª¬π‚Ñ± U ‚âÖ ‚Ñ± (f '' U)`.  -/
@[simps]
def pullback_obj_obj_of_image_open {X Y : Top.{v}} (f : X ‚ü∂ Y) (‚Ñ± : Y.presheaf C) (U : opens X)
  (H : is_open (f '' U)) : (pullback_obj f ‚Ñ±).obj (op U) ‚âÖ ‚Ñ±.obj (op ‚ü®_, H‚ü©) :=
begin
  let x : costructured_arrow (opens.map f).op (op U) :=
  { left := op ‚ü®f '' U, H‚ü©,
    hom := ((@hom_of_le _ _ _ ((opens.map f).obj ‚ü®_, H‚ü©) (set.image_preimage.le_u_l _)).op :
    op ((opens.map f).obj (‚ü®‚áëf '' ‚ÜëU, H‚ü©)) ‚ü∂ op U) },
  have hx : is_terminal x :=
  { lift := Œª s,
    begin
      fapply costructured_arrow.hom_mk,
      change op (unop _) ‚ü∂ op (‚ü®_, H‚ü© : opens _),
      refine (hom_of_le _).op,
      exact (set.image_subset f s.X.hom.unop.le).trans (set.image_preimage.l_u_le ‚Üë(unop s.X.left)),
      simp
    end },
  exact is_colimit.cocone_point_unique_up_to_iso
    (colimit.is_colimit _)
    (colimit_of_diagram_terminal hx _),
end

namespace pullback
variables {X Y : Top.{v}} (‚Ñ± : Y.presheaf C)

local attribute [reassoc] colimit.pre_desc

/-- The pullback along the identity is isomorphic to the original presheaf. -/
def id : pullback_obj (ùüô _) ‚Ñ± ‚âÖ ‚Ñ± :=
nat_iso.of_components
  (Œª U, pullback_obj_obj_of_image_open (ùüô _) ‚Ñ± (unop U) (by simpa using U.unop.2) ‚â™‚â´
    ‚Ñ±.map_iso (eq_to_iso (by simp)))
  (Œª U V i,
  begin
      ext, simp[-eq_to_hom_map,-eq_to_iso_map],
      erw category_theory.limits.colimit.pre_desc_assoc,
      erw colimit.Œπ_desc_assoc,
      erw colimit.Œπ_desc_assoc,
      dsimp, simp only [‚Üê‚Ñ±.map_comp], congr
  end)

lemma id_inv_app (U : opens Y) :
  (id ‚Ñ±).inv.app (op U) = colimit.Œπ (Lan.diagram (opens.map (ùüô Y)).op ‚Ñ± (op U))
    (@costructured_arrow.mk _ _ _ _ _ (op U) _ (eq_to_hom (by simp))) :=
begin
  dsimp[id], simp[-eq_to_hom_map,-eq_to_iso_map],dsimp[colimit_of_diagram_terminal],
  delta Lan.diagram,
  refine eq.trans _ (category.id_comp _),
  rw ‚Üê ‚Ñ±.map_id,
  congr,
  any_goals { apply subsingleton.helim },
  all_goals { simp }
end

end pullback
end pullback
variable (C)

/--
The pushforward functor.
-/
def pushforward {X Y : Top.{v}} (f : X ‚ü∂ Y) : X.presheaf C ‚•§ Y.presheaf C :=
{ obj := pushforward_obj f,
  map := @pushforward_map _ _ X Y f }

lemma id_pushforward {X : Top.{v}} : pushforward C (ùüô X) = ùü≠ (X.presheaf C) :=
begin
  apply category_theory.functor.ext,
  { intros, ext U, have h := f.congr,
    erw h (opens.op_map_id_obj U), simpa },
  { intros, apply pushforward.id_eq },
end
variables [has_colimits C]

/-- Pullback a presheaf on `Y` along a continuous map `f : X ‚ü∂ Y`, obtaining a presheaf
on `X`. -/
@[simps]
def pullback {X Y : Top.{v}} (f : X ‚ü∂ Y) : Y.presheaf C ‚•§ X.presheaf C := Lan (opens.map f).op

/-- The pullback and pushforward along a continuous map are adjoint to each other. -/
@[simps unit_app_app counit_app_app]
def pushforward_pullback_adjunction {X Y : Top.{v}} (f : X ‚ü∂ Y) :
  pullback C f ‚ä£ pushforward C f := Lan.adjunction _ _

end presheaf
end Top
