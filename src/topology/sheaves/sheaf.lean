/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import topology.sheaves.presheaf
import category_theory.limits.punit
import category_theory.limits.shapes.products
import category_theory.limits.shapes.equalizers

/-!
# Sheaves

We define sheaves on a topological space, with values in an arbitrary category.

The sheaf condition for a `F : presheaf C X` requires that the morphism
`F.obj U âŸ¶ âˆ F.obj (U i)` (where `U` is some open set which is the union of the `U i`)
is the equalizer of the two morphisms
`âˆ F.obj (U i) âŸ¶ âˆ F.obj (U i) âŠ“ (U j)`.

We provide the instance `category (sheaf C X)` as the full subcategory of presheaves,
and the fully faithful functor `sheaf.forget : sheaf C X â¥¤ presheaf C X`.
-/

universes v u

open category_theory
open category_theory.limits
open topological_space
open opposite
open topological_space.opens

namespace Top

variables {C : Type u} [category.{v} C] [has_products C]
variables {X : Top.{v}} (F : presheaf C X) {Î¹ : Type v} (U : Î¹ â†’ opens X)

namespace sheaf_condition

/-- The product of the sections of a presheaf over a family of open sets. -/
def pi_opens : C := âˆ (Î» i : Î¹, F.obj (op (U i)))
/--
The product of the sections of a presheaf over the pairwise intersections of
a family of open sets.
-/
def pi_inters : C := âˆ (Î» p : Î¹ Ã— Î¹, F.obj (op (U p.1 âŠ“ U p.2)))

/--
The morphism `Î  F.obj (U i) âŸ¶ Î  F.obj (U i) âŠ“ (U j)` whose components
are given by the restriction maps from `U i` to `U i âŠ“ U j`.
-/
def left_res : pi_opens F U âŸ¶ pi_inters F U :=
pi.lift (Î» p : Î¹ Ã— Î¹, pi.Ï€ _ p.1 â‰« F.map (inf_le_left (U p.1) (U p.2)).op)

/--
The morphism `Î  F.obj (U i) âŸ¶ Î  F.obj (U i) âŠ“ (U j)` whose components
are given by the restriction maps from `U j` to `U i âŠ“ U j`.
-/
def right_res : pi_opens F U âŸ¶ pi_inters F U :=
pi.lift (Î» p : Î¹ Ã— Î¹, pi.Ï€ _ p.2 â‰« F.map (inf_le_right (U p.1) (U p.2)).op)

/--
The morphism `F.obj U âŸ¶ Î  F.obj (U i)` whose components
are given by the restriction maps from `U j` to `U i âŠ“ U j`.
-/
def res : F.obj (op (supr U)) âŸ¶ pi_opens F U :=
pi.lift (Î» i : Î¹, F.map (topological_space.opens.le_supr U i).op)

lemma w : res F U â‰« left_res F U = res F U â‰« right_res F U :=
begin
  dsimp [res, left_res, right_res],
  ext,
  simp only [limit.lift_Ï€, limit.lift_Ï€_assoc, fan.mk_Ï€_app, category.assoc],
  rw [â†F.map_comp],
  rw [â†F.map_comp],
  congr,
end

/--
The equalizer diagram for the sheaf condition.
-/
@[reducible]
def diagram : walking_parallel_pair.{v} â¥¤ C :=
parallel_pair (left_res F U) (right_res F U)

/--
The restriction map `F.obj U âŸ¶ Î  F.obj (U i)` gives a cone over the equalizer diagram
for the sheaf condition. The sheaf condition asserts this cone is a limit cone.
-/
def fork : fork.{v} (left_res F U) (right_res F U) := fork.of_Î¹ _ (w F U)

@[simp]
lemma fork_X : (fork F U).X = F.obj (op (supr U)) := rfl

@[simp]
lemma fork_Î¹ : (fork F U).Î¹ = res F U := rfl
@[simp]
lemma fork_Ï€_app_walking_parallel_pair_zero :
  (fork F U).Ï€.app walking_parallel_pair.zero = res F U := rfl
@[simp]
lemma fork_Ï€_app_walking_parallel_pair_one :
  (fork F U).Ï€.app walking_parallel_pair.one = res F U â‰« left_res F U := rfl

variables {F} {G : presheaf C X}

/-- Isomorphic presheaves have isomorphic `pi_opens` for any cover `U`. -/
@[simp]
def pi_opens.iso_of_iso (Î± : F â‰… G) : pi_opens F U â‰… pi_opens G U :=
pi.map_iso (Î» X, Î±.app _)

/-- Isomorphic presheaves have isomorphic `pi_inters` for any cover `U`. -/
@[simp]
def pi_inters.iso_of_iso (Î± : F â‰… G) : pi_inters F U â‰… pi_inters G U :=
pi.map_iso (Î» X, Î±.app _)

/-- Isomorphic presheaves have isomorphic sheaf condition diagrams. -/
def diagram.iso_of_iso (Î± : F â‰… G) : diagram F U â‰… diagram G U :=
nat_iso.of_components
  begin rintro âŸ¨âŸ©, exact pi_opens.iso_of_iso U Î±, exact pi_inters.iso_of_iso U Î± end
  begin
    rintro âŸ¨âŸ© âŸ¨âŸ© âŸ¨âŸ©,
    { simp, },
    { ext, simp [left_res], },
    { ext, simp [right_res], },
    { simp, },
  end.

/--
If `F G : presheaf C X` are isomorphic presheaves,
then the `fork F U`, the canonical cone of the sheaf condition diagram for `F`,
is isomorphic to `fork F G` postcomposed with the corresponding isomorphic between
sheaf condition diagrams.
-/
def fork.iso_of_iso (Î± : F â‰… G) :
  fork F U â‰… (cones.postcompose (diagram.iso_of_iso U Î±).inv).obj (fork G U) :=
begin
  fapply fork.ext,
  { apply Î±.app, },
  { ext,
    dunfold fork.Î¹, -- Ugh, `simp` can't unfold abbreviations.
    simp [res, diagram.iso_of_iso], }
end

section open_embedding

variables {V : Top.{v}} {j : V âŸ¶ X} (oe : open_embedding j)
variables (ğ’° : Î¹ â†’ opens V)

/--
Push forward a cover along an open embedding.
-/
@[simp]
def cover.of_open_embedding : Î¹ â†’ opens X := (Î» i, oe.is_open_map.functor.obj (ğ’° i))

/--
The isomorphism between `pi_opens` corresponding to an open embedding.
-/
@[simp]
def pi_opens.iso_of_open_embedding :
  pi_opens (oe.is_open_map.functor.op â‹™ F) ğ’° â‰… pi_opens F (cover.of_open_embedding oe ğ’°) :=
pi.map_iso (Î» X, F.map_iso (iso.refl _))

/--
The isomorphism between `pi_inters` corresponding to an open embedding.
-/
@[simp]
def pi_inters.iso_of_open_embedding :
  pi_inters (oe.is_open_map.functor.op â‹™ F) ğ’° â‰… pi_inters F (cover.of_open_embedding oe ğ’°) :=
pi.map_iso (Î» X, F.map_iso
  begin
    dsimp [is_open_map.functor],
    exact iso.op
    { hom := hom_of_le (by
      { simp only [oe.to_embedding.inj, set.image_inter],
        apply le_refl _, }),
      inv := hom_of_le (by
      { simp only [oe.to_embedding.inj, set.image_inter],
        apply le_refl _, }), },
  end)

/-- The isomorphism of sheaf condition diagrams corresponding to an open embedding. -/
def diagram.iso_of_open_embedding :
  diagram (oe.is_open_map.functor.op â‹™ F) ğ’° â‰… diagram F (cover.of_open_embedding oe ğ’°) :=
nat_iso.of_components
  begin
    rintro âŸ¨âŸ©,
    exact pi_opens.iso_of_open_embedding oe ğ’°,
    exact pi_inters.iso_of_open_embedding oe ğ’°
  end
  begin
    rintro âŸ¨âŸ© âŸ¨âŸ© âŸ¨âŸ©,
    { simp, },
    { ext,
      dsimp [left_res, is_open_map.functor],
      simp only [limit.lift_Ï€, cones.postcompose_obj_Ï€, iso.op_hom, discrete.nat_iso_hom_app,
        functor.map_iso_refl, functor.map_iso_hom, limit.map_Ï€_assoc, limit.lift_map, fan.mk_Ï€_app,
        nat_trans.comp_app, category.assoc],
      dsimp,
      rw [category.id_comp, â†F.map_comp],
      refl, },
    { ext,
      dsimp [right_res, is_open_map.functor],
      simp only [limit.lift_Ï€, cones.postcompose_obj_Ï€, iso.op_hom, discrete.nat_iso_hom_app,
        functor.map_iso_refl, functor.map_iso_hom, limit.map_Ï€_assoc, limit.lift_map, fan.mk_Ï€_app,
        nat_trans.comp_app, category.assoc],
      dsimp,
      rw [category.id_comp, â†F.map_comp],
      refl, },
    { simp, },
  end.

/--
If `F : presheaf C X` is a presheaf, and `oe : U âŸ¶ X` is an open embedding,
then the sheaf condition fork for a cover `ğ’°` in `U` for the composition of `oe` and `F` is
isomorphic to sheaf condition fork for `oe '' ğ’°`, precomposed with the isomorphism
of indexing diagrams `diagram.iso_of_open_embedding`.

We use this to show that the restriction of sheaf along an open embedding is still a sheaf.
-/
def fork.iso_of_open_embedding :
  fork (oe.is_open_map.functor.op â‹™ F) ğ’° â‰…
    (cones.postcompose (diagram.iso_of_open_embedding oe ğ’°).inv).obj
      (fork F (cover.of_open_embedding oe ğ’°)) :=
begin
  fapply fork.ext,
  { dsimp [is_open_map.functor],
    exact
    F.map_iso (iso.op
    { hom := hom_of_le (by simp only [supr_s, supr_mk, le_def, subtype.coe_mk, set.le_eq_subset, set.image_Union]),
      inv := hom_of_le (by simp only [supr_s, supr_mk, le_def, subtype.coe_mk, set.le_eq_subset, set.image_Union]), }), },
  { ext,
    dunfold fork.Î¹, -- Ugh, it is unpleasant that we need this.
    simp only [res, diagram.iso_of_open_embedding, discrete.nat_iso_inv_app, functor.map_iso_inv,
      limit.lift_Ï€, cones.postcompose_obj_Ï€, functor.comp_map,
      fork_Ï€_app_walking_parallel_pair_zero, pi_opens.iso_of_open_embedding,
      nat_iso.of_components.inv_app, functor.map_iso_refl, functor.op_map, limit.lift_map,
      fan.mk_Ï€_app, nat_trans.comp_app, has_hom.hom.unop_op, category.assoc],
    dsimp,
    rw [category.comp_id, â†F.map_comp],
    refl, },
end

end open_embedding

end sheaf_condition

/--
The sheaf condition for a `F : presheaf C X` requires that the morphism
`F.obj U âŸ¶ âˆ F.obj (U i)` (where `U` is some open set which is the union of the `U i`)
is the equalizer of the two morphisms
`âˆ F.obj (U i) âŸ¶ âˆ F.obj (U i) âŠ“ (U j)`.
-/
-- One might prefer to work with sets of opens, rather than indexed families,
-- which would reduce the universe level here to `max u v`.
-- However as it's a subsingleton the universe level doesn't matter much.
@[derive subsingleton]
def sheaf_condition (F : presheaf C X) : Type (max u (v+1)) :=
Î  â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens X), is_limit (sheaf_condition.fork F U)

/--
The presheaf valued in `punit` over any topological space is a sheaf.
-/
def sheaf_condition_punit (F : presheaf (category_theory.discrete punit) X) :
  sheaf_condition F :=
Î» Î¹ U, punit_cone_is_limit

-- Let's construct a trivial example, to keep the inhabited linter happy.
instance sheaf_condition_inhabited (F : presheaf (category_theory.discrete punit) X) :
  inhabited (sheaf_condition F) := âŸ¨sheaf_condition_punit FâŸ©

/--
Transfer the sheaf condition across an isomorphism of presheaves.
-/
def sheaf_condition_equiv_of_iso {F G : presheaf C X} (Î± : F â‰… G) :
  sheaf_condition F â‰ƒ sheaf_condition G :=
equiv_of_subsingleton_of_subsingleton
(Î» c Î¹ U, is_limit.of_iso_limit
  ((is_limit.postcompose_inv_equiv _ _).symm (c U)) (sheaf_condition.fork.iso_of_iso U Î±.symm).symm)
(Î» c Î¹ U, is_limit.of_iso_limit
  ((is_limit.postcompose_inv_equiv _ _).symm (c U)) (sheaf_condition.fork.iso_of_iso U Î±).symm)

variables (C X)

/--
A `sheaf C X` is a presheaf of objects from `C` over a (bundled) topological space `X`,
satisfying the sheaf condition.
-/
structure sheaf :=
(presheaf : presheaf C X)
(sheaf_condition : sheaf_condition presheaf)

instance : category (sheaf C X) := induced_category.category sheaf.presheaf

-- Let's construct a trivial example, to keep the inhabited linter happy.
instance sheaf_inhabited : inhabited (sheaf (category_theory.discrete punit) X) :=
âŸ¨{ presheaf := functor.star _, sheaf_condition := default _ }âŸ©

namespace sheaf

/--
The forgetful functor from sheaves to presheaves.
-/
@[derive [full, faithful]]
def forget : Top.sheaf C X â¥¤ Top.presheaf C X := induced_functor sheaf.presheaf

end sheaf

end Top
