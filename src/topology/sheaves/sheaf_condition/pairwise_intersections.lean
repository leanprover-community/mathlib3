/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/

import topology.sheaves.sheaf_condition.sites
import category_theory.limits.preserves.basic
import category_theory.category.pairwise
import category_theory.limits.constructions.binary_products

/-!
# Equivalent formulations of the sheaf condition

We give an equivalent formulation of the sheaf condition.

Given any indexed type `Î¹`, we define `overlap Î¹`,
a category with objects corresponding to
* individual open sets, `single i`, and
* intersections of pairs of open sets, `pair i j`,
with morphisms from `pair i j` to both `single i` and `single j`.

Any open cover `U : Î¹ â†’ opens X` provides a functor `diagram U : overlap Î¹ â¥¤ (opens X)áµ’áµ–`.

There is a canonical cone over this functor, `cone U`, whose cone point is `supr U`,
and in fact this is a limit cone.

A presheaf `F : presheaf C X` is a sheaf precisely if it preserves this limit.
We express this in two equivalent ways, as
* `is_limit (F.map_cone (cone U))`, or
* `preserves_limit (diagram U) F`
-/

noncomputable theory

universes v u

open topological_space
open Top
open opposite
open category_theory
open category_theory.limits

namespace Top.presheaf

variables {X : Top.{v}}

variables {C : Type u} [category.{v} C]

/--
An alternative formulation of the sheaf condition
(which we prove equivalent to the usual one below as
`is_sheaf_iff_is_sheaf_pairwise_intersections`).

A presheaf is a sheaf if `F` sends the cone `(pairwise.cocone U).op` to a limit cone.
(Recall `pairwise.cocone U` has cone point `supr U`, mapping down to the `U i` and the `U i âŠ“ U j`.)
-/
def is_sheaf_pairwise_intersections (F : presheaf C X) : Prop :=
âˆ€ â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens X), nonempty (is_limit (F.map_cone (pairwise.cocone U).op))

/--
An alternative formulation of the sheaf condition
(which we prove equivalent to the usual one below as
`is_sheaf_iff_is_sheaf_preserves_limit_pairwise_intersections`).

A presheaf is a sheaf if `F` preserves the limit of `pairwise.diagram U`.
(Recall `pairwise.diagram U` is the diagram consisting of the pairwise intersections
`U i âŠ“ U j` mapping into the open sets `U i`. This diagram has limit `supr U`.)
-/
def is_sheaf_preserves_limit_pairwise_intersections (F : presheaf C X) : Prop :=
âˆ€ â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens X), nonempty (preserves_limit (pairwise.diagram U).op F)

/-!
The remainder of this file shows that these conditions are equivalent
to the usual sheaf condition.
-/

variables [has_products C]

namespace sheaf_condition_pairwise_intersections

open category_theory.pairwise category_theory.pairwise.hom
open sheaf_condition_equalizer_products

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_functor_obj (F : presheaf C X)
  â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens â†¥X) (c : limits.cone ((diagram U).op â‹™ F)) :
  limits.cone (sheaf_condition_equalizer_products.diagram F U) :=
{ X := c.X,
  Ï€ :=
  { app := Î» Z,
      walking_parallel_pair.cases_on Z
        (pi.lift (Î» (i : Î¹), c.Ï€.app (op (single i))))
        (pi.lift (Î» (b : Î¹ Ã— Î¹), c.Ï€.app (op (pair b.1 b.2)))),
    naturality' := Î» Y Z f,
    begin
      cases Y; cases Z; cases f,
      { ext i, dsimp,
        simp only [limit.lift_Ï€, category.id_comp, fan.mk_Ï€_app, category_theory.functor.map_id,
          category.assoc],
        dsimp,
        simp only [limit.lift_Ï€, category.id_comp, fan.mk_Ï€_app], },
      { ext âŸ¨i, jâŸ©, dsimp [sheaf_condition_equalizer_products.left_res],
        simp only [limit.lift_Ï€, limit.lift_Ï€_assoc, category.id_comp, fan.mk_Ï€_app,
          category.assoc],
        have h := c.Ï€.naturality (quiver.hom.op (hom.left i j)),
        dsimp at h,
        simpa using h, },
      { ext âŸ¨i, jâŸ©, dsimp [sheaf_condition_equalizer_products.right_res],
        simp only [limit.lift_Ï€, limit.lift_Ï€_assoc, category.id_comp, fan.mk_Ï€_app,
          category.assoc],
        have h := c.Ï€.naturality (quiver.hom.op (hom.right i j)),
        dsimp at h,
        simpa using h, },
      { ext i, dsimp,
        simp only [limit.lift_Ï€, category.id_comp, fan.mk_Ï€_app, category_theory.functor.map_id,
          category.assoc],
        dsimp,
        simp only [limit.lift_Ï€, category.id_comp, fan.mk_Ï€_app], },
    end, }, }

section
local attribute [tidy] tactic.case_bash

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_functor (F : presheaf C X)
  â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens â†¥X) :
  limits.cone ((diagram U).op â‹™ F) â¥¤
    limits.cone (sheaf_condition_equalizer_products.diagram F U) :=
{ obj := Î» c, cone_equiv_functor_obj F U c,
  map := Î» c c' f,
  { hom := f.hom,
    w' := Î» j, begin
      cases j;
      { ext, simp only [limits.fan.mk_Ï€_app, limits.cone_morphism.w,
        limits.limit.lift_Ï€, category.assoc, cone_equiv_functor_obj_Ï€_app], },
    end }, }.

end

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_inverse_obj (F : presheaf C X)
  â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens â†¥X)
  (c : limits.cone (sheaf_condition_equalizer_products.diagram F U)) :
  limits.cone ((diagram U).op â‹™ F) :=
{ X := c.X,
  Ï€ :=
  { app :=
    begin
      intro x,
      induction x using opposite.rec,
      rcases x with (âŸ¨iâŸ©|âŸ¨i,jâŸ©),
      { exact c.Ï€.app (walking_parallel_pair.zero) â‰« pi.Ï€ _ i, },
      { exact c.Ï€.app (walking_parallel_pair.one) â‰« pi.Ï€ _ (i, j), }
    end,
    naturality' :=
    begin
      intros x y f,
      induction x using opposite.rec,
      induction y using opposite.rec,
      have ef : f = f.unop.op := rfl,
      revert ef,
      generalize : f.unop = f',
      rintro rfl,
      rcases x with âŸ¨iâŸ©|âŸ¨âŸ©; rcases y with âŸ¨âŸ©|âŸ¨j,jâŸ©; rcases f' with âŸ¨âŸ©,
      { dsimp, erw [F.map_id], simp, },
      { dsimp, simp only [category.id_comp, category.assoc],
        have h := c.Ï€.naturality (walking_parallel_pair_hom.left),
        dsimp [sheaf_condition_equalizer_products.left_res] at h,
        simp only [category.id_comp] at h,
        have h' := h =â‰« pi.Ï€ _ (i, j),
        rw h',
        simp,
        refl, },
      { dsimp, simp only [category.id_comp, category.assoc],
        have h := c.Ï€.naturality (walking_parallel_pair_hom.right),
        dsimp [sheaf_condition_equalizer_products.right_res] at h,
        simp only [category.id_comp] at h,
        have h' := h =â‰« pi.Ï€ _ (j, i),
        rw h',
        simp,
        refl, },
      { dsimp, erw [F.map_id], simp, },
    end, }, }

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_inverse (F : presheaf C X)
  â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens â†¥X) :
  limits.cone (sheaf_condition_equalizer_products.diagram F U) â¥¤
    limits.cone ((diagram U).op â‹™ F) :=
{ obj := Î» c, cone_equiv_inverse_obj F U c,
  map := Î» c c' f,
  { hom := f.hom,
    w' :=
    begin
      intro x,
      induction x using opposite.rec,
      rcases x with (âŸ¨iâŸ©|âŸ¨i,jâŸ©),
      { dsimp,
        rw [â†(f.w walking_parallel_pair.zero), category.assoc], },
      { dsimp,
        rw [â†(f.w walking_parallel_pair.one), category.assoc], },
    end }, }.

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_unit_iso_app (F : presheaf C X) â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens â†¥X)
  (c : cone ((diagram U).op â‹™ F)) :
  (ğŸ­ (cone ((diagram U).op â‹™ F))).obj c â‰…
    (cone_equiv_functor F U â‹™ cone_equiv_inverse F U).obj c :=
{ hom :=
  { hom := ğŸ™ _,
    w' := Î» j, begin
      induction j using opposite.rec, rcases j;
      { dsimp, simp only [limits.fan.mk_Ï€_app, category.id_comp, limits.limit.lift_Ï€], }
    end, },
  inv :=
  { hom := ğŸ™ _,
    w' := Î» j, begin
      induction j using opposite.rec, rcases j;
      { dsimp, simp only [limits.fan.mk_Ï€_app, category.id_comp, limits.limit.lift_Ï€], }
    end },
  hom_inv_id' := begin
    ext,
    simp only [category.comp_id, limits.cone.category_comp_hom, limits.cone.category_id_hom],
  end,
  inv_hom_id' := begin
    ext,
    simp only [category.comp_id, limits.cone.category_comp_hom, limits.cone.category_id_hom],
  end, }

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_unit_iso (F : presheaf C X) â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens X) :
  ğŸ­ (limits.cone ((diagram U).op â‹™ F)) â‰…
    cone_equiv_functor F U â‹™ cone_equiv_inverse F U :=
nat_iso.of_components (cone_equiv_unit_iso_app F U) (by tidy)

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_counit_iso (F : presheaf C X) â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens X) :
  cone_equiv_inverse F U â‹™ cone_equiv_functor F U â‰…
    ğŸ­ (limits.cone (sheaf_condition_equalizer_products.diagram F U)) :=
nat_iso.of_components (Î» c,
{ hom :=
  { hom := ğŸ™ _,
    w' :=
    begin
      rintro âŸ¨_|_âŸ©,
      { ext, dsimp, simp only [category.id_comp, limits.fan.mk_Ï€_app, limits.limit.lift_Ï€], },
      { ext âŸ¨i,jâŸ©, dsimp, simp only [category.id_comp, limits.fan.mk_Ï€_app, limits.limit.lift_Ï€], },
    end },
  inv :=
  { hom := ğŸ™ _,
    w' :=
    begin
      rintro âŸ¨_|_âŸ©,
      { ext, dsimp, simp only [category.id_comp, limits.fan.mk_Ï€_app, limits.limit.lift_Ï€], },
      { ext âŸ¨i,jâŸ©, dsimp, simp only [category.id_comp, limits.fan.mk_Ï€_app, limits.limit.lift_Ï€], },
    end, },
  hom_inv_id' := by { ext, dsimp, simp only [category.comp_id], },
  inv_hom_id' := by { ext, dsimp, simp only [category.comp_id], }, })
(Î» c d f, by { ext, dsimp, simp only [category.comp_id, category.id_comp], })

/--
Cones over `diagram U â‹™ F` are the same as a cones over the usual sheaf condition equalizer diagram.
-/
@[simps]
def cone_equiv (F : presheaf C X) â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens X) :
  limits.cone ((diagram U).op â‹™ F) â‰Œ limits.cone (sheaf_condition_equalizer_products.diagram F U) :=
{ functor := cone_equiv_functor F U,
  inverse := cone_equiv_inverse F U,
  unit_iso := cone_equiv_unit_iso F U,
  counit_iso := cone_equiv_counit_iso F U, }

local attribute [reducible]
  sheaf_condition_equalizer_products.res
  sheaf_condition_equalizer_products.left_res

/--
If `sheaf_condition_equalizer_products.fork` is an equalizer,
then `F.map_cone (cone U)` is a limit cone.
-/
def is_limit_map_cone_of_is_limit_sheaf_condition_fork
  (F : presheaf C X) â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens X)
  (P : is_limit (sheaf_condition_equalizer_products.fork F U)) :
  is_limit (F.map_cone (cocone U).op) :=
is_limit.of_iso_limit ((is_limit.of_cone_equiv (cone_equiv F U).symm).symm P)
{ hom :=
  { hom := ğŸ™ _,
    w' :=
    begin
      intro x,
      induction x using opposite.rec,
      rcases x with âŸ¨âŸ©,
      { dsimp, simp, refl, },
      { dsimp,
        simp only [limit.lift_Ï€, limit.lift_Ï€_assoc, category.id_comp, fan.mk_Ï€_app,
          category.assoc],
        rw â†F.map_comp,
        refl, }
    end },
  inv :=
  { hom := ğŸ™ _,
    w' :=
    begin
      intro x,
      induction x using opposite.rec,
      rcases x with âŸ¨âŸ©,
      { dsimp, simp, refl, },
      { dsimp,
        simp only [limit.lift_Ï€, limit.lift_Ï€_assoc, category.id_comp, fan.mk_Ï€_app,
          category.assoc],
        rw â†F.map_comp,
        refl, }
    end },
  hom_inv_id' := by { ext, dsimp, simp only [category.comp_id], },
  inv_hom_id' := by { ext, dsimp, simp only [category.comp_id], }, }

/--
If `F.map_cone (cone U)` is a limit cone,
then `sheaf_condition_equalizer_products.fork` is an equalizer.
-/
def is_limit_sheaf_condition_fork_of_is_limit_map_cone
  (F : presheaf C X) â¦ƒÎ¹ : Type vâ¦„ (U : Î¹ â†’ opens X)
  (Q : is_limit (F.map_cone (cocone U).op)) :
  is_limit (sheaf_condition_equalizer_products.fork F U) :=
is_limit.of_iso_limit ((is_limit.of_cone_equiv (cone_equiv F U)).symm Q)
{ hom :=
  { hom := ğŸ™ _,
    w' :=
    begin
      rintro âŸ¨âŸ©,
      { dsimp, simp, refl, },
      { dsimp, ext âŸ¨i, jâŸ©,
        simp only [limit.lift_Ï€, limit.lift_Ï€_assoc, category.id_comp, fan.mk_Ï€_app,
          category.assoc],
        rw â†F.map_comp,
        refl, }
    end },
  inv :=
  { hom := ğŸ™ _,
    w' :=
    begin
      rintro âŸ¨âŸ©,
      { dsimp, simp, refl, },
      { dsimp, ext âŸ¨i, jâŸ©,
        simp only [limit.lift_Ï€, limit.lift_Ï€_assoc, category.id_comp, fan.mk_Ï€_app,
          category.assoc],
        rw â†F.map_comp,
        refl, }
    end },
  hom_inv_id' := by { ext, dsimp, simp only [category.comp_id], },
  inv_hom_id' := by { ext, dsimp, simp only [category.comp_id], }, }


end sheaf_condition_pairwise_intersections

open sheaf_condition_pairwise_intersections

/--
The sheaf condition in terms of an equalizer diagram is equivalent
to the reformulation in terms of a limit diagram over `U i` and `U i âŠ“ U j`.
-/
lemma is_sheaf_iff_is_sheaf_pairwise_intersections (F : presheaf C X) :
  F.is_sheaf â†” F.is_sheaf_pairwise_intersections :=
iff.intro (Î» h Î¹ U, âŸ¨is_limit_map_cone_of_is_limit_sheaf_condition_fork F U (h U).someâŸ©)
  (Î» h Î¹ U, âŸ¨is_limit_sheaf_condition_fork_of_is_limit_map_cone F U (h U).someâŸ©)

/--
The sheaf condition in terms of an equalizer diagram is equivalent
to the reformulation in terms of the presheaf preserving the limit of the diagram
consisting of the `U i` and `U i âŠ“ U j`.
-/
lemma is_sheaf_iff_is_sheaf_preserves_limit_pairwise_intersections (F : presheaf C X) :
  F.is_sheaf â†” F.is_sheaf_preserves_limit_pairwise_intersections :=
begin
  rw is_sheaf_iff_is_sheaf_pairwise_intersections,
  split,
  { intros h Î¹ U,
    exact âŸ¨preserves_limit_of_preserves_limit_cone (pairwise.cocone_is_colimit U).op (h U).someâŸ© },
  { intros h Î¹ U,
    haveI := (h U).some,
    exact âŸ¨preserves_limit.preserves (pairwise.cocone_is_colimit U).opâŸ© }
end

end Top.presheaf

namespace Top.sheaf

variables {X : Top.{v}} {C : Type u} [category.{v} C] [has_products C]
variables (F : X.sheaf C) (U V : opens X)
open category_theory.limits

/-- For a sheaf `F`, `F(U âˆª V)` is the pullback of `F(U) âŸ¶ F(U âˆ© V)` and `F(V) âŸ¶ F(U âˆ© V)`.
This is the pullback cone. -/
def inter_union_pullback_cone : pullback_cone
  (F.1.map (hom_of_le inf_le_left : U âˆ© V âŸ¶ _).op) (F.1.map (hom_of_le inf_le_right).op) :=
pullback_cone.mk (F.1.map (hom_of_le le_sup_left).op) (F.1.map (hom_of_le le_sup_right).op)
  (by { rw [â† F.1.map_comp, â† F.1.map_comp], congr })

@[simp] lemma inter_union_pullback_cone_X :
  (inter_union_pullback_cone F U V).X = F.1.obj (op $ U âˆª V) := rfl
@[simp] lemma inter_union_pullback_cone_fst :
  (inter_union_pullback_cone F U V).fst = F.1.map (hom_of_le le_sup_left).op := rfl
@[simp] lemma inter_union_pullback_cone_snd :
  (inter_union_pullback_cone F U V).snd = F.1.map (hom_of_le le_sup_right).op := rfl

variable (s : pullback_cone
  (F.1.map (hom_of_le inf_le_left : U âˆ© V âŸ¶ _).op) (F.1.map (hom_of_le inf_le_right).op))

/-- (Implementation).
Every cone over `F(U) âŸ¶ F(U âˆ© V)` and `F(V) âŸ¶ F(U âˆ© V)` factors through `F(U âˆª V)`. -/
def inter_union_pullback_cone_lift : s.X âŸ¶ F.1.obj (op (U âˆª V)) :=
begin
  let Î¹ : walking_pair â†’ opens X := Î» j, walking_pair.cases_on j U V,
  have hÎ¹ : U âˆª V = supr Î¹,
  { ext, split,
    { rintros (h|h),
    exacts [âŸ¨_,âŸ¨_,âŸ¨walking_pair.left,rflâŸ©,rflâŸ©,hâŸ©, âŸ¨_,âŸ¨_,âŸ¨walking_pair.right,rflâŸ©,rflâŸ©,hâŸ©] },
    { rintros âŸ¨_,âŸ¨_,âŸ¨âŸ¨âŸ©,âŸ¨âŸ©âŸ©,âŸ¨âŸ©âŸ©,zâŸ©, exacts [or.inl z, or.inr z] } },
  refine (F.1.is_sheaf_iff_is_sheaf_pairwise_intersections.mp F.2 Î¹).some.lift
    âŸ¨s.X, { app := _, naturality' := _ }âŸ© â‰« F.1.map (eq_to_hom hÎ¹).op,
  { apply opposite.rec,
    rintro ((_|_)|(_|_)),
    exacts [s.fst, s.snd, s.fst â‰« F.1.map (hom_of_le inf_le_left).op,
      s.snd â‰« F.1.map (hom_of_le inf_le_left).op] },
  rintros i j f,
  induction i using opposite.rec,
  induction j using opposite.rec,
  let g : j âŸ¶ i := f.unop, have : f = g.op := rfl, clear_value g, subst this,
  rcases i with ((_|_)|(_|_)); rcases j with ((_|_)|(_|_)); rcases g; dsimp;
    simp only [category.id_comp, s.condition, category_theory.functor.map_id, category.comp_id],
  { rw [â† cancel_mono (F.1.map (eq_to_hom $ inf_comm : U âˆ© V âŸ¶ _).op), category.assoc,
      category.assoc],
    erw [â† F.1.map_comp, â† F.1.map_comp],
    convert s.condition.symm },
  { convert s.condition }
end

lemma inter_union_pullback_cone_lift_left :
  inter_union_pullback_cone_lift F U V s â‰« F.1.map (hom_of_le le_sup_left).op = s.fst :=
begin
  erw [category.assoc, â†F.1.map_comp],
  exact (F.1.is_sheaf_iff_is_sheaf_pairwise_intersections.mp F.2 _).some.fac _
    (op $ pairwise.single walking_pair.left)
end

lemma inter_union_pullback_cone_lift_right :
  inter_union_pullback_cone_lift F U V s â‰« F.1.map (hom_of_le le_sup_right).op = s.snd :=
begin
  erw [category.assoc, â†F.1.map_comp],
  exact (F.1.is_sheaf_iff_is_sheaf_pairwise_intersections.mp F.2 _).some.fac _
    (op $ pairwise.single walking_pair.right)
end

/-- For a sheaf `F`, `F(U âˆª V)` is the pullback of `F(U) âŸ¶ F(U âˆ© V)` and `F(V) âŸ¶ F(U âˆ© V)`. -/
def is_limit_pullback_cone : is_limit (inter_union_pullback_cone F U V) :=
begin
  let Î¹ : walking_pair â†’ opens X := Î» j, walking_pair.cases_on j U V,
  have hÎ¹ : U âˆª V = supr Î¹,
  { ext, split,
    { rintros (h|h),
    exacts [âŸ¨_,âŸ¨_,âŸ¨walking_pair.left,rflâŸ©,rflâŸ©,hâŸ©, âŸ¨_,âŸ¨_,âŸ¨walking_pair.right,rflâŸ©,rflâŸ©,hâŸ©] },
    { rintros âŸ¨_,âŸ¨_,âŸ¨âŸ¨âŸ©,âŸ¨âŸ©âŸ©,âŸ¨âŸ©âŸ©,zâŸ©, exacts [or.inl z, or.inr z] } },
  apply pullback_cone.is_limit_aux',
  intro s,
  use inter_union_pullback_cone_lift F U V s,
  refine âŸ¨_,_,_âŸ©,
  { apply inter_union_pullback_cone_lift_left },
  { apply inter_union_pullback_cone_lift_right },
  { intros m hâ‚ hâ‚‚,
    rw â† cancel_mono (F.1.map (eq_to_hom hÎ¹.symm).op),
    apply (F.1.is_sheaf_iff_is_sheaf_pairwise_intersections.mp F.2 Î¹).some.hom_ext,
    apply opposite.rec,
    rintro ((_|_)|(_|_)); rw [category.assoc, category.assoc],
    { erw â† F.1.map_comp,
      convert hâ‚,
      apply inter_union_pullback_cone_lift_left },
    { erw â† F.1.map_comp,
      convert hâ‚‚,
      apply inter_union_pullback_cone_lift_right },
    all_goals
    { dsimp only [functor.op, pairwise.cocone_Î¹_app, functor.map_cone_Ï€_app,
        cocone.op, pairwise.cocone_Î¹_app_2, unop_op, op_comp, nat_trans.op],
      simp_rw [F.1.map_comp, â† category.assoc],
      congr' 1,
      simp_rw [category.assoc, â† F.1.map_comp] },
    { convert hâ‚,
      apply inter_union_pullback_cone_lift_left },
    { convert hâ‚‚,
      apply inter_union_pullback_cone_lift_right } }
end

/-- If `U, V` are disjoint, then `F(U âˆª V) = F(U) Ã— F(V)`. -/
def is_product_of_disjoint (h : U âˆ© V = âŠ¥) : is_limit
    (binary_fan.mk (F.1.map (hom_of_le le_sup_left : _ âŸ¶ U âŠ” V).op)
      (F.1.map (hom_of_le le_sup_right : _ âŸ¶ U âŠ” V).op)) :=
is_product_of_is_terminal_is_pullback _ _ _ _
  (F.is_terminal_of_eq_empty h) (is_limit_pullback_cone F U V)

end Top.sheaf
