/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/

import topology.sheaves.sheaf_condition.sites
import category_theory.limits.preserves.basic
import category_theory.category.pairwise
import category_theory.limits.constructions.binary_products
import algebra.category.Ring.constructions

/-!
# Equivalent formulations of the sheaf condition

We give an equivalent formulation of the sheaf condition.

Given any indexed type `Œπ`, we define `overlap Œπ`,
a category with objects corresponding to
* individual open sets, `single i`, and
* intersections of pairs of open sets, `pair i j`,
with morphisms from `pair i j` to both `single i` and `single j`.

Any open cover `U : Œπ ‚Üí opens X` provides a functor `diagram U : overlap Œπ ‚•§ (opens X)·µí·µñ`.

There is a canonical cone over this functor, `cone U`, whose cone point is `supr U`,
and in fact this is a limit cone.

A presheaf `F : presheaf C X` is a sheaf precisely if it preserves this limit.
We express this in two equivalent ways, as
* `is_limit (F.map_cone (cone U))`, or
* `preserves_limit (diagram U) F`
-/

noncomputable theory

universes v u

open topological_space
open Top
open opposite
open category_theory
open category_theory.limits

namespace Top.presheaf

variables {X : Top.{v}}

variables {C : Type u} [category.{v} C]

/--
An alternative formulation of the sheaf condition
(which we prove equivalent to the usual one below as
`is_sheaf_iff_is_sheaf_pairwise_intersections`).

A presheaf is a sheaf if `F` sends the cone `(pairwise.cocone U).op` to a limit cone.
(Recall `pairwise.cocone U` has cone point `supr U`, mapping down to the `U i` and the `U i ‚äì U j`.)
-/
def is_sheaf_pairwise_intersections (F : presheaf C X) : Prop :=
‚àÄ ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens X), nonempty (is_limit (F.map_cone (pairwise.cocone U).op))

/--
An alternative formulation of the sheaf condition
(which we prove equivalent to the usual one below as
`is_sheaf_iff_is_sheaf_preserves_limit_pairwise_intersections`).

A presheaf is a sheaf if `F` preserves the limit of `pairwise.diagram U`.
(Recall `pairwise.diagram U` is the diagram consisting of the pairwise intersections
`U i ‚äì U j` mapping into the open sets `U i`. This diagram has limit `supr U`.)
-/
def is_sheaf_preserves_limit_pairwise_intersections (F : presheaf C X) : Prop :=
‚àÄ ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens X), nonempty (preserves_limit (pairwise.diagram U).op F)

/-!
The remainder of this file shows that these conditions are equivalent
to the usual sheaf condition.
-/

variables [has_products.{v} C]

namespace sheaf_condition_pairwise_intersections

open category_theory.pairwise category_theory.pairwise.hom
open sheaf_condition_equalizer_products

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_functor_obj (F : presheaf C X)
  ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens ‚Ü•X) (c : limits.cone ((diagram U).op ‚ãô F)) :
  limits.cone (sheaf_condition_equalizer_products.diagram F U) :=
{ X := c.X,
  œÄ :=
  { app := Œª Z,
      walking_parallel_pair.cases_on Z
        (pi.lift (Œª (i : Œπ), c.œÄ.app (op (single i))))
        (pi.lift (Œª (b : Œπ √ó Œπ), c.œÄ.app (op (pair b.1 b.2)))),
    naturality' := Œª Y Z f,
    begin
      cases Y; cases Z; cases f,
      { ext i, dsimp,
        simp only [limit.lift_œÄ, category.id_comp, fan.mk_œÄ_app, category_theory.functor.map_id,
          category.assoc],
        dsimp,
        simp only [limit.lift_œÄ, category.id_comp, fan.mk_œÄ_app], },
      { ext ‚ü®i, j‚ü©, dsimp [sheaf_condition_equalizer_products.left_res],
        simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, category.id_comp, fan.mk_œÄ_app,
          category.assoc],
        have h := c.œÄ.naturality (quiver.hom.op (hom.left i j)),
        dsimp at h,
        simpa using h, },
      { ext ‚ü®i, j‚ü©, dsimp [sheaf_condition_equalizer_products.right_res],
        simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, category.id_comp, fan.mk_œÄ_app,
          category.assoc],
        have h := c.œÄ.naturality (quiver.hom.op (hom.right i j)),
        dsimp at h,
        simpa using h, },
      { ext i, dsimp,
        simp only [limit.lift_œÄ, category.id_comp, fan.mk_œÄ_app, category_theory.functor.map_id,
          category.assoc],
        dsimp,
        simp only [limit.lift_œÄ, category.id_comp, fan.mk_œÄ_app], },
    end, }, }

section
local attribute [tidy] tactic.case_bash

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_functor (F : presheaf C X)
  ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens ‚Ü•X) :
  limits.cone ((diagram U).op ‚ãô F) ‚•§
    limits.cone (sheaf_condition_equalizer_products.diagram F U) :=
{ obj := Œª c, cone_equiv_functor_obj F U c,
  map := Œª c c' f,
  { hom := f.hom,
    w' := Œª j, begin
      cases j;
      { ext, simp only [limits.fan.mk_œÄ_app, limits.cone_morphism.w,
        limits.limit.lift_œÄ, category.assoc, cone_equiv_functor_obj_œÄ_app], },
    end }, }.

end

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_inverse_obj (F : presheaf C X)
  ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens ‚Ü•X)
  (c : limits.cone (sheaf_condition_equalizer_products.diagram F U)) :
  limits.cone ((diagram U).op ‚ãô F) :=
{ X := c.X,
  œÄ :=
  { app :=
    begin
      intro x,
      induction x using opposite.rec,
      rcases x with (‚ü®i‚ü©|‚ü®i,j‚ü©),
      { exact c.œÄ.app (walking_parallel_pair.zero) ‚â´ pi.œÄ _ i, },
      { exact c.œÄ.app (walking_parallel_pair.one) ‚â´ pi.œÄ _ (i, j), }
    end,
    naturality' :=
    begin
      intros x y f,
      induction x using opposite.rec,
      induction y using opposite.rec,
      have ef : f = f.unop.op := rfl,
      revert ef,
      generalize : f.unop = f',
      rintro rfl,
      rcases x with ‚ü®i‚ü©|‚ü®‚ü©; rcases y with ‚ü®‚ü©|‚ü®j,j‚ü©; rcases f' with ‚ü®‚ü©,
      { dsimp, erw [F.map_id], simp, },
      { dsimp, simp only [category.id_comp, category.assoc],
        have h := c.œÄ.naturality (walking_parallel_pair_hom.left),
        dsimp [sheaf_condition_equalizer_products.left_res] at h,
        simp only [category.id_comp] at h,
        have h' := h =‚â´ pi.œÄ _ (i, j),
        rw h',
        simp only [category.assoc, limit.lift_œÄ, fan.mk_œÄ_app],
        refl, },
      { dsimp, simp only [category.id_comp, category.assoc],
        have h := c.œÄ.naturality (walking_parallel_pair_hom.right),
        dsimp [sheaf_condition_equalizer_products.right_res] at h,
        simp only [category.id_comp] at h,
        have h' := h =‚â´ pi.œÄ _ (j, i),
        rw h',
        simp,
        refl, },
      { dsimp, erw [F.map_id], simp, },
    end, }, }

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_inverse (F : presheaf C X)
  ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens ‚Ü•X) :
  limits.cone (sheaf_condition_equalizer_products.diagram F U) ‚•§
    limits.cone ((diagram U).op ‚ãô F) :=
{ obj := Œª c, cone_equiv_inverse_obj F U c,
  map := Œª c c' f,
  { hom := f.hom,
    w' :=
    begin
      intro x,
      induction x using opposite.rec,
      rcases x with (‚ü®i‚ü©|‚ü®i,j‚ü©),
      { dsimp,
        dunfold fork.Œπ,
        rw [‚Üê(f.w walking_parallel_pair.zero), category.assoc], },
      { dsimp,
        rw [‚Üê(f.w walking_parallel_pair.one), category.assoc], },
    end }, }.

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_unit_iso_app (F : presheaf C X) ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens ‚Ü•X)
  (c : cone ((diagram U).op ‚ãô F)) :
  (ùü≠ (cone ((diagram U).op ‚ãô F))).obj c ‚âÖ
    (cone_equiv_functor F U ‚ãô cone_equiv_inverse F U).obj c :=
{ hom :=
  { hom := ùüô _,
    w' := Œª j, begin
      induction j using opposite.rec, rcases j;
      { dsimp, simp only [limits.fan.mk_œÄ_app, category.id_comp, limits.limit.lift_œÄ], }
    end, },
  inv :=
  { hom := ùüô _,
    w' := Œª j, begin
      induction j using opposite.rec, rcases j;
      { dsimp, simp only [limits.fan.mk_œÄ_app, category.id_comp, limits.limit.lift_œÄ], }
    end },
  hom_inv_id' := begin
    ext,
    simp only [category.comp_id, limits.cone.category_comp_hom, limits.cone.category_id_hom],
  end,
  inv_hom_id' := begin
    ext,
    simp only [category.comp_id, limits.cone.category_comp_hom, limits.cone.category_id_hom],
  end, }

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_unit_iso (F : presheaf C X) ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens X) :
  ùü≠ (limits.cone ((diagram U).op ‚ãô F)) ‚âÖ
    cone_equiv_functor F U ‚ãô cone_equiv_inverse F U :=
nat_iso.of_components (cone_equiv_unit_iso_app F U) (by tidy)

/-- Implementation of `sheaf_condition_pairwise_intersections.cone_equiv`. -/
@[simps]
def cone_equiv_counit_iso (F : presheaf C X) ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens X) :
  cone_equiv_inverse F U ‚ãô cone_equiv_functor F U ‚âÖ
    ùü≠ (limits.cone (sheaf_condition_equalizer_products.diagram F U)) :=
nat_iso.of_components (Œª c,
{ hom :=
  { hom := ùüô _,
    w' :=
    begin
      rintro ‚ü®_|_‚ü©,
      { ext ‚ü®j‚ü©, dsimp, simp only [category.id_comp, limits.fan.mk_œÄ_app, limits.limit.lift_œÄ], },
      { ext ‚ü®i,j‚ü©, dsimp, simp only [category.id_comp, limits.fan.mk_œÄ_app, limits.limit.lift_œÄ], },
    end },
  inv :=
  { hom := ùüô _,
    w' :=
    begin
      rintro ‚ü®_|_‚ü©,
      { ext ‚ü®j‚ü©, dsimp, simp only [category.id_comp, limits.fan.mk_œÄ_app, limits.limit.lift_œÄ], },
      { ext ‚ü®i,j‚ü©, dsimp, simp only [category.id_comp, limits.fan.mk_œÄ_app, limits.limit.lift_œÄ], },
    end, },
  hom_inv_id' := by { ext, dsimp, simp only [category.comp_id], },
  inv_hom_id' := by { ext, dsimp, simp only [category.comp_id], }, })
(Œª c d f, by { ext, dsimp, simp only [category.comp_id, category.id_comp], })

/--
Cones over `diagram U ‚ãô F` are the same as a cones over the usual sheaf condition equalizer diagram.
-/
@[simps]
def cone_equiv (F : presheaf C X) ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens X) :
  limits.cone ((diagram U).op ‚ãô F) ‚âå limits.cone (sheaf_condition_equalizer_products.diagram F U) :=
{ functor := cone_equiv_functor F U,
  inverse := cone_equiv_inverse F U,
  unit_iso := cone_equiv_unit_iso F U,
  counit_iso := cone_equiv_counit_iso F U, }

local attribute [reducible]
  sheaf_condition_equalizer_products.res
  sheaf_condition_equalizer_products.left_res

/--
If `sheaf_condition_equalizer_products.fork` is an equalizer,
then `F.map_cone (cone U)` is a limit cone.
-/
def is_limit_map_cone_of_is_limit_sheaf_condition_fork
  (F : presheaf C X) ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens X)
  (P : is_limit (sheaf_condition_equalizer_products.fork F U)) :
  is_limit (F.map_cone (cocone U).op) :=
is_limit.of_iso_limit ((is_limit.of_cone_equiv (cone_equiv F U).symm).symm P)
{ hom :=
  { hom := ùüô _,
    w' :=
    begin
      intro x,
      induction x using opposite.rec,
      rcases x with ‚ü®‚ü©,
      { dsimp, simp, refl, },
      { dsimp,
        simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, category.id_comp, fan.mk_œÄ_app,
          category.assoc],
        rw ‚ÜêF.map_comp,
        refl, }
    end },
  inv :=
  { hom := ùüô _,
    w' :=
    begin
      intro x,
      induction x using opposite.rec,
      rcases x with ‚ü®‚ü©,
      { dsimp, simp, refl, },
      { dsimp,
        simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, category.id_comp, fan.mk_œÄ_app,
          category.assoc],
        rw ‚ÜêF.map_comp,
        refl, }
    end },
  hom_inv_id' := by { ext, dsimp, simp only [category.comp_id], },
  inv_hom_id' := by { ext, dsimp, simp only [category.comp_id], }, }

/--
If `F.map_cone (cone U)` is a limit cone,
then `sheaf_condition_equalizer_products.fork` is an equalizer.
-/
def is_limit_sheaf_condition_fork_of_is_limit_map_cone
  (F : presheaf C X) ‚¶ÉŒπ : Type v‚¶Ñ (U : Œπ ‚Üí opens X)
  (Q : is_limit (F.map_cone (cocone U).op)) :
  is_limit (sheaf_condition_equalizer_products.fork F U) :=
is_limit.of_iso_limit ((is_limit.of_cone_equiv (cone_equiv F U)).symm Q)
{ hom :=
  { hom := ùüô _,
    w' :=
    begin
      rintro ‚ü®‚ü©,
      { dsimp, simp, refl, },
      { dsimp, ext ‚ü®i, j‚ü©,
        simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, category.id_comp, fan.mk_œÄ_app,
          category.assoc],
        rw ‚ÜêF.map_comp,
        refl, }
    end },
  inv :=
  { hom := ùüô _,
    w' :=
    begin
      rintro ‚ü®‚ü©,
      { dsimp, simp, refl, },
      { dsimp, ext ‚ü®i, j‚ü©,
        simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, category.id_comp, fan.mk_œÄ_app,
          category.assoc],
        rw ‚ÜêF.map_comp,
        refl, }
    end },
  hom_inv_id' := by { ext, dsimp, simp only [category.comp_id], },
  inv_hom_id' := by { ext, dsimp, simp only [category.comp_id], }, }


end sheaf_condition_pairwise_intersections

open sheaf_condition_pairwise_intersections

/--
The sheaf condition in terms of an equalizer diagram is equivalent
to the reformulation in terms of a limit diagram over `U i` and `U i ‚äì U j`.
-/
lemma is_sheaf_iff_is_sheaf_pairwise_intersections (F : presheaf C X) :
  F.is_sheaf ‚Üî F.is_sheaf_pairwise_intersections :=
(is_sheaf_iff_is_sheaf_equalizer_products F).trans $
iff.intro (Œª h Œπ U, ‚ü®is_limit_map_cone_of_is_limit_sheaf_condition_fork F U (h U).some‚ü©)
  (Œª h Œπ U, ‚ü®is_limit_sheaf_condition_fork_of_is_limit_map_cone F U (h U).some‚ü©)

/--
The sheaf condition in terms of an equalizer diagram is equivalent
to the reformulation in terms of the presheaf preserving the limit of the diagram
consisting of the `U i` and `U i ‚äì U j`.
-/
lemma is_sheaf_iff_is_sheaf_preserves_limit_pairwise_intersections (F : presheaf C X) :
  F.is_sheaf ‚Üî F.is_sheaf_preserves_limit_pairwise_intersections :=
begin
  rw is_sheaf_iff_is_sheaf_pairwise_intersections,
  split,
  { intros h Œπ U,
    exact ‚ü®preserves_limit_of_preserves_limit_cone (pairwise.cocone_is_colimit U).op (h U).some‚ü© },
  { intros h Œπ U,
    haveI := (h U).some,
    exact ‚ü®preserves_limit.preserves (pairwise.cocone_is_colimit U).op‚ü© }
end

end Top.presheaf

namespace Top.sheaf

variables {X : Top.{v}} {C : Type u} [category.{v} C]
variables (F : X.sheaf C) (U V : opens X)
open category_theory.limits

/-- For a sheaf `F`, `F(U ‚äî V)` is the pullback of `F(U) ‚ü∂ F(U ‚äì V)` and `F(V) ‚ü∂ F(U ‚äì V)`.
This is the pullback cone. -/
def inter_union_pullback_cone : pullback_cone
  (F.1.map (hom_of_le inf_le_left : U ‚äì V ‚ü∂ _).op) (F.1.map (hom_of_le inf_le_right).op) :=
pullback_cone.mk (F.1.map (hom_of_le le_sup_left).op) (F.1.map (hom_of_le le_sup_right).op)
  (by { rw [‚Üê F.1.map_comp, ‚Üê F.1.map_comp], congr })

@[simp] lemma inter_union_pullback_cone_X :
  (inter_union_pullback_cone F U V).X = F.1.obj (op $ U ‚äî V) := rfl
@[simp] lemma inter_union_pullback_cone_fst :
  (inter_union_pullback_cone F U V).fst = F.1.map (hom_of_le le_sup_left).op := rfl
@[simp] lemma inter_union_pullback_cone_snd :
  (inter_union_pullback_cone F U V).snd = F.1.map (hom_of_le le_sup_right).op := rfl

variable (s : pullback_cone
  (F.1.map (hom_of_le inf_le_left : U ‚äì V ‚ü∂ _).op) (F.1.map (hom_of_le inf_le_right).op))

variable [has_products.{v} C]

/-- (Implementation).
Every cone over `F(U) ‚ü∂ F(U ‚äì V)` and `F(V) ‚ü∂ F(U ‚äì V)` factors through `F(U ‚äî V)`.
TODO: generalize to `C` without products.
-/
def inter_union_pullback_cone_lift : s.X ‚ü∂ F.1.obj (op (U ‚äî V)) :=
begin
  let Œπ : ulift.{v} walking_pair ‚Üí opens X := Œª j, walking_pair.cases_on j.down U V,
  have hŒπ : U ‚äî V = supr Œπ,
  { ext,
    rw [opens.coe_supr, set.mem_Union],
    split,
    { rintros (h|h),
      exacts [‚ü®‚ü®walking_pair.left‚ü©, h‚ü©, ‚ü®‚ü®walking_pair.right‚ü©, h‚ü©] },
    { rintro ‚ü®‚ü®_ | _‚ü©, h‚ü©,
      exacts [or.inl h, or.inr h] } },
  refine (F.presheaf.is_sheaf_iff_is_sheaf_pairwise_intersections.mp F.2 Œπ).some.lift
    ‚ü®s.X, { app := _, naturality' := _ }‚ü© ‚â´ F.1.map (eq_to_hom hŒπ).op,
  { apply opposite.rec,
    rintro ((_|_)|(_|_)),
    exacts [s.fst, s.snd, s.fst ‚â´ F.1.map (hom_of_le inf_le_left).op,
      s.snd ‚â´ F.1.map (hom_of_le inf_le_left).op] },
  rintros i j f,
  induction i using opposite.rec,
  induction j using opposite.rec,
  let g : j ‚ü∂ i := f.unop, have : f = g.op := rfl, clear_value g, subst this,
  rcases i with (‚ü®‚ü®(_|_)‚ü©‚ü©|‚ü®‚ü®(_|_)‚ü©,‚ü®_‚ü©‚ü©); rcases j with (‚ü®‚ü®(_|_)‚ü©‚ü©|‚ü®‚ü®(_|_)‚ü©,‚ü®_‚ü©‚ü©); rcases g; dsimp;
    simp only [category.id_comp, s.condition, category_theory.functor.map_id, category.comp_id],
  { rw [‚Üê cancel_mono (F.1.map (eq_to_hom $ inf_comm : U ‚äì V ‚ü∂ _).op), category.assoc,
      category.assoc],
    erw [‚Üê F.1.map_comp, ‚Üê F.1.map_comp],
    convert s.condition.symm },
end

lemma inter_union_pullback_cone_lift_left :
  inter_union_pullback_cone_lift F U V s ‚â´ F.1.map (hom_of_le le_sup_left).op = s.fst :=
begin
  dsimp,
  erw [category.assoc, ‚ÜêF.1.map_comp],
  exact (F.presheaf.is_sheaf_iff_is_sheaf_pairwise_intersections.mp F.2 _).some.fac _
    (op $ pairwise.single (ulift.up walking_pair.left))
end

lemma inter_union_pullback_cone_lift_right :
  inter_union_pullback_cone_lift F U V s ‚â´ F.1.map (hom_of_le le_sup_right).op = s.snd :=
begin
  erw [category.assoc, ‚ÜêF.1.map_comp],
  exact (F.presheaf.is_sheaf_iff_is_sheaf_pairwise_intersections.mp F.2 _).some.fac _
    (op $ pairwise.single (ulift.up walking_pair.right))
end

/-- For a sheaf `F`, `F(U ‚äî V)` is the pullback of `F(U) ‚ü∂ F(U ‚äì V)` and `F(V) ‚ü∂ F(U ‚äì V)`. -/
def is_limit_pullback_cone : is_limit (inter_union_pullback_cone F U V) :=
begin
  let Œπ : ulift.{v} walking_pair ‚Üí opens X := Œª ‚ü®j‚ü©, walking_pair.cases_on j U V,
  have hŒπ : U ‚äî V = supr Œπ,
  { ext,
    rw [opens.coe_supr, set.mem_Union],
    split,
    { rintros (h|h),
      exacts [‚ü®‚ü®walking_pair.left‚ü©, h‚ü©, ‚ü®‚ü®walking_pair.right‚ü©, h‚ü©] },
    { rintro ‚ü®‚ü®_ | _‚ü©, h‚ü©,
      exacts [or.inl h, or.inr h] } },
  apply pullback_cone.is_limit_aux',
  intro s,
  use inter_union_pullback_cone_lift F U V s,
  refine ‚ü®_,_,_‚ü©,
  { apply inter_union_pullback_cone_lift_left },
  { apply inter_union_pullback_cone_lift_right },
  { intros m h‚ÇÅ h‚ÇÇ,
    rw ‚Üê cancel_mono (F.1.map (eq_to_hom hŒπ.symm).op),
    apply (F.presheaf.is_sheaf_iff_is_sheaf_pairwise_intersections.mp F.2 Œπ).some.hom_ext,
    apply opposite.rec,
    rintro ((_|_)|(_|_)); rw [category.assoc, category.assoc],
    { erw ‚Üê F.1.map_comp,
      convert h‚ÇÅ,
      apply inter_union_pullback_cone_lift_left },
    { erw ‚Üê F.1.map_comp,
      convert h‚ÇÇ,
      apply inter_union_pullback_cone_lift_right },
    all_goals
    { dsimp only [functor.op, pairwise.cocone_Œπ_app, functor.map_cone_œÄ_app,
        cocone.op, pairwise.cocone_Œπ_app_2, unop_op, op_comp, nat_trans.op],
      simp_rw [F.1.map_comp, ‚Üê category.assoc],
      congr' 1,
      simp_rw [category.assoc, ‚Üê F.1.map_comp] },
    { convert h‚ÇÅ,
      apply inter_union_pullback_cone_lift_left },
    { convert h‚ÇÇ,
      apply inter_union_pullback_cone_lift_right } }
end

/-- If `U, V` are disjoint, then `F(U ‚äî V) = F(U) √ó F(V)`. -/
def is_product_of_disjoint (h : U ‚äì V = ‚ä•) : is_limit
    (binary_fan.mk (F.1.map (hom_of_le le_sup_left : _ ‚ü∂ U ‚äî V).op)
      (F.1.map (hom_of_le le_sup_right : _ ‚ü∂ U ‚äî V).op)) :=
is_product_of_is_terminal_is_pullback _ _ _ _
  (F.is_terminal_of_eq_empty h) (is_limit_pullback_cone F U V)

def obj_sup_iso_prod_eq_locus {X : Top} (F : X.sheaf CommRing)
  (U V : opens X) :
  F.1.obj (op $ U ‚äî V) ‚âÖ CommRing.of (ring_hom.eq_locus _ _) :=
(F.is_limit_pullback_cone U V).cone_point_unique_up_to_iso (CommRing.pullback_cone_is_limit _ _)

lemma obj_sup_iso_prod_eq_locus_hom_fst {X : Top} (F : X.sheaf CommRing)
  (U V : opens X) (x) :
  ((F.obj_sup_iso_prod_eq_locus U V).hom x).1.fst = F.1.map (hom_of_le le_sup_left).op x :=
concrete_category.congr_hom ((F.is_limit_pullback_cone U V).cone_point_unique_up_to_iso_hom_comp
  (CommRing.pullback_cone_is_limit _ _) walking_cospan.left) x

lemma obj_sup_iso_prod_eq_locus_hom_snd {X : Top} (F : X.sheaf CommRing)
  (U V : opens X) (x) :
  ((F.obj_sup_iso_prod_eq_locus U V).hom x).1.snd = F.1.map (hom_of_le le_sup_right).op x :=
concrete_category.congr_hom ((F.is_limit_pullback_cone U V).cone_point_unique_up_to_iso_hom_comp
  (CommRing.pullback_cone_is_limit _ _) walking_cospan.right) x

lemma obj_sup_iso_prod_eq_locus_inv_fst {X : Top} (F : X.sheaf CommRing)
  (U V : opens X) (x) :
  F.1.map (hom_of_le le_sup_left).op ((F.obj_sup_iso_prod_eq_locus U V).inv x) = x.1.1 :=
concrete_category.congr_hom ((F.is_limit_pullback_cone U V).cone_point_unique_up_to_iso_inv_comp
  (CommRing.pullback_cone_is_limit _ _) walking_cospan.left) x

lemma obj_sup_iso_prod_eq_locus_inv_snd {X : Top} (F : X.sheaf CommRing)
  (U V : opens X) (x) :
  F.1.map (hom_of_le le_sup_right).op ((F.obj_sup_iso_prod_eq_locus U V).inv x) = x.1.2 :=
concrete_category.congr_hom ((F.is_limit_pullback_cone U V).cone_point_unique_up_to_iso_inv_comp
  (CommRing.pullback_cone_is_limit _ _) walking_cospan.right) x

end Top.sheaf
