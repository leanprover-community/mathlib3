/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import algebraic_geometry.sheafed_space
import topology.sheaves.sheaf_condition.opens_le_cover
import topology.sheaves.stalks
import category_theory.preadditive.injective

/-!
# Skyscraper (pre)sheaves

A skyscraper (pre)sheaf is a (pre)sheaf supported at a single point: if `p‚ÇÄ ‚àà X` is a specified
point, then the skyscraper (pre)sheaf `ùìï` with value `A` is defined by `U ‚Ü¶ A` if `p‚ÇÄ ‚àà U` and
`U ‚Ü¶ *` if `p‚ÇÄ ‚àâ A` where `*` is some terminal object.

## Main definitions

* `skyscraper_presheaf`: `skyscraper_presheaf p‚ÇÄ A` is the skyscraper presheaf at point `p‚ÇÄ` with
  value `A`.
* `skyscraper_sheaf`: the skyscraper presheaf satisfies the sheaf condition.

## Main statements

* `skyscraper_presheaf_stalk_of_specializes`: if `y ‚àà closure {p‚ÇÄ}` then the stalk of
  `skyscraper_presheaf p‚ÇÄ S` at `y` is `S`.
* `skyscraper_presheaf_stalk_of_not_specializes`: if `y ‚àâ closure {p‚ÇÄ}` then the stalk of
  `skyscraper_presheaf p‚ÇÄ S` at `y` is `*` the terminal object.

-/

noncomputable theory

section

open topological_space
open category_theory category_theory.limits
open Top
open opposite

universes u v w

variables {X : Top.{u}} (p‚ÇÄ : X) {C : Type v} [category.{w} C] (S : C)
variables [has_terminal C] [Œ† (U : opens X), decidable (p‚ÇÄ ‚àà U)]

/--
A skyscraper presheaf is a presheaf supported at a single point: if `p‚ÇÄ ‚àà X` is a specified
point, then the skyscraper presheaf `ùìï` with value `A` is defined by `U ‚Ü¶ A` if `p‚ÇÄ ‚àà U` and
`U ‚Ü¶ *` if `p‚ÇÄ ‚àâ A` where `*` is some terminal object.
-/
@[simps] def skyscraper_presheaf : presheaf C X :=
{ obj := Œª U, if p‚ÇÄ ‚àà unop U then S else terminal C,
  map := Œª U V i, if h : p‚ÇÄ ‚àà unop V
    then eq_to_hom $ by erw [if_pos h, if_pos (le_of_hom i.unop h)]
    else ((if_neg h).symm.rec terminal_is_terminal).from _,
  map_id' := Œª U,
  begin
    split_ifs,
    { apply eq_to_hom_refl },
    { exact ((if_neg h).symm.rec terminal_is_terminal).hom_ext _ _ },
  end,
  map_comp' := Œª U V W iVU iWV,
  begin
    by_cases hW : p‚ÇÄ ‚àà unop W,
    { have hV : p‚ÇÄ ‚àà unop V := le_of_hom iWV.unop hW,
      simp only [dif_pos hW, dif_pos hV, eq_to_hom_trans] },
    { rw [dif_neg hW], apply ((if_neg hW).symm.rec terminal_is_terminal).hom_ext }
  end }

section

variables {p‚ÇÄ}

lemma skyscraper_presheaf_obj_of_mem {U : opens X} (h : p‚ÇÄ ‚àà U) :
  (skyscraper_presheaf p‚ÇÄ S).obj (op U) = S := if_pos h

lemma skyscraper_presheaf_obj_of_not_mem {U : opens X} (h : p‚ÇÄ ‚àâ U) :
  (skyscraper_presheaf p‚ÇÄ S).obj (op U) = terminal C := if_neg h

end

/--
A skyscraper sheaf is a sheaf supported at a single point: if `p‚ÇÄ ‚àà X` is a specified
point, then the skyscraper sheaf `ùìï` with value `A` is defined by `U ‚Ü¶ A` if `p‚ÇÄ ‚àà U` and
`U ‚Ü¶ *` if `p‚ÇÄ ‚àâ A` where `*` is some terminal object.
-/
def skyscraper_sheaf : sheaf C X :=
‚ü®_, (skyscraper_presheaf p‚ÇÄ S).is_sheaf_iff_is_sheaf_opens_le_cover.mpr $ Œª Œπ U, nonempty.intro
 { lift := Œª c, if h : p‚ÇÄ ‚àà (presheaf.sheaf_condition.opens_le_cover_cocone U).X
    then c.œÄ.app (op ‚ü®_, ‚ü®(opens.mem_supr.mp h).some, le_refl _‚ü©‚ü©) ‚â´ eq_to_hom
      begin
       dsimp, rw [if_pos h, if_pos (opens.mem_supr.mp h).some_spec],
      end
    else ((if_neg h).symm.rec terminal_is_terminal).from _,
   fac' := Œª c j,
   begin
    dsimp, split_ifs with h0, swap,
    { exact ((if_neg h0).symm.rec terminal_is_terminal).hom_ext _ _, },
    by_cases h1 : p‚ÇÄ ‚àà (presheaf.sheaf_condition.opens_le_cover_cocone U).X;
    split_ifs, swap,
    { rw [eq_comp_eq_to_hom], exact ((if_neg h1).symm.rec terminal_is_terminal).hom_ext _ _, },
    rw [category.assoc, eq_to_hom_trans],
    transitivity c.œÄ.app (op ‚ü®U (opens.mem_supr.mp h1).some ‚äì j.unop.obj, ‚ü®_, inf_le_left‚ü©‚ü©) ‚â´
      eq_to_hom _,
    rotate, { dsimp, rw [if_pos h0, if_pos], exact ‚ü®(opens.mem_supr.mp h1).some_spec, h0‚ü© },
    { have := @nat_trans.naturality _ _ _ _ _ _ c.œÄ j
        (op ‚ü®U (opens.mem_supr.mp h1).some ‚äì j.unop.obj, ‚ü®_, inf_le_left‚ü©‚ü©)
        (quiver.hom.op (hom_of_le _) : op j.unop ‚ü∂ _), swap, exact inf_le_right,
      dsimp at this, simp only [category.id_comp] at this,
      have h : p‚ÇÄ ‚àà U _ ‚äì j.unop.obj := ‚ü®(opens.mem_supr.mp h1).some_spec, h0‚ü©,
      split_ifs at this with h', swap, { exact false.elim (h' h), },
      rw [this, category.assoc, eq_to_hom_trans, eq_to_hom_refl, category.comp_id] },
    { have := @nat_trans.naturality _ _ _ _ _ _ c.œÄ
        (op ‚ü®_, ‚ü®(opens.mem_supr.mp h1).some, le_refl _‚ü©‚ü©)
        (op ‚ü®U (opens.mem_supr.mp h1).some ‚äì j.unop.obj, ‚ü®_, inf_le_left‚ü©‚ü©)
        (quiver.hom.op (hom_of_le inf_le_left)),
      dsimp at this, simp only [category.id_comp] at this,
      have h : p‚ÇÄ ‚àà U _ ‚äì j.unop.obj := ‚ü®(opens.mem_supr.mp h1).some_spec, h0‚ü©,
      split_ifs at this with h', swap, { exact false.elim (h' h), },
      rw [this, category.assoc, eq_to_hom_trans], },
   end,
   uniq' := Œª s f h0,
   begin
    dsimp at *, split_ifs with h1, swap,
    { exact ((if_neg h1).symm.rec terminal_is_terminal).hom_ext _ _ },
    specialize h0 (op ‚ü®_, ‚ü®(opens.mem_supr.mp h1).some, le_refl _‚ü©‚ü©),
    split_ifs at h0 with h2, swap, { exact false.elim (h2 (opens.mem_supr.mp h1).some_spec) },
    rw [‚Üêh0, category.assoc, eq_to_hom_trans, eq_to_hom_refl, category.comp_id],
   end }‚ü©

end

section

-- In this section, we calculate the stalks for skyscraper presheaves.
-- We need to restrict universe level.

open topological_space
open category_theory category_theory.limits
open Top
open opposite

universes u v

variables {X : Top.{u}} (p‚ÇÄ : X) {C : Type v} [category.{u} C] (S : C) [has_terminal C]

private lemma mem_nhds_of_not_specializes {y : X} (h : ¬¨p‚ÇÄ ‚§≥ y) :
  ‚àÉ (U : open_nhds y), p‚ÇÄ ‚àâ U.1 :=
by { contrapose! h, exact specializes_iff_forall_open.2 (Œª s o h‚ÇÅ, h ‚ü®‚ü®s, o‚ü©, h‚ÇÅ‚ü©) }

variable [Œ† (U : opens X), decidable (p‚ÇÄ ‚àà U)]
/--
The cocone at `S` for the salk functor of `skyscraper_presheaf p‚ÇÄ S` when `y ‚àà closure {p‚ÇÄ}`
-/
@[simps] def skyscraper_presheaf_cocone_of_specializes {y : X} (h : p‚ÇÄ ‚§≥ y) :
  cocone ((open_nhds.inclusion y).op ‚ãô skyscraper_presheaf p‚ÇÄ S) :=
{ X := S,
  Œπ := { app := Œª U, eq_to_hom $ if_pos $ h.mem_open U.unop.1.2 U.unop.2,
    naturality' := Œª U V inc, begin
      change dite _ _ _ ‚â´ _ = _, rw dif_pos,
      { erw [category.comp_id, eq_to_hom_trans], refl },
      { exact h.mem_open V.unop.1.2 V.unop.2 },
    end } }

/--
The cocone at `S` for the stalk functor of `skyscraper_presheaf p‚ÇÄ S` when `y ‚àà closure {p‚ÇÄ}` is a
colimit
-/
noncomputable def skyscraper_presheaf_cocone_is_colimit_of_specializes [has_colimits C]
  {y : X} (h : p‚ÇÄ ‚§≥ y) : is_colimit (skyscraper_presheaf_cocone_of_specializes p‚ÇÄ S h) :=
{ desc := Œª c, eq_to_hom (if_pos trivial).symm ‚â´ c.Œπ.app (op ‚ä§),
  fac' := Œª c U, begin
    rw ‚Üê c.w (hom_of_le $ (le_top : unop U ‚â§ _)).op,
    change _ ‚â´ _ ‚â´ dite _ _ _ ‚â´ _ = _,
    rw dif_pos,
    { simpa only [skyscraper_presheaf_cocone_of_specializes_Œπ_app,
        eq_to_hom_trans_assoc, eq_to_hom_refl, category.id_comp] },
    { exact h.mem_open U.unop.1.2 U.unop.2 },
  end,
  uniq' := Œª c f h, by rw [‚Üê h, skyscraper_presheaf_cocone_of_specializes_Œπ_app,
    eq_to_hom_trans_assoc, eq_to_hom_refl, category.id_comp] }

/--
If `y ‚àà closure {p‚ÇÄ}`, then the stalk of `skyscraper_presheaf p‚ÇÄ S` at `y` is `S`.
-/
@[reducible]
noncomputable def skyscraper_presheaf_stalk_of_specializes [has_colimits C]
  {y : X} (h : p‚ÇÄ ‚§≥ y) : (skyscraper_presheaf p‚ÇÄ S).stalk y ‚âÖ S :=
colimit.iso_colimit_cocone ‚ü®_, skyscraper_presheaf_cocone_is_colimit_of_specializes p‚ÇÄ S h‚ü©

/--
The cocone at `*` for the salk functor of `skyscraper_presheaf p‚ÇÄ S` when `y ‚àâ closure {p‚ÇÄ}`
-/
@[simps] def skyscraper_presheaf_cocone (y : X) :
  cocone ((open_nhds.inclusion y).op ‚ãô skyscraper_presheaf p‚ÇÄ S) :=
{ X := terminal C,
  Œπ :=
  { app := Œª U, terminal.from _,
    naturality' := Œª U V inc, terminal_is_terminal.hom_ext _ _ } }

/--
The cocone at `*` for the stalk functor of `skyscraper_presheaf p‚ÇÄ S` when `y ‚àâ closure {p‚ÇÄ}` is a
colimit
-/
noncomputable def skyscraper_presheaf_cocone_is_colimit_of_not_specializes [has_colimits C]
  {y : X} (h : ¬¨p‚ÇÄ ‚§≥ y) : is_colimit (skyscraper_presheaf_cocone p‚ÇÄ S y) :=
let h1 := mem_nhds_of_not_specializes p‚ÇÄ h in
{ desc := Œª c, eq_to_hom (if_neg h1.some_spec).symm ‚â´ c.Œπ.app (op h1.some),
  fac' := Œª c U, begin
    change _ = c.Œπ.app (op U.unop),
    simp only [‚Üê c.w (hom_of_le $ @inf_le_left _ _ h1.some U.unop).op,
      ‚Üê c.w (hom_of_le $ @inf_le_right _ _ h1.some U.unop).op, ‚Üê category.assoc],
    congr' 1,
    refine ((if_neg _).substr terminal_is_terminal).hom_ext _ _,
    exact Œª h, h1.some_spec h.1,
  end,
  uniq' := Œª c f H, begin
    rw [‚Üê category.id_comp f, ‚Üê H, ‚Üê category.assoc],
    congr' 1, apply terminal_is_terminal.hom_ext,
  end }

/--
If `y ‚àâ closure {p‚ÇÄ}`, then the stalk of `skyscraper_presheaf p‚ÇÄ S` at `y` is `*`
-/
@[reducible]
noncomputable def skyscraper_presheaf_stalk_of_not_specializes [has_colimits C]
  {y : X} (h : ¬¨p‚ÇÄ ‚§≥ y) : (skyscraper_presheaf p‚ÇÄ S).stalk y ‚âÖ terminal C :=
colimit.iso_colimit_cocone ‚ü®_, skyscraper_presheaf_cocone_is_colimit_of_not_specializes _ S h‚ü©

end
