/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Junyan Xu
-/
import topology.sheaves.punit
import topology.sheaves.stalks
import topology.sheaves.functors

/-!
# Skyscraper (pre)sheaves

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A skyscraper (pre)sheaf `ğ“• : (pre)sheaf C X` is the (pre)sheaf with value `A` at point `pâ‚€` that is
supported only at open sets contain `pâ‚€`, i.e. `ğ“•(U) = A` if `pâ‚€ âˆˆ U` and `ğ“•(U) = *` if `pâ‚€ âˆ‰ U`
where `*` is a terminal object of `C`. In terms of stalks, `ğ“•` is supported at all specializations
of `pâ‚€`, i.e. if `pâ‚€ â¤³ x` then `ğ“•â‚“ â‰… A` and if `Â¬ pâ‚€ â¤³ x` then `ğ“•â‚“ â‰… *`.

## Main definitions

* `skyscraper_presheaf`: `skyscraper_presheaf pâ‚€ A` is the skyscraper presheaf at point `pâ‚€` with
  value `A`.
* `skyscraper_sheaf`: the skyscraper presheaf satisfies the sheaf condition.

## Main statements

* `skyscraper_presheaf_stalk_of_specializes`: if `y âˆˆ closure {pâ‚€}` then the stalk of
  `skyscraper_presheaf pâ‚€ A` at `y` is `A`.
* `skyscraper_presheaf_stalk_of_not_specializes`: if `y âˆ‰ closure {pâ‚€}` then the stalk of
  `skyscraper_presheaf pâ‚€ A` at `y` is `*` the terminal object.

TODO: generalize universe level when calculating stalks, after generalizing universe level of stalk.
-/

noncomputable theory

open topological_space Top category_theory category_theory.limits opposite

universes u v w

variables {X : Top.{u}} (pâ‚€ : X) [Î  (U : opens X), decidable (pâ‚€ âˆˆ U)]

section

variables {C : Type v} [category.{w} C] [has_terminal C] (A : C)

/--
A skyscraper presheaf is a presheaf supported at a single point: if `pâ‚€ âˆˆ X` is a specified
point, then the skyscraper presheaf `ğ“•` with value `A` is defined by `U â†¦ A` if `pâ‚€ âˆˆ U` and
`U â†¦ *` if `pâ‚€ âˆ‰ A` where `*` is some terminal object.
-/
@[simps] def skyscraper_presheaf : presheaf C X :=
{ obj := Î» U, if pâ‚€ âˆˆ unop U then A else terminal C,
  map := Î» U V i, if h : pâ‚€ âˆˆ unop V
    then eq_to_hom $ by erw [if_pos h, if_pos (le_of_hom i.unop h)]
    else ((if_neg h).symm.rec terminal_is_terminal).from _,
  map_id' := Î» U, (em (pâ‚€ âˆˆ U.unop)).elim (Î» h, dif_pos h)
    (Î» h, ((if_neg h).symm.rec terminal_is_terminal).hom_ext _ _),
  map_comp' := Î» U V W iVU iWV,
  begin
    by_cases hW : pâ‚€ âˆˆ unop W,
    { have hV : pâ‚€ âˆˆ unop V := le_of_hom iWV.unop hW,
      simp only [dif_pos hW, dif_pos hV, eq_to_hom_trans] },
    { rw [dif_neg hW], apply ((if_neg hW).symm.rec terminal_is_terminal).hom_ext }
  end }

lemma skyscraper_presheaf_eq_pushforward
  [hd : Î  (U : opens (Top.of punit.{u+1})), decidable (punit.star âˆˆ U)] :
  skyscraper_presheaf pâ‚€ A =
  continuous_map.const (Top.of punit) pâ‚€ _* skyscraper_presheaf punit.star A :=
by convert_to @skyscraper_presheaf X pâ‚€
  (Î» U, hd $ (opens.map $ continuous_map.const _ pâ‚€).obj U) C _ _ A = _; congr <|> refl

/--
Taking skyscraper presheaf at a point is functorial: `c â†¦ skyscraper pâ‚€ c` defines a functor by
sending every `f : a âŸ¶ b` to the natural transformation `Î±` defined as: `Î±(U) = f : a âŸ¶ b` if
`pâ‚€ âˆˆ U` and the unique morphism to a terminal object in `C` if `pâ‚€ âˆ‰ U`.
-/
@[simps] def skyscraper_presheaf_functor.map' {a b : C} (f : a âŸ¶ b) :
  skyscraper_presheaf pâ‚€ a âŸ¶ skyscraper_presheaf pâ‚€ b :=
{ app := Î» U, if h : pâ‚€ âˆˆ U.unop
    then eq_to_hom (if_pos h) â‰« f â‰« eq_to_hom (if_pos h).symm
    else ((if_neg h).symm.rec terminal_is_terminal).from _,
  naturality' := Î» U V i,
  begin
    simp only [skyscraper_presheaf_map], by_cases hV : pâ‚€ âˆˆ V.unop,
    { have hU : pâ‚€ âˆˆ U.unop := le_of_hom i.unop hV, split_ifs,
      simpa only [eq_to_hom_trans_assoc, category.assoc, eq_to_hom_trans], },
    { apply ((if_neg hV).symm.rec terminal_is_terminal).hom_ext, },
  end }

lemma skyscraper_presheaf_functor.map'_id {a : C} :
  skyscraper_presheaf_functor.map' pâ‚€ (ğŸ™ a) = ğŸ™ _ :=
begin
  ext1, ext1, simp only [skyscraper_presheaf_functor.map'_app, nat_trans.id_app], split_ifs,
  { simp only [category.id_comp, category.comp_id, eq_to_hom_trans, eq_to_hom_refl], },
  { apply ((if_neg h).symm.rec terminal_is_terminal).hom_ext, },
end

lemma skyscraper_presheaf_functor.map'_comp {a b c : C} (f : a âŸ¶ b) (g : b âŸ¶ c) :
  skyscraper_presheaf_functor.map' pâ‚€ (f â‰« g) =
  skyscraper_presheaf_functor.map' pâ‚€ f â‰« skyscraper_presheaf_functor.map' pâ‚€ g :=
begin
  ext1, ext1, simp only [skyscraper_presheaf_functor.map'_app, nat_trans.comp_app], split_ifs,
  { simp only [category.assoc, eq_to_hom_trans_assoc, eq_to_hom_refl, category.id_comp], },
  { apply ((if_neg h).symm.rec terminal_is_terminal).hom_ext, },
end

/--
Taking skyscraper presheaf at a point is functorial: `c â†¦ skyscraper pâ‚€ c` defines a functor by
sending every `f : a âŸ¶ b` to the natural transformation `Î±` defined as: `Î±(U) = f : a âŸ¶ b` if
`pâ‚€ âˆˆ U` and the unique morphism to a terminal object in `C` if `pâ‚€ âˆ‰ U`.
-/
@[simps] def skyscraper_presheaf_functor : C â¥¤ presheaf C X :=
{ obj := skyscraper_presheaf pâ‚€,
  map := Î» _ _, skyscraper_presheaf_functor.map' pâ‚€,
  map_id' := Î» _, skyscraper_presheaf_functor.map'_id pâ‚€,
  map_comp' := Î» _ _ _, skyscraper_presheaf_functor.map'_comp pâ‚€ }

end

section

-- In this section, we calculate the stalks for skyscraper presheaves.
-- We need to restrict universe level.

variables {C : Type v} [category.{u} C] (A : C) [has_terminal C]

/--
The cocone at `A` for the stalk functor of `skyscraper_presheaf pâ‚€ A` when `y âˆˆ closure {pâ‚€}`
-/
@[simps] def skyscraper_presheaf_cocone_of_specializes {y : X} (h : pâ‚€ â¤³ y) :
  cocone ((open_nhds.inclusion y).op â‹™ skyscraper_presheaf pâ‚€ A) :=
{ X := A,
  Î¹ := { app := Î» U, eq_to_hom $ if_pos $ h.mem_open U.unop.1.2 U.unop.2,
    naturality' := Î» U V inc, begin
      change dite _ _ _ â‰« _ = _, rw dif_pos,
      { erw [category.comp_id, eq_to_hom_trans], refl },
      { exact h.mem_open V.unop.1.2 V.unop.2 },
    end } }

/--
The cocone at `A` for the stalk functor of `skyscraper_presheaf pâ‚€ A` when `y âˆˆ closure {pâ‚€}` is a
colimit
-/
noncomputable def skyscraper_presheaf_cocone_is_colimit_of_specializes
  {y : X} (h : pâ‚€ â¤³ y) : is_colimit (skyscraper_presheaf_cocone_of_specializes pâ‚€ A h) :=
{ desc := Î» c, eq_to_hom (if_pos trivial).symm â‰« c.Î¹.app (op âŠ¤),
  fac' := Î» c U, begin
    rw â† c.w (hom_of_le $ (le_top : unop U â‰¤ _)).op,
    change _ â‰« _ â‰« dite _ _ _ â‰« _ = _,
    rw dif_pos,
    { simpa only [skyscraper_presheaf_cocone_of_specializes_Î¹_app,
        eq_to_hom_trans_assoc, eq_to_hom_refl, category.id_comp] },
    { exact h.mem_open U.unop.1.2 U.unop.2 },
  end,
  uniq' := Î» c f h, by rw [â† h, skyscraper_presheaf_cocone_of_specializes_Î¹_app,
    eq_to_hom_trans_assoc, eq_to_hom_refl, category.id_comp] }

/--
If `y âˆˆ closure {pâ‚€}`, then the stalk of `skyscraper_presheaf pâ‚€ A` at `y` is `A`.
-/
noncomputable def skyscraper_presheaf_stalk_of_specializes [has_colimits C]
  {y : X} (h : pâ‚€ â¤³ y) : (skyscraper_presheaf pâ‚€ A).stalk y â‰… A :=
colimit.iso_colimit_cocone âŸ¨_, skyscraper_presheaf_cocone_is_colimit_of_specializes pâ‚€ A hâŸ©

/--
The cocone at `*` for the stalk functor of `skyscraper_presheaf pâ‚€ A` when `y âˆ‰ closure {pâ‚€}`
-/
@[simps] def skyscraper_presheaf_cocone (y : X) :
  cocone ((open_nhds.inclusion y).op â‹™ skyscraper_presheaf pâ‚€ A) :=
{ X := terminal C,
  Î¹ :=
  { app := Î» U, terminal.from _,
    naturality' := Î» U V inc, terminal_is_terminal.hom_ext _ _ } }

/--
The cocone at `*` for the stalk functor of `skyscraper_presheaf pâ‚€ A` when `y âˆ‰ closure {pâ‚€}` is a
colimit
-/
noncomputable def skyscraper_presheaf_cocone_is_colimit_of_not_specializes
  {y : X} (h : Â¬pâ‚€ â¤³ y) : is_colimit (skyscraper_presheaf_cocone pâ‚€ A y) :=
let h1 : âˆƒ (U : open_nhds y), pâ‚€ âˆ‰ U.1 :=
  let âŸ¨U, ho, hâ‚€, hyâŸ© := not_specializes_iff_exists_open.mp h in âŸ¨âŸ¨âŸ¨U, hoâŸ©, hâ‚€âŸ©, hyâŸ© in
{ desc := Î» c, eq_to_hom (if_neg h1.some_spec).symm â‰« c.Î¹.app (op h1.some),
  fac' := Î» c U, begin
    change _ = c.Î¹.app (op U.unop),
    simp only [â† c.w (hom_of_le $ @inf_le_left _ _ h1.some U.unop).op,
      â† c.w (hom_of_le $ @inf_le_right _ _ h1.some U.unop).op, â† category.assoc],
    congr' 1,
    refine ((if_neg _).symm.rec terminal_is_terminal).hom_ext _ _,
    exact Î» h, h1.some_spec h.1,
  end,
  uniq' := Î» c f H, begin
    rw [â† category.id_comp f, â† H, â† category.assoc],
    congr' 1, apply terminal_is_terminal.hom_ext,
  end }

/--
If `y âˆ‰ closure {pâ‚€}`, then the stalk of `skyscraper_presheaf pâ‚€ A` at `y` is isomorphic to a
terminal object.
-/
noncomputable def skyscraper_presheaf_stalk_of_not_specializes [has_colimits C]
  {y : X} (h : Â¬pâ‚€ â¤³ y) : (skyscraper_presheaf pâ‚€ A).stalk y â‰… terminal C :=
colimit.iso_colimit_cocone âŸ¨_, skyscraper_presheaf_cocone_is_colimit_of_not_specializes _ A hâŸ©

/--
If `y âˆ‰ closure {pâ‚€}`, then the stalk of `skyscraper_presheaf pâ‚€ A` at `y` is a terminal object
-/
def skyscraper_presheaf_stalk_of_not_specializes_is_terminal
  [has_colimits C] {y : X} (h : Â¬pâ‚€ â¤³ y) : is_terminal ((skyscraper_presheaf pâ‚€ A).stalk y) :=
is_terminal.of_iso terminal_is_terminal $ (skyscraper_presheaf_stalk_of_not_specializes _ _ h).symm

lemma skyscraper_presheaf_is_sheaf : (skyscraper_presheaf pâ‚€ A).is_sheaf :=
by classical; exact (presheaf.is_sheaf_iso_iff
  (eq_to_iso $ skyscraper_presheaf_eq_pushforward pâ‚€ A)).mpr
  (sheaf.pushforward_sheaf_of_sheaf _ (presheaf.is_sheaf_on_punit_of_is_terminal _
  (by { dsimp, rw if_neg, exact terminal_is_terminal, exact set.not_mem_empty punit.star })))

/--
The skyscraper presheaf supported at `pâ‚€` with value `A` is the sheaf that assigns `A` to all opens
`U` that contain `pâ‚€` and assigns `*` otherwise.
-/
def skyscraper_sheaf : sheaf C X :=
âŸ¨skyscraper_presheaf pâ‚€ A, skyscraper_presheaf_is_sheaf _ _âŸ©

/--
Taking skyscraper sheaf at a point is functorial: `c â†¦ skyscraper pâ‚€ c` defines a functor by
sending every `f : a âŸ¶ b` to the natural transformation `Î±` defined as: `Î±(U) = f : a âŸ¶ b` if
`pâ‚€ âˆˆ U` and the unique morphism to a terminal object in `C` if `pâ‚€ âˆ‰ U`.
-/
def skyscraper_sheaf_functor : C â¥¤ sheaf C X :=
{ obj := Î» c, skyscraper_sheaf pâ‚€ c,
  map := Î» a b f, Sheaf.hom.mk $ (skyscraper_presheaf_functor pâ‚€).map f,
  map_id' := Î» c, Sheaf.hom.ext _ _ $ (skyscraper_presheaf_functor pâ‚€).map_id _,
  map_comp' := Î» _ _ _ f g, Sheaf.hom.ext _ _ $ (skyscraper_presheaf_functor pâ‚€).map_comp _ _ }

namespace stalk_skyscraper_presheaf_adjunction_auxs

variables [has_colimits C]

/--
If `f : ğ“•.stalk pâ‚€ âŸ¶ c`, then a natural transformation `ğ“• âŸ¶ skyscraper_presheaf pâ‚€ c` can be
defined by: `ğ“•.germ pâ‚€ â‰« f : ğ“•(U) âŸ¶ c` if `pâ‚€ âˆˆ U` and the unique morphism to a terminal object
if `pâ‚€ âˆ‰ U`.
-/
@[simps] def to_skyscraper_presheaf {ğ“• : presheaf C X} {c : C} (f : ğ“•.stalk pâ‚€ âŸ¶ c) :
  ğ“• âŸ¶ skyscraper_presheaf pâ‚€ c :=
{ app := Î» U, if h : pâ‚€ âˆˆ U.unop
    then ğ“•.germ âŸ¨pâ‚€, hâŸ© â‰« f â‰« eq_to_hom (if_pos h).symm
    else ((if_neg h).symm.rec terminal_is_terminal).from _,
  naturality' := Î» U V inc,
  begin
    dsimp, by_cases hV : pâ‚€ âˆˆ V.unop,
    { have hU : pâ‚€ âˆˆ U.unop := le_of_hom inc.unop hV, split_ifs,
      erw [â†category.assoc, ğ“•.germ_res inc.unop, category.assoc, category.assoc, eq_to_hom_trans],
      refl, },
    { split_ifs, apply ((if_neg hV).symm.rec terminal_is_terminal).hom_ext },
  end }

/--
If `f : ğ“• âŸ¶ skyscraper_presheaf pâ‚€ c` is a natural transformation, then there is a morphism
`ğ“•.stalk pâ‚€ âŸ¶ c` defined as the morphism from colimit to cocone at `c`.
-/
def from_stalk {ğ“• : presheaf C X} {c : C} (f : ğ“• âŸ¶ skyscraper_presheaf pâ‚€ c) :
  ğ“•.stalk pâ‚€ âŸ¶ c :=
let Ï‡ : cocone ((open_nhds.inclusion pâ‚€).op â‹™ ğ“•) := cocone.mk c $
{ app := Î» U, f.app (op U.unop.1) â‰« eq_to_hom (if_pos U.unop.2),
  naturality' := Î» U V inc,
  begin
    dsimp, erw [category.comp_id, â†category.assoc, comp_eq_to_hom_iff, category.assoc,
      eq_to_hom_trans, f.naturality, skyscraper_presheaf_map],
    have hV : pâ‚€ âˆˆ (open_nhds.inclusion pâ‚€).obj V.unop := V.unop.2, split_ifs,
    simpa only [comp_eq_to_hom_iff, category.assoc, eq_to_hom_trans, eq_to_hom_refl,
      category.comp_id],
  end } in colimit.desc _ Ï‡

lemma to_skyscraper_from_stalk {ğ“• : presheaf C X} {c : C} (f : ğ“• âŸ¶ skyscraper_presheaf pâ‚€ c) :
  to_skyscraper_presheaf pâ‚€ (from_stalk _ f) = f :=
nat_trans.ext _ _ $ funext $ Î» U, (em (pâ‚€ âˆˆ U.unop)).elim
(Î» h, by { dsimp, split_ifs, erw [â†category.assoc, colimit.Î¹_desc, category.assoc,
  eq_to_hom_trans, eq_to_hom_refl, category.comp_id], refl }) $
Î» h, by { dsimp, split_ifs, apply ((if_neg h).symm.rec terminal_is_terminal).hom_ext }

lemma from_stalk_to_skyscraper {ğ“• : presheaf C X} {c : C} (f : ğ“•.stalk pâ‚€ âŸ¶ c) :
  from_stalk pâ‚€ (to_skyscraper_presheaf _ f) = f :=
colimit.hom_ext $ Î» U, by { erw [colimit.Î¹_desc], dsimp, rw dif_pos U.unop.2, rw [category.assoc,
  category.assoc, eq_to_hom_trans, eq_to_hom_refl, category.comp_id, presheaf.germ], congr' 3,
  apply_fun opposite.unop using unop_injective, rw [unop_op], ext, refl }

/--
The unit in `presheaf.stalk âŠ£ skyscraper_presheaf_functor`
-/
@[simps] protected def unit :
  ğŸ­ (presheaf C X) âŸ¶ presheaf.stalk_functor C pâ‚€ â‹™ skyscraper_presheaf_functor pâ‚€ :=
{ app := Î» ğ“•, to_skyscraper_presheaf _ $ ğŸ™ _,
  naturality' := Î» ğ“• ğ“– f,
  begin
    ext U, dsimp, split_ifs,
    { simp only [category.id_comp, â†category.assoc], rw [comp_eq_to_hom_iff],
      simp only [category.assoc, eq_to_hom_trans, eq_to_hom_refl, category.comp_id],
      erw [colimit.Î¹_map], refl, },
    { apply ((if_neg h).symm.rec terminal_is_terminal).hom_ext, },
  end }

/--
The counit in `presheaf.stalk âŠ£ skyscraper_presheaf_functor`
-/
@[simps] protected def counit :
  (skyscraper_presheaf_functor pâ‚€ â‹™ (presheaf.stalk_functor C pâ‚€ : presheaf C X â¥¤ C)) âŸ¶ ğŸ­ C :=
{ app := Î» c, (skyscraper_presheaf_stalk_of_specializes pâ‚€ c specializes_rfl).hom,
  naturality' := Î» x y f, colimit.hom_ext $ Î» U,
  begin
    erw [â†category.assoc, colimit.Î¹_map, colimit.iso_colimit_cocone_Î¹_hom_assoc,
      skyscraper_presheaf_cocone_of_specializes_Î¹_app, category.assoc, colimit.Î¹_desc,
      whiskering_left_obj_map, whisker_left_app, skyscraper_presheaf_functor.map'_app,
      dif_pos U.unop.2, skyscraper_presheaf_cocone_of_specializes_Î¹_app, comp_eq_to_hom_iff,
      category.assoc, eq_to_hom_comp_iff, â†category.assoc, eq_to_hom_trans, eq_to_hom_refl,
      category.id_comp, comp_eq_to_hom_iff, category.assoc, eq_to_hom_trans, eq_to_hom_refl,
      category.comp_id, category_theory.functor.id_map],
  end }

end stalk_skyscraper_presheaf_adjunction_auxs

section

open stalk_skyscraper_presheaf_adjunction_auxs

/--
`skyscraper_presheaf_functor` is the right adjoint of `presheaf.stalk_functor`
-/
def skyscraper_presheaf_stalk_adjunction [has_colimits C] :
  (presheaf.stalk_functor C pâ‚€ : presheaf C X â¥¤ C) âŠ£ skyscraper_presheaf_functor pâ‚€ :=
{ hom_equiv := Î» c ğ“•,
  { to_fun := to_skyscraper_presheaf _,
    inv_fun := from_stalk _,
    left_inv := from_stalk_to_skyscraper _,
    right_inv := to_skyscraper_from_stalk _ },
  unit := stalk_skyscraper_presheaf_adjunction_auxs.unit _,
  counit := stalk_skyscraper_presheaf_adjunction_auxs.counit _,
  hom_equiv_unit' := Î» ğ“• c Î±,
  begin
    ext U, simp only [equiv.coe_fn_mk, to_skyscraper_presheaf_app, nat_trans.comp_app,
      skyscraper_presheaf_functor.map'_app, skyscraper_presheaf_functor_map, unit_app], split_ifs,
    { erw [category.id_comp, â†category.assoc, comp_eq_to_hom_iff, category.assoc, category.assoc,
        category.assoc,  category.assoc, eq_to_hom_trans, eq_to_hom_refl, category.comp_id,
        â†category.assoc _ _ Î±, eq_to_hom_trans, eq_to_hom_refl, category.id_comp], },
    { apply ((if_neg h).symm.rec terminal_is_terminal).hom_ext }
  end,
  hom_equiv_counit' := Î» ğ“• c Î±,
  begin
    ext U, simp only [equiv.coe_fn_symm_mk, counit_app],
    erw [colimit.Î¹_desc, â†category.assoc, colimit.Î¹_map, whisker_left_app, category.assoc,
      colimit.Î¹_desc], refl,
  end }

instance [has_colimits C] : is_right_adjoint (skyscraper_presheaf_functor pâ‚€ : C â¥¤ presheaf C X) :=
âŸ¨_, skyscraper_presheaf_stalk_adjunction _âŸ©

instance [has_colimits C] : is_left_adjoint (presheaf.stalk_functor C pâ‚€) :=
âŸ¨_, skyscraper_presheaf_stalk_adjunction _âŸ©

/--
Taking stalks of a sheaf is the left adjoint functor to `skyscraper_sheaf_functor`
-/
def stalk_skyscraper_sheaf_adjunction [has_colimits C] :
  sheaf.forget C X â‹™ presheaf.stalk_functor _ pâ‚€ âŠ£ skyscraper_sheaf_functor pâ‚€ :=
{ hom_equiv := Î» ğ“• c,
  âŸ¨Î» f, âŸ¨to_skyscraper_presheaf pâ‚€ fâŸ©, Î» g, from_stalk pâ‚€ g.1, from_stalk_to_skyscraper pâ‚€,
   Î» g, by { ext1, apply to_skyscraper_from_stalk }âŸ©,
  unit :=
  { app := Î» ğ“•, âŸ¨(stalk_skyscraper_presheaf_adjunction_auxs.unit pâ‚€).app ğ“•.1âŸ©,
    naturality' := Î» ğ“ ğ“‘ âŸ¨fâŸ©,
      by { ext1, apply (stalk_skyscraper_presheaf_adjunction_auxs.unit pâ‚€).naturality } },
  counit := stalk_skyscraper_presheaf_adjunction_auxs.counit pâ‚€,
  hom_equiv_unit' := Î» ğ“ c f,
    by { ext1, exact (skyscraper_presheaf_stalk_adjunction pâ‚€).hom_equiv_unit },
  hom_equiv_counit' := Î» ğ“ c f, (skyscraper_presheaf_stalk_adjunction pâ‚€).hom_equiv_counit }

instance [has_colimits C] : is_right_adjoint (skyscraper_sheaf_functor pâ‚€ : C â¥¤ sheaf C X) :=
âŸ¨_, stalk_skyscraper_sheaf_adjunction _âŸ©

end

end
