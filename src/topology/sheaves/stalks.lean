/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Justus Springer
-/
import topology.category.Top.open_nhds
import topology.sheaves.presheaf
import topology.sheaves.sheaf_condition.unique_gluing
import category_theory.limits.types
import category_theory.limits.preserves.filtered
import category_theory.limits.final
import topology.sober
import tactic.elementwise

/-!
# Stalks

For a presheaf `F` on a topological space `X`, valued in some category `C`, the *stalk* of `F`
at the point `x : X` is defined as the colimit of the following functor

(nhds x)·µí·µñ ‚•§ (opens X)·µí·µñ ‚•§ C

where the functor on the left is the inclusion of categories and the functor on the right is `F`.
For an open neighborhood `U` of `x`, we define the map `F.germ x : F.obj (op U) ‚ü∂ F.stalk x` as the
canonical morphism into this colimit.

Taking stalks is functorial: For every point `x : X` we define a functor `stalk_functor C x`,
sending presheaves on `X` to objects of `C`. Furthermore, for a map `f : X ‚ü∂ Y` between
topological spaces, we define `stalk_pushforward` as the induced map on the stalks
`(f _* ‚Ñ±).stalk (f x) ‚ü∂ ‚Ñ±.stalk x`.

Some lemmas about stalks and germs only hold for certain classes of concrete categories. A basic
property of forgetful functors of categories of algebraic structures (like `Mon`, `CommRing`,...)
is that they preserve filtered colimits. Since stalks are filtered colimits, this ensures that
the stalks of presheaves valued in these categories behave exactly as for `Type`-valued presheaves.
For example, in `germ_exist` we prove that in such a category, every element of the stalk is the
germ of a section.

Furthermore, if we require the forgetful functor to reflect isomorphisms and preserve limits (as
is the case for most algebraic structures), we have access to the unique gluing API and can prove
further properties. Most notably, in `is_iso_iff_stalk_functor_map_iso`, we prove that in such
a category, a morphism of sheaves is an isomorphism if and only if all of its stalk maps are
isomorphisms.

See also the definition of "algebraic structures" in the stacks project:
https://stacks.math.columbia.edu/tag/007L

-/

noncomputable theory

universes v u v' u'

open category_theory
open Top
open category_theory.limits
open topological_space
open opposite

variables {C : Type u} [category.{v} C]

variables [has_colimits.{v} C]

variables {X Y Z : Top.{v}}

namespace Top.presheaf

variables (C)
/-- Stalks are functorial with respect to morphisms of presheaves over a fixed `X`. -/
def stalk_functor (x : X) : X.presheaf C ‚•§ C :=
((whiskering_left _ _ C).obj (open_nhds.inclusion x).op) ‚ãô colim

variables {C}

/--
The stalk of a presheaf `F` at a point `x` is calculated as the colimit of the functor
nbhds x ‚•§ opens F.X ‚•§ C
-/
def stalk (‚Ñ± : X.presheaf C) (x : X) : C :=
(stalk_functor C x).obj ‚Ñ± -- -- colimit ((open_nhds.inclusion x).op ‚ãô ‚Ñ±)

@[simp] lemma stalk_functor_obj (‚Ñ± : X.presheaf C) (x : X) :
  (stalk_functor C x).obj ‚Ñ± = ‚Ñ±.stalk x := rfl

/--
The germ of a section of a presheaf over an open at a point of that open.
-/
def germ (F : X.presheaf C) {U : opens X} (x : U) : F.obj (op U) ‚ü∂ stalk F x :=
colimit.Œπ ((open_nhds.inclusion x.1).op ‚ãô F) (op ‚ü®U, x.2‚ü©)

@[simp, elementwise]
lemma germ_res (F : X.presheaf C) {U V : opens X} (i : U ‚ü∂ V) (x : U) :
  F.map i.op ‚â´ germ F x = germ F (i x : V) :=
let i' : (‚ü®U, x.2‚ü© : open_nhds x.1) ‚ü∂ ‚ü®V, (i x : V).2‚ü© := i in
colimit.w ((open_nhds.inclusion x.1).op ‚ãô F) i'.op

/--
A morphism from the stalk of `F` at `x` to some object `Y` is completely determined by its
composition with the `germ` morphisms.
-/
lemma stalk_hom_ext (F : X.presheaf C) {x} {Y : C} {f‚ÇÅ f‚ÇÇ : F.stalk x ‚ü∂ Y}
  (ih : ‚àÄ (U : opens X) (hxU : x ‚àà U), F.germ ‚ü®x, hxU‚ü© ‚â´ f‚ÇÅ = F.germ ‚ü®x, hxU‚ü© ‚â´ f‚ÇÇ) : f‚ÇÅ = f‚ÇÇ :=
colimit.hom_ext $ Œª U, by { induction U using opposite.rec, cases U with U hxU, exact ih U hxU }

@[simp, reassoc, elementwise]
lemma stalk_functor_map_germ {F G : X.presheaf C} (U : opens X) (x : U)
  (f : F ‚ü∂ G) : germ F x ‚â´ (stalk_functor C x.1).map f = f.app (op U) ‚â´ germ G x :=
colimit.Œπ_map (whisker_left ((open_nhds.inclusion x.1).op) f) (op ‚ü®U, x.2‚ü©)

variables (C)

/--
For a presheaf `F` on a space `X`, a continuous map `f : X ‚ü∂ Y` induces a morphisms between the
stalk of `f _ * F` at `f x` and the stalk of `F` at `x`.
-/
def stalk_pushforward (f : X ‚ü∂ Y) (F : X.presheaf C) (x : X) : (f _* F).stalk (f x) ‚ü∂ F.stalk x :=
begin
  -- This is a hack; Lean doesn't like to elaborate the term written directly.
  transitivity,
  swap,
  exact colimit.pre _ (open_nhds.map f x).op,
  exact colim.map (whisker_right (nat_trans.op (open_nhds.inclusion_map_iso f x).inv) F),
end

@[simp, elementwise, reassoc]
lemma stalk_pushforward_germ (f : X ‚ü∂ Y) (F : X.presheaf C) (U : opens Y)
  (x : (opens.map f).obj U) :
  (f _* F).germ ‚ü®f x, x.2‚ü© ‚â´ F.stalk_pushforward C f x = F.germ x :=
begin
  rw [stalk_pushforward, germ, colimit.Œπ_map_assoc, colimit.Œπ_pre, whisker_right_app],
  erw [category_theory.functor.map_id, category.id_comp],
  refl,
end

-- Here are two other potential solutions, suggested by @fpvandoorn at
-- <https://github.com/leanprover-community/mathlib/pull/1018#discussion_r283978240>
-- However, I can't get the subsequent two proofs to work with either one.

-- def stalk_pushforward (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) (x : X) :
--   (f _* ‚Ñ±).stalk (f x) ‚ü∂ ‚Ñ±.stalk x :=
-- colim.map ((functor.associator _ _ _).inv ‚â´
--   whisker_right (nat_trans.op (open_nhds.inclusion_map_iso f x).inv) ‚Ñ±) ‚â´
-- colimit.pre ((open_nhds.inclusion x).op ‚ãô ‚Ñ±) (open_nhds.map f x).op

-- def stalk_pushforward (f : X ‚ü∂ Y) (‚Ñ± : X.presheaf C) (x : X) :
--   (f _* ‚Ñ±).stalk (f x) ‚ü∂ ‚Ñ±.stalk x :=
-- (colim.map (whisker_right (nat_trans.op (open_nhds.inclusion_map_iso f x).inv) ‚Ñ±) :
--   colim.obj ((open_nhds.inclusion (f x) ‚ãô opens.map f).op ‚ãô ‚Ñ±) ‚ü∂ _) ‚â´
-- colimit.pre ((open_nhds.inclusion x).op ‚ãô ‚Ñ±) (open_nhds.map f x).op

namespace stalk_pushforward
local attribute [tidy] tactic.op_induction'

@[simp] lemma id (‚Ñ± : X.presheaf C) (x : X) :
  ‚Ñ±.stalk_pushforward C (ùüô X) x = (stalk_functor C x).map ((pushforward.id ‚Ñ±).hom) :=
begin
  dsimp [stalk_pushforward, stalk_functor],
  ext1,
  tactic.op_induction',
  cases j, cases j_val,
  rw [colimit.Œπ_map_assoc, colimit.Œπ_map, colimit.Œπ_pre, whisker_left_app, whisker_right_app,
       pushforward.id_hom_app, eq_to_hom_map, eq_to_hom_refl],
  dsimp,
  -- FIXME A simp lemma which unfortunately doesn't fire:
  erw [category_theory.functor.map_id],
end

-- This proof is sadly not at all robust:
-- having to use `erw` at all is a bad sign.
@[simp] lemma comp (‚Ñ± : X.presheaf C) (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
  ‚Ñ±.stalk_pushforward C (f ‚â´ g) x =
  ((f _* ‚Ñ±).stalk_pushforward C g (f x)) ‚â´ (‚Ñ±.stalk_pushforward C f x) :=
begin
  dsimp [stalk_pushforward, stalk_functor],
  ext U,
  induction U using opposite.rec,
  cases U,
  cases U_val,
  simp only [colimit.Œπ_map_assoc, colimit.Œπ_pre_assoc,
             whisker_right_app, category.assoc],
  dsimp,
  -- FIXME: Some of these are simp lemmas, but don't fire successfully:
  erw [category_theory.functor.map_id, category.id_comp, category.id_comp, category.id_comp,
       colimit.Œπ_pre, colimit.Œπ_pre],
  refl,
end

lemma stalk_pushforward_iso_of_open_embedding {f : X ‚ü∂ Y} (hf : open_embedding f)
   (F : X.presheaf C) (x : X) : is_iso (F.stalk_pushforward _ f x) :=
 begin
   haveI := functor.initial_of_adjunction (hf.is_open_map.adjunction_nhds x),
   convert is_iso.of_iso ((functor.final.colimit_iso (hf.is_open_map.functor_nhds x).op
     ((open_nhds.inclusion (f x)).op ‚ãô f _* F) : _).symm ‚â™‚â´ colim.map_iso _),
   swap,
   { fapply nat_iso.of_components,
     { intro U,
       refine F.map_iso (eq_to_iso _),
       dsimp only [functor.op],
       exact congr_arg op (subtype.eq $ set.preimage_image_eq (unop U).1.1 hf.inj) },
     { intros U V i, erw [‚Üê F.map_comp, ‚Üê F.map_comp], congr } },
   { ext U,
     rw ‚Üê iso.comp_inv_eq,
     erw colimit.Œπ_map_assoc,
     rw [colimit.Œπ_pre, category.assoc],
     erw [colimit.Œπ_map_assoc, colimit.Œπ_pre, ‚Üê F.map_comp_assoc],
     apply colimit.w ((open_nhds.inclusion (f x)).op ‚ãô f _* F) _,
     dsimp only [functor.op],
     refine ((hom_of_le _).op : op (unop U) ‚ü∂ _),
     exact set.image_preimage_subset _ _ },
 end

end stalk_pushforward

section stalk_pullback

/-- The morphism `‚Ñ±_{f x} ‚ü∂ (f‚Åª¬π‚Ñ±)‚Çì` that factors through `(f_*f‚Åª¬π‚Ñ±)_{f x}`. -/
def stalk_pullback_hom (f : X ‚ü∂ Y) (F : Y.presheaf C) (x : X) :
  F.stalk (f x) ‚ü∂ (pullback_obj f F).stalk x :=
(stalk_functor _ (f x)).map ((pushforward_pullback_adjunction C f).unit.app F) ‚â´
  stalk_pushforward _ _ _ x

/-- The morphism `(f‚Åª¬π‚Ñ±)(U) ‚ü∂ ‚Ñ±_{f(x)}` for some `U ‚àã x`. -/
def germ_to_pullback_stalk (f : X ‚ü∂ Y) (F : Y.presheaf C) (U : opens X) (x : U) :
  (pullback_obj f F).obj (op U) ‚ü∂ F.stalk (f x) :=
colimit.desc (Lan.diagram (opens.map f).op F (op U))
{ X := F.stalk (f x),
  Œπ := { app := Œª V, F.germ ‚ü®f x, V.hom.unop.le x.2‚ü©,
          naturality' := Œª _ _ i, by { erw category.comp_id, exact F.germ_res i.left.unop _ } } }

/-- The morphism `(f‚Åª¬π‚Ñ±)‚Çì ‚ü∂ ‚Ñ±_{f(x)}`. -/
def stalk_pullback_inv (f : X ‚ü∂ Y) (F : Y.presheaf C) (x : X) :
  (pullback_obj f F).stalk x ‚ü∂ F.stalk (f x) :=
colimit.desc ((open_nhds.inclusion x).op ‚ãô presheaf.pullback_obj f F)
{ X := F.stalk (f x),
  Œπ := { app := Œª U, F.germ_to_pullback_stalk _ f (unop U).1 ‚ü®x, (unop U).2‚ü©,
          naturality' := Œª _ _ _, by { erw [colimit.pre_desc, category.comp_id], congr } } }

/-- The isomorphism `‚Ñ±_{f(x)} ‚âÖ (f‚Åª¬π‚Ñ±)‚Çì`. -/
def stalk_pullback_iso (f : X ‚ü∂ Y) (F : Y.presheaf C) (x : X) :
  F.stalk (f x) ‚âÖ (pullback_obj f F).stalk x :=
{ hom := stalk_pullback_hom _ _ _ _,
  inv := stalk_pullback_inv _ _ _ _,
  hom_inv_id' :=
  begin
    delta stalk_pullback_hom stalk_pullback_inv stalk_functor presheaf.pullback stalk_pushforward
      germ_to_pullback_stalk germ,
    ext j,
    induction j using opposite.rec,
    cases j,
    simp only [topological_space.open_nhds.inclusion_map_iso_inv, whisker_right_app,
      whisker_left_app, whiskering_left_obj_map, functor.comp_map, colimit.Œπ_map_assoc,
      nat_trans.op_id, Lan_obj_map, pushforward_pullback_adjunction_unit_app_app, category.assoc,
      colimit.Œπ_pre_assoc],
    erw [colimit.Œπ_desc, colimit.pre_desc, colimit.Œπ_desc, category.comp_id],
    simpa
  end,
  inv_hom_id' :=
  begin
    delta stalk_pullback_hom stalk_pullback_inv stalk_functor presheaf.pullback stalk_pushforward,
    ext U j,
    induction U using opposite.rec,
    cases U, cases j, cases j_right,
    erw [colimit.map_desc, colimit.map_desc, colimit.Œπ_desc_assoc,
      colimit.Œπ_desc_assoc, colimit.Œπ_desc, category.comp_id],
    simp only [cocone.whisker_Œπ, colimit.cocone_Œπ, open_nhds.inclusion_map_iso_inv,
      cocones.precompose_obj_Œπ, whisker_right_app, whisker_left_app, nat_trans.comp_app,
      whiskering_left_obj_map, nat_trans.op_id, Lan_obj_map,
      pushforward_pullback_adjunction_unit_app_app],
    erw ‚Üêcolimit.w _
      (@hom_of_le (open_nhds x) _
         ‚ü®_, U_property‚ü© ‚ü®(opens.map f).obj (unop j_left), j_hom.unop.le U_property‚ü©
         j_hom.unop.le).op,
    erw colimit.Œπ_pre_assoc (Lan.diagram _ F _) (costructured_arrow.map _),
    erw colimit.Œπ_pre_assoc (Lan.diagram _ F _) (costructured_arrow.map _),
    congr,
    simp only [category.assoc, costructured_arrow.map_mk],
    delta costructured_arrow.mk,
    congr
  end }

end stalk_pullback

section stalk_specializes

variables {C}

/-- If `x` specializes to `y`, then there is a natural map `F.stalk y ‚ü∂ F.stalk x`. -/
noncomputable
def stalk_specializes (F : X.presheaf C) {x y : X} (h : x ‚§≥ y) : F.stalk y ‚ü∂ F.stalk x :=
begin
  refine colimit.desc _ ‚ü®_,Œª U, _,_‚ü©,
  { exact colimit.Œπ ((open_nhds.inclusion x).op ‚ãô F)
      (op ‚ü®(unop U).1, (specializes_iff_forall_open.mp h _ (unop U).1.2 (unop U).2 : _)‚ü©) },
  { intros U V i,
    dsimp,
    rw category.comp_id,
    let U' : open_nhds x := ‚ü®_, (specializes_iff_forall_open.mp h _ (unop U).1.2 (unop U).2 : _)‚ü©,
    let V' : open_nhds x := ‚ü®_, (specializes_iff_forall_open.mp h _ (unop V).1.2 (unop V).2 : _)‚ü©,
    exact colimit.w ((open_nhds.inclusion x).op ‚ãô F) (show V' ‚ü∂ U', from i.unop).op }
end

@[simp, reassoc, elementwise]
lemma germ_stalk_specializes (F : X.presheaf C) {U : opens X} {y : U} {x : X} (h : x ‚§≥ y) :
  F.germ y ‚â´ F.stalk_specializes h =
    F.germ ‚ü®x, specializes_iff_forall_open.mp h _ U.2 y.prop‚ü© := colimit.Œπ_desc _ _

@[simp, reassoc, elementwise]
lemma germ_stalk_specializes' (F : X.presheaf C) {U : opens X} {x y : X} (h : x ‚§≥ y) (hy : y ‚àà U) :
  F.germ ‚ü®y, hy‚ü© ‚â´ F.stalk_specializes h =
    F.germ ‚ü®x, specializes_iff_forall_open.mp h _ U.2 hy‚ü© := colimit.Œπ_desc _ _

@[simp, reassoc, elementwise]
lemma stalk_specializes_stalk_functor_map {F G : X.presheaf C} (f : F ‚ü∂ G) {x y : X} (h : x ‚§≥ y) :
  F.stalk_specializes h ‚â´ (stalk_functor C x).map f =
    (stalk_functor C y).map f ‚â´ G.stalk_specializes h :=
by { ext, delta stalk_functor, simpa [stalk_specializes] }

@[simp, reassoc, elementwise]
lemma stalk_specializes_stalk_pushforward (f : X ‚ü∂ Y) (F : X.presheaf C) {x y : X} (h : x ‚§≥ y) :
  (f _* F).stalk_specializes (f.map_specialization h) ‚â´ F.stalk_pushforward _ f x =
    F.stalk_pushforward _ f y ‚â´ F.stalk_specializes h :=
by { ext, delta stalk_pushforward, simpa [stalk_specializes] }

end stalk_specializes

section concrete

variables {C}
variables [concrete_category.{v} C]

local attribute [instance] concrete_category.has_coe_to_sort concrete_category.has_coe_to_fun

@[ext]
lemma germ_ext (F : X.presheaf C) {U V : opens X} {x : X} {hxU : x ‚àà U} {hxV : x ‚àà V}
  (W : opens X) (hxW : x ‚àà W) (iWU : W ‚ü∂ U) (iWV : W ‚ü∂ V) {sU : F.obj (op U)} {sV : F.obj (op V)}
  (ih : F.map iWU.op sU = F.map iWV.op sV) :
  F.germ ‚ü®x, hxU‚ü© sU = F.germ ‚ü®x, hxV‚ü© sV :=
by erw [‚Üê F.germ_res iWU ‚ü®x, hxW‚ü©,
    ‚Üê F.germ_res iWV ‚ü®x, hxW‚ü©, comp_apply, comp_apply, ih]

variables [preserves_filtered_colimits (forget C)]

/--
For presheaves valued in a concrete category whose forgetful functor preserves filtered colimits,
every element of the stalk is the germ of a section.
-/
lemma germ_exist (F : X.presheaf C) (x : X) (t : stalk F x) :
  ‚àÉ (U : opens X) (m : x ‚àà U) (s : F.obj (op U)), F.germ ‚ü®x, m‚ü© s = t :=
begin
  obtain ‚ü®U, s, e‚ü© := types.jointly_surjective _
    (is_colimit_of_preserves (forget C) (colimit.is_colimit _)) t,
  revert s e,
  rw [(show U = op (unop U), from rfl)],
  generalize : unop U = V, clear U,
  cases V with V m,
  intros s e,
  exact ‚ü®V, m, s, e‚ü©,
end

lemma germ_eq (F : X.presheaf C) {U V : opens X} (x : X) (mU : x ‚àà U) (mV : x ‚àà V)
  (s : F.obj (op U)) (t : F.obj (op V))
  (h : germ F ‚ü®x, mU‚ü© s = germ F ‚ü®x, mV‚ü© t) :
  ‚àÉ (W : opens X) (m : x ‚àà W) (iU : W ‚ü∂ U) (iV : W ‚ü∂ V), F.map iU.op s = F.map iV.op t :=
begin
  obtain ‚ü®W, iU, iV, e‚ü© := (types.filtered_colimit.is_colimit_eq_iff _
    (is_colimit_of_preserves _ (colimit.is_colimit ((open_nhds.inclusion x).op ‚ãô F)))).mp h,
  exact ‚ü®(unop W).1, (unop W).2, iU.unop, iV.unop, e‚ü©,
end

lemma stalk_functor_map_injective_of_app_injective {F G : presheaf C X} (f : F ‚ü∂ G)
  (h : ‚àÄ U : opens X, function.injective (f.app (op U))) (x : X) :
  function.injective ((stalk_functor C x).map f) := Œª s t hst,
begin
  rcases germ_exist F x s with ‚ü®U‚ÇÅ, hxU‚ÇÅ, s, rfl‚ü©,
  rcases germ_exist F x t with ‚ü®U‚ÇÇ, hxU‚ÇÇ, t, rfl‚ü©,
  simp only [stalk_functor_map_germ_apply _ ‚ü®x,_‚ü©] at hst,
  obtain ‚ü®W, hxW, iWU‚ÇÅ, iWU‚ÇÇ, heq‚ü© := G.germ_eq x hxU‚ÇÅ hxU‚ÇÇ _ _ hst,
  rw [‚Üê comp_apply, ‚Üê comp_apply, ‚Üê f.naturality, ‚Üê f.naturality, comp_apply, comp_apply] at heq,
  replace heq := h W heq,
  convert congr_arg (F.germ ‚ü®x,hxW‚ü©) heq,
  exacts [(F.germ_res_apply iWU‚ÇÅ ‚ü®x,hxW‚ü© s).symm,
          (F.germ_res_apply iWU‚ÇÇ ‚ü®x,hxW‚ü© t).symm],
end


variables [has_limits C] [preserves_limits (forget C)] [reflects_isomorphisms (forget C)]

/--
Let `F` be a sheaf valued in a concrete category, whose forgetful functor reflects isomorphisms,
preserves limits and filtered colimits. Then two sections who agree on every stalk must be equal.
-/
lemma section_ext (F : sheaf C X) (U : opens X) (s t : F.1.obj (op U))
  (h : ‚àÄ x : U, F.1.germ x s = F.1.germ x t) :
  s = t :=
begin
  -- We use `germ_eq` and the axiom of choice, to pick for every point `x` a neighbourhood
  -- `V x`, such that the restrictions of `s` and `t` to `V x` coincide.
  choose V m i‚ÇÅ i‚ÇÇ heq using Œª x : U, F.1.germ_eq x.1 x.2 x.2 s t (h x),
  -- Since `F` is a sheaf, we can prove the equality locally, if we can show that these
  -- neighborhoods form a cover of `U`.
  apply F.eq_of_locally_eq' V U i‚ÇÅ,
  { intros x hxU,
    rw [opens.mem_coe, opens.mem_supr],
    exact ‚ü®‚ü®x, hxU‚ü©, m ‚ü®x, hxU‚ü©‚ü© },
  { intro x,
    rw [heq, subsingleton.elim (i‚ÇÅ x) (i‚ÇÇ x)] }
end

/-
Note that the analogous statement for surjectivity is false: Surjectivity on stalks does not
imply surjectivity of the components of a sheaf morphism. However it does imply that the morphism
is an epi, but this fact is not yet formalized.
-/
lemma app_injective_of_stalk_functor_map_injective {F : sheaf C X} {G : presheaf C X}
  (f : F.1 ‚ü∂ G) (U : opens X) (h : ‚àÄ x : U, function.injective ((stalk_functor C x.val).map f)) :
  function.injective (f.app (op U)) :=
Œª s t hst, section_ext F _ _ _ $ Œª x, h x $ by
  rw [stalk_functor_map_germ_apply, stalk_functor_map_germ_apply, hst]

lemma app_injective_iff_stalk_functor_map_injective {F : sheaf C X}
  {G : presheaf C X} (f : F.1 ‚ü∂ G) :
  (‚àÄ x : X, function.injective ((stalk_functor C x).map f)) ‚Üî
  (‚àÄ U : opens X, function.injective (f.app (op U))) :=
‚ü®Œª h U, app_injective_of_stalk_functor_map_injective f U (Œª x, h x.1),
  stalk_functor_map_injective_of_app_injective f‚ü©

/-- For surjectivity, we are given an arbitrary section `t` and need to find a preimage for it.
We claim that it suffices to find preimages *locally*. That is, for each `x : U` we construct
a neighborhood `V ‚â§ U` and a section `s : F.obj (op V))` such that `f.app (op V) s` and `t`
agree on `V`. -/
lemma app_surjective_of_injective_of_locally_surjective {F G : sheaf C X} (f : F ‚ü∂ G)
  (U : opens X) (hinj : ‚àÄ x : U, function.injective ((stalk_functor C x.1).map f))
  (hsurj : ‚àÄ (t) (x : U), ‚àÉ (V : opens X) (m : x.1 ‚àà V) (iVU : V ‚ü∂ U) (s : F.1.obj (op V)),
    f.app (op V) s = G.1.map iVU.op t) :
  function.surjective (f.app (op U)) :=
begin
  intro t,
  -- We use the axiom of choice to pick around each point `x` an open neighborhood `V` and a
  -- preimage under `f` on `V`.
  choose V mV iVU sf heq using hsurj t,
  -- These neighborhoods clearly cover all of `U`.
  have V_cover : U ‚â§ supr V,
  { intros x hxU,
    rw [opens.mem_coe, opens.mem_supr],
    exact ‚ü®‚ü®x, hxU‚ü©, mV ‚ü®x, hxU‚ü©‚ü© },
  -- Since `F` is a sheaf, we can glue all the local preimages together to get a global preimage.
  obtain ‚ü®s, s_spec, -‚ü© := F.exists_unique_gluing' V U iVU V_cover sf _,
  { use s,
    apply G.eq_of_locally_eq' V U iVU V_cover,
    intro x,
    rw [‚Üê comp_apply, ‚Üê f.naturality, comp_apply, s_spec, heq] },
  { intros x y,
    -- What's left to show here is that the secions `sf` are compatible, i.e. they agree on
    -- the intersections `V x ‚äì V y`. We prove this by showing that all germs are equal.
    apply section_ext,
    intro z,
    -- Here, we need to use injectivity of the stalk maps.
    apply (hinj ‚ü®z, (iVU x).le ((inf_le_left : V x ‚äì V y ‚â§ V x) z.2)‚ü©),
    dsimp only,
    erw [stalk_functor_map_germ_apply, stalk_functor_map_germ_apply],
    simp_rw [‚Üê comp_apply, f.naturality, comp_apply, heq, ‚Üê comp_apply, ‚Üê G.1.map_comp],
    refl }
end

lemma app_surjective_of_stalk_functor_map_bijective {F G : sheaf C X} (f : F ‚ü∂ G)
  (U : opens X) (h : ‚àÄ x : U, function.bijective ((stalk_functor C x.val).map f)) :
  function.surjective (f.app (op U)) :=
begin
  refine app_surjective_of_injective_of_locally_surjective f U (Œª x, (h x).1) (Œª t x, _),
  -- Now we need to prove our initial claim: That we can find preimages of `t` locally.
  -- Since `f` is surjective on stalks, we can find a preimage `s‚ÇÄ` of the germ of `t` at `x`
  obtain ‚ü®s‚ÇÄ,hs‚ÇÄ‚ü© := (h x).2 (G.1.germ x t),
  -- ... and this preimage must come from some section `s‚ÇÅ` defined on some open neighborhood `V‚ÇÅ`
  obtain ‚ü®V‚ÇÅ,hxV‚ÇÅ,s‚ÇÅ,hs‚ÇÅ‚ü© := F.1.germ_exist x.1 s‚ÇÄ,
  subst hs‚ÇÅ, rename hs‚ÇÄ hs‚ÇÅ,
  erw stalk_functor_map_germ_apply V‚ÇÅ ‚ü®x.1,hxV‚ÇÅ‚ü© f s‚ÇÅ at hs‚ÇÅ,
  -- Now, the germ of `f.app (op V‚ÇÅ) s‚ÇÅ` equals the germ of `t`, hence they must coincide on
  -- some open neighborhood `V‚ÇÇ`.
  obtain ‚ü®V‚ÇÇ, hxV‚ÇÇ, iV‚ÇÇV‚ÇÅ, iV‚ÇÇU, heq‚ü© := G.1.germ_eq x.1 hxV‚ÇÅ x.2 _ _ hs‚ÇÅ,
  -- The restriction of `s‚ÇÅ` to that neighborhood is our desired local preimage.
  use [V‚ÇÇ, hxV‚ÇÇ, iV‚ÇÇU, F.1.map iV‚ÇÇV‚ÇÅ.op s‚ÇÅ],
  rw [‚Üê comp_apply, f.naturality, comp_apply, heq],
end

lemma app_bijective_of_stalk_functor_map_bijective {F G : sheaf C X} (f : F ‚ü∂ G)
   (U : opens X) (h : ‚àÄ x : U, function.bijective ((stalk_functor C x.val).map f)) :
  function.bijective (f.app (op U)) :=
‚ü®app_injective_of_stalk_functor_map_injective f U (Œª x, (h x).1),
  app_surjective_of_stalk_functor_map_bijective f U h‚ü©

lemma app_is_iso_of_stalk_functor_map_iso {F G : sheaf C X} (f : F ‚ü∂ G) (U : opens X)
  [‚àÄ x : U, is_iso ((stalk_functor C x.val).map f)] : is_iso (f.app (op U)) :=
begin
  -- Since the forgetful functor of `C` reflects isomorphisms, it suffices to see that the
  -- underlying map between types is an isomorphism, i.e. bijective.
  suffices : is_iso ((forget C).map (f.app (op U))),
  { exactI is_iso_of_reflects_iso (f.app (op U)) (forget C) },
  rw is_iso_iff_bijective,
  apply app_bijective_of_stalk_functor_map_bijective,
  intro x,
  apply (is_iso_iff_bijective _).mp,
  exact functor.map_is_iso (forget C) ((stalk_functor C x.1).map f)
end

/--
Let `F` and `G` be sheaves valued in a concrete category, whose forgetful functor reflects
isomorphisms, preserves limits and filtered colimits. Then if the stalk maps of a morphism
`f : F ‚ü∂ G` are all isomorphisms, `f` must be an isomorphism.
-/
-- Making this an instance would cause a loop in typeclass resolution with `functor.map_is_iso`
lemma is_iso_of_stalk_functor_map_iso {F G : sheaf C X} (f : F ‚ü∂ G)
  [‚àÄ x : X, is_iso ((stalk_functor C x).map f)] : is_iso f :=
begin
  -- Since the inclusion functor from sheaves to presheaves is fully faithful, it suffices to
  -- show that `f`, as a morphism between _presheaves_, is an isomorphism.
  suffices : is_iso ((sheaf.forget C X).map f),
  { exactI is_iso_of_fully_faithful (sheaf.forget C X) f },
  -- We show that all components of `f` are isomorphisms.
  suffices : ‚àÄ U : (opens X)·µí·µñ, is_iso (f.app U),
  { exact @nat_iso.is_iso_of_is_iso_app _ _ _ _ F.1 G.1 f this, },
  intro U, induction U using opposite.rec,
  apply app_is_iso_of_stalk_functor_map_iso
end

/--
Let `F` and `G` be sheaves valued in a concrete category, whose forgetful functor reflects
isomorphisms, preserves limits and filtered colimits. Then a morphism `f : F ‚ü∂ G` is an
isomorphism if and only if all of its stalk maps are isomorphisms.
-/
lemma is_iso_iff_stalk_functor_map_iso {F G : sheaf C X} (f : F ‚ü∂ G) :
  is_iso f ‚Üî ‚àÄ x : X, is_iso ((stalk_functor C x).map f) :=
begin
  split,
  { intros h x, resetI,
    exact @functor.map_is_iso _ _ _ _ _ _ (stalk_functor C x) f
      ((sheaf.forget C X).map_is_iso f) },
  { intro h,
    exactI is_iso_of_stalk_functor_map_iso f }
end

end concrete

end Top.presheaf
