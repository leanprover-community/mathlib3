/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/
import algebraic_geometry.prime_spectrum
import ring_theory.localization
import algebra.category.CommRing
import topology.sheaves.local_predicate
import topology.sheaves.forget
import ring_theory.bundled_subring

/-!
# The structure sheaf on `prime_spectrum R`.

-/

universe u

noncomputable theory

variables (R : Type u) [comm_ring R]

open Top
open topological_space
open category_theory
open opposite

/--
The type family over `prime_spectrum R` consisting of the localization over each point.
-/
@[reducible]
def localizations := Î» (P : Top.of (prime_spectrum R)), localization.at_prime P.as_ideal

variables {R}

/--
We will define the structure sheaf as
the subsheaf of all dependent functions in `Î  x : U, localizations R x`
consisting of those functions which can locally be expressed as a ratio of
(the images in the localization of) elements of `R`.

Quoting Hartshorne:

For an open set $$U âŠ† Spec A$$, we define $$ğ’ª(U)$$ to be the set of functions
$$s : U â†’ â¨†_{ğ”­ âˆˆ U} A_ğ”­$$, such that $s(ğ”­) âˆˆ A_ğ”­$$ for each $$ğ”­$$,
and such that $$s$$ is locally a quotient of elements of $$A$$:
to be precise, we require that for each $$ğ”­ âˆˆ U$$, there is a neighborhood $$V$$ of $$ğ”­$$,
contained in $$U$$, and elements $$a, f âˆˆ A$$, such that for each $$ğ”® âˆˆ V, f âˆ‰ ğ”®$$,
and $$s(ğ”®) = a/f$$ in $$A_ğ”®$$.

Now Hartshorne had the disadvantage of not knowing about dependent functions,
so we replace his circumlocution about functions into a disjoint union with
`Î  x : U, stalks x`.
-/
def locally_fraction {U : opens (Top.of (prime_spectrum R))} (f : Î  x : U, localizations R x) : Prop :=
âˆ€ x : U, âˆƒ (V) (m : x.1 âˆˆ V) (i : V âŸ¶ U),
  âˆƒ (r s : R), âˆ€ y : V,
  Â¬ (s âˆˆ y.1.as_ideal) âˆ§
    f (i y : U) * (localization.of _).to_map s = (localization.of _).to_map r

variables (R)

/--
We verify that `locally_fraction` is a `local_predicate`.
This is purely formal, just shuffling around quantifiers.
-/
def locally_fraction_local : local_predicate (localizations R) :=
{ pred := Î» U f, locally_fraction f,
  res := Î» V U i f h x,
  begin
    rcases h (i x : U) with âŸ¨W, m, i, r, s, wâŸ©,
    exact âŸ¨V âŠ“ W, âŸ¨x.2, mâŸ©, opens.inf_le_left V W, r, s, (Î» y, w âŸ¨y.1, y.2.2âŸ©)âŸ©,
  end,
  locality := Î» U f w x,
  begin
    rcases w x with âŸ¨V, m, i, hâŸ©, clear w,
    rcases h âŸ¨x.1, mâŸ© with âŸ¨V', m', i', r, s, h'âŸ©, clear h,
    exact âŸ¨V', m', i' â‰« i, r, s, h'âŸ©,
  end, }

/--
The structure sheaf (valued in `Type`, not yet `CommRing`) is the subsheaf consisting of
functions satisfying `locally_fraction`.
-/
def structure_sheaf_in_Type : sheaf (Type u) (Top.of (prime_spectrum R)) :=
subsheaf_to_Types (locally_fraction_local R)

/--
The functions satisfying `locally_fraction` form a subring.
-/
def sections_subring (U : (opens (Top.of (prime_spectrum R)))áµ’áµ–) :
  subring (Î  x : unop U, localizations R x) :=
{ carrier := { f | locally_fraction f },
  zero_mem' :=
  begin
    refine Î» x, âŸ¨unop U, x.2, ğŸ™ _, 0, 1, Î» y, âŸ¨_, _âŸ©âŸ©,
    { rw â†ideal.ne_top_iff_one, exact y.1.is_prime.1, },
    { simp, },
  end,
  one_mem' :=
  begin
    refine Î» x, âŸ¨unop U, x.2, ğŸ™ _, 1, 1, Î» y, âŸ¨_, _âŸ©âŸ©,
    { rw â†ideal.ne_top_iff_one, exact y.1.is_prime.1, },
    { simp, },
  end,
  add_mem' :=
  begin
    intros a b ha hb x,
    rcases ha x with âŸ¨Va, ma, ia, ra, sa, waâŸ©,
    rcases hb x with âŸ¨Vb, mb, ib, rb, sb, wbâŸ©,
    refine âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, opens.inf_le_left _ _ â‰« ia, ra * sb + rb * sa, sa * sb, _âŸ©,
    intro y,
    rcases wa (opens.inf_le_left _ _ y) with âŸ¨nma, waâŸ©,
    rcases wb (opens.inf_le_right _ _ y) with âŸ¨nmb, wbâŸ©,
    fsplit,
    { intro H, cases y.1.is_prime.mem_or_mem H; contradiction, },
    { simp only [add_mul, ring_hom.map_add, pi.add_apply, ring_hom.map_mul],
      erw [â†wa, â†wb],
      simp only [mul_assoc],
      congr' 2,
      rw [mul_comm], refl, }
  end,
  neg_mem' :=
  begin
    intros a ha x,
    rcases ha x with âŸ¨V, m, i, r, s, wâŸ©,
    refine âŸ¨V, m, i, -r, s, _âŸ©,
    intro y,
    rcases w y with âŸ¨nm, wâŸ©,
    fsplit,
    { exact nm, },
    { simp only [ring_hom.map_neg, pi.neg_apply],
      erw [â†w],
      simp only [neg_mul_eq_neg_mul_symm], }
  end,
  mul_mem' :=
  begin
    intros a b ha hb x,
    rcases ha x with âŸ¨Va, ma, ia, ra, sa, waâŸ©,
    rcases hb x with âŸ¨Vb, mb, ib, rb, sb, wbâŸ©,
    refine âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, opens.inf_le_left _ _ â‰« ia, ra * rb, sa * sb, _âŸ©,
    intro y,
    rcases wa (opens.inf_le_left _ _ y) with âŸ¨nma, waâŸ©,
    rcases wb (opens.inf_le_right _ _ y) with âŸ¨nmb, wbâŸ©,
    fsplit,
    { intro H, cases y.1.is_prime.mem_or_mem H; contradiction, },
    { simp only [pi.mul_apply, ring_hom.map_mul],
      erw [â†wa, â†wb],
      simp only [mul_left_comm, mul_assoc, mul_comm],
      refl, }
  end, }

instance comm_ring_structure_sheaf_in_Type_obj (U : (opens (Top.of (prime_spectrum R)))áµ’áµ–) :
  comm_ring ((structure_sheaf_in_Type R).presheaf.obj U) :=
(sections_subring R U).to_comm_ring

/--
The structure presheaf, valued in `CommRing`, constructed by dressing up the `Type` valued
structure presheaf.
-/
@[simps]
def structure_presheaf_in_CommRing : presheaf CommRing (Top.of (prime_spectrum R)) :=
{ obj := Î» U, CommRing.of ((structure_sheaf_in_Type R).presheaf.obj U),
  map := Î» U V i,
  { to_fun := ((structure_sheaf_in_Type R).presheaf.map i),
    map_zero' := rfl,
    map_add' := Î» x y, rfl,
    map_one' := rfl,
    map_mul' := Î» x y, rfl, }, }

/--
Some glue, verifying that that structure presheaf valued in `CommRing` agrees
with the `Type` valued structure presheaf.
-/
def structure_presheaf_comp_forget :
  structure_presheaf_in_CommRing R â‹™ (forget CommRing) â‰… (structure_sheaf_in_Type R).presheaf :=
nat_iso.of_components
  (Î» U, iso.refl _)
  (by tidy)

/--
The structure sheaf on $$Spec R$$, valued in `CommRing`.
-/
def structure_sheaf : sheaf CommRing (Top.of (prime_spectrum R)) :=
{ presheaf := structure_presheaf_in_CommRing R,
  sheaf_condition :=
    -- We check the sheaf condition under `forget CommRing`.
    (sheaf_condition_equiv_sheaf_condition_comp _ _).symm
      (sheaf_condition_equiv_of_iso (structure_presheaf_comp_forget R).symm
        (structure_sheaf_in_Type R).sheaf_condition), }

-- TODO: we need to prove that the stalk at `P` is `localization.at_prime P.as_ideal`
