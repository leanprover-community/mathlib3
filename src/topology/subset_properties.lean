/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro

Properties of subsets of topological spaces: compact, clopen, irreducible,
connected, totally disconnected, totally separated.
-/

import topology.constructions

open set filter lattice classical
open_locale classical topological_space

universes u v
variables {Î± : Type u} {Î² : Type v} [topological_space Î±]

/- compact sets -/
section compact

/-- A set `s` is compact if for every filter `f` that contains `s`,
    every set of `f` also meets every neighborhood of some `a âˆˆ s`. -/
def compact (s : set Î±) := âˆ€f, f â‰  âŠ¥ â†’ f â‰¤ principal s â†’ âˆƒaâˆˆs, f âŠ“ ğ“ a â‰  âŠ¥

lemma compact_inter {s t : set Î±} (hs : compact s) (ht : is_closed t) : compact (s âˆ© t) :=
assume f hnf hstf,
let âŸ¨a, hsa, (ha : f âŠ“ ğ“ a â‰  âŠ¥)âŸ© := hs f hnf (le_trans hstf (le_principal_iff.2 (inter_subset_left _ _))) in
have âˆ€a, principal t âŠ“ ğ“ a â‰  âŠ¥ â†’ a âˆˆ t,
  by intro a; rw [inf_comm]; rw [is_closed_iff_nhds] at ht; exact ht a,
have a âˆˆ t,
  from this a $ neq_bot_of_le_neq_bot ha $ inf_le_inf (le_trans hstf (le_principal_iff.2 (inter_subset_right _ _))) (le_refl _),
âŸ¨a, âŸ¨hsa, thisâŸ©, haâŸ©

lemma compact_diff {s t : set Î±} (hs : compact s) (ht : is_open t) : compact (s \ t) :=
compact_inter hs (is_closed_compl_iff.mpr ht)

lemma compact_of_is_closed_subset {s t : set Î±}
  (hs : compact s) (ht : is_closed t) (h : t âŠ† s) : compact t :=
by convert â† compact_inter hs ht; exact inter_eq_self_of_subset_right h

lemma compact_adherence_nhdset {s t : set Î±} {f : filter Î±}
  (hs : compact s) (hfâ‚‚ : f â‰¤ principal s) (htâ‚ : is_open t) (htâ‚‚ : âˆ€aâˆˆs, ğ“ a âŠ“ f â‰  âŠ¥ â†’ a âˆˆ t) :
  t âˆˆ f :=
classical.by_cases mem_sets_of_neq_bot $
  assume : f âŠ“ principal (- t) â‰  âŠ¥,
  let âŸ¨a, ha, (hfa : f âŠ“ principal (-t) âŠ“ ğ“ a â‰  âŠ¥)âŸ© := hs _ this $ inf_le_left_of_le hfâ‚‚ in
  have a âˆˆ t,
    from htâ‚‚ a ha $ neq_bot_of_le_neq_bot hfa $ le_inf inf_le_right $ inf_le_left_of_le inf_le_left,
  have ğ“ a âŠ“ principal (-t) â‰  âŠ¥,
    from neq_bot_of_le_neq_bot hfa $ le_inf inf_le_right $ inf_le_left_of_le inf_le_right,
  have âˆ€sâˆˆ(ğ“ a âŠ“ principal (-t)).sets, s â‰  âˆ…,
    from forall_sets_neq_empty_iff_neq_bot.mpr this,
  have false,
    from this _ âŸ¨t, mem_nhds_sets htâ‚ â€¹a âˆˆ tâ€º, -t, subset.refl _, subset.refl _âŸ© (inter_compl_self _),
  by contradiction

lemma compact_iff_ultrafilter_le_nhds {s : set Î±} :
  compact s â†” (âˆ€f, is_ultrafilter f â†’ f â‰¤ principal s â†’ âˆƒaâˆˆs, f â‰¤ ğ“ a) :=
âŸ¨assume hs : compact s, assume f hf hfs,
  let âŸ¨a, ha, hâŸ© := hs _ hf.left hfs in
  âŸ¨a, ha, le_of_ultrafilter hf hâŸ©,

  assume hs : (âˆ€f, is_ultrafilter f â†’ f â‰¤ principal s â†’ âˆƒaâˆˆs, f â‰¤ ğ“ a),
  assume f hf hfs,
  let âŸ¨a, ha, (h : ultrafilter_of f â‰¤ ğ“ a)âŸ© :=
    hs (ultrafilter_of f) (ultrafilter_ultrafilter_of hf) (le_trans ultrafilter_of_le hfs) in
  have ultrafilter_of f âŠ“ ğ“ a â‰  âŠ¥,
    by simp only [inf_of_le_left, h]; exact (ultrafilter_ultrafilter_of hf).left,
  âŸ¨a, ha, neq_bot_of_le_neq_bot this (inf_le_inf ultrafilter_of_le (le_refl _))âŸ©âŸ©

lemma compact_elim_finite_subcover {s : set Î±} {c : set (set Î±)}
  (hs : compact s) (hcâ‚ : âˆ€tâˆˆc, is_open t) (hcâ‚‚ : s âŠ† â‹ƒâ‚€ c) : âˆƒc'âŠ†c, finite c' âˆ§ s âŠ† â‹ƒâ‚€ c' :=
classical.by_contradiction $ assume h,
  have h : âˆ€{c'}, c' âŠ† c â†’ finite c' â†’ Â¬ s âŠ† â‹ƒâ‚€ c',
    from assume c' hâ‚ hâ‚‚ hâ‚ƒ, h âŸ¨c', hâ‚, hâ‚‚, hâ‚ƒâŸ©,
  let
    f : filter Î± := (â¨…c':{c' : set (set Î±) // c' âŠ† c âˆ§ finite c'}, principal (s - â‹ƒâ‚€ c')),
    âŸ¨a, haâŸ© := @exists_mem_of_ne_empty Î± s
      (assume h', h (empty_subset _) finite_empty $ h'.symm â–¸ empty_subset _)
  in
  have f â‰  âŠ¥, from infi_neq_bot_of_directed âŸ¨aâŸ©
    (assume âŸ¨câ‚, hcâ‚, hc'â‚âŸ© âŸ¨câ‚‚, hcâ‚‚, hc'â‚‚âŸ©, âŸ¨âŸ¨câ‚ âˆª câ‚‚, union_subset hcâ‚ hcâ‚‚, finite_union hc'â‚ hc'â‚‚âŸ©,
      principal_mono.mpr $ diff_subset_diff_right $ sUnion_mono $ subset_union_left _ _,
      principal_mono.mpr $ diff_subset_diff_right $ sUnion_mono $ subset_union_right _ _âŸ©)
    (assume âŸ¨c', hc'â‚, hc'â‚‚âŸ©, show principal (s \ _) â‰  âŠ¥, by simp only [ne.def, principal_eq_bot_iff, diff_eq_empty]; exact h hc'â‚ hc'â‚‚),
  have f â‰¤ principal s, from infi_le_of_le âŸ¨âˆ…, empty_subset _, finite_emptyâŸ© $
    show principal (s \ â‹ƒâ‚€âˆ…) â‰¤ principal s, from le_principal_iff.2 (diff_subset _ _),
  let
    âŸ¨a, ha, (h : f âŠ“ ğ“ a â‰  âŠ¥)âŸ© := hs f â€¹f â‰  âŠ¥â€º this,
    âŸ¨t, htâ‚, (htâ‚‚ : a âˆˆ t)âŸ© := hcâ‚‚ ha
  in
  have f â‰¤ principal (-t),
    from infi_le_of_le âŸ¨{t}, by rwa singleton_subset_iff, finite_insert _ finite_emptyâŸ© $
      principal_mono.mpr $
        show s - â‹ƒâ‚€{t} âŠ† - t, begin rw sUnion_singleton; exact assume x âŸ¨_, hntâŸ©, hnt end,
  have is_closed (- t), from is_open_compl_iff.mp $ by rw lattice.neg_neg; exact hcâ‚ t htâ‚,
  have a âˆˆ - t, from is_closed_iff_nhds.mp this _ $ neq_bot_of_le_neq_bot h $
    le_inf inf_le_right (inf_le_left_of_le â€¹f â‰¤ principal (- t)â€º),
  this â€¹a âˆˆ tâ€º

lemma compact_elim_finite_subcover_image {s : set Î±} {b : set Î²} {c : Î² â†’ set Î±}
  (hs : compact s) (hcâ‚ : âˆ€iâˆˆb, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒiâˆˆb, c i) :
  âˆƒb'âŠ†b, finite b' âˆ§ s âŠ† â‹ƒiâˆˆb', c i :=
if h : b = âˆ… then âŸ¨âˆ…, empty_subset _, finite_empty, h â–¸ hcâ‚‚âŸ© else
let âŸ¨i, hiâŸ© := exists_mem_of_ne_empty h in
have hc'â‚ : âˆ€iâˆˆc '' b, is_open i, from assume i âŸ¨j, hj, hâŸ©, h â–¸ hcâ‚ _ hj,
have hc'â‚‚ : s âŠ† â‹ƒâ‚€ (c '' b), by rwa set.sUnion_image,
let âŸ¨d, hdâ‚, hdâ‚‚, hdâ‚ƒâŸ© := compact_elim_finite_subcover hs hc'â‚ hc'â‚‚ in
have âˆ€x : d, âˆƒi, i âˆˆ b âˆ§ c i = x, from assume âŸ¨x, hxâŸ©, hdâ‚ hx,
let âŸ¨f', hfâŸ© := axiom_of_choice this,
    f := Î»x:set Î±, (if h : x âˆˆ d then f' âŸ¨x, hâŸ© else i : Î²) in
have âˆ€(x : Î±) (i : set Î±), i âˆˆ d â†’ x âˆˆ i â†’ (âˆƒ (i : Î²), i âˆˆ f '' d âˆ§ x âˆˆ c i),
  from assume x i hid hxi, âŸ¨f i, mem_image_of_mem f hid,
    by simpa only [f, dif_pos hid, (hf âŸ¨_, hidâŸ©).2] using hxiâŸ©,
âŸ¨f '' d,
  assume i âŸ¨j, hj, hâŸ©,
  h â–¸ by simpa only [f, dif_pos hj] using (hf âŸ¨_, hjâŸ©).1,
  finite_image f hdâ‚‚,
  subset.trans hdâ‚ƒ $ by simpa only [subset_def, mem_sUnion, exists_imp_distrib, mem_Union, exists_prop, and_imp]âŸ©

section
-- this proof times out without this
local attribute [instance, priority 1000] classical.prop_decidable
lemma compact_of_finite_subcover {s : set Î±}
  (h : âˆ€c, (âˆ€tâˆˆc, is_open t) â†’ s âŠ† â‹ƒâ‚€ c â†’ âˆƒc'âŠ†c, finite c' âˆ§ s âŠ† â‹ƒâ‚€ c') : compact s :=
assume f hfn hfs, classical.by_contradiction $ assume : Â¬ (âˆƒxâˆˆs, f âŠ“ ğ“ x â‰  âŠ¥),
  have hf : âˆ€xâˆˆs, ğ“ x âŠ“ f = âŠ¥,
    by simpa only [not_exists, not_not, inf_comm],
  have Â¬ âˆƒxâˆˆs, âˆ€tâˆˆf.sets, x âˆˆ closure t,
    from assume âŸ¨x, hxs, hxâŸ©,
    have âˆ… âˆˆ ğ“ x âŠ“ f, by rw [empty_in_sets_eq_bot, hf x hxs],
    let âŸ¨tâ‚, htâ‚, tâ‚‚, htâ‚‚, htâŸ© := by rw [mem_inf_sets] at this; exact this in
    have âˆ… âˆˆ ğ“ x âŠ“ principal tâ‚‚,
      from (ğ“ x âŠ“ principal tâ‚‚).sets_of_superset (inter_mem_inf_sets htâ‚ (subset.refl tâ‚‚)) ht,
    have ğ“ x âŠ“ principal tâ‚‚ = âŠ¥,
      by rwa [empty_in_sets_eq_bot] at this,
    by simp only [closure_eq_nhds] at hx; exact hx tâ‚‚ htâ‚‚ this,
  have âˆ€xâˆˆs, âˆƒtâˆˆf.sets, x âˆ‰ closure t, by simpa only [not_exists, not_forall],
  let c := (Î»t, - closure t) '' f.sets, âŸ¨c', hcc', hcf, hsc'âŸ© := h c
    (assume t âŸ¨s, hs, hâŸ©, h â–¸ is_closed_closure) (by simpa only [subset_def, sUnion_image, mem_Union]) in
  let âŸ¨b, hbâŸ© := axiom_of_choice $
    show âˆ€s:c', âˆƒt, t âˆˆ f âˆ§ - closure t = s,
      from assume âŸ¨x, hxâŸ©, hcc' hx in
  have (â‹‚sâˆˆc', if h : s âˆˆ c' then b âŸ¨s, hâŸ© else univ) âˆˆ f,
    from Inter_mem_sets hcf $ assume t ht, by rw [dif_pos ht]; exact (hb âŸ¨t, htâŸ©).left,
  have s âˆ© (â‹‚sâˆˆc', if h : s âˆˆ c' then b âŸ¨s, hâŸ© else univ) âˆˆ f,
    from inter_mem_sets (le_principal_iff.1 hfs) this,
  have âˆ… âˆˆ f,
    from mem_sets_of_superset this $ assume x âŸ¨hxs, hxiâŸ©,
    let âŸ¨t, htc', hxtâŸ© := (show âˆƒt âˆˆ c', x âˆˆ t, from hsc' hxs) in
    have -closure (b âŸ¨t, htc'âŸ©) = t, from (hb _).right,
    have x âˆˆ - t,
      from this â–¸ (calc x âˆˆ b âŸ¨t, htc'âŸ© : by rw mem_bInter_iff at hxi; have h := hxi t htc'; rwa [dif_pos htc'] at h
        ... âŠ† closure (b âŸ¨t, htc'âŸ©) : subset_closure
        ... âŠ† - - closure (b âŸ¨t, htc'âŸ©) : by rw lattice.neg_neg; exact subset.refl _),
    show false, from this hxt,
  hfn $ by rwa [empty_in_sets_eq_bot] at this
end

lemma compact_iff_finite_subcover {s : set Î±} :
  compact s â†” (âˆ€c, (âˆ€tâˆˆc, is_open t) â†’ s âŠ† â‹ƒâ‚€ c â†’ âˆƒc'âŠ†c, finite c' âˆ§ s âŠ† â‹ƒâ‚€ c') :=
âŸ¨assume hc c, compact_elim_finite_subcover hc, compact_of_finite_subcoverâŸ©

lemma compact_empty : compact (âˆ… : set Î±) :=
assume f hnf hsf, not.elim hnf $
empty_in_sets_eq_bot.1 $ le_principal_iff.1 hsf

lemma compact_singleton {a : Î±} : compact ({a} : set Î±) :=
compact_of_finite_subcover $ assume c hcâ‚ hcâ‚‚,
  let âŸ¨i, hic, haiâŸ© := (show âˆƒi âˆˆ c, a âˆˆ i, from mem_sUnion.1 $ singleton_subset_iff.1 hcâ‚‚) in
  âŸ¨{i}, singleton_subset_iff.2 hic, finite_singleton _, by rwa [sUnion_singleton, singleton_subset_iff]âŸ©

lemma compact_bUnion_of_compact {s : set Î²} {f : Î² â†’ set Î±} (hs : finite s) :
  (âˆ€i âˆˆ s, compact (f i)) â†’ compact (â‹ƒi âˆˆ s, f i) :=
assume hf, compact_of_finite_subcover $ assume c c_open c_cover,
  have âˆ€i : subtype s, âˆƒc' âŠ† c, finite c' âˆ§ f i âŠ† â‹ƒâ‚€ c', from
    assume âŸ¨i, hiâŸ©, compact_elim_finite_subcover (hf i hi) c_open
      (calc f i âŠ† â‹ƒi âˆˆ s, f i : subset_bUnion_of_mem hi
            ... âŠ† â‹ƒâ‚€ c        : c_cover),
  let âŸ¨finite_subcovers, hâŸ© := axiom_of_choice this in
  let c' := â‹ƒi, finite_subcovers i in
  have c' âŠ† c, from Union_subset (Î»i, (h i).fst),
  have finite c', from @finite_Union _ _ hs.fintype _ (Î»i, (h i).snd.1),
  have (â‹ƒi âˆˆ s, f i) âŠ† â‹ƒâ‚€ c', from bUnion_subset $ Î»i hi, calc
    f i âŠ† â‹ƒâ‚€ finite_subcovers âŸ¨i,hiâŸ© : (h âŸ¨i,hiâŸ©).snd.2
    ... âŠ† â‹ƒâ‚€ c'                      : sUnion_mono (subset_Union _ _),
  âŸ¨c', â€¹c' âŠ† câ€º, â€¹finite c'â€º, thisâŸ©

lemma compact_Union_of_compact {f : Î² â†’ set Î±} [fintype Î²]
  (h : âˆ€i, compact (f i)) : compact (â‹ƒi, f i) :=
by rw â† bUnion_univ; exact compact_bUnion_of_compact finite_univ (Î» i _, h i)

lemma compact_of_finite {s : set Î±} (hs : finite s) : compact s :=
bUnion_of_singleton s â–¸ compact_bUnion_of_compact hs (Î» _ _, compact_singleton)

lemma compact_union_of_compact {s t : set Î±} (hs : compact s) (ht : compact t) : compact (s âˆª t) :=
by rw union_eq_Union; exact compact_Union_of_compact (Î» b, by cases b; assumption)

section tube_lemma

variables [topological_space Î²]

def nhds_contain_boxes (s : set Î±) (t : set Î²) : Prop :=
âˆ€ (n : set (Î± Ã— Î²)) (hn : is_open n) (hp : set.prod s t âŠ† n),
âˆƒ (u : set Î±) (v : set Î²), is_open u âˆ§ is_open v âˆ§ s âŠ† u âˆ§ t âŠ† v âˆ§ set.prod u v âŠ† n

lemma nhds_contain_boxes.symm {s : set Î±} {t : set Î²} :
  nhds_contain_boxes s t â†’ nhds_contain_boxes t s :=
assume H n hn hp,
  let âŸ¨u, v, uo, vo, su, tv, pâŸ© :=
    H (prod.swap â»Â¹' n)
      (continuous_swap n hn)
      (by rwa [â†image_subset_iff, prod.swap, image_swap_prod]) in
  âŸ¨v, u, vo, uo, tv, su,
    by rwa [â†image_subset_iff, prod.swap, image_swap_prod] at pâŸ©

lemma nhds_contain_boxes.comm {s : set Î±} {t : set Î²} :
  nhds_contain_boxes s t â†” nhds_contain_boxes t s :=
iff.intro nhds_contain_boxes.symm nhds_contain_boxes.symm

lemma nhds_contain_boxes_of_singleton {x : Î±} {y : Î²} :
  nhds_contain_boxes ({x} : set Î±) ({y} : set Î²) :=
assume n hn hp,
  let âŸ¨u, v, uo, vo, xu, yv, hp'âŸ© :=
    is_open_prod_iff.mp hn x y (hp $ by simp) in
  âŸ¨u, v, uo, vo, by simpa, by simpa, hp'âŸ©

lemma nhds_contain_boxes_of_compact {s : set Î±} (hs : compact s) (t : set Î²)
  (H : âˆ€ x âˆˆ s, nhds_contain_boxes ({x} : set Î±) t) : nhds_contain_boxes s t :=
assume n hn hp,
have âˆ€x : subtype s, âˆƒuv : set Î± Ã— set Î²,
     is_open uv.1 âˆ§ is_open uv.2 âˆ§ {â†‘x} âŠ† uv.1 âˆ§ t âŠ† uv.2 âˆ§ set.prod uv.1 uv.2 âŠ† n,
  from assume âŸ¨x, hxâŸ©,
    have set.prod {x} t âŠ† n, from
      subset.trans (prod_mono (by simpa) (subset.refl _)) hp,
    let âŸ¨ux,vx,H1âŸ© := H x hx n hn this in âŸ¨âŸ¨ux,vxâŸ©,H1âŸ©,
let âŸ¨uvs, hâŸ© := classical.axiom_of_choice this in
have us_cover : s âŠ† â‹ƒi, (uvs i).1, from
  assume x hx, set.subset_Union _ âŸ¨x,hxâŸ© (by simpa using (h âŸ¨x,hxâŸ©).2.2.1),
let âŸ¨s0, _, s0_fin, s0_coverâŸ© :=
  compact_elim_finite_subcover_image hs (Î»i _, (h i).1) $
    by rw bUnion_univ; exact us_cover in
let u := â‹ƒ(i âˆˆ s0), (uvs i).1 in
let v := â‹‚(i âˆˆ s0), (uvs i).2 in
have is_open u, from is_open_bUnion (Î»i _, (h i).1),
have is_open v, from is_open_bInter s0_fin (Î»i _, (h i).2.1),
have t âŠ† v, from subset_bInter (Î»i _, (h i).2.2.2.1),
have set.prod u v âŠ† n, from assume âŸ¨x',y'âŸ© âŸ¨hx',hy'âŸ©,
  have âˆƒi âˆˆ s0, x' âˆˆ (uvs i).1, by simpa using hx',
  let âŸ¨i,is0,hiâŸ© := this in
  (h i).2.2.2.2 âŸ¨hi, (bInter_subset_of_mem is0 : v âŠ† (uvs i).2) hy'âŸ©,
âŸ¨u, v, â€¹is_open uâ€º, â€¹is_open vâ€º, s0_cover, â€¹t âŠ† vâ€º, â€¹set.prod u v âŠ† nâ€ºâŸ©

lemma generalized_tube_lemma {s : set Î±} (hs : compact s) {t : set Î²} (ht : compact t)
  {n : set (Î± Ã— Î²)} (hn : is_open n) (hp : set.prod s t âŠ† n) :
  âˆƒ (u : set Î±) (v : set Î²), is_open u âˆ§ is_open v âˆ§ s âŠ† u âˆ§ t âŠ† v âˆ§ set.prod u v âŠ† n :=
have _, from
  nhds_contain_boxes_of_compact hs t $ assume x _, nhds_contain_boxes.symm $
    nhds_contain_boxes_of_compact ht {x} $ assume y _, nhds_contain_boxes_of_singleton,
this n hn hp

end tube_lemma

/-- Type class for compact spaces. Separation is sometimes included in the definition, especially
in the French literature, but we do not include it here. -/
class compact_space (Î± : Type*) [topological_space Î±] : Prop :=
(compact_univ : compact (univ : set Î±))

lemma compact_univ [h : compact_space Î±] : compact (univ : set Î±) := h.compact_univ

lemma compact_of_closed [compact_space Î±] {s : set Î±} (h : is_closed s) :
  compact s :=
compact_of_is_closed_subset compact_univ h (subset_univ _)

variables [topological_space Î²]

lemma compact_image {s : set Î±} {f : Î± â†’ Î²} (hs : compact s) (hf : continuous f) :
  compact (f '' s) :=
compact_of_finite_subcover $ assume c hco hcs,
  have hdo : âˆ€tâˆˆc, is_open (f â»Â¹' t), from assume t' ht, hf _ $ hco _ ht,
  have hds : s âŠ† â‹ƒiâˆˆc, f â»Â¹' i,
    by simpa [subset_def, -mem_image] using hcs,
  let âŸ¨d', hcd', hfd', hd'âŸ© := compact_elim_finite_subcover_image hs hdo hds in
  âŸ¨d', hcd', hfd', by simpa [subset_def, -mem_image, image_subset_iff] using hd'âŸ©

lemma compact_range [compact_space Î±] {f : Î± â†’ Î²} (hf : continuous f) :
  compact (range f) :=
by rw â† image_univ; exact compact_image compact_univ hf

lemma compact_iff_compact_image_of_embedding {s : set Î±} {f : Î± â†’ Î²} (hf : embedding f) :
  compact s â†” compact (f '' s) :=
iff.intro (assume h, compact_image h hf.continuous) $ assume h, begin
  rw compact_iff_ultrafilter_le_nhds at âŠ¢ h,
  intros u hu us',
  let u' : filter Î² := map f u,
  have : u' â‰¤ principal (f '' s), begin
    rw [map_le_iff_le_comap, comap_principal], convert us',
    exact preimage_image_eq _ hf.inj
  end,
  rcases h u' (ultrafilter_map hu) this with âŸ¨_, âŸ¨a, ha, âŸ¨âŸ©âŸ©, _âŸ©,
  refine âŸ¨a, ha, _âŸ©,
  rwa [hf.induced, nhds_induced, â†map_le_iff_le_comap]
end

lemma compact_iff_compact_in_subtype {p : Î± â†’ Prop} {s : set {a // p a}} :
  compact s â†” compact (subtype.val '' s) :=
compact_iff_compact_image_of_embedding embedding_subtype_val

lemma compact_iff_compact_univ {s : set Î±} : compact s â†” compact (univ : set (subtype s)) :=
by rw [compact_iff_compact_in_subtype, image_univ, subtype.val_range]; refl

lemma compact_iff_compact_space {s : set Î±} : compact s â†” compact_space s :=
compact_iff_compact_univ.trans âŸ¨Î» h, âŸ¨hâŸ©, @compact_space.compact_univ _ _âŸ©

lemma compact_prod (s : set Î±) (t : set Î²) (ha : compact s) (hb : compact t) : compact (set.prod s t) :=
begin
  rw compact_iff_ultrafilter_le_nhds at ha hb âŠ¢,
  intros f hf hfs,
  rw le_principal_iff at hfs,
  rcases ha (map prod.fst f) (ultrafilter_map hf)
    (le_principal_iff.2 (mem_map_sets_iff.2
      âŸ¨_, hfs, image_subset_iff.2 (Î» s h, h.1)âŸ©)) with âŸ¨a, sa, haâŸ©,
  rcases hb (map prod.snd f) (ultrafilter_map hf)
    (le_principal_iff.2 (mem_map_sets_iff.2
      âŸ¨_, hfs, image_subset_iff.2 (Î» s h, h.2)âŸ©)) with âŸ¨b, tb, hbâŸ©,
  rw map_le_iff_le_comap at ha hb,
  refine âŸ¨âŸ¨a, bâŸ©, âŸ¨sa, tbâŸ©, _âŸ©,
  rw nhds_prod_eq, exact le_inf ha hb
end

instance [compact_space Î±] [compact_space Î²] : compact_space (Î± Ã— Î²) :=
âŸ¨begin
  have A : compact (set.prod (univ : set Î±) (univ : set Î²)) :=
    compact_prod univ univ compact_univ compact_univ,
  have : set.prod (univ : set Î±) (univ : set Î²) = (univ : set (Î± Ã— Î²)) := by simp,
  rwa this at A,
endâŸ©

instance [compact_space Î±] [compact_space Î²] : compact_space (Î± âŠ• Î²) :=
âŸ¨begin
  have A : compact (@sum.inl Î± Î² '' univ) := compact_image compact_univ continuous_inl,
  have B : compact (@sum.inr Î± Î² '' univ) := compact_image compact_univ continuous_inr,
  have C := compact_union_of_compact A B,
  have : (@sum.inl Î± Î² '' univ) âˆª (@sum.inr Î± Î² '' univ) = univ := by ext; cases x; simp,
  rwa this at C,
endâŸ©

section tychonoff
variables {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [âˆ€i, topological_space (Ï€ i)]

/-- Tychonoff's theorem -/
lemma compact_pi_infinite {s : Î i:Î¹, set (Ï€ i)} :
  (âˆ€i, compact (s i)) â†’ compact {x : Î i:Î¹, Ï€ i | âˆ€i, x i âˆˆ s i} :=
begin
  simp [compact_iff_ultrafilter_le_nhds, nhds_pi],
  exact assume h f hf hfs,
    let p : Î i:Î¹, filter (Ï€ i) := Î»i, map (Î»x:Î i:Î¹, Ï€ i, x i) f in
    have âˆ€i:Î¹, âˆƒa, aâˆˆs i âˆ§ p i â‰¤ ğ“ a,
      from assume i, h i (p i) (ultrafilter_map hf) $
      show (Î»x:Î i:Î¹, Ï€ i, x i) â»Â¹' s i âˆˆ f.sets,
        from mem_sets_of_superset hfs $ assume x (hx : âˆ€i, x i âˆˆ s i), hx i,
    let âŸ¨a, haâŸ© := classical.axiom_of_choice this in
    âŸ¨a, assume i, (ha i).left, assume i, map_le_iff_le_comap.mp $ (ha i).rightâŸ©
end

instance pi.compact [âˆ€i:Î¹, compact_space (Ï€ i)] : compact_space (Î i, Ï€ i) :=
âŸ¨begin
  have A : compact {x : Î i:Î¹, Ï€ i | âˆ€i, x i âˆˆ (univ : set (Ï€ i))} :=
    compact_pi_infinite (Î»i, compact_univ),
  have : {x : Î i:Î¹, Ï€ i | âˆ€i, x i âˆˆ (univ : set (Ï€ i))} = univ := by ext; simp,
  rwa this at A,
endâŸ©

end tychonoff

instance quot.compact_space {r : Î± â†’ Î± â†’ Prop} [compact_space Î±] :
  compact_space (quot r) :=
âŸ¨begin
   have : quot.mk r '' univ = univ,
     by rw [image_univ, range_iff_surjective]; exact quot.exists_rep,
   rw â†this,
   exact compact_image compact_univ continuous_quot_mk
 endâŸ©

instance quotient.compact_space {s : setoid Î±} [compact_space Î±] :
  compact_space (quotient s) :=
quot.compact_space

/-- There are various definitions of "locally compact space" in the literature, which agree for
Hausdorff spaces but not in general. This one is the precise condition on X needed for the
evaluation `map C(X, Y) Ã— X â†’ Y` to be continuous for all `Y` when `C(X, Y)` is given the
compact-open topology. -/
class locally_compact_space (Î± : Type*) [topological_space Î±] : Prop :=
(local_compact_nhds : âˆ€ (x : Î±) (n âˆˆ ğ“ x), âˆƒ s âˆˆ ğ“ x, s âŠ† n âˆ§ compact s)

end compact

section clopen

def is_clopen (s : set Î±) : Prop :=
is_open s âˆ§ is_closed s

theorem is_clopen_union {s t : set Î±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s âˆª t) :=
âŸ¨is_open_union hs.1 ht.1, is_closed_union hs.2 ht.2âŸ©

theorem is_clopen_inter {s t : set Î±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s âˆ© t) :=
âŸ¨is_open_inter hs.1 ht.1, is_closed_inter hs.2 ht.2âŸ©

@[simp] theorem is_clopen_empty : is_clopen (âˆ… : set Î±) :=
âŸ¨is_open_empty, is_closed_emptyâŸ©

@[simp] theorem is_clopen_univ : is_clopen (univ : set Î±) :=
âŸ¨is_open_univ, is_closed_univâŸ©

theorem is_clopen_compl {s : set Î±} (hs : is_clopen s) : is_clopen (-s) :=
âŸ¨hs.2, is_closed_compl_iff.2 hs.1âŸ©

@[simp] theorem is_clopen_compl_iff {s : set Î±} : is_clopen (-s) â†” is_clopen s :=
âŸ¨Î» h, compl_compl s â–¸ is_clopen_compl h, is_clopen_complâŸ©

theorem is_clopen_diff {s t : set Î±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s-t) :=
is_clopen_inter hs (is_clopen_compl ht)

end clopen

section irreducible

/-- A irreducible set is one where there is no non-trivial pair of disjoint opens. -/
def is_irreducible (s : set Î±) : Prop :=
âˆ€ (u v : set Î±), is_open u â†’ is_open v â†’
  (âˆƒ x, x âˆˆ s âˆ© u) â†’ (âˆƒ x, x âˆˆ s âˆ© v) â†’ âˆƒ x, x âˆˆ s âˆ© (u âˆ© v)

theorem is_irreducible_empty : is_irreducible (âˆ… : set Î±) :=
Î» _ _ _ _ _ âŸ¨x, h1, h2âŸ©, h1.elim

theorem is_irreducible_singleton {x} : is_irreducible ({x} : set Î±) :=
Î» u v _ _ âŸ¨y, h1, h2âŸ© âŸ¨z, h3, h4âŸ©, by rw mem_singleton_iff at h1 h3;
substs y z; exact âŸ¨x, or.inl rfl, h2, h4âŸ©

theorem is_irreducible_closure {s : set Î±} (H : is_irreducible s) :
  is_irreducible (closure s) :=
Î» u v hu hv âŸ¨y, hycs, hyuâŸ© âŸ¨z, hzcs, hzvâŸ©,
let âŸ¨p, hpu, hpsâŸ© := exists_mem_of_ne_empty (mem_closure_iff.1 hycs u hu hyu) in
let âŸ¨q, hqv, hqsâŸ© := exists_mem_of_ne_empty (mem_closure_iff.1 hzcs v hv hzv) in
let âŸ¨r, hrs, hruvâŸ© := H u v hu hv âŸ¨p, hps, hpuâŸ© âŸ¨q, hqs, hqvâŸ© in
âŸ¨r, subset_closure hrs, hruvâŸ©

theorem exists_irreducible (s : set Î±) (H : is_irreducible s) :
  âˆƒ t : set Î±, is_irreducible t âˆ§ s âŠ† t âˆ§ âˆ€ u, is_irreducible u â†’ t âŠ† u â†’ u = t :=
let âŸ¨m, hm, hsm, hmmâŸ© := zorn.zorn_subsetâ‚€ { t : set Î± | is_irreducible t }
  (Î» c hc hcc hcn, let âŸ¨t, htcâŸ© := exists_mem_of_ne_empty hcn in
    âŸ¨â‹ƒâ‚€ c, Î» u v hu hv âŸ¨y, hy, hyuâŸ© âŸ¨z, hz, hzvâŸ©,
      let âŸ¨p, hpc, hypâŸ© := mem_sUnion.1 hy,
          âŸ¨q, hqc, hzqâŸ© := mem_sUnion.1 hz in
      or.cases_on (zorn.chain.total hcc hpc hqc)
        (assume hpq : p âŠ† q, let âŸ¨x, hxp, hxuvâŸ© := hc hqc u v hu hv
            âŸ¨y, hpq hyp, hyuâŸ© âŸ¨z, hzq, hzvâŸ© in
          âŸ¨x, mem_sUnion_of_mem hxp hqc, hxuvâŸ©)
        (assume hqp : q âŠ† p, let âŸ¨x, hxp, hxuvâŸ© := hc hpc u v hu hv
            âŸ¨y, hyp, hyuâŸ© âŸ¨z, hqp hzq, hzvâŸ© in
          âŸ¨x, mem_sUnion_of_mem hxp hpc, hxuvâŸ©),
    Î» x hxc, set.subset_sUnion_of_mem hxcâŸ©) s H in
âŸ¨m, hm, hsm, Î» u hu hmu, hmm _ hu hmuâŸ©

def irreducible_component (x : Î±) : set Î± :=
classical.some (exists_irreducible {x} is_irreducible_singleton)

theorem is_irreducible_irreducible_component {x : Î±} : is_irreducible (irreducible_component x) :=
(classical.some_spec (exists_irreducible {x} is_irreducible_singleton)).1

theorem mem_irreducible_component {x : Î±} : x âˆˆ irreducible_component x :=
singleton_subset_iff.1
  (classical.some_spec (exists_irreducible {x} is_irreducible_singleton)).2.1

theorem eq_irreducible_component {x : Î±} :
  âˆ€ {s : set Î±}, is_irreducible s â†’ irreducible_component x âŠ† s â†’ s = irreducible_component x :=
(classical.some_spec (exists_irreducible {x} is_irreducible_singleton)).2.2

theorem is_closed_irreducible_component {x : Î±} :
  is_closed (irreducible_component x) :=
closure_eq_iff_is_closed.1 $ eq_irreducible_component
  (is_irreducible_closure is_irreducible_irreducible_component)
  subset_closure

/-- A irreducible space is one where there is no non-trivial pair of disjoint opens. -/
class irreducible_space (Î± : Type u) [topological_space Î±] : Prop :=
(is_irreducible_univ : is_irreducible (univ : set Î±))

theorem irreducible_exists_mem_inter [irreducible_space Î±] {s t : set Î±} :
  is_open s â†’ is_open t â†’ (âˆƒ x, x âˆˆ s) â†’ (âˆƒ x, x âˆˆ t) â†’ âˆƒ x, x âˆˆ s âˆ© t :=
by simpa only [univ_inter, univ_subset_iff] using
  @irreducible_space.is_irreducible_univ Î± _ _ s t

end irreducible

section connected

/-- A connected set is one where there is no non-trivial open partition. -/
def is_connected (s : set Î±) : Prop :=
âˆ€ (u v : set Î±), is_open u â†’ is_open v â†’ s âŠ† u âˆª v â†’
  (âˆƒ x, x âˆˆ s âˆ© u) â†’ (âˆƒ x, x âˆˆ s âˆ© v) â†’ âˆƒ x, x âˆˆ s âˆ© (u âˆ© v)

theorem is_connected_of_is_irreducible {s : set Î±} (H : is_irreducible s) : is_connected s :=
Î» _ _ hu hv _, H _ _ hu hv

theorem is_connected_empty : is_connected (âˆ… : set Î±) :=
is_connected_of_is_irreducible is_irreducible_empty

theorem is_connected_singleton {x} : is_connected ({x} : set Î±) :=
is_connected_of_is_irreducible is_irreducible_singleton

theorem is_connected_sUnion (x : Î±) (c : set (set Î±)) (H1 : âˆ€ s âˆˆ c, x âˆˆ s)
  (H2 : âˆ€ s âˆˆ c, is_connected s) : is_connected (â‹ƒâ‚€ c) :=
begin
  rintro u v hu hv hUcuv âŸ¨y, hyUc, hyuâŸ© âŸ¨z, hzUc, hzvâŸ©,
  cases classical.em (c = âˆ…) with hc hc,
  { rw [hc, sUnion_empty] at hyUc, exact hyUc.elim },
  cases ne_empty_iff_exists_mem.1 hc with s hs,
  cases hUcuv (mem_sUnion_of_mem (H1 s hs) hs) with hxu hxv,
  { rcases hzUc with âŸ¨t, htc, hztâŸ©,
    specialize H2 t htc u v hu hv (subset.trans (subset_sUnion_of_mem htc) hUcuv),
    cases H2 âŸ¨x, H1 t htc, hxuâŸ© âŸ¨z, hzt, hzvâŸ© with r hr,
    exact âŸ¨r, mem_sUnion_of_mem hr.1 htc, hr.2âŸ© },
  { rcases hyUc with âŸ¨t, htc, hytâŸ©,
    specialize H2 t htc u v hu hv (subset.trans (subset_sUnion_of_mem htc) hUcuv),
    cases H2 âŸ¨y, hyt, hyuâŸ© âŸ¨x, H1 t htc, hxvâŸ© with r hr,
    exact âŸ¨r, mem_sUnion_of_mem hr.1 htc, hr.2âŸ© }
end

theorem is_connected_union (x : Î±) {s t : set Î±} (H1 : x âˆˆ s) (H2 : x âˆˆ t)
  (H3 : is_connected s) (H4 : is_connected t) : is_connected (s âˆª t) :=
have _ := is_connected_sUnion x {t,s}
  (by rintro r (rfl | rfl | h); [exact H1, exact H2, exact h.elim])
  (by rintro r (rfl | rfl | h); [exact H3, exact H4, exact h.elim]),
have h2 : â‹ƒâ‚€ {t, s} = s âˆª t, from (sUnion_insert _ _).trans (by rw sUnion_singleton),
by rwa h2 at this

theorem is_connected_closure {s : set Î±} (H : is_connected s) :
  is_connected (closure s) :=
Î» u v hu hv hcsuv âŸ¨y, hycs, hyuâŸ© âŸ¨z, hzcs, hzvâŸ©,
let âŸ¨p, hpu, hpsâŸ© := exists_mem_of_ne_empty (mem_closure_iff.1 hycs u hu hyu) in
let âŸ¨q, hqv, hqsâŸ© := exists_mem_of_ne_empty (mem_closure_iff.1 hzcs v hv hzv) in
let âŸ¨r, hrs, hruvâŸ© := H u v hu hv (subset.trans subset_closure hcsuv) âŸ¨p, hps, hpuâŸ© âŸ¨q, hqs, hqvâŸ© in
âŸ¨r, subset_closure hrs, hruvâŸ©

def connected_component (x : Î±) : set Î± :=
â‹ƒâ‚€ { s : set Î± | is_connected s âˆ§ x âˆˆ s }

theorem is_connected_connected_component {x : Î±} : is_connected (connected_component x) :=
is_connected_sUnion x _ (Î» _, and.right) (Î» _, and.left)

theorem mem_connected_component {x : Î±} : x âˆˆ connected_component x :=
mem_sUnion_of_mem (mem_singleton x) âŸ¨is_connected_singleton, mem_singleton xâŸ©

theorem subset_connected_component {x : Î±} {s : set Î±} (H1 : is_connected s) (H2 : x âˆˆ s) :
  s âŠ† connected_component x :=
Î» z hz, mem_sUnion_of_mem hz âŸ¨H1, H2âŸ©

theorem is_closed_connected_component {x : Î±} :
  is_closed (connected_component x) :=
closure_eq_iff_is_closed.1 $ subset.antisymm
  (subset_connected_component
    (is_connected_closure is_connected_connected_component)
    (subset_closure mem_connected_component))
  subset_closure

theorem irreducible_component_subset_connected_component {x : Î±} :
  irreducible_component x âŠ† connected_component x :=
subset_connected_component
  (is_connected_of_is_irreducible is_irreducible_irreducible_component)
  mem_irreducible_component

/-- A connected space is one where there is no non-trivial open partition. -/
class connected_space (Î± : Type u) [topological_space Î±] : Prop :=
(is_connected_univ : is_connected (univ : set Î±))

@[priority 100] -- see Note [lower instance priority]
instance irreducible_space.connected_space (Î± : Type u) [topological_space Î±]
  [irreducible_space Î±] : connected_space Î± :=
âŸ¨is_connected_of_is_irreducible $ irreducible_space.is_irreducible_univ Î±âŸ©

theorem exists_mem_inter [connected_space Î±] {s t : set Î±} :
  is_open s â†’ is_open t â†’ s âˆª t = univ â†’
    (âˆƒ x, x âˆˆ s) â†’ (âˆƒ x, x âˆˆ t) â†’ âˆƒ x, x âˆˆ s âˆ© t :=
by simpa only [univ_inter, univ_subset_iff] using
  @connected_space.is_connected_univ Î± _ _ s t

theorem is_clopen_iff [connected_space Î±] {s : set Î±} : is_clopen s â†” s = âˆ… âˆ¨ s = univ :=
âŸ¨Î» hs, classical.by_contradiction $ Î» h,
  have h1 : s â‰  âˆ… âˆ§ -s â‰  âˆ…, from âŸ¨mt or.inl h,
    mt (Î» h2, or.inr $ (by rw [â† compl_compl s, h2, compl_empty] : s = univ)) hâŸ©,
  let âŸ¨_, h2, h3âŸ© := exists_mem_inter hs.1 hs.2 (union_compl_self s)
    (ne_empty_iff_exists_mem.1 h1.1) (ne_empty_iff_exists_mem.1 h1.2) in
  h3 h2,
by rintro (rfl | rfl); [exact is_clopen_empty, exact is_clopen_univ]âŸ©

end connected

section totally_disconnected

def is_totally_disconnected (s : set Î±) : Prop :=
âˆ€ t, t âŠ† s â†’ is_connected t â†’ subsingleton t

theorem is_totally_disconnected_empty : is_totally_disconnected (âˆ… : set Î±) :=
Î» t ht _, âŸ¨Î» âŸ¨_, hâŸ©, (ht h).elimâŸ©

theorem is_totally_disconnected_singleton {x} : is_totally_disconnected ({x} : set Î±) :=
Î» t ht _, âŸ¨Î» âŸ¨p, hpâŸ© âŸ¨q, hqâŸ©, subtype.eq $ show p = q,
from (eq_of_mem_singleton (ht hp)).symm â–¸ (eq_of_mem_singleton (ht hq)).symmâŸ©

class totally_disconnected_space (Î± : Type u) [topological_space Î±] : Prop :=
(is_totally_disconnected_univ : is_totally_disconnected (univ : set Î±))

end totally_disconnected

section totally_separated

def is_totally_separated (s : set Î±) : Prop :=
âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, x â‰  y â†’ âˆƒ u v : set Î±, is_open u âˆ§ is_open v âˆ§
  x âˆˆ u âˆ§ y âˆˆ v âˆ§ s âŠ† u âˆª v âˆ§ u âˆ© v = âˆ…

theorem is_totally_separated_empty : is_totally_separated (âˆ… : set Î±) :=
Î» x, false.elim

theorem is_totally_separated_singleton {x} : is_totally_separated ({x} : set Î±) :=
Î» p hp q hq hpq, (hpq $ (eq_of_mem_singleton hp).symm â–¸ (eq_of_mem_singleton hq).symm).elim

theorem is_totally_disconnected_of_is_totally_separated {s : set Î±}
  (H : is_totally_separated s) : is_totally_disconnected s :=
Î» t hts ht, âŸ¨Î» âŸ¨x, hxtâŸ© âŸ¨y, hytâŸ©, subtype.eq $ classical.by_contradiction $
assume hxy : x â‰  y, let âŸ¨u, v, hu, hv, hxu, hyv, hsuv, huvâŸ© := H x (hts hxt) y (hts hyt) hxy in
let âŸ¨r, hrt, hruvâŸ© := ht u v hu hv (subset.trans hts hsuv) âŸ¨x, hxt, hxuâŸ© âŸ¨y, hyt, hyvâŸ© in
((ext_iff _ _).1 huv r).1 hruvâŸ©

class totally_separated_space (Î± : Type u) [topological_space Î±] : Prop :=
(is_totally_separated_univ : is_totally_separated (univ : set Î±))

@[priority 100] -- see Note [lower instance priority]
instance totally_separated_space.totally_disconnected_space (Î± : Type u) [topological_space Î±]
  [totally_separated_space Î±] : totally_disconnected_space Î± :=
âŸ¨is_totally_disconnected_of_is_totally_separated $ totally_separated_space.is_totally_separated_univ Î±âŸ©

end totally_separated
