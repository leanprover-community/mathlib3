/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Yury Kudryashov
-/

import topology.continuous_on

/-!
# Properties of subsets of topological spaces

## Main definitions

`compact`, `is_clopen`, `is_irreducible`, `is_connected`, `is_totally_disconnected`, `is_totally_separated`

TODO: write better docs

## On the definition of irreducible and connected sets/spaces

In informal mathematics, irreducible and connected spaces are assumed to be nonempty.
We formalise the predicate without that assumption
as `is_preirreducible` and `is_preconnected` respectively.
In other words, the only difference is whether the empty space
counts as irreducible and/or connected.
There are good reasons to consider the empty space to be ‚Äútoo simple to be simple‚Äù
See also https://ncatlab.org/nlab/show/too+simple+to+be+simple,
and in particular
https://ncatlab.org/nlab/show/too+simple+to+be+simple#relationship_to_biased_definitions.
-/

open set filter lattice classical
open_locale classical topological_space

universes u v
variables {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±]

/- compact sets -/
section compact

/-- A set `s` is compact if for every filter `f` that contains `s`,
    every set of `f` also meets every neighborhood of some `a ‚àà s`. -/
def compact (s : set Œ±) := ‚àÄf, f ‚â† ‚ä• ‚Üí f ‚â§ principal s ‚Üí ‚àÉa‚ààs, f ‚äì ùìù a ‚â† ‚ä•

lemma compact.inter_right {s t : set Œ±} (hs : compact s) (ht : is_closed t) : compact (s ‚à© t) :=
assume f hnf hstf,
let ‚ü®a, hsa, (ha : f ‚äì ùìù a ‚â† ‚ä•)‚ü© := hs f hnf (le_trans hstf (le_principal_iff.2 (inter_subset_left _ _))) in
have a ‚àà t,
  from ht.mem_of_nhds_within_ne_bot $ ne_bot_of_le_ne_bot (by { rw inf_comm at ha, exact ha }) $
    inf_le_inf (le_refl _) (le_trans hstf (le_principal_iff.2 (inter_subset_right _ _))),
‚ü®a, ‚ü®hsa, this‚ü©, ha‚ü©

lemma compact.inter_left {s t : set Œ±} (ht : compact t) (hs : is_closed s) : compact (s ‚à© t) :=
inter_comm t s ‚ñ∏ ht.inter_right hs

lemma compact_diff {s t : set Œ±} (hs : compact s) (ht : is_open t) : compact (s \ t) :=
hs.inter_right (is_closed_compl_iff.mpr ht)

lemma compact_of_is_closed_subset {s t : set Œ±}
  (hs : compact s) (ht : is_closed t) (h : t ‚äÜ s) : compact t :=
inter_eq_self_of_subset_right h ‚ñ∏ hs.inter_right ht

lemma compact.adherence_nhdset {s t : set Œ±} {f : filter Œ±}
  (hs : compact s) (hf‚ÇÇ : f ‚â§ principal s) (ht‚ÇÅ : is_open t) (ht‚ÇÇ : ‚àÄa‚ààs, ùìù a ‚äì f ‚â† ‚ä• ‚Üí a ‚àà t) :
  t ‚àà f :=
classical.by_cases mem_sets_of_eq_bot $
  assume : f ‚äì principal (- t) ‚â† ‚ä•,
  let ‚ü®a, ha, (hfa : f ‚äì principal (-t) ‚äì ùìù a ‚â† ‚ä•)‚ü© := hs _ this $ inf_le_left_of_le hf‚ÇÇ in
  have a ‚àà t,
    from ht‚ÇÇ a ha $ ne_bot_of_le_ne_bot hfa $ le_inf inf_le_right $ inf_le_left_of_le inf_le_left,
  have (-t) ‚à© t ‚àà nhds_within a (-t),
    from inter_mem_nhds_within _ (mem_nhds_sets ht‚ÇÅ this),
  have A : nhds_within a (-t) = ‚ä•,
    from empty_in_sets_eq_bot.1 $ compl_inter_self t ‚ñ∏ this,
  have nhds_within a (-t) ‚â† ‚ä•,
    from ne_bot_of_le_ne_bot hfa $ le_inf inf_le_right $ inf_le_left_of_le inf_le_right,
  absurd A this

lemma compact_iff_ultrafilter_le_nhds {s : set Œ±} :
  compact s ‚Üî (‚àÄf, is_ultrafilter f ‚Üí f ‚â§ principal s ‚Üí ‚àÉa‚ààs, f ‚â§ ùìù a) :=
‚ü®assume hs : compact s, assume f hf hfs,
  let ‚ü®a, ha, h‚ü© := hs _ hf.left hfs in
  ‚ü®a, ha, le_of_ultrafilter hf h‚ü©,

  assume hs : (‚àÄf, is_ultrafilter f ‚Üí f ‚â§ principal s ‚Üí ‚àÉa‚ààs, f ‚â§ ùìù a),
  assume f hf hfs,
  let ‚ü®a, ha, (h : ultrafilter_of f ‚â§ ùìù a)‚ü© :=
    hs (ultrafilter_of f) (ultrafilter_ultrafilter_of hf) (le_trans ultrafilter_of_le hfs) in
  have ultrafilter_of f ‚äì ùìù a ‚â† ‚ä•,
    by simp only [inf_of_le_left, h]; exact (ultrafilter_ultrafilter_of hf).left,
  ‚ü®a, ha, ne_bot_of_le_ne_bot this (inf_le_inf ultrafilter_of_le (le_refl _))‚ü©‚ü©

/-- For every open cover of a compact set, there exists a finite subcover. -/
lemma compact.elim_finite_subcover {s : set Œ±} {Œπ : Type v} (hs : compact s)
  (U : Œπ ‚Üí set Œ±) (hUo : ‚àÄi, is_open (U i)) (hsU : s ‚äÜ ‚ãÉ i, U i) :
  ‚àÉ t : finset Œπ, s ‚äÜ ‚ãÉ i ‚àà t, U i :=
classical.by_contradiction $ assume h,
  have h : ‚àÄ t : finset Œπ, ¬¨ s ‚äÜ ‚ãÉ i ‚àà t, U i,
    from assume t ht, h ‚ü®t, ht‚ü©,
  let
    f : filter Œ± := (‚®Öt:finset Œπ, principal (s - ‚ãÉ i ‚àà t, U i)),
    ‚ü®a, ha‚ü© := (@ne_empty_iff_nonempty Œ± s).1 (assume h', h ‚àÖ $ h'.symm ‚ñ∏ empty_subset _)
  in
  have f ‚â† ‚ä•, from infi_ne_bot_of_directed ‚ü®a‚ü©
  (assume t‚ÇÅ t‚ÇÇ, ‚ü®t‚ÇÅ ‚à™ t‚ÇÇ,
    principal_mono.mpr $ diff_subset_diff_right $
      bUnion_subset_bUnion_left $ finset.subset_union_left _ _,
    principal_mono.mpr $ diff_subset_diff_right $
      bUnion_subset_bUnion_left $ finset.subset_union_right _ _‚ü©)
   (assume t, show principal (s \ _) ‚â† ‚ä•,
     by simp only [ne.def, principal_eq_bot_iff, diff_eq_empty]; exact h _),
  have f ‚â§ principal s, from infi_le_of_le ‚àÖ $
    show principal (s \ _) ‚â§ principal s, from le_principal_iff.2 (diff_subset _ _),
  let
    ‚ü®a, ha, (h : f ‚äì ùìù a ‚â† ‚ä•)‚ü© := hs f ‚Äπf ‚â† ‚ä•‚Ä∫ this,
    ‚ü®_, ‚ü®i, rfl‚ü©, (ha : a ‚àà U i)‚ü© := hsU ha
  in
  have f ‚â§ principal (- U i),
    from infi_le_of_le {i} $ principal_mono.mpr $ show s - _ ‚äÜ - U i, by simp [diff_subset_iff],
  have is_closed (- U i), from is_open_compl_iff.mp $ by rw lattice.neg_neg; exact hUo i,
  have a ‚àà - U i, from is_closed_iff_nhds.mp this _ $ ne_bot_of_le_ne_bot h $
    le_inf inf_le_right (inf_le_left_of_le ‚Äπf ‚â§ principal (- U i)‚Ä∫),
  this ‚Äπa ‚àà U i‚Ä∫

/-- For every family of closed sets whose intersection avoids a compact set,
there exists a finite subfamily whose intersection avoids this compact set. -/
lemma compact.elim_finite_subfamily_closed {s : set Œ±} {Œπ : Type v} (hs : compact s)
  (Z : Œπ ‚Üí set Œ±) (hZc : ‚àÄi, is_closed (Z i)) (hsZ : s ‚à© (‚ãÇ i, Z i) = ‚àÖ) :
  ‚àÉ t : finset Œπ, s ‚à© (‚ãÇ i ‚àà t, Z i) = ‚àÖ :=
let ‚ü®t, ht‚ü© := hs.elim_finite_subcover (Œª i, - Z i) hZc
  (by simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, set.mem_Union,
    exists_prop, set.mem_inter_eq, not_and, iff_self, set.mem_Inter, set.mem_compl_eq] using hsZ)
    in
‚ü®t, by simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, set.mem_Union,
    exists_prop, set.mem_inter_eq, not_and, iff_self, set.mem_Inter, set.mem_compl_eq] using ht‚ü©

/-- Cantor's intersection theorem:
the intersection of a directed family of nonempty compact closed sets is nonempty. -/
lemma compact.nonempty_Inter_of_directed_nonempty_compact_closed
  {Œπ : Type v} [hŒπ : nonempty Œπ] (Z : Œπ ‚Üí set Œ±) (hZd : directed (‚äá) Z)
  (hZn : ‚àÄ i, (Z i).nonempty) (hZc : ‚àÄ i, compact (Z i)) (hZcl : ‚àÄ i, is_closed (Z i)) :
  (‚ãÇ i, Z i).nonempty :=
begin
  apply hŒπ.elim,
  intro i‚ÇÄ,
  let Z' := Œª i, Z i ‚à© Z i‚ÇÄ,
  suffices : (‚ãÇ i, Z' i).nonempty,
  { exact nonempty.mono (Inter_subset_Inter $ assume i, inter_subset_left (Z i) (Z i‚ÇÄ)) this },
  rw ‚Üê ne_empty_iff_nonempty,
  intro H,
  obtain ‚ü®t, ht‚ü© : ‚àÉ (t : finset Œπ), ((Z i‚ÇÄ) ‚à© ‚ãÇ (i ‚àà t), Z' i) = ‚àÖ,
    from (hZc i‚ÇÄ).elim_finite_subfamily_closed Z'
      (assume i, is_closed_inter (hZcl i) (hZcl i‚ÇÄ)) (by rw [H, inter_empty]),
  obtain ‚ü®i‚ÇÅ, hi‚ÇÅ‚ü© : ‚àÉ i‚ÇÅ : Œπ, Z i‚ÇÅ ‚äÜ Z i‚ÇÄ ‚àß ‚àÄ i ‚àà t, Z i‚ÇÅ ‚äÜ Z' i,
  { rcases directed.finset_le hŒπ hZd t with ‚ü®i, hi‚ü©,
    rcases hZd i i‚ÇÄ with ‚ü®i‚ÇÅ, hi‚ÇÅ, hi‚ÇÅ‚ÇÄ‚ü©,
    use [i‚ÇÅ, hi‚ÇÅ‚ÇÄ],
    intros j hj,
    exact subset_inter (subset.trans hi‚ÇÅ (hi j hj)) hi‚ÇÅ‚ÇÄ },
  suffices : ((Z i‚ÇÄ) ‚à© ‚ãÇ (i ‚àà t), Z' i).nonempty,
  { rw ‚Üê ne_empty_iff_nonempty at this, contradiction },
  refine nonempty.mono _ (hZn i‚ÇÅ),
  exact subset_inter hi‚ÇÅ.left (subset_bInter hi‚ÇÅ.right)
end

/-- Cantor's intersection theorem for sequences indexed by `‚Ñï`:
the intersection of a decreasing sequence of nonempty compact closed sets is nonempty. -/
lemma compact.nonempty_Inter_of_sequence_nonempty_compact_closed
  (Z : ‚Ñï ‚Üí set Œ±) (hZd : ‚àÄ i, Z (i+1) ‚äÜ Z i)
  (hZn : ‚àÄ i, (Z i).nonempty) (hZ0 : compact (Z 0)) (hZcl : ‚àÄ i, is_closed (Z i)) :
  (‚ãÇ i, Z i).nonempty :=
have Zmono : _, from @monotone_of_monotone_nat (order_dual _) _ Z hZd,
have hZd : directed (‚äá) Z, from directed_of_mono Z Zmono,
have ‚àÄ i, Z i ‚äÜ Z 0, from assume i, Zmono $ zero_le i,
have hZc : ‚àÄ i, compact (Z i), from assume i, compact_of_is_closed_subset hZ0 (hZcl i) (this i),
compact.nonempty_Inter_of_directed_nonempty_compact_closed Z hZd hZn hZc hZcl

/-- For every open cover of a compact set, there exists a finite subcover. -/
lemma compact.elim_finite_subcover_image {s : set Œ±} {b : set Œ≤} {c : Œ≤ ‚Üí set Œ±}
  (hs : compact s) (hc‚ÇÅ : ‚àÄi‚ààb, is_open (c i)) (hc‚ÇÇ : s ‚äÜ ‚ãÉi‚ààb, c i) :
  ‚àÉb'‚äÜb, finite b' ‚àß s ‚äÜ ‚ãÉi‚ààb', c i :=
begin
  rcases hs.elim_finite_subcover (Œª i, c i.1 : b ‚Üí set Œ±) _ _ with ‚ü®d, hd‚ü©,
  refine ‚ü®‚Üë(d.image subtype.val), _, finset.finite_to_set _, _‚ü©,
  { intros i hi,
    erw finset.mem_image at hi,
    rcases hi with ‚ü®s, hsd, rfl‚ü©,
    exact s.property },
  { refine subset.trans hd _,
    rintros x ‚ü®_, ‚ü®s, rfl‚ü©, ‚ü®_, ‚ü®hsd, rfl‚ü©, H‚ü©‚ü©,
    refine ‚ü®c s.val, ‚ü®s.val, _‚ü©, H‚ü©,
    simp [finset.mem_image_of_mem subtype.val hsd] },
  { rintro ‚ü®i, hi‚ü©, exact hc‚ÇÅ i hi },
  { refine subset.trans hc‚ÇÇ _,
    rintros x ‚ü®_, ‚ü®i, rfl‚ü©, ‚ü®_, ‚ü®hib, rfl‚ü©, H‚ü©‚ü©,
    exact ‚ü®_, ‚ü®‚ü®i, hib‚ü©, rfl‚ü©, H‚ü© },
end

/-- A set `s` is compact if for every family of closed sets whose intersection avoids `s`,
there exists a finite subfamily whose intersection avoids `s`. -/
theorem compact_of_finite_subfamily_closed {s : set Œ±}
  (h : Œ† {Œπ : Type u} (Z : Œπ ‚Üí (set Œ±)), (‚àÄ i, is_closed (Z i)) ‚Üí
    s ‚à© (‚ãÇ i, Z i) = ‚àÖ ‚Üí (‚àÉ (t : finset Œπ), s ‚à© (‚ãÇ i ‚àà t, Z i) = ‚àÖ)) :
  compact s :=
assume f hfn hfs, classical.by_contradiction $ assume : ¬¨ (‚àÉx‚ààs, f ‚äì ùìù x ‚â† ‚ä•),
  have hf : ‚àÄx‚ààs, ùìù x ‚äì f = ‚ä•,
    by simpa only [not_exists, not_not, inf_comm],
  have ¬¨ ‚àÉx‚ààs, ‚àÄt‚ààf.sets, x ‚àà closure t,
    from assume ‚ü®x, hxs, hx‚ü©,
    have ‚àÖ ‚àà ùìù x ‚äì f, by rw [empty_in_sets_eq_bot, hf x hxs],
    let ‚ü®t‚ÇÅ, ht‚ÇÅ, t‚ÇÇ, ht‚ÇÇ, ht‚ü© := by rw [mem_inf_sets] at this; exact this in
    have ‚àÖ ‚àà ùìù x ‚äì principal t‚ÇÇ,
      from (ùìù x ‚äì principal t‚ÇÇ).sets_of_superset (inter_mem_inf_sets ht‚ÇÅ (subset.refl t‚ÇÇ)) ht,
    have ùìù x ‚äì principal t‚ÇÇ = ‚ä•,
      by rwa [empty_in_sets_eq_bot] at this,
    by simp only [closure_eq_nhds] at hx; exact hx t‚ÇÇ ht‚ÇÇ this,
  let ‚ü®t, ht‚ü© := h (Œª i : f.sets, closure i.1) (Œª i, is_closed_closure)
    (by simpa [eq_empty_iff_forall_not_mem, not_exists]) in
  have (‚ãÇi‚ààt, subtype.val i) ‚àà f,
    from Inter_mem_sets t.finite_to_set $ assume i hi, i.2,
  have s ‚à© (‚ãÇi‚ààt, subtype.val i) ‚àà f,
    from inter_mem_sets (le_principal_iff.1 hfs) this,
  have ‚àÖ ‚àà f,
    from mem_sets_of_superset this $ assume x ‚ü®hxs, hx‚ü©,
    let ‚ü®i, hit, hxi‚ü© := (show ‚àÉi ‚àà t, x ‚àâ closure (subtype.val i),
      by { rw [eq_empty_iff_forall_not_mem] at ht, simpa [hxs, not_forall] using ht x }) in
    have x ‚àà closure i.val, from subset_closure (mem_bInter_iff.mp hx i hit),
    show false, from hxi this,
  hfn $ by rwa [empty_in_sets_eq_bot] at this

/-- A set `s` is compact if for every open cover of `s`, there exists a finite subcover. -/
lemma compact_of_finite_subcover {s : set Œ±}
  (h : Œ† {Œπ : Type u} (U : Œπ ‚Üí (set Œ±)), (‚àÄ i, is_open (U i)) ‚Üí
    s ‚äÜ (‚ãÉ i, U i) ‚Üí (‚àÉ (t : finset Œπ), s ‚äÜ (‚ãÉ i ‚àà t, U i))) :
  compact s :=
compact_of_finite_subfamily_closed $
  assume Œπ Z hZc hsZ,
  let ‚ü®t, ht‚ü© := h (Œª i, - Z i) (assume i, is_open_compl_iff.mpr $ hZc i)
    (by simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, set.mem_Union,
      exists_prop, set.mem_inter_eq, not_and, iff_self, set.mem_Inter, set.mem_compl_eq] using hsZ)
      in
  ‚ü®t, by simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, set.mem_Union,
      exists_prop, set.mem_inter_eq, not_and, iff_self, set.mem_Inter, set.mem_compl_eq] using ht‚ü©

/-- A set `s` is compact if and only if
for every open cover of `s`, there exists a finite subcover. -/
lemma compact_iff_finite_subcover {s : set Œ±} :
  compact s ‚Üî (Œ† {Œπ : Type u} (U : Œπ ‚Üí (set Œ±)), (‚àÄ i, is_open (U i)) ‚Üí
    s ‚äÜ (‚ãÉ i, U i) ‚Üí (‚àÉ (t : finset Œπ), s ‚äÜ (‚ãÉ i ‚àà t, U i))) :=
‚ü®assume hs Œπ, hs.elim_finite_subcover, compact_of_finite_subcover‚ü©

/-- A set `s` is compact if and only if
for every family of closed sets whose intersection avoids `s`,
there exists a finite subfamily whose intersection avoids `s`. -/
theorem compact_iff_finite_subfamily_closed {s : set Œ±} :
  compact s ‚Üî (Œ† {Œπ : Type u} (Z : Œπ ‚Üí (set Œ±)), (‚àÄ i, is_closed (Z i)) ‚Üí
    s ‚à© (‚ãÇ i, Z i) = ‚àÖ ‚Üí (‚àÉ (t : finset Œπ), s ‚à© (‚ãÇ i ‚àà t, Z i) = ‚àÖ)) :=
‚ü®assume hs Œπ, hs.elim_finite_subfamily_closed, compact_of_finite_subfamily_closed‚ü©

@[simp]
lemma compact_empty : compact (‚àÖ : set Œ±) :=
assume f hnf hsf, not.elim hnf $
empty_in_sets_eq_bot.1 $ le_principal_iff.1 hsf

@[simp]
lemma compact_singleton {a : Œ±} : compact ({a} : set Œ±) :=
compact_of_finite_subcover $ assume Œπ U hUo hsU,
  let ‚ü®i, hai‚ü© := (show ‚àÉi : Œπ, a ‚àà U i, from mem_Union.1 $ singleton_subset_iff.1 hsU) in
  ‚ü®{i}, singleton_subset_iff.2 (by simpa only [finset.bUnion_singleton])‚ü©

lemma set.finite.compact_bUnion {s : set Œ≤} {f : Œ≤ ‚Üí set Œ±} (hs : finite s)
  (hf : ‚àÄi ‚àà s, compact (f i)) :
  compact (‚ãÉi ‚àà s, f i) :=
compact_of_finite_subcover $ assume Œπ U hUo hsU,
  have ‚àÄi : subtype s, ‚àÉt : finset Œπ, f i ‚äÜ (‚ãÉ j ‚àà t, U j), from
    assume ‚ü®i, hi‚ü©, (hf i hi).elim_finite_subcover _ hUo
      (calc f i ‚äÜ ‚ãÉi ‚àà s, f i : subset_bUnion_of_mem hi
            ... ‚äÜ ‚ãÉj, U j     : hsU),
  let ‚ü®finite_subcovers, h‚ü© := axiom_of_choice this in
  by haveI : fintype (subtype s) := hs.fintype; exact
  let t := finset.bind finset.univ finite_subcovers in
  have (‚ãÉi ‚àà s, f i) ‚äÜ (‚ãÉ i ‚àà t, U i), from bUnion_subset $
    assume i hi, calc
    f i ‚äÜ (‚ãÉ j ‚àà finite_subcovers ‚ü®i, hi‚ü©, U j) : (h ‚ü®i, hi‚ü©)
    ... ‚äÜ (‚ãÉ j ‚àà t, U j) : bUnion_subset_bUnion_left $
      assume j hj, finset.mem_bind.mpr ‚ü®_, finset.mem_univ _, hj‚ü©,
  ‚ü®t, this‚ü©

lemma compact_Union {f : Œ≤ ‚Üí set Œ±} [fintype Œ≤]
  (h : ‚àÄi, compact (f i)) : compact (‚ãÉi, f i) :=
by rw ‚Üê bUnion_univ; exact finite_univ.compact_bUnion (Œª i _, h i)

lemma set.finite.compact {s : set Œ±} (hs : finite s) : compact s :=
bUnion_of_singleton s ‚ñ∏ hs.compact_bUnion (Œª _ _, compact_singleton)

lemma compact.union {s t : set Œ±} (hs : compact s) (ht : compact t) : compact (s ‚à™ t) :=
by rw union_eq_Union; exact compact_Union (Œª b, by cases b; assumption)

section tube_lemma

variables [topological_space Œ≤]

/-- `nhds_contain_boxes s t` means that any open neighborhood of `s √ó t` in `Œ± √ó Œ≤` includes
a product of an open neighborhood of `s` by an open neighborhood of `t`. -/
def nhds_contain_boxes (s : set Œ±) (t : set Œ≤) : Prop :=
‚àÄ (n : set (Œ± √ó Œ≤)) (hn : is_open n) (hp : set.prod s t ‚äÜ n),
‚àÉ (u : set Œ±) (v : set Œ≤), is_open u ‚àß is_open v ‚àß s ‚äÜ u ‚àß t ‚äÜ v ‚àß set.prod u v ‚äÜ n

lemma nhds_contain_boxes.symm {s : set Œ±} {t : set Œ≤} :
  nhds_contain_boxes s t ‚Üí nhds_contain_boxes t s :=
assume H n hn hp,
  let ‚ü®u, v, uo, vo, su, tv, p‚ü© :=
    H (prod.swap ‚Åª¬π' n)
      (continuous_swap n hn)
      (by rwa [‚Üêimage_subset_iff, prod.swap, image_swap_prod]) in
  ‚ü®v, u, vo, uo, tv, su,
    by rwa [‚Üêimage_subset_iff, prod.swap, image_swap_prod] at p‚ü©

lemma nhds_contain_boxes.comm {s : set Œ±} {t : set Œ≤} :
  nhds_contain_boxes s t ‚Üî nhds_contain_boxes t s :=
iff.intro nhds_contain_boxes.symm nhds_contain_boxes.symm

lemma nhds_contain_boxes_of_singleton {x : Œ±} {y : Œ≤} :
  nhds_contain_boxes ({x} : set Œ±) ({y} : set Œ≤) :=
assume n hn hp,
  let ‚ü®u, v, uo, vo, xu, yv, hp'‚ü© :=
    is_open_prod_iff.mp hn x y (hp $ by simp) in
  ‚ü®u, v, uo, vo, by simpa, by simpa, hp'‚ü©

lemma nhds_contain_boxes_of_compact {s : set Œ±} (hs : compact s) (t : set Œ≤)
  (H : ‚àÄ x ‚àà s, nhds_contain_boxes ({x} : set Œ±) t) : nhds_contain_boxes s t :=
assume n hn hp,
have ‚àÄx : subtype s, ‚àÉuv : set Œ± √ó set Œ≤,
     is_open uv.1 ‚àß is_open uv.2 ‚àß {‚Üëx} ‚äÜ uv.1 ‚àß t ‚äÜ uv.2 ‚àß set.prod uv.1 uv.2 ‚äÜ n,
  from assume ‚ü®x, hx‚ü©,
    have set.prod {x} t ‚äÜ n, from
      subset.trans (prod_mono (by simpa) (subset.refl _)) hp,
    let ‚ü®ux,vx,H1‚ü© := H x hx n hn this in ‚ü®‚ü®ux,vx‚ü©,H1‚ü©,
let ‚ü®uvs, h‚ü© := classical.axiom_of_choice this in
have us_cover : s ‚äÜ ‚ãÉi, (uvs i).1, from
  assume x hx, set.subset_Union _ ‚ü®x,hx‚ü© (by simpa using (h ‚ü®x,hx‚ü©).2.2.1),
let ‚ü®s0, s0_cover‚ü© :=
  hs.elim_finite_subcover _ (Œªi, (h i).1) us_cover in
let u := ‚ãÉ(i ‚àà s0), (uvs i).1 in
let v := ‚ãÇ(i ‚àà s0), (uvs i).2 in
have is_open u, from is_open_bUnion (Œªi _, (h i).1),
have is_open v, from is_open_bInter s0.finite_to_set (Œªi _, (h i).2.1),
have t ‚äÜ v, from subset_bInter (Œªi _, (h i).2.2.2.1),
have set.prod u v ‚äÜ n, from assume ‚ü®x',y'‚ü© ‚ü®hx',hy'‚ü©,
  have ‚àÉi ‚àà s0, x' ‚àà (uvs i).1, by simpa using hx',
  let ‚ü®i,is0,hi‚ü© := this in
  (h i).2.2.2.2 ‚ü®hi, (bInter_subset_of_mem is0 : v ‚äÜ (uvs i).2) hy'‚ü©,
‚ü®u, v, ‚Äπis_open u‚Ä∫, ‚Äπis_open v‚Ä∫, s0_cover, ‚Äπt ‚äÜ v‚Ä∫, ‚Äπset.prod u v ‚äÜ n‚Ä∫‚ü©

lemma generalized_tube_lemma {s : set Œ±} (hs : compact s) {t : set Œ≤} (ht : compact t)
  {n : set (Œ± √ó Œ≤)} (hn : is_open n) (hp : set.prod s t ‚äÜ n) :
  ‚àÉ (u : set Œ±) (v : set Œ≤), is_open u ‚àß is_open v ‚àß s ‚äÜ u ‚àß t ‚äÜ v ‚àß set.prod u v ‚äÜ n :=
have _, from
  nhds_contain_boxes_of_compact hs t $ assume x _, nhds_contain_boxes.symm $
    nhds_contain_boxes_of_compact ht {x} $ assume y _, nhds_contain_boxes_of_singleton,
this n hn hp

end tube_lemma

/-- Type class for compact spaces. Separation is sometimes included in the definition, especially
in the French literature, but we do not include it here. -/
class compact_space (Œ± : Type*) [topological_space Œ±] : Prop :=
(compact_univ : compact (univ : set Œ±))

lemma compact_univ [h : compact_space Œ±] : compact (univ : set Œ±) := h.compact_univ

theorem compact_space_of_finite_subfamily_closed {Œ± : Type u} [topological_space Œ±]
  (h : Œ† {Œπ : Type u} (Z : Œπ ‚Üí (set Œ±)), (‚àÄ i, is_closed (Z i)) ‚Üí
    (‚ãÇ i, Z i) = ‚àÖ ‚Üí (‚àÉ (t : finset Œπ), (‚ãÇ i ‚àà t, Z i) = ‚àÖ)) :
  compact_space Œ± :=
{ compact_univ :=
  begin
    apply compact_of_finite_subfamily_closed,
    intros Œπ Z, specialize h Z,
    simpa using h
  end }

lemma is_closed.compact [compact_space Œ±] {s : set Œ±} (h : is_closed s) :
  compact s :=
compact_of_is_closed_subset compact_univ h (subset_univ _)

variables [topological_space Œ≤]

lemma compact.image_of_continuous_on {s : set Œ±} {f : Œ± ‚Üí Œ≤} (hs : compact s)
  (hf : continuous_on f s) : compact (f '' s) :=
begin
  intros l lne ls,
  have ne_bot : l.comap f ‚äì principal s ‚â† ‚ä•,
    from comap_inf_principal_ne_bot_of_image_mem lne (le_principal_iff.1 ls),
  rcases hs (l.comap f ‚äì principal s) ne_bot inf_le_right with ‚ü®a, has, ha‚ü©,
  use [f a, mem_image_of_mem f has],
  rw [inf_assoc, @inf_comm _ _ _ (ùìù a)] at ha,
  exact ne_bot_of_le_ne_bot (@@map_ne_bot f ha) (tendsto_comap.inf $ hf a has)
end

lemma compact.image {s : set Œ±} {f : Œ± ‚Üí Œ≤} (hs : compact s) (hf : continuous f) :
  compact (f '' s) :=
hs.image_of_continuous_on hf.continuous_on

lemma compact_range [compact_space Œ±] {f : Œ± ‚Üí Œ≤} (hf : continuous f) :
  compact (range f) :=
by rw ‚Üê image_univ; exact compact_univ.image hf

lemma embedding.compact_iff_compact_image {s : set Œ±} {f : Œ± ‚Üí Œ≤} (hf : embedding f) :
  compact s ‚Üî compact (f '' s) :=
iff.intro (assume h, h.image hf.continuous) $ assume h, begin
  rw compact_iff_ultrafilter_le_nhds at ‚ä¢ h,
  intros u hu us',
  let u' : filter Œ≤ := map f u,
  have : u' ‚â§ principal (f '' s), begin
    rw [map_le_iff_le_comap, comap_principal], convert us',
    exact preimage_image_eq _ hf.inj
  end,
  rcases h u' (ultrafilter_map hu) this with ‚ü®_, ‚ü®a, ha, ‚ü®‚ü©‚ü©, _‚ü©,
  refine ‚ü®a, ha, _‚ü©,
  rwa [hf.induced, nhds_induced, ‚Üêmap_le_iff_le_comap]
end

lemma compact_iff_compact_in_subtype {p : Œ± ‚Üí Prop} {s : set {a // p a}} :
  compact s ‚Üî compact (subtype.val '' s) :=
embedding_subtype_val.compact_iff_compact_image

lemma compact_iff_compact_univ {s : set Œ±} : compact s ‚Üî compact (univ : set (subtype s)) :=
by rw [compact_iff_compact_in_subtype, image_univ, subtype.val_range]; refl

lemma compact_iff_compact_space {s : set Œ±} : compact s ‚Üî compact_space s :=
compact_iff_compact_univ.trans ‚ü®Œª h, ‚ü®h‚ü©, @compact_space.compact_univ _ _‚ü©

lemma compact.prod {s : set Œ±} {t : set Œ≤} (hs : compact s) (ht : compact t) : compact (set.prod s t) :=
begin
  rw compact_iff_ultrafilter_le_nhds at hs ht ‚ä¢,
  intros f hf hfs,
  rw le_principal_iff at hfs,
  rcases hs (map prod.fst f) (ultrafilter_map hf)
    (le_principal_iff.2 (mem_map_sets_iff.2
      ‚ü®_, hfs, image_subset_iff.2 (Œª s h, h.1)‚ü©)) with ‚ü®a, sa, ha‚ü©,
  rcases ht (map prod.snd f) (ultrafilter_map hf)
    (le_principal_iff.2 (mem_map_sets_iff.2
      ‚ü®_, hfs, image_subset_iff.2 (Œª s h, h.2)‚ü©)) with ‚ü®b, tb, hb‚ü©,
  rw map_le_iff_le_comap at ha hb,
  refine ‚ü®‚ü®a, b‚ü©, ‚ü®sa, tb‚ü©, _‚ü©,
  rw nhds_prod_eq, exact le_inf ha hb
end

/-- Finite topological spaces are compact. -/
@[priority 100] instance fintype.compact_space [fintype Œ±] : compact_space Œ± :=
{ compact_univ := set.finite_univ.compact }

/-- The product of two compact spaces is compact. -/
instance [compact_space Œ±] [compact_space Œ≤] : compact_space (Œ± √ó Œ≤) :=
‚ü®by { rw ‚Üê univ_prod_univ, exact compact_univ.prod compact_univ }‚ü©

/-- The disjoint union of two compact spaces is compact. -/
instance [compact_space Œ±] [compact_space Œ≤] : compact_space (Œ± ‚äï Œ≤) :=
‚ü®begin
  rw ‚Üê range_inl_union_range_inr,
  exact (compact_range continuous_inl).union (compact_range continuous_inr)
end‚ü©

section tychonoff
variables {Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [‚àÄi, topological_space (œÄ i)]

/-- Tychonoff's theorem -/
lemma compact_pi_infinite {s : Œ†i:Œπ, set (œÄ i)} :
  (‚àÄi, compact (s i)) ‚Üí compact {x : Œ†i:Œπ, œÄ i | ‚àÄi, x i ‚àà s i} :=
begin
  simp [compact_iff_ultrafilter_le_nhds, nhds_pi],
  exact assume h f hf hfs,
    let p : Œ†i:Œπ, filter (œÄ i) := Œªi, map (Œªx:Œ†i:Œπ, œÄ i, x i) f in
    have ‚àÄi:Œπ, ‚àÉa, a‚ààs i ‚àß p i ‚â§ ùìù a,
      from assume i, h i (p i) (ultrafilter_map hf) $
      show (Œªx:Œ†i:Œπ, œÄ i, x i) ‚Åª¬π' s i ‚àà f.sets,
        from mem_sets_of_superset hfs $ assume x (hx : ‚àÄi, x i ‚àà s i), hx i,
    let ‚ü®a, ha‚ü© := classical.axiom_of_choice this in
    ‚ü®a, assume i, (ha i).left, assume i, map_le_iff_le_comap.mp $ (ha i).right‚ü©
end

instance pi.compact [‚àÄi:Œπ, compact_space (œÄ i)] : compact_space (Œ†i, œÄ i) :=
‚ü®begin
  have A : compact {x : Œ†i:Œπ, œÄ i | ‚àÄi, x i ‚àà (univ : set (œÄ i))} :=
    compact_pi_infinite (Œªi, compact_univ),
  have : {x : Œ†i:Œπ, œÄ i | ‚àÄi, x i ‚àà (univ : set (œÄ i))} = univ := by ext; simp,
  rwa this at A,
end‚ü©

end tychonoff

instance quot.compact_space {r : Œ± ‚Üí Œ± ‚Üí Prop} [compact_space Œ±] :
  compact_space (quot r) :=
‚ü®by { rw ‚Üê range_quot_mk, exact compact_range continuous_quot_mk }‚ü©

instance quotient.compact_space {s : setoid Œ±} [compact_space Œ±] :
  compact_space (quotient s) :=
quot.compact_space

/-- There are various definitions of "locally compact space" in the literature, which agree for
Hausdorff spaces but not in general. This one is the precise condition on X needed for the
evaluation `map C(X, Y) √ó X ‚Üí Y` to be continuous for all `Y` when `C(X, Y)` is given the
compact-open topology. -/
class locally_compact_space (Œ± : Type*) [topological_space Œ±] : Prop :=
(local_compact_nhds : ‚àÄ (x : Œ±) (n ‚àà ùìù x), ‚àÉ s ‚àà ùìù x, s ‚äÜ n ‚àß compact s)

end compact

section clopen

/-- A set is clopen if it is both open and closed. -/
def is_clopen (s : set Œ±) : Prop :=
is_open s ‚àß is_closed s

theorem is_clopen_union {s t : set Œ±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s ‚à™ t) :=
‚ü®is_open_union hs.1 ht.1, is_closed_union hs.2 ht.2‚ü©

theorem is_clopen_inter {s t : set Œ±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s ‚à© t) :=
‚ü®is_open_inter hs.1 ht.1, is_closed_inter hs.2 ht.2‚ü©

@[simp] theorem is_clopen_empty : is_clopen (‚àÖ : set Œ±) :=
‚ü®is_open_empty, is_closed_empty‚ü©

@[simp] theorem is_clopen_univ : is_clopen (univ : set Œ±) :=
‚ü®is_open_univ, is_closed_univ‚ü©

theorem is_clopen_compl {s : set Œ±} (hs : is_clopen s) : is_clopen (-s) :=
‚ü®hs.2, is_closed_compl_iff.2 hs.1‚ü©

@[simp] theorem is_clopen_compl_iff {s : set Œ±} : is_clopen (-s) ‚Üî is_clopen s :=
‚ü®Œª h, compl_compl s ‚ñ∏ is_clopen_compl h, is_clopen_compl‚ü©

theorem is_clopen_diff {s t : set Œ±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s-t) :=
is_clopen_inter hs (is_clopen_compl ht)

end clopen

section preirreducible

/-- A preirreducible set `s` is one where there is no non-trivial pair of disjoint opens on `s`. -/
def is_preirreducible (s : set Œ±) : Prop :=
‚àÄ (u v : set Œ±), is_open u ‚Üí is_open v ‚Üí
  (s ‚à© u).nonempty ‚Üí (s ‚à© v).nonempty ‚Üí (s ‚à© (u ‚à© v)).nonempty

/-- An irreducible set `s` is one that is nonempty and
where there is no non-trivial pair of disjoint opens on `s`. -/
def is_irreducible (s : set Œ±) : Prop :=
s.nonempty ‚àß is_preirreducible s

lemma is_irreducible.nonempty {s : set Œ±} (h : is_irreducible s) :
  s.nonempty := h.1

lemma is_irreducible.is_preirreducible {s : set Œ±} (h : is_irreducible s) :
  is_preirreducible s := h.2

theorem is_preirreducible_empty : is_preirreducible (‚àÖ : set Œ±) :=
Œª _ _ _ _ _ ‚ü®x, h1, h2‚ü©, h1.elim

theorem is_irreducible_singleton {x} : is_irreducible ({x} : set Œ±) :=
‚ü®singleton_nonempty x,
 Œª u v _ _ ‚ü®y, h1, h2‚ü© ‚ü®z, h3, h4‚ü©, by rw mem_singleton_iff at h1 h3;
 substs y z; exact ‚ü®x, or.inl rfl, h2, h4‚ü©‚ü©

theorem is_preirreducible.closure {s : set Œ±} (H : is_preirreducible s) :
  is_preirreducible (closure s) :=
Œª u v hu hv ‚ü®y, hycs, hyu‚ü© ‚ü®z, hzcs, hzv‚ü©,
let ‚ü®p, hpu, hps‚ü© := mem_closure_iff.1 hycs u hu hyu in
let ‚ü®q, hqv, hqs‚ü© := mem_closure_iff.1 hzcs v hv hzv in
let ‚ü®r, hrs, hruv‚ü© := H u v hu hv ‚ü®p, hps, hpu‚ü© ‚ü®q, hqs, hqv‚ü© in
‚ü®r, subset_closure hrs, hruv‚ü©

lemma is_irreducible.closure {s : set Œ±} (h : is_irreducible s) :
  is_irreducible (closure s) :=
‚ü®h.nonempty.closure, h.is_preirreducible.closure‚ü©

theorem exists_preirreducible (s : set Œ±) (H : is_preirreducible s) :
  ‚àÉ t : set Œ±, is_preirreducible t ‚àß s ‚äÜ t ‚àß ‚àÄ u, is_preirreducible u ‚Üí t ‚äÜ u ‚Üí u = t :=
let ‚ü®m, hm, hsm, hmm‚ü© := zorn.zorn_subset‚ÇÄ {t : set Œ± | is_preirreducible t}
  (Œª c hc hcc hcn, let ‚ü®t, htc‚ü© := hcn in
    ‚ü®‚ãÉ‚ÇÄ c, Œª u v hu hv ‚ü®y, hy, hyu‚ü© ‚ü®z, hz, hzv‚ü©,
      let ‚ü®p, hpc, hyp‚ü© := mem_sUnion.1 hy,
          ‚ü®q, hqc, hzq‚ü© := mem_sUnion.1 hz in
      or.cases_on (zorn.chain.total hcc hpc hqc)
        (assume hpq : p ‚äÜ q, let ‚ü®x, hxp, hxuv‚ü© := hc hqc u v hu hv
            ‚ü®y, hpq hyp, hyu‚ü© ‚ü®z, hzq, hzv‚ü© in
          ‚ü®x, mem_sUnion_of_mem hxp hqc, hxuv‚ü©)
        (assume hqp : q ‚äÜ p, let ‚ü®x, hxp, hxuv‚ü© := hc hpc u v hu hv
            ‚ü®y, hyp, hyu‚ü© ‚ü®z, hqp hzq, hzv‚ü© in
          ‚ü®x, mem_sUnion_of_mem hxp hpc, hxuv‚ü©),
    Œª x hxc, set.subset_sUnion_of_mem hxc‚ü©) s H in
‚ü®m, hm, hsm, Œª u hu hmu, hmm _ hu hmu‚ü©

/-- A maximal irreducible set that contains a given point. -/
def irreducible_component (x : Œ±) : set Œ± :=
classical.some (exists_preirreducible {x} is_irreducible_singleton.is_preirreducible)

lemma irreducible_component_property (x : Œ±) :
  is_preirreducible (irreducible_component x) ‚àß {x} ‚äÜ (irreducible_component x) ‚àß
  ‚àÄ u, is_preirreducible u ‚Üí (irreducible_component x) ‚äÜ u ‚Üí u = (irreducible_component x) :=
classical.some_spec (exists_preirreducible {x} is_irreducible_singleton.is_preirreducible)

theorem mem_irreducible_component {x : Œ±} : x ‚àà irreducible_component x :=
singleton_subset_iff.1 (irreducible_component_property x).2.1

theorem is_irreducible_irreducible_component {x : Œ±} : is_irreducible (irreducible_component x) :=
‚ü®‚ü®x, mem_irreducible_component‚ü©, (irreducible_component_property x).1‚ü©

theorem eq_irreducible_component {x : Œ±} :
  ‚àÄ {s : set Œ±}, is_preirreducible s ‚Üí irreducible_component x ‚äÜ s ‚Üí s = irreducible_component x :=
(irreducible_component_property x).2.2

theorem is_closed_irreducible_component {x : Œ±} :
  is_closed (irreducible_component x) :=
closure_eq_iff_is_closed.1 $ eq_irreducible_component
  is_irreducible_irreducible_component.is_preirreducible.closure
  subset_closure

/-- A preirreducible space is one where there is no non-trivial pair of disjoint opens. -/
class preirreducible_space (Œ± : Type u) [topological_space Œ±] : Prop :=
(is_preirreducible_univ : is_preirreducible (univ : set Œ±))

/-- An irreducible space is one that is nonempty
and where there is no non-trivial pair of disjoint opens. -/
class irreducible_space (Œ± : Type u) [topological_space Œ±] extends preirreducible_space Œ± : Prop :=
(to_nonempty : nonempty Œ±)

attribute [instance] irreducible_space.to_nonempty

theorem nonempty_preirreducible_inter [preirreducible_space Œ±] {s t : set Œ±} :
  is_open s ‚Üí is_open t ‚Üí s.nonempty ‚Üí t.nonempty ‚Üí (s ‚à© t).nonempty :=
by simpa only [univ_inter, univ_subset_iff] using
  @preirreducible_space.is_preirreducible_univ Œ± _ _ s t

theorem is_preirreducible.image [topological_space Œ≤] {s : set Œ±} (H : is_preirreducible s)
  (f : Œ± ‚Üí Œ≤) (hf : continuous_on f s) : is_preirreducible (f '' s) :=
begin
  rintros u v hu hv ‚ü®_, ‚ü®‚ü®x, hx, rfl‚ü©, hxu‚ü©‚ü© ‚ü®_, ‚ü®‚ü®y, hy, rfl‚ü©, hyv‚ü©‚ü©,
  rw ‚Üê set.mem_preimage at hxu hyv,
  rcases continuous_on_iff'.1 hf u hu with ‚ü®u', hu', u'_eq‚ü©,
  rcases continuous_on_iff'.1 hf v hv with ‚ü®v', hv', v'_eq‚ü©,
  have := H u' v' hu' hv',
  rw [set.inter_comm s u', ‚Üê u'_eq] at this,
  rw [set.inter_comm s v', ‚Üê v'_eq] at this,
  rcases this ‚ü®x, hxu, hx‚ü© ‚ü®y, hyv, hy‚ü© with ‚ü®z, hzs, hzu', hzv'‚ü©,
  refine ‚ü®f z, mem_image_of_mem f hzs, _, _‚ü©,
  all_goals
  { rw ‚Üê set.mem_preimage,
    apply set.mem_of_mem_inter_left,
    show z ‚àà _ ‚à© s,
    simp [*] }
end

theorem is_irreducible.image [topological_space Œ≤] {s : set Œ±} (H : is_irreducible s)
  (f : Œ± ‚Üí Œ≤) (hf : continuous_on f s) : is_irreducible (f '' s) :=
‚ü®nonempty_image_iff.mpr H.nonempty, H.is_preirreducible.image f hf‚ü©

lemma subtype.preirreducible_space {s : set Œ±} (h : is_preirreducible s) :
  preirreducible_space s :=
{ is_preirreducible_univ :=
  begin
    intros u v hu hv hsu hsv,
    rw is_open_induced_iff at hu hv,
    rcases hu with ‚ü®u, hu, rfl‚ü©,
    rcases hv with ‚ü®v, hv, rfl‚ü©,
    rcases hsu with ‚ü®‚ü®x, hxs‚ü©, hxs', hxu‚ü©,
    rcases hsv with ‚ü®‚ü®y, hys‚ü©, hys', hyv‚ü©,
    rcases h u v hu hv ‚ü®x, hxs, hxu‚ü© ‚ü®y, hys, hyv‚ü© with ‚ü®z, hzs, ‚ü®hzu, hzv‚ü©‚ü©,
    exact ‚ü®‚ü®z, hzs‚ü©, ‚ü®set.mem_univ _, ‚ü®hzu, hzv‚ü©‚ü©‚ü©
  end }

lemma subtype.irreducible_space {s : set Œ±} (h : is_irreducible s) :
  irreducible_space s :=
{ is_preirreducible_univ :=
  (subtype.preirreducible_space h.is_preirreducible).is_preirreducible_univ,
  to_nonempty := h.nonempty.to_subtype }

/-- A set `s` is irreducible if and only if
for every finite collection of open sets all of whose members intersect `s`,
`s` also intersects the intersection of the entire collection
(i.e., there is an element of `s` contained in every member of the collection). -/
lemma is_irreducible_iff_sInter {s : set Œ±} :
  is_irreducible s ‚Üî
  ‚àÄ (U : finset (set Œ±)) (hU : ‚àÄ u ‚àà U, is_open u) (H : ‚àÄ u ‚àà U, (s ‚à© u).nonempty),
  (s ‚à© ‚ãÇ‚ÇÄ ‚ÜëU).nonempty :=
begin
  split; intro h,
  { intro U, apply finset.induction_on U,
    { intros, simpa using h.nonempty },
    { intros u U hu IH hU H,
      rw [finset.coe_insert, sInter_insert],
      apply h.2,
      { solve_by_elim [finset.mem_insert_self] },
      { apply is_open_sInter (finset.finite_to_set U),
        intros, solve_by_elim [finset.mem_insert_of_mem] },
      { solve_by_elim [finset.mem_insert_self] },
      { apply IH,
        all_goals { intros, solve_by_elim [finset.mem_insert_of_mem] } } } },
  { split,
    { simpa using h ‚àÖ _ _; intro u; simp },
    intros u v hu hv hu' hv',
    simpa only [finset.coe_insert, sInter_singleton, finset.insert_empty_eq_singleton,
      finset.coe_singleton, finset.has_insert_eq_insert, sInter_insert]
      using h {v,u} _ _,
    all_goals
    { intro t,
      rw [finset.insert_empty_eq_singleton, finset.has_insert_eq_insert,
          finset.mem_insert, finset.mem_singleton],
      rintro (rfl|rfl); assumption } }
end

/-- A set is preirreducible if and only if
for every cover by two closed sets, it is contained in one of the two covering sets. -/
lemma is_preirreducible_iff_closed_union_closed {s : set Œ±} :
  is_preirreducible s ‚Üî
  ‚àÄ (z‚ÇÅ z‚ÇÇ : set Œ±), is_closed z‚ÇÅ ‚Üí is_closed z‚ÇÇ ‚Üí s ‚äÜ z‚ÇÅ ‚à™ z‚ÇÇ ‚Üí s ‚äÜ z‚ÇÅ ‚à® s ‚äÜ z‚ÇÇ :=
begin
  split,
  all_goals
  { intros h t‚ÇÅ t‚ÇÇ ht‚ÇÅ ht‚ÇÇ,
    specialize h (-t‚ÇÅ) (-t‚ÇÇ),
    simp only [is_open_compl_iff, is_closed_compl_iff] at h,
    specialize h ht‚ÇÅ ht‚ÇÇ },
  { contrapose!, simp only [not_subset],
    rintro ‚ü®‚ü®x, hx, hx'‚ü©, ‚ü®y, hy, hy'‚ü©‚ü©,
    rcases h ‚ü®x, hx, hx'‚ü© ‚ü®y, hy, hy'‚ü© with ‚ü®z, hz, hz'‚ü©,
    rw ‚Üê compl_union at hz',
    exact ‚ü®z, hz, hz'‚ü© },
  { rintro ‚ü®x, hx, hx'‚ü© ‚ü®y, hy, hy'‚ü©,
    rw ‚Üê compl_inter at h,
    delta set.nonempty,
    rw imp_iff_not_or at h,
    contrapose! h,
    split,
    { intros z hz hz', exact h z ‚ü®hz, hz'‚ü© },
    { split; intro H; refine H _ ‚Äπ_‚Ä∫; assumption } }
end

/-- A set is irreducible if and only if
for every cover by a finite collection of closed sets,
it is contained in one of the members of the collection. -/
lemma is_irreducible_iff_sUnion_closed {s : set Œ±} :
  is_irreducible s ‚Üî
  ‚àÄ (Z : finset (set Œ±)) (hZ : ‚àÄ z ‚àà Z, is_closed z) (H : s ‚äÜ ‚ãÉ‚ÇÄ ‚ÜëZ),
  ‚àÉ z ‚àà Z, s ‚äÜ z :=
begin
  rw [is_irreducible, is_preirreducible_iff_closed_union_closed],
  split; intro h,
  { intro Z, apply finset.induction_on Z,
    { intros, rw [finset.coe_empty, sUnion_empty] at H,
      rcases h.1 with ‚ü®x, hx‚ü©,
      exfalso, tauto },
    { intros z Z hz IH hZ H,
      cases h.2 z (‚ãÉ‚ÇÄ ‚ÜëZ) _ _ _
        with h' h',
      { exact ‚ü®z, finset.mem_insert_self _ _, h'‚ü© },
      { rcases IH _ h' with ‚ü®z', hz', hsz'‚ü©,
        { exact ‚ü®z', finset.mem_insert_of_mem hz', hsz'‚ü© },
        { intros, solve_by_elim [finset.mem_insert_of_mem] } },
      { solve_by_elim [finset.mem_insert_self] },
      { rw sUnion_eq_bUnion,
        apply is_closed_bUnion (finset.finite_to_set Z),
        { intros, solve_by_elim [finset.mem_insert_of_mem] } },
      { simpa using H } } },
  { split,
    { by_contradiction hs,
      simpa using h ‚àÖ _ _,
      { intro z, simp },
      { simpa [set.nonempty] using hs } },
    intros z‚ÇÅ z‚ÇÇ hz‚ÇÅ hz‚ÇÇ H,
    have := h {z‚ÇÇ, z‚ÇÅ} _ _,
    simp only [exists_prop, finset.insert_empty_eq_singleton, finset.has_insert_eq_insert,
      finset.mem_insert, finset.mem_singleton] at this,
    { rcases this with ‚ü®z, rfl|rfl, hz‚ü©; tauto },
    { intro t,
      rw [finset.insert_empty_eq_singleton, finset.has_insert_eq_insert,
          finset.mem_insert, finset.mem_singleton],
      rintro (rfl|rfl); assumption },
    { simpa using H } }
end

end preirreducible

section preconnected

/-- A preconnected set is one where there is no non-trivial open partition. -/
def is_preconnected (s : set Œ±) : Prop :=
‚àÄ (u v : set Œ±), is_open u ‚Üí is_open v ‚Üí s ‚äÜ u ‚à™ v ‚Üí
  (s ‚à© u).nonempty ‚Üí (s ‚à© v).nonempty ‚Üí (s ‚à© (u ‚à© v)).nonempty

/-- A connected set is one that is nonempty and where there is no non-trivial open partition. -/
def is_connected (s : set Œ±) : Prop :=
s.nonempty ‚àß is_preconnected s

lemma is_connected.nonempty {s : set Œ±} (h : is_connected s) :
  s.nonempty := h.1

lemma is_connected.is_preconnected {s : set Œ±} (h : is_connected s) :
  is_preconnected s := h.2

theorem is_preirreducible.is_preconnected {s : set Œ±} (H : is_preirreducible s) : is_preconnected s :=
Œª _ _ hu hv _, H _ _ hu hv

theorem is_irreducible.is_connected {s : set Œ±} (H : is_irreducible s) : is_connected s :=
‚ü®H.nonempty, H.is_preirreducible.is_preconnected‚ü©

theorem is_preconnected_empty : is_preconnected (‚àÖ : set Œ±) :=
is_preirreducible_empty.is_preconnected

theorem is_connected_singleton {x} : is_connected ({x} : set Œ±) :=
is_irreducible_singleton.is_connected

/-- If any point of a set is joined to a fixed point by a preconnected subset,
then the original set is preconnected as well. -/
theorem is_preconnected_of_forall {s : set Œ±} (x : Œ±)
  (H : ‚àÄ y ‚àà s, ‚àÉ t ‚äÜ s, x ‚àà t ‚àß y ‚àà t ‚àß is_preconnected t) :
  is_preconnected s :=
begin
  rintros u v hu hv hs ‚ü®z, zs, zu‚ü© ‚ü®y, ys, yv‚ü©,
  have xs : x ‚àà s, by { rcases H y ys with ‚ü®t, ts, xt, yt, ht‚ü©, exact ts xt },
  wlog xu : x ‚àà u := hs xs using [u v y z, v u z y],
  rcases H y ys with ‚ü®t, ts, xt, yt, ht‚ü©,
  have := ht u v hu hv(subset.trans ts hs) ‚ü®x, xt, xu‚ü© ‚ü®y, yt, yv‚ü©,
  exact this.imp (Œª z hz, ‚ü®ts hz.1, hz.2‚ü©)
end

/-- If any two points of a set are contained in a preconnected subset,
then the original set is preconnected as well. -/
theorem is_preconnected_of_forall_pair {s : set Œ±}
  (H : ‚àÄ x y ‚àà s, ‚àÉ t ‚äÜ s, x ‚àà t ‚àß y ‚àà t ‚àß is_preconnected t) :
  is_preconnected s :=
begin
  rintros u v hu hv hs ‚ü®x, xs, xu‚ü© ‚ü®y, ys, yv‚ü©,
  rcases H x y xs ys with ‚ü®t, ts, xt, yt, ht‚ü©,
  have := ht u v hu hv(subset.trans ts hs) ‚ü®x, xt, xu‚ü© ‚ü®y, yt, yv‚ü©,
  exact this.imp (Œª z hz, ‚ü®ts hz.1, hz.2‚ü©)
end

/-- A union of a family of preconnected sets with a common point is preconnected as well. -/
theorem is_preconnected_sUnion (x : Œ±) (c : set (set Œ±)) (H1 : ‚àÄ s ‚àà c, x ‚àà s)
  (H2 : ‚àÄ s ‚àà c, is_preconnected s) : is_preconnected (‚ãÉ‚ÇÄ c) :=
begin
  apply is_preconnected_of_forall x,
  rintros y ‚ü®s, sc, ys‚ü©,
  exact ‚ü®s, subset_sUnion_of_mem sc, H1 s sc, ys, H2 s sc‚ü©
end

theorem is_preconnected.union (x : Œ±) {s t : set Œ±} (H1 : x ‚àà s) (H2 : x ‚àà t)
  (H3 : is_preconnected s) (H4 : is_preconnected t) : is_preconnected (s ‚à™ t) :=
sUnion_pair s t ‚ñ∏ is_preconnected_sUnion x {s, t}
  (by rintro r (rfl | rfl | h); [exact H2, exact H1, exact h.elim])
  (by rintro r (rfl | rfl | h); [exact H4, exact H3, exact h.elim])

theorem is_connected.union {s t : set Œ±} (H : (s ‚à© t).nonempty)
  (Hs : is_connected s) (Ht : is_connected t) : is_connected (s ‚à™ t) :=
begin
  rcases H with ‚ü®x, hx‚ü©,
  refine ‚ü®‚ü®x, mem_union_left t (mem_of_mem_inter_left hx)‚ü©, _‚ü©,
  exact is_preconnected.union x (mem_of_mem_inter_left hx) (mem_of_mem_inter_right hx)
    Hs.is_preconnected Ht.is_preconnected
end

theorem is_preconnected.closure {s : set Œ±} (H : is_preconnected s) :
  is_preconnected (closure s) :=
Œª u v hu hv hcsuv ‚ü®y, hycs, hyu‚ü© ‚ü®z, hzcs, hzv‚ü©,
let ‚ü®p, hpu, hps‚ü© := mem_closure_iff.1 hycs u hu hyu in
let ‚ü®q, hqv, hqs‚ü© := mem_closure_iff.1 hzcs v hv hzv in
let ‚ü®r, hrs, hruv‚ü© := H u v hu hv (subset.trans subset_closure hcsuv) ‚ü®p, hps, hpu‚ü© ‚ü®q, hqs, hqv‚ü© in
‚ü®r, subset_closure hrs, hruv‚ü©

theorem is_connected.closure {s : set Œ±} (H : is_connected s) :
  is_connected (closure s) :=
‚ü®H.nonempty.closure, H.is_preconnected.closure‚ü©

theorem is_preconnected.image [topological_space Œ≤] {s : set Œ±} (H : is_preconnected s)
  (f : Œ± ‚Üí Œ≤) (hf : continuous_on f s) : is_preconnected (f '' s) :=
begin
  -- Unfold/destruct definitions in hypotheses
  rintros u v hu hv huv ‚ü®_, ‚ü®x, xs, rfl‚ü©, xu‚ü© ‚ü®_, ‚ü®y, ys, rfl‚ü©, yv‚ü©,
  rcases continuous_on_iff'.1 hf u hu with ‚ü®u', hu', u'_eq‚ü©,
  rcases continuous_on_iff'.1 hf v hv with ‚ü®v', hv', v'_eq‚ü©,
  -- Reformulate `huv : f '' s ‚äÜ u ‚à™ v` in terms of `u'` and `v'`
  replace huv : s ‚äÜ u' ‚à™ v',
  { rw [image_subset_iff, preimage_union] at huv,
    replace huv := subset_inter huv (subset.refl _),
    rw [inter_distrib_right, u'_eq, v'_eq, ‚Üê inter_distrib_right] at huv,
    exact (subset_inter_iff.1 huv).1 },
  -- Now `s ‚äÜ u' ‚à™ v'`, so we can apply `‚Äπis_preconnected s‚Ä∫`
  obtain ‚ü®z, hz‚ü© : (s ‚à© (u' ‚à© v')).nonempty,
  { refine H u' v' hu' hv' huv ‚ü®x, _‚ü© ‚ü®y, _‚ü©; rw inter_comm,
    exacts [u'_eq ‚ñ∏ ‚ü®xu, xs‚ü©, v'_eq ‚ñ∏ ‚ü®yv, ys‚ü©] },
  rw [‚Üê inter_self s, inter_assoc, inter_left_comm s u', ‚Üê inter_assoc,
    inter_comm s, inter_comm s, ‚Üê u'_eq, ‚Üê v'_eq] at hz,
  exact ‚ü®f z, ‚ü®z, hz.1.2, rfl‚ü©, hz.1.1, hz.2.1‚ü©
end

theorem is_connected.image [topological_space Œ≤] {s : set Œ±} (H : is_connected s)
  (f : Œ± ‚Üí Œ≤) (hf : continuous_on f s) : is_connected (f '' s) :=
‚ü®nonempty_image_iff.mpr H.nonempty, H.is_preconnected.image f hf‚ü©

theorem is_preconnected_closed_iff {s : set Œ±} :
  is_preconnected s ‚Üî ‚àÄ t t', is_closed t ‚Üí is_closed t' ‚Üí s ‚äÜ t ‚à™ t' ‚Üí
    (s ‚à© t).nonempty ‚Üí (s ‚à© t').nonempty ‚Üí (s ‚à© (t ‚à© t')).nonempty :=
‚ü®begin
  rintros h t t' ht ht' htt' ‚ü®x, xs, xt‚ü© ‚ü®y, ys, yt'‚ü©,
  by_contradiction h',
  rw [‚Üê ne_empty_iff_nonempty, ne.def, not_not, ‚Üê subset_compl_iff_disjoint, compl_inter] at h',
  have xt' : x ‚àâ t', from (h' xs).elim (absurd xt) id,
  have yt : y ‚àâ t, from (h' ys).elim id (absurd yt'),
  have := ne_empty_iff_nonempty.2 (h (-t) (-t') (is_open_compl_iff.2 ht)
    (is_open_compl_iff.2 ht') h' ‚ü®y, ys, yt‚ü© ‚ü®x, xs, xt'‚ü©),
  rw [ne.def, ‚Üê compl_union, ‚Üê subset_compl_iff_disjoint, compl_compl] at this,
  contradiction
end,
begin
  rintros h u v hu hv huv ‚ü®x, xs, xu‚ü© ‚ü®y, ys, yv‚ü©,
  by_contradiction h',
  rw [‚Üê ne_empty_iff_nonempty, ne.def, not_not,
    ‚Üê subset_compl_iff_disjoint, compl_inter] at h',
  have xv : x ‚àâ v, from (h' xs).elim (absurd xu) id,
  have yu : y ‚àâ u, from (h' ys).elim id (absurd yv),
  have := ne_empty_iff_nonempty.2 (h (-u) (-v) (is_closed_compl_iff.2 hu)
    (is_closed_compl_iff.2 hv) h' ‚ü®y, ys, yu‚ü© ‚ü®x, xs, xv‚ü©),
  rw [ne.def, ‚Üê compl_union, ‚Üê subset_compl_iff_disjoint, compl_compl] at this,
  contradiction
end‚ü©

/-- The connected component of a point is the maximal connected set
that contains this point. -/
def connected_component (x : Œ±) : set Œ± :=
‚ãÉ‚ÇÄ { s : set Œ± | is_preconnected s ‚àß x ‚àà s }

theorem mem_connected_component {x : Œ±} : x ‚àà connected_component x :=
mem_sUnion_of_mem (mem_singleton x) ‚ü®is_connected_singleton.is_preconnected, mem_singleton x‚ü©

theorem is_connected_connected_component {x : Œ±} : is_connected (connected_component x) :=
‚ü®‚ü®x, mem_connected_component‚ü©, is_preconnected_sUnion x _ (Œª _, and.right) (Œª _, and.left)‚ü©

theorem subset_connected_component {x : Œ±} {s : set Œ±} (H1 : is_preconnected s) (H2 : x ‚àà s) :
  s ‚äÜ connected_component x :=
Œª z hz, mem_sUnion_of_mem hz ‚ü®H1, H2‚ü©

theorem is_closed_connected_component {x : Œ±} :
  is_closed (connected_component x) :=
closure_eq_iff_is_closed.1 $ subset.antisymm
  (subset_connected_component
    is_connected_connected_component.closure.is_preconnected
    (subset_closure mem_connected_component))
  subset_closure

theorem irreducible_component_subset_connected_component {x : Œ±} :
  irreducible_component x ‚äÜ connected_component x :=
subset_connected_component
  is_irreducible_irreducible_component.is_connected.is_preconnected
  mem_irreducible_component

/-- A preconnected space is one where there is no non-trivial open partition. -/
class preconnected_space (Œ± : Type u) [topological_space Œ±] : Prop :=
(is_preconnected_univ : is_preconnected (univ : set Œ±))

/-- A connected space is a nonempty one where there is no non-trivial open partition. -/
class connected_space (Œ± : Type u) [topological_space Œ±] extends preconnected_space Œ± : Prop :=
(to_nonempty : nonempty Œ±)

attribute [instance] connected_space.to_nonempty

@[priority 100] -- see Note [lower instance priority]
instance preirreducible_space.preconnected_space (Œ± : Type u) [topological_space Œ±]
  [preirreducible_space Œ±] : preconnected_space Œ± :=
‚ü®(preirreducible_space.is_preirreducible_univ Œ±).is_preconnected‚ü©

@[priority 100] -- see Note [lower instance priority]
instance irreducible_space.connected_space (Œ± : Type u) [topological_space Œ±]
  [irreducible_space Œ±] : connected_space Œ± :=
{ to_nonempty := irreducible_space.to_nonempty Œ± }

theorem nonempty_inter [preconnected_space Œ±] {s t : set Œ±} :
  is_open s ‚Üí is_open t ‚Üí s ‚à™ t = univ ‚Üí s.nonempty ‚Üí t.nonempty ‚Üí (s ‚à© t).nonempty :=
by simpa only [univ_inter, univ_subset_iff] using
  @preconnected_space.is_preconnected_univ Œ± _ _ s t

theorem is_clopen_iff [preconnected_space Œ±] {s : set Œ±} : is_clopen s ‚Üî s = ‚àÖ ‚à® s = univ :=
‚ü®Œª hs, classical.by_contradiction $ Œª h,
  have h1 : s ‚â† ‚àÖ ‚àß -s ‚â† ‚àÖ, from ‚ü®mt or.inl h,
    mt (Œª h2, or.inr $ (by rw [‚Üê compl_compl s, h2, compl_empty] : s = univ)) h‚ü©,
  let ‚ü®_, h2, h3‚ü© := nonempty_inter hs.1 hs.2 (union_compl_self s)
    (ne_empty_iff_nonempty.1 h1.1) (ne_empty_iff_nonempty.1 h1.2) in
  h3 h2,
by rintro (rfl | rfl); [exact is_clopen_empty, exact is_clopen_univ]‚ü©

lemma subtype.preconnected_space {s : set Œ±} (h : is_preconnected s) :
  preconnected_space s :=
{ is_preconnected_univ :=
  begin
    intros u v hu hv hs hsu hsv,
    rw is_open_induced_iff at hu hv,
    rcases hu with ‚ü®u, hu, rfl‚ü©,
    rcases hv with ‚ü®v, hv, rfl‚ü©,
    rcases hsu with ‚ü®‚ü®x, hxs‚ü©, hxs', hxu‚ü©,
    rcases hsv with ‚ü®‚ü®y, hys‚ü©, hys', hyv‚ü©,
    rcases h u v hu hv _ ‚ü®x, hxs, hxu‚ü© ‚ü®y, hys, hyv‚ü© with ‚ü®z, hzs, ‚ü®hzu, hzv‚ü©‚ü©,
    exact ‚ü®‚ü®z, hzs‚ü©, ‚ü®set.mem_univ _, ‚ü®hzu, hzv‚ü©‚ü©‚ü©,
    intros z hz,
    rcases hs (set.mem_univ ‚ü®z, hz‚ü©) with hzu|hzv,
    { left, assumption },
    { right, assumption }
  end }

lemma subtype.connected_space {s : set Œ±} (h : is_connected s) :
  connected_space s :=
{ is_preconnected_univ :=
  (subtype.preconnected_space h.is_preconnected).is_preconnected_univ,
  to_nonempty := h.nonempty.to_subtype }

/-- A set `s` is preconnected if and only if
for every cover by two open sets that are disjoint on `s`,
it is contained in one of the two covering sets. -/
lemma is_preconnected_iff_subset_of_disjoint {s : set Œ±} :
  is_preconnected s ‚Üî
  ‚àÄ (u v : set Œ±) (hu : is_open u) (hv : is_open v) (hs : s ‚äÜ u ‚à™ v) (huv : s ‚à© (u ‚à© v) = ‚àÖ),
  s ‚äÜ u ‚à® s ‚äÜ v :=
begin
  split; intro h,
  { intros u v hu hv hs huv,
    specialize h u v hu hv hs,
    contrapose! huv,
    rw ne_empty_iff_nonempty,
    simp [not_subset] at huv,
    rcases huv with ‚ü®‚ü®x, hxs, hxu‚ü©, ‚ü®y, hys, hyv‚ü©‚ü©,
    have hxv : x ‚àà v := classical.or_iff_not_imp_left.mp (hs hxs) hxu,
    have hyu : y ‚àà u := classical.or_iff_not_imp_right.mp (hs hys) hyv,
    exact h ‚ü®y, hys, hyu‚ü© ‚ü®x, hxs, hxv‚ü© },
  { intros u v hu hv hs hsu hsv,
    rw ‚Üê ne_empty_iff_nonempty,
    intro H,
    specialize h u v hu hv hs H,
    contrapose H,
    apply ne_empty_iff_nonempty.mpr,
    cases h,
    { rcases hsv with ‚ü®x, hxs, hxv‚ü©, exact ‚ü®x, hxs, ‚ü®h hxs, hxv‚ü©‚ü© },
    { rcases hsu with ‚ü®x, hxs, hxu‚ü©, exact ‚ü®x, hxs, ‚ü®hxu, h hxs‚ü©‚ü© } }
end

/-- A set `s` is connected if and only if
for every cover by a finite collection of open sets that are pairwise disjoint on `s`,
it is contained in one of the members of the collection. -/
lemma is_connected_iff_sUnion_disjoint_open {s : set Œ±} :
  is_connected s ‚Üî
  ‚àÄ (U : finset (set Œ±)) (H : ‚àÄ (u v : set Œ±), u ‚àà U ‚Üí v ‚àà U ‚Üí (s ‚à© (u ‚à© v)).nonempty ‚Üí u = v)
  (hU : ‚àÄ u ‚àà U, is_open u) (hs : s ‚äÜ ‚ãÉ‚ÇÄ ‚ÜëU),
  ‚àÉ u ‚àà U, s ‚äÜ u :=
begin
  rw [is_connected, is_preconnected_iff_subset_of_disjoint],
  split; intro h,
  { intro U, apply finset.induction_on U,
    { rcases h.left,
      suffices : s ‚äÜ ‚àÖ ‚Üí false, { simpa },
      intro, solve_by_elim },
    { intros u U hu IH hs hU H,
      rw [finset.coe_insert, sUnion_insert] at H,
      cases h.2 u (‚ãÉ‚ÇÄ ‚ÜëU) _ _ H _ with hsu hsU,
      { exact ‚ü®u, finset.mem_insert_self _ _, hsu‚ü© },
      { rcases IH _ _ hsU with ‚ü®v, hvU, hsv‚ü©,
        { exact ‚ü®v, finset.mem_insert_of_mem hvU, hsv‚ü© },
        { intros, apply hs; solve_by_elim [finset.mem_insert_of_mem] },
        { intros, solve_by_elim [finset.mem_insert_of_mem] } },
      { solve_by_elim [finset.mem_insert_self] },
      { apply is_open_sUnion,
        intros, solve_by_elim [finset.mem_insert_of_mem] },
      { apply eq_empty_of_subset_empty,
        rintro x ‚ü®hxs, hxu, hxU‚ü©,
        rw mem_sUnion at hxU,
        rcases hxU with ‚ü®v, hvU, hxv‚ü©,
        rcases hs u v (finset.mem_insert_self _ _) (finset.mem_insert_of_mem hvU) _ with rfl,
        { contradiction },
        { exact ‚ü®x, hxs, hxu, hxv‚ü© } } } },
  { split,
    { rw ‚Üê ne_empty_iff_nonempty,
      by_contradiction hs, push_neg at hs, subst hs,
      simpa using h ‚àÖ _ _ _; simp },
    intros u v hu hv hs hsuv,
    rcases h {v, u} _ _ _ with ‚ü®t, ht, ht'‚ü©,
    { rw [finset.insert_empty_eq_singleton, finset.has_insert_eq_insert,
          finset.mem_insert, finset.mem_singleton] at ht,
      rcases ht with rfl|rfl; tauto },
    { intros t‚ÇÅ t‚ÇÇ ht‚ÇÅ ht‚ÇÇ hst,
      rw ‚Üê ne_empty_iff_nonempty at hst,
      rw [finset.insert_empty_eq_singleton, finset.has_insert_eq_insert,
          finset.mem_insert, finset.mem_singleton] at ht‚ÇÅ ht‚ÇÇ,
      rcases ht‚ÇÅ with rfl|rfl; rcases ht‚ÇÇ with rfl|rfl,
      all_goals { refl <|> contradiction <|> skip },
      rw inter_comm t‚ÇÅ at hst, contradiction },
    { intro t,
      rw [finset.insert_empty_eq_singleton, finset.has_insert_eq_insert,
          finset.mem_insert, finset.mem_singleton],
      rintro (rfl|rfl); assumption },
    { simpa using hs } }
end

end preconnected

section totally_disconnected

/-- A set is called totally disconnected if all of its connected components are singletons. -/
def is_totally_disconnected (s : set Œ±) : Prop :=
‚àÄ t, t ‚äÜ s ‚Üí is_preconnected t ‚Üí subsingleton t

theorem is_totally_disconnected_empty : is_totally_disconnected (‚àÖ : set Œ±) :=
Œª t ht _, ‚ü®Œª ‚ü®_, h‚ü©, (ht h).elim‚ü©

theorem is_totally_disconnected_singleton {x} : is_totally_disconnected ({x} : set Œ±) :=
Œª t ht _, ‚ü®Œª ‚ü®p, hp‚ü© ‚ü®q, hq‚ü©, subtype.eq $ show p = q,
from (eq_of_mem_singleton (ht hp)).symm ‚ñ∏ (eq_of_mem_singleton (ht hq)).symm‚ü©

/-- A space is totally disconnected if all of its connected components are singletons. -/
class totally_disconnected_space (Œ± : Type u) [topological_space Œ±] : Prop :=
(is_totally_disconnected_univ : is_totally_disconnected (univ : set Œ±))

end totally_disconnected

section totally_separated

/-- A set `s` is called totally separated if any two points of this set can be separated
by two disjoint open sets covering `s`. -/
def is_totally_separated (s : set Œ±) : Prop :=
‚àÄ x ‚àà s, ‚àÄ y ‚àà s, x ‚â† y ‚Üí ‚àÉ u v : set Œ±, is_open u ‚àß is_open v ‚àß
  x ‚àà u ‚àß y ‚àà v ‚àß s ‚äÜ u ‚à™ v ‚àß u ‚à© v = ‚àÖ

theorem is_totally_separated_empty : is_totally_separated (‚àÖ : set Œ±) :=
Œª x, false.elim

theorem is_totally_separated_singleton {x} : is_totally_separated ({x} : set Œ±) :=
Œª p hp q hq hpq, (hpq $ (eq_of_mem_singleton hp).symm ‚ñ∏ (eq_of_mem_singleton hq).symm).elim

theorem is_totally_disconnected_of_is_totally_separated {s : set Œ±}
  (H : is_totally_separated s) : is_totally_disconnected s :=
Œª t hts ht, ‚ü®Œª ‚ü®x, hxt‚ü© ‚ü®y, hyt‚ü©, subtype.eq $ classical.by_contradiction $
assume hxy : x ‚â† y, let ‚ü®u, v, hu, hv, hxu, hyv, hsuv, huv‚ü© := H x (hts hxt) y (hts hyt) hxy in
let ‚ü®r, hrt, hruv‚ü© := ht u v hu hv (subset.trans hts hsuv) ‚ü®x, hxt, hxu‚ü© ‚ü®y, hyt, hyv‚ü© in
((ext_iff _ _).1 huv r).1 hruv‚ü©

/-- A space is totally separated if any two points can be separated by two disjoint open sets
covering the whole space. -/
class totally_separated_space (Œ± : Type u) [topological_space Œ±] : Prop :=
(is_totally_separated_univ : is_totally_separated (univ : set Œ±))

@[priority 100] -- see Note [lower instance priority]
instance totally_separated_space.totally_disconnected_space (Œ± : Type u) [topological_space Œ±]
  [totally_separated_space Œ±] : totally_disconnected_space Œ± :=
‚ü®is_totally_disconnected_of_is_totally_separated $ totally_separated_space.is_totally_separated_univ Œ±‚ü©

end totally_separated
