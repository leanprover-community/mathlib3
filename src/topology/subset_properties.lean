/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov
-/
import topology.bases
import data.finset.order
import data.set.accumulate
import tactic.tfae

/-!
# Properties of subsets of topological spaces

In this file we define various properties of subsets of a topological space, and some classes on
topological spaces.

## Main definitions

We define the following properties for sets in a topological space:

* `is_compact`: each open cover has a finite subcover. This is defined in mathlib using filters.
  The main property of a compact set is `is_compact.elim_finite_subcover`.
* `is_clopen`: a set that is both open and closed.
* `is_irreducible`: a nonempty set that has contains no non-trivial pair of disjoint opens.
  See also the section below in the module doc.

For each of these definitions (except for `is_clopen`), we also have a class stating that the whole
space satisfies that property:
`compact_space`, `irreducible_space`

Furthermore, we have two more classes:
* `locally_compact_space`: for every point `x`, every open neighborhood of `x` contains a compact
  neighborhood of `x`. The definition is formulated in terms of the neighborhood filter.
* `sigma_compact_space`: a space that is the union of a countably many compact subspaces.

## On the definition of irreducible and connected sets/spaces

In informal mathematics, irreducible spaces are assumed to be nonempty.
We formalise the predicate without that assumption as `is_preirreducible`.
In other words, the only difference is whether the empty space counts as irreducible.
There are good reasons to consider the empty space to be â€œtoo simple to be simpleâ€
See also https://ncatlab.org/nlab/show/too+simple+to+be+simple,
and in particular
https://ncatlab.org/nlab/show/too+simple+to+be+simple#relationship_to_biased_definitions.
-/

open set filter classical topological_space
open_locale classical topological_space filter

universes u v
variables {Î± : Type u} {Î² : Type v} [topological_space Î±] {s t : set Î±}

/- compact sets -/
section compact

/-- A set `s` is compact if for every nontrivial filter `f` that contains `s`,
    there exists `a âˆˆ s` such that every set of `f` meets every neighborhood of `a`. -/
def is_compact (s : set Î±) := âˆ€ â¦ƒfâ¦„ [ne_bot f], f â‰¤ ğ“Ÿ s â†’ âˆƒaâˆˆs, cluster_pt a f

/-- The complement to a compact set belongs to a filter `f` if it belongs to each filter
`ğ“ a âŠ“ f`, `a âˆˆ s`. -/
lemma is_compact.compl_mem_sets (hs : is_compact s) {f : filter Î±} (hf : âˆ€ a âˆˆ s, sá¶œ âˆˆ ğ“ a âŠ“ f) :
  sá¶œ âˆˆ f :=
begin
  contrapose! hf,
  simp only [not_mem_iff_inf_principal_compl, compl_compl, inf_assoc, â† exists_prop] at hf âŠ¢,
  exact @hs _ hf inf_le_right
end

/-- The complement to a compact set belongs to a filter `f` if each `a âˆˆ s` has a neighborhood `t`
within `s` such that `tá¶œ` belongs to `f`. -/
lemma is_compact.compl_mem_sets_of_nhds_within (hs : is_compact s) {f : filter Î±}
  (hf : âˆ€ a âˆˆ s, âˆƒ t âˆˆ ğ“[s] a, tá¶œ âˆˆ f) :
  sá¶œ âˆˆ f :=
begin
  refine hs.compl_mem_sets (Î» a ha, _),
  rcases hf a ha with âŸ¨t, ht, hstâŸ©,
  replace ht := mem_inf_principal.1 ht,
  apply mem_inf_of_inter ht hst,
  rintros x âŸ¨hâ‚, hâ‚‚âŸ© hs,
  exact hâ‚‚ (hâ‚ hs)
end

/-- If `p : set Î± â†’ Prop` is stable under restriction and union, and each point `x`
  of a compact set `s` has a neighborhood `t` within `s` such that `p t`, then `p s` holds. -/
@[elab_as_eliminator]
lemma is_compact.induction_on {s : set Î±} (hs : is_compact s) {p : set Î± â†’ Prop} (he : p âˆ…)
  (hmono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ p t â†’ p s) (hunion : âˆ€ â¦ƒs tâ¦„, p s â†’ p t â†’ p (s âˆª t))
  (hnhds : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, p t) :
  p s :=
let f : filter Î± :=
  { sets := {t | p tá¶œ},
    univ_sets := by simpa,
    sets_of_superset := Î» tâ‚ tâ‚‚ htâ‚ ht, hmono (compl_subset_compl.2 ht) htâ‚,
    inter_sets := Î» tâ‚ tâ‚‚ htâ‚ htâ‚‚, by simp [compl_inter, hunion htâ‚ htâ‚‚] } in
have sá¶œ âˆˆ f, from hs.compl_mem_sets_of_nhds_within (by simpa using hnhds),
by simpa

/-- The intersection of a compact set and a closed set is a compact set. -/
lemma is_compact.inter_right (hs : is_compact s) (ht : is_closed t) :
  is_compact (s âˆ© t) :=
begin
  introsI f hnf hstf,
  obtain âŸ¨a, hsa, haâŸ© : âˆƒ a âˆˆ s, cluster_pt a f :=
    hs (le_trans hstf (le_principal_iff.2 (inter_subset_left _ _))),
  have : a âˆˆ t :=
    (ht.mem_of_nhds_within_ne_bot $ ha.mono $
      le_trans hstf (le_principal_iff.2 (inter_subset_right _ _))),
  exact âŸ¨a, âŸ¨hsa, thisâŸ©, haâŸ©
end

/-- The intersection of a closed set and a compact set is a compact set. -/
lemma is_compact.inter_left (ht : is_compact t) (hs : is_closed s) : is_compact (s âˆ© t) :=
inter_comm t s â–¸ ht.inter_right hs

/-- The set difference of a compact set and an open set is a compact set. -/
lemma is_compact.diff (hs : is_compact s) (ht : is_open t) : is_compact (s \ t) :=
hs.inter_right (is_closed_compl_iff.mpr ht)

/-- A closed subset of a compact set is a compact set. -/
lemma compact_of_is_closed_subset (hs : is_compact s) (ht : is_closed t) (h : t âŠ† s) :
  is_compact t :=
inter_eq_self_of_subset_right h â–¸ hs.inter_right ht

lemma is_compact.adherence_nhdset {f : filter Î±}
  (hs : is_compact s) (hfâ‚‚ : f â‰¤ ğ“Ÿ s) (htâ‚ : is_open t) (htâ‚‚ : âˆ€aâˆˆs, cluster_pt a f â†’ a âˆˆ t) :
  t âˆˆ f :=
classical.by_cases mem_of_eq_bot $
  assume : f âŠ“ ğ“Ÿ tá¶œ â‰  âŠ¥,
  let âŸ¨a, ha, (hfa : cluster_pt a $ f âŠ“ ğ“Ÿ tá¶œ)âŸ© := @@hs âŸ¨thisâŸ© $ inf_le_of_left_le hfâ‚‚ in
  have a âˆˆ t,
    from htâ‚‚ a ha (hfa.of_inf_left),
  have tá¶œ âˆ© t âˆˆ ğ“[tá¶œ] a,
    from inter_mem_nhds_within _ (is_open.mem_nhds htâ‚ this),
  have A : ğ“[tá¶œ] a = âŠ¥,
    from empty_mem_iff_bot.1 $ compl_inter_self t â–¸ this,
  have ğ“[tá¶œ] a â‰  âŠ¥,
    from hfa.of_inf_right.ne,
  absurd A this

lemma is_compact_iff_ultrafilter_le_nhds :
  is_compact s â†” (âˆ€f : ultrafilter Î±, â†‘f â‰¤ ğ“Ÿ s â†’ âˆƒaâˆˆs, â†‘f â‰¤ ğ“ a) :=
begin
  refine (forall_ne_bot_le_iff _).trans _,
  { rintro f g hle âŸ¨a, has, hafâŸ©,
    exact âŸ¨a, has, haf.mono hleâŸ© },
  { simp only [ultrafilter.cluster_pt_iff] }
end

alias is_compact_iff_ultrafilter_le_nhds â†” is_compact.ultrafilter_le_nhds _

/-- For every open directed cover of a compact set, there exists a single element of the
cover which itself includes the set. -/
lemma is_compact.elim_directed_cover {Î¹ : Type v} [hÎ¹ : nonempty Î¹] (hs : is_compact s)
  (U : Î¹ â†’ set Î±) (hUo : âˆ€i, is_open (U i)) (hsU : s âŠ† â‹ƒ i, U i) (hdU : directed (âŠ†) U) :
  âˆƒ i, s âŠ† U i :=
hÎ¹.elim $ Î» iâ‚€, is_compact.induction_on hs âŸ¨iâ‚€, empty_subset _âŸ©
  (Î» sâ‚ sâ‚‚ hs âŸ¨i, hiâŸ©, âŸ¨i, subset.trans hs hiâŸ©)
  (Î» sâ‚ sâ‚‚ âŸ¨i, hiâŸ© âŸ¨j, hjâŸ©, let âŸ¨k, hki, hkjâŸ© := hdU i j in
    âŸ¨k, union_subset (subset.trans hi hki) (subset.trans hj hkj)âŸ©)
  (Î» x hx, let âŸ¨i, hiâŸ© := mem_Union.1 (hsU hx) in
    âŸ¨U i, mem_nhds_within_of_mem_nhds (is_open.mem_nhds (hUo i) hi), i, subset.refl _âŸ©)

/-- For every open cover of a compact set, there exists a finite subcover. -/
lemma is_compact.elim_finite_subcover {Î¹ : Type v} (hs : is_compact s)
  (U : Î¹ â†’ set Î±) (hUo : âˆ€i, is_open (U i)) (hsU : s âŠ† â‹ƒ i, U i) :
  âˆƒ t : finset Î¹, s âŠ† â‹ƒ i âˆˆ t, U i :=
hs.elim_directed_cover _ (Î» t, is_open_bUnion $ Î» i _, hUo i) (Union_eq_Union_finset U â–¸ hsU)
  (directed_of_sup $ Î» tâ‚ tâ‚‚ h, bUnion_subset_bUnion_left h)

lemma is_compact.elim_nhds_subcover' (hs : is_compact s) (U : Î  x âˆˆ s, set Î±)
  (hU : âˆ€ x âˆˆ s, U x â€¹x âˆˆ sâ€º âˆˆ ğ“ x) :
  âˆƒ t : finset s, s âŠ† â‹ƒ x âˆˆ t, U (x : s) x.2 :=
(hs.elim_finite_subcover (Î» x : s, interior (U x x.2)) (Î» x, is_open_interior)
  (Î» x hx, mem_Union.2 âŸ¨âŸ¨x, hxâŸ©, mem_interior_iff_mem_nhds.2 $ hU _ _âŸ©)).imp $ Î» t ht,
subset.trans ht $ bUnion_mono $ Î» _ _, interior_subset

lemma is_compact.elim_nhds_subcover (hs : is_compact s) (U : Î± â†’ set Î±) (hU : âˆ€ x âˆˆ s, U x âˆˆ ğ“ x) :
  âˆƒ t : finset Î±, (âˆ€ x âˆˆ t, x âˆˆ s) âˆ§ s âŠ† â‹ƒ x âˆˆ t, U x :=
let âŸ¨t, htâŸ© := hs.elim_nhds_subcover' (Î» x _, U x) hU
in âŸ¨t.image coe, Î» x hx, let âŸ¨y, hyt, hyxâŸ© := finset.mem_image.1 hx in hyx â–¸ y.2,
  by rwa finset.set_bUnion_finset_imageâŸ©

/-- For every family of closed sets whose intersection avoids a compact set,
there exists a finite subfamily whose intersection avoids this compact set. -/
lemma is_compact.elim_finite_subfamily_closed {s : set Î±} {Î¹ : Type v} (hs : is_compact s)
  (Z : Î¹ â†’ set Î±) (hZc : âˆ€i, is_closed (Z i)) (hsZ : s âˆ© (â‹‚ i, Z i) = âˆ…) :
  âˆƒ t : finset Î¹, s âˆ© (â‹‚ i âˆˆ t, Z i) = âˆ… :=
let âŸ¨t, htâŸ© := hs.elim_finite_subcover (Î» i, (Z i)á¶œ) (Î» i, (hZc i).is_open_compl)
  (by simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, mem_Union,
    exists_prop, mem_inter_eq, not_and, iff_self, mem_Inter, mem_compl_eq] using hsZ)
    in
âŸ¨t, by simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, mem_Union,
    exists_prop, mem_inter_eq, not_and, iff_self, mem_Inter, mem_compl_eq] using htâŸ©

/-- If `s` is a compact set in a topological space `Î±` and `f : Î¹ â†’ set Î±` is a locally finite
family of sets, then `f i âˆ© s` is nonempty only for a finitely many `i`. -/
lemma locally_finite.finite_nonempty_inter_compact {Î¹ : Type*} {f : Î¹ â†’ set Î±}
  (hf : locally_finite f) {s : set Î±} (hs : is_compact s) :
  finite {i | (f i âˆ© s).nonempty} :=
begin
  choose U hxU hUf using hf,
  rcases hs.elim_nhds_subcover U (Î» x _, hxU x) with âŸ¨t, -, hsUâŸ©,
  refine (t.finite_to_set.bUnion (Î» x _, hUf x)).subset _,
  rintro i âŸ¨x, hxâŸ©,
  rcases mem_bUnion_iff.1 (hsU hx.2) with âŸ¨c, hct, hcxâŸ©,
  exact mem_bUnion hct âŸ¨x, hx.1, hcxâŸ©
end

/-- To show that a compact set intersects the intersection of a family of closed sets,
  it is sufficient to show that it intersects every finite subfamily. -/
lemma is_compact.inter_Inter_nonempty {s : set Î±} {Î¹ : Type v} (hs : is_compact s)
  (Z : Î¹ â†’ set Î±) (hZc : âˆ€i, is_closed (Z i)) (hsZ : âˆ€ t : finset Î¹, (s âˆ© â‹‚ i âˆˆ t, Z i).nonempty) :
  (s âˆ© â‹‚ i, Z i).nonempty :=
begin
  simp only [â† ne_empty_iff_nonempty] at hsZ âŠ¢,
  apply mt (hs.elim_finite_subfamily_closed Z hZc), push_neg, exact hsZ
end

/-- Cantor's intersection theorem:
the intersection of a directed family of nonempty compact closed sets is nonempty. -/
lemma is_compact.nonempty_Inter_of_directed_nonempty_compact_closed
  {Î¹ : Type v} [hÎ¹ : nonempty Î¹] (Z : Î¹ â†’ set Î±) (hZd : directed (âŠ‡) Z)
  (hZn : âˆ€ i, (Z i).nonempty) (hZc : âˆ€ i, is_compact (Z i)) (hZcl : âˆ€ i, is_closed (Z i)) :
  (â‹‚ i, Z i).nonempty :=
begin
  apply hÎ¹.elim,
  intro iâ‚€,
  let Z' := Î» i, Z i âˆ© Z iâ‚€,
  suffices : (â‹‚ i, Z' i).nonempty,
  { exact nonempty.mono (Inter_subset_Inter $ assume i, inter_subset_left (Z i) (Z iâ‚€)) this },
  rw â† ne_empty_iff_nonempty,
  intro H,
  obtain âŸ¨t, htâŸ© : âˆƒ (t : finset Î¹), ((Z iâ‚€) âˆ© â‹‚ (i âˆˆ t), Z' i) = âˆ…,
    from (hZc iâ‚€).elim_finite_subfamily_closed Z'
      (assume i, is_closed.inter (hZcl i) (hZcl iâ‚€)) (by rw [H, inter_empty]),
  obtain âŸ¨iâ‚, hiâ‚âŸ© : âˆƒ iâ‚ : Î¹, Z iâ‚ âŠ† Z iâ‚€ âˆ§ âˆ€ i âˆˆ t, Z iâ‚ âŠ† Z' i,
  { rcases directed.finset_le hZd t with âŸ¨i, hiâŸ©,
    rcases hZd i iâ‚€ with âŸ¨iâ‚, hiâ‚, hiâ‚â‚€âŸ©,
    use [iâ‚, hiâ‚â‚€],
    intros j hj,
    exact subset_inter (subset.trans hiâ‚ (hi j hj)) hiâ‚â‚€ },
  suffices : ((Z iâ‚€) âˆ© â‹‚ (i âˆˆ t), Z' i).nonempty,
  { rw â† ne_empty_iff_nonempty at this, contradiction },
  refine nonempty.mono _ (hZn iâ‚),
  exact subset_inter hiâ‚.left (subset_bInter hiâ‚.right)
end

/-- Cantor's intersection theorem for sequences indexed by `â„•`:
the intersection of a decreasing sequence of nonempty compact closed sets is nonempty. -/
lemma is_compact.nonempty_Inter_of_sequence_nonempty_compact_closed
  (Z : â„• â†’ set Î±) (hZd : âˆ€ i, Z (i+1) âŠ† Z i)
  (hZn : âˆ€ i, (Z i).nonempty) (hZ0 : is_compact (Z 0)) (hZcl : âˆ€ i, is_closed (Z i)) :
  (â‹‚ i, Z i).nonempty :=
have Zmono : antitone Z := antitone_nat_of_succ_le hZd,
have hZd : directed (âŠ‡) Z, from directed_of_sup Zmono,
have âˆ€ i, Z i âŠ† Z 0, from assume i, Zmono $ zero_le i,
have hZc : âˆ€ i, is_compact (Z i), from assume i, compact_of_is_closed_subset hZ0 (hZcl i) (this i),
is_compact.nonempty_Inter_of_directed_nonempty_compact_closed Z hZd hZn hZc hZcl

/-- For every open cover of a compact set, there exists a finite subcover. -/
lemma is_compact.elim_finite_subcover_image {b : set Î²} {c : Î² â†’ set Î±}
  (hs : is_compact s) (hcâ‚ : âˆ€iâˆˆb, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒiâˆˆb, c i) :
  âˆƒb'âŠ†b, finite b' âˆ§ s âŠ† â‹ƒiâˆˆb', c i :=
begin
  rcases hs.elim_finite_subcover (Î» i, c i : b â†’ set Î±) _ _ with âŸ¨d, hdâŸ©;
    [skip, simpa using hcâ‚, simpa using hcâ‚‚],
  refine âŸ¨â†‘(d.image coe), _, finset.finite_to_set _, _âŸ©,
  { simp },
  { rwa [finset.coe_image, bUnion_image] }
end

/-- A set `s` is compact if for every family of closed sets whose intersection avoids `s`,
there exists a finite subfamily whose intersection avoids `s`. -/
theorem is_compact_of_finite_subfamily_closed
  (h : Î  {Î¹ : Type u} (Z : Î¹ â†’ (set Î±)), (âˆ€ i, is_closed (Z i)) â†’
    s âˆ© (â‹‚ i, Z i) = âˆ… â†’ (âˆƒ (t : finset Î¹), s âˆ© (â‹‚ i âˆˆ t, Z i) = âˆ…)) :
  is_compact s :=
assume f hfn hfs, classical.by_contradiction $ assume : Â¬ (âˆƒxâˆˆs, cluster_pt x f),
  have hf : âˆ€xâˆˆs, ğ“ x âŠ“ f = âŠ¥,
    by simpa only [cluster_pt, not_exists, not_not, ne_bot_iff],
  have Â¬ âˆƒxâˆˆs, âˆ€tâˆˆf.sets, x âˆˆ closure t,
    from assume âŸ¨x, hxs, hxâŸ©,
    have âˆ… âˆˆ ğ“ x âŠ“ f, by rw [empty_mem_iff_bot, hf x hxs],
    let âŸ¨tâ‚, htâ‚, tâ‚‚, htâ‚‚, htâŸ© := by rw [mem_inf_iff] at this; exact this in
    have âˆ… âˆˆ ğ“[tâ‚‚] x,
      by { rw [ht, inter_comm], exact inter_mem_nhds_within _ htâ‚ },
    have ğ“[tâ‚‚] x = âŠ¥,
      by rwa [empty_mem_iff_bot] at this,
    by simp only [closure_eq_cluster_pts] at hx; exact (hx tâ‚‚ htâ‚‚).ne this,
  let âŸ¨t, htâŸ© := h (Î» i : f.sets, closure i.1) (Î» i, is_closed_closure)
    (by simpa [eq_empty_iff_forall_not_mem, not_exists]) in
  have (â‹‚iâˆˆt, subtype.val i) âˆˆ f,
    from t.Inter_mem_sets.2 $ assume i hi, i.2,
  have s âˆ© (â‹‚iâˆˆt, subtype.val i) âˆˆ f,
    from inter_mem (le_principal_iff.1 hfs) this,
  have âˆ… âˆˆ f,
    from mem_of_superset this $ assume x âŸ¨hxs, hxâŸ©,
    let âŸ¨i, hit, hxiâŸ© := (show âˆƒi âˆˆ t, x âˆ‰ closure (subtype.val i),
      by { rw [eq_empty_iff_forall_not_mem] at ht, simpa [hxs, not_forall] using ht x }) in
    have x âˆˆ closure i.val, from subset_closure (mem_bInter_iff.mp hx i hit),
    show false, from hxi this,
  hfn.ne $ by rwa [empty_mem_iff_bot] at this

/-- A set `s` is compact if for every open cover of `s`, there exists a finite subcover. -/
lemma is_compact_of_finite_subcover
  (h : Î  {Î¹ : Type u} (U : Î¹ â†’ (set Î±)), (âˆ€ i, is_open (U i)) â†’
    s âŠ† (â‹ƒ i, U i) â†’ (âˆƒ (t : finset Î¹), s âŠ† (â‹ƒ i âˆˆ t, U i))) :
  is_compact s :=
is_compact_of_finite_subfamily_closed $
  assume Î¹ Z hZc hsZ,
  let âŸ¨t, htâŸ© := h (Î» i, (Z i)á¶œ) (assume i, is_open_compl_iff.mpr $ hZc i)
    (by simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, mem_Union,
      exists_prop, mem_inter_eq, not_and, iff_self, mem_Inter, mem_compl_eq] using hsZ)
      in
  âŸ¨t, by simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, mem_Union,
      exists_prop, mem_inter_eq, not_and, iff_self, mem_Inter, mem_compl_eq] using htâŸ©

/-- A set `s` is compact if and only if
for every open cover of `s`, there exists a finite subcover. -/
lemma is_compact_iff_finite_subcover :
  is_compact s â†” (Î  {Î¹ : Type u} (U : Î¹ â†’ (set Î±)), (âˆ€ i, is_open (U i)) â†’
    s âŠ† (â‹ƒ i, U i) â†’ (âˆƒ (t : finset Î¹), s âŠ† (â‹ƒ i âˆˆ t, U i))) :=
âŸ¨assume hs Î¹, hs.elim_finite_subcover, is_compact_of_finite_subcoverâŸ©

/-- A set `s` is compact if and only if
for every family of closed sets whose intersection avoids `s`,
there exists a finite subfamily whose intersection avoids `s`. -/
theorem is_compact_iff_finite_subfamily_closed :
  is_compact s â†” (Î  {Î¹ : Type u} (Z : Î¹ â†’ (set Î±)), (âˆ€ i, is_closed (Z i)) â†’
    s âˆ© (â‹‚ i, Z i) = âˆ… â†’ (âˆƒ (t : finset Î¹), s âˆ© (â‹‚ i âˆˆ t, Z i) = âˆ…)) :=
âŸ¨assume hs Î¹, hs.elim_finite_subfamily_closed, is_compact_of_finite_subfamily_closedâŸ©

@[simp]
lemma is_compact_empty : is_compact (âˆ… : set Î±) :=
assume f hnf hsf, not.elim hnf.ne $
empty_mem_iff_bot.1 $ le_principal_iff.1 hsf

@[simp]
lemma is_compact_singleton {a : Î±} : is_compact ({a} : set Î±) :=
Î» f hf hfa, âŸ¨a, rfl, cluster_pt.of_le_nhds'
  (hfa.trans $ by simpa only [principal_singleton] using pure_le_nhds a) hfâŸ©

lemma set.subsingleton.is_compact {s : set Î±} (hs : s.subsingleton) : is_compact s :=
subsingleton.induction_on hs is_compact_empty $ Î» x, is_compact_singleton

lemma set.finite.compact_bUnion {s : set Î²} {f : Î² â†’ set Î±} (hs : finite s)
  (hf : âˆ€i âˆˆ s, is_compact (f i)) :
  is_compact (â‹ƒi âˆˆ s, f i) :=
is_compact_of_finite_subcover $ assume Î¹ U hUo hsU,
  have âˆ€i : subtype s, âˆƒt : finset Î¹, f i âŠ† (â‹ƒ j âˆˆ t, U j), from
    assume âŸ¨i, hiâŸ©, (hf i hi).elim_finite_subcover _ hUo
      (calc f i âŠ† â‹ƒi âˆˆ s, f i : subset_bUnion_of_mem hi
            ... âŠ† â‹ƒj, U j     : hsU),
  let âŸ¨finite_subcovers, hâŸ© := axiom_of_choice this in
  by haveI : fintype (subtype s) := hs.fintype; exact
  let t := finset.bUnion finset.univ finite_subcovers in
  have (â‹ƒi âˆˆ s, f i) âŠ† (â‹ƒ i âˆˆ t, U i), from bUnion_subset $
    assume i hi, calc
    f i âŠ† (â‹ƒ j âˆˆ finite_subcovers âŸ¨i, hiâŸ©, U j) : (h âŸ¨i, hiâŸ©)
    ... âŠ† (â‹ƒ j âˆˆ t, U j) : bUnion_subset_bUnion_left $
      assume j hj, finset.mem_bUnion.mpr âŸ¨_, finset.mem_univ _, hjâŸ©,
  âŸ¨t, thisâŸ©

lemma finset.compact_bUnion (s : finset Î²) {f : Î² â†’ set Î±} (hf : âˆ€i âˆˆ s, is_compact (f i)) :
  is_compact (â‹ƒi âˆˆ s, f i) :=
s.finite_to_set.compact_bUnion hf

lemma compact_accumulate {K : â„• â†’ set Î±} (hK : âˆ€ n, is_compact (K n)) (n : â„•) :
  is_compact (accumulate K n) :=
(finite_le_nat n).compact_bUnion $ Î» k _, hK k

lemma compact_Union {f : Î² â†’ set Î±} [fintype Î²]
  (h : âˆ€i, is_compact (f i)) : is_compact (â‹ƒi, f i) :=
by rw â† bUnion_univ; exact finite_univ.compact_bUnion (Î» i _, h i)

lemma set.finite.is_compact (hs : finite s) : is_compact s :=
bUnion_of_singleton s â–¸ hs.compact_bUnion (Î» _ _, is_compact_singleton)

lemma finite_of_is_compact_of_discrete [discrete_topology Î±] (s : set Î±) (hs : is_compact s) :
  s.finite :=
begin
  have := hs.elim_finite_subcover (Î» x : Î±, ({x} : set Î±))
    (Î» x, is_open_discrete _),
  simp only [set.subset_univ, forall_prop_of_true, set.Union_of_singleton] at this,
  rcases this with âŸ¨t, htâŸ©,
  suffices : (â‹ƒ (i : Î±) (H : i âˆˆ t), {i} : set Î±) = (t : set Î±),
  { rw this at ht, exact t.finite_to_set.subset ht },
  ext x,
  simp only [exists_prop, set.mem_Union, set.mem_singleton_iff, exists_eq_right', finset.mem_coe]
end

lemma is_compact.union (hs : is_compact s) (ht : is_compact t) : is_compact (s âˆª t) :=
by rw union_eq_Union; exact compact_Union (Î» b, by cases b; assumption)

lemma is_compact.insert (hs : is_compact s) (a) : is_compact (insert a s) :=
is_compact_singleton.union hs

/-- If `V : Î¹ â†’ set Î±` is a decreasing family of closed compact sets then any neighborhood of
`â‹‚ i, V i` contains some `V i`. We assume each `V i` is compact *and* closed because `Î±` is
not assumed to be Hausdorff. See `exists_subset_nhd_of_compact` for version assuming this. -/
lemma exists_subset_nhd_of_compact' {Î¹ : Type*} [nonempty Î¹] {V : Î¹ â†’ set Î±} (hV : directed (âŠ‡) V)
  (hV_cpct : âˆ€ i, is_compact (V i)) (hV_closed : âˆ€ i, is_closed (V i))
  {U : set Î±} (hU : âˆ€ x âˆˆ â‹‚ i, V i, U âˆˆ ğ“ x) : âˆƒ i, V i âŠ† U :=
begin
  set Y := â‹‚ i, V i,
  obtain âŸ¨W, hsubW, W_op, hWUâŸ© : âˆƒ W, Y âŠ† W âˆ§ is_open W âˆ§ W âŠ† U,
    from exists_open_set_nhds hU,
  suffices : âˆƒ i, V i âŠ† W,
  { rcases this with âŸ¨i, hiâŸ©,
    refine âŸ¨i, set.subset.trans hi hWUâŸ© },
  by_contradiction H,
  push_neg at H,
  replace H : âˆ€ i, (V i âˆ© Wá¶œ).nonempty := Î» i, set.inter_compl_nonempty_iff.mpr (H i),
  have : (â‹‚ i, V i âˆ© Wá¶œ).nonempty,
  { apply is_compact.nonempty_Inter_of_directed_nonempty_compact_closed _ _ H,
    { intro i,
      exact (hV_cpct i).inter_right W_op.is_closed_compl },
    { intro i,
      apply (hV_closed i).inter W_op.is_closed_compl },
    { intros i j,
      rcases hV i j with âŸ¨k, hki, hkjâŸ©,
      use k,
      split ; intro x ; simp only [and_imp, mem_inter_eq, mem_compl_eq] ; tauto } },
  have : Â¬ (â‹‚ (i : Î¹), V i) âŠ† W,
    by simpa [â† Inter_inter, inter_compl_nonempty_iff],
  contradiction
end

namespace filter

/-- `filter.cocompact` is the filter generated by complements to compact sets. -/
def cocompact (Î± : Type*) [topological_space Î±] : filter Î± :=
â¨… (s : set Î±) (hs : is_compact s), ğ“Ÿ (sá¶œ)

lemma has_basis_cocompact : (cocompact Î±).has_basis is_compact compl :=
has_basis_binfi_principal'
  (Î» s hs t ht, âŸ¨s âˆª t, hs.union ht, compl_subset_compl.2 (subset_union_left s t),
    compl_subset_compl.2 (subset_union_right s t)âŸ©)
  âŸ¨âˆ…, is_compact_emptyâŸ©

lemma mem_cocompact : s âˆˆ cocompact Î± â†” âˆƒ t, is_compact t âˆ§ tá¶œ âŠ† s :=
has_basis_cocompact.mem_iff.trans $ exists_congr $ Î» t, exists_prop

lemma mem_cocompact' : s âˆˆ cocompact Î± â†” âˆƒ t, is_compact t âˆ§ sá¶œ âŠ† t :=
mem_cocompact.trans $ exists_congr $ Î» t, and_congr_right $ Î» ht, compl_subset_comm

lemma _root_.is_compact.compl_mem_cocompact (hs : is_compact s) : sá¶œ âˆˆ filter.cocompact Î± :=
has_basis_cocompact.mem_of_mem hs

/-- `filter.coclosed_compact` is the filter generated by complements to closed compact sets.
In a Hausdorff space, this is the same as `filter.cocompact`. -/
def coclosed_compact (Î± : Type*) [topological_space Î±] : filter Î± :=
â¨… (s : set Î±) (hâ‚ : is_closed s) (hâ‚‚ : is_compact s), ğ“Ÿ (sá¶œ)

lemma has_basis_coclosed_compact :
  (filter.coclosed_compact Î±).has_basis (Î» s, is_closed s âˆ§ is_compact s) compl :=
begin
  simp only [filter.coclosed_compact, infi_and'],
  refine has_basis_binfi_principal' _ âŸ¨âˆ…, is_closed_empty, is_compact_emptyâŸ©,
  rintro s âŸ¨hsâ‚, hsâ‚‚âŸ© t âŸ¨htâ‚, htâ‚‚âŸ©,
  exact âŸ¨s âˆª t, âŸ¨âŸ¨hsâ‚.union htâ‚, hsâ‚‚.union htâ‚‚âŸ©, compl_subset_compl.2 (subset_union_left _ _),
    compl_subset_compl.2 (subset_union_right _ _)âŸ©âŸ©
end

lemma mem_coclosed_compact : s âˆˆ coclosed_compact Î± â†” âˆƒ t, is_closed t âˆ§ is_compact t âˆ§ tá¶œ âŠ† s :=
by simp [has_basis_coclosed_compact.mem_iff, and_assoc]

lemma mem_coclosed_compact' : s âˆˆ coclosed_compact Î± â†” âˆƒ t, is_closed t âˆ§ is_compact t âˆ§ sá¶œ âŠ† t :=
by simp only [mem_coclosed_compact, compl_subset_comm]

lemma cocompact_le_coclosed_compact : cocompact Î± â‰¤ coclosed_compact Î± :=
infi_le_infi $ Î» s, le_infi $ Î» _, le_rfl

end filter

section tube_lemma

variables [topological_space Î²]

/-- `nhds_contain_boxes s t` means that any open neighborhood of `s Ã— t` in `Î± Ã— Î²` includes
a product of an open neighborhood of `s` by an open neighborhood of `t`. -/
def nhds_contain_boxes (s : set Î±) (t : set Î²) : Prop :=
âˆ€ (n : set (Î± Ã— Î²)) (hn : is_open n) (hp : set.prod s t âŠ† n),
âˆƒ (u : set Î±) (v : set Î²), is_open u âˆ§ is_open v âˆ§ s âŠ† u âˆ§ t âŠ† v âˆ§ set.prod u v âŠ† n

lemma nhds_contain_boxes.symm {s : set Î±} {t : set Î²} :
  nhds_contain_boxes s t â†’ nhds_contain_boxes t s :=
assume H n hn hp,
  let âŸ¨u, v, uo, vo, su, tv, pâŸ© :=
    H (prod.swap â»Â¹' n)
      (hn.preimage continuous_swap)
      (by rwa [â†image_subset_iff, image_swap_prod]) in
  âŸ¨v, u, vo, uo, tv, su,
    by rwa [â†image_subset_iff, image_swap_prod] at pâŸ©

lemma nhds_contain_boxes.comm {s : set Î±} {t : set Î²} :
  nhds_contain_boxes s t â†” nhds_contain_boxes t s :=
iff.intro nhds_contain_boxes.symm nhds_contain_boxes.symm

lemma nhds_contain_boxes_of_singleton {x : Î±} {y : Î²} :
  nhds_contain_boxes ({x} : set Î±) ({y} : set Î²) :=
assume n hn hp,
  let âŸ¨u, v, uo, vo, xu, yv, hp'âŸ© :=
    is_open_prod_iff.mp hn x y (hp $ by simp) in
  âŸ¨u, v, uo, vo, by simpa, by simpa, hp'âŸ©

lemma nhds_contain_boxes_of_compact {s : set Î±} (hs : is_compact s) (t : set Î²)
  (H : âˆ€ x âˆˆ s, nhds_contain_boxes ({x} : set Î±) t) : nhds_contain_boxes s t :=
assume n hn hp,
have âˆ€x : subtype s, âˆƒuv : set Î± Ã— set Î²,
     is_open uv.1 âˆ§ is_open uv.2 âˆ§ {â†‘x} âŠ† uv.1 âˆ§ t âŠ† uv.2 âˆ§ set.prod uv.1 uv.2 âŠ† n,
  from assume âŸ¨x, hxâŸ©,
    have set.prod {x} t âŠ† n, from
      subset.trans (prod_mono (by simpa) (subset.refl _)) hp,
    let âŸ¨ux,vx,H1âŸ© := H x hx n hn this in âŸ¨âŸ¨ux,vxâŸ©,H1âŸ©,
let âŸ¨uvs, hâŸ© := classical.axiom_of_choice this in
have us_cover : s âŠ† â‹ƒi, (uvs i).1, from
  assume x hx, subset_Union _ âŸ¨x,hxâŸ© (by simpa using (h âŸ¨x,hxâŸ©).2.2.1),
let âŸ¨s0, s0_coverâŸ© :=
  hs.elim_finite_subcover _ (Î»i, (h i).1) us_cover in
let u := â‹ƒ(i âˆˆ s0), (uvs i).1 in
let v := â‹‚(i âˆˆ s0), (uvs i).2 in
have is_open u, from is_open_bUnion (Î»i _, (h i).1),
have is_open v, from is_open_bInter s0.finite_to_set (Î»i _, (h i).2.1),
have t âŠ† v, from subset_bInter (Î»i _, (h i).2.2.2.1),
have set.prod u v âŠ† n, from assume âŸ¨x',y'âŸ© âŸ¨hx',hy'âŸ©,
  have âˆƒi âˆˆ s0, x' âˆˆ (uvs i).1, by simpa using hx',
  let âŸ¨i,is0,hiâŸ© := this in
  (h i).2.2.2.2 âŸ¨hi, (bInter_subset_of_mem is0 : v âŠ† (uvs i).2) hy'âŸ©,
âŸ¨u, v, â€¹is_open uâ€º, â€¹is_open vâ€º, s0_cover, â€¹t âŠ† vâ€º, â€¹set.prod u v âŠ† nâ€ºâŸ©

/-- If `s` and `t` are compact sets and `n` is an open neighborhood of `s Ã— t`, then there exist
open neighborhoods `u âŠ‡ s` and `v âŠ‡ t` such that `u Ã— v âŠ† n`. -/
lemma generalized_tube_lemma {s : set Î±} (hs : is_compact s) {t : set Î²} (ht : is_compact t)
  {n : set (Î± Ã— Î²)} (hn : is_open n) (hp : set.prod s t âŠ† n) :
  âˆƒ (u : set Î±) (v : set Î²), is_open u âˆ§ is_open v âˆ§ s âŠ† u âˆ§ t âŠ† v âˆ§ set.prod u v âŠ† n :=
have _, from
  nhds_contain_boxes_of_compact hs t $ assume x _, nhds_contain_boxes.symm $
    nhds_contain_boxes_of_compact ht {x} $ assume y _, nhds_contain_boxes_of_singleton,
this n hn hp

end tube_lemma

/-- Type class for compact spaces. Separation is sometimes included in the definition, especially
in the French literature, but we do not include it here. -/
class compact_space (Î± : Type*) [topological_space Î±] : Prop :=
(compact_univ : is_compact (univ : set Î±))

@[priority 10] -- see Note [lower instance priority]
instance subsingleton.compact_space [subsingleton Î±] : compact_space Î± :=
âŸ¨subsingleton_univ.is_compactâŸ©

lemma is_compact_univ_iff : is_compact (univ : set Î±) â†” compact_space Î± := âŸ¨Î» h, âŸ¨hâŸ©, Î» h, h.1âŸ©

lemma compact_univ [h : compact_space Î±] : is_compact (univ : set Î±) := h.compact_univ

lemma cluster_point_of_compact [compact_space Î±] (f : filter Î±) [ne_bot f] :
  âˆƒ x, cluster_pt x f :=
by simpa using compact_univ (show f â‰¤ ğ“Ÿ univ, by simp)

lemma compact_space.elim_nhds_subcover {Î± : Type*} [topological_space Î±] [compact_space Î±]
  (U : Î± â†’ set Î±) (hU : âˆ€ x, U x âˆˆ ğ“ x) :
  âˆƒ t : finset Î±, (â‹ƒ x âˆˆ t, U x) = âŠ¤ :=
begin
  obtain âŸ¨t, -, sâŸ© := is_compact.elim_nhds_subcover compact_univ U (Î» x m, hU x),
  exact âŸ¨t, by { rw eq_top_iff, exact s }âŸ©,
end

theorem compact_space_of_finite_subfamily_closed {Î± : Type u} [topological_space Î±]
  (h : Î  {Î¹ : Type u} (Z : Î¹ â†’ (set Î±)), (âˆ€ i, is_closed (Z i)) â†’
    (â‹‚ i, Z i) = âˆ… â†’ âˆƒ (t : finset Î¹), (â‹‚ i âˆˆ t, Z i) = âˆ…) :
  compact_space Î± :=
{ compact_univ :=
  begin
    apply is_compact_of_finite_subfamily_closed,
    intros Î¹ Z, specialize h Z,
    simpa using h
  end }

lemma is_closed.is_compact [compact_space Î±] {s : set Î±} (h : is_closed s) :
  is_compact s :=
compact_of_is_closed_subset compact_univ h (subset_univ _)

lemma filter.cocompact_ne_bot_tfae (Î± : Type*) [topological_space Î±] :
  tfae [ne_bot (filter.cocompact Î±),
    ne_bot (filter.coclosed_compact Î±),
    Â¬is_compact (univ : set Î±),
    Â¬compact_space Î±] :=
begin
  tfae_have : 1 â†’ 2, from Î» h, h.mono filter.cocompact_le_coclosed_compact,
  tfae_have : 3 â†” 4, from not_congr is_compact_univ_iff,
  tfae_have : 2 â†’ 3, from Î» hâ‚ hâ‚‚, (filter.has_basis_coclosed_compact.ne_bot_iff.1 hâ‚
    âŸ¨is_closed_univ, hâ‚‚âŸ©).ne_empty compl_univ,
  tfae_have : 3 â†’ 1,
  { refine Î» hâ‚, filter.has_basis_cocompact.ne_bot_iff.2 (Î» s hs, _),
    contrapose! hâ‚, rw [not_nonempty_iff_eq_empty, compl_empty_iff] at hâ‚,
    rwa â† hâ‚ },
  tfae_finish
end

/-- `ne_bot (cocompact Î±)` is the canonical way to say that `Î±` is not a compact space using
typeclasses. -/
instance [ne_bot (filter.cocompact Î±)] : ne_bot (filter.coclosed_compact Î±) :=
((filter.cocompact_ne_bot_tfae Î±).out 0 1).mp â€¹_â€º

/-- A compact discrete space is finite. -/
noncomputable
def fintype_of_compact_of_discrete [compact_space Î±] [discrete_topology Î±] :
  fintype Î± :=
fintype_of_univ_finite $ finite_of_is_compact_of_discrete _ compact_univ

lemma finite_cover_nhds_interior [compact_space Î±] {U : Î± â†’ set Î±} (hU : âˆ€ x, U x âˆˆ ğ“ x) :
  âˆƒ t : finset Î±, (â‹ƒ x âˆˆ t, interior (U x)) = univ :=
let âŸ¨t, htâŸ© := compact_univ.elim_finite_subcover (Î» x, interior (U x)) (Î» x, is_open_interior)
  (Î» x _, mem_Union.2 âŸ¨x, mem_interior_iff_mem_nhds.2 (hU x)âŸ©)
in âŸ¨t, univ_subset_iff.1 htâŸ©

lemma finite_cover_nhds [compact_space Î±] {U : Î± â†’ set Î±} (hU : âˆ€ x, U x âˆˆ ğ“ x) :
  âˆƒ t : finset Î±, (â‹ƒ x âˆˆ t, U x) = univ :=
let âŸ¨t, htâŸ© := finite_cover_nhds_interior hU in âŸ¨t, univ_subset_iff.1 $
  ht â–¸ bUnion_mono (Î» x hx, interior_subset)âŸ©

/-- If `Î±` is a compact space, then a locally finite family of sets of `Î±` can have only finitely
many nonempty elements. -/
lemma locally_finite.finite_nonempty_of_compact {Î¹ : Type*} [compact_space Î±] {f : Î¹ â†’ set Î±}
  (hf : locally_finite f) :
  finite {i | (f i).nonempty} :=
by simpa only [inter_univ]  using hf.finite_nonempty_inter_compact compact_univ

/-- If `Î±` is a compact space, then a locally finite family of nonempty sets of `Î±` can have only
finitely many elements, `set.finite` version. -/
lemma locally_finite.finite_of_compact {Î¹ : Type*} [compact_space Î±] {f : Î¹ â†’ set Î±}
  (hf : locally_finite f) (hne : âˆ€ i, (f i).nonempty) :
  finite (univ : set Î¹) :=
by simpa only [hne] using hf.finite_nonempty_of_compact

/-- If `Î±` is a compact space, then a locally finite family of nonempty sets of `Î±` can have only
finitely many elements, `fintype` version. -/
noncomputable def locally_finite.fintype_of_compact {Î¹ : Type*} [compact_space Î±] {f : Î¹ â†’ set Î±}
  (hf : locally_finite f) (hne : âˆ€ i, (f i).nonempty) :
  fintype Î¹ :=
fintype_of_univ_finite (hf.finite_of_compact hne)

variables [topological_space Î²]

lemma is_compact.image_of_continuous_on {f : Î± â†’ Î²} (hs : is_compact s) (hf : continuous_on f s) :
  is_compact (f '' s) :=
begin
  intros l lne ls,
  have : ne_bot (l.comap f âŠ“ ğ“Ÿ s) :=
    comap_inf_principal_ne_bot_of_image_mem lne (le_principal_iff.1 ls),
  obtain âŸ¨a, has, haâŸ© : âˆƒ a âˆˆ s, cluster_pt a (l.comap f âŠ“ ğ“Ÿ s) := @@hs this inf_le_right,
  use [f a, mem_image_of_mem f has],
  have : tendsto f (ğ“ a âŠ“ (comap f l âŠ“ ğ“Ÿ s)) (ğ“ (f a) âŠ“ l),
  { convert (hf a has).inf (@tendsto_comap _ _ f l) using 1,
    rw nhds_within,
    ac_refl },
  exact @@tendsto.ne_bot _ this ha,
end

lemma is_compact.image {f : Î± â†’ Î²} (hs : is_compact s) (hf : continuous f) :
  is_compact (f '' s) :=
hs.image_of_continuous_on hf.continuous_on

/-- The comap of the cocompact filter on `Î²` by a continuous function `f : Î± â†’ Î²` is less than or
equal to the cocompact filter on `Î±`.
This is a reformulation of the fact that images of compact sets are compact. -/
lemma filter.comap_cocompact {f : Î± â†’ Î²} (hf : continuous f) :
  (filter.cocompact Î²).comap f â‰¤ filter.cocompact Î± :=
begin
  rw (filter.has_basis_cocompact.comap f).le_basis_iff filter.has_basis_cocompact,
  intros t ht,
  refine âŸ¨f '' t, ht.image hf, _âŸ©,
  simpa using t.subset_preimage_image f
end

lemma is_compact_range [compact_space Î±] {f : Î± â†’ Î²} (hf : continuous f) :
  is_compact (range f) :=
by rw â† image_univ; exact compact_univ.image hf

/-- If X is is_compact then prâ‚‚ : X Ã— Y â†’ Y is a closed map -/
theorem is_closed_proj_of_is_compact
  {X : Type*} [topological_space X] [compact_space X]
  {Y : Type*} [topological_space Y]  :
  is_closed_map (prod.snd : X Ã— Y â†’ Y) :=
begin
  set Ï€X := (prod.fst : X Ã— Y â†’ X),
  set Ï€Y := (prod.snd : X Ã— Y â†’ Y),
  assume C (hC : is_closed C),
  rw is_closed_iff_cluster_pt at hC âŠ¢,
  assume y (y_closure : cluster_pt y $ ğ“Ÿ (Ï€Y '' C)),
  have : ne_bot (map Ï€X (comap Ï€Y (ğ“ y) âŠ“ ğ“Ÿ C)),
  { suffices : ne_bot (map Ï€Y (comap Ï€Y (ğ“ y) âŠ“ ğ“Ÿ C)),
      by simpa only [map_ne_bot_iff],
    convert y_closure,
    calc map Ï€Y (comap Ï€Y (ğ“ y) âŠ“ ğ“Ÿ C) =
       ğ“ y âŠ“ map Ï€Y (ğ“Ÿ C) : filter.push_pull' _ _ _
      ... = ğ“ y âŠ“ ğ“Ÿ (Ï€Y '' C) : by rw map_principal },
  resetI,
  obtain âŸ¨x, hxâŸ© : âˆƒ x, cluster_pt x (map Ï€X (comap Ï€Y (ğ“ y) âŠ“ ğ“Ÿ C)),
    from cluster_point_of_compact _,
  refine âŸ¨âŸ¨x, yâŸ©, _, by simp [Ï€Y]âŸ©,
  apply hC,
  rw [cluster_pt, â† filter.map_ne_bot_iff Ï€X],
  convert hx,
  calc map Ï€X (ğ“ (x, y) âŠ“ ğ“Ÿ C)
      = map Ï€X (comap Ï€X (ğ“ x) âŠ“ comap Ï€Y (ğ“ y) âŠ“ ğ“Ÿ C) : by rw [nhds_prod_eq, filter.prod]
  ... = map Ï€X (comap Ï€Y (ğ“ y) âŠ“ ğ“Ÿ C âŠ“ comap Ï€X (ğ“ x)) : by ac_refl
  ... = map Ï€X (comap Ï€Y (ğ“ y) âŠ“ ğ“Ÿ C) âŠ“ ğ“ x            : by rw filter.push_pull
  ... = ğ“ x âŠ“ map Ï€X (comap Ï€Y (ğ“ y) âŠ“ ğ“Ÿ C)            : by rw inf_comm
end

lemma exists_subset_nhd_of_compact_space [compact_space Î±] {Î¹ : Type*} [nonempty Î¹]
  {V : Î¹ â†’ set Î±} (hV : directed (âŠ‡) V) (hV_closed : âˆ€ i, is_closed (V i))
  {U : set Î±} (hU : âˆ€ x âˆˆ â‹‚ i, V i, U âˆˆ ğ“ x) : âˆƒ i, V i âŠ† U :=
exists_subset_nhd_of_compact' hV (Î» i, (hV_closed i).is_compact) hV_closed hU

lemma embedding.is_compact_iff_is_compact_image {f : Î± â†’ Î²} (hf : embedding f) :
  is_compact s â†” is_compact (f '' s) :=
iff.intro (assume h, h.image hf.continuous) $ assume h, begin
  rw is_compact_iff_ultrafilter_le_nhds at âŠ¢ h,
  intros u us',
  have : â†‘(u.map f) â‰¤ ğ“Ÿ (f '' s), begin
    rw [ultrafilter.coe_map, map_le_iff_le_comap, comap_principal], convert us',
    exact preimage_image_eq _ hf.inj
  end,
  rcases h (u.map f) this with âŸ¨_, âŸ¨a, ha, âŸ¨âŸ©âŸ©, _âŸ©,
  refine âŸ¨a, ha, _âŸ©,
  rwa [hf.induced, nhds_induced, â†map_le_iff_le_comap]
end

/-- A closed embedding is proper, ie, inverse images of compact sets are contained in compacts. -/
lemma closed_embedding.tendsto_cocompact
  {f : Î± â†’ Î²} (hf : closed_embedding f) : tendsto f (filter.cocompact Î±) (filter.cocompact Î²) :=
begin
  rw filter.has_basis_cocompact.tendsto_iff filter.has_basis_cocompact,
  intros K hK,
  refine âŸ¨f â»Â¹' (K âˆ© (set.range f)), _, Î» x hx, by simpa using hxâŸ©,
  apply hf.to_embedding.is_compact_iff_is_compact_image.mpr,
  rw set.image_preimage_eq_of_subset (set.inter_subset_right _ _),
  exact hK.inter_right hf.closed_range,
end

lemma compact_iff_compact_in_subtype {p : Î± â†’ Prop} {s : set {a // p a}} :
  is_compact s â†” is_compact ((coe : _ â†’ Î±) '' s) :=
embedding_subtype_coe.is_compact_iff_is_compact_image

lemma is_compact_iff_is_compact_univ {s : set Î±} : is_compact s â†” is_compact (univ : set s) :=
by rw [compact_iff_compact_in_subtype, image_univ, subtype.range_coe]; refl

lemma is_compact_iff_compact_space {s : set Î±} : is_compact s â†” compact_space s :=
is_compact_iff_is_compact_univ.trans âŸ¨Î» h, âŸ¨hâŸ©, @compact_space.compact_univ _ _âŸ©

lemma is_compact.prod {s : set Î±} {t : set Î²} (hs : is_compact s) (ht : is_compact t) :
  is_compact (set.prod s t) :=
begin
  rw is_compact_iff_ultrafilter_le_nhds at hs ht âŠ¢,
  intros f hfs,
  rw le_principal_iff at hfs,
  obtain âŸ¨a : Î±, sa : a âˆˆ s, ha : map prod.fst â†‘f â‰¤ ğ“ aâŸ© :=
    hs (f.map prod.fst) (le_principal_iff.2 $ mem_map.2 $ mem_of_superset hfs (Î» x, and.left)),
  obtain âŸ¨b : Î², tb : b âˆˆ t, hb : map prod.snd â†‘f â‰¤ ğ“ bâŸ© :=
    ht (f.map prod.snd) (le_principal_iff.2 $ mem_map.2 $
      mem_of_superset hfs (Î» x, and.right)),
  rw map_le_iff_le_comap at ha hb,
  refine âŸ¨âŸ¨a, bâŸ©, âŸ¨sa, tbâŸ©, _âŸ©,
  rw nhds_prod_eq, exact le_inf ha hb
end

lemma inducing.is_compact_iff {f : Î± â†’ Î²} (hf : inducing f) {s : set Î±} :
  is_compact (f '' s) â†” is_compact s :=
begin
  split,
  { introsI hs F F_ne_bot F_le,
    obtain âŸ¨_, âŸ¨x, x_in : x âˆˆ s, rflâŸ©, hx : cluster_pt (f x) (map f F)âŸ© :=
      hs (calc map f F â‰¤ map f (ğ“Ÿ s) : map_mono F_le
                  ... = ğ“Ÿ (f '' s) : map_principal),
    use [x, x_in],
    suffices : (map f (ğ“ x âŠ“ F)).ne_bot, by simpa [filter.map_ne_bot_iff],
    rwa calc map f (ğ“ x âŠ“ F) = map f ((comap f $ ğ“ $ f x) âŠ“ F) : by rw hf.nhds_eq_comap
                          ... = ğ“ (f x) âŠ“ map f F : filter.push_pull' _ _ _ },
  { intro hs,
    exact hs.image hf.continuous }
end

/-- Finite topological spaces are compact. -/
@[priority 100] instance fintype.compact_space [fintype Î±] : compact_space Î± :=
{ compact_univ := finite_univ.is_compact }

/-- The product of two compact spaces is compact. -/
instance [compact_space Î±] [compact_space Î²] : compact_space (Î± Ã— Î²) :=
âŸ¨by { rw â† univ_prod_univ, exact compact_univ.prod compact_univ }âŸ©

/-- The disjoint union of two compact spaces is compact. -/
instance [compact_space Î±] [compact_space Î²] : compact_space (Î± âŠ• Î²) :=
âŸ¨begin
  rw â† range_inl_union_range_inr,
  exact (is_compact_range continuous_inl).union (is_compact_range continuous_inr)
endâŸ©

/-- The coproduct of the cocompact filters on two topological spaces is the cocompact filter on
their product. -/
lemma filter.coprod_cocompact {Î² : Type*} [topological_space Î²]:
  (filter.cocompact Î±).coprod (filter.cocompact Î²) = filter.cocompact (Î± Ã— Î²) :=
begin
  ext S,
  simp only [mem_coprod_iff, exists_prop, mem_comap, filter.mem_cocompact],
  split,
  { rintro âŸ¨âŸ¨A, âŸ¨t, ht, hAtâŸ©, hASâŸ©, B, âŸ¨t', ht', hBt'âŸ©, hBSâŸ©,
    refine âŸ¨t.prod t', ht.prod ht', _âŸ©,
    refine subset.trans _ (union_subset hAS hBS),
    rw compl_subset_comm at âŠ¢ hAt hBt',
    refine subset.trans _ (set.prod_mono hAt hBt'),
    intros x,
    simp only [compl_union, mem_inter_eq, mem_prod, mem_preimage, mem_compl_eq],
    tauto },
  { rintros âŸ¨t, ht, htSâŸ©,
    refine âŸ¨âŸ¨(prod.fst '' t)á¶œ, _, _âŸ©, âŸ¨(prod.snd '' t)á¶œ, _, _âŸ©âŸ©,
    { exact âŸ¨prod.fst '' t, ht.image continuous_fst, subset.rflâŸ© },
    { rw preimage_compl,
      rw compl_subset_comm at âŠ¢ htS,
      exact subset.trans htS (subset_preimage_image prod.fst _) },
    { exact âŸ¨prod.snd '' t, ht.image continuous_snd, subset.rflâŸ© },
    { rw preimage_compl,
      rw compl_subset_comm at âŠ¢ htS,
      exact subset.trans htS (subset_preimage_image prod.snd _) } }
end

section tychonoff
variables {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [âˆ€ i, topological_space (Ï€ i)]

/-- **Tychonoff's theorem** -/
lemma is_compact_pi_infinite {s : Î  i, set (Ï€ i)} :
  (âˆ€ i, is_compact (s i)) â†’ is_compact {x : Î  i, Ï€ i | âˆ€ i, x i âˆˆ s i} :=
begin
  simp only [is_compact_iff_ultrafilter_le_nhds, nhds_pi, exists_prop, mem_set_of_eq, le_infi_iff,
    le_principal_iff],
  intros h f hfs,
  have : âˆ€i:Î¹, âˆƒa, aâˆˆs i âˆ§ tendsto (Î»x:Î i:Î¹, Ï€ i, x i) f (ğ“ a),
  { refine Î» i, h i (f.map _) (mem_map.2 _),
    exact mem_of_superset hfs (Î» x hx, hx i) },
  choose a ha,
  exact  âŸ¨a, assume i, (ha i).left, assume i, (ha i).right.le_comapâŸ©
end

/-- A version of Tychonoff's theorem that uses `set.pi`. -/
lemma is_compact_univ_pi {s : Î  i, set (Ï€ i)} (h : âˆ€ i, is_compact (s i)) :
  is_compact (pi univ s) :=
by { convert is_compact_pi_infinite h, simp only [pi, forall_prop_of_true, mem_univ] }

instance pi.compact_space [âˆ€ i, compact_space (Ï€ i)] : compact_space (Î i, Ï€ i) :=
âŸ¨by { rw [â† pi_univ univ], exact is_compact_univ_pi (Î» i, compact_univ) }âŸ©

/-- Product of compact sets is compact -/
lemma filter.Coprod_cocompact {Î´ : Type*} {Îº : Î´ â†’ Type*} [Î  d, topological_space (Îº d)] :
  filter.Coprod (Î» d, filter.cocompact (Îº d)) = filter.cocompact (Î  d, Îº d) :=
begin
  ext S,
  simp only [mem_coprod_iff, exists_prop, mem_comap, filter.mem_cocompact],
  split,
  { intros h,
    rw filter.mem_Coprod_iff at h,
    choose t ht1 ht2 using h,
    choose t1 ht11 ht12 using Î» d, filter.mem_cocompact.mp (ht1 d),
    refine âŸ¨set.pi set.univ t1, _, _âŸ©,
    { convert is_compact_pi_infinite ht11,
      ext,
      simp },
    { refine subset.trans _ (set.Union_subset ht2),
      intros x,
      simp only [mem_Union, mem_univ_pi, exists_imp_distrib, mem_compl_eq, not_forall],
      intros d h,
      exact âŸ¨d, ht12 d hâŸ© } },
  { rintros âŸ¨t, h1, h2âŸ©,
    rw filter.mem_Coprod_iff,
    intros d,
    refine âŸ¨((Î» (k : Î  (d : Î´), Îº d), k d) '' t)á¶œ, _, _âŸ©,
    { rw filter.mem_cocompact,
      refine âŸ¨(Î» (k : Î  (d : Î´), Îº d), k d) '' t, _, set.subset.refl _âŸ©,
      exact is_compact.image h1 (continuous_pi_iff.mp (continuous_id) d) },
    refine subset.trans _ h2,
    intros x hx,
    simp only [not_exists, mem_image, mem_preimage, mem_compl_eq] at hx,
    simpa using mt (hx x) },
end

end tychonoff

instance quot.compact_space {r : Î± â†’ Î± â†’ Prop} [compact_space Î±] :
  compact_space (quot r) :=
âŸ¨by { rw â† range_quot_mk, exact is_compact_range continuous_quot_mk }âŸ©

instance quotient.compact_space {s : setoid Î±} [compact_space Î±] :
  compact_space (quotient s) :=
quot.compact_space

/-- There are various definitions of "locally compact space" in the literature, which agree for
Hausdorff spaces but not in general. This one is the precise condition on X needed for the
evaluation `map C(X, Y) Ã— X â†’ Y` to be continuous for all `Y` when `C(X, Y)` is given the
compact-open topology. -/
class locally_compact_space (Î± : Type*) [topological_space Î±] : Prop :=
(local_compact_nhds : âˆ€ (x : Î±) (n âˆˆ ğ“ x), âˆƒ s âˆˆ ğ“ x, s âŠ† n âˆ§ is_compact s)

lemma compact_basis_nhds [locally_compact_space Î±] (x : Î±) :
  (ğ“ x).has_basis (Î» s, s âˆˆ ğ“ x âˆ§ is_compact s) (Î» s, s) :=
has_basis_self.2 $ by simpa only [and_comm] using locally_compact_space.local_compact_nhds x

lemma locally_compact_space_of_has_basis {Î¹ : Î± â†’ Type*} {p : Î  x, Î¹ x â†’ Prop}
  {s : Î  x, Î¹ x â†’ set Î±} (h : âˆ€ x, (ğ“ x).has_basis (p x) (s x))
  (hc : âˆ€ x i, p x i â†’ is_compact (s x i)) :
  locally_compact_space Î± :=
âŸ¨Î» x t ht, let âŸ¨i, hp, htâŸ© := (h x).mem_iff.1 ht in âŸ¨s x i, (h x).mem_of_mem hp, ht, hc x i hpâŸ©âŸ©

instance locally_compact_space.prod (Î± : Type*) (Î² : Type*) [topological_space Î±]
  [topological_space Î²] [locally_compact_space Î±] [locally_compact_space Î²] :
  locally_compact_space (Î± Ã— Î²) :=
have _ := Î» x : Î± Ã— Î², (compact_basis_nhds x.1).prod_nhds' (compact_basis_nhds x.2),
locally_compact_space_of_has_basis this $ Î» x s âŸ¨âŸ¨_, hâ‚âŸ©, _, hâ‚‚âŸ©, hâ‚.prod hâ‚‚

/-- A reformulation of the definition of locally compact space: In a locally compact space,
  every open set containing `x` has a compact subset containing `x` in its interior. -/
lemma exists_compact_subset [locally_compact_space Î±] {x : Î±} {U : set Î±}
  (hU : is_open U) (hx : x âˆˆ U) : âˆƒ (K : set Î±), is_compact K âˆ§ x âˆˆ interior K âˆ§ K âŠ† U :=
begin
  rcases locally_compact_space.local_compact_nhds x U (hU.mem_nhds hx) with âŸ¨K, h1K, h2K, h3KâŸ©,
  exact âŸ¨K, h3K, mem_interior_iff_mem_nhds.2 h1K, h2KâŸ©,
end

/-- In a locally compact space every point has a compact neighborhood. -/
lemma exists_compact_mem_nhds [locally_compact_space Î±] (x : Î±) :
  âˆƒ K, is_compact K âˆ§ K âˆˆ ğ“ x :=
let âŸ¨K, hKc, hx, HâŸ© := exists_compact_subset is_open_univ (mem_univ x)
in âŸ¨K, hKc, mem_interior_iff_mem_nhds.1 hxâŸ©

/-- In a locally compact space, every compact set is contained in the interior of a compact set. -/
lemma exists_compact_superset [locally_compact_space Î±] {K : set Î±} (hK : is_compact K) :
  âˆƒ K', is_compact K' âˆ§ K âŠ† interior K' :=
begin
  choose U hUc hxU using Î» x : K, exists_compact_mem_nhds (x : Î±),
  have : K âŠ† â‹ƒ x, interior (U x),
    from Î» x hx, mem_Union.2 âŸ¨âŸ¨x, hxâŸ©, mem_interior_iff_mem_nhds.2 (hxU _)âŸ©,
  rcases hK.elim_finite_subcover _ _ this with âŸ¨t, htâŸ©,
  { refine âŸ¨_, t.compact_bUnion (Î» x _, hUc x), Î» x hx, _âŸ©,
    rcases mem_bUnion_iff.1 (ht hx) with âŸ¨y, hyt, hyâŸ©,
    exact interior_mono (subset_bUnion_of_mem hyt) hy },
  { exact Î» _, is_open_interior }
end

lemma ultrafilter.le_nhds_Lim [compact_space Î±] (F : ultrafilter Î±) :
  â†‘F â‰¤ ğ“ (@Lim _ _ (F : filter Î±).nonempty_of_ne_bot F) :=
begin
  rcases compact_univ.ultrafilter_le_nhds F (by simp) with âŸ¨x, -, hâŸ©,
  exact le_nhds_Lim âŸ¨x,hâŸ©,
end

theorem is_closed.exists_minimal_nonempty_closed_subset [compact_space Î±]
  {S : set Î±} (hS : is_closed S) (hne : S.nonempty) :
  âˆƒ (V : set Î±),
    V âŠ† S âˆ§ V.nonempty âˆ§ is_closed V âˆ§
      (âˆ€ (V' : set Î±), V' âŠ† V â†’ V'.nonempty â†’ is_closed V' â†’ V' = V) :=
begin
  let opens := {U : set Î± | Sá¶œ âŠ† U âˆ§ is_open U âˆ§ Uá¶œ.nonempty},
  obtain âŸ¨U, âŸ¨Uc, Uo, UcneâŸ©, hâŸ© := zorn.zorn_subset opens (Î» c hc hz, begin
    by_cases hcne : c.nonempty,
    { obtain âŸ¨Uâ‚€, hUâ‚€âŸ© := hcne,
      haveI : nonempty {U // U âˆˆ c} := âŸ¨âŸ¨Uâ‚€, hUâ‚€âŸ©âŸ©,
      obtain âŸ¨Uâ‚€compl, Uâ‚€opn, Uâ‚€neâŸ© := hc hUâ‚€,
      use â‹ƒâ‚€ c,
      refine âŸ¨âŸ¨_, _, _âŸ©, Î» U hU a ha, âŸ¨U, hU, haâŸ©âŸ©,
      { exact Î» a ha, âŸ¨Uâ‚€, hUâ‚€, Uâ‚€compl haâŸ© },
      { exact is_open_sUnion (Î» _ h, (hc h).2.1) },
      { convert_to (â‹‚(U : {U // U âˆˆ c}), U.1á¶œ).nonempty,
        { ext,
          simp only [not_exists, exists_prop, not_and, set.mem_Inter, subtype.forall,
            set.mem_set_of_eq, set.mem_compl_eq, subtype.val_eq_coe],
          refl, },
        apply is_compact.nonempty_Inter_of_directed_nonempty_compact_closed,
        { rintros âŸ¨U, hUâŸ© âŸ¨U', hU'âŸ©,
          obtain âŸ¨V, hVc, hVU, hVU'âŸ© := zorn.chain.directed_on hz U hU U' hU',
          exact âŸ¨âŸ¨V, hVcâŸ©, set.compl_subset_compl.mpr hVU, set.compl_subset_compl.mpr hVU'âŸ©, },
        { exact Î» U, (hc U.2).2.2, },
        { exact Î» U, (is_closed_compl_iff.mpr (hc U.2).2.1).is_compact, },
        { exact Î» U, (is_closed_compl_iff.mpr (hc U.2).2.1), } } },
    { use Sá¶œ,
      refine âŸ¨âŸ¨set.subset.refl _, is_open_compl_iff.mpr hS, _âŸ©, Î» U Uc, (hcne âŸ¨U, UcâŸ©).elimâŸ©,
      rw compl_compl,
      exact hne, }
  end),
  refine âŸ¨Uá¶œ, set.compl_subset_comm.mp Uc, Ucne, is_closed_compl_iff.mpr Uo, _âŸ©,
  intros V' V'sub V'ne V'cls,
  have : V'á¶œ = U,
  { refine h V'á¶œ âŸ¨_, is_open_compl_iff.mpr V'cls, _âŸ© (set.subset_compl_comm.mp V'sub),
    exact set.subset.trans Uc (set.subset_compl_comm.mp V'sub),
    simp only [compl_compl, V'ne], },
  rw [â†this, compl_compl],
end

/-- A Ïƒ-compact space is a space that is the union of a countable collection of compact subspaces.
  Note that a locally compact separable Tâ‚‚ space need not be Ïƒ-compact.
  The sequence can be extracted using `topological_space.compact_covering`. -/
class sigma_compact_space (Î± : Type*) [topological_space Î±] : Prop :=
(exists_compact_covering : âˆƒ K : â„• â†’ set Î±, (âˆ€ n, is_compact (K n)) âˆ§ (â‹ƒ n, K n) = univ)

@[priority 200] -- see Note [lower instance priority]
instance compact_space.sigma_compact [compact_space Î±] : sigma_compact_space Î± :=
âŸ¨âŸ¨Î» _, univ, Î» _, compact_univ, Union_const _âŸ©âŸ©

lemma sigma_compact_space.of_countable (S : set (set Î±)) (Hc : countable S)
  (Hcomp : âˆ€ s âˆˆ S, is_compact s) (HU : â‹ƒâ‚€ S = univ) : sigma_compact_space Î± :=
âŸ¨(exists_seq_cover_iff_countable âŸ¨_, is_compact_emptyâŸ©).2 âŸ¨S, Hc, Hcomp, HUâŸ©âŸ©

@[priority 100] -- see Note [lower instance priority]
instance sigma_compact_space_of_locally_compact_second_countable [locally_compact_space Î±]
  [second_countable_topology Î±] : sigma_compact_space Î± :=
begin
  choose K hKc hxK using Î» x : Î±, exists_compact_mem_nhds x,
  rcases countable_cover_nhds hxK with âŸ¨s, hsc, hsUâŸ©,
  refine sigma_compact_space.of_countable _ (hsc.image K) (ball_image_iff.2 $ Î» x _, hKc x) _,
  rwa sUnion_image
end

variables (Î±) [sigma_compact_space Î±]
open sigma_compact_space

/-- A choice of compact covering for a `Ïƒ`-compact space, chosen to be monotone. -/
def compact_covering : â„• â†’ set Î± :=
accumulate exists_compact_covering.some

lemma is_compact_compact_covering (n : â„•) : is_compact (compact_covering Î± n) :=
compact_accumulate (classical.some_spec sigma_compact_space.exists_compact_covering).1 n

lemma Union_compact_covering : (â‹ƒ n, compact_covering Î± n) = univ :=
begin
  rw [compact_covering, Union_accumulate],
  exact (classical.some_spec sigma_compact_space.exists_compact_covering).2
end

@[mono] lemma compact_covering_subset â¦ƒm n : â„•â¦„ (h : m â‰¤ n) :
  compact_covering Î± m âŠ† compact_covering Î± n :=
monotone_accumulate h

variable {Î±}

lemma exists_mem_compact_covering (x : Î±) : âˆƒ n, x âˆˆ compact_covering Î± n :=
Union_eq_univ_iff.mp (Union_compact_covering Î±) x

/-- If `Î±` is a `Ïƒ`-compact space, then a locally finite family of nonempty sets of `Î±` can have
only countably many elements, `set.countable` version. -/
lemma locally_finite.countable_of_sigma_compact {Î¹ : Type*} {f : Î¹ â†’ set Î±} (hf : locally_finite f)
  (hne : âˆ€ i, (f i).nonempty) :
  countable (univ : set Î¹) :=
begin
  have := Î» n, hf.finite_nonempty_inter_compact (is_compact_compact_covering Î± n),
  refine (countable_Union (Î» n, (this n).countable)).mono (Î» i hi, _),
  rcases hne i with âŸ¨x, hxâŸ©,
  rcases Union_eq_univ_iff.1 (Union_compact_covering Î±) x with âŸ¨n, hnâŸ©,
  exact mem_Union.2 âŸ¨n, x, hx, hnâŸ©
end

/-- In a topological space with sigma compact topology, if `f` is a function that sends each point
`x` of a closed set `s` to a neighborhood of `x` within `s`, then for some countable set `t âŠ† s`,
the neighborhoods `f x`, `x âˆˆ t`, cover the whole set `s`. -/
lemma countable_cover_nhds_within_of_sigma_compact {f : Î± â†’ set Î±} {s : set Î±} (hs : is_closed s)
  (hf : âˆ€ x âˆˆ s, f x âˆˆ ğ“[s] x) : âˆƒ t âŠ† s, countable t âˆ§ s âŠ† â‹ƒ x âˆˆ t, f x :=
begin
  simp only [nhds_within, mem_inf_principal] at hf,
  choose t ht hsub using Î» n, ((is_compact_compact_covering Î± n).inter_right hs).elim_nhds_subcover
    _ (Î» x hx, hf x hx.right),
  refine âŸ¨â‹ƒ n, (t n : set Î±), Union_subset $ Î» n x hx, (ht n x hx).2,
    countable_Union $ Î» n, (t n).countable_to_set, Î» x hx, mem_bUnion_iff.2 _âŸ©,
  rcases exists_mem_compact_covering x with âŸ¨n, hnâŸ©,
  rcases mem_bUnion_iff.1 (hsub n âŸ¨hn, hxâŸ©) with âŸ¨y, hyt : y âˆˆ t n, hyf : x âˆˆ s â†’ x âˆˆ f yâŸ©,
  exact âŸ¨y, mem_Union.2 âŸ¨n, hytâŸ©, hyf hxâŸ©
end

/-- In a topological space with sigma compact topology, if `f` is a function that sends each
point `x` to a neighborhood of `x`, then for some countable set `s`, the neighborhoods `f x`,
`x âˆˆ s`, cover the whole space. -/
lemma countable_cover_nhds_of_sigma_compact {f : Î± â†’ set Î±}
  (hf : âˆ€ x, f x âˆˆ ğ“ x) : âˆƒ s : set Î±, countable s âˆ§ (â‹ƒ x âˆˆ s, f x) = univ :=
begin
  simp only [â† nhds_within_univ] at hf,
  rcases countable_cover_nhds_within_of_sigma_compact is_closed_univ (Î» x _, hf x)
    with âŸ¨s, -, hsc, hsUâŸ©,
  exact âŸ¨s, hsc, univ_subset_iff.1 hsUâŸ©
end

end compact

/-- An [exhaustion by compact sets](https://en.wikipedia.org/wiki/Exhaustion_by_compact_sets) of a
topological space is a sequence of compact sets `K n` such that `K n âŠ† interior (K (n + 1))` and
`(â‹ƒ n, K n) = univ`.

If `X` is a locally compact sigma compact space, then `compact_exhaustion.choice X` provides
a choice of an exhaustion by compact sets. This choice is also available as
`(default : compact_exhaustion X)`. -/
structure compact_exhaustion (X : Type*) [topological_space X] :=
(to_fun : â„• â†’ set X)
(is_compact' : âˆ€ n, is_compact (to_fun n))
(subset_interior_succ' : âˆ€ n, to_fun n âŠ† interior (to_fun (n + 1)))
(Union_eq' : (â‹ƒ n, to_fun n) = univ)

namespace compact_exhaustion

instance : has_coe_to_fun (compact_exhaustion Î±) := âŸ¨_, to_funâŸ©

variables {Î±} (K : compact_exhaustion Î±)

protected lemma is_compact (n : â„•) : is_compact (K n) := K.is_compact' n

lemma subset_interior_succ (n : â„•) : K n âŠ† interior (K (n + 1)) :=
K.subset_interior_succ' n

lemma subset_succ (n : â„•) : K n âŠ† K (n + 1) :=
subset.trans (K.subset_interior_succ n) interior_subset

@[mono] protected lemma subset â¦ƒm n : â„•â¦„ (h : m â‰¤ n) : K m âŠ† K n :=
show K m â‰¤ K n, from monotone_nat_of_le_succ K.subset_succ h

lemma subset_interior â¦ƒm n : â„•â¦„ (h : m < n) : K m âŠ† interior (K n) :=
subset.trans (K.subset_interior_succ m) $ interior_mono $ K.subset h

lemma Union_eq : (â‹ƒ n, K n) = univ := K.Union_eq'

lemma exists_mem (x : Î±) : âˆƒ n, x âˆˆ K n := Union_eq_univ_iff.1 K.Union_eq x

/-- The minimal `n` such that `x âˆˆ K n`. -/
protected noncomputable def find (x : Î±) : â„• := nat.find (K.exists_mem x)

lemma mem_find (x : Î±) : x âˆˆ K (K.find x) := nat.find_spec (K.exists_mem x)

lemma mem_iff_find_le {x : Î±} {n : â„•} : x âˆˆ K n â†” K.find x â‰¤ n :=
âŸ¨Î» h, nat.find_min' (K.exists_mem x) h, Î» h, K.subset h $ K.mem_find xâŸ©

/-- Prepend the empty set to a compact exhaustion `K n`. -/
def shiftr : compact_exhaustion Î± :=
{ to_fun := Î» n, nat.cases_on n âˆ… K,
  is_compact' := Î» n, nat.cases_on n is_compact_empty K.is_compact,
  subset_interior_succ' := Î» n, nat.cases_on n (empty_subset _) K.subset_interior_succ,
  Union_eq' := Union_eq_univ_iff.2 $ Î» x, âŸ¨K.find x + 1, K.mem_find xâŸ© }

@[simp] lemma find_shiftr (x : Î±) : K.shiftr.find x = K.find x + 1 :=
nat.find_comp_succ _ _ (not_mem_empty _)

lemma mem_diff_shiftr_find (x : Î±) : x âˆˆ K.shiftr (K.find x + 1) \ K.shiftr (K.find x) :=
âŸ¨K.mem_find _, mt K.shiftr.mem_iff_find_le.1 $
  by simp only [find_shiftr, not_le, nat.lt_succ_self]âŸ©

/-- A choice of an
[exhaustion by compact sets](https://en.wikipedia.org/wiki/Exhaustion_by_compact_sets)
of a locally compact sigma compact space. -/
noncomputable def choice (X : Type*) [topological_space X] [locally_compact_space X]
  [sigma_compact_space X] : compact_exhaustion X :=
begin
  apply classical.choice,
  let K : â„• â†’ {s : set X // is_compact s} :=
    Î» n, nat.rec_on n âŸ¨âˆ…, is_compact_emptyâŸ©
      (Î» n s, âŸ¨(exists_compact_superset s.2).some âˆª compact_covering X n,
        (exists_compact_superset s.2).some_spec.1.union (is_compact_compact_covering _ _)âŸ©),
  refine âŸ¨âŸ¨Î» n, K n, Î» n, (K n).2, Î» n, _, _âŸ©âŸ©,
  { exact subset.trans (exists_compact_superset (K n).2).some_spec.2
      (interior_mono $ subset_union_left _ _) },
  { refine univ_subset_iff.1 (Union_compact_covering X â–¸ _),
    exact Union_subset_Union2 (Î» n, âŸ¨n + 1, subset_union_right _ _âŸ©) }
end

noncomputable instance [locally_compact_space Î±] [sigma_compact_space Î±] :
  inhabited (compact_exhaustion Î±) :=
âŸ¨compact_exhaustion.choice Î±âŸ©

end compact_exhaustion

section clopen

/-- A set is clopen if it is both open and closed. -/
def is_clopen (s : set Î±) : Prop :=
is_open s âˆ§ is_closed s

theorem is_clopen.union {s t : set Î±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s âˆª t) :=
âŸ¨is_open.union hs.1 ht.1, is_closed.union hs.2 ht.2âŸ©

theorem is_clopen.inter {s t : set Î±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s âˆ© t) :=
âŸ¨is_open.inter hs.1 ht.1, is_closed.inter hs.2 ht.2âŸ©

@[simp] theorem is_clopen_empty : is_clopen (âˆ… : set Î±) :=
âŸ¨is_open_empty, is_closed_emptyâŸ©

@[simp] theorem is_clopen_univ : is_clopen (univ : set Î±) :=
âŸ¨is_open_univ, is_closed_univâŸ©

theorem is_clopen.compl {s : set Î±} (hs : is_clopen s) : is_clopen sá¶œ :=
âŸ¨hs.2.is_open_compl, is_closed_compl_iff.2 hs.1âŸ©

@[simp] theorem is_clopen_compl_iff {s : set Î±} : is_clopen sá¶œ â†” is_clopen s :=
âŸ¨Î» h, compl_compl s â–¸ is_clopen.compl h, is_clopen.complâŸ©

theorem is_clopen.diff {s t : set Î±} (hs : is_clopen s) (ht : is_clopen t) : is_clopen (s \ t) :=
hs.inter ht.compl

lemma is_clopen_Union {Î² : Type*} [fintype Î²] {s : Î² â†’ set Î±}
  (h : âˆ€ i, is_clopen (s i)) : is_clopen (â‹ƒ i, s i) :=
âŸ¨is_open_Union (forall_and_distrib.1 h).1, is_closed_Union (forall_and_distrib.1 h).2âŸ©

lemma is_clopen_bUnion {Î² : Type*} {s : finset Î²} {f : Î² â†’ set Î±} (h : âˆ€i âˆˆ s, is_clopen $ f i) :
  is_clopen (â‹ƒ i âˆˆ s, f i) :=
begin
  refine âŸ¨is_open_bUnion (Î» i hi, (h i hi).1), _âŸ©,
  show is_closed (â‹ƒ (i : Î²) (H : i âˆˆ (s : set Î²)), f i),
  rw bUnion_eq_Union,
  exact is_closed_Union (Î» âŸ¨i, hiâŸ©,(h i hi).2)
end

lemma is_clopen_Inter {Î² : Type*} [fintype Î²] {s : Î² â†’ set Î±}
  (h : âˆ€ i, is_clopen (s i)) : is_clopen (â‹‚ i, s i) :=
âŸ¨(is_open_Inter (forall_and_distrib.1 h).1), (is_closed_Inter (forall_and_distrib.1 h).2)âŸ©

lemma is_clopen_bInter {Î² : Type*} {s : finset Î²} {f : Î² â†’ set Î±} (h : âˆ€iâˆˆs, is_clopen (f i)) :
  is_clopen (â‹‚iâˆˆs, f i) :=
âŸ¨ is_open_bInter âŸ¨finset_coe.fintype sâŸ© (Î» i hi, (h i hi).1),
  by {show is_closed (â‹‚ (i : Î²) (H : i âˆˆ (â†‘s : set Î²)), f i), rw bInter_eq_Inter,
    apply is_closed_Inter, rintro âŸ¨i, hiâŸ©, exact (h i hi).2}âŸ©

lemma continuous_on.preimage_clopen_of_clopen {Î²: Type*} [topological_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {t : set Î²} (hf : continuous_on f s) (hs : is_clopen s)
  (ht : is_clopen t) : is_clopen (s âˆ© fâ»Â¹' t) :=
âŸ¨continuous_on.preimage_open_of_open hf hs.1 ht.1,
  continuous_on.preimage_closed_of_closed hf hs.2 ht.2âŸ©

/-- The intersection of a disjoint covering by two open sets of a clopen set will be clopen. -/
theorem is_clopen_inter_of_disjoint_cover_clopen {Z a b : set Î±} (h : is_clopen Z)
  (cover : Z âŠ† a âˆª b) (ha : is_open a) (hb : is_open b) (hab : a âˆ© b = âˆ…) : is_clopen (Z âˆ© a) :=
begin
  refine âŸ¨is_open.inter h.1 ha, _âŸ©,
  have : is_closed (Z âˆ© bá¶œ) := is_closed.inter h.2 (is_closed_compl_iff.2 hb),
  convert this using 1,
  apply subset.antisymm,
  { exact inter_subset_inter_right Z (subset_compl_iff_disjoint.2 hab) },
  { rintros x âŸ¨hxâ‚, hxâ‚‚âŸ©,
    exact âŸ¨hxâ‚, by simpa [not_mem_of_mem_compl hxâ‚‚] using cover hxâ‚âŸ© }
end

@[simp] lemma is_clopen_discrete [discrete_topology Î±] (x : set Î±) : is_clopen x :=
âŸ¨is_open_discrete _, is_closed_discrete _âŸ©

end clopen

section preirreducible

/-- A preirreducible set `s` is one where there is no non-trivial pair of disjoint opens on `s`. -/
def is_preirreducible (s : set Î±) : Prop :=
âˆ€ (u v : set Î±), is_open u â†’ is_open v â†’
  (s âˆ© u).nonempty â†’ (s âˆ© v).nonempty â†’ (s âˆ© (u âˆ© v)).nonempty

/-- An irreducible set `s` is one that is nonempty and
where there is no non-trivial pair of disjoint opens on `s`. -/
def is_irreducible (s : set Î±) : Prop :=
s.nonempty âˆ§ is_preirreducible s

lemma is_irreducible.nonempty {s : set Î±} (h : is_irreducible s) :
  s.nonempty := h.1

lemma is_irreducible.is_preirreducible {s : set Î±} (h : is_irreducible s) :
  is_preirreducible s := h.2

theorem is_preirreducible_empty : is_preirreducible (âˆ… : set Î±) :=
Î» _ _ _ _ _ âŸ¨x, h1, h2âŸ©, h1.elim

theorem is_irreducible_singleton {x} : is_irreducible ({x} : set Î±) :=
âŸ¨singleton_nonempty x,
 Î» u v _ _ âŸ¨y, h1, h2âŸ© âŸ¨z, h3, h4âŸ©, by rw mem_singleton_iff at h1 h3;
 substs y z; exact âŸ¨x, rfl, h2, h4âŸ©âŸ©

theorem is_preirreducible.closure {s : set Î±} (H : is_preirreducible s) :
  is_preirreducible (closure s) :=
Î» u v hu hv âŸ¨y, hycs, hyuâŸ© âŸ¨z, hzcs, hzvâŸ©,
let âŸ¨p, hpu, hpsâŸ© := mem_closure_iff.1 hycs u hu hyu in
let âŸ¨q, hqv, hqsâŸ© := mem_closure_iff.1 hzcs v hv hzv in
let âŸ¨r, hrs, hruvâŸ© := H u v hu hv âŸ¨p, hps, hpuâŸ© âŸ¨q, hqs, hqvâŸ© in
âŸ¨r, subset_closure hrs, hruvâŸ©

lemma is_irreducible.closure {s : set Î±} (h : is_irreducible s) :
  is_irreducible (closure s) :=
âŸ¨h.nonempty.closure, h.is_preirreducible.closureâŸ©

theorem exists_preirreducible (s : set Î±) (H : is_preirreducible s) :
  âˆƒ t : set Î±, is_preirreducible t âˆ§ s âŠ† t âˆ§ âˆ€ u, is_preirreducible u â†’ t âŠ† u â†’ u = t :=
let âŸ¨m, hm, hsm, hmmâŸ© := zorn.zorn_subset_nonempty {t : set Î± | is_preirreducible t}
  (Î» c hc hcc hcn, let âŸ¨t, htcâŸ© := hcn in
    âŸ¨â‹ƒâ‚€ c, Î» u v hu hv âŸ¨y, hy, hyuâŸ© âŸ¨z, hz, hzvâŸ©,
      let âŸ¨p, hpc, hypâŸ© := mem_sUnion.1 hy,
          âŸ¨q, hqc, hzqâŸ© := mem_sUnion.1 hz in
      or.cases_on (zorn.chain.total hcc hpc hqc)
        (assume hpq : p âŠ† q, let âŸ¨x, hxp, hxuvâŸ© := hc hqc u v hu hv
            âŸ¨y, hpq hyp, hyuâŸ© âŸ¨z, hzq, hzvâŸ© in
          âŸ¨x, mem_sUnion_of_mem hxp hqc, hxuvâŸ©)
        (assume hqp : q âŠ† p, let âŸ¨x, hxp, hxuvâŸ© := hc hpc u v hu hv
            âŸ¨y, hyp, hyuâŸ© âŸ¨z, hqp hzq, hzvâŸ© in
          âŸ¨x, mem_sUnion_of_mem hxp hpc, hxuvâŸ©),
    Î» x hxc, subset_sUnion_of_mem hxcâŸ©) s H in
âŸ¨m, hm, hsm, Î» u hu hmu, hmm _ hu hmuâŸ©

/-- A maximal irreducible set that contains a given point. -/
def irreducible_component (x : Î±) : set Î± :=
classical.some (exists_preirreducible {x} is_irreducible_singleton.is_preirreducible)

lemma irreducible_component_property (x : Î±) :
  is_preirreducible (irreducible_component x) âˆ§ {x} âŠ† (irreducible_component x) âˆ§
  âˆ€ u, is_preirreducible u â†’ (irreducible_component x) âŠ† u â†’ u = (irreducible_component x) :=
classical.some_spec (exists_preirreducible {x} is_irreducible_singleton.is_preirreducible)

theorem mem_irreducible_component {x : Î±} : x âˆˆ irreducible_component x :=
singleton_subset_iff.1 (irreducible_component_property x).2.1

theorem is_irreducible_irreducible_component {x : Î±} : is_irreducible (irreducible_component x) :=
âŸ¨âŸ¨x, mem_irreducible_componentâŸ©, (irreducible_component_property x).1âŸ©

theorem eq_irreducible_component {x : Î±} :
  âˆ€ {s : set Î±}, is_preirreducible s â†’ irreducible_component x âŠ† s â†’ s = irreducible_component x :=
(irreducible_component_property x).2.2

theorem is_closed_irreducible_component {x : Î±} :
  is_closed (irreducible_component x) :=
closure_eq_iff_is_closed.1 $ eq_irreducible_component
  is_irreducible_irreducible_component.is_preirreducible.closure
  subset_closure

/-- A preirreducible space is one where there is no non-trivial pair of disjoint opens. -/
class preirreducible_space (Î± : Type u) [topological_space Î±] : Prop :=
(is_preirreducible_univ [] : is_preirreducible (univ : set Î±))

/-- An irreducible space is one that is nonempty
and where there is no non-trivial pair of disjoint opens. -/
class irreducible_space (Î± : Type u) [topological_space Î±] extends preirreducible_space Î± : Prop :=
(to_nonempty [] : nonempty Î±)

-- see Note [lower instance priority]
attribute [instance, priority 50] irreducible_space.to_nonempty

theorem nonempty_preirreducible_inter [preirreducible_space Î±] {s t : set Î±} :
  is_open s â†’ is_open t â†’ s.nonempty â†’ t.nonempty â†’ (s âˆ© t).nonempty :=
by simpa only [univ_inter, univ_subset_iff] using
  @preirreducible_space.is_preirreducible_univ Î± _ _ s t

theorem is_preirreducible.image [topological_space Î²] {s : set Î±} (H : is_preirreducible s)
  (f : Î± â†’ Î²) (hf : continuous_on f s) : is_preirreducible (f '' s) :=
begin
  rintros u v hu hv âŸ¨_, âŸ¨âŸ¨x, hx, rflâŸ©, hxuâŸ©âŸ© âŸ¨_, âŸ¨âŸ¨y, hy, rflâŸ©, hyvâŸ©âŸ©,
  rw â† mem_preimage at hxu hyv,
  rcases continuous_on_iff'.1 hf u hu with âŸ¨u', hu', u'_eqâŸ©,
  rcases continuous_on_iff'.1 hf v hv with âŸ¨v', hv', v'_eqâŸ©,
  have := H u' v' hu' hv',
  rw [inter_comm s u', â† u'_eq] at this,
  rw [inter_comm s v', â† v'_eq] at this,
  rcases this âŸ¨x, hxu, hxâŸ© âŸ¨y, hyv, hyâŸ© with âŸ¨z, hzs, hzu', hzv'âŸ©,
  refine âŸ¨f z, mem_image_of_mem f hzs, _, _âŸ©,
  all_goals
  { rw â† mem_preimage,
    apply mem_of_mem_inter_left,
    show z âˆˆ _ âˆ© s,
    simp [*] }
end

theorem is_irreducible.image [topological_space Î²] {s : set Î±} (H : is_irreducible s)
  (f : Î± â†’ Î²) (hf : continuous_on f s) : is_irreducible (f '' s) :=
âŸ¨nonempty_image_iff.mpr H.nonempty, H.is_preirreducible.image f hfâŸ©

lemma subtype.preirreducible_space {s : set Î±} (h : is_preirreducible s) :
  preirreducible_space s :=
{ is_preirreducible_univ :=
  begin
    intros u v hu hv hsu hsv,
    rw is_open_induced_iff at hu hv,
    rcases hu with âŸ¨u, hu, rflâŸ©,
    rcases hv with âŸ¨v, hv, rflâŸ©,
    rcases hsu with âŸ¨âŸ¨x, hxsâŸ©, hxs', hxuâŸ©,
    rcases hsv with âŸ¨âŸ¨y, hysâŸ©, hys', hyvâŸ©,
    rcases h u v hu hv âŸ¨x, hxs, hxuâŸ© âŸ¨y, hys, hyvâŸ© with âŸ¨z, hzs, âŸ¨hzu, hzvâŸ©âŸ©,
    exact âŸ¨âŸ¨z, hzsâŸ©, âŸ¨set.mem_univ _, âŸ¨hzu, hzvâŸ©âŸ©âŸ©
  end }

lemma subtype.irreducible_space {s : set Î±} (h : is_irreducible s) :
  irreducible_space s :=
{ is_preirreducible_univ :=
  (subtype.preirreducible_space h.is_preirreducible).is_preirreducible_univ,
  to_nonempty := h.nonempty.to_subtype }

/-- A set `s` is irreducible if and only if
for every finite collection of open sets all of whose members intersect `s`,
`s` also intersects the intersection of the entire collection
(i.e., there is an element of `s` contained in every member of the collection). -/
lemma is_irreducible_iff_sInter {s : set Î±} :
  is_irreducible s â†”
  âˆ€ (U : finset (set Î±)) (hU : âˆ€ u âˆˆ U, is_open u) (H : âˆ€ u âˆˆ U, (s âˆ© u).nonempty),
  (s âˆ© â‹‚â‚€ â†‘U).nonempty :=
begin
  split; intro h,
  { intro U, apply finset.induction_on U,
    { intros, simpa using h.nonempty },
    { intros u U hu IH hU H,
      rw [finset.coe_insert, sInter_insert],
      apply h.2,
      { solve_by_elim [finset.mem_insert_self] },
      { apply is_open_sInter (finset.finite_to_set U),
        intros, solve_by_elim [finset.mem_insert_of_mem] },
      { solve_by_elim [finset.mem_insert_self] },
      { apply IH,
        all_goals { intros, solve_by_elim [finset.mem_insert_of_mem] } } } },
  { split,
    { simpa using h âˆ… _ _; intro u; simp },
    intros u v hu hv hu' hv',
    simpa using h {u,v} _ _,
    all_goals
    { intro t,
      rw [finset.mem_insert, finset.mem_singleton],
      rintro (rfl|rfl); assumption } }
end

/-- A set is preirreducible if and only if
for every cover by two closed sets, it is contained in one of the two covering sets. -/
lemma is_preirreducible_iff_closed_union_closed {s : set Î±} :
  is_preirreducible s â†”
  âˆ€ (zâ‚ zâ‚‚ : set Î±), is_closed zâ‚ â†’ is_closed zâ‚‚ â†’ s âŠ† zâ‚ âˆª zâ‚‚ â†’ s âŠ† zâ‚ âˆ¨ s âŠ† zâ‚‚ :=
begin
  split,
  all_goals
  { intros h tâ‚ tâ‚‚ htâ‚ htâ‚‚,
    specialize h tâ‚á¶œ tâ‚‚á¶œ,
    simp only [is_open_compl_iff, is_closed_compl_iff] at h,
    specialize h htâ‚ htâ‚‚ },
  { contrapose!, simp only [not_subset],
    rintro âŸ¨âŸ¨x, hx, hx'âŸ©, âŸ¨y, hy, hy'âŸ©âŸ©,
    rcases h âŸ¨x, hx, hx'âŸ© âŸ¨y, hy, hy'âŸ© with âŸ¨z, hz, hz'âŸ©,
    rw â† compl_union at hz',
    exact âŸ¨z, hz, hz'âŸ© },
  { rintro âŸ¨x, hx, hx'âŸ© âŸ¨y, hy, hy'âŸ©,
    rw â† compl_inter at h,
    delta set.nonempty,
    rw imp_iff_not_or at h,
    contrapose! h,
    split,
    { intros z hz hz', exact h z âŸ¨hz, hz'âŸ© },
    { split; intro H; refine H _ â€¹_â€º; assumption } }
end

/-- A set is irreducible if and only if
for every cover by a finite collection of closed sets,
it is contained in one of the members of the collection. -/
lemma is_irreducible_iff_sUnion_closed {s : set Î±} :
  is_irreducible s â†”
  âˆ€ (Z : finset (set Î±)) (hZ : âˆ€ z âˆˆ Z, is_closed z) (H : s âŠ† â‹ƒâ‚€ â†‘Z),
  âˆƒ z âˆˆ Z, s âŠ† z :=
begin
  rw [is_irreducible, is_preirreducible_iff_closed_union_closed],
  split; intro h,
  { intro Z, apply finset.induction_on Z,
    { intros, rw [finset.coe_empty, sUnion_empty] at H,
      rcases h.1 with âŸ¨x, hxâŸ©,
      exfalso, tauto },
    { intros z Z hz IH hZ H,
      cases h.2 z (â‹ƒâ‚€ â†‘Z) _ _ _
        with h' h',
      { exact âŸ¨z, finset.mem_insert_self _ _, h'âŸ© },
      { rcases IH _ h' with âŸ¨z', hz', hsz'âŸ©,
        { exact âŸ¨z', finset.mem_insert_of_mem hz', hsz'âŸ© },
        { intros, solve_by_elim [finset.mem_insert_of_mem] } },
      { solve_by_elim [finset.mem_insert_self] },
      { rw sUnion_eq_bUnion,
        apply is_closed_bUnion (finset.finite_to_set Z),
        { intros, solve_by_elim [finset.mem_insert_of_mem] } },
      { simpa using H } } },
  { split,
    { by_contradiction hs,
      simpa using h âˆ… _ _,
      { intro z, simp },
      { simpa [set.nonempty] using hs } },
    intros zâ‚ zâ‚‚ hzâ‚ hzâ‚‚ H,
    have := h {zâ‚, zâ‚‚} _ _,
    simp only [exists_prop, finset.mem_insert, finset.mem_singleton] at this,
    { rcases this with âŸ¨z, rfl|rfl, hzâŸ©; tauto },
    { intro t,
      rw [finset.mem_insert, finset.mem_singleton],
      rintro (rfl|rfl); assumption },
    { simpa using H } }
end

end preirreducible
