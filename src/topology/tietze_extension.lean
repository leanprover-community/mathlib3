/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.specific_limits.basic
import data.set.intervals.iso_Ioo
import topology.algebra.order.monotone_continuity
import topology.urysohns_bounded

/-!
# Tietze extension theorem

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we prove a few version of the Tietze extension theorem. The theorem says that a
continuous function `s ‚Üí ‚Ñù` defined on a closed set in a normal topological space `Y` can be
extended to a continuous function on the whole space. Moreover, if all values of the original
function belong to some (finite or infinite, open or closed) interval, then the extension can be
chosen so that it takes values in the same interval. In particular, if the original function is a
bounded function, then there exists a bounded extension of the same norm.

The proof mostly follows <https://ncatlab.org/nlab/show/Tietze+extension+theorem>. We patch a small
gap in the proof for unbounded functions, see
`exists_extension_forall_exists_le_ge_of_closed_embedding`.

## Implementation notes

We first prove the theorems for a closed embedding `e : X ‚Üí Y` of a topological space into a normal
topological space, then specialize them to the case `X = s : set Y`, `e = coe`.

## Tags

Tietze extension theorem, Urysohn's lemma, normal topological space
-/

variables {X Y : Type*} [topological_space X] [topological_space Y] [normal_space Y]

open metric set filter
open_locale bounded_continuous_function topology
noncomputable theory

namespace bounded_continuous_function

/-- One step in the proof of the Tietze extension theorem. If `e : C(X, Y)` is a closed embedding
of a topological space into a normal topological space and `f : X ‚Üí·µá ‚Ñù` is a bounded continuous
function, then there exists a bounded continuous function `g : Y ‚Üí·µá ‚Ñù` of the norm `‚Äñg‚Äñ ‚â§ ‚Äñf‚Äñ / 3`
such that the distance between `g ‚àò e` and `f` is at most `(2 / 3) * ‚Äñf‚Äñ`. -/
lemma tietze_extension_step (f : X ‚Üí·µá ‚Ñù) (e : C(X, Y)) (he : closed_embedding e) :
  ‚àÉ g : Y ‚Üí·µá ‚Ñù, ‚Äñg‚Äñ ‚â§ ‚Äñf‚Äñ / 3 ‚àß dist (g.comp_continuous e) f ‚â§ (2 / 3) * ‚Äñf‚Äñ :=
begin
  have h3 : (0 : ‚Ñù) < 3 := by norm_num1,
  have h23 : 0 < (2 / 3 : ‚Ñù) := by norm_num1,
  -- In the trivial case `f = 0`, we take `g = 0`
  rcases eq_or_ne f 0 with (rfl|hf), { use 0, simp },
  replace hf : 0 < ‚Äñf‚Äñ := norm_pos_iff.2 hf,
  /- Otherwise, the closed sets `e '' (f ‚Åª¬π' (Iic (-‚Äñf‚Äñ / 3)))` and `e '' (f ‚Åª¬π' (Ici (‚Äñf‚Äñ / 3)))`
  are disjoint, hence by Urysohn's lemma there exists a function `g` that is equal to `-‚Äñf‚Äñ / 3`
  on the former set and is equal to `‚Äñf‚Äñ / 3` on the latter set. This function `g` satisfies the
  assertions of the lemma. -/
  have hf3 : -‚Äñf‚Äñ / 3 < ‚Äñf‚Äñ / 3, from (div_lt_div_right h3).2 (left.neg_lt_self hf),
  have hc‚ÇÅ : is_closed (e '' (f ‚Åª¬π' (Iic (-‚Äñf‚Äñ / 3)))),
    from he.is_closed_map _ (is_closed_Iic.preimage f.continuous),
  have hc‚ÇÇ : is_closed (e '' (f ‚Åª¬π' (Ici (‚Äñf‚Äñ / 3)))),
    from he.is_closed_map _ (is_closed_Ici.preimage f.continuous),
  have hd : disjoint (e '' (f ‚Åª¬π' (Iic (-‚Äñf‚Äñ / 3)))) (e '' (f ‚Åª¬π' (Ici (‚Äñf‚Äñ / 3)))),
  { refine disjoint_image_of_injective he.inj (disjoint.preimage _ _),
    rwa [Iic_disjoint_Ici, not_le] },
  rcases exists_bounded_mem_Icc_of_closed_of_le hc‚ÇÅ hc‚ÇÇ hd hf3.le with ‚ü®g, hg‚ÇÅ, hg‚ÇÇ, hgf‚ü©,
  refine ‚ü®g, _, _‚ü©,
  { refine (norm_le $ div_nonneg hf.le h3.le).mpr (Œª y, _),
    simpa [abs_le, neg_div] using hgf y },
  { refine (dist_le $ mul_nonneg h23.le hf.le).mpr (Œª x, _),
    have hfx : -‚Äñf‚Äñ ‚â§ f x ‚àß f x ‚â§ ‚Äñf‚Äñ,
      by simpa only [real.norm_eq_abs, abs_le] using f.norm_coe_le_norm x,
    cases le_total (f x) (-‚Äñf‚Äñ / 3) with hle‚ÇÅ hle‚ÇÅ,
    { calc |g (e x) - f x| = -‚Äñf‚Äñ / 3 - f x:
        by rw [hg‚ÇÅ (mem_image_of_mem _ hle‚ÇÅ), abs_of_nonneg (sub_nonneg.2 hle‚ÇÅ)]
      ... ‚â§ (2 / 3) * ‚Äñf‚Äñ : by linarith },
    { cases le_total (f x) (‚Äñf‚Äñ / 3) with hle‚ÇÇ hle‚ÇÇ,
      { simp only [neg_div] at *,
        calc dist (g (e x)) (f x) ‚â§ |g (e x)| + |f x| : dist_le_norm_add_norm _ _
        ... ‚â§ ‚Äñf‚Äñ / 3 + ‚Äñf‚Äñ / 3 :
          add_le_add (abs_le.2 $ hgf _) (abs_le.2 ‚ü®hle‚ÇÅ, hle‚ÇÇ‚ü©)
        ... = (2 / 3) * ‚Äñf‚Äñ : by linarith },
      { calc |g (e x) - f x| = f x - ‚Äñf‚Äñ / 3 :
          by rw [hg‚ÇÇ (mem_image_of_mem _ hle‚ÇÇ), abs_sub_comm, abs_of_nonneg (sub_nonneg.2 hle‚ÇÇ)]
        ... ‚â§ (2 / 3) * ‚Äñf‚Äñ : by linarith } } }
end

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version with a closed
embedding and bundled composition. If `e : C(X, Y)` is a closed embedding of a topological space
into a normal topological space and `f : X ‚Üí·µá ‚Ñù` is a bounded continuous function, then there exists
a bounded continuous function `g : Y ‚Üí·µá ‚Ñù` of the same norm such that `g ‚àò e = f`. -/
lemma exists_extension_norm_eq_of_closed_embedding' (f : X ‚Üí·µá ‚Ñù) (e : C(X, Y))
  (he : closed_embedding e) :
  ‚àÉ g : Y ‚Üí·µá ‚Ñù, ‚Äñg‚Äñ = ‚Äñf‚Äñ ‚àß g.comp_continuous e = f :=
begin
  /- For the proof, we iterate `tietze_extension_step`. Each time we apply it to the difference
  between the previous approximation and `f`. -/
  choose F hF_norm hF_dist using Œª f : X ‚Üí·µá ‚Ñù, tietze_extension_step f e he,
  set g : ‚Ñï ‚Üí Y ‚Üí·µá ‚Ñù := Œª n, (Œª g, g + F (f - g.comp_continuous e))^[n] 0,
  have g0 : g 0 = 0 := rfl,
  have g_succ : ‚àÄ n, g (n + 1) = g n + F (f - (g n).comp_continuous e),
    from Œª n, function.iterate_succ_apply' _ _ _,
  have hgf : ‚àÄ n, dist ((g n).comp_continuous e) f ‚â§ (2 / 3) ^ n * ‚Äñf‚Äñ,
  { intro n, induction n with n ihn,
    { simp [g0] },
    { rw [g_succ n, add_comp_continuous, ‚Üê dist_sub_right, add_sub_cancel', pow_succ, mul_assoc],
      refine (hF_dist _).trans (mul_le_mul_of_nonneg_left _ (by norm_num1)),
      rwa ‚Üê dist_eq_norm' } },
  have hg_dist : ‚àÄ n, dist (g n) (g (n + 1)) ‚â§ 1 / 3 * ‚Äñf‚Äñ * (2 / 3) ^ n,
  { intro n,
    calc dist (g n) (g (n + 1)) = ‚ÄñF (f - (g n).comp_continuous e)‚Äñ :
      by rw [g_succ, dist_eq_norm', add_sub_cancel']
    ... ‚â§ ‚Äñf - (g n).comp_continuous e‚Äñ / 3 : hF_norm _
    ... = (1 / 3) * dist ((g n).comp_continuous e) f :
      by rw [dist_eq_norm', one_div, div_eq_inv_mul]
    ... ‚â§ (1 / 3) * ((2 / 3) ^ n * ‚Äñf‚Äñ) :
      mul_le_mul_of_nonneg_left (hgf n) (by norm_num1)
    ... = 1 / 3 * ‚Äñf‚Äñ * (2 / 3) ^ n : by ac_refl },
  have hg_cau : cauchy_seq g, from cauchy_seq_of_le_geometric _ _ (by norm_num1) hg_dist,
  have : tendsto (Œª n, (g n).comp_continuous e) at_top (ùìù $ (lim at_top g).comp_continuous e),
    from ((continuous_comp_continuous e).tendsto _).comp hg_cau.tendsto_lim,
  have hge : (lim at_top g).comp_continuous e = f,
  { refine tendsto_nhds_unique this (tendsto_iff_dist_tendsto_zero.2 _),
    refine squeeze_zero (Œª _, dist_nonneg) hgf _,
    rw ‚Üê zero_mul (‚Äñf‚Äñ),
    refine (tendsto_pow_at_top_nhds_0_of_lt_1 _ _).mul tendsto_const_nhds; norm_num1 },
  refine ‚ü®lim at_top g, le_antisymm _ _, hge‚ü©,
  { rw [‚Üê dist_zero_left, ‚Üê g0],
    refine (dist_le_of_le_geometric_of_tendsto‚ÇÄ _ _ (by norm_num1)
      hg_dist hg_cau.tendsto_lim).trans_eq _,
    field_simp [show (3 - 2 : ‚Ñù) = 1, by norm_num1] },
  { rw ‚Üê hge, exact norm_comp_continuous_le _ _ }
end

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version with a closed
embedding and unbundled composition. If `e : C(X, Y)` is a closed embedding of a topological space
into a normal topological space and `f : X ‚Üí·µá ‚Ñù` is a bounded continuous function, then there exists
a bounded continuous function `g : Y ‚Üí·µá ‚Ñù` of the same norm such that `g ‚àò e = f`. -/
lemma exists_extension_norm_eq_of_closed_embedding (f : X ‚Üí·µá ‚Ñù) {e : X ‚Üí Y}
  (he : closed_embedding e) :
  ‚àÉ g : Y ‚Üí·µá ‚Ñù, ‚Äñg‚Äñ = ‚Äñf‚Äñ ‚àß g ‚àò e = f :=
begin
  rcases exists_extension_norm_eq_of_closed_embedding' f ‚ü®e, he.continuous‚ü© he with ‚ü®g, hg, rfl‚ü©,
  exact ‚ü®g, hg, rfl‚ü©
end

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
set. If `f` is a bounded continuous real-valued function defined on a closed set in a normal
topological space, then it can be extended to a bounded continuous function of the same norm defined
on the whole space. -/
lemma exists_norm_eq_restrict_eq_of_closed {s : set Y} (f : s ‚Üí·µá ‚Ñù) (hs : is_closed s) :
  ‚àÉ g : Y ‚Üí·µá ‚Ñù, ‚Äñg‚Äñ = ‚Äñf‚Äñ ‚àß g.restrict s = f :=
exists_extension_norm_eq_of_closed_embedding' f ((continuous_map.id _).restrict s)
  (closed_embedding_subtype_coe hs)

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding and a bounded continuous function that takes values in a non-trivial closed interval.
See also `exists_extension_forall_mem_of_closed_embedding` for a more general statement that works
for any interval (finite or infinite, open or closed).

If `e : X ‚Üí Y` is a closed embedding and `f : X ‚Üí·µá ‚Ñù` is a bounded continuous function such that
`f x ‚àà [a, b]` for all `x`, where `a ‚â§ b`, then there exists a bounded continuous function
`g : Y ‚Üí·µá ‚Ñù` such that `g y ‚àà [a, b]` for all `y` and `g ‚àò e = f`. -/
lemma exists_extension_forall_mem_Icc_of_closed_embedding (f : X ‚Üí·µá ‚Ñù) {a b : ‚Ñù} {e : X ‚Üí Y}
  (hf : ‚àÄ x, f x ‚àà Icc a b) (hle : a ‚â§ b) (he : closed_embedding e) :
  ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, g y ‚àà Icc a b) ‚àß g ‚àò e = f :=
begin
  rcases exists_extension_norm_eq_of_closed_embedding (f - const X ((a + b) / 2)) he
    with ‚ü®g, hgf, hge‚ü©,
  refine ‚ü®const Y ((a + b) / 2) + g, Œª y, _, _‚ü©,
  { suffices : ‚Äñf - const X ((a + b) / 2)‚Äñ ‚â§ (b - a) / 2,
      by simpa [real.Icc_eq_closed_ball, add_mem_closed_ball_iff_norm]
        using (norm_coe_le_norm g y).trans (hgf.trans_le this),
    refine (norm_le $ div_nonneg (sub_nonneg.2 hle) zero_le_two).2 (Œª x, _),
    simpa only [real.Icc_eq_closed_ball] using hf x },
  { ext x,
    have : g (e x) = f x - (a + b) / 2 := congr_fun hge x,
    simp [this] }
end

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a bounded continuous real-valued function on `X`. Then there
exists a bounded continuous function `g : Y ‚Üí·µá ‚Ñù` such that `g ‚àò e = f` and each value `g y` belongs
to a closed interval `[f x‚ÇÅ, f x‚ÇÇ]` for some `x‚ÇÅ` and `x‚ÇÇ`.  -/
lemma exists_extension_forall_exists_le_ge_of_closed_embedding [nonempty X] (f : X ‚Üí·µá ‚Ñù) {e : X ‚Üí Y}
  (he : closed_embedding e) :
  ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, ‚àÉ x‚ÇÅ x‚ÇÇ, g y ‚àà Icc (f x‚ÇÅ) (f x‚ÇÇ)) ‚àß g ‚àò e = f :=
begin
  inhabit X,
  -- Put `a = ‚®Ö x, f x` and `b = ‚®Ü x, f x`
  obtain ‚ü®a, ha‚ü© : ‚àÉ a, is_glb (range f) a,
    from ‚ü®_, is_glb_cinfi (real.bounded_iff_bdd_below_bdd_above.1 f.bounded_range).1‚ü©,
  obtain ‚ü®b, hb‚ü© : ‚àÉ b, is_lub (range f) b,
    from ‚ü®_, is_lub_csupr (real.bounded_iff_bdd_below_bdd_above.1 f.bounded_range).2‚ü©,
  -- Then `f x ‚àà [a, b]` for all `x`
  have hmem : ‚àÄ x, f x ‚àà Icc a b, from Œª x, ‚ü®ha.1 ‚ü®x, rfl‚ü©, hb.1 ‚ü®x, rfl‚ü©‚ü©,
  -- Rule out the trivial case `a = b`
  have hle : a ‚â§ b := (hmem default).1.trans (hmem default).2,
  rcases hle.eq_or_lt with (rfl|hlt),
  { have : ‚àÄ x, f x = a, by simpa using hmem,
    use const Y a, simp [this, function.funext_iff] },
  -- Put `c = (a + b) / 2`. Then `a < c < b` and `c - a = b - c`.
  set c := (a + b) / 2,
  have hac : a < c := left_lt_add_div_two.2 hlt,
  have hcb : c < b := add_div_two_lt_right.2 hlt,
  have hsub : c - a = b - c, by { simp only [c], field_simp, ring },
  /- Due to `exists_extension_forall_mem_Icc_of_closed_embedding`, there exists an extension `g`
  such that `g y ‚àà [a, b]` for all `y`. However, if `a` and/or `b` do not belong to the range of
  `f`, then we need to ensure that these points do not belong to the range of `g`. This is done
  in two almost identical steps. First we deal with the case `‚àÄ x, f x ‚â† a`. -/
  obtain ‚ü®g, hg_mem, hgf‚ü© : ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, ‚àÉ x, g y ‚àà Icc (f x) b) ‚àß g ‚àò e = f,
  { rcases exists_extension_forall_mem_Icc_of_closed_embedding f hmem hle he
      with ‚ü®g, hg_mem, hgf‚ü©,
    -- If `a ‚àà range f`, then we are done.
    rcases em (‚àÉ x, f x = a) with ‚ü®x, rfl‚ü©|ha',
    { exact ‚ü®g, Œª y, ‚ü®x, hg_mem _‚ü©, hgf‚ü© },
    /- Otherwise, `g ‚Åª¬π' {a}` is disjoint with `range e ‚à™ g ‚Åª¬π' (Ici c)`, hence there exists a
    function `dg : Y ‚Üí ‚Ñù` such that `dg ‚àò e = 0`, `dg y = 0` whenever `c ‚â§ g y`, `dg y = c - a`
    whenever `g y = a`, and `0 ‚â§ dg y ‚â§ c - a` for all `y`.  -/
    have hd : disjoint (range e ‚à™ g ‚Åª¬π' (Ici c)) (g ‚Åª¬π' {a}),
    { refine disjoint_union_left.2 ‚ü®_, disjoint.preimage _ _‚ü©,
      { rw set.disjoint_left,
        rintro _ ‚ü®x, rfl‚ü© (rfl : g (e x) = a),
        exact ha' ‚ü®x, (congr_fun hgf x).symm‚ü© },
      { exact set.disjoint_singleton_right.2 hac.not_le } },
    rcases exists_bounded_mem_Icc_of_closed_of_le
      (he.closed_range.union $ is_closed_Ici.preimage g.continuous)
      (is_closed_singleton.preimage g.continuous) hd (sub_nonneg.2 hac.le)
      with ‚ü®dg, dg0, dga, dgmem‚ü©,
    replace hgf : ‚àÄ x, (g + dg) (e x) = f x,
    { intro x, simp [dg0 (or.inl $ mem_range_self _), ‚Üê hgf] },
    refine ‚ü®g + dg, Œª y, _, funext hgf‚ü©,
    { have hay : a < (g + dg) y,
      { rcases (hg_mem y).1.eq_or_lt with rfl|hlt,
        { refine (lt_add_iff_pos_right _).2 _,
          calc 0 < c - g y : sub_pos.2 hac
             ... = dg y    : (dga rfl).symm },
        { exact hlt.trans_le ((le_add_iff_nonneg_right _).2 $ (dgmem y).1) } },
      rcases ha.exists_between hay with ‚ü®_, ‚ü®x, rfl‚ü©, hax, hxy‚ü©,
      refine ‚ü®x, hxy.le, _‚ü©,
      cases le_total c (g y) with hc hc,
      { simp [dg0 (or.inr hc), (hg_mem y).2] },
      { calc g y + dg y ‚â§ c + (c - a) : add_le_add hc (dgmem _).2
                    ... = b           : by rw [hsub, add_sub_cancel'_right] } } },
  /- Now we deal with the case `‚àÄ x, f x ‚â† b`. The proof is the same as in the first case, with
  minor modifications that make it hard to deduplicate code. -/
  choose xl hxl hgb using hg_mem,
  rcases em (‚àÉ x, f x = b) with ‚ü®x, rfl‚ü©|hb',
  { exact ‚ü®g, Œª y, ‚ü®xl y, x, hxl y, hgb y‚ü©, hgf‚ü© },
  have hd : disjoint (range e ‚à™ g ‚Åª¬π' (Iic c)) (g ‚Åª¬π' {b}),
  { refine disjoint_union_left.2 ‚ü®_, disjoint.preimage _ _‚ü©,
    { rw set.disjoint_left,
      rintro _ ‚ü®x, rfl‚ü© (rfl : g (e x) = b),
      exact hb' ‚ü®x, (congr_fun hgf x).symm‚ü© },
    { exact set.disjoint_singleton_right.2 hcb.not_le } },
  rcases exists_bounded_mem_Icc_of_closed_of_le
    (he.closed_range.union $ is_closed_Iic.preimage g.continuous)
    (is_closed_singleton.preimage g.continuous) hd (sub_nonneg.2 hcb.le)
    with ‚ü®dg, dg0, dgb, dgmem‚ü©,
  replace hgf : ‚àÄ x, (g - dg) (e x) = f x,
  { intro x, simp [dg0 (or.inl $ mem_range_self _), ‚Üê hgf] },
  refine ‚ü®g - dg, Œª y, _, funext hgf‚ü©,
  { have hyb : (g - dg) y < b,
    { rcases (hgb y).eq_or_lt with rfl|hlt,
      { refine (sub_lt_self_iff _).2 _,
        calc 0 < g y - c : sub_pos.2 hcb
           ... = dg y    : (dgb rfl).symm },
      { exact ((sub_le_self_iff _).2 (dgmem _).1).trans_lt hlt } },
    rcases hb.exists_between hyb with ‚ü®_, ‚ü®xu, rfl‚ü©, hyxu, hxub‚ü©,
    cases lt_or_le c (g y) with hc hc,
    { rcases em (a ‚àà range f) with ‚ü®x, rfl‚ü©|ha',
      { refine ‚ü®x, xu, _, hyxu.le‚ü©,
        calc f x = c - (b - c) : by rw [‚Üê hsub, sub_sub_cancel]
             ... ‚â§ g y - dg y  : sub_le_sub hc.le (dgmem _).2 },
      { have hay : a < (g - dg) y,
        { calc a = c - (b - c)   : by rw [‚Üê hsub, sub_sub_cancel]
             ... < g y - (b - c) : sub_lt_sub_right hc _
             ... ‚â§ g y - dg y    : sub_le_sub_left (dgmem _).2 _ },
        rcases ha.exists_between hay with ‚ü®_, ‚ü®x, rfl‚ü©, ha, hxy‚ü©,
        exact ‚ü®x, xu, hxy.le, hyxu.le‚ü© } },
    { refine ‚ü®xl y, xu, _, hyxu.le‚ü©,
      simp [dg0 (or.inr hc), hxl] } },
end

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a bounded continuous real-valued function on `X`. Let `t` be
a nonempty convex set of real numbers (we use `ord_connected` instead of `convex` to automatically
deduce this argument by typeclass search) such that `f x ‚àà t` for all `x`. Then there exists
a bounded continuous real-valued function `g : Y ‚Üí·µá ‚Ñù` such that `g y ‚àà t` for all `y` and
`g ‚àò e = f`. -/
lemma exists_extension_forall_mem_of_closed_embedding (f : X ‚Üí·µá ‚Ñù) {t : set ‚Ñù} {e : X ‚Üí Y}
  [hs : ord_connected t] (hf : ‚àÄ x, f x ‚àà t) (hne : t.nonempty) (he : closed_embedding e) :
  ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, g y ‚àà t) ‚àß g ‚àò e = f :=
begin
  casesI is_empty_or_nonempty X,
  { rcases hne with ‚ü®c, hc‚ü©,
    refine ‚ü®const Y c, Œª y, hc, funext $ Œª x, is_empty_elim x‚ü© },
  rcases exists_extension_forall_exists_le_ge_of_closed_embedding f he with ‚ü®g, hg, hgf‚ü©,
  refine ‚ü®g, Œª y, _, hgf‚ü©,
  rcases hg y with ‚ü®xl, xu, h‚ü©,
  exact hs.out (hf _) (hf _) h
end

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
set. Let `s` be a closed set in a normal topological space `Y`. Let `f` be a bounded continuous
real-valued function on `s`. Let `t` be a nonempty convex set of real numbers (we use
`ord_connected` instead of `convex` to automatically deduce this argument by typeclass search) such
that `f x ‚àà t` for all `x : s`. Then there exists a bounded continuous real-valued function
`g : Y ‚Üí·µá ‚Ñù` such that `g y ‚àà t` for all `y` and `g.restrict s = f`. -/
lemma exists_forall_mem_restrict_eq_of_closed {s : set Y} (f : s ‚Üí·µá ‚Ñù) (hs : is_closed s)
  {t : set ‚Ñù} [ord_connected t] (hf : ‚àÄ x, f x ‚àà t) (hne : t.nonempty) :
  ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, g y ‚àà t) ‚àß g.restrict s = f :=
begin
  rcases exists_extension_forall_mem_of_closed_embedding f hf hne (closed_embedding_subtype_coe hs)
    with ‚ü®g, hg, hgf‚ü©,
  exact ‚ü®g, hg, fun_like.coe_injective hgf‚ü©
end

end bounded_continuous_function

namespace continuous_map

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a continuous real-valued function on `X`. Let `t` be a nonempty
convex set of real numbers (we use `ord_connected` instead of `convex` to automatically deduce this
argument by typeclass search) such that `f x ‚àà t` for all `x`. Then there exists a continuous
real-valued function `g : C(Y, ‚Ñù)` such that `g y ‚àà t` for all `y` and `g ‚àò e = f`. -/
lemma exists_extension_forall_mem_of_closed_embedding (f : C(X, ‚Ñù)) {t : set ‚Ñù} {e : X ‚Üí Y}
  [hs : ord_connected t] (hf : ‚àÄ x, f x ‚àà t) (hne : t.nonempty) (he : closed_embedding e) :
  ‚àÉ g : C(Y, ‚Ñù), (‚àÄ y, g y ‚àà t) ‚àß g ‚àò e = f :=
begin
  have h : ‚Ñù ‚âÉo Ioo (-1 : ‚Ñù) 1 := order_iso_Ioo_neg_one_one ‚Ñù,
  set F : X ‚Üí·µá ‚Ñù :=
  { to_fun := coe ‚àò (h ‚àò f),
    continuous_to_fun := continuous_subtype_coe.comp (h.continuous.comp f.continuous),
    map_bounded' := bounded_range_iff.1 ((bounded_Ioo (-1 : ‚Ñù) 1).mono $
      forall_range_iff.2 $ Œª x, (h (f x)).2) },
  set t' : set ‚Ñù := (coe ‚àò h) '' t,
  have ht_sub : t' ‚äÜ Ioo (-1 : ‚Ñù) 1 := image_subset_iff.2 (Œª x hx, (h x).2),
  haveI : ord_connected t',
  { constructor, rintros _ ‚ü®x, hx, rfl‚ü© _ ‚ü®y, hy, rfl‚ü© z hz,
    lift z to Ioo (-1 : ‚Ñù) 1 using (Icc_subset_Ioo (h x).2.1 (h y).2.2 hz),
    change z ‚àà Icc (h x) (h y) at hz, rw [‚Üê h.image_Icc] at hz,
    rcases hz with ‚ü®z, hz, rfl‚ü©,
    exact ‚ü®z, hs.out hx hy hz, rfl‚ü© },
  have hFt : ‚àÄ x, F x ‚àà t', from Œª x, mem_image_of_mem _ (hf x),
  rcases F.exists_extension_forall_mem_of_closed_embedding hFt (hne.image _) he
    with ‚ü®G, hG, hGF‚ü©,
  set g : C(Y, ‚Ñù) := ‚ü®h.symm ‚àò cod_restrict G _ (Œª y, ht_sub (hG y)), h.symm.continuous.comp $
    G.continuous.subtype_mk _‚ü©,
  have hgG : ‚àÄ {y a}, g y = a ‚Üî G y = h a,
    from Œª y a, h.to_equiv.symm_apply_eq.trans subtype.ext_iff,
  refine ‚ü®g, Œª y, _, _‚ü©,
  { rcases hG y with ‚ü®a, ha, hay‚ü©,
    convert ha,
    exact hgG.2 hay.symm },
  { ext x, exact hgG.2 (congr_fun hGF _) }
end

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a continuous real-valued function on `X`. Then there exists a
continuous real-valued function `g : C(Y, ‚Ñù)` such that `g ‚àò e = f`. -/
lemma exists_extension_of_closed_embedding (f : C(X, ‚Ñù)) (e : X ‚Üí Y) (he : closed_embedding e) :
  ‚àÉ g : C(Y, ‚Ñù), g ‚àò e = f :=
(exists_extension_forall_mem_of_closed_embedding f (Œª x, mem_univ _) univ_nonempty he).imp $
  Œª g, and.right

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed set. Let
`s` be a closed set in a normal topological space `Y`. Let `f` be a continuous real-valued function
on `s`. Let `t` be a nonempty convex set of real numbers (we use `ord_connected` instead of `convex`
to automatically deduce this argument by typeclass search) such that `f x ‚àà t` for all `x : s`. Then
there exists a continuous real-valued function `g : C(Y, ‚Ñù)` such that `g y ‚àà t` for all `y` and
`g.restrict s = f`. -/
lemma exists_restrict_eq_forall_mem_of_closed {s : set Y} (f : C(s, ‚Ñù)) {t : set ‚Ñù}
  [ord_connected t] (ht : ‚àÄ x, f x ‚àà t) (hne : t.nonempty) (hs : is_closed s) :
  ‚àÉ g : C(Y, ‚Ñù), (‚àÄ y, g y ‚àà t) ‚àß g.restrict s = f :=
let ‚ü®g, hgt, hgf‚ü© := exists_extension_forall_mem_of_closed_embedding f ht hne
  (closed_embedding_subtype_coe hs)
in ‚ü®g, hgt, coe_injective hgf‚ü©

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed set. Let
`s` be a closed set in a normal topological space `Y`. Let `f` be a continuous real-valued function
on `s`. Then there exists a continuous real-valued function `g : C(Y, ‚Ñù)` such that
`g.restrict s = f`. -/
lemma exists_restrict_eq_of_closed {s : set Y} (f : C(s, ‚Ñù)) (hs : is_closed s) :
  ‚àÉ g : C(Y, ‚Ñù), g.restrict s = f :=
let ‚ü®g, hg, hgf‚ü© := exists_restrict_eq_forall_mem_of_closed f (Œª _, mem_univ _) univ_nonempty hs
in ‚ü®g, hgf‚ü©

end continuous_map
