/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import algebra.absolute_value
import topology.uniform_space.basic

/-!
# Uniform structure induced by an absolute value

We build a uniform space structure on a commutative ring `R` equipped with an absolute value into
a linear ordered field `ğ•œ`. Of course in the case `R` is `â„š`, `â„` or `â„‚` and
`ğ•œ = â„`, we get the same thing as the metric space construction, and the general construction
follows exactly the same path.

## Implementation details

Note that we import `data.real.cau_seq` because this is where absolute values are defined, but
the current file does not depend on real numbers. TODO: extract absolute values from that
`data.real` folder.

## References

* [N. Bourbaki, *Topologie gÃ©nÃ©rale*][bourbaki1966]

## Tags

absolute value, uniform spaces
-/

open set function filter uniform_space
open_locale filter

namespace is_absolute_value
variables {ğ•œ : Type*} [linear_ordered_field ğ•œ]
variables {R : Type*} [comm_ring R] (abv : R â†’ ğ•œ) [is_absolute_value abv]

/-- The uniformity coming from an absolute value. -/
def uniform_space_core : uniform_space.core R :=
{ uniformity := (â¨… Îµ>0, ğ“Ÿ {p:RÃ—R | abv (p.2 - p.1) < Îµ}),
  refl := le_infi $ assume Îµ, le_infi $ assume Îµ_pos, principal_mono.2
    (Î» âŸ¨x, yâŸ© h, by simpa [show x = y, from h, abv_zero abv]),
  symm := tendsto_infi.2 $ assume Îµ, tendsto_infi.2 $ assume h,
    tendsto_infi' Îµ $ tendsto_infi' h $ tendsto_principal_principal.2 $ Î» âŸ¨x, yâŸ© h,
      have h : abv (y - x) < Îµ, by simpa [-sub_eq_add_neg] using h,
      by rwa abv_sub abv at h,
  comp := le_infi $ assume Îµ, le_infi $ assume h, lift'_le
    (mem_infi_of_mem (Îµ / 2) $ mem_infi_of_mem (div_pos h zero_lt_two) (subset.refl _)) $
    have âˆ€ (a b c : R), abv (c-a) < Îµ / 2 â†’ abv (b-c) < Îµ / 2 â†’ abv (b-a) < Îµ,
      from assume a b c hac hcb,
       calc abv (b - a) â‰¤ _ : abv_sub_le abv b c a
        ... = abv (c - a) + abv (b - c) : add_comm _ _
        ... < Îµ / 2 + Îµ / 2 : add_lt_add hac hcb
        ... = Îµ : by rw [div_add_div_same, add_self_div_two],
    by simpa [comp_rel] }

/-- The uniform structure coming from an absolute value. -/
def uniform_space : uniform_space R :=
uniform_space.of_core (uniform_space_core abv)

theorem mem_uniformity {s : set (RÃ—R)} :
  s âˆˆ (uniform_space_core abv).uniformity â†”
  (âˆƒÎµ>0, âˆ€{a b:R}, abv (b - a) < Îµ â†’ (a, b) âˆˆ s) :=
begin
  suffices : s âˆˆ (â¨… Îµ: {Îµ : ğ•œ // Îµ > 0}, ğ“Ÿ {p:RÃ—R | abv (p.2 - p.1) < Îµ.val}) â†” _,
  { rw infi_subtype at this,
    exact this },
  rw mem_infi_of_directed,
  { simp [subset_def] },
  { rintros âŸ¨r, hrâŸ© âŸ¨p, hpâŸ©,
    exact âŸ¨âŸ¨min r p, lt_min hr hpâŸ©, by simp [lt_min_iff, (â‰¥)] {contextual := tt}âŸ©, },
end

end is_absolute_value
