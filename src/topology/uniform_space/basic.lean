/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot

Theory of uniform spaces.

Uniform spaces are a generalization of metric spaces and topological groups. Many concepts directly
generalize to uniform spaces, e.g.

* completeness
* extension of uniform continuous functions to complete spaces
* uniform contiunuity & embedding
* totally bounded
* totally bounded âˆ§ complete â†’ compact

The central concept of uniform spaces is its uniformity: a filter relating two elements of the
space. This filter is reflexive, symmetric and transitive. So a set (i.e. a relation) in this filter
represents a 'distance': it is reflexive, symmetric and the uniformity contains a set for which the
`triangular` rule holds.

The formalization is mostly based on the books:
  N. Bourbaki: General Topology
  I. M. James: Topologies and Uniformities
A major difference is that this formalization is heavily based on the filter library.
-/
import order.filter.lift
import topology.separation

open set filter classical
open_locale classical topological_space

set_option eqn_compiler.zeta true

universes u
section
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*} {Î¹ : Sort*}

/-- The identity relation, or the graph of the identity function -/
def id_rel {Î± : Type*} := {p : Î± Ã— Î± | p.1 = p.2}

@[simp] theorem mem_id_rel {a b : Î±} : (a, b) âˆˆ @id_rel Î± â†” a = b := iff.rfl

@[simp] theorem id_rel_subset {s : set (Î± Ã— Î±)} : id_rel âŠ† s â†” âˆ€ a, (a, a) âˆˆ s :=
by simp [subset_def]; exact forall_congr (Î» a, by simp)

/-- The composition of relations -/
def comp_rel {Î± : Type u} (râ‚ râ‚‚ : set (Î±Ã—Î±)) := {p : Î± Ã— Î± | âˆƒz:Î±, (p.1, z) âˆˆ râ‚ âˆ§ (z, p.2) âˆˆ râ‚‚}

@[simp] theorem mem_comp_rel {râ‚ râ‚‚ : set (Î±Ã—Î±)}
  {x y : Î±} : (x, y) âˆˆ comp_rel râ‚ râ‚‚ â†” âˆƒ z, (x, z) âˆˆ râ‚ âˆ§ (z, y) âˆˆ râ‚‚ := iff.rfl

@[simp] theorem swap_id_rel : prod.swap '' id_rel = @id_rel Î± :=
set.ext $ assume âŸ¨a, bâŸ©, by simp [image_swap_eq_preimage_swap]; exact eq_comm

theorem monotone_comp_rel [preorder Î²] {f g : Î² â†’ set (Î±Ã—Î±)}
  (hf : monotone f) (hg : monotone g) : monotone (Î»x, comp_rel (f x) (g x)) :=
assume a b h p âŸ¨z, hâ‚, hâ‚‚âŸ©, âŸ¨z, hf h hâ‚, hg h hâ‚‚âŸ©

lemma prod_mk_mem_comp_rel {a b c : Î±} {s t : set (Î±Ã—Î±)} (hâ‚ : (a, c) âˆˆ s) (hâ‚‚ : (c, b) âˆˆ t) :
  (a, b) âˆˆ comp_rel s t :=
âŸ¨c, hâ‚, hâ‚‚âŸ©

@[simp] lemma id_comp_rel {r : set (Î±Ã—Î±)} : comp_rel id_rel r = r :=
set.ext $ assume âŸ¨a, bâŸ©, by simp

lemma comp_rel_assoc {r s t : set (Î±Ã—Î±)} :
  comp_rel (comp_rel r s) t = comp_rel r (comp_rel s t) :=
by ext p; cases p; simp only [mem_comp_rel]; tauto

/-- This core description of a uniform space is outside of the type class hierarchy. It is useful
  for constructions of uniform spaces, when the topology is derived from the uniform space. -/
structure uniform_space.core (Î± : Type u) :=
(uniformity : filter (Î± Ã— Î±))
(refl       : principal id_rel â‰¤ uniformity)
(symm       : tendsto prod.swap uniformity uniformity)
(comp       : uniformity.lift' (Î»s, comp_rel s s) â‰¤ uniformity)

/-- An alternative constructor for `uniform_space.core`. This version unfolds various
`filter`-related definitions. -/
def uniform_space.core.mk' {Î± : Type u} (U : filter (Î± Ã— Î±))
  (refl : âˆ€ (r âˆˆ U) x, (x, x) âˆˆ r)
  (symm : âˆ€ r âˆˆ U, {p | prod.swap p âˆˆ r} âˆˆ U)
  (comp : âˆ€ r âˆˆ U, âˆƒ t âˆˆ U, comp_rel t t âŠ† r) : uniform_space.core Î± :=
âŸ¨U, Î» r ru, id_rel_subset.2 (refl _ ru), symm,
  begin
    intros r ru,
    rw [mem_lift'_sets],
    exact comp _ ru,
    apply monotone_comp_rel; exact monotone_id,
  endâŸ©

/-- A uniform space generates a topological space -/
def uniform_space.core.to_topological_space {Î± : Type u} (u : uniform_space.core Î±) :
  topological_space Î± :=
{ is_open        := Î»s, âˆ€xâˆˆs, { p : Î± Ã— Î± | p.1 = x â†’ p.2 âˆˆ s } âˆˆ u.uniformity,
  is_open_univ   := by simp; intro; exact univ_mem_sets,
  is_open_inter  :=
    assume s t hs ht x âŸ¨xs, xtâŸ©, by filter_upwards [hs x xs, ht x xt]; simp {contextual := tt},
  is_open_sUnion :=
    assume s hs x âŸ¨t, ts, xtâŸ©, by filter_upwards [hs t ts x xt] assume p ph h, âŸ¨t, ts, ph hâŸ© }

lemma uniform_space.core_eq : âˆ€{uâ‚ uâ‚‚ : uniform_space.core Î±}, uâ‚.uniformity = uâ‚‚.uniformity â†’ uâ‚ = uâ‚‚
| âŸ¨uâ‚, _, _, _âŸ©  âŸ¨uâ‚‚, _, _, _âŸ© h := have uâ‚ = uâ‚‚, from h, by simp [*]

section prio
set_option default_priority 100 -- see Note [default priority]
/-- A uniform space is a generalization of the "uniform" topological aspects of a
  metric space. It consists of a filter on `Î± Ã— Î±` called the "uniformity", which
  satisfies properties analogous to the reflexivity, symmetry, and triangle properties
  of a metric.

  A metric space has a natural uniformity, and a uniform space has a natural topology.
  A topological group also has a natural uniformity, even when it is not metrizable. -/
class uniform_space (Î± : Type u) extends topological_space Î±, uniform_space.core Î± :=
(is_open_uniformity : âˆ€s, is_open s â†” (âˆ€xâˆˆs, { p : Î± Ã— Î± | p.1 = x â†’ p.2 âˆˆ s } âˆˆ uniformity))
end prio

@[pattern] def uniform_space.mk' {Î±} (t : topological_space Î±)
  (c : uniform_space.core Î±)
  (is_open_uniformity : âˆ€s:set Î±, t.is_open s â†”
    (âˆ€xâˆˆs, { p : Î± Ã— Î± | p.1 = x â†’ p.2 âˆˆ s } âˆˆ c.uniformity)) :
  uniform_space Î± := âŸ¨c, is_open_uniformityâŸ©

/-- Construct a `uniform_space` from a `uniform_space.core`. -/
def uniform_space.of_core {Î± : Type u} (u : uniform_space.core Î±) : uniform_space Î± :=
{ to_core := u,
  to_topological_space := u.to_topological_space,
  is_open_uniformity := assume a, iff.rfl }

/-- Construct a `uniform_space` from a `u : uniform_space.core` and a `topological_space` structure
that is equal to `u.to_topological_space`. -/
def uniform_space.of_core_eq {Î± : Type u} (u : uniform_space.core Î±) (t : topological_space Î±)
  (h : t = u.to_topological_space) : uniform_space Î± :=
{ to_core := u,
  to_topological_space := t,
  is_open_uniformity := assume a, h.symm â–¸ iff.rfl }

lemma uniform_space.to_core_to_topological_space (u : uniform_space Î±) :
  u.to_core.to_topological_space = u.to_topological_space :=
topological_space_eq $ funext $ assume s,
  by rw [uniform_space.core.to_topological_space, uniform_space.is_open_uniformity]

@[ext]
lemma uniform_space_eq : âˆ€{uâ‚ uâ‚‚ : uniform_space Î±}, uâ‚.uniformity = uâ‚‚.uniformity â†’ uâ‚ = uâ‚‚
| (uniform_space.mk' tâ‚ uâ‚ oâ‚)  (uniform_space.mk' tâ‚‚ uâ‚‚ oâ‚‚) h :=
  have uâ‚ = uâ‚‚, from uniform_space.core_eq h,
  have tâ‚ = tâ‚‚, from topological_space_eq $ funext $ assume s, by rw [oâ‚, oâ‚‚]; simp [this],
  by simp [*]

lemma uniform_space.of_core_eq_to_core
  (u : uniform_space Î±) (t : topological_space Î±) (h : t = u.to_core.to_topological_space) :
  uniform_space.of_core_eq u.to_core t h = u :=
uniform_space_eq rfl

section uniform_space
variables [uniform_space Î±]

/-- The uniformity is a filter on Î± Ã— Î± (inferred from an ambient uniform space
  structure on Î±). -/
def uniformity (Î± : Type u) [uniform_space Î±] : filter (Î± Ã— Î±) :=
  (@uniform_space.to_core Î± _).uniformity

localized "notation `ğ“¤` := uniformity" in uniformity

lemma is_open_uniformity {s : set Î±} :
  is_open s â†” (âˆ€xâˆˆs, { p : Î± Ã— Î± | p.1 = x â†’ p.2 âˆˆ s } âˆˆ ğ“¤ Î±) :=
uniform_space.is_open_uniformity s

lemma refl_le_uniformity : principal id_rel â‰¤ ğ“¤ Î± :=
(@uniform_space.to_core Î± _).refl

lemma refl_mem_uniformity {x : Î±} {s : set (Î± Ã— Î±)} (h : s âˆˆ ğ“¤ Î±) :
  (x, x) âˆˆ s :=
refl_le_uniformity h rfl

lemma symm_le_uniformity : map (@prod.swap Î± Î±) (ğ“¤ _) â‰¤ (ğ“¤ _) :=
(@uniform_space.to_core Î± _).symm

lemma comp_le_uniformity : (ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), comp_rel s s) â‰¤ ğ“¤ Î± :=
(@uniform_space.to_core Î± _).comp

lemma tendsto_swap_uniformity : tendsto (@prod.swap Î± Î±) (ğ“¤ Î±) (ğ“¤ Î±) :=
symm_le_uniformity

lemma comp_mem_uniformity_sets {s : set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) :
  âˆƒ t âˆˆ ğ“¤ Î±, comp_rel t t âŠ† s :=
have s âˆˆ (ğ“¤ Î±).lift' (Î»t:set (Î±Ã—Î±), comp_rel t t),
  from comp_le_uniformity hs,
(mem_lift'_sets $ monotone_comp_rel monotone_id monotone_id).mp this

/-- Relation `Î» f g, tendsto (Î» x, (f x, g x)) l (ğ“¤ Î±)` is transitive. -/
lemma filter.tendsto.uniformity_trans {l : filter Î²} {fâ‚ fâ‚‚ fâ‚ƒ : Î² â†’ Î±}
  (hâ‚â‚‚ : tendsto (Î» x, (fâ‚ x, fâ‚‚ x)) l (ğ“¤ Î±)) (hâ‚‚â‚ƒ : tendsto (Î» x, (fâ‚‚ x, fâ‚ƒ x)) l (ğ“¤ Î±)) :
  tendsto (Î» x, (fâ‚ x, fâ‚ƒ x)) l (ğ“¤ Î±) :=
begin
  refine le_trans (le_lift' $ Î» s hs, mem_map.2 _) comp_le_uniformity,
  filter_upwards [hâ‚â‚‚ hs, hâ‚‚â‚ƒ hs],
  exact Î» x hxâ‚â‚‚ hxâ‚‚â‚ƒ, âŸ¨_, hxâ‚â‚‚, hxâ‚‚â‚ƒâŸ©
end

/-- Relation `Î» f g, tendsto (Î» x, (f x, g x)) l (ğ“¤ Î±)` is symmetric -/
lemma filter.tendsto.uniformity_symm {l : filter Î²} {f : Î² â†’ Î± Ã— Î±}
  (h : tendsto f l (ğ“¤ Î±)) :
  tendsto (Î» x, ((f x).2, (f x).1)) l (ğ“¤ Î±) :=
tendsto_swap_uniformity.comp h

/-- Relation `Î» f g, tendsto (Î» x, (f x, g x)) l (ğ“¤ Î±)` is reflexive. -/
lemma tendsto_diag_uniformity (f : Î² â†’ Î±) (l : filter Î²) :
  tendsto (Î» x, (f x, f x)) l (ğ“¤ Î±) :=
assume s hs, mem_map.2 $ univ_mem_sets' $ Î» x, refl_mem_uniformity hs

lemma tendsto_const_uniformity {a : Î±} {f : filter Î²} : tendsto (Î» _, (a, a)) f (ğ“¤ Î±) :=
tendsto_diag_uniformity (Î» _, a) f

lemma symm_of_uniformity {s : set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) :
  âˆƒ t âˆˆ ğ“¤ Î±, (âˆ€a b, (a, b) âˆˆ t â†’ (b, a) âˆˆ t) âˆ§ t âŠ† s :=
have preimage prod.swap s âˆˆ ğ“¤ Î±, from symm_le_uniformity hs,
âŸ¨s âˆ© preimage prod.swap s, inter_mem_sets hs this, assume a b âŸ¨hâ‚, hâ‚‚âŸ©, âŸ¨hâ‚‚, hâ‚âŸ©, inter_subset_left _ _âŸ©

lemma comp_symm_of_uniformity {s : set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) :
  âˆƒ t âˆˆ ğ“¤ Î±, (âˆ€{a b}, (a, b) âˆˆ t â†’ (b, a) âˆˆ t) âˆ§ comp_rel t t âŠ† s :=
let âŸ¨t, htâ‚, htâ‚‚âŸ© := comp_mem_uniformity_sets hs in
let âŸ¨t', ht', ht'â‚, ht'â‚‚âŸ© := symm_of_uniformity htâ‚ in
âŸ¨t', ht', ht'â‚, subset.trans (monotone_comp_rel monotone_id monotone_id ht'â‚‚) htâ‚‚âŸ©

lemma uniformity_le_symm : ğ“¤ Î± â‰¤ (@prod.swap Î± Î±) <$> ğ“¤ Î± :=
by rw [map_swap_eq_comap_swap];
from map_le_iff_le_comap.1 tendsto_swap_uniformity

lemma uniformity_eq_symm : ğ“¤ Î± = (@prod.swap Î± Î±) <$> ğ“¤ Î± :=
le_antisymm uniformity_le_symm symm_le_uniformity

theorem uniformity_lift_le_swap {g : set (Î±Ã—Î±) â†’ filter Î²} {f : filter Î²} (hg : monotone g)
  (h : (ğ“¤ Î±).lift (Î»s, g (preimage prod.swap s)) â‰¤ f) : (ğ“¤ Î±).lift g â‰¤ f :=
calc (ğ“¤ Î±).lift g â‰¤ (filter.map (@prod.swap Î± Î±) $ ğ“¤ Î±).lift g :
    lift_mono uniformity_le_symm (le_refl _)
  ... â‰¤ _ :
    by rw [map_lift_eq2 hg, image_swap_eq_preimage_swap]; exact h

lemma uniformity_lift_le_comp {f : set (Î±Ã—Î±) â†’ filter Î²} (h : monotone f) :
  (ğ“¤ Î±).lift (Î»s, f (comp_rel s s)) â‰¤ (ğ“¤ Î±).lift f :=
calc (ğ“¤ Î±).lift (Î»s, f (comp_rel s s)) =
    ((ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), comp_rel s s)).lift f :
  begin
    rw [lift_lift'_assoc],
    exact monotone_comp_rel monotone_id monotone_id,
    exact h
  end
  ... â‰¤ (ğ“¤ Î±).lift f : lift_mono comp_le_uniformity (le_refl _)

lemma comp_le_uniformity3 :
  (ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), comp_rel s (comp_rel s s)) â‰¤ (ğ“¤ Î±) :=
calc (ğ“¤ Î±).lift' (Î»d, comp_rel d (comp_rel d d)) =
  (ğ“¤ Î±).lift (Î»s, (ğ“¤ Î±).lift' (Î»t:set(Î±Ã—Î±), comp_rel s (comp_rel t t))) :
  begin
    rw [lift_lift'_same_eq_lift'],
    exact (assume x, monotone_comp_rel monotone_const $ monotone_comp_rel monotone_id monotone_id),
    exact (assume x, monotone_comp_rel monotone_id monotone_const),
  end
  ... â‰¤ (ğ“¤ Î±).lift (Î»s, (ğ“¤ Î±).lift' (Î»t:set(Î±Ã—Î±), comp_rel s t)) :
    lift_mono' $ assume s hs, @uniformity_lift_le_comp Î± _ _ (principal âˆ˜ comp_rel s) $
      monotone_principal.comp (monotone_comp_rel monotone_const monotone_id)
  ... = (ğ“¤ Î±).lift' (Î»s:set(Î±Ã—Î±), comp_rel s s) :
    lift_lift'_same_eq_lift'
      (assume s, monotone_comp_rel monotone_const monotone_id)
      (assume s, monotone_comp_rel monotone_id monotone_const)
  ... â‰¤ (ğ“¤ Î±) : comp_le_uniformity

lemma filter.has_basis.mem_uniformity_iff {p : Î² â†’ Prop} {s : Î² â†’ set (Î±Ã—Î±)}
  (h : (ğ“¤ Î±).has_basis p s) {t : set (Î± Ã— Î±)} :
  t âˆˆ ğ“¤ Î± â†” âˆƒ i (hi : p i), âˆ€ a b, (a, b) âˆˆ s i â†’ (a, b) âˆˆ t :=
h.mem_iff.trans $ by simp only [prod.forall, subset_def]

lemma mem_nhds_uniformity_iff_right {x : Î±} {s : set Î±} :
  s âˆˆ ğ“ x â†” {p : Î± Ã— Î± | p.1 = x â†’ p.2 âˆˆ s} âˆˆ ğ“¤ Î± :=
âŸ¨ begin
    simp only [mem_nhds_sets_iff, is_open_uniformity, and_imp, exists_imp_distrib],
    exact assume t ts ht xt, by filter_upwards [ht x xt] assume âŸ¨x', yâŸ© h eq, ts $ h eq
  end,

  assume hs,
  mem_nhds_sets_iff.mpr âŸ¨{x | {p : Î± Ã— Î± | p.1 = x â†’ p.2 âˆˆ s} âˆˆ ğ“¤ Î±},
    assume x' hx', refl_mem_uniformity hx' rfl,
    is_open_uniformity.mpr $ assume x' hx',
      let âŸ¨t, ht, trâŸ© := comp_mem_uniformity_sets hx' in
      by filter_upwards [ht] assume âŸ¨a, bâŸ© hp' (hax' : a = x'),
      by filter_upwards [ht] assume âŸ¨a, b'âŸ© hp'' (hab : a = b),
      have hp : (x', b) âˆˆ t, from hax' â–¸ hp',
      have (b, b') âˆˆ t, from hab â–¸ hp'',
      have (x', b') âˆˆ comp_rel t t, from âŸ¨b, hp, thisâŸ©,
      show b' âˆˆ s,
        from tr this rfl,
    hsâŸ©âŸ©

lemma mem_nhds_uniformity_iff_left {x : Î±} {s : set Î±} :
  s âˆˆ ğ“ x â†” {p : Î± Ã— Î± | p.2 = x â†’ p.1 âˆˆ s} âˆˆ ğ“¤ Î± :=
by { rw [uniformity_eq_symm, mem_nhds_uniformity_iff_right], refl }

lemma nhds_eq_comap_uniformity {x : Î±} : ğ“ x = (ğ“¤ Î±).comap (prod.mk x) :=
by ext s; rw [mem_nhds_uniformity_iff_right, mem_comap_sets]; from iff.intro
  (assume hs, âŸ¨_, hs, assume x hx, hx rflâŸ©)
  (assume âŸ¨t, h, htâŸ©, (ğ“¤ Î±).sets_of_superset h $
    assume âŸ¨pâ‚, pâ‚‚âŸ© hp (h : pâ‚ = x), ht $ by simp [h.symm, hp])

lemma nhds_basis_uniformity' {p : Î² â†’ Prop} {s : Î² â†’ set (Î± Ã— Î±)} (h : (ğ“¤ Î±).has_basis p s) {x : Î±} :
  (ğ“ x).has_basis p (Î» i, {y | (x, y) âˆˆ s i}) :=
by { rw [nhds_eq_comap_uniformity], exact h.comap (prod.mk x) }

lemma nhds_basis_uniformity {p : Î² â†’ Prop} {s : Î² â†’ set (Î± Ã— Î±)} (h : (ğ“¤ Î±).has_basis p s) {x : Î±} :
  (ğ“ x).has_basis p (Î» i, {y | (y, x) âˆˆ s i}) :=
begin
  replace h := h.comap prod.swap,
  rw [â† map_swap_eq_comap_swap, â† uniformity_eq_symm] at h,
  exact nhds_basis_uniformity' h
end

lemma nhds_eq_uniformity {x : Î±} : ğ“ x = (ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), {y | (x, y) âˆˆ s}) :=
(nhds_basis_uniformity' (ğ“¤ Î±).basis_sets).eq_binfi

lemma mem_nhds_left (x : Î±) {s : set (Î±Ã—Î±)} (h : s âˆˆ ğ“¤ Î±) :
  {y : Î± | (x, y) âˆˆ s} âˆˆ ğ“ x :=
(nhds_basis_uniformity' (ğ“¤ Î±).basis_sets).mem_of_mem h

lemma mem_nhds_right (y : Î±) {s : set (Î±Ã—Î±)} (h : s âˆˆ ğ“¤ Î±) :
  {x : Î± | (x, y) âˆˆ s} âˆˆ ğ“ y :=
mem_nhds_left _ (symm_le_uniformity h)

lemma tendsto_right_nhds_uniformity {a : Î±} : tendsto (Î»a', (a', a)) (ğ“ a) (ğ“¤ Î±) :=
assume s, mem_nhds_right a

lemma tendsto_left_nhds_uniformity {a : Î±} : tendsto (Î»a', (a, a')) (ğ“ a) (ğ“¤ Î±) :=
assume s, mem_nhds_left a

lemma lift_nhds_left {x : Î±} {g : set Î± â†’ filter Î²} (hg : monotone g) :
  (ğ“ x).lift g = (ğ“¤ Î±).lift (Î»s:set (Î±Ã—Î±), g {y | (x, y) âˆˆ s}) :=
eq.trans
  begin
    rw [nhds_eq_uniformity],
    exact (filter.lift_assoc $ monotone_principal.comp $ monotone_preimage.comp monotone_preimage )
  end
  (congr_arg _ $ funext $ assume s, filter.lift_principal hg)

lemma lift_nhds_right {x : Î±} {g : set Î± â†’ filter Î²} (hg : monotone g) :
  (ğ“ x).lift g = (ğ“¤ Î±).lift (Î»s:set (Î±Ã—Î±), g {y | (y, x) âˆˆ s}) :=
calc (ğ“ x).lift g = (ğ“¤ Î±).lift (Î»s:set (Î±Ã—Î±), g {y | (x, y) âˆˆ s}) : lift_nhds_left hg
  ... = ((@prod.swap Î± Î±) <$> (ğ“¤ Î±)).lift (Î»s:set (Î±Ã—Î±), g {y | (x, y) âˆˆ s}) : by rw [â†uniformity_eq_symm]
  ... = (ğ“¤ Î±).lift (Î»s:set (Î±Ã—Î±), g {y | (x, y) âˆˆ image prod.swap s}) :
    map_lift_eq2 $ hg.comp monotone_preimage
  ... = _ : by simp [image_swap_eq_preimage_swap]

lemma nhds_nhds_eq_uniformity_uniformity_prod {a b : Î±} :
  filter.prod (ğ“ a) (ğ“ b) =
  (ğ“¤ Î±).lift (Î»s:set (Î±Ã—Î±), (ğ“¤ Î±).lift' (Î»t:set (Î±Ã—Î±),
    set.prod {y : Î± | (y, a) âˆˆ s} {y : Î± | (b, y) âˆˆ t})) :=
begin
  rw [prod_def],
  show (ğ“ a).lift (Î»s:set Î±, (ğ“ b).lift (Î»t:set Î±, principal (set.prod s t))) = _,
  rw [lift_nhds_right],
  apply congr_arg, funext s,
  rw [lift_nhds_left],
  refl,
  exact monotone_principal.comp (monotone_prod monotone_const monotone_id),
  exact (monotone_lift' monotone_const $ monotone_lam $
    assume x, monotone_prod monotone_id monotone_const)
end

lemma nhds_eq_uniformity_prod {a b : Î±} :
  ğ“ (a, b) =
  (ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), set.prod {y : Î± | (y, a) âˆˆ s} {y : Î± | (b, y) âˆˆ s}) :=
begin
  rw [nhds_prod_eq, nhds_nhds_eq_uniformity_uniformity_prod, lift_lift'_same_eq_lift'],
  { intro s, exact monotone_prod monotone_const monotone_preimage },
  { intro t, exact monotone_prod monotone_preimage monotone_const }
end

lemma nhdset_of_mem_uniformity {d : set (Î±Ã—Î±)} (s : set (Î±Ã—Î±)) (hd : d âˆˆ ğ“¤ Î±) :
  âˆƒ(t : set (Î±Ã—Î±)), is_open t âˆ§ s âŠ† t âˆ§ t âŠ† {p | âˆƒx y, (p.1, x) âˆˆ d âˆ§ (x, y) âˆˆ s âˆ§ (y, p.2) âˆˆ d} :=
let cl_d := {p:Î±Ã—Î± | âˆƒx y, (p.1, x) âˆˆ d âˆ§ (x, y) âˆˆ s âˆ§ (y, p.2) âˆˆ d} in
have âˆ€p âˆˆ s, âˆƒt âŠ† cl_d, is_open t âˆ§ p âˆˆ t, from
  assume âŸ¨x, yâŸ© hp, mem_nhds_sets_iff.mp $
  show cl_d âˆˆ ğ“ (x, y),
  begin
    rw [nhds_eq_uniformity_prod, mem_lift'_sets],
    exact âŸ¨d, hd, assume âŸ¨a, bâŸ© âŸ¨ha, hbâŸ©, âŸ¨x, y, ha, hp, hbâŸ©âŸ©,
    exact monotone_prod monotone_preimage monotone_preimage
  end,
have âˆƒt:(Î (p:Î±Ã—Î±) (h:p âˆˆ s), set (Î±Ã—Î±)),
    âˆ€p, âˆ€h:p âˆˆ s, t p h âŠ† cl_d âˆ§ is_open (t p h) âˆ§ p âˆˆ t p h,
  by simp [classical.skolem] at this; simp; assumption,
match this with
| âŸ¨t, htâŸ© :=
  âŸ¨(â‹ƒ p:Î±Ã—Î±, â‹ƒ h : p âˆˆ s, t p h : set (Î±Ã—Î±)),
    is_open_Union $ assume (p:Î±Ã—Î±), is_open_Union $ assume hp, (ht p hp).right.left,
    assume âŸ¨a, bâŸ© hp, begin simp; exact âŸ¨a, b, hp, (ht (a,b) hp).right.rightâŸ© end,
    Union_subset $ assume p, Union_subset $ assume hp, (ht p hp).leftâŸ©
end

lemma closure_eq_inter_uniformity {t : set (Î±Ã—Î±)} :
  closure t = (â‹‚ d âˆˆ ğ“¤ Î±, comp_rel d (comp_rel t d)) :=
set.ext $ assume âŸ¨a, bâŸ©,
calc (a, b) âˆˆ closure t â†” (ğ“ (a, b) âŠ“ principal t â‰  âŠ¥) : by simp [closure_eq_nhds]
  ... â†” (((@prod.swap Î± Î±) <$> ğ“¤ Î±).lift'
      (Î» (s : set (Î± Ã— Î±)), set.prod {x : Î± | (x, a) âˆˆ s} {y : Î± | (b, y) âˆˆ s}) âŠ“ principal t â‰  âŠ¥) :
    by rw [â†uniformity_eq_symm, nhds_eq_uniformity_prod]
  ... â†” ((map (@prod.swap Î± Î±) (ğ“¤ Î±)).lift'
      (Î» (s : set (Î± Ã— Î±)), set.prod {x : Î± | (x, a) âˆˆ s} {y : Î± | (b, y) âˆˆ s}) âŠ“ principal t â‰  âŠ¥) :
    by refl
  ... â†” ((ğ“¤ Î±).lift'
      (Î» (s : set (Î± Ã— Î±)), set.prod {y : Î± | (a, y) âˆˆ s} {x : Î± | (x, b) âˆˆ s}) âŠ“ principal t â‰  âŠ¥) :
  begin
    rw [map_lift'_eq2],
    simp [image_swap_eq_preimage_swap, function.comp],
    exact monotone_prod monotone_preimage monotone_preimage
  end
  ... â†” (âˆ€s âˆˆ ğ“¤ Î±, (set.prod {y : Î± | (a, y) âˆˆ s} {x : Î± | (x, b) âˆˆ s} âˆ© t).nonempty) :
  begin
    rw [lift'_inf_principal_eq, lift'_ne_bot_iff],
    exact monotone_inter (monotone_prod monotone_preimage monotone_preimage) monotone_const
  end
  ... â†” (âˆ€ s âˆˆ ğ“¤ Î±, (a, b) âˆˆ comp_rel s (comp_rel t s)) :
    forall_congr $ assume s, forall_congr $ assume hs,
    âŸ¨assume âŸ¨âŸ¨x, yâŸ©, âŸ¨âŸ¨hx, hyâŸ©, hxytâŸ©âŸ©, âŸ¨x, hx, y, hxyt, hyâŸ©,
      assume âŸ¨x, hx, y, hxyt, hyâŸ©, âŸ¨âŸ¨x, yâŸ©, âŸ¨âŸ¨hx, hyâŸ©, hxytâŸ©âŸ©âŸ©
  ... â†” _ : by simp

lemma uniformity_eq_uniformity_closure : ğ“¤ Î± = (ğ“¤ Î±).lift' closure :=
le_antisymm
  (le_infi $ assume s, le_infi $ assume hs, by simp; filter_upwards [hs] subset_closure)
  (calc (ğ“¤ Î±).lift' closure â‰¤ (ğ“¤ Î±).lift' (Î»d, comp_rel d (comp_rel d d)) :
      lift'_mono' (by intros s hs; rw [closure_eq_inter_uniformity]; exact bInter_subset_of_mem hs)
    ... â‰¤ (ğ“¤ Î±) : comp_le_uniformity3)

lemma uniformity_eq_uniformity_interior : ğ“¤ Î± = (ğ“¤ Î±).lift' interior :=
le_antisymm
  (le_infi $ assume d, le_infi $ assume hd,
    let âŸ¨s, hs, hs_compâŸ© := (mem_lift'_sets $
      monotone_comp_rel monotone_id $ monotone_comp_rel monotone_id monotone_id).mp (comp_le_uniformity3 hd) in
    let âŸ¨t, ht, hst, ht_compâŸ© := nhdset_of_mem_uniformity s hs in
    have s âŠ† interior d, from
      calc s âŠ† t : hst
       ... âŠ† interior d : (subset_interior_iff_subset_of_open ht).mpr $
        assume x, assume : x âˆˆ t, let âŸ¨x, y, hâ‚, hâ‚‚, hâ‚ƒâŸ© := ht_comp this in hs_comp âŸ¨x, hâ‚, y, hâ‚‚, hâ‚ƒâŸ©,
    have interior d âˆˆ ğ“¤ Î±, by filter_upwards [hs] this,
    by simp [this])
  (assume s hs, ((ğ“¤ Î±).lift' interior).sets_of_superset (mem_lift' hs) interior_subset)

lemma interior_mem_uniformity {s : set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) :
  interior s âˆˆ ğ“¤ Î± :=
by rw [uniformity_eq_uniformity_interior]; exact mem_lift' hs

lemma mem_uniformity_is_closed {s : set (Î±Ã—Î±)} (h : s âˆˆ ğ“¤ Î±) :
  âˆƒt âˆˆ ğ“¤ Î±, is_closed t âˆ§ t âŠ† s :=
have s âˆˆ (ğ“¤ Î±).lift' closure, by rwa [uniformity_eq_uniformity_closure] at h,
have âˆƒ t âˆˆ ğ“¤ Î±, closure t âŠ† s,
  by rwa [mem_lift'_sets] at this; apply closure_mono,
let âŸ¨t, ht, hstâŸ© := this in
âŸ¨closure t, (ğ“¤ Î±).sets_of_superset ht subset_closure, is_closed_closure, hstâŸ©

/-! ### Uniform continuity -/

/-- A function `f : Î± â†’ Î²` is *uniformly continuous* if `(f x, f y)` tends to the diagonal
as `(x, y)` tends to the diagonal. In other words, if `x` is sufficiently close to `y`, then
`f x` is close to `f y` no matter where `x` and `y` are located in `Î±`. -/
def uniform_continuous [uniform_space Î²] (f : Î± â†’ Î²) :=
tendsto (Î»x:Î±Ã—Î±, (f x.1, f x.2)) (ğ“¤ Î±) (ğ“¤ Î²)

theorem uniform_continuous_def [uniform_space Î²] {f : Î± â†’ Î²} :
  uniform_continuous f â†” âˆ€ r âˆˆ ğ“¤ Î²,
    {x : Î± Ã— Î± | (f x.1, f x.2) âˆˆ r} âˆˆ ğ“¤ Î± :=
iff.rfl

lemma uniform_continuous_of_const [uniform_space Î²] {c : Î± â†’ Î²} (h : âˆ€a b, c a = c b) :
  uniform_continuous c :=
have (Î» (x : Î± Ã— Î±), (c (x.fst), c (x.snd))) â»Â¹' id_rel = univ, from
  eq_univ_iff_forall.2 $ assume âŸ¨a, bâŸ©, h a b,
le_trans (map_le_iff_le_comap.2 $ by simp [comap_principal, this, univ_mem_sets]) refl_le_uniformity

lemma uniform_continuous_id : uniform_continuous (@id Î±) :=
by simp [uniform_continuous]; exact tendsto_id

lemma uniform_continuous_const [uniform_space Î²] {b : Î²} : uniform_continuous (Î»a:Î±, b) :=
uniform_continuous_of_const $ Î» _ _, rfl

lemma uniform_continuous.comp [uniform_space Î²] [uniform_space Î³] {g : Î² â†’ Î³} {f : Î± â†’ Î²}
  (hg : uniform_continuous g) (hf : uniform_continuous f) : uniform_continuous (g âˆ˜ f) :=
hg.comp hf

lemma filter.has_basis.uniform_continuous_iff [uniform_space Î²] {p : Î³ â†’ Prop} {s : Î³ â†’ set (Î±Ã—Î±)}
  (ha : (ğ“¤ Î±).has_basis p s) {q : Î´ â†’ Prop} {t : Î´ â†’ set (Î²Ã—Î²)} (hb : (ğ“¤ Î²).has_basis q t)
  {f : Î± â†’ Î²} :
  uniform_continuous f â†” âˆ€ i (hi : q i), âˆƒ j (hj : p j), âˆ€ x y, (x, y) âˆˆ s j â†’ (f x, f y) âˆˆ t i :=
(ha.tendsto_iff hb).trans $ by simp only [prod.forall]

end uniform_space
end

open_locale uniformity

section constructions
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*} {Î¹ : Sort*}

instance : partial_order (uniform_space Î±) :=
{ le          := Î»t s, t.uniformity â‰¤ s.uniformity,
  le_antisymm := assume t s hâ‚ hâ‚‚, uniform_space_eq $ le_antisymm hâ‚ hâ‚‚,
  le_refl     := assume t, le_refl _,
  le_trans    := assume a b c hâ‚ hâ‚‚, le_trans hâ‚ hâ‚‚ }

instance : has_Inf (uniform_space Î±) :=
âŸ¨assume s, uniform_space.of_core {
  uniformity := (â¨…uâˆˆs, @uniformity Î± u),
  refl       := le_infi $ assume u, le_infi $ assume hu, u.refl,
  symm       := le_infi $ assume u, le_infi $ assume hu,
    le_trans (map_mono $ infi_le_of_le _ $ infi_le _ hu) u.symm,
  comp       := le_infi $ assume u, le_infi $ assume hu,
    le_trans (lift'_mono (infi_le_of_le _ $ infi_le _ hu) $ le_refl _) u.comp }âŸ©

private lemma Inf_le {tt : set (uniform_space Î±)} {t : uniform_space Î±} (h : t âˆˆ tt) :
  Inf tt â‰¤ t :=
show (â¨…uâˆˆtt, @uniformity Î± u) â‰¤ t.uniformity,
  from infi_le_of_le t $ infi_le _ h

private lemma le_Inf {tt : set (uniform_space Î±)} {t : uniform_space Î±} (h : âˆ€t'âˆˆtt, t â‰¤ t') :
  t â‰¤ Inf tt :=
show t.uniformity â‰¤ (â¨…uâˆˆtt, @uniformity Î± u),
  from le_infi $ assume t', le_infi $ assume ht', h t' ht'

instance : has_top (uniform_space Î±) :=
âŸ¨uniform_space.of_core { uniformity := âŠ¤, refl := le_top, symm := le_top, comp := le_top }âŸ©

instance : has_bot (uniform_space Î±) :=
âŸ¨{ to_topological_space := âŠ¥,
  uniformity  := principal id_rel,
  refl        := le_refl _,
  symm        := by simp [tendsto]; apply subset.refl,
  comp        :=
  begin
    rw [lift'_principal], {simp},
    exact monotone_comp_rel monotone_id monotone_id
  end,
  is_open_uniformity :=
    assume s, by simp [is_open_fold, subset_def, id_rel] {contextual := tt } } âŸ©

instance : complete_lattice (uniform_space Î±) :=
{ sup           := Î»a b, Inf {x | a â‰¤ x âˆ§ b â‰¤ x},
  le_sup_left   := Î» a b, le_Inf (Î» _ âŸ¨h, _âŸ©, h),
  le_sup_right  := Î» a b, le_Inf (Î» _ âŸ¨_, hâŸ©, h),
  sup_le        := Î» a b c hâ‚ hâ‚‚, Inf_le âŸ¨hâ‚, hâ‚‚âŸ©,
  inf           := Î» a b, Inf {a, b},
  le_inf        := Î» a b c hâ‚ hâ‚‚, le_Inf (Î» u h,
                     by { cases h, exact h.symm â–¸ hâ‚, exact (mem_singleton_iff.1 h).symm â–¸ hâ‚‚ }),
  inf_le_left   := Î» a b, Inf_le (by simp),
  inf_le_right  := Î» a b, Inf_le (by simp),
  top           := âŠ¤,
  le_top        := Î» a, show a.uniformity â‰¤ âŠ¤, from le_top,
  bot           := âŠ¥,
  bot_le        := Î» u, u.refl,
  Sup           := Î» tt, Inf {t | âˆ€ t' âˆˆ tt, t' â‰¤ t},
  le_Sup        := Î» s u h, le_Inf (Î» u' h', h' u h),
  Sup_le        := Î» s u h, Inf_le h,
  Inf           := Inf,
  le_Inf        := Î» s a hs, le_Inf hs,
  Inf_le        := Î» s a ha, Inf_le ha,
  ..uniform_space.partial_order }

lemma infi_uniformity {Î¹ : Sort*} {u : Î¹ â†’ uniform_space Î±} :
  (infi u).uniformity = (â¨…i, (u i).uniformity) :=
show (â¨…a (h : âˆƒi:Î¹, u i = a), a.uniformity) = _, from
le_antisymm
  (le_infi $ assume i, infi_le_of_le (u i) $ infi_le _ âŸ¨i, rflâŸ©)
  (le_infi $ assume a, le_infi $ assume âŸ¨i, (ha : u i = a)âŸ©, ha â–¸ infi_le _ _)

lemma inf_uniformity {u v : uniform_space Î±} :
  (u âŠ“ v).uniformity = u.uniformity âŠ“ v.uniformity :=
have (u âŠ“ v) = (â¨…i (h : i = u âˆ¨ i = v), i), by simp [infi_or, infi_inf_eq],
calc (u âŠ“ v).uniformity = ((â¨…i (h : i = u âˆ¨ i = v), i) : uniform_space Î±).uniformity : by rw [this]
  ... = _ : by simp [infi_uniformity, infi_or, infi_inf_eq]

instance inhabited_uniform_space : inhabited (uniform_space Î±) := âŸ¨âŠ¥âŸ©
instance inhabited_uniform_space_core : inhabited (uniform_space.core Î±) :=
âŸ¨@uniform_space.to_core _ (default _)âŸ©

/-- Given `f : Î± â†’ Î²` and a uniformity `u` on `Î²`, the inverse image of `u` under `f`
  is the inverse image in the filter sense of the induced function `Î± Ã— Î± â†’ Î² Ã— Î²`. -/
def uniform_space.comap (f : Î± â†’ Î²) (u : uniform_space Î²) : uniform_space Î± :=
{ uniformity := u.uniformity.comap (Î»p:Î±Ã—Î±, (f p.1, f p.2)),
  to_topological_space := u.to_topological_space.induced f,
  refl := le_trans (by simp; exact assume âŸ¨a, bâŸ© (h : a = b), h â–¸ rfl) (comap_mono u.refl),
  symm := by simp [tendsto_comap_iff, prod.swap, (âˆ˜)]; exact tendsto_swap_uniformity.comp tendsto_comap,
  comp := le_trans
    begin
      rw [comap_lift'_eq, comap_lift'_eq2],
      exact (lift'_mono' $ assume s hs âŸ¨aâ‚, aâ‚‚âŸ© âŸ¨x, hâ‚, hâ‚‚âŸ©, âŸ¨f x, hâ‚, hâ‚‚âŸ©),
      repeat { exact monotone_comp_rel monotone_id monotone_id }
    end
    (comap_mono u.comp),
  is_open_uniformity := Î» s, begin
    change (@is_open Î± (u.to_topological_space.induced f) s â†” _),
    simp [is_open_iff_nhds, nhds_induced, mem_nhds_uniformity_iff_right, filter.comap, and_comm],
    refine ball_congr (Î» x hx, âŸ¨_, _âŸ©),
    { rintro âŸ¨t, hts, htâŸ©, refine âŸ¨_, ht, _âŸ©,
      rintro âŸ¨xâ‚, xâ‚‚âŸ© h rfl, exact hts (h rfl) },
    { rintro âŸ¨t, ht, htsâŸ©,
      exact âŸ¨{y | (f x, y) âˆˆ t}, Î» y hy, @hts (x, y) hy rfl,
        mem_nhds_uniformity_iff_right.1 $ mem_nhds_left _ htâŸ© }
  end }

lemma uniform_space_comap_id {Î± : Type*} : uniform_space.comap (id : Î± â†’ Î±) = id :=
by ext u ; dsimp [uniform_space.comap] ; rw [prod.id_prod, filter.comap_id]

lemma uniform_space.comap_comap_comp {Î± Î² Î³} [uÎ³ : uniform_space Î³] {f : Î± â†’ Î²} {g : Î² â†’ Î³} :
  uniform_space.comap (g âˆ˜ f) uÎ³ = uniform_space.comap f (uniform_space.comap g uÎ³) :=
by ext ; dsimp [uniform_space.comap] ; rw filter.comap_comap_comp

lemma uniform_continuous_iff {Î± Î²} [uÎ± : uniform_space Î±] [uÎ² : uniform_space Î²] {f : Î± â†’ Î²} :
  uniform_continuous f â†” uÎ± â‰¤ uÎ².comap f :=
filter.map_le_iff_le_comap

lemma uniform_continuous_comap {f : Î± â†’ Î²} [u : uniform_space Î²] :
  @uniform_continuous Î± Î² (uniform_space.comap f u) u f :=
tendsto_comap

theorem to_topological_space_comap {f : Î± â†’ Î²} {u : uniform_space Î²} :
  @uniform_space.to_topological_space _ (uniform_space.comap f u) =
  topological_space.induced f (@uniform_space.to_topological_space Î² u) := rfl

lemma uniform_continuous_comap' {f : Î³ â†’ Î²} {g : Î± â†’ Î³} [v : uniform_space Î²] [u : uniform_space Î±]
  (h : uniform_continuous (f âˆ˜ g)) : @uniform_continuous Î± Î³ u (uniform_space.comap f v) g :=
tendsto_comap_iff.2 h

lemma to_topological_space_mono {uâ‚ uâ‚‚ : uniform_space Î±} (h : uâ‚ â‰¤ uâ‚‚) :
  @uniform_space.to_topological_space _ uâ‚ â‰¤ @uniform_space.to_topological_space _ uâ‚‚ :=
le_of_nhds_le_nhds $ assume a,
  by rw [@nhds_eq_uniformity Î± uâ‚ a, @nhds_eq_uniformity Î± uâ‚‚ a]; exact (lift'_mono h $ le_refl _)

lemma uniform_continuous.continuous [uniform_space Î±] [uniform_space Î²] {f : Î± â†’ Î²}
  (hf : uniform_continuous f) : continuous f :=
continuous_iff_le_induced.mpr $ to_topological_space_mono $ uniform_continuous_iff.1 hf

lemma to_topological_space_bot : @uniform_space.to_topological_space Î± âŠ¥ = âŠ¥ := rfl

lemma to_topological_space_top : @uniform_space.to_topological_space Î± âŠ¤ = âŠ¤ :=
top_unique $ assume s hs, s.eq_empty_or_nonempty.elim
  (assume : s = âˆ…, this.symm â–¸ @is_open_empty _ âŠ¤)
  (assume  âŸ¨x, hxâŸ©,
    have s = univ, from top_unique $ assume y hy, hs x hx (x, y) rfl,
    this.symm â–¸ @is_open_univ _ âŠ¤)

lemma to_topological_space_infi {Î¹ : Sort*} {u : Î¹ â†’ uniform_space Î±} :
  (infi u).to_topological_space = â¨…i, (u i).to_topological_space :=
classical.by_cases
  (assume h : nonempty Î¹,
    eq_of_nhds_eq_nhds $ assume a,
    begin
      rw [nhds_infi, nhds_eq_uniformity],
      change (infi u).uniformity.lift' (preimage $ prod.mk a) = _,
      begin
        rw [infi_uniformity, lift'_infi],
        exact (congr_arg _ $ funext $ assume i, (@nhds_eq_uniformity Î± (u i) a).symm),
        exact h,
        exact assume a b, rfl
      end
    end)
  (assume : Â¬ nonempty Î¹,
    le_antisymm
      (le_infi $ assume i, to_topological_space_mono $ infi_le _ _)
      (have infi u = âŠ¤, from top_unique $ le_infi $ assume i, (this âŸ¨iâŸ©).elim,
        have @uniform_space.to_topological_space _ (infi u) = âŠ¤,
          from this.symm â–¸ to_topological_space_top,
        this.symm â–¸ le_top))

lemma to_topological_space_Inf {s : set (uniform_space Î±)} :
  (Inf s).to_topological_space = (â¨…iâˆˆs, @uniform_space.to_topological_space Î± i) :=
begin
  rw [Inf_eq_infi, to_topological_space_infi],
  apply congr rfl,
  funext x,
  exact to_topological_space_infi
end

lemma to_topological_space_inf {u v : uniform_space Î±} :
  (u âŠ“ v).to_topological_space = u.to_topological_space âŠ“ v.to_topological_space :=
by rw [to_topological_space_Inf, infi_pair]

instance : uniform_space empty := âŠ¥
instance : uniform_space unit := âŠ¥
instance : uniform_space bool := âŠ¥
instance : uniform_space â„• := âŠ¥
instance : uniform_space â„¤ := âŠ¥

instance {p : Î± â†’ Prop} [t : uniform_space Î±] : uniform_space (subtype p) :=
uniform_space.comap subtype.val t

lemma uniformity_subtype {p : Î± â†’ Prop} [t : uniform_space Î±] :
  ğ“¤ (subtype p) = comap (Î»q:subtype p Ã— subtype p, (q.1.1, q.2.1)) (ğ“¤ Î±) :=
rfl

lemma uniform_continuous_subtype_val {p : Î± â†’ Prop} [uniform_space Î±] :
  uniform_continuous (subtype.val : {a : Î± // p a} â†’ Î±) :=
uniform_continuous_comap

lemma uniform_continuous_subtype_mk {p : Î± â†’ Prop} [uniform_space Î±] [uniform_space Î²]
  {f : Î² â†’ Î±} (hf : uniform_continuous f) (h : âˆ€x, p (f x)) :
  uniform_continuous (Î»x, âŸ¨f x, h xâŸ© : Î² â†’ subtype p) :=
uniform_continuous_comap' hf

lemma tendsto_of_uniform_continuous_subtype
  [uniform_space Î±] [uniform_space Î²] {f : Î± â†’ Î²} {s : set Î±} {a : Î±}
  (hf : uniform_continuous (Î»x:s, f x.val)) (ha : s âˆˆ ğ“ a) :
  tendsto f (ğ“ a) (ğ“ (f a)) :=
by rw [(@map_nhds_subtype_val_eq Î± _ s a (mem_of_nhds ha) ha).symm]; exact
tendsto_map' (continuous_iff_continuous_at.mp hf.continuous _)


section prod

/- a similar product space is possible on the function space (uniformity of pointwise convergence),
  but we want to have the uniformity of uniform convergence on function spaces -/
instance [uâ‚ : uniform_space Î±] [uâ‚‚ : uniform_space Î²] : uniform_space (Î± Ã— Î²) :=
uniform_space.of_core_eq
  (uâ‚.comap prod.fst âŠ“ uâ‚‚.comap prod.snd).to_core
  prod.topological_space
  (calc prod.topological_space = (uâ‚.comap prod.fst âŠ“ uâ‚‚.comap prod.snd).to_topological_space :
      by rw [to_topological_space_inf, to_topological_space_comap, to_topological_space_comap]; refl
    ... = _ : by rw [uniform_space.to_core_to_topological_space])

theorem uniformity_prod [uniform_space Î±] [uniform_space Î²] : ğ“¤ (Î± Ã— Î²) =
  (ğ“¤ Î±).comap (Î»p:(Î± Ã— Î²) Ã— Î± Ã— Î², (p.1.1, p.2.1)) âŠ“
  (ğ“¤ Î²).comap (Î»p:(Î± Ã— Î²) Ã— Î± Ã— Î², (p.1.2, p.2.2)) :=
inf_uniformity

lemma uniformity_prod_eq_prod [uniform_space Î±] [uniform_space Î²] :
  ğ“¤ (Î±Ã—Î²) =
    map (Î»p:(Î±Ã—Î±)Ã—(Î²Ã—Î²), ((p.1.1, p.2.1), (p.1.2, p.2.2))) (filter.prod (ğ“¤ Î±) (ğ“¤ Î²)) :=
have map (Î»p:(Î±Ã—Î±)Ã—(Î²Ã—Î²), ((p.1.1, p.2.1), (p.1.2, p.2.2))) =
  comap (Î»p:(Î±Ã—Î²)Ã—(Î±Ã—Î²), ((p.1.1, p.2.1), (p.1.2, p.2.2))),
  from funext $ assume f, map_eq_comap_of_inverse
    (funext $ assume âŸ¨âŸ¨_, _âŸ©, âŸ¨_, _âŸ©âŸ©, rfl) (funext $ assume âŸ¨âŸ¨_, _âŸ©, âŸ¨_, _âŸ©âŸ©, rfl),
by rw [this, uniformity_prod, filter.prod, comap_inf, comap_comap_comp, comap_comap_comp]

lemma mem_map_sets_iff' {Î± : Type*} {Î² : Type*} {f : filter Î±} {m : Î± â†’ Î²} {t : set Î²} :
  t âˆˆ (map m f).sets â†” (âˆƒsâˆˆf, m '' s âŠ† t) :=
mem_map_sets_iff

lemma mem_uniformity_of_uniform_continuous_invariant [uniform_space Î±] {s:set (Î±Ã—Î±)} {f : Î± â†’ Î± â†’ Î±}
  (hf : uniform_continuous (Î»p:Î±Ã—Î±, f p.1 p.2)) (hs : s âˆˆ ğ“¤ Î±) :
  âˆƒuâˆˆğ“¤ Î±, âˆ€a b c, (a, b) âˆˆ u â†’ (f a c, f b c) âˆˆ s :=
begin
  rw [uniform_continuous, uniformity_prod_eq_prod, tendsto_map'_iff, (âˆ˜)] at hf,
  rcases mem_map_sets_iff'.1 (hf hs) with âŸ¨t, ht, htsâŸ©, clear hf,
  rcases mem_prod_iff.1 ht with âŸ¨u, hu, v, hv, huvtâŸ©, clear ht,
  refine âŸ¨u, hu, assume a b c hab, hts $ (mem_image _ _ _).2 âŸ¨âŸ¨âŸ¨a, bâŸ©, âŸ¨c, câŸ©âŸ©, huvt âŸ¨_, _âŸ©, _âŸ©âŸ©,
  exact hab,
  exact refl_mem_uniformity hv,
  refl
end

lemma mem_uniform_prod [tâ‚ : uniform_space Î±] [tâ‚‚ : uniform_space Î²] {a : set (Î± Ã— Î±)} {b : set (Î² Ã— Î²)}
  (ha : a âˆˆ ğ“¤ Î±) (hb : b âˆˆ ğ“¤ Î²) :
  {p:(Î±Ã—Î²)Ã—(Î±Ã—Î²) | (p.1.1, p.2.1) âˆˆ a âˆ§ (p.1.2, p.2.2) âˆˆ b } âˆˆ (@uniformity (Î± Ã— Î²) _) :=
by rw [uniformity_prod]; exact inter_mem_inf_sets (preimage_mem_comap ha) (preimage_mem_comap hb)

lemma tendsto_prod_uniformity_fst [uniform_space Î±] [uniform_space Î²] :
  tendsto (Î»p:(Î±Ã—Î²)Ã—(Î±Ã—Î²), (p.1.1, p.2.1)) (ğ“¤ (Î± Ã— Î²)) (ğ“¤ Î±) :=
le_trans (map_mono (@inf_le_left (uniform_space (Î±Ã—Î²)) _ _ _)) map_comap_le

lemma tendsto_prod_uniformity_snd [uniform_space Î±] [uniform_space Î²] :
  tendsto (Î»p:(Î±Ã—Î²)Ã—(Î±Ã—Î²), (p.1.2, p.2.2)) (ğ“¤ (Î± Ã— Î²)) (ğ“¤ Î²) :=
le_trans (map_mono (@inf_le_right (uniform_space (Î±Ã—Î²)) _ _ _)) map_comap_le

lemma uniform_continuous_fst [uniform_space Î±] [uniform_space Î²] : uniform_continuous (Î»p:Î±Ã—Î², p.1) :=
tendsto_prod_uniformity_fst

lemma uniform_continuous_snd [uniform_space Î±] [uniform_space Î²] : uniform_continuous (Î»p:Î±Ã—Î², p.2) :=
tendsto_prod_uniformity_snd

variables [uniform_space Î±] [uniform_space Î²] [uniform_space Î³]
lemma uniform_continuous.prod_mk
  {fâ‚ : Î± â†’ Î²} {fâ‚‚ : Î± â†’ Î³} (hâ‚ : uniform_continuous fâ‚) (hâ‚‚ : uniform_continuous fâ‚‚) :
  uniform_continuous (Î»a, (fâ‚ a, fâ‚‚ a)) :=
by rw [uniform_continuous, uniformity_prod]; exact
tendsto_inf.2 âŸ¨tendsto_comap_iff.2 hâ‚, tendsto_comap_iff.2 hâ‚‚âŸ©

lemma uniform_continuous.prod_mk_left {f : Î± Ã— Î² â†’ Î³} (h : uniform_continuous f) (b) :
  uniform_continuous (Î» a, f (a,b)) :=
h.comp (uniform_continuous_id.prod_mk uniform_continuous_const)

lemma uniform_continuous.prod_mk_right {f : Î± Ã— Î² â†’ Î³} (h : uniform_continuous f) (a) :
  uniform_continuous (Î» b, f (a,b)) :=
h.comp (uniform_continuous_const.prod_mk  uniform_continuous_id)

lemma uniform_continuous.prod_map [uniform_space Î´] {f : Î± â†’ Î³} {g : Î² â†’ Î´}
  (hf : uniform_continuous f) (hg : uniform_continuous g) :
  uniform_continuous (prod.map f g) :=
(hf.comp uniform_continuous_fst).prod_mk (hg.comp uniform_continuous_snd)

lemma to_topological_space_prod {Î±} {Î²} [u : uniform_space Î±] [v : uniform_space Î²] :
  @uniform_space.to_topological_space (Î± Ã— Î²) prod.uniform_space =
    @prod.topological_space Î± Î² u.to_topological_space v.to_topological_space := rfl

end prod

section
open uniform_space function
variables {Î´' : Type*} [uniform_space Î±] [uniform_space Î²] [uniform_space Î³] [uniform_space Î´]
  [uniform_space Î´']

local notation f `âˆ˜â‚‚` g := function.bicompr f g

def uniform_continuousâ‚‚ (f : Î± â†’ Î² â†’ Î³) := uniform_continuous (uncurry f)

lemma uniform_continuousâ‚‚_def (f : Î± â†’ Î² â†’ Î³) :
  uniform_continuousâ‚‚ f â†” uniform_continuous (uncurry f) := iff.rfl

lemma uniform_continuousâ‚‚.uniform_continuous {f : Î± â†’ Î² â†’ Î³} (h : uniform_continuousâ‚‚ f) :
  uniform_continuous (uncurry f) := h

lemma uniform_continuousâ‚‚_curry (f : Î± Ã— Î² â†’ Î³) :
  uniform_continuousâ‚‚ (function.curry f) â†” uniform_continuous f :=
by rw [uniform_continuousâ‚‚, uncurry_curry]

lemma uniform_continuousâ‚‚.comp {f : Î± â†’ Î² â†’ Î³} {g : Î³ â†’ Î´}
  (hg : uniform_continuous g) (hf : uniform_continuousâ‚‚ f) :
  uniform_continuousâ‚‚ (g âˆ˜â‚‚ f) :=
hg.comp hf

lemma uniform_continuousâ‚‚.bicompl {f : Î± â†’ Î² â†’ Î³} {ga : Î´ â†’ Î±} {gb : Î´' â†’ Î²}
  (hf : uniform_continuousâ‚‚ f) (hga : uniform_continuous ga) (hgb : uniform_continuous gb) :
  uniform_continuousâ‚‚ (bicompl f ga gb) :=
hf.uniform_continuous.comp (hga.prod_map hgb)

end

lemma to_topological_space_subtype [u : uniform_space Î±] {p : Î± â†’ Prop} :
  @uniform_space.to_topological_space (subtype p) subtype.uniform_space =
    @subtype.topological_space Î± p u.to_topological_space := rfl

section sum
variables [uniform_space Î±] [uniform_space Î²]
open sum

/-- Uniformity on a disjoint union. Entourages of the diagonal in the union are obtained
by taking independently an entourage of the diagonal in the first part, and an entourage of
the diagonal in the second part. -/
def uniform_space.core.sum : uniform_space.core (Î± âŠ• Î²) :=
uniform_space.core.mk'
  (map (Î» p : Î± Ã— Î±, (inl p.1, inl p.2)) (ğ“¤ Î±) âŠ” map (Î» p : Î² Ã— Î², (inr p.1, inr p.2)) (ğ“¤ Î²))
  (Î» r âŸ¨Hâ‚, Hâ‚‚âŸ© x, by cases x; [apply refl_mem_uniformity Hâ‚, apply refl_mem_uniformity Hâ‚‚])
  (Î» r âŸ¨Hâ‚, Hâ‚‚âŸ©, âŸ¨symm_le_uniformity Hâ‚, symm_le_uniformity Hâ‚‚âŸ©)
  (Î» r âŸ¨HrÎ±, HrÎ²âŸ©, begin
    rcases comp_mem_uniformity_sets HrÎ± with âŸ¨tÎ±, htÎ±, HtÎ±âŸ©,
    rcases comp_mem_uniformity_sets HrÎ² with âŸ¨tÎ², htÎ², HtÎ²âŸ©,
    refine âŸ¨_,
      âŸ¨mem_map_sets_iff.2 âŸ¨tÎ±, htÎ±, subset_union_left _ _âŸ©,
       mem_map_sets_iff.2 âŸ¨tÎ², htÎ², subset_union_right _ _âŸ©âŸ©, _âŸ©,
    rintros âŸ¨_, _âŸ© âŸ¨z, âŸ¨âŸ¨a, bâŸ©, hab, âŸ¨âŸ©âŸ© | âŸ¨âŸ¨a, bâŸ©, hab, âŸ¨âŸ©âŸ©,
                       âŸ¨âŸ¨_, câŸ©, hbc, âŸ¨âŸ©âŸ© | âŸ¨âŸ¨_, câŸ©, hbc, âŸ¨âŸ©âŸ©âŸ©,
    { have A : (a, c) âˆˆ comp_rel tÎ± tÎ± := âŸ¨b, hab, hbcâŸ©,
      exact HtÎ± A },
    { have A : (a, c) âˆˆ comp_rel tÎ² tÎ² := âŸ¨b, hab, hbcâŸ©,
      exact HtÎ² A }
  end)

/-- The union of an entourage of the diagonal in each set of a disjoint union is again an entourage
of the diagonal. -/
lemma union_mem_uniformity_sum
  {a : set (Î± Ã— Î±)} (ha : a âˆˆ ğ“¤ Î±) {b : set (Î² Ã— Î²)} (hb : b âˆˆ ğ“¤ Î²) :
  ((Î» p : (Î± Ã— Î±), (inl p.1, inl p.2)) '' a âˆª (Î» p : (Î² Ã— Î²), (inr p.1, inr p.2)) '' b) âˆˆ
    (@uniform_space.core.sum Î± Î² _ _).uniformity :=
âŸ¨mem_map_sets_iff.2 âŸ¨_, ha, subset_union_left _ _âŸ©, mem_map_sets_iff.2 âŸ¨_, hb, subset_union_right _ _âŸ©âŸ©

/- To prove that the topology defined by the uniform structure on the disjoint union coincides with
the disjoint union topology, we need two lemmas saying that open sets can be characterized by
the uniform structure -/
lemma uniformity_sum_of_open_aux {s : set (Î± âŠ• Î²)} (hs : is_open s) {x : Î± âŠ• Î²} (xs : x âˆˆ s) :
  { p : ((Î± âŠ• Î²) Ã— (Î± âŠ• Î²)) | p.1 = x â†’ p.2 âˆˆ s } âˆˆ (@uniform_space.core.sum Î± Î² _ _).uniformity :=
begin
  cases x,
  { refine mem_sets_of_superset
      (union_mem_uniformity_sum (mem_nhds_uniformity_iff_right.1 (mem_nhds_sets hs.1 xs)) univ_mem_sets)
      (union_subset _ _);
    rintro _ âŸ¨âŸ¨_, bâŸ©, h, âŸ¨âŸ©âŸ© âŸ¨âŸ©,
    exact h rfl },
  { refine mem_sets_of_superset
      (union_mem_uniformity_sum univ_mem_sets (mem_nhds_uniformity_iff_right.1 (mem_nhds_sets hs.2 xs)))
      (union_subset _ _);
    rintro _ âŸ¨âŸ¨a, _âŸ©, h, âŸ¨âŸ©âŸ© âŸ¨âŸ©,
    exact h rfl },
end

lemma open_of_uniformity_sum_aux {s : set (Î± âŠ• Î²)}
  (hs : âˆ€x âˆˆ s, { p : ((Î± âŠ• Î²) Ã— (Î± âŠ• Î²)) | p.1 = x â†’ p.2 âˆˆ s } âˆˆ (@uniform_space.core.sum Î± Î² _ _).uniformity) :
  is_open s :=
begin
  split,
  { refine (@is_open_iff_mem_nhds Î± _ _).2 (Î» a ha, mem_nhds_uniformity_iff_right.2 _),
    rcases mem_map_sets_iff.1 (hs _ ha).1 with âŸ¨t, ht, stâŸ©,
    refine mem_sets_of_superset ht _,
    rintro p pt rfl, exact st âŸ¨_, pt, rflâŸ© rfl },
  { refine (@is_open_iff_mem_nhds Î² _ _).2 (Î» b hb, mem_nhds_uniformity_iff_right.2 _),
    rcases mem_map_sets_iff.1 (hs _ hb).2 with âŸ¨t, ht, stâŸ©,
    refine mem_sets_of_superset ht _,
    rintro p pt rfl, exact st âŸ¨_, pt, rflâŸ© rfl }
end

/- We can now define the uniform structure on the disjoint union -/
instance sum.uniform_space : uniform_space (Î± âŠ• Î²) :=
{ to_core := uniform_space.core.sum,
  is_open_uniformity := Î» s, âŸ¨uniformity_sum_of_open_aux, open_of_uniformity_sum_auxâŸ© }

lemma sum.uniformity : ğ“¤ (Î± âŠ• Î²) =
    map (Î» p : Î± Ã— Î±, (inl p.1, inl p.2)) (ğ“¤ Î±) âŠ”
    map (Î» p : Î² Ã— Î², (inr p.1, inr p.2)) (ğ“¤ Î²) := rfl

end sum

end constructions

lemma lebesgue_number_lemma {Î± : Type u} [uniform_space Î±] {s : set Î±} {Î¹} {c : Î¹ â†’ set Î±}
  (hs : compact s) (hcâ‚ : âˆ€ i, is_open (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) :
  âˆƒ n âˆˆ ğ“¤ Î±, âˆ€ x âˆˆ s, âˆƒ i, {y | (x, y) âˆˆ n} âŠ† c i :=
begin
  let u := Î» n, {x | âˆƒ i (m âˆˆ ğ“¤ Î±), {y | (x, y) âˆˆ comp_rel m n} âŠ† c i},
  have huâ‚ : âˆ€ n âˆˆ ğ“¤ Î±, is_open (u n),
  { refine Î» n hn, is_open_uniformity.2 _,
    rintro x âŸ¨i, m, hm, hâŸ©,
    rcases comp_mem_uniformity_sets hm with âŸ¨m', hm', mm'âŸ©,
    apply (ğ“¤ Î±).sets_of_superset hm',
    rintros âŸ¨x, yâŸ© hp rfl,
    refine âŸ¨i, m', hm', Î» z hz, h (monotone_comp_rel monotone_id monotone_const mm' _)âŸ©,
    dsimp at hz âŠ¢, rw comp_rel_assoc,
    exact âŸ¨y, hp, hzâŸ© },
  have huâ‚‚ : s âŠ† â‹ƒ n âˆˆ ğ“¤ Î±, u n,
  { intros x hx,
    rcases mem_Union.1 (hcâ‚‚ hx) with âŸ¨i, hâŸ©,
    rcases comp_mem_uniformity_sets (is_open_uniformity.1 (hcâ‚ i) x h) with âŸ¨m', hm', mm'âŸ©,
    exact mem_bUnion hm' âŸ¨i, _, hm', Î» y hy, mm' hy rflâŸ© },
  rcases hs.elim_finite_subcover_image huâ‚ huâ‚‚ with âŸ¨b, bu, b_fin, b_coverâŸ©,
  refine âŸ¨_, Inter_mem_sets b_fin bu, Î» x hx, _âŸ©,
  rcases mem_bUnion_iff.1 (b_cover hx) with âŸ¨n, bn, i, m, hm, hâŸ©,
  refine âŸ¨i, Î» y hy, h _âŸ©,
  exact prod_mk_mem_comp_rel (refl_mem_uniformity hm) (bInter_subset_of_mem bn hy)
end

lemma lebesgue_number_lemma_sUnion {Î± : Type u} [uniform_space Î±] {s : set Î±} {c : set (set Î±)}
  (hs : compact s) (hcâ‚ : âˆ€ t âˆˆ c, is_open t) (hcâ‚‚ : s âŠ† â‹ƒâ‚€ c) :
  âˆƒ n âˆˆ ğ“¤ Î±, âˆ€ x âˆˆ s, âˆƒ t âˆˆ c, âˆ€ y, (x, y) âˆˆ n â†’ y âˆˆ t :=
by rw sUnion_eq_Union at hcâ‚‚;
   simpa using lebesgue_number_lemma hs (by simpa) hcâ‚‚

/-!
### Expressing continuity properties in uniform spaces

We reformulate the various continuity properties of functions taking values in a uniform space
in terms of the uniformity in the target. Since the same lemmas (essentially with the same names)
also exist for metric spaces and emetric spaces (reformulating things in terms of the distance or
the edistance in the target), we put them in a namespace `uniform` here.

In the metric and emetric space setting, there are also similar lemmas where one assumes that
both the source and the target are metric spaces, reformulating things in terms of the distance
on both sides. These lemmas are generally written without primes, and the versions where only
the target is a metric space is primed. We follow the same convention here, thus giving lemmas
with primes.
-/

namespace uniform

variables {Î± : Type*} {Î² : Type*} [uniform_space Î±]

theorem tendsto_nhds_right {f : filter Î²} {u : Î² â†’ Î±} {a : Î±} :
  tendsto u f (ğ“ a) â†” tendsto (Î» x, (a, u x)) f (ğ“¤ Î±)  :=
âŸ¨Î» H, tendsto_left_nhds_uniformity.comp H,
Î» H s hs, by simpa [mem_of_nhds hs] using H (mem_nhds_uniformity_iff_right.1 hs)âŸ©

theorem tendsto_nhds_left {f : filter Î²} {u : Î² â†’ Î±} {a : Î±} :
  tendsto u f (ğ“ a) â†” tendsto (Î» x, (u x, a)) f (ğ“¤ Î±)  :=
âŸ¨Î» H, tendsto_right_nhds_uniformity.comp H,
Î» H s hs, by simpa [mem_of_nhds hs] using H (mem_nhds_uniformity_iff_left.1 hs)âŸ©

theorem continuous_at_iff'_right [topological_space Î²] {f : Î² â†’ Î±} {b : Î²} :
  continuous_at f b â†” tendsto (Î» x, (f b, f x)) (ğ“ b) (ğ“¤ Î±) :=
by rw [continuous_at, tendsto_nhds_right]

theorem continuous_at_iff'_left [topological_space Î²] {f : Î² â†’ Î±} {b : Î²} :
  continuous_at f b â†” tendsto (Î» x, (f x, f b)) (ğ“ b) (ğ“¤ Î±) :=
by rw [continuous_at, tendsto_nhds_left]

theorem continuous_within_at_iff'_right [topological_space Î²] {f : Î² â†’ Î±} {b : Î²} {s : set Î²} :
  continuous_within_at f s b â†” tendsto (Î» x, (f b, f x)) (nhds_within b s) (ğ“¤ Î±) :=
by rw [continuous_within_at, tendsto_nhds_right]

theorem continuous_within_at_iff'_left [topological_space Î²] {f : Î² â†’ Î±} {b : Î²} {s : set Î²} :
  continuous_within_at f s b â†” tendsto (Î» x, (f x, f b)) (nhds_within b s) (ğ“¤ Î±) :=
by rw [continuous_within_at, tendsto_nhds_left]

theorem continuous_on_iff'_right [topological_space Î²] {f : Î² â†’ Î±} {s : set Î²} :
  continuous_on f s â†” âˆ€ b âˆˆ s, tendsto (Î» x, (f b, f x)) (nhds_within b s) (ğ“¤ Î±) :=
by simp [continuous_on, continuous_within_at_iff'_right]

theorem continuous_on_iff'_left [topological_space Î²] {f : Î² â†’ Î±} {s : set Î²} :
  continuous_on f s â†” âˆ€ b âˆˆ s, tendsto (Î» x, (f x, f b)) (nhds_within b s) (ğ“¤ Î±) :=
by simp [continuous_on, continuous_within_at_iff'_left]

theorem continuous_iff'_right [topological_space Î²] {f : Î² â†’ Î±} :
  continuous f â†” âˆ€ b, tendsto (Î» x, (f b, f x)) (ğ“ b) (ğ“¤ Î±) :=
continuous_iff_continuous_at.trans $ forall_congr $ Î» b, tendsto_nhds_right

theorem continuous_iff'_left [topological_space Î²] {f : Î² â†’ Î±} :
  continuous f â†” âˆ€ b, tendsto (Î» x, (f x, f b)) (ğ“ b) (ğ“¤ Î±) :=
continuous_iff_continuous_at.trans $ forall_congr $ Î» b, tendsto_nhds_left

end uniform

lemma filter.tendsto.congr_uniformity {Î± Î²} [uniform_space Î²] {f g : Î± â†’ Î²} {l : filter Î±} {b : Î²}
  (hf : tendsto f l (ğ“ b)) (hg : tendsto (Î» x, (f x, g x)) l (ğ“¤ Î²)) :
  tendsto g l (ğ“ b) :=
uniform.tendsto_nhds_right.2 $ (uniform.tendsto_nhds_right.1 hf).uniformity_trans hg

lemma uniform.tendsto_congr {Î± Î²} [uniform_space Î²] {f g : Î± â†’ Î²} {l : filter Î±} {b : Î²}
  (hfg : tendsto (Î» x, (f x, g x)) l (ğ“¤ Î²)) :
  tendsto f l (ğ“ b) â†” tendsto g l (ğ“ b) :=
âŸ¨Î» h, h.congr_uniformity hfg, Î» h, h.congr_uniformity hfg.uniformity_symmâŸ©
