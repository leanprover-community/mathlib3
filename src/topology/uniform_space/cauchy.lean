/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import topology.bases
import topology.uniform_space.basic
/-!
# Theory of Cauchy filters in uniform spaces. Complete uniform spaces. Totally bounded subsets.
-/
universes u v

open filter topological_space set classical uniform_space
open_locale classical uniformity topological_space filter

variables {Î± : Type u} {Î² : Type v} [uniform_space Î±]

/-- A filter `f` is Cauchy if for every entourage `r`, there exists an
  `s âˆˆ f` such that `s Ã— s âŠ† r`. This is a generalization of Cauchy
  sequences, because if `a : â„• â†’ Î±` then the filter of sets containing
  cofinitely many of the `a n` is Cauchy iff `a` is a Cauchy sequence. -/
def cauchy (f : filter Î±) := ne_bot f âˆ§ f Ã—á¶  f â‰¤ (ğ“¤ Î±)

/-- A set `s` is called *complete*, if any Cauchy filter `f` such that `s âˆˆ f`
has a limit in `s` (formally, it satisfies `f â‰¤ ğ“ x` for some `x âˆˆ s`). -/
def is_complete (s : set Î±) := âˆ€f, cauchy f â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒxâˆˆs, f â‰¤ ğ“ x

lemma filter.has_basis.cauchy_iff {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set (Î± Ã— Î±)} (h : (ğ“¤ Î±).has_basis p s)
  {f : filter Î±} :
  cauchy f â†” (ne_bot f âˆ§ (âˆ€ i, p i â†’ âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, (x, y) âˆˆ s i)) :=
and_congr iff.rfl $ (f.basis_sets.prod_self.le_basis_iff h).trans $
  by simp only [subset_def, prod.forall, mem_prod_eq, and_imp, id]

lemma cauchy_iff' {f : filter Î±} :
  cauchy f â†” (ne_bot f âˆ§ (âˆ€ s âˆˆ ğ“¤ Î±, âˆƒtâˆˆf, âˆ€ x y âˆˆ t, (x, y) âˆˆ s)) :=
(ğ“¤ Î±).basis_sets.cauchy_iff

lemma cauchy_iff {f : filter Î±} :
  cauchy f â†” (ne_bot f âˆ§ (âˆ€ s âˆˆ ğ“¤ Î±, âˆƒtâˆˆf, (set.prod t t) âŠ† s)) :=
cauchy_iff'.trans $ by simp only [subset_def, prod.forall, mem_prod_eq, and_imp, id]

lemma cauchy_map_iff {l : filter Î²} {f : Î² â†’ Î±} :
  cauchy (l.map f) â†” (ne_bot l âˆ§ tendsto (Î»p:Î²Ã—Î², (f p.1, f p.2)) (l Ã—á¶  l) (ğ“¤ Î±)) :=
by rw [cauchy, map_ne_bot_iff, prod_map_map_eq, tendsto]

lemma cauchy_map_iff' {l : filter Î²} [hl : ne_bot l] {f : Î² â†’ Î±} :
  cauchy (l.map f) â†” tendsto (Î»p:Î²Ã—Î², (f p.1, f p.2)) (l Ã—á¶  l) (ğ“¤ Î±) :=
cauchy_map_iff.trans $ and_iff_right hl

lemma cauchy.mono {f g : filter Î±} [hg : ne_bot g] (h_c : cauchy f) (h_le : g â‰¤ f) : cauchy g :=
âŸ¨hg, le_trans (filter.prod_mono h_le h_le) h_c.rightâŸ©

lemma cauchy.mono' {f g : filter Î±} (h_c : cauchy f) (hg : ne_bot g) (h_le : g â‰¤ f) : cauchy g :=
h_c.mono h_le

lemma cauchy_nhds {a : Î±} : cauchy (ğ“ a) :=
âŸ¨nhds_ne_bot, nhds_prod_eq.symm.trans_le (nhds_le_uniformity a)âŸ©

lemma cauchy_pure {a : Î±} : cauchy (pure a) :=
cauchy_nhds.mono (pure_le_nhds a)

lemma filter.tendsto.cauchy_map {l : filter Î²} [ne_bot l] {f : Î² â†’ Î±} {a : Î±}
  (h : tendsto f l (ğ“ a)) :
  cauchy (map f l) :=
cauchy_nhds.mono h

lemma cauchy.prod [uniform_space Î²] {f : filter Î±} {g : filter Î²} (hf : cauchy f) (hg : cauchy g) :
  cauchy (f Ã—á¶  g) :=
begin
  refine âŸ¨hf.1.prod hg.1, _âŸ©,
  simp only [uniformity_prod, le_inf_iff, â† map_le_iff_le_comap, â† prod_map_map_eq],
  exact âŸ¨le_trans (prod_mono tendsto_fst tendsto_fst) hf.2,
    le_trans (prod_mono tendsto_snd tendsto_snd) hg.2âŸ©
end

/-- The common part of the proofs of `le_nhds_of_cauchy_adhp` and
`sequentially_complete.le_nhds_of_seq_tendsto_nhds`: if for any entourage `s`
one can choose a set `t âˆˆ f` of diameter `s` such that it contains a point `y`
with `(x, y) âˆˆ s`, then `f` converges to `x`. -/
lemma le_nhds_of_cauchy_adhp_aux {f : filter Î±} {x : Î±}
  (adhs : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ t âˆˆ f, (set.prod t t âŠ† s) âˆ§ âˆƒ y, (x, y) âˆˆ s âˆ§ y âˆˆ t) :
  f â‰¤ ğ“ x :=
begin
  -- Consider a neighborhood `s` of `x`
  assume s hs,
  -- Take an entourage twice smaller than `s`
  rcases comp_mem_uniformity_sets (mem_nhds_uniformity_iff_right.1 hs) with âŸ¨U, U_mem, hUâŸ©,
  -- Take a set `t âˆˆ f`, `t Ã— t âŠ† U`, and a point `y âˆˆ t` such that `(x, y) âˆˆ U`
  rcases adhs U U_mem with âŸ¨t, t_mem, ht, y, hxy, hyâŸ©,
  apply mem_of_superset t_mem,
  -- Given a point `z âˆˆ t`, we have `(x, y) âˆˆ U` and `(y, z) âˆˆ t Ã— t âŠ† U`, hence `z âˆˆ s`
  exact (Î» z hz, hU (prod_mk_mem_comp_rel hxy (ht $ mk_mem_prod hy hz)) rfl)
end

/-- If `x` is an adherent (cluster) point for a Cauchy filter `f`, then it is a limit point
for `f`. -/
lemma le_nhds_of_cauchy_adhp {f : filter Î±} {x : Î±} (hf : cauchy f)
  (adhs : cluster_pt x f) : f â‰¤ ğ“ x :=
le_nhds_of_cauchy_adhp_aux
begin
  assume s hs,
  obtain âŸ¨t, t_mem, htâŸ© : âˆƒ t âˆˆ f, set.prod t t âŠ† s,
    from (cauchy_iff.1 hf).2 s hs,
  use [t, t_mem, ht],
  exact (forall_mem_nonempty_iff_ne_bot.2 adhs _
    (inter_mem_inf (mem_nhds_left x hs) t_mem ))
end

lemma le_nhds_iff_adhp_of_cauchy {f : filter Î±} {x : Î±} (hf : cauchy f) :
  f â‰¤ ğ“ x â†” cluster_pt x f :=
âŸ¨assume h, cluster_pt.of_le_nhds' h hf.1, le_nhds_of_cauchy_adhp hfâŸ©

lemma cauchy.map [uniform_space Î²] {f : filter Î±} {m : Î± â†’ Î²}
  (hf : cauchy f) (hm : uniform_continuous m) : cauchy (map m f) :=
âŸ¨hf.1.map _,
  calc map m f Ã—á¶  map m f = map (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (f Ã—á¶  f) : filter.prod_map_map_eq
    ... â‰¤ map (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î±) : map_mono hf.right
    ... â‰¤ ğ“¤ Î² : hmâŸ©

lemma cauchy.comap [uniform_space Î²] {f : filter Î²} {m : Î± â†’ Î²}
  (hf : cauchy f) (hm : comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î²) â‰¤ ğ“¤ Î±)
  [ne_bot (comap m f)] : cauchy (comap m f) :=
âŸ¨â€¹_â€º,
  calc comap m f Ã—á¶  comap m f = comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (f Ã—á¶  f) : filter.prod_comap_comap_eq
    ... â‰¤ comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î²) : comap_mono hf.right
    ... â‰¤ ğ“¤ Î± : hmâŸ©

lemma cauchy.comap' [uniform_space Î²] {f : filter Î²} {m : Î± â†’ Î²}
  (hf : cauchy f) (hm : comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î²) â‰¤ ğ“¤ Î±)
  (hb : ne_bot (comap m f)) : cauchy (comap m f) :=
hf.comap hm

/-- Cauchy sequences. Usually defined on â„•, but often it is also useful to say that a function
defined on â„ is Cauchy at +âˆ to deduce convergence. Therefore, we define it in a type class that
is general enough to cover both â„• and â„, which are the main motivating examples. -/
def cauchy_seq [semilattice_sup Î²] (u : Î² â†’ Î±) := cauchy (at_top.map u)

lemma cauchy_seq.tendsto_uniformity [semilattice_sup Î²] {u : Î² â†’ Î±} (h : cauchy_seq u) :
  tendsto (prod.map u u) at_top (ğ“¤ Î±) :=
by simpa only [tendsto, prod_map_map_eq', prod_at_top_at_top_eq] using h.right

lemma cauchy_seq.nonempty [semilattice_sup Î²] {u : Î² â†’ Î±} (hu : cauchy_seq u) : nonempty Î² :=
@nonempty_of_ne_bot _ _ $ (map_ne_bot_iff _).1 hu.1

lemma cauchy_seq.mem_entourage {Î² : Type*} [semilattice_sup Î²] {u : Î² â†’ Î±}
  (h : cauchy_seq u) {V : set (Î± Ã— Î±)} (hV : V âˆˆ ğ“¤ Î±) :
  âˆƒ kâ‚€, âˆ€ i j, kâ‚€ â‰¤ i â†’ kâ‚€ â‰¤ j â†’ (u i, u j) âˆˆ V :=
begin
  haveI := h.nonempty,
  have := h.tendsto_uniformity, rw â† prod_at_top_at_top_eq at this,
  simpa [maps_to] using at_top_basis.prod_self.tendsto_left_iff.1 this V hV
end

lemma filter.tendsto.cauchy_seq [semilattice_sup Î²] [nonempty Î²] {f : Î² â†’ Î±} {x}
  (hx : tendsto f at_top (ğ“ x)) :
  cauchy_seq f :=
hx.cauchy_map

lemma cauchy_seq_const (x : Î±) : cauchy_seq (Î» n : â„•, x) :=
tendsto_const_nhds.cauchy_seq

lemma cauchy_seq_iff_tendsto [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” tendsto (prod.map u u) at_top (ğ“¤ Î±) :=
cauchy_map_iff'.trans $ by simp only [prod_at_top_at_top_eq, prod.map_def]

lemma cauchy_seq.comp_tendsto {Î³} [semilattice_sup Î²] [semilattice_sup Î³] [nonempty Î³]
  {f : Î² â†’ Î±} (hf : cauchy_seq f) {g : Î³ â†’ Î²} (hg : tendsto g at_top at_top) :
  cauchy_seq (f âˆ˜ g) :=
cauchy_seq_iff_tendsto.2 $ hf.tendsto_uniformity.comp (hg.prod_at_top hg)

lemma cauchy_seq.subseq_subseq_mem {V : â„• â†’ set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±)
  {u : â„• â†’ Î±} (hu : cauchy_seq u)
  {f g : â„• â†’ â„•} (hf : tendsto f at_top at_top) (hg : tendsto g at_top at_top) :
  âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ âˆ€ n, ((u âˆ˜ f âˆ˜ Ï†) n, (u âˆ˜ g âˆ˜ Ï†) n) âˆˆ V n :=
begin
  rw cauchy_seq_iff_tendsto at hu,
  exact ((hu.comp $ hf.prod_at_top hg).comp tendsto_at_top_diagonal).subseq_mem hV,
end

lemma cauchy_seq_iff' {u : â„• â†’ Î±} :
  cauchy_seq u â†” âˆ€ V âˆˆ ğ“¤ Î±, âˆ€á¶  k in at_top, k âˆˆ (prod.map u u) â»Â¹' V :=
by simpa only [cauchy_seq_iff_tendsto]

lemma cauchy_seq_iff {u : â„• â†’ Î±} :
  cauchy_seq u â†” âˆ€ V âˆˆ ğ“¤ Î±, âˆƒ N, âˆ€ k â‰¥ N, âˆ€ l â‰¥ N, (u k, u l) âˆˆ V :=
by simp [cauchy_seq_iff', filter.eventually_at_top_prod_self', prod_map]

lemma cauchy_seq.cauchy_map_cofinite {u : â„• â†’ Î±} (hu : cauchy_seq u) :
  cauchy (filter.map u cofinite) :=
begin
  rw nat.cofinite_eq_at_top,
  exact hu,
end

lemma cauchy_seq.prod_map {Î³ Î´} [uniform_space Î²] [semilattice_sup Î³] [semilattice_sup Î´]
  {u : Î³ â†’ Î±} {v : Î´ â†’ Î²}
  (hu : cauchy_seq u) (hv : cauchy_seq v) : cauchy_seq (prod.map u v) :=
by simpa only [cauchy_seq, prod_map_map_eq', prod_at_top_at_top_eq] using hu.prod hv

lemma cauchy_seq.prod {Î³} [uniform_space Î²] [semilattice_sup Î³] {u : Î³ â†’ Î±} {v : Î³ â†’ Î²}
  (hu : cauchy_seq u) (hv : cauchy_seq v) : cauchy_seq (Î» x, (u x, v x)) :=
begin
  haveI := hu.nonempty,
  exact (hu.prod hv).mono (tendsto.prod_mk le_rfl le_rfl)
end

lemma cauchy_seq.eventually_eventually [semilattice_sup Î²] [nonempty Î²] {u : Î² â†’ Î±}
  (hu : cauchy_seq u) {V : set (Î± Ã— Î±)} (hV : V âˆˆ ğ“¤ Î±) :
  âˆ€á¶  k in at_top, âˆ€á¶  l in at_top, (u k, u l) âˆˆ V :=
eventually_eventually_at_top $ hu.tendsto_uniformity hV

lemma uniform_continuous.comp_cauchy_seq {Î³} [uniform_space Î²] [semilattice_sup Î³]
  {f : Î± â†’ Î²} (hf : uniform_continuous f) {u : Î³ â†’ Î±} (hu : cauchy_seq u) :
  cauchy_seq (f âˆ˜ u) :=
hu.map hf

lemma cauchy_seq.subseq_mem {V : â„• â†’ set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±)
  {u : â„• â†’ Î±} (hu : cauchy_seq u) :
  âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ âˆ€ n, (u $ Ï† (n + 1), u $ Ï† n) âˆˆ V n :=
begin
  have : âˆ€ n, âˆƒ N, âˆ€ k â‰¥ N, âˆ€ l â‰¥ k, (u l, u k) âˆˆ V n,
  { intro n,
    rw [cauchy_seq_iff] at hu,
    rcases hu _ (hV n) with âŸ¨N, HâŸ©,
    exact âŸ¨N, Î» k hk l hl, H _ (le_trans hk hl) _ hk âŸ© },
  obtain âŸ¨Ï† : â„• â†’ â„•, Ï†_extr : strict_mono Ï†, hÏ† : âˆ€ n, âˆ€ l â‰¥ Ï† n, (u l, u $ Ï† n) âˆˆ V nâŸ© :=
    extraction_forall_of_eventually' this,
  exact âŸ¨Ï†, Ï†_extr, Î» n, hÏ† _ _ (Ï†_extr $ lt_add_one n).leâŸ©,
end

lemma filter.tendsto.subseq_mem_entourage {V : â„• â†’ set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±)
  {u : â„• â†’ Î±} {a : Î±} (hu : tendsto u at_top (ğ“ a)) :
  âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ (u (Ï† 0), a) âˆˆ V 0 âˆ§ âˆ€ n, (u $ Ï† (n + 1), u $ Ï† n) âˆˆ V (n + 1) :=
begin
  rcases mem_at_top_sets.1 (hu (ball_mem_nhds a (symm_le_uniformity $ hV 0))) with âŸ¨n, hnâŸ©,
  rcases (hu.comp (tendsto_add_at_top_nat n)).cauchy_seq.subseq_mem (Î» n, hV (n + 1))
    with âŸ¨Ï†, Ï†_mono, hÏ†VâŸ©,
  exact âŸ¨Î» k, Ï† k + n, Ï†_mono.add_const _, hn _ le_add_self, hÏ†VâŸ©
end

/-- If a Cauchy sequence has a convergent subsequence, then it converges. -/
lemma tendsto_nhds_of_cauchy_seq_of_subseq
  [semilattice_sup Î²] {u : Î² â†’ Î±} (hu : cauchy_seq u)
  {Î¹ : Type*} {f : Î¹ â†’ Î²} {p : filter Î¹} [ne_bot p]
  (hf : tendsto f p at_top) {a : Î±} (ha : tendsto (u âˆ˜ f) p (ğ“ a)) :
  tendsto u at_top (ğ“ a) :=
le_nhds_of_cauchy_adhp hu (map_cluster_pt_of_comp hf ha)

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma filter.has_basis.cauchy_seq_iff {Î³} [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±}
  {p : Î³ â†’ Prop} {s : Î³ â†’ set (Î± Ã— Î±)} (h : (ğ“¤ Î±).has_basis p s) :
  cauchy_seq u â†” âˆ€ i, p i â†’ âˆƒN, âˆ€m nâ‰¥N, (u m, u n) âˆˆ s i :=
begin
  rw [cauchy_seq_iff_tendsto, â† prod_at_top_at_top_eq],
  refine (at_top_basis.prod_self.tendsto_iff h).trans _,
  simp only [exists_prop, true_and, maps_to, preimage, subset_def, prod.forall,
    mem_prod_eq, mem_set_of_eq, mem_Ici, and_imp, prod.map]
end

lemma filter.has_basis.cauchy_seq_iff' {Î³} [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±}
  {p : Î³ â†’ Prop} {s : Î³ â†’ set (Î± Ã— Î±)} (H : (ğ“¤ Î±).has_basis p s) :
  cauchy_seq u â†” âˆ€ i, p i â†’ âˆƒN, âˆ€nâ‰¥N, (u n, u N) âˆˆ s i :=
begin
  refine H.cauchy_seq_iff.trans âŸ¨Î» h i hi, _, Î» h i hi, _âŸ©,
  { exact (h i hi).imp (Î» N hN n hn, hN n N hn (le_refl N)) },
  { rcases comp_symm_of_uniformity (H.mem_of_mem hi) with âŸ¨t, ht, ht', htsâŸ©,
    rcases H.mem_iff.1 ht with âŸ¨j, hj, hjtâŸ©,
    refine (h j hj).imp (Î» N hN m n hm hn, hts âŸ¨u N, hjt _, ht' $ hjt _âŸ©),
    { exact hN m hm },
    { exact hN n hn } }
end

lemma cauchy_seq_of_controlled [semilattice_sup Î²] [nonempty Î²]
  (U : Î² â†’ set (Î± Ã— Î±)) (hU : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s)
  {f : Î² â†’ Î±} (hf : âˆ€ {N m n : Î²}, N â‰¤ m â†’ N â‰¤ n â†’ (f m, f n) âˆˆ U N) :
  cauchy_seq f :=
cauchy_seq_iff_tendsto.2
begin
  assume s hs,
  rw [mem_map, mem_at_top_sets],
  cases hU s hs with N hN,
  refine âŸ¨(N, N), Î» mn hmn, _âŸ©,
  cases mn with m n,
  exact hN (hf hmn.1 hmn.2)
end

/-- A complete space is defined here using uniformities. A uniform space
  is complete if every Cauchy filter converges. -/
class complete_space (Î± : Type u) [uniform_space Î±] : Prop :=
(complete : âˆ€{f:filter Î±}, cauchy f â†’ âˆƒx, f â‰¤ ğ“ x)

lemma complete_univ {Î± : Type u} [uniform_space Î±] [complete_space Î±] :
  is_complete (univ : set Î±) :=
begin
  assume f hf _,
  rcases complete_space.complete hf with âŸ¨x, hxâŸ©,
  exact âŸ¨x, mem_univ x, hxâŸ©
end

instance complete_space.prod [uniform_space Î²] [complete_space Î±] [complete_space Î²] :
  complete_space (Î± Ã— Î²) :=
{ complete := Î» f hf,
    let âŸ¨x1, hx1âŸ© := complete_space.complete $ hf.map uniform_continuous_fst in
    let âŸ¨x2, hx2âŸ© := complete_space.complete $ hf.map uniform_continuous_snd in
    âŸ¨(x1, x2), by rw [nhds_prod_eq, filter.prod_def];
      from filter.le_lift (Î» s hs, filter.le_lift' $ Î» t ht,
        have H1 : prod.fst â»Â¹' s âˆˆ f.sets := hx1 hs,
        have H2 : prod.snd â»Â¹' t âˆˆ f.sets := hx2 ht,
        filter.inter_mem H1 H2)âŸ© }

/--If `univ` is complete, the space is a complete space -/
lemma complete_space_of_is_complete_univ (h : is_complete (univ : set Î±)) : complete_space Î± :=
âŸ¨Î» f hf, let âŸ¨x, _, hxâŸ© := h f hf ((@principal_univ Î±).symm â–¸ le_top) in âŸ¨x, hxâŸ©âŸ©

lemma complete_space_iff_is_complete_univ :
  complete_space Î± â†” is_complete (univ : set Î±) :=
âŸ¨@complete_univ Î± _, complete_space_of_is_complete_univâŸ©

lemma cauchy_iff_exists_le_nhds [complete_space Î±] {l : filter Î±} [ne_bot l] :
  cauchy l â†” (âˆƒx, l â‰¤ ğ“ x) :=
âŸ¨complete_space.complete, assume âŸ¨x, hxâŸ©, cauchy_nhds.mono hxâŸ©

lemma cauchy_map_iff_exists_tendsto [complete_space Î±] {l : filter Î²} {f : Î² â†’ Î±} [ne_bot l] :
  cauchy (l.map f) â†” (âˆƒx, tendsto f l (ğ“ x)) :=
cauchy_iff_exists_le_nhds

/-- A Cauchy sequence in a complete space converges -/
theorem cauchy_seq_tendsto_of_complete [semilattice_sup Î²] [complete_space Î±]
  {u : Î² â†’ Î±} (H : cauchy_seq u) : âˆƒx, tendsto u at_top (ğ“ x) :=
complete_space.complete H

/-- If `K` is a complete subset, then any cauchy sequence in `K` converges to a point in `K` -/
lemma cauchy_seq_tendsto_of_is_complete [semilattice_sup Î²] {K : set Î±} (hâ‚ : is_complete K)
  {u : Î² â†’ Î±} (hâ‚‚ : âˆ€ n, u n âˆˆ K) (hâ‚ƒ : cauchy_seq u) : âˆƒ v âˆˆ K, tendsto u at_top (ğ“ v) :=
hâ‚ _ hâ‚ƒ $ le_principal_iff.2 $ mem_map_iff_exists_image.2 âŸ¨univ, univ_mem,
  by { simp only [image_univ], rintros _ âŸ¨n, rflâŸ©, exact hâ‚‚ n }âŸ©

theorem cauchy.le_nhds_Lim [complete_space Î±] [nonempty Î±] {f : filter Î±} (hf : cauchy f) :
  f â‰¤ ğ“ (Lim f) :=
le_nhds_Lim (complete_space.complete hf)

theorem cauchy_seq.tendsto_lim [semilattice_sup Î²] [complete_space Î±] [nonempty Î±] {u : Î² â†’ Î±}
  (h : cauchy_seq u) :
  tendsto u at_top (ğ“ $ lim at_top u) :=
h.le_nhds_Lim

lemma is_closed.is_complete [complete_space Î±] {s : set Î±}
  (h : is_closed s) : is_complete s :=
Î» f cf fs, let âŸ¨x, hxâŸ© := complete_space.complete cf in
âŸ¨x, is_closed_iff_cluster_pt.mp h x (cf.left.mono (le_inf hx fs)), hxâŸ©

/-- A set `s` is totally bounded if for every entourage `d` there is a finite
  set of points `t` such that every element of `s` is `d`-near to some element of `t`. -/
def totally_bounded (s : set Î±) : Prop :=
âˆ€d âˆˆ ğ“¤ Î±, âˆƒt : set Î±, finite t âˆ§ s âŠ† (â‹ƒyâˆˆt, {x | (x,y) âˆˆ d})

theorem totally_bounded_iff_subset {s : set Î±} : totally_bounded s â†”
  âˆ€d âˆˆ ğ“¤ Î±, âˆƒt âŠ† s, finite t âˆ§ s âŠ† (â‹ƒyâˆˆt, {x | (x,y) âˆˆ d}) :=
âŸ¨Î» H d hd, begin
  rcases comp_symm_of_uniformity hd with âŸ¨r, hr, rs, rdâŸ©,
  rcases H r hr with âŸ¨k, fk, ksâŸ©,
  let u := k âˆ© {y | âˆƒ x âˆˆ s, (x, y) âˆˆ r},
  choose hk f hfs hfr using Î» x : u, x.coe_prop,
  refine âŸ¨range f, _, _, _âŸ©,
  { exact range_subset_iff.2 hfs },
  { haveI : fintype u := (fk.inter_of_left _).fintype,
    exact finite_range f },
  { intros x xs,
    obtain âŸ¨y, hy, xyâŸ© : âˆƒ y âˆˆ k, (x, y) âˆˆ r, from mem_bUnion_iff.1 (ks xs),
    rw [bUnion_range, mem_Union],
    set z : â†¥u := âŸ¨y, hy, âŸ¨x, xs, xyâŸ©âŸ©,
    exact âŸ¨z, rd $ mem_comp_rel.2 âŸ¨y, xy, rs (hfr z)âŸ©âŸ© }
end,
Î» H d hd, let âŸ¨t, _, htâŸ© := H d hd in âŸ¨t, htâŸ©âŸ©

lemma totally_bounded_of_forall_symm {s : set Î±}
  (h : âˆ€ V âˆˆ ğ“¤ Î±, symmetric_rel V â†’ âˆƒ t : set Î±, finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, ball y V) :
totally_bounded s :=
begin
  intros V V_in,
  rcases h _ (symmetrize_mem_uniformity V_in) (symmetric_symmetrize_rel V) with âŸ¨t, tfin, hâŸ©,
  refine âŸ¨t, tfin, subset.trans h _âŸ©,
  mono,
  intros x x_in z z_in,
  exact z_in.right
end

lemma totally_bounded_subset {sâ‚ sâ‚‚ : set Î±} (hs : sâ‚ âŠ† sâ‚‚)
  (h : totally_bounded sâ‚‚) : totally_bounded sâ‚ :=
assume d hd, let âŸ¨t, htâ‚, htâ‚‚âŸ© := h d hd in âŸ¨t, htâ‚, subset.trans hs htâ‚‚âŸ©

lemma totally_bounded_empty : totally_bounded (âˆ… : set Î±) :=
Î» d hd, âŸ¨âˆ…, finite_empty, empty_subset _âŸ©

/-- The closure of a totally bounded set is totally bounded. -/
lemma totally_bounded.closure {s : set Î±} (h : totally_bounded s) :
  totally_bounded (closure s) :=
assume t ht,
let âŸ¨t', ht', hct', htt'âŸ© := mem_uniformity_is_closed ht, âŸ¨c, hcf, hcâŸ© := h t' ht' in
âŸ¨c, hcf,
  calc closure s âŠ† closure (â‹ƒ (y : Î±) (H : y âˆˆ c), {x : Î± | (x, y) âˆˆ t'}) : closure_mono hc
    ... = _ : is_closed.closure_eq $ is_closed_bUnion hcf $ assume i hi,
      continuous_iff_is_closed.mp (continuous_id.prod_mk continuous_const) _ hct'
    ... âŠ† _ : bUnion_subset $ assume i hi, subset.trans (assume x, @htt' (x, i))
      (subset_bUnion_of_mem hi)âŸ©

/-- The image of a totally bounded set under a unifromly continuous map is totally bounded. -/
lemma totally_bounded.image [uniform_space Î²] {f : Î± â†’ Î²} {s : set Î±}
  (hs : totally_bounded s) (hf : uniform_continuous f) : totally_bounded (f '' s) :=
assume t ht,
have {p:Î±Ã—Î± | (f p.1, f p.2) âˆˆ t} âˆˆ ğ“¤ Î±,
  from hf ht,
let âŸ¨c, hfc, hctâŸ© := hs _ this in
âŸ¨f '' c, hfc.image f,
  begin
    simp [image_subset_iff],
    simp [subset_def] at hct,
    intros x hx, simp,
    exact hct x hx
  endâŸ©

lemma ultrafilter.cauchy_of_totally_bounded {s : set Î±} (f : ultrafilter Î±)
  (hs : totally_bounded s) (h : â†‘f â‰¤ ğ“Ÿ s) : cauchy (f : filter Î±) :=
âŸ¨f.ne_bot', assume t ht,
  let âŸ¨t', ht'â‚, ht'_symm, ht'_tâŸ© := comp_symm_of_uniformity ht in
  let âŸ¨i, hi, hs_unionâŸ© := hs t' ht'â‚ in
  have (â‹ƒyâˆˆi, {x | (x,y) âˆˆ t'}) âˆˆ f,
    from mem_of_superset (le_principal_iff.mp h) hs_union,
  have âˆƒyâˆˆi, {x | (x,y) âˆˆ t'} âˆˆ f,
    from (ultrafilter.finite_bUnion_mem_iff hi).1 this,
  let âŸ¨y, hy, hifâŸ© := this in
  have set.prod {x | (x,y) âˆˆ t'} {x | (x,y) âˆˆ t'} âŠ† comp_rel t' t',
    from assume âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨(hâ‚ : (xâ‚, y) âˆˆ t'), (hâ‚‚ : (xâ‚‚, y) âˆˆ t')âŸ©,
      âŸ¨y, hâ‚, ht'_symm hâ‚‚âŸ©,
  mem_of_superset (prod_mem_prod hif hif) (subset.trans this ht'_t)âŸ©

lemma totally_bounded_iff_filter {s : set Î±} :
  totally_bounded s â†” (âˆ€f, ne_bot f â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒc â‰¤ f, cauchy c) :=
begin
  split,
  { introsI H f hf hfs,
    exact âŸ¨ultrafilter.of f, ultrafilter.of_le f,
      (ultrafilter.of f).cauchy_of_totally_bounded H ((ultrafilter.of_le f).trans hfs)âŸ© },
  { intros H d hd,
    contrapose! H with hd_cover,
    set f := â¨… t : finset Î±, ğ“Ÿ (s \ â‹ƒ y âˆˆ t, {x | (x, y) âˆˆ d}),
    have : ne_bot f,
    { refine infi_ne_bot_of_directed' (directed_of_sup _) _,
      { intros tâ‚ tâ‚‚ h,
        exact principal_mono.2 (diff_subset_diff_right $ bUnion_subset_bUnion_left h) },
      { intro t,
        simpa [nonempty_diff] using hd_cover t t.finite_to_set } },
    have : f â‰¤ ğ“Ÿ s, from infi_le_of_le âˆ… (by simp),
    refine âŸ¨f, â€¹_â€º, â€¹_â€º, Î» c hcf hc, _âŸ©,
    rcases mem_prod_same_iff.1 (hc.2 hd) with âŸ¨m, hm, hmdâŸ©,
    have : m âˆ© s âˆˆ c, from inter_mem hm (le_principal_iff.mp (hcf.trans â€¹_â€º)),
    rcases hc.1.nonempty_of_mem this with âŸ¨y, hym, hysâŸ©,
    set ys := â‹ƒ y' âˆˆ ({y} : finset Î±), {x | (x, y') âˆˆ d},
    have : m âŠ† ys, by simpa [ys] using Î» x hx, hmd (mk_mem_prod hx hym),
    have : c â‰¤ ğ“Ÿ (s \ ys) := hcf.trans (infi_le_of_le {y} le_rfl),
    refine hc.1.ne (empty_mem_iff_bot.mp _),
    filter_upwards [le_principal_iff.1 this, hm],
    refine Î» x hx hxm, hx.2 _,
    simpa [ys] using hmd (mk_mem_prod hxm hym) }
end

lemma totally_bounded_iff_ultrafilter {s : set Î±} :
  totally_bounded s â†” (âˆ€f : ultrafilter Î±, â†‘f â‰¤ ğ“Ÿ s â†’ cauchy (f : filter Î±)) :=
begin
  refine âŸ¨Î» hs f, f.cauchy_of_totally_bounded hs, Î» H, totally_bounded_iff_filter.2 _âŸ©,
  introsI f hf hfs,
  exact âŸ¨ultrafilter.of f, ultrafilter.of_le f, H _ ((ultrafilter.of_le f).trans hfs)âŸ©
end

lemma compact_iff_totally_bounded_complete {s : set Î±} :
  is_compact s â†” totally_bounded s âˆ§ is_complete s :=
âŸ¨Î» hs, âŸ¨totally_bounded_iff_ultrafilter.2 (Î» f hf,
    let âŸ¨x, xs, fxâŸ© := is_compact_iff_ultrafilter_le_nhds.1 hs f hf in cauchy_nhds.mono fx),
  Î» f fc fs,
    let âŸ¨a, as, faâŸ© := @hs f fc.1 fs in
    âŸ¨a, as, le_nhds_of_cauchy_adhp fc faâŸ©âŸ©,
 Î» âŸ¨ht, hcâŸ©, is_compact_iff_ultrafilter_le_nhds.2
   (Î»f hf, hc _ (totally_bounded_iff_ultrafilter.1 ht f hf) hf)âŸ©

lemma is_compact.totally_bounded {s : set Î±} (h : is_compact s) : totally_bounded s :=
(compact_iff_totally_bounded_complete.1 h).1

lemma is_compact.is_complete {s : set Î±} (h : is_compact s) : is_complete s :=
(compact_iff_totally_bounded_complete.1 h).2

@[priority 100] -- see Note [lower instance priority]
instance complete_of_compact {Î± : Type u} [uniform_space Î±] [compact_space Î±] : complete_space Î± :=
âŸ¨Î»f hf, by simpa using (compact_iff_totally_bounded_complete.1 compact_univ).2 f hfâŸ©

lemma compact_of_totally_bounded_is_closed [complete_space Î±] {s : set Î±}
  (ht : totally_bounded s) (hc : is_closed s) : is_compact s :=
(@compact_iff_totally_bounded_complete Î± _ s).2 âŸ¨ht, hc.is_completeâŸ©

/-!
### Sequentially complete space

In this section we prove that a uniform space is complete provided that it is sequentially complete
(i.e., any Cauchy sequence converges) and its uniformity filter admits a countable generating set.
In particular, this applies to (e)metric spaces, see the files `topology/metric_space/emetric_space`
and `topology/metric_space/basic`.

More precisely, we assume that there is a sequence of entourages `U_n` such that any other
entourage includes one of `U_n`. Then any Cauchy filter `f` generates a decreasing sequence of
sets `s_n âˆˆ f` such that `s_n Ã— s_n âŠ† U_n`. Choose a sequence `x_nâˆˆs_n`. It is easy to show
that this is a Cauchy sequence. If this sequence converges to some `a`, then `f â‰¤ ğ“ a`. -/

namespace sequentially_complete

variables {f : filter Î±} (hf : cauchy f) {U : â„• â†’ set (Î± Ã— Î±)}
  (U_mem : âˆ€ n, U n âˆˆ ğ“¤ Î±) (U_le : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s)

open set finset

noncomputable theory

/-- An auxiliary sequence of sets approximating a Cauchy filter. -/
def set_seq_aux (n : â„•) : {s : set Î± // âˆƒ (_ : s âˆˆ f), s.prod s âŠ† U n } :=
indefinite_description _ $ (cauchy_iff.1 hf).2 (U n) (U_mem n)

/-- Given a Cauchy filter `f` and a sequence `U` of entourages, `set_seq` provides
an antitone sequence of sets `s n âˆˆ f` such that `(s n).prod (s n) âŠ† U`. -/
def set_seq (n : â„•) : set Î± :=  â‹‚ m âˆˆ Iic n, (set_seq_aux hf U_mem m).val

lemma set_seq_mem (n : â„•) : set_seq hf U_mem n âˆˆ f :=
(bInter_mem (finite_le_nat n)).2 (Î» m _, (set_seq_aux hf U_mem m).2.fst)

lemma set_seq_mono â¦ƒm n : â„•â¦„ (h : m â‰¤ n) : set_seq hf U_mem n âŠ† set_seq hf U_mem m :=
bInter_subset_bInter_left (Î» k hk, le_trans hk h)

lemma set_seq_sub_aux (n : â„•) : set_seq hf U_mem n âŠ† set_seq_aux hf U_mem n :=
bInter_subset_of_mem right_mem_Iic

lemma set_seq_prod_subset {N m n} (hm : N â‰¤ m) (hn : N â‰¤ n) :
  (set_seq hf U_mem m).prod (set_seq hf U_mem n) âŠ† U N :=
begin
  assume p hp,
  refine (set_seq_aux hf U_mem N).2.snd âŸ¨_, _âŸ©;
    apply set_seq_sub_aux,
  exact set_seq_mono hf U_mem hm hp.1,
  exact set_seq_mono hf U_mem hn hp.2
end

/-- A sequence of points such that `seq n âˆˆ set_seq n`. Here `set_seq` is an antitone
sequence of sets `set_seq n âˆˆ f` with diameters controlled by a given sequence
of entourages. -/
def seq (n : â„•) : Î± := some $ hf.1.nonempty_of_mem (set_seq_mem hf U_mem n)

lemma seq_mem (n : â„•) : seq hf U_mem n âˆˆ set_seq hf U_mem n :=
some_spec $ hf.1.nonempty_of_mem (set_seq_mem hf U_mem n)

lemma seq_pair_mem â¦ƒN m n : â„•â¦„ (hm : N â‰¤ m) (hn : N â‰¤ n) :
  (seq hf U_mem m, seq hf U_mem n) âˆˆ U N :=
set_seq_prod_subset hf U_mem hm hn âŸ¨seq_mem hf U_mem m, seq_mem hf U_mem nâŸ©

include U_le

theorem seq_is_cauchy_seq : cauchy_seq $ seq hf U_mem :=
cauchy_seq_of_controlled U U_le $ seq_pair_mem hf U_mem

/-- If the sequence `sequentially_complete.seq` converges to `a`, then `f â‰¤ ğ“ a`. -/
theorem le_nhds_of_seq_tendsto_nhds â¦ƒa : Î±â¦„ (ha : tendsto (seq hf U_mem) at_top (ğ“ a)) :
  f â‰¤ ğ“ a :=
le_nhds_of_cauchy_adhp_aux
begin
  assume s hs,
  rcases U_le s hs with âŸ¨m, hmâŸ©,
  rcases tendsto_at_top'.1 ha _ (mem_nhds_left a (U_mem m)) with âŸ¨n, hnâŸ©,
  refine âŸ¨set_seq hf U_mem (max m n), set_seq_mem hf U_mem _, _,
          seq hf U_mem (max m n), _, seq_mem hf U_mem _âŸ©,
  { have := le_max_left m n,
    exact set.subset.trans (set_seq_prod_subset hf U_mem this this) hm },
  { exact hm (hn _ $ le_max_right m n) }
end

end sequentially_complete

namespace uniform_space

open sequentially_complete

variables [is_countably_generated (ğ“¤ Î±)]

/-- A uniform space is complete provided that (a) its uniformity filter has a countable basis;
(b) any sequence satisfying a "controlled" version of the Cauchy condition converges. -/
theorem complete_of_convergent_controlled_sequences (U : â„• â†’ set (Î± Ã— Î±)) (U_mem : âˆ€ n, U n âˆˆ ğ“¤ Î±)
  (HU : âˆ€ u : â„• â†’ Î±, (âˆ€ N m n, N â‰¤ m â†’ N â‰¤ n â†’ (u m, u n) âˆˆ U N) â†’ âˆƒ a, tendsto u at_top (ğ“ a)) :
  complete_space Î± :=
begin
  obtain âŸ¨U', U'_mono, hU'âŸ© := (ğ“¤ Î±).exists_antitone_seq,
  have Hmem : âˆ€ n, U n âˆ© U' n âˆˆ ğ“¤ Î±,
    from Î» n, inter_mem (U_mem n) (hU'.2 âŸ¨n, subset.refl _âŸ©),
  refine âŸ¨Î» f hf, (HU (seq hf Hmem) (Î» N m n hm hn, _)).imp $
    le_nhds_of_seq_tendsto_nhds _ _ (Î» s hs, _)âŸ©,
  { rcases (hU'.1 hs) with âŸ¨N, hNâŸ©,
    exact âŸ¨N, subset.trans (inter_subset_right _ _) hNâŸ© },
  { exact inter_subset_left _ _ (seq_pair_mem hf Hmem hm hn) }
end

/-- A sequentially complete uniform space with a countable basis of the uniformity filter is
complete. -/
theorem complete_of_cauchy_seq_tendsto
  (H' : âˆ€ u : â„• â†’ Î±, cauchy_seq u â†’ âˆƒa, tendsto u at_top (ğ“ a)) :
  complete_space Î± :=
let âŸ¨U', U'_mono, hU'âŸ© := (ğ“¤ Î±).exists_antitone_seq in
complete_of_convergent_controlled_sequences U' (Î» n, hU'.2 âŸ¨n, subset.refl _âŸ©)
  (Î» u hu, H' u $ cauchy_seq_of_controlled U' (Î» s hs, hU'.1 hs) hu)

variable (Î±)

@[priority 100]
instance first_countable_topology : first_countable_topology Î± :=
âŸ¨Î» a, by { rw nhds_eq_comap_uniformity, apply_instance }âŸ©

/-- A separable uniform space with countably generated uniformity filter is second countable:
one obtains a countable basis by taking the balls centered at points in a dense subset,
and with rational "radii" from a countable open symmetric antitone basis of `ğ“¤ Î±`. We do not
register this as an instance, as there is already an instance going in the other direction
from second countable spaces to separable spaces, and we want to avoid loops. -/
lemma second_countable_of_separable [separable_space Î±] : second_countable_topology Î± :=
begin
  rcases exists_countable_dense Î± with âŸ¨s, hsc, hsdâŸ©,
  obtain âŸ¨t : â„• â†’ set (Î± Ã— Î±),
    hto : âˆ€ (i : â„•), t i âˆˆ (ğ“¤ Î±).sets âˆ§ is_open (t i) âˆ§ symmetric_rel (t i),
      h_basis : (ğ“¤ Î±).has_antitone_basis tâŸ© :=
    (@uniformity_has_basis_open_symmetric Î± _).exists_antitone_subbasis,
  choose ht_mem hto hts using hto,
  refine âŸ¨âŸ¨â‹ƒ (x âˆˆ s), range (Î» k, ball x (t k)), hsc.bUnion (Î» x hx, countable_range _), _âŸ©âŸ©,
  refine (is_topological_basis_of_open_of_nhds _ _).eq_generate_from,
  { simp only [mem_bUnion_iff, mem_range],
    rintros _ âŸ¨x, hxs, k, rflâŸ©,
    exact is_open_ball x (hto k) },
  { intros x V hxV hVo,
    simp only [mem_bUnion_iff, mem_range, exists_prop],
    rcases uniform_space.mem_nhds_iff.1 (is_open.mem_nhds hVo hxV) with âŸ¨U, hU, hUVâŸ©,
    rcases comp_symm_of_uniformity hU with âŸ¨U', hU', hsymm, hUU'âŸ©,
    rcases h_basis.to_has_basis.mem_iff.1 hU' with âŸ¨k, -, hkâŸ©,
    rcases hsd.inter_open_nonempty (ball x $ t k) (is_open_ball x (hto k))
      âŸ¨x, uniform_space.mem_ball_self _ (ht_mem k)âŸ© with âŸ¨y, hxy, hysâŸ©,
    refine âŸ¨_, âŸ¨y, hys, k, rflâŸ©, (hts k).subset hxy, Î» z hz, _âŸ©,
    exact hUV (ball_subset_of_comp_subset (hk hxy) hUU' (hk hz)) }
end

end uniform_space
