/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro

Theory of Cauchy filters in uniform spaces. Complete uniform spaces. Totally bounded subsets.
-/
import topology.uniform_space.basic

open filter topological_space lattice set classical
open_locale classical
variables {Î± : Type*} {Î² : Type*} [uniform_space Î±]
universe u

open_locale uniformity topological_space

/-- A filter `f` is Cauchy if for every entourage `r`, there exists an
  `s âˆˆ f` such that `s Ã— s âŠ† r`. This is a generalization of Cauchy
  sequences, because if `a : â„• â†’ Î±` then the filter of sets containing
  cofinitely many of the `a n` is Cauchy iff `a` is a Cauchy sequence. -/
def cauchy (f : filter Î±) := f â‰  âŠ¥ âˆ§ filter.prod f f â‰¤ (ğ“¤ Î±)

def is_complete (s : set Î±) := âˆ€f, cauchy f â†’ f â‰¤ principal s â†’ âˆƒxâˆˆs, f â‰¤ ğ“ x

lemma cauchy_iff {f : filter Î±} :
  cauchy f â†” (f â‰  âŠ¥ âˆ§ (âˆ€ s âˆˆ ğ“¤ Î±, âˆƒtâˆˆf.sets, set.prod t t âŠ† s)) :=
and_congr (iff.refl _) $ forall_congr $ assume s, forall_congr $ assume hs, mem_prod_same_iff

lemma cauchy_map_iff {l : filter Î²} {f : Î² â†’ Î±} :
  cauchy (l.map f) â†” (l â‰  âŠ¥ âˆ§ tendsto (Î»p:Î²Ã—Î², (f p.1, f p.2)) (l.prod l) (ğ“¤ Î±)) :=
by rw [cauchy, (â‰ ), map_eq_bot_iff, prod_map_map_eq]; refl

lemma cauchy_downwards {f g : filter Î±} (h_c : cauchy f) (hg : g â‰  âŠ¥) (h_le : g â‰¤ f) : cauchy g :=
âŸ¨hg, le_trans (filter.prod_mono h_le h_le) h_c.rightâŸ©

lemma cauchy_nhds {a : Î±} : cauchy (ğ“ a) :=
âŸ¨nhds_neq_bot,
  calc filter.prod (ğ“ a) (ğ“ a) =
    (ğ“¤ Î±).lift (Î»s:set (Î±Ã—Î±), (ğ“¤ Î±).lift' (Î»t:set(Î±Ã—Î±),
      set.prod {y : Î± | (y, a) âˆˆ s} {y : Î± | (a, y) âˆˆ t})) : nhds_nhds_eq_uniformity_uniformity_prod
    ... â‰¤ (ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), comp_rel s s) :
      le_infi $ assume s, le_infi $ assume hs,
      infi_le_of_le s $ infi_le_of_le hs $ infi_le_of_le s $ infi_le_of_le hs $
      principal_mono.mpr $
      assume âŸ¨x, yâŸ© âŸ¨(hx : (x, a) âˆˆ s), (hy : (a, y) âˆˆ s)âŸ©, âŸ¨a, hx, hyâŸ©
    ... â‰¤ ğ“¤ Î± : comp_le_uniformityâŸ©

lemma cauchy_pure {a : Î±} : cauchy (pure a) :=
cauchy_downwards cauchy_nhds
  (show principal {a} â‰  âŠ¥, by simp)
  (pure_le_nhds a)

lemma le_nhds_of_cauchy_adhp {f : filter Î±} {x : Î±} (hf : cauchy f)
  (adhs : f âŠ“ ğ“ x â‰  âŠ¥) : f â‰¤ ğ“ x :=
have âˆ€sâˆˆf.sets, x âˆˆ closure s,
begin
  intros s hs,
  simp [closure_eq_nhds, inf_comm],
  exact assume h', adhs $ bot_unique $ h' â–¸ inf_le_inf (by simp; exact hs) (le_refl _)
end,
calc f â‰¤ f.lift' (Î»s:set Î±, {y | x âˆˆ closure s âˆ§ y âˆˆ closure s}) :
    le_infi $ assume s, le_infi $ assume hs,
    begin
      rw [â†forall_sets_neq_empty_iff_neq_bot] at adhs,
      simp [this s hs],
      exact mem_sets_of_superset hs subset_closure
    end
  ... â‰¤ f.lift' (Î»s:set Î±, {y | (x, y) âˆˆ closure (set.prod s s)}) :
    by simp [closure_prod_eq]; exact le_refl _
  ... = (filter.prod f f).lift' (Î»s:set (Î±Ã—Î±), {y | (x, y) âˆˆ closure s}) :
  begin
    rw [prod_same_eq],
    rw [lift'_lift'_assoc],
    exact monotone_prod monotone_id monotone_id,
    exact monotone_preimage.comp (assume s t h x h', closure_mono h h')
  end
  ... â‰¤ (ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), {y | (x, y) âˆˆ closure s}) : lift'_mono hf.right (le_refl _)
  ... = ((ğ“¤ Î±).lift' closure).lift' (Î»s:set (Î±Ã—Î±), {y | (x, y) âˆˆ s}) :
  begin
    rw [lift'_lift'_assoc],
    exact assume s t h, closure_mono h,
    exact monotone_preimage
  end
  ... = (ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), {y | (x, y) âˆˆ s}) :
    by rw [â†uniformity_eq_uniformity_closure]
  ... = ğ“ x :
    by rw [nhds_eq_uniformity]

lemma le_nhds_iff_adhp_of_cauchy {f : filter Î±} {x : Î±} (hf : cauchy f) :
  f â‰¤ ğ“ x â†” f âŠ“ ğ“ x â‰  âŠ¥ :=
âŸ¨assume h, (inf_of_le_left h).symm â–¸ hf.left,
le_nhds_of_cauchy_adhp hfâŸ©

lemma cauchy_map [uniform_space Î²] {f : filter Î±} {m : Î± â†’ Î²}
  (hm : uniform_continuous m) (hf : cauchy f) : cauchy (map m f) :=
âŸ¨have f â‰  âŠ¥, from hf.left, by simp; assumption,
  calc filter.prod (map m f) (map m f) =
          map (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (filter.prod f f) : filter.prod_map_map_eq
    ... â‰¤ map (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î±) : map_mono hf.right
    ... â‰¤ ğ“¤ Î² : hmâŸ©

lemma cauchy_comap [uniform_space Î²] {f : filter Î²} {m : Î± â†’ Î²}
  (hm : comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î²) â‰¤ ğ“¤ Î±)
  (hf : cauchy f) (hb : comap m f â‰  âŠ¥) : cauchy (comap m f) :=
âŸ¨hb,
  calc filter.prod (comap m f) (comap m f) =
          comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (filter.prod f f) : filter.prod_comap_comap_eq
    ... â‰¤ comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î²) : comap_mono hf.right
    ... â‰¤ ğ“¤ Î± : hmâŸ©

/-- Cauchy sequences. Usually defined on â„•, but often it is also useful to say that a function
defined on â„ is Cauchy at +âˆ to deduce convergence. Therefore, we define it in a type class that
is general enough to cover both â„• and â„, which are the main motivating examples. -/
def cauchy_seq [semilattice_sup Î²] (u : Î² â†’ Î±) := cauchy (at_top.map u)

lemma cauchy_seq_iff_prod_map [inhabited Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” map (prod.map u u) at_top â‰¤ ğ“¤ Î± :=
iff.trans (and_iff_right (map_ne_bot at_top_ne_bot)) (prod_map_at_top_eq u u â–¸ iff.rfl)

/-- A complete space is defined here using uniformities. A uniform space
  is complete if every Cauchy filter converges. -/
class complete_space (Î± : Type u) [uniform_space Î±] : Prop :=
(complete : âˆ€{f:filter Î±}, cauchy f â†’ âˆƒx, f â‰¤ ğ“ x)

lemma complete_univ {Î± : Type u} [uniform_space Î±] [complete_space Î±] :
  is_complete (univ : set Î±) :=
begin
  assume f hf _,
  rcases complete_space.complete hf with âŸ¨x, hxâŸ©,
  exact âŸ¨x, by simp, hxâŸ©
end

lemma cauchy_prod [uniform_space Î²] {f : filter Î±} {g : filter Î²} :
  cauchy f â†’ cauchy g â†’ cauchy (filter.prod f g)
| âŸ¨f_proper, hfâŸ© âŸ¨g_proper, hgâŸ© := âŸ¨filter.prod_neq_bot.2 âŸ¨f_proper, g_properâŸ©,
  let p_Î± := Î»p:(Î±Ã—Î²)Ã—(Î±Ã—Î²), (p.1.1, p.2.1), p_Î² := Î»p:(Î±Ã—Î²)Ã—(Î±Ã—Î²), (p.1.2, p.2.2) in
  suffices (f.prod f).comap p_Î± âŠ“ (g.prod g).comap p_Î² â‰¤ (ğ“¤ Î±).comap p_Î± âŠ“ (ğ“¤ Î²).comap p_Î²,
    by simpa [uniformity_prod, filter.prod, filter.comap_inf, filter.comap_comap_comp, (âˆ˜),
        lattice.inf_assoc, lattice.inf_comm, lattice.inf_left_comm],
  lattice.inf_le_inf (filter.comap_mono hf) (filter.comap_mono hg)âŸ©

instance complete_space.prod [uniform_space Î²] [complete_space Î±] [complete_space Î²] :
  complete_space (Î± Ã— Î²) :=
{ complete := Î» f hf,
    let âŸ¨x1, hx1âŸ© := complete_space.complete $ cauchy_map uniform_continuous_fst hf in
    let âŸ¨x2, hx2âŸ© := complete_space.complete $ cauchy_map uniform_continuous_snd hf in
    âŸ¨(x1, x2), by rw [nhds_prod_eq, filter.prod_def];
      from filter.le_lift (Î» s hs, filter.le_lift' $ Î» t ht,
        have H1 : prod.fst â»Â¹' s âˆˆ f.sets := hx1 hs,
        have H2 : prod.snd â»Â¹' t âˆˆ f.sets := hx2 ht,
        filter.inter_mem_sets H1 H2)âŸ© }

/--If `univ` is complete, the space is a complete space -/
lemma complete_space_of_is_complete_univ (h : is_complete (univ : set Î±)) : complete_space Î± :=
âŸ¨Î» f hf, let âŸ¨x, _, hxâŸ© := h f hf ((@principal_univ Î±).symm â–¸ le_top) in âŸ¨x, hxâŸ©âŸ©

lemma cauchy_iff_exists_le_nhds [complete_space Î±] {l : filter Î±} (hl : l â‰  âŠ¥) :
  cauchy l â†” (âˆƒx, l â‰¤ ğ“ x) :=
âŸ¨complete_space.complete, assume âŸ¨x, hxâŸ©, cauchy_downwards cauchy_nhds hl hxâŸ©

lemma cauchy_map_iff_exists_tendsto [complete_space Î±] {l : filter Î²} {f : Î² â†’ Î±}
  (hl : l â‰  âŠ¥) : cauchy (l.map f) â†” (âˆƒx, tendsto f l (ğ“ x)) :=
cauchy_iff_exists_le_nhds (map_ne_bot hl)

/-- A Cauchy sequence in a complete space converges -/
theorem cauchy_seq_tendsto_of_complete [semilattice_sup Î²] [complete_space Î±]
  {u : Î² â†’ Î±} (H : cauchy_seq u) : âˆƒx, tendsto u at_top (ğ“ x) :=
complete_space.complete H

/-- If `K` is a complete subset, then any cauchy sequence in `K` converges to a point in `K` -/
lemma cauchy_seq_tendsto_of_is_complete [semilattice_sup Î²] {K : set Î±} (hâ‚ : is_complete K)
  {u : Î² â†’ Î±} (hâ‚‚ : âˆ€ n, u n âˆˆ K) (hâ‚ƒ : cauchy_seq u) : âˆƒ v âˆˆ K, tendsto u at_top (ğ“ v) :=
hâ‚ _ hâ‚ƒ $ le_principal_iff.2 $ mem_map_sets_iff.2 âŸ¨univ, univ_mem_sets,
  by { simp only [image_univ], rintros _ âŸ¨n, rflâŸ©, exact hâ‚‚ n }âŸ©

theorem le_nhds_lim_of_cauchy {Î±} [uniform_space Î±] [complete_space Î±]
  [inhabited Î±] {f : filter Î±} (hf : cauchy f) : f â‰¤ ğ“ (lim f) :=
lim_spec (complete_space.complete hf)

lemma is_complete_of_is_closed [complete_space Î±] {s : set Î±}
  (h : is_closed s) : is_complete s :=
Î» f cf fs, let âŸ¨x, hxâŸ© := complete_space.complete cf in
âŸ¨x, is_closed_iff_nhds.mp h x (neq_bot_of_le_neq_bot cf.left (le_inf hx fs)), hxâŸ©

/-- A set `s` is totally bounded if for every entourage `d` there is a finite
  set of points `t` such that every element of `s` is `d`-near to some element of `t`. -/
def totally_bounded (s : set Î±) : Prop :=
âˆ€d âˆˆ ğ“¤ Î±, âˆƒt : set Î±, finite t âˆ§ s âŠ† (â‹ƒyâˆˆt, {x | (x,y) âˆˆ d})

theorem totally_bounded_iff_subset {s : set Î±} : totally_bounded s â†”
  âˆ€d âˆˆ ğ“¤ Î±, âˆƒt âŠ† s, finite t âˆ§ s âŠ† (â‹ƒyâˆˆt, {x | (x,y) âˆˆ d}) :=
âŸ¨Î» H d hd, begin
  rcases comp_symm_of_uniformity hd with âŸ¨r, hr, rs, rdâŸ©,
  rcases H r hr with âŸ¨k, fk, ksâŸ©,
  let u := {y âˆˆ k | âˆƒ x, x âˆˆ s âˆ§ (x, y) âˆˆ r},
  let f : u â†’ Î± := Î» x, classical.some x.2.2,
  have : âˆ€ x : u, f x âˆˆ s âˆ§ (f x, x.1) âˆˆ r := Î» x, classical.some_spec x.2.2,
  refine âŸ¨range f, _, _, _âŸ©,
  { exact range_subset_iff.2 (Î» x, (this x).1) },
  { have : finite u := finite_subset fk (Î» x h, h.1),
    exact âŸ¨@set.fintype_range _ _ _ _ this.fintypeâŸ© },
  { intros x xs,
    have := ks xs, simp at this,
    rcases this with âŸ¨y, hy, xyâŸ©,
    let z : coe_sort u := âŸ¨y, hy, x, xs, xyâŸ©,
    exact mem_bUnion_iff.2 âŸ¨_, âŸ¨z, rflâŸ©, rd $ mem_comp_rel.2 âŸ¨_, xy, rs (this z).2âŸ©âŸ© }
end,
Î» H d hd, let âŸ¨t, _, htâŸ© := H d hd in âŸ¨t, htâŸ©âŸ©

lemma totally_bounded_subset {sâ‚ sâ‚‚ : set Î±} (hs : sâ‚ âŠ† sâ‚‚)
  (h : totally_bounded sâ‚‚) : totally_bounded sâ‚ :=
assume d hd, let âŸ¨t, htâ‚, htâ‚‚âŸ© := h d hd in âŸ¨t, htâ‚, subset.trans hs htâ‚‚âŸ©

lemma totally_bounded_empty : totally_bounded (âˆ… : set Î±) :=
Î» d hd, âŸ¨âˆ…, finite_empty, empty_subset _âŸ©

lemma totally_bounded_closure {s : set Î±} (h : totally_bounded s) :
  totally_bounded (closure s) :=
assume t ht,
let âŸ¨t', ht', hct', htt'âŸ© := mem_uniformity_is_closed ht, âŸ¨c, hcf, hcâŸ© := h t' ht' in
âŸ¨c, hcf,
  calc closure s âŠ† closure (â‹ƒ (y : Î±) (H : y âˆˆ c), {x : Î± | (x, y) âˆˆ t'}) : closure_mono hc
    ... = _ : closure_eq_of_is_closed $ is_closed_bUnion hcf $ assume i hi,
      continuous_iff_is_closed.mp (continuous_id.prod_mk continuous_const) _ hct'
    ... âŠ† _ : bUnion_subset $ assume i hi, subset.trans (assume x, @htt' (x, i))
      (subset_bUnion_of_mem hi)âŸ©

lemma totally_bounded_image [uniform_space Î²] {f : Î± â†’ Î²} {s : set Î±}
  (hf : uniform_continuous f) (hs : totally_bounded s) : totally_bounded (f '' s) :=
assume t ht,
have {p:Î±Ã—Î± | (f p.1, f p.2) âˆˆ t} âˆˆ ğ“¤ Î±,
  from hf ht,
let âŸ¨c, hfc, hctâŸ© := hs _ this in
âŸ¨f '' c, finite_image f hfc,
  begin
    simp [image_subset_iff],
    simp [subset_def] at hct,
    intros x hx, simp [-mem_image],
    exact let âŸ¨i, hi, htâŸ© := hct x hx in âŸ¨f i, mem_image_of_mem f hi, htâŸ©
  endâŸ©

lemma cauchy_of_totally_bounded_of_ultrafilter {s : set Î±} {f : filter Î±}
  (hs : totally_bounded s) (hf : is_ultrafilter f) (h : f â‰¤ principal s) : cauchy f :=
âŸ¨hf.left, assume t ht,
  let âŸ¨t', ht'â‚, ht'_symm, ht'_tâŸ© := comp_symm_of_uniformity ht in
  let âŸ¨i, hi, hs_unionâŸ© := hs t' ht'â‚ in
  have (â‹ƒyâˆˆi, {x | (x,y) âˆˆ t'}) âˆˆ f.sets,
    from mem_sets_of_superset (le_principal_iff.mp h) hs_union,
  have âˆƒyâˆˆi, {x | (x,y) âˆˆ t'} âˆˆ f.sets,
    from mem_of_finite_Union_ultrafilter hf hi this,
  let âŸ¨y, hy, hifâŸ© := this in
  have set.prod {x | (x,y) âˆˆ t'} {x | (x,y) âˆˆ t'} âŠ† comp_rel t' t',
    from assume âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨(hâ‚ : (xâ‚, y) âˆˆ t'), (hâ‚‚ : (xâ‚‚, y) âˆˆ t')âŸ©,
      âŸ¨y, hâ‚, ht'_symm hâ‚‚âŸ©,
  (filter.prod f f).sets_of_superset (prod_mem_prod hif hif) (subset.trans this ht'_t)âŸ©

lemma totally_bounded_iff_filter {s : set Î±} :
  totally_bounded s â†” (âˆ€f, f â‰  âŠ¥ â†’ f â‰¤ principal s â†’ âˆƒc â‰¤ f, cauchy c) :=
âŸ¨assume : totally_bounded s, assume f hf hs,
  âŸ¨ultrafilter_of f, ultrafilter_of_le,
    cauchy_of_totally_bounded_of_ultrafilter this
      (ultrafilter_ultrafilter_of hf) (le_trans ultrafilter_of_le hs)âŸ©,

  assume h : âˆ€f, f â‰  âŠ¥ â†’ f â‰¤ principal s â†’ âˆƒc â‰¤ f, cauchy c, assume d hd,
  classical.by_contradiction $ assume hs,
  have hd_cover : âˆ€{t:set Î±}, finite t â†’ Â¬ s âŠ† (â‹ƒyâˆˆt, {x | (x,y) âˆˆ d}),
    by simpa using hs,
  let
    f := â¨…t:{t : set Î± // finite t}, principal (s \ (â‹ƒyâˆˆt.val, {x | (x,y) âˆˆ d})),
    âŸ¨a, haâŸ© := @exists_mem_of_ne_empty Î± s
      (assume h, hd_cover finite_empty $ h.symm â–¸ empty_subset _)
  in
  have f â‰  âŠ¥,
    from infi_neq_bot_of_directed âŸ¨aâŸ©
      (assume âŸ¨tâ‚, htâ‚âŸ© âŸ¨tâ‚‚, htâ‚‚âŸ©, âŸ¨âŸ¨tâ‚ âˆª tâ‚‚, finite_union htâ‚ htâ‚‚âŸ©,
        principal_mono.mpr $ diff_subset_diff_right $ Union_subset_Union $
          assume t, Union_subset_Union_const or.inl,
        principal_mono.mpr $ diff_subset_diff_right $ Union_subset_Union $
          assume t, Union_subset_Union_const or.inrâŸ©)
      (assume âŸ¨t, htâŸ©, by simp [diff_eq_empty]; exact hd_cover ht),
  have f â‰¤ principal s, from infi_le_of_le âŸ¨âˆ…, finite_emptyâŸ© $ by simp; exact subset.refl s,
  let
    âŸ¨c, (hcâ‚ : c â‰¤ f), (hcâ‚‚ : cauchy c)âŸ© := h f â€¹f â‰  âŠ¥â€º this,
    âŸ¨m, hm, (hmd : set.prod m m âŠ† d)âŸ© := (@mem_prod_same_iff Î± c d).mp $ hcâ‚‚.right hd
  in
  have c â‰¤ principal s, from le_trans â€¹c â‰¤ fâ€º this,
  have m âˆ© s âˆˆ c.sets, from inter_mem_sets hm $ le_principal_iff.mp this,
  let âŸ¨y, hym, hysâŸ© := inhabited_of_mem_sets hcâ‚‚.left this in
  let ys := (â‹ƒy'âˆˆ({y}:set Î±), {x | (x, y') âˆˆ d}) in
  have m âŠ† ys,
    from assume y' hy',
      show  y' âˆˆ (â‹ƒy'âˆˆ({y}:set Î±), {x | (x, y') âˆˆ d}),
        by simp; exact @hmd (y', y) âŸ¨hy', hymâŸ©,
  have c â‰¤ principal (s - ys),
    from le_trans hcâ‚ $ infi_le_of_le âŸ¨{y}, finite_singleton _âŸ© $ le_refl _,
  have (s - ys) âˆ© (m âˆ© s) âˆˆ c.sets,
    from inter_mem_sets (le_principal_iff.mp this) â€¹m âˆ© s âˆˆ c.setsâ€º,
  have âˆ… âˆˆ c.sets,
    from c.sets_of_superset this $ assume x âŸ¨âŸ¨hxs, hxysâŸ©, hxm, _âŸ©, hxys $ â€¹m âŠ† ysâ€º hxm,
  hcâ‚‚.left $ empty_in_sets_eq_bot.mp thisâŸ©

lemma totally_bounded_iff_ultrafilter {s : set Î±} :
  totally_bounded s â†” (âˆ€f, is_ultrafilter f â†’ f â‰¤ principal s â†’ cauchy f) :=
âŸ¨assume hs f, cauchy_of_totally_bounded_of_ultrafilter hs,
  assume h, totally_bounded_iff_filter.mpr $ assume f hf hfs,
  have cauchy (ultrafilter_of f),
    from h (ultrafilter_of f) (ultrafilter_ultrafilter_of hf) (le_trans ultrafilter_of_le hfs),
  âŸ¨ultrafilter_of f, ultrafilter_of_le, thisâŸ©âŸ©

lemma compact_iff_totally_bounded_complete {s : set Î±} :
  compact s â†” totally_bounded s âˆ§ is_complete s :=
âŸ¨Î» hs, âŸ¨totally_bounded_iff_ultrafilter.2 (Î» f hf1 hf2,
    let âŸ¨x, xs, fxâŸ© := compact_iff_ultrafilter_le_nhds.1 hs f hf1 hf2 in
    cauchy_downwards (cauchy_nhds) (hf1.1) fx),
  Î» f fc fs,
    let âŸ¨a, as, faâŸ© := hs f fc.1 fs in
    âŸ¨a, as, le_nhds_of_cauchy_adhp fc faâŸ©âŸ©,
Î» âŸ¨ht, hcâŸ©, compact_iff_ultrafilter_le_nhds.2
  (Î»f hf hfs, hc _ (totally_bounded_iff_ultrafilter.1 ht _ hf hfs) hfs)âŸ©

instance complete_of_compact {Î± : Type u} [uniform_space Î±] [compact_space Î±] : complete_space Î± :=
âŸ¨Î»f hf, by simpa [principal_univ] using (compact_iff_totally_bounded_complete.1 compact_univ).2 f hfâŸ©

lemma compact_of_totally_bounded_is_closed [complete_space Î±] {s : set Î±}
  (ht : totally_bounded s) (hc : is_closed s) : compact s :=
(@compact_iff_totally_bounded_complete Î± _ s).2 âŸ¨ht, is_complete_of_is_closed hcâŸ©
