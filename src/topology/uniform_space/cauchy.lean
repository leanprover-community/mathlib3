/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import topology.uniform_space.basic
import topology.bases
import data.set.intervals
/-!
# Theory of Cauchy filters in uniform spaces. Complete uniform spaces. Totally bounded subsets.
-/
universes u v

open filter topological_space set classical uniform_space
open_locale classical uniformity topological_space filter

variables {Î± : Type u} {Î² : Type v} [uniform_space Î±]

/-- A filter `f` is Cauchy if for every entourage `r`, there exists an
  `s âˆˆ f` such that `s Ã— s âŠ† r`. This is a generalization of Cauchy
  sequences, because if `a : â„• â†’ Î±` then the filter of sets containing
  cofinitely many of the `a n` is Cauchy iff `a` is a Cauchy sequence. -/
def cauchy (f : filter Î±) := f â‰  âŠ¥ âˆ§ filter.prod f f â‰¤ (ğ“¤ Î±)

/-- A set `s` is called *complete*, if any Cauchy filter `f` such that `s âˆˆ f`
has a limit in `s` (formally, it satisfies `f â‰¤ ğ“ x` for some `x âˆˆ s`). -/
def is_complete (s : set Î±) := âˆ€f, cauchy f â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒxâˆˆs, f â‰¤ ğ“ x

lemma filter.has_basis.cauchy_iff {p : Î² â†’ Prop} {s : Î² â†’ set (Î± Ã— Î±)} (h : (ğ“¤ Î±).has_basis p s)
  {f : filter Î±} :
  cauchy f â†” (f â‰  âŠ¥ âˆ§ (âˆ€ i, p i â†’ âˆƒ t âˆˆ f, âˆ€ x y âˆˆ t, (x, y) âˆˆ s i)) :=
and_congr iff.rfl $ (f.basis_sets.prod_self.le_basis_iff h).trans $
  by simp only [subset_def, prod.forall, mem_prod_eq, and_imp, id]

lemma cauchy_iff' {f : filter Î±} :
  cauchy f â†” (f â‰  âŠ¥ âˆ§ (âˆ€ s âˆˆ ğ“¤ Î±, âˆƒtâˆˆf, âˆ€ x y âˆˆ t, (x, y) âˆˆ s)) :=
(ğ“¤ Î±).basis_sets.cauchy_iff

lemma cauchy_iff {f : filter Î±} :
  cauchy f â†” (f â‰  âŠ¥ âˆ§ (âˆ€ s âˆˆ ğ“¤ Î±, âˆƒtâˆˆf, (set.prod t t) âŠ† s)) :=
(ğ“¤ Î±).basis_sets.cauchy_iff.trans $
  by simp only [subset_def, prod.forall, mem_prod_eq, and_imp, id]

lemma cauchy_map_iff {l : filter Î²} {f : Î² â†’ Î±} :
  cauchy (l.map f) â†” (l â‰  âŠ¥ âˆ§ tendsto (Î»p:Î²Ã—Î², (f p.1, f p.2)) (l.prod l) (ğ“¤ Î±)) :=
by rw [cauchy, (â‰ ), map_eq_bot_iff, prod_map_map_eq]; refl

lemma cauchy_downwards {f g : filter Î±} (h_c : cauchy f) (hg : g â‰  âŠ¥) (h_le : g â‰¤ f) : cauchy g :=
âŸ¨hg, le_trans (filter.prod_mono h_le h_le) h_c.rightâŸ©

lemma cauchy_nhds {a : Î±} : cauchy (ğ“ a) :=
âŸ¨nhds_ne_bot,
  calc filter.prod (ğ“ a) (ğ“ a) =
    (ğ“¤ Î±).lift (Î»s:set (Î±Ã—Î±), (ğ“¤ Î±).lift' (Î»t:set(Î±Ã—Î±),
      set.prod {y : Î± | (y, a) âˆˆ s} {y : Î± | (a, y) âˆˆ t})) : nhds_nhds_eq_uniformity_uniformity_prod
    ... â‰¤ (ğ“¤ Î±).lift' (Î»s:set (Î±Ã—Î±), comp_rel s s) :
      le_infi $ assume s, le_infi $ assume hs,
      infi_le_of_le s $ infi_le_of_le hs $ infi_le_of_le s $ infi_le_of_le hs $
      principal_mono.mpr $
      assume âŸ¨x, yâŸ© âŸ¨(hx : (x, a) âˆˆ s), (hy : (a, y) âˆˆ s)âŸ©, âŸ¨a, hx, hyâŸ©
    ... â‰¤ ğ“¤ Î± : comp_le_uniformityâŸ©

lemma cauchy_pure {a : Î±} : cauchy (pure a) :=
cauchy_downwards cauchy_nhds pure_ne_bot (pure_le_nhds a)

/-- The common part of the proofs of `le_nhds_of_cauchy_adhp` and
`sequentially_complete.le_nhds_of_seq_tendsto_nhds`: if for any entourage `s`
one can choose a set `t âˆˆ f` of diameter `s` such that it contains a point `y`
with `(x, y) âˆˆ s`, then `f` converges to `x`. -/
lemma le_nhds_of_cauchy_adhp_aux {f : filter Î±} {x : Î±}
  (adhs : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ t âˆˆ f, (set.prod t t âŠ† s) âˆ§ âˆƒ y, (x, y) âˆˆ s âˆ§ y âˆˆ t) :
  f â‰¤ ğ“ x :=
begin
  -- Consider a neighborhood `s` of `x`
  assume s hs,
  -- Take an entourage twice smaller than `s`
  rcases comp_mem_uniformity_sets (mem_nhds_uniformity_iff_right.1 hs) with âŸ¨U, U_mem, hUâŸ©,
  -- Take a set `t âˆˆ f`, `t Ã— t âŠ† U`, and a point `y âˆˆ t` such that `(x, y) âˆˆ U`
  rcases adhs U U_mem with âŸ¨t, t_mem, ht, y, hxy, hyâŸ©,
  apply mem_sets_of_superset t_mem,
  -- Given a point `z âˆˆ t`, we have `(x, y) âˆˆ U` and `(y, z) âˆˆ t Ã— t âŠ† U`, hence `z âˆˆ s`
  exact (Î» z hz, hU (prod_mk_mem_comp_rel hxy (ht $ mk_mem_prod hy hz)) rfl)
end

/-- If `x` is an adherent (cluster) point for a Cauchy filter `f`, then it is a limit point
for `f`. -/
lemma le_nhds_of_cauchy_adhp {f : filter Î±} {x : Î±} (hf : cauchy f)
  (adhs : cluster_pt x f) : f â‰¤ ğ“ x :=
le_nhds_of_cauchy_adhp_aux
begin
  assume s hs,
  obtain âŸ¨t, t_mem, htâŸ© : âˆƒ (t : set Î±) (h : t âˆˆ f), t.prod t âŠ† s,
    from (cauchy_iff.1 hf).2 s hs,
  use [t, t_mem, ht],
  exact (forall_sets_nonempty_iff_ne_bot.2 adhs _
    (inter_mem_inf_sets (mem_nhds_left x hs) t_mem ))
end

lemma le_nhds_iff_adhp_of_cauchy {f : filter Î±} {x : Î±} (hf : cauchy f) :
  f â‰¤ ğ“ x â†” cluster_pt x f :=
âŸ¨assume h, cluster_pt.of_le_nhds h hf.1, le_nhds_of_cauchy_adhp hfâŸ©

lemma cauchy_map [uniform_space Î²] {f : filter Î±} {m : Î± â†’ Î²}
  (hm : uniform_continuous m) (hf : cauchy f) : cauchy (map m f) :=
âŸ¨have f â‰  âŠ¥, from hf.left, by simp; assumption,
  calc filter.prod (map m f) (map m f) =
          map (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (filter.prod f f) : filter.prod_map_map_eq
    ... â‰¤ map (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î±) : map_mono hf.right
    ... â‰¤ ğ“¤ Î² : hmâŸ©

lemma cauchy_comap [uniform_space Î²] {f : filter Î²} {m : Î± â†’ Î²}
  (hm : comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î²) â‰¤ ğ“¤ Î±)
  (hf : cauchy f) (hb : comap m f â‰  âŠ¥) : cauchy (comap m f) :=
âŸ¨hb,
  calc filter.prod (comap m f) (comap m f) =
          comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (filter.prod f f) : filter.prod_comap_comap_eq
    ... â‰¤ comap (Î»p:Î±Ã—Î±, (m p.1, m p.2)) (ğ“¤ Î²) : comap_mono hf.right
    ... â‰¤ ğ“¤ Î± : hmâŸ©

/-- Cauchy sequences. Usually defined on â„•, but often it is also useful to say that a function
defined on â„ is Cauchy at +âˆ to deduce convergence. Therefore, we define it in a type class that
is general enough to cover both â„• and â„, which are the main motivating examples. -/
def cauchy_seq [semilattice_sup Î²] (u : Î² â†’ Î±) := cauchy (at_top.map u)

lemma cauchy_seq.mem_entourage {Î¹ : Type*} [nonempty Î¹] [decidable_linear_order Î¹] {u : Î¹ â†’ Î±}
(h : cauchy_seq u) {V : set (Î± Ã— Î±)} (hV : V âˆˆ ğ“¤ Î±) : âˆƒ kâ‚€, âˆ€ i j, kâ‚€ â‰¤ i â†’ kâ‚€ â‰¤ j â†’ (u i, u j) âˆˆ V :=
begin
  have := h.right hV,
  obtain âŸ¨âŸ¨iâ‚€, jâ‚€âŸ©, HâŸ© : âˆƒ a, âˆ€ b : Î¹ Ã— Î¹, b â‰¥ a â†’ prod.map u u b âˆˆ V,
    by rwa [prod_map_at_top_eq, mem_map, mem_at_top_sets] at this,
  refine âŸ¨max iâ‚€ jâ‚€, _âŸ©,
  intros i j hi hj,
  exact H (i, j) âŸ¨le_of_max_le_left  hi, le_of_max_le_right hjâŸ©,
end

lemma cauchy_seq_of_tendsto_nhds [semilattice_sup Î²] [nonempty Î²] (f : Î² â†’ Î±) {x}
  (hx : tendsto f at_top (ğ“ x)) :
  cauchy_seq f :=
cauchy_downwards cauchy_nhds (map_ne_bot at_top_ne_bot) hx

lemma cauchy_seq_iff_tendsto [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±} :
  cauchy_seq u â†” tendsto (prod.map u u) at_top (ğ“¤ Î±) :=
cauchy_map_iff.trans $ (and_iff_right at_top_ne_bot).trans $
  by simp only [prod_at_top_at_top_eq, prod.map_def]

/-- If a Cauchy sequence has a convergent subsequence, then it converges. -/
lemma tendsto_nhds_of_cauchy_seq_of_subseq
  [semilattice_sup Î²] {u : Î² â†’ Î±} (hu : cauchy_seq u)
  {Î¹ : Type*} {f : Î¹ â†’ Î²} {p : filter Î¹} (hp : p â‰  âŠ¥)
  (hf : tendsto f p at_top) {a : Î±} (ha : tendsto (u âˆ˜ f) p (ğ“ a)) :
  tendsto u at_top (ğ“ a) :=
le_nhds_of_cauchy_adhp hu (map_cluster_pt_of_comp hp hf ha)

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma filter.has_basis.cauchy_seq_iff {Î³} [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±}
  {p : Î³ â†’ Prop} {s : Î³ â†’ set (Î± Ã— Î±)} (h : (ğ“¤ Î±).has_basis p s) :
  cauchy_seq u â†” âˆ€ i, p i â†’ âˆƒN, âˆ€m nâ‰¥N, (u m, u n) âˆˆ s i :=
begin
  rw [cauchy_seq_iff_tendsto, â† prod_at_top_at_top_eq],
  refine (at_top_basis.prod_self.tendsto_iff h).trans _,
  simp only [exists_prop, true_and, maps_to, preimage, subset_def, prod.forall,
    mem_prod_eq, mem_set_of_eq, mem_Ici, and_imp, prod.map]
end

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma filter.has_basis.cauchy_seq_iff' {Î³} [nonempty Î²] [semilattice_sup Î²] {u : Î² â†’ Î±}
  {p : Î³ â†’ Prop} {s : Î³ â†’ set (Î± Ã— Î±)} (H : (ğ“¤ Î±).has_basis p s) :
  cauchy_seq u â†” âˆ€ i, p i â†’ âˆƒN, âˆ€nâ‰¥N, (u n, u N) âˆˆ s i :=
begin
  refine H.cauchy_seq_iff.trans âŸ¨Î» h i hi, _, Î» h i hi, _âŸ©,
  { exact (h i hi).imp (Î» N hN n hn, hN n N hn (le_refl N)) },
  { rcases comp_symm_of_uniformity (H.mem_of_mem hi) with âŸ¨t, ht, ht', htsâŸ©,
    rcases H.mem_iff.1 ht with âŸ¨j, hj, hjtâŸ©,
    refine (h j hj).imp (Î» N hN m n hm hn, hts âŸ¨u N, hjt _, ht' $ hjt _âŸ©),
    { exact hN m hm },
    { exact hN n hn } }
end

lemma cauchy_seq_of_controlled [semilattice_sup Î²] [nonempty Î²]
  (U : Î² â†’ set (Î± Ã— Î±)) (hU : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s)
  {f : Î² â†’ Î±} (hf : âˆ€ {N m n : Î²}, N â‰¤ m â†’ N â‰¤ n â†’ (f m, f n) âˆˆ U N) :
  cauchy_seq f :=
cauchy_seq_iff_tendsto.2
begin
  assume s hs,
  rw [mem_map, mem_at_top_sets],
  cases hU s hs with N hN,
  refine âŸ¨(N, N), Î» mn hmn, _âŸ©,
  cases mn with m n,
  exact hN (hf hmn.1 hmn.2)
end

/-- A complete space is defined here using uniformities. A uniform space
  is complete if every Cauchy filter converges. -/
class complete_space (Î± : Type u) [uniform_space Î±] : Prop :=
(complete : âˆ€{f:filter Î±}, cauchy f â†’ âˆƒx, f â‰¤ ğ“ x)

lemma complete_univ {Î± : Type u} [uniform_space Î±] [complete_space Î±] :
  is_complete (univ : set Î±) :=
begin
  assume f hf _,
  rcases complete_space.complete hf with âŸ¨x, hxâŸ©,
  exact âŸ¨x, mem_univ x, hxâŸ©
end

lemma cauchy_prod [uniform_space Î²] {f : filter Î±} {g : filter Î²} :
  cauchy f â†’ cauchy g â†’ cauchy (filter.prod f g)
| âŸ¨f_proper, hfâŸ© âŸ¨g_proper, hgâŸ© := âŸ¨filter.prod_ne_bot.2 âŸ¨f_proper, g_properâŸ©,
  let p_Î± := Î»p:(Î±Ã—Î²)Ã—(Î±Ã—Î²), (p.1.1, p.2.1), p_Î² := Î»p:(Î±Ã—Î²)Ã—(Î±Ã—Î²), (p.1.2, p.2.2) in
  suffices (f.prod f).comap p_Î± âŠ“ (g.prod g).comap p_Î² â‰¤ (ğ“¤ Î±).comap p_Î± âŠ“ (ğ“¤ Î²).comap p_Î²,
    by simpa [uniformity_prod, filter.prod, filter.comap_inf, filter.comap_comap_comp, (âˆ˜),
        inf_assoc, inf_comm, inf_left_comm],
  inf_le_inf (filter.comap_mono hf) (filter.comap_mono hg)âŸ©

instance complete_space.prod [uniform_space Î²] [complete_space Î±] [complete_space Î²] :
  complete_space (Î± Ã— Î²) :=
{ complete := Î» f hf,
    let âŸ¨x1, hx1âŸ© := complete_space.complete $ cauchy_map uniform_continuous_fst hf in
    let âŸ¨x2, hx2âŸ© := complete_space.complete $ cauchy_map uniform_continuous_snd hf in
    âŸ¨(x1, x2), by rw [nhds_prod_eq, filter.prod_def];
      from filter.le_lift (Î» s hs, filter.le_lift' $ Î» t ht,
        have H1 : prod.fst â»Â¹' s âˆˆ f.sets := hx1 hs,
        have H2 : prod.snd â»Â¹' t âˆˆ f.sets := hx2 ht,
        filter.inter_mem_sets H1 H2)âŸ© }

/--If `univ` is complete, the space is a complete space -/
lemma complete_space_of_is_complete_univ (h : is_complete (univ : set Î±)) : complete_space Î± :=
âŸ¨Î» f hf, let âŸ¨x, _, hxâŸ© := h f hf ((@principal_univ Î±).symm â–¸ le_top) in âŸ¨x, hxâŸ©âŸ©

lemma complete_space_iff_is_complete_univ :
  complete_space Î± â†” is_complete (univ : set Î±) :=
âŸ¨@complete_univ Î± _, complete_space_of_is_complete_univâŸ©

lemma cauchy_iff_exists_le_nhds [complete_space Î±] {l : filter Î±} (hl : l â‰  âŠ¥) :
  cauchy l â†” (âˆƒx, l â‰¤ ğ“ x) :=
âŸ¨complete_space.complete, assume âŸ¨x, hxâŸ©, cauchy_downwards cauchy_nhds hl hxâŸ©

lemma cauchy_map_iff_exists_tendsto [complete_space Î±] {l : filter Î²} {f : Î² â†’ Î±}
  (hl : l â‰  âŠ¥) : cauchy (l.map f) â†” (âˆƒx, tendsto f l (ğ“ x)) :=
cauchy_iff_exists_le_nhds (map_ne_bot hl)

/-- A Cauchy sequence in a complete space converges -/
theorem cauchy_seq_tendsto_of_complete [semilattice_sup Î²] [complete_space Î±]
  {u : Î² â†’ Î±} (H : cauchy_seq u) : âˆƒx, tendsto u at_top (ğ“ x) :=
complete_space.complete H

/-- If `K` is a complete subset, then any cauchy sequence in `K` converges to a point in `K` -/
lemma cauchy_seq_tendsto_of_is_complete [semilattice_sup Î²] {K : set Î±} (hâ‚ : is_complete K)
  {u : Î² â†’ Î±} (hâ‚‚ : âˆ€ n, u n âˆˆ K) (hâ‚ƒ : cauchy_seq u) : âˆƒ v âˆˆ K, tendsto u at_top (ğ“ v) :=
hâ‚ _ hâ‚ƒ $ le_principal_iff.2 $ mem_map_sets_iff.2 âŸ¨univ, univ_mem_sets,
  by { simp only [image_univ], rintros _ âŸ¨n, rflâŸ©, exact hâ‚‚ n }âŸ©

theorem cauchy.le_nhds_Lim [complete_space Î±] [nonempty Î±] {f : filter Î±} (hf : cauchy f) :
  f â‰¤ ğ“ (Lim f) :=
Lim_spec (complete_space.complete hf)

theorem cauchy_seq.tendsto_lim [semilattice_sup Î²] [complete_space Î±] [nonempty Î±] {u : Î² â†’ Î±}
  (h : cauchy_seq u) :
  tendsto u at_top (ğ“ $ lim at_top u) :=
h.le_nhds_Lim

lemma is_complete_of_is_closed [complete_space Î±] {s : set Î±}
  (h : is_closed s) : is_complete s :=
Î» f cf fs, let âŸ¨x, hxâŸ© := complete_space.complete cf in
âŸ¨x, is_closed_iff_nhds.mp h x (ne_bot_of_le_ne_bot cf.left (le_inf hx fs)), hxâŸ©

/-- A set `s` is totally bounded if for every entourage `d` there is a finite
  set of points `t` such that every element of `s` is `d`-near to some element of `t`. -/
def totally_bounded (s : set Î±) : Prop :=
âˆ€d âˆˆ ğ“¤ Î±, âˆƒt : set Î±, finite t âˆ§ s âŠ† (â‹ƒyâˆˆt, {x | (x,y) âˆˆ d})

theorem totally_bounded_iff_subset {s : set Î±} : totally_bounded s â†”
  âˆ€d âˆˆ ğ“¤ Î±, âˆƒt âŠ† s, finite t âˆ§ s âŠ† (â‹ƒyâˆˆt, {x | (x,y) âˆˆ d}) :=
âŸ¨Î» H d hd, begin
  rcases comp_symm_of_uniformity hd with âŸ¨r, hr, rs, rdâŸ©,
  rcases H r hr with âŸ¨k, fk, ksâŸ©,
  let u := {y âˆˆ k | âˆƒ x, x âˆˆ s âˆ§ (x, y) âˆˆ r},
  let f : u â†’ Î± := Î» x, classical.some x.2.2,
  have : âˆ€ x : u, f x âˆˆ s âˆ§ (f x, x.1) âˆˆ r := Î» x, classical.some_spec x.2.2,
  refine âŸ¨range f, _, _, _âŸ©,
  { exact range_subset_iff.2 (Î» x, (this x).1) },
  { have : finite u := fk.subset (Î» x h, h.1),
    exact âŸ¨@set.fintype_range _ _ _ _ this.fintypeâŸ© },
  { intros x xs,
    have := ks xs, simp at this,
    rcases this with âŸ¨y, hy, xyâŸ©,
    let z : coe_sort u := âŸ¨y, hy, x, xs, xyâŸ©,
    exact mem_bUnion_iff.2 âŸ¨_, âŸ¨z, rflâŸ©, rd $ mem_comp_rel.2 âŸ¨_, xy, rs (this z).2âŸ©âŸ© }
end,
Î» H d hd, let âŸ¨t, _, htâŸ© := H d hd in âŸ¨t, htâŸ©âŸ©

lemma totally_bounded_of_forall_symm {s : set Î±}
  (h : âˆ€ V âˆˆ ğ“¤ Î±, symmetric_rel V â†’ âˆƒ t : set Î±, finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, ball y V) :
totally_bounded s :=
begin
  intros V V_in,
  rcases h _ (symmetrize_mem_uniformity V_in) (symmetric_symmetrize_rel V) with âŸ¨t, tfin, hâŸ©,
  refine âŸ¨t, tfin, subset.trans h _âŸ©,
  mono,
  intros x x_in z z_in,
  exact z_in.right
end

lemma totally_bounded_subset {sâ‚ sâ‚‚ : set Î±} (hs : sâ‚ âŠ† sâ‚‚)
  (h : totally_bounded sâ‚‚) : totally_bounded sâ‚ :=
assume d hd, let âŸ¨t, htâ‚, htâ‚‚âŸ© := h d hd in âŸ¨t, htâ‚, subset.trans hs htâ‚‚âŸ©

lemma totally_bounded_empty : totally_bounded (âˆ… : set Î±) :=
Î» d hd, âŸ¨âˆ…, finite_empty, empty_subset _âŸ©

lemma totally_bounded_closure {s : set Î±} (h : totally_bounded s) :
  totally_bounded (closure s) :=
assume t ht,
let âŸ¨t', ht', hct', htt'âŸ© := mem_uniformity_is_closed ht, âŸ¨c, hcf, hcâŸ© := h t' ht' in
âŸ¨c, hcf,
  calc closure s âŠ† closure (â‹ƒ (y : Î±) (H : y âˆˆ c), {x : Î± | (x, y) âˆˆ t'}) : closure_mono hc
    ... = _ : closure_eq_of_is_closed $ is_closed_bUnion hcf $ assume i hi,
      continuous_iff_is_closed.mp (continuous_id.prod_mk continuous_const) _ hct'
    ... âŠ† _ : bUnion_subset $ assume i hi, subset.trans (assume x, @htt' (x, i))
      (subset_bUnion_of_mem hi)âŸ©

lemma totally_bounded_image [uniform_space Î²] {f : Î± â†’ Î²} {s : set Î±}
  (hf : uniform_continuous f) (hs : totally_bounded s) : totally_bounded (f '' s) :=
assume t ht,
have {p:Î±Ã—Î± | (f p.1, f p.2) âˆˆ t} âˆˆ ğ“¤ Î±,
  from hf ht,
let âŸ¨c, hfc, hctâŸ© := hs _ this in
âŸ¨f '' c, hfc.image f,
  begin
    simp [image_subset_iff],
    simp [subset_def] at hct,
    intros x hx, simp,
    exact hct x hx
  endâŸ©

lemma cauchy_of_totally_bounded_of_ultrafilter {s : set Î±} {f : filter Î±}
  (hs : totally_bounded s) (hf : is_ultrafilter f) (h : f â‰¤ ğ“Ÿ s) : cauchy f :=
âŸ¨hf.left, assume t ht,
  let âŸ¨t', ht'â‚, ht'_symm, ht'_tâŸ© := comp_symm_of_uniformity ht in
  let âŸ¨i, hi, hs_unionâŸ© := hs t' ht'â‚ in
  have (â‹ƒyâˆˆi, {x | (x,y) âˆˆ t'}) âˆˆ f.sets,
    from mem_sets_of_superset (le_principal_iff.mp h) hs_union,
  have âˆƒyâˆˆi, {x | (x,y) âˆˆ t'} âˆˆ f.sets,
    from mem_of_finite_Union_ultrafilter hf hi this,
  let âŸ¨y, hy, hifâŸ© := this in
  have set.prod {x | (x,y) âˆˆ t'} {x | (x,y) âˆˆ t'} âŠ† comp_rel t' t',
    from assume âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨(hâ‚ : (xâ‚, y) âˆˆ t'), (hâ‚‚ : (xâ‚‚, y) âˆˆ t')âŸ©,
      âŸ¨y, hâ‚, ht'_symm hâ‚‚âŸ©,
  (filter.prod f f).sets_of_superset (prod_mem_prod hif hif) (subset.trans this ht'_t)âŸ©

lemma totally_bounded_iff_filter {s : set Î±} :
  totally_bounded s â†” (âˆ€f, f â‰  âŠ¥ â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒc â‰¤ f, cauchy c) :=
âŸ¨assume : totally_bounded s, assume f hf hs,
  âŸ¨ultrafilter_of f, ultrafilter_of_le,
    cauchy_of_totally_bounded_of_ultrafilter this
      (ultrafilter_ultrafilter_of hf) (le_trans ultrafilter_of_le hs)âŸ©,

  assume h : âˆ€f, f â‰  âŠ¥ â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒc â‰¤ f, cauchy c, assume d hd,
  classical.by_contradiction $ assume hs,
  have hd_cover : âˆ€{t:set Î±}, finite t â†’ Â¬ s âŠ† (â‹ƒyâˆˆt, {x | (x,y) âˆˆ d}),
    by simpa using hs,
  let
    f := â¨…t:{t : set Î± // finite t}, ğ“Ÿ (s \ (â‹ƒyâˆˆt.val, {x | (x,y) âˆˆ d})),
    âŸ¨a, haâŸ© := (@ne_empty_iff_nonempty Î± s).1
      (assume h, hd_cover finite_empty $ h.symm â–¸ empty_subset _)
  in
  have f â‰  âŠ¥,
    from infi_ne_bot_of_directed âŸ¨aâŸ©
      (assume âŸ¨tâ‚, htâ‚âŸ© âŸ¨tâ‚‚, htâ‚‚âŸ©, âŸ¨âŸ¨tâ‚ âˆª tâ‚‚, htâ‚.union htâ‚‚âŸ©,
        principal_mono.mpr $ diff_subset_diff_right $ Union_subset_Union $
          assume t, Union_subset_Union_const or.inl,
        principal_mono.mpr $ diff_subset_diff_right $ Union_subset_Union $
          assume t, Union_subset_Union_const or.inrâŸ©)
      (assume âŸ¨t, htâŸ©, by simp [diff_eq_empty]; exact hd_cover ht),
  have f â‰¤ ğ“Ÿ s, from infi_le_of_le âŸ¨âˆ…, finite_emptyâŸ© $ by simp; exact subset.refl s,
  let
    âŸ¨c, (hcâ‚ : c â‰¤ f), (hcâ‚‚ : cauchy c)âŸ© := h f â€¹f â‰  âŠ¥â€º this,
    âŸ¨m, hm, (hmd : set.prod m m âŠ† d)âŸ© := (@mem_prod_same_iff Î± c d).mp $ hcâ‚‚.right hd
  in
  have c â‰¤ ğ“Ÿ s, from le_trans â€¹c â‰¤ fâ€º this,
  have m âˆ© s âˆˆ c.sets, from inter_mem_sets hm $ le_principal_iff.mp this,
  let âŸ¨y, hym, hysâŸ© := nonempty_of_mem_sets hcâ‚‚.left this in
  let ys := (â‹ƒy'âˆˆ({y}:set Î±), {x | (x, y') âˆˆ d}) in
  have m âŠ† ys,
    from assume y' hy',
      show  y' âˆˆ (â‹ƒy'âˆˆ({y}:set Î±), {x | (x, y') âˆˆ d}),
        by simp; exact @hmd (y', y) âŸ¨hy', hymâŸ©,
  have c â‰¤ ğ“Ÿ (s \ ys),
    from le_trans hcâ‚ $ infi_le_of_le âŸ¨{y}, finite_singleton _âŸ© $ le_refl _,
  have (s \ ys) âˆ© (m âˆ© s) âˆˆ c.sets,
    from inter_mem_sets (le_principal_iff.mp this) â€¹m âˆ© s âˆˆ c.setsâ€º,
  have âˆ… âˆˆ c.sets,
    from c.sets_of_superset this $ assume x âŸ¨âŸ¨hxs, hxysâŸ©, hxm, _âŸ©, hxys $ â€¹m âŠ† ysâ€º hxm,
  hcâ‚‚.left $ empty_in_sets_eq_bot.mp thisâŸ©

lemma totally_bounded_iff_ultrafilter {s : set Î±} :
  totally_bounded s â†” (âˆ€f, is_ultrafilter f â†’ f â‰¤ ğ“Ÿ s â†’ cauchy f) :=
âŸ¨assume hs f, cauchy_of_totally_bounded_of_ultrafilter hs,
  assume h, totally_bounded_iff_filter.mpr $ assume f hf hfs,
  have cauchy (ultrafilter_of f),
    from h (ultrafilter_of f) (ultrafilter_ultrafilter_of hf) (le_trans ultrafilter_of_le hfs),
  âŸ¨ultrafilter_of f, ultrafilter_of_le, thisâŸ©âŸ©

lemma compact_iff_totally_bounded_complete {s : set Î±} :
  compact s â†” totally_bounded s âˆ§ is_complete s :=
âŸ¨Î» hs, âŸ¨totally_bounded_iff_ultrafilter.2 (Î» f hf1 hf2,
    let âŸ¨x, xs, fxâŸ© := compact_iff_ultrafilter_le_nhds.1 hs f hf1 hf2 in
    cauchy_downwards (cauchy_nhds) (hf1.1) fx),
  Î» f fc fs,
    let âŸ¨a, as, faâŸ© := hs f fc.1 fs in
    âŸ¨a, as, le_nhds_of_cauchy_adhp fc faâŸ©âŸ©,
Î» âŸ¨ht, hcâŸ©, compact_iff_ultrafilter_le_nhds.2
  (Î»f hf hfs, hc _ (totally_bounded_iff_ultrafilter.1 ht _ hf hfs) hfs)âŸ©

@[priority 100] -- see Note [lower instance priority]
instance complete_of_compact {Î± : Type u} [uniform_space Î±] [compact_space Î±] : complete_space Î± :=
âŸ¨Î»f hf, by simpa [principal_univ] using (compact_iff_totally_bounded_complete.1 compact_univ).2 f hfâŸ©

lemma compact_of_totally_bounded_is_closed [complete_space Î±] {s : set Î±}
  (ht : totally_bounded s) (hc : is_closed s) : compact s :=
(@compact_iff_totally_bounded_complete Î± _ s).2 âŸ¨ht, is_complete_of_is_closed hcâŸ©

/-!
### Sequentially complete space

In this section we prove that a uniform space is complete provided that it is sequentially complete
(i.e., any Cauchy sequence converges) and its uniformity filter admits a countable generating set.
In particular, this applies to (e)metric spaces, see the files `topology/metric_space/emetric_space`
and `topology/metric_space/basic`.

More precisely, we assume that there is a sequence of entourages `U_n` such that any other
entourage includes one of `U_n`. Then any Cauchy filter `f` generates a decreasing sequence of
sets `s_n âˆˆ f` such that `s_n Ã— s_n âŠ† U_n`. Choose a sequence `x_nâˆˆs_n`. It is easy to show
that this is a Cauchy sequence. If this sequence converges to some `a`, then `f â‰¤ ğ“ a`. -/

namespace sequentially_complete

variables {f : filter Î±} (hf : cauchy f) {U : â„• â†’ set (Î± Ã— Î±)}
  (U_mem : âˆ€ n, U n âˆˆ ğ“¤ Î±) (U_le : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s)

open set finset

noncomputable theory

/-- An auxiliary sequence of sets approximating a Cauchy filter. -/
def set_seq_aux (n : â„•) : {s : set Î± // âˆƒ (_ : s âˆˆ f), s.prod s âŠ† U n } :=
indefinite_description _ $ (cauchy_iff.1 hf).2 (U n) (U_mem n)

/-- Given a Cauchy filter `f` and a sequence `U` of entourages, `set_seq` provides
a sequence of monotonically decreasing sets `s n âˆˆ f` such that `(s n).prod (s n) âŠ† U`. -/
def set_seq (n : â„•) : set Î± :=  â‹‚ m âˆˆ Iic n, (set_seq_aux hf U_mem m).val

lemma set_seq_mem (n : â„•) : set_seq hf U_mem n âˆˆ f :=
Inter_mem_sets (finite_le_nat n) (Î» m _, (set_seq_aux hf U_mem m).2.fst)

lemma set_seq_mono â¦ƒm n : â„•â¦„ (h : m â‰¤ n) : set_seq hf U_mem n âŠ† set_seq hf U_mem m :=
bInter_subset_bInter_left (Î» k hk, le_trans hk h)

lemma set_seq_sub_aux (n : â„•) : set_seq hf U_mem n âŠ† set_seq_aux hf U_mem n :=
bInter_subset_of_mem right_mem_Iic

lemma set_seq_prod_subset {N m n} (hm : N â‰¤ m) (hn : N â‰¤ n) :
  (set_seq hf U_mem m).prod (set_seq hf U_mem n) âŠ† U N :=
begin
  assume p hp,
  refine (set_seq_aux hf U_mem N).2.snd âŸ¨_, _âŸ©;
    apply set_seq_sub_aux,
  exact set_seq_mono hf U_mem hm hp.1,
  exact set_seq_mono hf U_mem hn hp.2
end

/-- A sequence of points such that `seq n âˆˆ set_seq n`. Here `set_seq` is a monotonically
decreasing sequence of sets `set_seq n âˆˆ f` with diameters controlled by a given sequence
of entourages. -/
def seq (n : â„•) : Î± := some $ nonempty_of_mem_sets hf.1 (set_seq_mem hf U_mem n)

lemma seq_mem (n : â„•) : seq hf U_mem n âˆˆ set_seq hf U_mem n :=
some_spec $ nonempty_of_mem_sets hf.1 (set_seq_mem hf U_mem n)

lemma seq_pair_mem â¦ƒN m n : â„•â¦„ (hm : N â‰¤ m) (hn : N â‰¤ n) :
  (seq hf U_mem m, seq hf U_mem n) âˆˆ U N :=
set_seq_prod_subset hf U_mem hm hn âŸ¨seq_mem hf U_mem m, seq_mem hf U_mem nâŸ©

include U_le

theorem seq_is_cauchy_seq : cauchy_seq $ seq hf U_mem :=
cauchy_seq_of_controlled U U_le $ seq_pair_mem hf U_mem

/-- If the sequence `sequentially_complete.seq` converges to `a`, then `f â‰¤ ğ“ a`. -/
theorem le_nhds_of_seq_tendsto_nhds â¦ƒa : Î±â¦„ (ha : tendsto (seq hf U_mem) at_top (ğ“ a)) :
  f â‰¤ ğ“ a :=
le_nhds_of_cauchy_adhp_aux
begin
  assume s hs,
  rcases U_le s hs with âŸ¨m, hmâŸ©,
  rcases (tendsto_at_top' _ _).1 ha _ (mem_nhds_left a (U_mem m)) with âŸ¨n, hnâŸ©,
  refine âŸ¨set_seq hf U_mem (max m n), set_seq_mem hf U_mem _, _,
          seq hf U_mem (max m n), _, seq_mem hf U_mem _âŸ©,
  { have := le_max_left m n,
    exact set.subset.trans (set_seq_prod_subset hf U_mem this this) hm },
  { exact hm (hn _ $ le_max_right m n) }
end

end sequentially_complete

namespace uniform_space

open sequentially_complete

variables (H : is_countably_generated (ğ“¤ Î±))

include H

/-- A uniform space is complete provided that (a) its uniformity filter has a countable basis;
(b) any sequence satisfying a "controlled" version of the Cauchy condition converges. -/
theorem complete_of_convergent_controlled_sequences (U : â„• â†’ set (Î± Ã— Î±)) (U_mem : âˆ€ n, U n âˆˆ ğ“¤ Î±)
  (HU : âˆ€ u : â„• â†’ Î±, (âˆ€ N m n, N â‰¤ m â†’ N â‰¤ n â†’ (u m, u n) âˆˆ U N) â†’ âˆƒ a, tendsto u at_top (ğ“ a)) :
  complete_space Î± :=
begin
  rcases H.exists_antimono_seq' with âŸ¨U', U'_mono, hU'âŸ©,
  have Hmem : âˆ€ n, U n âˆ© U' n âˆˆ ğ“¤ Î±,
    from Î» n, inter_mem_sets (U_mem n) (hU'.2 âŸ¨n, subset.refl _âŸ©),
  refine âŸ¨Î» f hf, (HU (seq hf Hmem) (Î» N m n hm hn, _)).imp $
    le_nhds_of_seq_tendsto_nhds _ _ (Î» s hs, _)âŸ©,
  { rcases (hU'.1 hs) with âŸ¨N, hNâŸ©,
    exact âŸ¨N, subset.trans (inter_subset_right _ _) hNâŸ© },
  { exact inter_subset_left _ _ (seq_pair_mem hf Hmem hm hn) }
end

/-- A sequentially complete uniform space with a countable basis of the uniformity filter is
complete. -/
theorem complete_of_cauchy_seq_tendsto
  (H' : âˆ€ u : â„• â†’ Î±, cauchy_seq u â†’ âˆƒa, tendsto u at_top (ğ“ a)) :
  complete_space Î± :=
let âŸ¨U', U'_mono, hU'âŸ© := H.exists_antimono_seq' in
complete_of_convergent_controlled_sequences H U' (Î» n, hU'.2 âŸ¨n, subset.refl _âŸ©)
  (Î» u hu, H' u $ cauchy_seq_of_controlled U' (Î» s hs, hU'.1 hs) hu)

protected lemma first_countable_topology : first_countable_topology Î± :=
âŸ¨Î» a, by { rw nhds_eq_comap_uniformity, exact H.comap (prod.mk a) }âŸ©

end uniform_space
