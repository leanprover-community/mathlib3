/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Yury Kudryashov
-/
import topology.uniform_space.uniform_convergence
import topology.uniform_space.equicontinuity
import topology.separation
import topology.support

/-!
# Compact separated uniform spaces

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

## Main statements

* `compact_space_uniformity`: On a compact uniform space, the topology determines the
  uniform structure, entourages are exactly the neighborhoods of the diagonal.

* `uniform_space_of_compact_t2`: every compact T2 topological structure is induced by a uniform
  structure. This uniform structure is described in the previous item.

* **Heine-Cantor** theorem: continuous functions on compact uniform spaces with values in uniform
  spaces are automatically uniformly continuous. There are several variations, the main one is
  `compact_space.uniform_continuous_of_continuous`.

## Implementation notes

The construction `uniform_space_of_compact_t2` is not declared as an instance, as it would badly
loop.

## tags

uniform space, uniform continuity, compact space
-/

open_locale classical uniformity topology filter
open filter uniform_space set

variables {Î± Î² Î³ : Type*} [uniform_space Î±] [uniform_space Î²]

/-!
### Uniformity on compact spaces
-/

/-- On a compact uniform space, the topology determines the uniform structure, entourages are
exactly the neighborhoods of the diagonal. -/
lemma nhds_set_diagonal_eq_uniformity [compact_space Î±] : ğ“Ë¢ (diagonal Î±) = ğ“¤ Î± :=
begin
  refine nhds_set_diagonal_le_uniformity.antisymm _,
  have : (ğ“¤ (Î± Ã— Î±)).has_basis (Î» U, U âˆˆ ğ“¤ Î±)
    (Î» U, (Î» p : (Î± Ã— Î±) Ã— Î± Ã— Î±, ((p.1.1, p.2.1), p.1.2, p.2.2)) â»Â¹' U Ã—Ë¢ U),
  { rw [uniformity_prod_eq_comap_prod],
    exact (ğ“¤ Î±).basis_sets.prod_self.comap _ },
  refine (is_compact_diagonal.nhds_set_basis_uniformity this).ge_iff.2 (Î» U hU, _),
  exact mem_of_superset hU (Î» âŸ¨x, yâŸ© hxy, mem_Unionâ‚‚.2 âŸ¨(x, x), rfl, refl_mem_uniformity hU, hxyâŸ©)
end

/-- On a compact uniform space, the topology determines the uniform structure, entourages are
exactly the neighborhoods of the diagonal. -/
lemma compact_space_uniformity [compact_space Î±] : ğ“¤ Î± = â¨† x, ğ“ (x, x) :=
nhds_set_diagonal_eq_uniformity.symm.trans (nhds_set_diagonal _)

lemma unique_uniformity_of_compact [t : topological_space Î³] [compact_space Î³]
  {u u' : uniform_space Î³} (h : u.to_topological_space = t) (h' : u'.to_topological_space = t) :
  u = u' :=
begin
  apply uniform_space_eq,
  change uniformity _ = uniformity _,
  haveI : @compact_space Î³ u.to_topological_space, { rwa h },
  haveI : @compact_space Î³ u'.to_topological_space, { rwa h' },
  rw [compact_space_uniformity, compact_space_uniformity, h, h']
end

/-- The unique uniform structure inducing a given compact topological structure. -/
def uniform_space_of_compact_t2 [topological_space Î³] [compact_space Î³] [t2_space Î³] :
  uniform_space Î³ :=
{ uniformity := ğ“Ë¢ (diagonal Î³),
  refl := principal_le_nhds_set,
  symm := continuous_swap.tendsto_nhds_set $ Î» x, eq.symm,
  comp := begin
    /-
    This is the difficult part of the proof. We need to prove that, for each neighborhood `W`
    of the diagonal `Î”`, there exists a smaller neighborhood `V` such that `V â—‹ V âŠ† W`.
    -/
    set ğ“Î” := ğ“Ë¢ (diagonal Î³), -- The filter of neighborhoods of Î”
    set F := ğ“Î”.lift' (Î» (s : set (Î³ Ã— Î³)), s â—‹ s), -- Compositions of neighborhoods of Î”
    -- If this weren't true, then there would be V âˆˆ ğ“Î” such that F âŠ“ ğ“Ÿ Vá¶œ â‰  âŠ¥
    rw le_iff_forall_inf_principal_compl,
    intros V V_in,
    by_contra H,
    haveI : ne_bot (F âŠ“ ğ“Ÿ Vá¶œ) := âŸ¨HâŸ©,
    -- Hence compactness would give us a cluster point (x, y) for F âŠ“ ğ“Ÿ Vá¶œ
    obtain âŸ¨âŸ¨x, yâŸ©, hxyâŸ© : âˆƒ (p : Î³ Ã— Î³), cluster_pt p (F âŠ“ ğ“Ÿ Vá¶œ) := cluster_point_of_compact _,
    -- In particular (x, y) is a cluster point of ğ“Ÿ Vá¶œ, hence is not in the interior of V,
    -- and a fortiori not in Î”, so x â‰  y
    have clV : cluster_pt (x, y) (ğ“Ÿ $ Vá¶œ) := hxy.of_inf_right,
    have : (x, y) âˆ‰ interior V,
    { have : (x, y) âˆˆ closure (Vá¶œ), by rwa mem_closure_iff_cluster_pt,
      rwa closure_compl at this },
    have diag_subset : diagonal Î³ âŠ† interior V,
      from subset_interior_iff_mem_nhds_set.2 V_in,
    have x_ne_y : x â‰  y,
      from mt (@diag_subset (x, y)) this,
    -- Since Î³ is compact and Hausdorff, it is normal, hence Tâ‚ƒ.
    haveI : normal_space Î³ := normal_of_compact_t2,
    -- So there are closed neighboords Vâ‚ and Vâ‚‚ of x and y contained in disjoint open neighborhoods
    -- Uâ‚ and Uâ‚‚.
    obtain
      âŸ¨Uâ‚, Uâ‚_in, Vâ‚, Vâ‚_in, Uâ‚‚, Uâ‚‚_inâ‚‚, Vâ‚‚, Vâ‚‚_in, Vâ‚_cl, Vâ‚‚_cl, Uâ‚_op, Uâ‚‚_op, VUâ‚, VUâ‚‚, hUâ‚â‚‚âŸ© :=
       disjoint_nested_nhds x_ne_y,
    -- We set Uâ‚ƒ := (Vâ‚ âˆª Vâ‚‚)á¶œ so that W := Uâ‚ Ã—Ë¢ Uâ‚ âˆª Uâ‚‚ Ã—Ë¢ Uâ‚‚ âˆª Uâ‚ƒ Ã—Ë¢ Uâ‚ƒ is an open
    -- neighborhood of Î”.
    let Uâ‚ƒ := (Vâ‚ âˆª Vâ‚‚)á¶œ,
    have Uâ‚ƒ_op : is_open Uâ‚ƒ := (Vâ‚_cl.union Vâ‚‚_cl).is_open_compl,
    let W := Uâ‚ Ã—Ë¢ Uâ‚ âˆª Uâ‚‚ Ã—Ë¢ Uâ‚‚ âˆª Uâ‚ƒ Ã—Ë¢ Uâ‚ƒ,
    have W_in : W âˆˆ ğ“Î”,
    { rw [mem_nhds_set_iff_forall],
      rintros âŸ¨z, z'âŸ© (rfl : z = z'),
      refine is_open.mem_nhds _ _,
      { apply_rules [is_open.union, is_open.prod] },
      { simp only [mem_union, mem_prod, and_self],
        exact (em _).imp_left (Î» h, union_subset_union VUâ‚ VUâ‚‚ h) } },
    -- So W â—‹ W âˆˆ F by definition of F
    have : W â—‹ W âˆˆ F, by simpa only using mem_lift' W_in,
    -- And Vâ‚ Ã—Ë¢ Vâ‚‚ âˆˆ ğ“ (x, y)
    have hVâ‚â‚‚ : Vâ‚ Ã—Ë¢ Vâ‚‚ âˆˆ ğ“ (x, y) := prod_mem_nhds Vâ‚_in Vâ‚‚_in,
    -- But (x, y) is also a cluster point of F so (Vâ‚ Ã—Ë¢ Vâ‚‚) âˆ© (W â—‹ W) â‰  âˆ…
    -- However the construction of W implies (Vâ‚ Ã—Ë¢ Vâ‚‚) âˆ© (W â—‹ W) = âˆ….
    -- Indeed assume for contradiction there is some (u, v) in the intersection.
    obtain âŸ¨âŸ¨u, vâŸ©, âŸ¨u_in, v_inâŸ©, w, huw, hwvâŸ© := cluster_pt_iff.mp (hxy.of_inf_left) hVâ‚â‚‚ this,
    -- So u âˆˆ Vâ‚, v âˆˆ Vâ‚‚, and there exists some w such that (u, w) âˆˆ W and (w ,v) âˆˆ W.
    -- Because u is in Vâ‚ which is disjoint from Uâ‚‚ and Uâ‚ƒ, (u, w) âˆˆ W forces (u, w) âˆˆ Uâ‚ Ã—Ë¢ Uâ‚.
    have uw_in : (u, w) âˆˆ Uâ‚ Ã—Ë¢ Uâ‚ := (huw.resolve_right $ Î» h, (h.1 $ or.inl u_in)).resolve_right
      (Î» h, hUâ‚â‚‚.le_bot âŸ¨VUâ‚ u_in, h.1âŸ©),
    -- Similarly, because v âˆˆ Vâ‚‚, (w ,v) âˆˆ W forces (w, v) âˆˆ Uâ‚‚ Ã—Ë¢ Uâ‚‚.
    have wv_in : (w, v) âˆˆ Uâ‚‚ Ã—Ë¢ Uâ‚‚ := (hwv.resolve_right $ Î» h, (h.2 $ or.inr v_in)).resolve_left
      (Î» h, hUâ‚â‚‚.le_bot âŸ¨h.2, VUâ‚‚ v_inâŸ©),
    -- Hence w âˆˆ Uâ‚ âˆ© Uâ‚‚ which is empty.
    -- So we have a contradiction
    exact hUâ‚â‚‚.le_bot âŸ¨uw_in.2, wv_in.1âŸ©,
  end,
  is_open_uniformity := begin
    -- Here we need to prove the topology induced by the constructed uniformity is the
    -- topology we started with.
    suffices : âˆ€ x : Î³, filter.comap (prod.mk x) (ğ“Ë¢ (diagonal Î³)) = ğ“ x,
    { intros s,
      simp_rw [is_open_fold, is_open_iff_mem_nhds, â† mem_comap_prod_mk, this] },
    intros x,
    simp_rw [nhds_set_diagonal, comap_supr, nhds_prod_eq, comap_prod, (âˆ˜), comap_id'],
    rw [supr_split_single _ x, comap_const_of_mem (Î» V, mem_of_mem_nhds)],
    suffices : âˆ€ y â‰  x, comap (Î» (y : Î³), x) (ğ“ y) âŠ“ ğ“ y â‰¤ ğ“ x,
      by simpa,
    intros y hxy,
    simp [comap_const_of_not_mem (compl_singleton_mem_nhds hxy) (not_not.2 rfl)]
  end }

/-!
###Â Heine-Cantor theorem
-/

/-- Heine-Cantor: a continuous function on a compact uniform space is uniformly
continuous. -/
lemma compact_space.uniform_continuous_of_continuous [compact_space Î±]
  {f : Î± â†’ Î²} (h : continuous f) : uniform_continuous f :=
have tendsto (prod.map f f) (ğ“Ë¢ (diagonal Î±)) (ğ“Ë¢ (diagonal Î²)),
  from (h.prod_map h).tendsto_nhds_set maps_to_prod_map_diagonal,
(this.mono_left nhds_set_diagonal_eq_uniformity.ge).mono_right nhds_set_diagonal_le_uniformity

/-- Heine-Cantor: a continuous function on a compact set of a uniform space is uniformly
continuous. -/
lemma is_compact.uniform_continuous_on_of_continuous {s : set Î±} {f : Î± â†’ Î²}
  (hs : is_compact s) (hf : continuous_on f s) : uniform_continuous_on f s :=
begin
  rw uniform_continuous_on_iff_restrict,
  rw is_compact_iff_compact_space at hs,
  rw continuous_on_iff_continuous_restrict at hf,
  resetI,
  exact compact_space.uniform_continuous_of_continuous hf,
end

/-- If `s` is compact and `f` is continuous at all points of `s`, then `f` is
"uniformly continuous at the set `s`", i.e. `f x` is close to `f y` whenever `x âˆˆ s` and `y` is
close to `x` (even if `y` is not itself in `s`, so this is a stronger assertion than
`uniform_continuous_on s`). -/
lemma is_compact.uniform_continuous_at_of_continuous_at {r : set (Î² Ã— Î²)} {s : set Î±}
  (hs : is_compact s) (f : Î± â†’ Î²) (hf : âˆ€ a âˆˆ s, continuous_at f a) (hr : r âˆˆ ğ“¤ Î²) :
  {x : Î± Ã— Î± | x.1 âˆˆ s â†’ (f x.1, f x.2) âˆˆ r} âˆˆ ğ“¤ Î± :=
begin
  obtain âŸ¨t, ht, htsymm, htrâŸ© := comp_symm_mem_uniformity_sets hr,
  choose U hU T hT hb using Î» a ha, exists_mem_nhds_ball_subset_of_mem_nhds
    ((hf a ha).preimage_mem_nhds $ mem_nhds_left _ ht),
  obtain âŸ¨fs, hsUâŸ© := hs.elim_nhds_subcover' U hU,
  apply mem_of_superset ((bInter_finset_mem fs).2 $ Î» a _, hT a a.2),
  rintro âŸ¨aâ‚, aâ‚‚âŸ© h hâ‚,
  obtain âŸ¨a, ha, haUâŸ© := set.mem_Unionâ‚‚.1 (hsU hâ‚),
  apply htr,
  refine âŸ¨f a, htsymm.mk_mem_comm.1 (hb _ _ _ haU _), hb _ _ _ haU _âŸ©,
  exacts [mem_ball_self _ (hT a a.2), mem_Interâ‚‚.1 h a ha],
end

lemma continuous.uniform_continuous_of_tendsto_cocompact {f : Î± â†’ Î²} {x : Î²}
  (h_cont : continuous f) (hx : tendsto f (cocompact Î±) (ğ“ x)) : uniform_continuous f :=
uniform_continuous_def.2 $ Î» r hr, begin
  obtain âŸ¨t, ht, htsymm, htrâŸ© := comp_symm_mem_uniformity_sets hr,
  obtain âŸ¨s, hs, hstâŸ© := mem_cocompact.1 (hx $ mem_nhds_left _ ht),
  apply mem_of_superset (symmetrize_mem_uniformity $ hs.uniform_continuous_at_of_continuous_at
    f (Î» _ _, h_cont.continuous_at) $ symmetrize_mem_uniformity hr),
  rintro âŸ¨bâ‚, bâ‚‚âŸ© h,
  by_cases hâ‚ : bâ‚ âˆˆ s, { exact (h.1 hâ‚).1 },
  by_cases hâ‚‚ : bâ‚‚ âˆˆ s, { exact (h.2 hâ‚‚).2 },
  apply htr,
  exact âŸ¨x, htsymm.mk_mem_comm.1 (hst hâ‚), hst hâ‚‚âŸ©,
end

/-- If `f` has compact multiplicative support, then `f` tends to 1 at infinity. -/
@[to_additive "If `f` has compact support, then `f` tends to zero at infinity."]
lemma has_compact_mul_support.is_one_at_infty {f : Î± â†’ Î³} [topological_space Î³] [has_one Î³]
  (h : has_compact_mul_support f) : tendsto f (cocompact Î±) (ğ“ 1) :=
begin
  -- porting note: move to src/topology/support.lean once the port is over
  intros N hN,
  rw [mem_map, mem_cocompact'],
  refine âŸ¨mul_tsupport f, h.is_compact, _âŸ©,
  rw compl_subset_comm,
  intros v hv,
  rw [mem_preimage, image_eq_one_of_nmem_mul_tsupport hv],
  exact mem_of_mem_nhds hN,
end

@[to_additive]
lemma has_compact_mul_support.uniform_continuous_of_continuous {f : Î± â†’ Î²} [has_one Î²]
  (h1 : has_compact_mul_support f) (h2 : continuous f) : uniform_continuous f :=
h2.uniform_continuous_of_tendsto_cocompact h1.is_one_at_infty

/-- A family of functions `Î± â†’ Î² â†’ Î³` tends uniformly to its value at `x` if `Î±` is locally compact,
`Î²` is compact and `f` is continuous on `U Ã— (univ : set Î²)` for some neighborhood `U` of `x`. -/
lemma continuous_on.tendsto_uniformly [locally_compact_space Î±] [compact_space Î²]
  [uniform_space Î³] {f : Î± â†’ Î² â†’ Î³} {x : Î±} {U : set Î±}
  (hxU : U âˆˆ ğ“ x) (h : continuous_on â†¿f (U Ã—Ë¢ univ)) :
  tendsto_uniformly f (f x) (ğ“ x) :=
begin
  rcases locally_compact_space.local_compact_nhds _ _ hxU with âŸ¨K, hxK, hKU, hKâŸ©,
  have : uniform_continuous_on â†¿f (K Ã—Ë¢ univ),
    from is_compact.uniform_continuous_on_of_continuous (hK.prod is_compact_univ)
      (h.mono $ prod_mono hKU subset.rfl),
  exact this.tendsto_uniformly hxK
end

/-- A continuous family of functions `Î± â†’ Î² â†’ Î³` tends uniformly to its value at `x` if `Î±` is
locally compact and `Î²` is compact. -/
lemma continuous.tendsto_uniformly [locally_compact_space Î±] [compact_space Î²] [uniform_space Î³]
  (f : Î± â†’ Î² â†’ Î³) (h : continuous â†¿f) (x : Î±) : tendsto_uniformly f (f x) (ğ“ x) :=
h.continuous_on.tendsto_uniformly univ_mem

section uniform_convergence

/-- An equicontinuous family of functions defined on a compact uniform space is automatically
uniformly equicontinuous. -/
lemma compact_space.uniform_equicontinuous_of_equicontinuous {Î¹ : Type*} {F : Î¹ â†’ Î² â†’ Î±}
  [compact_space Î²] (h : equicontinuous F) :
  uniform_equicontinuous F :=
begin
  rw equicontinuous_iff_continuous at h,
  rw uniform_equicontinuous_iff_uniform_continuous,
  exact compact_space.uniform_continuous_of_continuous h
end

end uniform_convergence
