/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import topology.uniform_space.separation
import topology.uniform_space.uniform_convergence
/-!
# Compact separated uniform spaces

## Main statements

* `compact_space_uniformity`: On a separated compact uniform space, the topology determines the
  uniform structure, entourages are exactly the neighborhoods of the diagonal.
* `uniform_space_of_compact_t2`: every compact T2 topological structure is induced by a uniform
  structure. This uniform structure is described in the previous item.
* Heine-Cantor theorem: continuous functions on compact separated uniform spaces with values in
  uniform spaces are automatically uniformly continuous. There are several variations, the main one
  is `compact_space.uniform_continuous_of_continuous`.

## Implementation notes

The construction `uniform_space_of_compact_t2` is not declared as an instance, as it would badly
loop.

## tags

uniform space, uniform continuity, compact space
-/

open_locale classical uniformity topological_space filter
open filter uniform_space set

variables {Î± Î² Î³ : Type*} [uniform_space Î±] [uniform_space Î²]


/-!
### Uniformity on compact separated spaces
-/

/-- On a separated compact uniform space, the topology determines the uniform structure, entourages
are exactly the neighborhoods of the diagonal. -/
lemma compact_space_uniformity [compact_space Î±] [separated_space Î±] : ğ“¤ Î± = â¨† x : Î±, ğ“ (x, x) :=
begin
  symmetry, refine le_antisymm supr_nhds_le_uniformity _,
  by_contra H,
  obtain âŸ¨V, hV, hâŸ© : âˆƒ V : set (Î± Ã— Î±), (âˆ€ x : Î±, V âˆˆ ğ“ (x, x)) âˆ§ ğ“¤ Î± âŠ“ ğ“Ÿ Vá¶œ â‰  âŠ¥,
  { simpa [le_iff_forall_inf_principal_compl] using H },
  let F := ğ“¤ Î± âŠ“ ğ“Ÿ Vá¶œ,
  haveI : ne_bot F := âŸ¨hâŸ©,
  obtain âŸ¨âŸ¨x, yâŸ©, hxâŸ© : âˆƒ (p : Î± Ã— Î±), cluster_pt p F :=
    cluster_point_of_compact F,
  have : cluster_pt (x, y) (ğ“¤ Î±) :=
    hx.of_inf_left,
  obtain rfl : x = y := eq_of_uniformity_inf_nhds this,
  have : cluster_pt (x, x) (ğ“Ÿ Vá¶œ) :=
   hx.of_inf_right,
  have : (x, x) âˆ‰ interior V,
  { have : (x, x) âˆˆ closure Vá¶œ, by rwa mem_closure_iff_cluster_pt,
    rwa closure_compl at this },
  have : (x, x) âˆˆ interior V,
  { rw mem_interior_iff_mem_nhds,
    exact hV x },
  contradiction
end

lemma unique_uniformity_of_compact_t2 [t : topological_space Î³] [compact_space Î³]
[t2_space Î³] {u u' : uniform_space Î³}
(h : u.to_topological_space = t) (h' : u'.to_topological_space = t) : u = u' :=
begin
  apply uniform_space_eq,
  change uniformity _ = uniformity _,
  haveI : @compact_space Î³ u.to_topological_space, { rw h ; assumption },
  haveI : @compact_space Î³ u'.to_topological_space, { rw h' ; assumption },
  haveI : @separated_space Î³ u, { rwa [separated_iff_t2, h] },
  haveI : @separated_space Î³ u', { rwa [separated_iff_t2, h'] },
  rw [compact_space_uniformity, compact_space_uniformity, h, h']
end

/-- The unique uniform structure inducing a given compact Hausdorff topological structure. -/
def uniform_space_of_compact_t2 [topological_space Î³] [compact_space Î³] [t2_space Î³] :
  uniform_space Î³ :=
{ uniformity := â¨† x, ğ“ (x, x),
  refl := begin
    simp_rw [filter.principal_le_iff, mem_supr],
    rintros V V_in âŸ¨x, _âŸ© âŸ¨âŸ©,
    exact mem_of_mem_nhds (V_in x),
  end,
  symm := begin
    refine le_of_eq _,
    rw map_supr,
    congr' with x : 1,
    erw [nhds_prod_eq, â† prod_comm],
  end,
  comp := begin
    /-
    This is the difficult part of the proof. We need to prove that, for each neighborhood W
    of the diagonal Î”, W â—‹ W is still a neighborhood of the diagonal.
    -/
    set ğ“Î” := â¨† x : Î³, ğ“ (x, x), -- The filter of neighborhoods of Î”
    set F := ğ“Î”.lift' (Î» (s : set (Î³ Ã— Î³)), s â—‹ s), -- Compositions of neighborhoods of Î”
    -- If this weren't true, then there would be V âˆˆ ğ“Î” such that F âŠ“ ğ“Ÿ Vá¶œ â‰  âŠ¥
    rw le_iff_forall_inf_principal_compl,
    intros V V_in,
    by_contra H,
    haveI : ne_bot (F âŠ“ ğ“Ÿ Vá¶œ) := âŸ¨HâŸ©,
    -- Hence compactness would give us a cluster point (x, y) for F âŠ“ ğ“Ÿ Vá¶œ
    obtain âŸ¨âŸ¨x, yâŸ©, hxyâŸ© : âˆƒ (p : Î³ Ã— Î³), cluster_pt p (F âŠ“ ğ“Ÿ Vá¶œ) := cluster_point_of_compact _,
    -- In particular (x, y) is a cluster point of ğ“Ÿ Vá¶œ, hence is not in the interior of V,
    -- and a fortiori not in Î”, so x â‰  y
    have clV : cluster_pt (x, y) (ğ“Ÿ $ Vá¶œ) := hxy.of_inf_right,
    have : (x, y) âˆ‰ interior V,
    { have : (x, y) âˆˆ closure (Vá¶œ), by rwa mem_closure_iff_cluster_pt,
      rwa closure_compl at this },
    have diag_subset : diagonal Î³ âŠ† interior V,
    { rw subset_interior_iff_nhds,
      rintros âŸ¨x, xâŸ© âŸ¨âŸ©,
      exact (mem_supr.mp V_in : _) x },
    have x_ne_y : x â‰  y,
    { intro h,
      apply this,
      apply diag_subset,
      simp [h] },
    -- Since Î³ is compact and Hausdorff, it is normal, hence regular.
    haveI : normal_space Î³ := normal_of_compact_t2,
    -- So there are closed neighboords Vâ‚ and Vâ‚‚ of x and y contained in disjoint open neighborhoods
    -- Uâ‚ and Uâ‚‚.
    obtain
      âŸ¨Uâ‚, Uâ‚_in, Vâ‚, Vâ‚_in, Uâ‚‚, Uâ‚‚_inâ‚‚, Vâ‚‚, Vâ‚‚_in, Vâ‚_cl, Vâ‚‚_cl, Uâ‚_op, Uâ‚‚_op, VUâ‚, VUâ‚‚, hUâ‚â‚‚âŸ© :=
       disjoint_nested_nhds x_ne_y,
    -- We set Uâ‚ƒ := (Vâ‚ âˆª Vâ‚‚)á¶œ so that W := Uâ‚ Ã—Ë¢ Uâ‚ âˆª Uâ‚‚ Ã—Ë¢ Uâ‚‚ âˆª Uâ‚ƒ Ã—Ë¢ Uâ‚ƒ is an open
    -- neighborhood of Î”.
    let Uâ‚ƒ := (Vâ‚ âˆª Vâ‚‚)á¶œ,
    have Uâ‚ƒ_op : is_open Uâ‚ƒ :=
      is_open_compl_iff.mpr (is_closed.union Vâ‚_cl Vâ‚‚_cl),
    let W := Uâ‚ Ã—Ë¢ Uâ‚ âˆª Uâ‚‚ Ã—Ë¢ Uâ‚‚ âˆª Uâ‚ƒ Ã—Ë¢ Uâ‚ƒ,
    have W_in : W âˆˆ ğ“Î”,
    { rw mem_supr,
      intros x,
      apply is_open.mem_nhds (is_open.union (is_open.union _ _) _),
      { by_cases hx : x âˆˆ Vâ‚ âˆª Vâ‚‚,
        { left,
          cases hx with hx hx ; [left, right] ; split ; tauto },
        { right,
          rw mem_prod,
          tauto }, },
      all_goals { simp only [is_open.prod, *] } },
    -- So W â—‹ W âˆˆ F by definition of F
    have : W â—‹ W âˆˆ F, by simpa only using mem_lift' W_in,
    -- And Vâ‚ Ã—Ë¢ Vâ‚‚ âˆˆ ğ“ (x, y)
    have hVâ‚â‚‚ : Vâ‚ Ã—Ë¢ Vâ‚‚ âˆˆ ğ“ (x, y) := prod_mem_nhds Vâ‚_in Vâ‚‚_in,
    -- But (x, y) is also a cluster point of F so (Vâ‚ Ã—Ë¢ Vâ‚‚) âˆ© (W â—‹ W) â‰  âˆ…
    -- However the construction of W implies (Vâ‚ Ã—Ë¢ Vâ‚‚) âˆ© (W â—‹ W) = âˆ….
    -- Indeed assume for contradiction there is some (u, v) in the intersection.
    obtain âŸ¨âŸ¨u, vâŸ©, âŸ¨u_in, v_inâŸ©, w, huw, hwvâŸ© := cluster_pt_iff.mp (hxy.of_inf_left) hVâ‚â‚‚ this,
    -- So u âˆˆ Vâ‚, v âˆˆ Vâ‚‚, and there exists some w such that (u, w) âˆˆ W and (w ,v) âˆˆ W.
    -- Because u is in Vâ‚ which is disjoint from Uâ‚‚ and Uâ‚ƒ, (u, w) âˆˆ W forces (u, w) âˆˆ Uâ‚ Ã—Ë¢ Uâ‚.
    have uw_in : (u, w) âˆˆ Uâ‚ Ã—Ë¢ Uâ‚ := (huw.resolve_right $ Î» h, (h.1 $ or.inl u_in)).resolve_right
      (Î» h, hUâ‚â‚‚ âŸ¨VUâ‚ u_in, h.1âŸ©),
    -- Similarly, because v âˆˆ Vâ‚‚, (w ,v) âˆˆ W forces (w, v) âˆˆ Uâ‚‚ Ã—Ë¢ Uâ‚‚.
    have wv_in : (w, v) âˆˆ Uâ‚‚ Ã—Ë¢ Uâ‚‚ := (hwv.resolve_right $ Î» h, (h.2 $ or.inr v_in)).resolve_left
      (Î» h, hUâ‚â‚‚ âŸ¨h.2, VUâ‚‚ v_inâŸ©),
    -- Hence w âˆˆ Uâ‚ âˆ© Uâ‚‚ which is empty.
    -- So we have a contradiction
    exact hUâ‚â‚‚ âŸ¨uw_in.2, wv_in.1âŸ©,
  end,
  is_open_uniformity := begin
    -- Here we need to prove the topology induced by the constructed uniformity is the
    -- topology we started with.
    suffices : âˆ€ x : Î³, filter.comap (prod.mk x) (â¨† y, ğ“ (y ,y)) = ğ“ x,
    { intros s,
      change is_open s â†” _,
      simp_rw [is_open_iff_mem_nhds, nhds_eq_comap_uniformity_aux, this] },
    intros x,
    simp_rw [comap_supr, nhds_prod_eq, comap_prod,
             show prod.fst âˆ˜ prod.mk x = Î» y : Î³, x, by ext ; simp,
             show prod.snd âˆ˜ (prod.mk x) = (id : Î³ â†’ Î³), by ext ; refl, comap_id],
    rw [supr_split_single _ x, comap_const_of_mem (Î» V, mem_of_mem_nhds)],
    suffices : âˆ€ y â‰  x, comap (Î» (y : Î³), x) (ğ“ y) âŠ“ ğ“ y â‰¤ ğ“ x,
      by simpa,
    intros y hxy,
    simp [comap_const_of_not_mem (compl_singleton_mem_nhds hxy) (by simp)],
  end }

/-!
###Â Heine-Cantor theorem
-/

/-- Heine-Cantor: a continuous function on a compact separated uniform space is uniformly
continuous. -/
lemma compact_space.uniform_continuous_of_continuous [compact_space Î±] [separated_space Î±]
  {f : Î± â†’ Î²} (h : continuous f) : uniform_continuous f :=
calc
map (prod.map f f) (ğ“¤ Î±) = map (prod.map f f) (â¨† x, ğ“ (x, x))  : by rw compact_space_uniformity
                     ... =  â¨† x, map (prod.map f f) (ğ“ (x, x)) : by rw map_supr
                     ... â‰¤ â¨† x, ğ“ (f x, f x)     : supr_mono (Î» x, (h.prod_map h).continuous_at)
                     ... â‰¤ â¨† y, ğ“ (y, y)         : supr_comp_le (Î» y, ğ“ (y, y)) f
                     ... â‰¤ ğ“¤ Î²                   : supr_nhds_le_uniformity

/-- Heine-Cantor: a continuous function on a compact separated set of a uniform space is
uniformly continuous. -/
lemma is_compact.uniform_continuous_on_of_continuous' {s : set Î±} {f : Î± â†’ Î²}
  (hs : is_compact s) (hs' : is_separated s) (hf : continuous_on f s) : uniform_continuous_on f s :=
begin
  rw uniform_continuous_on_iff_restrict,
  rw is_separated_iff_induced at hs',
  rw is_compact_iff_compact_space at hs,
  rw continuous_on_iff_continuous_restrict at hf,
  resetI,
  exact compact_space.uniform_continuous_of_continuous hf,
end

/-- Heine-Cantor: a continuous function on a compact set of a separated uniform space
is uniformly continuous. -/
lemma is_compact.uniform_continuous_on_of_continuous [separated_space Î±] {s : set Î±} {f : Î± â†’ Î²}
  (hs : is_compact s) (hf : continuous_on f s) : uniform_continuous_on f s :=
hs.uniform_continuous_on_of_continuous' (is_separated_of_separated_space s) hf

/-- A family of functions `Î± â†’ Î² â†’ Î³` tends uniformly to its value at `x` if `Î±` is locally compact,
`Î²` is compact and separated and `f` is continuous on `U Ã— (univ : set Î²)` for some separated
neighborhood `U` of `x`. -/
lemma continuous_on.tendsto_uniformly [locally_compact_space Î±] [compact_space Î²]
  [separated_space Î²] [uniform_space Î³] {f : Î± â†’ Î² â†’ Î³} {x : Î±} {U : set Î±}
  (hxU : U âˆˆ ğ“ x) (hU : is_separated U) (h : continuous_on â†¿f (U Ã—Ë¢ (univ : set Î²))) :
  tendsto_uniformly f (f x) (ğ“ x) :=
begin
  rcases locally_compact_space.local_compact_nhds _ _ hxU with âŸ¨K, hxK, hKU, hKâŸ©,
  have : uniform_continuous_on â†¿f (K Ã—Ë¢ (univ : set Î²)),
  { refine is_compact.uniform_continuous_on_of_continuous' (hK.prod compact_univ) _
      (h.mono $ prod_mono hKU subset.rfl),
    exact (hU.mono hKU).prod (is_separated_of_separated_space _) },
  exact this.tendsto_uniformly hxK
end

/-- A continuous family of functions `Î± â†’ Î² â†’ Î³` tends uniformly to its value at `x` if `Î±` is
locally compact and `Î²` is compact and separated. -/
lemma continuous.tendsto_uniformly [separated_space Î±] [locally_compact_space Î±]
  [compact_space Î²] [separated_space Î²] [uniform_space Î³]
  (f : Î± â†’ Î² â†’ Î³) (h : continuous â†¿f) (x : Î±) : tendsto_uniformly f (f x) (ğ“ x) :=
h.continuous_on.tendsto_uniformly univ_mem $ is_separated_of_separated_space _
