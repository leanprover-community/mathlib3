/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import topology.uniform_space.uniform_convergence_topology

/-!
# Equicontinuity of a family of functions

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Let `X` be a topological space and `Î±` a `uniform_space`. A family of functions `F : Î¹ â†’ X â†’ Î±`
is said to be *equicontinuous at a point `xâ‚€ : X`* when, for any entourage `U` in `Î±`, there is a
neighborhood `V` of `xâ‚€` such that, for all `x âˆˆ V`, and *for all `i`*, `F i x` is `U`-close to
`F i xâ‚€`. In other words, one has `âˆ€ U âˆˆ ð“¤ Î±, âˆ€á¶  x in ð“ xâ‚€, âˆ€ i, (F i xâ‚€, F i x) âˆˆ U`.
For maps between metric spaces, this corresponds to
`âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x, âˆ€ i, dist xâ‚€ x < Î´ â†’ dist (F i xâ‚€) (F i x) < Îµ`.

`F` is said to be *equicontinuous* if it is equicontinuous at each point.

A closely related concept is that of ***uniform*** *equicontinuity* of a family of functions
`F : Î¹ â†’ Î² â†’ Î±` between uniform spaces, which means that, for any entourage `U` in `Î±`, there is an
entourage `V` in `Î²` such that, if `x` and `y` are `V`-close, then *for all `i`*, `F i x` and
`F i y` are `U`-close. In other words, one has
`âˆ€ U âˆˆ ð“¤ Î±, âˆ€á¶  xy in ð“¤ Î², âˆ€ i, (F i xy.1, F i xy.2) âˆˆ U`.
For maps between metric spaces, this corresponds to
`âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x y, âˆ€ i, dist x y < Î´ â†’ dist (F i xâ‚€) (F i x) < Îµ`.

## Main definitions

* `equicontinuous_at`: equicontinuity of a family of functions at a point
* `equicontinuous`: equicontinuity of a family of functions on the whole domain
* `uniform_equicontinuous`: uniform equicontinuity of a family of functions on the whole domain

## Main statements

* `equicontinuous_iff_continuous`: equicontinuity can be expressed as a simple continuity
  condition between well-chosen function spaces. This is really useful for building up the theory.
* `equicontinuous.closure`: if a set of functions is equicontinuous, its closure
  *for the topology of uniform convergence* is also equicontinuous.

## Notations

Throughout this file, we use :
- `Î¹`, `Îº` for indexing types
- `X`, `Y`, `Z` for topological spaces
- `Î±`, `Î²`, `Î³` for uniform spaces

## Implementation details

We choose to express equicontinuity as a properties of indexed families of functions rather
than sets of functions for the following reasons:
- it is really easy to express equicontinuity of `H : set (X â†’ Î±)` using our setup: it is just
  equicontinuity of the family `coe : â†¥H â†’ (X â†’ Î±)`. On the other hand, going the other way around
  would require working with the range of the family, which is always annoying because it
  introduces useless existentials.
- in most applications, one doesn't work with bare functions but with a more specific hom type
  `hom`. Equicontinuity of a set `H : set hom` would then have to be expressed as equicontinuity
  of `coe_fn '' H`, which is super annoying to work with. This is much simpler with families,
  because equicontinuity of a family `ð“• : Î¹ â†’ hom` would simply be expressed as equicontinuity
  of `coe_fn âˆ˜ ð“•`, which doesn't introduce any nasty existentials.

To simplify statements, we do provide abbreviations `set.equicontinuous_at`, `set.equicontinuous`
and `set.uniform_equicontinuous` asserting the corresponding fact about the family
`coe : â†¥H â†’ (X â†’ Î±)` where `H : set (X â†’ Î±)`. Note however that these won't work for sets of hom
types, and in that case one should go back to the family definition rather than using `set.image`.

Since we have no use case for it yet, we don't introduce any relative version
(i.e no `equicontinuous_within_at` or `equicontinuous_on`), but this is more of a conservative
position than a design decision, so anyone needing relative versions should feel free to add them,
and that should hopefully be a straightforward task.

## References

* [N. Bourbaki, *General Topology, Chapter X*][bourbaki1966]

## Tags

equicontinuity, uniform convergence, ascoli
-/

section

open uniform_space filter set
open_locale uniformity topology uniform_convergence

variables {Î¹ Îº X Y Z Î± Î² Î³ ð“• : Type*} [topological_space X] [topological_space Y]
  [topological_space Z] [uniform_space Î±] [uniform_space Î²] [uniform_space Î³]

/-- A family `F : Î¹ â†’ X â†’ Î±` of functions from a topological space to a uniform space is
*equicontinuous at `xâ‚€ : X`* if, for all entourage `U âˆˆ ð“¤ Î±`, there is a neighborhood `V` of `xâ‚€`
such that, for all `x âˆˆ V` and for all `i : Î¹`, `F i x` is `U`-close to `F i xâ‚€`. -/
def equicontinuous_at (F : Î¹ â†’ X â†’ Î±) (xâ‚€ : X) : Prop :=
âˆ€ U âˆˆ ð“¤ Î±, âˆ€á¶  x in ð“ xâ‚€, âˆ€ i, (F i xâ‚€, F i x) âˆˆ U

/-- We say that a set `H : set (X â†’ Î±)` of functions is equicontinuous at a point if the family
`coe : â†¥H â†’ (X â†’ Î±)` is equicontinuous at that point. -/
protected abbreviation set.equicontinuous_at (H : set $ X â†’ Î±) (xâ‚€ : X) : Prop :=
equicontinuous_at (coe : H â†’ X â†’ Î±) xâ‚€

/-- A family `F : Î¹ â†’ X â†’ Î±` of functions from a topological space to a uniform space is
*equicontinuous* on all of `X` if it is equicontinuous at each point of `X`. -/
def equicontinuous (F : Î¹ â†’ X â†’ Î±) : Prop :=
âˆ€ xâ‚€, equicontinuous_at F xâ‚€

/-- We say that a set `H : set (X â†’ Î±)` of functions is equicontinuous if the family
`coe : â†¥H â†’ (X â†’ Î±)` is equicontinuous. -/
protected abbreviation set.equicontinuous (H : set $ X â†’ Î±) : Prop :=
equicontinuous (coe : H â†’ X â†’ Î±)

/-- A family `F : Î¹ â†’ Î² â†’ Î±` of functions between uniform spaces is *uniformly equicontinuous* if,
for all entourage `U âˆˆ ð“¤ Î±`, there is an entourage `V âˆˆ ð“¤ Î²` such that, whenever `x` and `y` are
`V`-close, we have that, *for all `i : Î¹`*, `F i x` is `U`-close to `F i xâ‚€`. -/
def uniform_equicontinuous (F : Î¹ â†’ Î² â†’ Î±) : Prop :=
âˆ€ U âˆˆ ð“¤ Î±, âˆ€á¶  (xy : Î² Ã— Î²) in ð“¤ Î², âˆ€ i, (F i xy.1, F i xy.2) âˆˆ U

/-- We say that a set `H : set (X â†’ Î±)` of functions is uniformly equicontinuous if the family
`coe : â†¥H â†’ (X â†’ Î±)` is uniformly equicontinuous. -/
protected abbreviation set.uniform_equicontinuous (H : set $ Î² â†’ Î±) : Prop :=
uniform_equicontinuous (coe : H â†’ Î² â†’ Î±)

/-- Reformulation of equicontinuity at `xâ‚€` comparing two variables near `xâ‚€` instead of comparing
only one with `xâ‚€`. -/
lemma equicontinuous_at_iff_pair {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X} : equicontinuous_at F xâ‚€ â†”
  âˆ€ U âˆˆ ð“¤ Î±, âˆƒ V âˆˆ ð“ xâ‚€, âˆ€ (x y âˆˆ V) i, (F i x, F i y) âˆˆ U :=
begin
  split; intros H U hU,
  { rcases comp_symm_mem_uniformity_sets hU with âŸ¨V, hV, hVsymm, hVUâŸ©,
    refine âŸ¨_, H V hV, Î» x hx y hy i, hVU (prod_mk_mem_comp_rel _ (hy i))âŸ©,
    exact hVsymm.mk_mem_comm.mp (hx i) },
  { rcases H U hU with âŸ¨V, hV, hVUâŸ©,
    filter_upwards [hV] using Î» x hx i, (hVU xâ‚€ (mem_of_mem_nhds hV) x hx i) }
end

/-- Uniform equicontinuity implies equicontinuity. -/
lemma uniform_equicontinuous.equicontinuous {F : Î¹ â†’ Î² â†’ Î±} (h : uniform_equicontinuous F) :
  equicontinuous F :=
Î» xâ‚€ U hU, mem_of_superset (ball_mem_nhds xâ‚€ (h U hU)) (Î» x hx i, hx i)

/-- Each function of a family equicontinuous at `xâ‚€` is continuous at `xâ‚€`. -/
lemma equicontinuous_at.continuous_at {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X} (h : equicontinuous_at F xâ‚€)
  (i : Î¹) : continuous_at (F i) xâ‚€ :=
begin
  intros U hU,
  rw uniform_space.mem_nhds_iff at hU,
  rcases hU with âŸ¨V, hVâ‚, hVâ‚‚âŸ©,
  exact mem_map.mpr (mem_of_superset (h V hVâ‚) (Î» x hx, hVâ‚‚ (hx i)))
end

protected lemma set.equicontinuous_at.continuous_at_of_mem {H : set $ X â†’ Î±} {xâ‚€ : X}
  (h : H.equicontinuous_at xâ‚€) {f : X â†’ Î±} (hf : f âˆˆ H) : continuous_at f xâ‚€ :=
h.continuous_at âŸ¨f, hfâŸ©

/-- Each function of an equicontinuous family is continuous. -/
lemma equicontinuous.continuous {F : Î¹ â†’ X â†’ Î±} (h : equicontinuous F) (i : Î¹) :
  continuous (F i) :=
continuous_iff_continuous_at.mpr (Î» x, (h x).continuous_at i)

protected lemma set.equicontinuous.continuous_of_mem {H : set $ X â†’ Î±} (h : H.equicontinuous)
  {f : X â†’ Î±} (hf : f âˆˆ H) : continuous f :=
h.continuous âŸ¨f, hfâŸ©

/-- Each function of a uniformly equicontinuous family is uniformly continuous. -/
lemma uniform_equicontinuous.uniform_continuous {F : Î¹ â†’ Î² â†’ Î±} (h : uniform_equicontinuous F)
  (i : Î¹) : uniform_continuous (F i) :=
Î» U hU, mem_map.mpr (mem_of_superset (h U hU) $ Î» xy hxy, (hxy i))

protected lemma set.uniform_equicontinuous.uniform_continuous_of_mem {H : set $ Î² â†’ Î±}
  (h : H.uniform_equicontinuous) {f : Î² â†’ Î±} (hf : f âˆˆ H) : uniform_continuous f :=
h.uniform_continuous âŸ¨f, hfâŸ©

/-- Taking sub-families preserves equicontinuity at a point. -/
lemma equicontinuous_at.comp {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X} (h : equicontinuous_at F xâ‚€) (u : Îº â†’ Î¹) :
  equicontinuous_at (F âˆ˜ u) xâ‚€ :=
Î» U hU, (h U hU).mono (Î» x H k, H (u k))

protected lemma set.equicontinuous_at.mono {H H' : set $ X â†’ Î±} {xâ‚€ : X}
  (h : H.equicontinuous_at xâ‚€) (hH : H' âŠ† H) : H'.equicontinuous_at xâ‚€ :=
h.comp (inclusion hH)

/-- Taking sub-families preserves equicontinuity. -/
lemma equicontinuous.comp {F : Î¹ â†’ X â†’ Î±} (h : equicontinuous F) (u : Îº â†’ Î¹) :
  equicontinuous (F âˆ˜ u) :=
Î» x, (h x).comp u

protected lemma set.equicontinuous.mono {H H' : set $ X â†’ Î±}
  (h : H.equicontinuous) (hH : H' âŠ† H) : H'.equicontinuous :=
h.comp (inclusion hH)

/-- Taking sub-families preserves uniform equicontinuity. -/
lemma uniform_equicontinuous.comp {F : Î¹ â†’ Î² â†’ Î±} (h : uniform_equicontinuous F) (u : Îº â†’ Î¹) :
  uniform_equicontinuous (F âˆ˜ u) :=
Î» U hU, (h U hU).mono (Î» x H k, H (u k))

protected lemma set.uniform_equicontinuous.mono {H H' : set $ Î² â†’ Î±}
  (h : H.uniform_equicontinuous) (hH : H' âŠ† H) : H'.uniform_equicontinuous :=
h.comp (inclusion hH)

/-- A family `ð“• : Î¹ â†’ X â†’ Î±` is equicontinuous at `xâ‚€` iff `range ð“•` is equicontinuous at `xâ‚€`,
i.e the family `coe : range F â†’ X â†’ Î±` is equicontinuous at `xâ‚€`. -/
lemma equicontinuous_at_iff_range {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X} :
  equicontinuous_at F xâ‚€ â†” equicontinuous_at (coe : range F â†’ X â†’ Î±) xâ‚€ :=
âŸ¨Î» h, by rw â† comp_range_splitting F; exact h.comp _, Î» h, h.comp (range_factorization F)âŸ©

/-- A family `ð“• : Î¹ â†’ X â†’ Î±` is equicontinuous iff `range ð“•` is equicontinuous,
i.e the family `coe : range F â†’ X â†’ Î±` is equicontinuous. -/
lemma equicontinuous_iff_range {F : Î¹ â†’ X â†’ Î±} :
  equicontinuous F â†” equicontinuous (coe : range F â†’ X â†’ Î±) :=
forall_congr (Î» xâ‚€, equicontinuous_at_iff_range)

/-- A family `ð“• : Î¹ â†’ Î² â†’ Î±` is uniformly equicontinuous iff `range ð“•` is uniformly equicontinuous,
i.e the family `coe : range F â†’ Î² â†’ Î±` is uniformly equicontinuous. -/
lemma uniform_equicontinuous_at_iff_range {F : Î¹ â†’ Î² â†’ Î±} :
  uniform_equicontinuous F â†” uniform_equicontinuous (coe : range F â†’ Î² â†’ Î±) :=
âŸ¨Î» h, by rw â† comp_range_splitting F; exact h.comp _, Î» h, h.comp (range_factorization F)âŸ©

section

open uniform_fun

/-- A family `ð“• : Î¹ â†’ X â†’ Î±` is equicontinuous at `xâ‚€` iff the function `swap ð“• : X â†’ Î¹ â†’ Î±` is
continuous at `xâ‚€` *when `Î¹ â†’ Î±` is equipped with the topology of uniform convergence*. This is
very useful for developping the equicontinuity API, but it should not be used directly for other
purposes. -/
lemma equicontinuous_at_iff_continuous_at {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X} :
  equicontinuous_at F xâ‚€ â†” continuous_at (of_fun âˆ˜ function.swap F : X â†’ Î¹ â†’áµ¤ Î±) xâ‚€ :=
by rw [continuous_at, (uniform_fun.has_basis_nhds Î¹ Î± _).tendsto_right_iff]; refl

/-- A family `ð“• : Î¹ â†’ X â†’ Î±` is equicontinuous iff the function `swap ð“• : X â†’ Î¹ â†’ Î±` is
continuous *when `Î¹ â†’ Î±` is equipped with the topology of uniform convergence*. This is
very useful for developping the equicontinuity API, but it should not be used directly for other
purposes. -/
lemma equicontinuous_iff_continuous {F : Î¹ â†’ X â†’ Î±} :
  equicontinuous F â†” continuous (of_fun âˆ˜ function.swap F : X â†’ Î¹ â†’áµ¤ Î±) :=
by simp_rw [equicontinuous, continuous_iff_continuous_at, equicontinuous_at_iff_continuous_at]

/-- A family `ð“• : Î¹ â†’ Î² â†’ Î±` is uniformly equicontinuous iff the function `swap ð“• : Î² â†’ Î¹ â†’ Î±` is
uniformly continuous *when `Î¹ â†’ Î±` is equipped with the uniform structure of uniform convergence*.
This is very useful for developping the equicontinuity API, but it should not be used directly
for other purposes. -/
lemma uniform_equicontinuous_iff_uniform_continuous {F : Î¹ â†’ Î² â†’ Î±} :
  uniform_equicontinuous F â†” uniform_continuous (of_fun âˆ˜ function.swap F : Î² â†’ Î¹ â†’áµ¤ Î±) :=
by rw [uniform_continuous, (uniform_fun.has_basis_uniformity Î¹ Î±).tendsto_right_iff]; refl

lemma filter.has_basis.equicontinuous_at_iff_left {Îº : Type*} {p : Îº â†’ Prop} {s : Îº â†’ set X}
  {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X} (hX : (ð“ xâ‚€).has_basis p s) : equicontinuous_at F xâ‚€ â†”
  âˆ€ U âˆˆ ð“¤ Î±, âˆƒ k (_ : p k), âˆ€ x âˆˆ s k, âˆ€ i, (F i xâ‚€, F i x) âˆˆ U :=
begin
  rw [equicontinuous_at_iff_continuous_at, continuous_at,
      hX.tendsto_iff (uniform_fun.has_basis_nhds Î¹ Î± _)],
  refl
end

lemma filter.has_basis.equicontinuous_at_iff_right {Îº : Type*} {p : Îº â†’ Prop} {s : Îº â†’ set (Î± Ã— Î±)}
  {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X} (hÎ± : (ð“¤ Î±).has_basis p s) : equicontinuous_at F xâ‚€ â†”
  âˆ€ k, p k â†’ âˆ€á¶  x in ð“ xâ‚€, âˆ€ i, (F i xâ‚€, F i x) âˆˆ s k :=
begin
  rw [equicontinuous_at_iff_continuous_at, continuous_at,
      (uniform_fun.has_basis_nhds_of_basis Î¹ Î± _ hÎ±).tendsto_right_iff],
  refl
end

lemma filter.has_basis.equicontinuous_at_iff {Îºâ‚ Îºâ‚‚ : Type*} {pâ‚ : Îºâ‚ â†’ Prop} {sâ‚ : Îºâ‚ â†’ set X}
  {pâ‚‚ : Îºâ‚‚ â†’ Prop} {sâ‚‚ : Îºâ‚‚ â†’ set (Î± Ã— Î±)} {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X}
  (hX : (ð“ xâ‚€).has_basis pâ‚ sâ‚) (hÎ± : (ð“¤ Î±).has_basis pâ‚‚ sâ‚‚) : equicontinuous_at F xâ‚€ â†”
  âˆ€ kâ‚‚, pâ‚‚ kâ‚‚ â†’ âˆƒ kâ‚ (_ : pâ‚ kâ‚), âˆ€ x âˆˆ sâ‚ kâ‚, âˆ€ i, (F i xâ‚€, F i x) âˆˆ sâ‚‚ kâ‚‚ :=
begin
  rw [equicontinuous_at_iff_continuous_at, continuous_at,
      hX.tendsto_iff (uniform_fun.has_basis_nhds_of_basis Î¹ Î± _ hÎ±)],
  refl
end

lemma filter.has_basis.uniform_equicontinuous_iff_left {Îº : Type*} {p : Îº â†’ Prop}
  {s : Îº â†’ set (Î² Ã— Î²)} {F : Î¹ â†’ Î² â†’ Î±} (hÎ² : (ð“¤ Î²).has_basis p s) : uniform_equicontinuous F â†”
  âˆ€ U âˆˆ ð“¤ Î±, âˆƒ k (_ : p k), âˆ€ x y, (x, y) âˆˆ s k â†’ âˆ€ i, (F i x, F i y) âˆˆ U :=
begin
  rw [uniform_equicontinuous_iff_uniform_continuous, uniform_continuous,
      hÎ².tendsto_iff (uniform_fun.has_basis_uniformity Î¹ Î±)],
  simp_rw [prod.forall],
  refl
end

lemma filter.has_basis.uniform_equicontinuous_iff_right {Îº : Type*} {p : Îº â†’ Prop}
  {s : Îº â†’ set (Î± Ã— Î±)} {F : Î¹ â†’ Î² â†’ Î±} (hÎ± : (ð“¤ Î±).has_basis p s) : uniform_equicontinuous F â†”
  âˆ€ k, p k â†’ âˆ€á¶  (xy : Î² Ã— Î²) in ð“¤ Î², âˆ€ i, (F i xy.1, F i xy.2) âˆˆ s k :=
begin
  rw [uniform_equicontinuous_iff_uniform_continuous, uniform_continuous,
      (uniform_fun.has_basis_uniformity_of_basis Î¹ Î± hÎ±).tendsto_right_iff],
  refl
end

lemma filter.has_basis.uniform_equicontinuous_iff {Îºâ‚ Îºâ‚‚ : Type*} {pâ‚ : Îºâ‚ â†’ Prop}
  {sâ‚ : Îºâ‚ â†’ set (Î² Ã— Î²)} {pâ‚‚ : Îºâ‚‚ â†’ Prop} {sâ‚‚ : Îºâ‚‚ â†’ set (Î± Ã— Î±)} {F : Î¹ â†’ Î² â†’ Î±}
  (hÎ² : (ð“¤ Î²).has_basis pâ‚ sâ‚) (hÎ± : (ð“¤ Î±).has_basis pâ‚‚ sâ‚‚) : uniform_equicontinuous F â†”
  âˆ€ kâ‚‚, pâ‚‚ kâ‚‚ â†’ âˆƒ kâ‚ (_ : pâ‚ kâ‚), âˆ€ x y, (x, y) âˆˆ sâ‚ kâ‚ â†’ âˆ€ i, (F i x, F i y) âˆˆ sâ‚‚ kâ‚‚ :=
begin
  rw [uniform_equicontinuous_iff_uniform_continuous, uniform_continuous,
      hÎ².tendsto_iff (uniform_fun.has_basis_uniformity_of_basis Î¹ Î± hÎ±)],
  simp_rw [prod.forall],
  refl
end

/-- Given `u : Î± â†’ Î²` a uniform inducing map, a family `ð“• : Î¹ â†’ X â†’ Î±` is equicontinuous at a point
`xâ‚€ : X` iff the family `ð“•'`, obtained by precomposing each function of `ð“•` by `u`, is
equicontinuous at `xâ‚€`. -/
lemma uniform_inducing.equicontinuous_at_iff {F : Î¹ â†’ X â†’ Î±} {xâ‚€ : X} {u : Î± â†’ Î²}
  (hu : uniform_inducing u) :
  equicontinuous_at F xâ‚€ â†” equicontinuous_at (((âˆ˜) u) âˆ˜ F) xâ‚€ :=
begin
  have := (uniform_fun.postcomp_uniform_inducing hu).inducing,
  rw [equicontinuous_at_iff_continuous_at, equicontinuous_at_iff_continuous_at,
      this.continuous_at_iff],
  refl
end

/-- Given `u : Î± â†’ Î²` a uniform inducing map, a family `ð“• : Î¹ â†’ X â†’ Î±` is equicontinuous iff the
family `ð“•'`, obtained by precomposing each function of `ð“•` by `u`, is equicontinuous. -/
lemma uniform_inducing.equicontinuous_iff {F : Î¹ â†’ X â†’ Î±} {u : Î± â†’ Î²}
  (hu : uniform_inducing u) :
  equicontinuous F â†” equicontinuous (((âˆ˜) u) âˆ˜ F) :=
begin
  congrm (âˆ€ x, (_ : Prop)),
  rw hu.equicontinuous_at_iff
end

/-- Given `u : Î± â†’ Î³` a uniform inducing map, a family `ð“• : Î¹ â†’ Î² â†’ Î±` is uniformly equicontinuous
iff the family `ð“•'`, obtained by precomposing each function of `ð“•` by `u`, is uniformly
equicontinuous. -/
lemma uniform_inducing.uniform_equicontinuous_iff {F : Î¹ â†’ Î² â†’ Î±} {u : Î± â†’ Î³}
  (hu : uniform_inducing u) :
  uniform_equicontinuous F â†” uniform_equicontinuous (((âˆ˜) u) âˆ˜ F) :=
begin
  have := uniform_fun.postcomp_uniform_inducing hu,
  rw [uniform_equicontinuous_iff_uniform_continuous, uniform_equicontinuous_iff_uniform_continuous,
      this.uniform_continuous_iff],
  refl
end

/-- A version of `equicontinuous_at.closure` applicable to subsets of types which embed continuously
into `X â†’ Î±` with the product topology. It turns out we don't need any other condition on the
embedding than continuity, but in practice this will mostly be applied to `fun_like` types where
the coercion is injective. -/
lemma equicontinuous_at.closure' {A : set Y} {u : Y â†’ X â†’ Î±} {xâ‚€ : X}
  (hA : equicontinuous_at (u âˆ˜ coe : A â†’ X â†’ Î±) xâ‚€) (hu : continuous u) :
  equicontinuous_at (u âˆ˜ coe : closure A â†’ X â†’ Î±) xâ‚€ :=
begin
  intros U hU,
  rcases mem_uniformity_is_closed hU with âŸ¨V, hV, hVclosed, hVUâŸ©,
  filter_upwards [hA V hV] with x hx,
  rw set_coe.forall at *,
  change A âŠ† (Î» f, (u f xâ‚€, u f x)) â»Â¹' V at hx,
  refine (closure_minimal hx $ hVclosed.preimage $ _).trans (preimage_mono hVU),
  exact continuous.prod_mk ((continuous_apply xâ‚€).comp hu) ((continuous_apply x).comp hu)
end

/-- If a set of functions is equicontinuous at some `xâ‚€`, its closure for the product topology is
also equicontinuous at `xâ‚€`. -/
lemma equicontinuous_at.closure {A : set $ X â†’ Î±} {xâ‚€ : X} (hA : A.equicontinuous_at xâ‚€) :
  (closure A).equicontinuous_at xâ‚€ :=
@equicontinuous_at.closure' _ _ _ _ _ _ _ id _ hA continuous_id

/-- If `ð“• : Î¹ â†’ X â†’ Î±` tends to `f : X â†’ Î±` *pointwise* along some nontrivial filter, and if the
family `ð“•` is equicontinuous at some `xâ‚€ : X`, then the limit is continuous at `xâ‚€`. -/
lemma filter.tendsto.continuous_at_of_equicontinuous_at {l : filter Î¹} [l.ne_bot] {F : Î¹ â†’ X â†’ Î±}
  {f : X â†’ Î±} {xâ‚€ : X} (hâ‚ : tendsto F l (ð“ f)) (hâ‚‚ : equicontinuous_at F xâ‚€) :
  continuous_at f xâ‚€ :=
(equicontinuous_at_iff_range.mp hâ‚‚).closure.continuous_at
  âŸ¨f, mem_closure_of_tendsto hâ‚ $ eventually_of_forall mem_range_selfâŸ©

/-- A version of `equicontinuous.closure` applicable to subsets of types which embed continuously
into `X â†’ Î±` with the product topology. It turns out we don't need any other condition on the
embedding than continuity, but in practice this will mostly be applied to `fun_like` types where
the coercion is injective. -/
lemma equicontinuous.closure' {A : set Y} {u : Y â†’ X â†’ Î±}
  (hA : equicontinuous (u âˆ˜ coe : A â†’ X â†’ Î±)) (hu : continuous u) :
  equicontinuous (u âˆ˜ coe : closure A â†’ X â†’ Î±) :=
Î» x, (hA x).closure' hu

/-- If a set of functions is equicontinuous, its closure for the product topology is also
equicontinuous. -/
lemma equicontinuous.closure {A : set $ X â†’ Î±} (hA : A.equicontinuous) :
  (closure A).equicontinuous :=
Î» x, (hA x).closure

/-- If `ð“• : Î¹ â†’ X â†’ Î±` tends to `f : X â†’ Î±` *pointwise* along some nontrivial filter, and if the
family `ð“•` is equicontinuous, then the limit is continuous. -/
lemma filter.tendsto.continuous_of_equicontinuous_at {l : filter Î¹} [l.ne_bot] {F : Î¹ â†’ X â†’ Î±}
  {f : X â†’ Î±} (hâ‚ : tendsto F l (ð“ f)) (hâ‚‚ : equicontinuous F) :
  continuous f :=
continuous_iff_continuous_at.mpr (Î» x, hâ‚.continuous_at_of_equicontinuous_at (hâ‚‚ x))

/-- A version of `uniform_equicontinuous.closure` applicable to subsets of types which embed
continuously into `Î² â†’ Î±` with the product topology. It turns out we don't need any other condition
on the embedding than continuity, but in practice this will mostly be applied to `fun_like` types
where the coercion is injective. -/
lemma uniform_equicontinuous.closure' {A : set Y} {u : Y â†’ Î² â†’ Î±}
  (hA : uniform_equicontinuous (u âˆ˜ coe : A â†’ Î² â†’ Î±)) (hu : continuous u) :
  uniform_equicontinuous (u âˆ˜ coe : closure A â†’ Î² â†’ Î±) :=
begin
  intros U hU,
  rcases mem_uniformity_is_closed hU with âŸ¨V, hV, hVclosed, hVUâŸ©,
  filter_upwards [hA V hV],
  rintros âŸ¨x, yâŸ© hxy,
  rw set_coe.forall at *,
  change A âŠ† (Î» f, (u f x, u f y)) â»Â¹' V at hxy,
  refine (closure_minimal hxy $ hVclosed.preimage $ _).trans (preimage_mono hVU),
  exact continuous.prod_mk ((continuous_apply x).comp hu) ((continuous_apply y).comp hu)
end

/-- If a set of functions is uniformly equicontinuous, its closure for the product topology is also
uniformly equicontinuous. -/
lemma uniform_equicontinuous.closure {A : set $ Î² â†’ Î±} (hA : A.uniform_equicontinuous) :
  (closure A).uniform_equicontinuous :=
@uniform_equicontinuous.closure' _ _ _ _ _ _ _ id hA continuous_id

/-- If `ð“• : Î¹ â†’ Î² â†’ Î±` tends to `f : Î² â†’ Î±` *pointwise* along some nontrivial filter, and if the
family `ð“•` is uniformly equicontinuous, then the limit is uniformly continuous. -/
lemma filter.tendsto.uniform_continuous_of_uniform_equicontinuous {l : filter Î¹} [l.ne_bot]
  {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} (hâ‚ : tendsto F l (ð“ f)) (hâ‚‚ : uniform_equicontinuous F) :
  uniform_continuous f :=
(uniform_equicontinuous_at_iff_range.mp hâ‚‚).closure.uniform_continuous
  âŸ¨f, mem_closure_of_tendsto hâ‚ $ eventually_of_forall mem_range_selfâŸ©

end

end
