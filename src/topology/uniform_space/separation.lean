/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Patrick Massot
-/

import tactic.apply_fun
import topology.uniform_space.basic
import topology.separation

/-!
# Hausdorff properties of uniform spaces. Separation quotient.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file studies uniform spaces whose underlying topological spaces are separated
(also known as Hausdorff or Tâ‚‚).
This turns out to be equivalent to asking that the intersection of all entourages
is the diagonal only. This condition actually implies the stronger separation property
that the space is Tâ‚ƒ, hence those conditions are equivalent for topologies coming from
a uniform structure.

More generally, the intersection `ğ“¢ X` of all entourages of `X`, which has type `set (X Ã— X)` is an
equivalence relation on `X`. Points which are equivalent under the relation are basically
undistinguishable from the point of view of the uniform structure. For instance any uniformly
continuous function will send equivalent points to the same value.

The quotient `separation_quotient X` of `X` by `ğ“¢ X` has a natural uniform structure which is
separated, and satisfies a universal property: every uniformly continuous function
from `X` to a separated uniform space uniquely factors through `separation_quotient X`.
As usual, this allows to turn `separation_quotient` into a functor (but we don't use the
category theory library in this file).

These notions admit relative versions, one can ask that `s : set X` is separated, this
is equivalent to asking that the uniform structure induced on `s` is separated.

## Main definitions

* `separation_relation X : set (X Ã— X)`: the separation relation
* `separated_space X`: a predicate class asserting that `X` is separated
* `separation_quotient X`: the maximal separated quotient of `X`.
* `separation_quotient.lift f`: factors a map `f : X â†’ Y` through the separation quotient of `X`.
* `separation_quotient.map f`: turns a map `f : X â†’ Y` into a map between the separation quotients
  of `X` and `Y`.

## Main results

* `separated_iff_t2`: the equivalence between being separated and being Hausdorff for uniform
  spaces.
* `separation_quotient.uniform_continuous_lift`: factoring a uniformly continuous map through the
  separation quotient gives a uniformly continuous map.
* `separation_quotient.uniform_continuous_map`: maps induced between separation quotients are
  uniformly continuous.

##Â Notations

Localized in `uniformity`, we have the notation `ğ“¢ X` for the separation relation
on a uniform space `X`,

## Implementation notes

The separation setoid `separation_setoid` is not declared as a global instance.
It is made a local instance while building the theory of `separation_quotient`.
The factored map `separation_quotient.lift f` is defined without imposing any condition on
`f`, but returns junk if `f` is not uniformly continuous (constant junk hence it is always
uniformly continuous).

-/

open filter topological_space set classical function uniform_space
open_locale classical topology uniformity filter
noncomputable theory
set_option eqn_compiler.zeta true

universes u v w
variables {Î± : Type u} {Î² : Type v} {Î³ : Type w}
variables [uniform_space Î±] [uniform_space Î²] [uniform_space Î³]


/-!
### Separated uniform spaces
-/

@[priority 100]
instance uniform_space.to_regular_space : regular_space Î± :=
regular_space.of_basis
  (Î» a, by { rw [nhds_eq_comap_uniformity], exact uniformity_has_basis_closed.comap _ })
  (Î» a V hV, hV.2.preimage $ continuous_const.prod_mk continuous_id)

/-- The separation relation is the intersection of all entourages.
  Two points which are related by the separation relation are "indistinguishable"
  according to the uniform structure. -/
protected def separation_rel (Î± : Type u) [u : uniform_space Î±] :=
â‹‚â‚€ (ğ“¤ Î±).sets

localized "notation (name := separation_rel) `ğ“¢` := separation_rel" in uniformity

lemma separated_equiv : equivalence (Î»x y, (x, y) âˆˆ ğ“¢ Î±) :=
âŸ¨assume x, assume s, refl_mem_uniformity,
  assume x y, assume h (s : set (Î±Ã—Î±)) hs,
    have preimage prod.swap s âˆˆ ğ“¤ Î±,
      from symm_le_uniformity hs,
    h _ this,
  assume x y z (hxy : (x, y) âˆˆ ğ“¢ Î±) (hyz : (y, z) âˆˆ ğ“¢ Î±)
      s (hs : s âˆˆ ğ“¤ Î±),
    let âŸ¨t, ht, (h_ts : comp_rel t t âŠ† s)âŸ© := comp_mem_uniformity_sets hs in
    h_ts $ show (x, z) âˆˆ comp_rel t t,
      from âŸ¨y, hxy t ht, hyz t htâŸ©âŸ©

lemma filter.has_basis.mem_separation_rel {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set (Î± Ã— Î±)}
  (h : (ğ“¤ Î±).has_basis p s) {a : Î± Ã— Î±} :
  a âˆˆ ğ“¢ Î± â†” âˆ€ i, p i â†’ a âˆˆ s i :=
h.forall_mem_mem

theorem separation_rel_iff_specializes {a b : Î±} : (a, b) âˆˆ ğ“¢ Î± â†” a â¤³ b :=
by simp only [(ğ“¤ Î±).basis_sets.mem_separation_rel, id, mem_set_of_eq,
  (nhds_basis_uniformity (ğ“¤ Î±).basis_sets).specializes_iff]

theorem separation_rel_iff_inseparable {a b : Î±} : (a, b) âˆˆ ğ“¢ Î± â†” inseparable a b :=
  separation_rel_iff_specializes.trans specializes_iff_inseparable

/-- A uniform space is separated if its separation relation is trivial (each point
is related only to itself). -/
class separated_space (Î± : Type u) [uniform_space Î±] : Prop := (out : ğ“¢ Î± = id_rel)

theorem separated_space_iff {Î± : Type u} [uniform_space Î±] :
  separated_space Î± â†” ğ“¢ Î± = id_rel :=
âŸ¨Î» h, h.1, Î» h, âŸ¨hâŸ©âŸ©

theorem separated_def {Î± : Type u} [uniform_space Î±] :
  separated_space Î± â†” âˆ€ x y, (âˆ€ r âˆˆ ğ“¤ Î±, (x, y) âˆˆ r) â†’ x = y :=
by simp [separated_space_iff, id_rel_subset.2 separated_equiv.1, subset.antisymm_iff];
   simp [subset_def, separation_rel]

theorem separated_def' {Î± : Type u} [uniform_space Î±] :
  separated_space Î± â†” âˆ€ x y, x â‰  y â†’ âˆƒ r âˆˆ ğ“¤ Î±, (x, y) âˆ‰ r :=
separated_def.trans $ forallâ‚‚_congr $ Î» x y, by rw â† not_imp_not; simp [not_forall]

lemma eq_of_uniformity {Î± : Type*} [uniform_space Î±] [separated_space Î±] {x y : Î±}
  (h : âˆ€ {V}, V âˆˆ ğ“¤ Î± â†’ (x, y) âˆˆ V) : x = y :=
separated_def.mp â€¹separated_space Î±â€º x y (Î» _, h)

lemma eq_of_uniformity_basis {Î± : Type*} [uniform_space Î±] [separated_space Î±] {Î¹ : Type*}
  {p : Î¹ â†’ Prop} {s : Î¹ â†’ set (Î± Ã— Î±)} (hs : (ğ“¤ Î±).has_basis p s) {x y : Î±}
  (h : âˆ€ {i}, p i â†’ (x, y) âˆˆ s i) : x = y :=
eq_of_uniformity (Î» V V_in, let âŸ¨i, hi, HâŸ© := hs.mem_iff.mp V_in in H (h hi))

lemma eq_of_forall_symmetric {Î± : Type*} [uniform_space Î±] [separated_space Î±] {x y : Î±}
  (h : âˆ€ {V}, V âˆˆ ğ“¤ Î± â†’ symmetric_rel V â†’ (x, y) âˆˆ V) : x = y :=
eq_of_uniformity_basis has_basis_symmetric (by simpa [and_imp] using Î» _, h)

lemma eq_of_cluster_pt_uniformity [separated_space Î±] {x y : Î±} (h : cluster_pt (x, y) (ğ“¤ Î±)) :
  x = y :=
eq_of_uniformity_basis uniformity_has_basis_closed $ Î» V âŸ¨hV, hVcâŸ©,
  is_closed_iff_cluster_pt.1 hVc _ $ h.mono $ le_principal_iff.2 hV

lemma id_rel_sub_separation_relation (Î± : Type*) [uniform_space Î±] : id_rel âŠ† ğ“¢ Î± :=
begin
  unfold separation_rel,
  rw id_rel_subset,
  intros x,
  suffices : âˆ€ t âˆˆ ğ“¤ Î±, (x, x) âˆˆ t, by simpa only [refl_mem_uniformity],
  exact Î» t, refl_mem_uniformity,
end

lemma separation_rel_comap  {f : Î± â†’ Î²}
  (h : â€¹uniform_space Î±â€º = uniform_space.comap f â€¹uniform_space Î²â€º) :
  ğ“¢ Î± = (prod.map f f) â»Â¹' ğ“¢ Î² :=
begin
  unfreezingI { subst h },
  dsimp [separation_rel],
  simp_rw [uniformity_comap, (filter.comap_has_basis (prod.map f f) (ğ“¤ Î²)).sInter_sets,
      â† preimage_Inter, sInter_eq_bInter],
  refl,
end

protected lemma filter.has_basis.separation_rel {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set (Î± Ã— Î±)}
  (h : has_basis (ğ“¤ Î±) p s) :
  ğ“¢ Î± = â‹‚ i (hi : p i), s i :=
by { unfold separation_rel, rw h.sInter_sets }

lemma separation_rel_eq_inter_closure : ğ“¢ Î± = â‹‚â‚€ (closure '' (ğ“¤ Î±).sets) :=
by simp [uniformity_has_basis_closure.separation_rel]

lemma is_closed_separation_rel : is_closed (ğ“¢ Î±) :=
begin
  rw separation_rel_eq_inter_closure,
  apply is_closed_sInter,
  rintros _ âŸ¨t, t_in, rflâŸ©,
  exact is_closed_closure,
end

lemma separated_iff_t2 : separated_space Î± â†” t2_space Î± :=
begin
  classical,
  split ; introI h,
  { rw [t2_iff_is_closed_diagonal, â† show ğ“¢ Î± = diagonal Î±, from h.1],
    exact is_closed_separation_rel },
  { rw separated_def',
    intros x y hxy,
    rcases t2_separation hxy with âŸ¨u, v, uo, vo, hx, hy, hâŸ©,
    rcases is_open_iff_ball_subset.1 uo x hx with âŸ¨r, hrU, hrâŸ©,
    exact âŸ¨r, hrU, Î» H, h.le_bot âŸ¨hr H, hyâŸ©âŸ© }
end

@[priority 100] -- see Note [lower instance priority]
instance separated_t3 [separated_space Î±] : t3_space Î± :=
by { haveI := separated_iff_t2.mp â€¹_â€º, exact âŸ¨âŸ© }

instance subtype.separated_space [separated_space Î±] (s : set Î±) : separated_space s :=
separated_iff_t2.mpr subtype.t2_space

lemma is_closed_of_spaced_out [separated_space Î±] {Vâ‚€ : set (Î± Ã— Î±)} (Vâ‚€_in : Vâ‚€ âˆˆ ğ“¤ Î±)
  {s : set Î±} (hs : s.pairwise (Î» x y, (x, y) âˆ‰ Vâ‚€)) : is_closed s :=
begin
  rcases comp_symm_mem_uniformity_sets Vâ‚€_in with âŸ¨Vâ‚, Vâ‚_in, Vâ‚_symm, h_compâŸ©,
  apply is_closed_of_closure_subset,
  intros x hx,
  rw mem_closure_iff_ball at hx,
  rcases hx Vâ‚_in with âŸ¨y, hy, hy'âŸ©,
  suffices : x = y, by rwa this,
  apply eq_of_forall_symmetric,
  intros V V_in V_symm,
  rcases hx (inter_mem Vâ‚_in V_in) with âŸ¨z, hz, hz'âŸ©,
  obtain rfl : z = y,
  { by_contra hzy,
    exact hs hz' hy' hzy (h_comp $ mem_comp_of_mem_ball Vâ‚_symm (ball_inter_left x _ _ hz) hy) },
  exact ball_inter_right x _ _ hz
end

lemma is_closed_range_of_spaced_out {Î¹} [separated_space Î±] {Vâ‚€ : set (Î± Ã— Î±)} (Vâ‚€_in : Vâ‚€ âˆˆ ğ“¤ Î±)
  {f : Î¹ â†’ Î±} (hf : pairwise (Î» x y, (f x, f y) âˆ‰ Vâ‚€)) : is_closed (range f) :=
is_closed_of_spaced_out Vâ‚€_in $
  by { rintro _ âŸ¨x, rflâŸ© _ âŸ¨y, rflâŸ© h, exact hf (ne_of_apply_ne f h) }


/-!
### Separation quotient
-/
namespace uniform_space

/-- The separation relation of a uniform space seen as a setoid. -/
def separation_setoid (Î± : Type u) [uniform_space Î±] : setoid Î± :=
âŸ¨Î»x y, (x, y) âˆˆ ğ“¢ Î±, separated_equivâŸ©

local attribute [instance] separation_setoid

instance separation_setoid.uniform_space {Î± : Type u} [u : uniform_space Î±] :
  uniform_space (quotient (separation_setoid Î±)) :=
{ to_topological_space := u.to_topological_space.coinduced (Î»x, âŸ¦xâŸ§),
  uniformity := map (Î»p:(Î±Ã—Î±), (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§)) u.uniformity,
  refl := le_trans (by simp [quotient.exists_rep]) (filter.map_mono refl_le_uniformity),
  symm := tendsto_map' $
    by simp [prod.swap, (âˆ˜)]; exact tendsto_map.comp tendsto_swap_uniformity,
  comp := calc (map (Î» (p : Î± Ã— Î±), (âŸ¦p.fstâŸ§, âŸ¦p.sndâŸ§)) u.uniformity).lift' (Î»s, comp_rel s s) =
          u.uniformity.lift' ((Î»s, comp_rel s s) âˆ˜ image (Î» (p : Î± Ã— Î±), (âŸ¦p.fstâŸ§, âŸ¦p.sndâŸ§))) :
      map_lift'_eq2 $ monotone_id.comp_rel monotone_id
    ... â‰¤ u.uniformity.lift' (image (Î» (p : Î± Ã— Î±), (âŸ¦p.fstâŸ§, âŸ¦p.sndâŸ§)) âˆ˜
            (Î»s:set (Î±Ã—Î±), comp_rel s (comp_rel s s))) :
      lift'_mono' $ assume s hs âŸ¨a, bâŸ© âŸ¨c, âŸ¨âŸ¨aâ‚, aâ‚‚âŸ©, ha, a_eqâŸ©, âŸ¨âŸ¨bâ‚, bâ‚‚âŸ©, hb, b_eqâŸ©âŸ©,
      begin
        simp at a_eq,
        simp at b_eq,
        have h : âŸ¦aâ‚‚âŸ§ = âŸ¦bâ‚âŸ§, { rw [a_eq.right, b_eq.left] },
        have h : (aâ‚‚, bâ‚) âˆˆ ğ“¢ Î± := quotient.exact h,
        simp [function.comp, set.image, comp_rel, and.comm, and.left_comm, and.assoc],
        exact âŸ¨aâ‚, a_eq.left, bâ‚‚, b_eq.right, aâ‚‚, ha, bâ‚, h s hs, hbâŸ©
      end
    ... = map (Î»p:(Î±Ã—Î±), (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§))
            (u.uniformity.lift' (Î»s:set (Î±Ã—Î±), comp_rel s (comp_rel s s))) :
      by rw [map_lift'_eq];
        exact monotone_id.comp_rel (monotone_id.comp_rel monotone_id)
    ... â‰¤ map (Î»p:(Î±Ã—Î±), (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§)) u.uniformity :
      map_mono comp_le_uniformity3,
  is_open_uniformity := assume s,
    have âˆ€a, âŸ¦aâŸ§ âˆˆ s â†’
        ({p:Î±Ã—Î± | p.1 = a â†’ âŸ¦p.2âŸ§ âˆˆ s} âˆˆ ğ“¤ Î± â†”
          {p:Î±Ã—Î± | p.1 â‰ˆ a â†’ âŸ¦p.2âŸ§ âˆˆ s} âˆˆ ğ“¤ Î±),
      from assume a ha,
      âŸ¨assume h,
        let âŸ¨t, ht, htsâŸ© := comp_mem_uniformity_sets h in
        have hts : âˆ€{aâ‚ aâ‚‚}, (a, aâ‚) âˆˆ t â†’ (aâ‚, aâ‚‚) âˆˆ t â†’ âŸ¦aâ‚‚âŸ§ âˆˆ s,
          from assume aâ‚ aâ‚‚ haâ‚ haâ‚‚, @hts (a, aâ‚‚) âŸ¨aâ‚, haâ‚, haâ‚‚âŸ© rfl,
        have ht' : âˆ€{aâ‚ aâ‚‚}, aâ‚ â‰ˆ aâ‚‚ â†’ (aâ‚, aâ‚‚) âˆˆ t,
          from assume aâ‚ aâ‚‚ h, sInter_subset_of_mem ht h,
        u.uniformity.sets_of_superset ht $ assume âŸ¨aâ‚, aâ‚‚âŸ© hâ‚ hâ‚‚, hts (ht' $ setoid.symm hâ‚‚) hâ‚,
        assume h, u.uniformity.sets_of_superset h $ by simp {contextual := tt}âŸ©,
    begin
      simp only [is_open_coinduced, is_open_uniformity, uniformity, forall_quotient_iff,
        mem_preimage, mem_map, preimage_set_of_eq, quotient.eq],
      exact âŸ¨Î»h a ha, (this a ha).mp $ h a ha, Î»h a ha, (this a ha).mpr $ h a haâŸ©
    end }

lemma uniformity_quotient :
  ğ“¤ (quotient (separation_setoid Î±)) = (ğ“¤ Î±).map (Î»p:(Î±Ã—Î±), (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§)) :=
rfl

lemma uniform_continuous_quotient_mk :
  uniform_continuous (quotient.mk : Î± â†’ quotient (separation_setoid Î±)) :=
le_rfl

lemma uniform_continuous_quotient {f : quotient (separation_setoid Î±) â†’ Î²}
  (hf : uniform_continuous (Î»x, f âŸ¦xâŸ§)) : uniform_continuous f :=
hf

lemma uniform_continuous_quotient_lift
  {f : Î± â†’ Î²} {h : âˆ€a b, (a, b) âˆˆ ğ“¢ Î± â†’ f a = f b}
  (hf : uniform_continuous f) : uniform_continuous (Î»a, quotient.lift f h a) :=
uniform_continuous_quotient hf

lemma uniform_continuous_quotient_liftâ‚‚
  {f : Î± â†’ Î² â†’ Î³} {h : âˆ€a c b d, (a, b) âˆˆ ğ“¢ Î± â†’ (c, d) âˆˆ ğ“¢ Î² â†’ f a c = f b d}
  (hf : uniform_continuous (Î»p:Î±Ã—Î², f p.1 p.2)) :
  uniform_continuous (Î»p:_Ã—_, quotient.liftâ‚‚ f h p.1 p.2) :=
begin
  rw [uniform_continuous, uniformity_prod_eq_prod, uniformity_quotient, uniformity_quotient,
    filter.prod_map_map_eq, filter.tendsto_map'_iff, filter.tendsto_map'_iff],
  rwa [uniform_continuous, uniformity_prod_eq_prod, filter.tendsto_map'_iff] at hf
end

lemma comap_quotient_le_uniformity :
  (ğ“¤ $ quotient $ separation_setoid Î±).comap (Î» (p : Î± Ã— Î±), (âŸ¦p.fstâŸ§, âŸ¦p.sndâŸ§)) â‰¤ (ğ“¤ Î±) :=
assume t' ht',
let âŸ¨t, ht, tt_t'âŸ© := comp_mem_uniformity_sets ht' in
let âŸ¨s, hs, ss_tâŸ© := comp_mem_uniformity_sets ht in
âŸ¨(Î»p:Î±Ã—Î±, (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§)) '' s,
  (ğ“¤ Î±).sets_of_superset hs $ assume x hx, âŸ¨x, hx, rflâŸ©,
  assume âŸ¨aâ‚, aâ‚‚âŸ© âŸ¨âŸ¨bâ‚, bâ‚‚âŸ©, hb, ab_eqâŸ©,
  have âŸ¦bâ‚âŸ§ = âŸ¦aâ‚âŸ§ âˆ§ âŸ¦bâ‚‚âŸ§ = âŸ¦aâ‚‚âŸ§, from prod.mk.inj ab_eq,
  have bâ‚ â‰ˆ aâ‚ âˆ§ bâ‚‚ â‰ˆ aâ‚‚, from and.imp quotient.exact quotient.exact this,
  have abâ‚ : (aâ‚, bâ‚) âˆˆ t, from (setoid.symm this.left) t ht,
  have baâ‚‚ : (bâ‚‚, aâ‚‚) âˆˆ s, from this.right s hs,
  tt_t' âŸ¨bâ‚, show ((aâ‚, aâ‚‚).1, bâ‚) âˆˆ t, from abâ‚,
    ss_t âŸ¨bâ‚‚, show ((bâ‚, aâ‚‚).1, bâ‚‚) âˆˆ s, from hb, baâ‚‚âŸ©âŸ©âŸ©

lemma comap_quotient_eq_uniformity :
  (ğ“¤ $ quotient $ separation_setoid Î±).comap (Î» (p : Î± Ã— Î±), (âŸ¦p.fstâŸ§, âŸ¦p.sndâŸ§)) = ğ“¤ Î± :=
le_antisymm comap_quotient_le_uniformity le_comap_map


instance separated_separation : separated_space (quotient (separation_setoid Î±)) :=
âŸ¨set.ext $ assume âŸ¨a, bâŸ©, quotient.induction_onâ‚‚ a b $ assume a b,
  âŸ¨assume h,
    have a â‰ˆ b, from assume s hs,
      have s âˆˆ (ğ“¤ $ quotient $ separation_setoid Î±).comap (Î»p:(Î±Ã—Î±), (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§)),
        from comap_quotient_le_uniformity hs,
      let âŸ¨t, ht, htsâŸ© := this in
      hts begin dsimp [preimage], exact h t ht end,
    show âŸ¦aâŸ§ = âŸ¦bâŸ§, from quotient.sound this,

  assume heq : âŸ¦aâŸ§ = âŸ¦bâŸ§, assume h hs,
  heq â–¸ refl_mem_uniformity hsâŸ©âŸ©

lemma separated_of_uniform_continuous {f : Î± â†’ Î²} {x y : Î±}
  (H : uniform_continuous f) (h : x â‰ˆ y) : f x â‰ˆ f y :=
assume _ h', h _ (H h')

lemma eq_of_separated_of_uniform_continuous [separated_space Î²] {f : Î± â†’ Î²} {x y : Î±}
  (H : uniform_continuous f) (h : x â‰ˆ y) : f x = f y :=
separated_def.1 (by apply_instance) _ _ $ separated_of_uniform_continuous H h

/-- The maximal separated quotient of a uniform space `Î±`. -/
def separation_quotient (Î± : Type*) [uniform_space Î±] := quotient (separation_setoid Î±)

namespace separation_quotient
instance : uniform_space (separation_quotient Î±) := separation_setoid.uniform_space
instance : separated_space (separation_quotient Î±) := uniform_space.separated_separation
instance [inhabited Î±] : inhabited (separation_quotient Î±) :=
quotient.inhabited (separation_setoid Î±)

lemma mk_eq_mk {x y : Î±} : (âŸ¦xâŸ§ : separation_quotient Î±) = âŸ¦yâŸ§ â†” inseparable x y :=
quotient.eq'.trans separation_rel_iff_inseparable

/-- Factoring functions to a separated space through the separation quotient. -/
def lift [separated_space Î²] (f : Î± â†’ Î²) : (separation_quotient Î± â†’ Î²) :=
if h : uniform_continuous f then
  quotient.lift f (Î» x y, eq_of_separated_of_uniform_continuous h)
else
  Î» x, f (nonempty.some âŸ¨x.outâŸ©)

lemma lift_mk [separated_space Î²] {f : Î± â†’ Î²} (h : uniform_continuous f) (a : Î±) :
  lift f âŸ¦aâŸ§ = f a :=
by rw [lift, dif_pos h]; refl

lemma uniform_continuous_lift [separated_space Î²] (f : Î± â†’ Î²) : uniform_continuous (lift f) :=
begin
  by_cases hf : uniform_continuous f,
  { rw [lift, dif_pos hf], exact uniform_continuous_quotient_lift hf },
  { rw [lift, dif_neg hf], exact uniform_continuous_of_const (assume a b, rfl) }
end

/-- The separation quotient functor acting on functions. -/
def map (f : Î± â†’ Î²) : separation_quotient Î± â†’ separation_quotient Î² :=
lift (quotient.mk âˆ˜ f)

lemma map_mk {f : Î± â†’ Î²} (h : uniform_continuous f) (a : Î±) : map f âŸ¦aâŸ§ = âŸ¦f aâŸ§ :=
by rw [map, lift_mk (uniform_continuous_quotient_mk.comp h)]

lemma uniform_continuous_map (f : Î± â†’ Î²) : uniform_continuous (map f) :=
uniform_continuous_lift (quotient.mk âˆ˜ f)

lemma map_unique {f : Î± â†’ Î²} (hf : uniform_continuous f)
  {g : separation_quotient Î± â†’ separation_quotient Î²}
  (comm : quotient.mk âˆ˜ f = g âˆ˜ quotient.mk) : map f = g :=
by ext âŸ¨aâŸ©;
calc map f âŸ¦aâŸ§ = âŸ¦f aâŸ§ : map_mk hf a
  ... = g âŸ¦aâŸ§ : congr_fun comm a

lemma map_id : map (@id Î±) = id :=
map_unique uniform_continuous_id rfl

lemma map_comp {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : uniform_continuous f) (hg : uniform_continuous g) :
  map g âˆ˜ map f = map (g âˆ˜ f) :=
(map_unique (hg.comp hf) $ by simp only [(âˆ˜), map_mk, hf, hg]).symm

end separation_quotient

lemma separation_prod {aâ‚ aâ‚‚ : Î±} {bâ‚ bâ‚‚ : Î²} : (aâ‚, bâ‚) â‰ˆ (aâ‚‚, bâ‚‚) â†” aâ‚ â‰ˆ aâ‚‚ âˆ§ bâ‚ â‰ˆ bâ‚‚ :=
begin
  split,
  { assume h,
    exact âŸ¨separated_of_uniform_continuous uniform_continuous_fst h,
           separated_of_uniform_continuous uniform_continuous_snd hâŸ© },
  { rintros âŸ¨eqv_Î±, eqv_Î²âŸ© r r_in,
    rw uniformity_prod at r_in,
    rcases r_in with âŸ¨t_Î±, âŸ¨r_Î±, r_Î±_in, h_Î±âŸ©, t_Î², âŸ¨r_Î², r_Î²_in, h_Î²âŸ©, rflâŸ©,
    let p_Î± := Î»(p : (Î± Ã— Î²) Ã— (Î± Ã— Î²)), (p.1.1, p.2.1),
    let p_Î² := Î»(p : (Î± Ã— Î²) Ã— (Î± Ã— Î²)), (p.1.2, p.2.2),
    have key_Î± : p_Î± ((aâ‚, bâ‚), (aâ‚‚, bâ‚‚)) âˆˆ r_Î±, { simp [p_Î±, eqv_Î± r_Î± r_Î±_in] },
    have key_Î² : p_Î² ((aâ‚, bâ‚), (aâ‚‚, bâ‚‚)) âˆˆ r_Î², { simp [p_Î², eqv_Î² r_Î² r_Î²_in] },
    exact âŸ¨h_Î± key_Î±, h_Î² key_Î²âŸ© },
end

instance separated.prod [separated_space Î±] [separated_space Î²] : separated_space (Î± Ã— Î²) :=
separated_def.2 $ assume x y H, prod.ext
  (eq_of_separated_of_uniform_continuous uniform_continuous_fst H)
  (eq_of_separated_of_uniform_continuous uniform_continuous_snd H)

end uniform_space
