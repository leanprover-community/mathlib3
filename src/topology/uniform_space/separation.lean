/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Patrick Massot

Hausdorff properties of uniform spaces. Separation quotient.
-/
import topology.uniform_space.basic


open filter topological_space lattice set classical
local attribute [instance, priority 0] prop_decidable
noncomputable theory
set_option eqn_compiler.zeta true

universes u v w
variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}
variables [uniform_space Œ±] [uniform_space Œ≤] [uniform_space Œ≥]

local notation `ùì§` := uniformity

/- separated uniformity -/

/-- The separation relation is the intersection of all entourages.
  Two points which are related by the separation relation are "indistinguishable"
  according to the uniform structure. -/
protected def separation_rel (Œ± : Type u) [u : uniform_space Œ±] :=
‚ãÇ‚ÇÄ (ùì§ Œ±).sets

lemma separated_equiv : equivalence (Œªx y, (x, y) ‚àà separation_rel Œ±) :=
‚ü®assume x, assume s, refl_mem_uniformity,
  assume x y, assume h (s : set (Œ±√óŒ±)) hs,
    have preimage prod.swap s ‚àà ùì§ Œ±,
      from symm_le_uniformity hs,
    h _ this,
  assume x y z (hxy : (x, y) ‚àà separation_rel Œ±) (hyz : (y, z) ‚àà separation_rel Œ±)
      s (hs : s ‚àà ùì§ Œ±),
    let ‚ü®t, ht, (h_ts : comp_rel t t ‚äÜ s)‚ü© := comp_mem_uniformity_sets hs in
    h_ts $ show (x, z) ‚àà comp_rel t t,
      from ‚ü®y, hxy t ht, hyz t ht‚ü©‚ü©

@[class] def separated (Œ± : Type u) [uniform_space Œ±] :=
separation_rel Œ± = id_rel

theorem separated_def {Œ± : Type u} [uniform_space Œ±] :
  separated Œ± ‚Üî ‚àÄ x y, (‚àÄ r ‚àà ùì§ Œ±, (x, y) ‚àà r) ‚Üí x = y :=
by simp [separated, id_rel_subset.2 separated_equiv.1, subset.antisymm_iff];
   simp [subset_def, separation_rel]

theorem separated_def' {Œ± : Type u} [uniform_space Œ±] :
  separated Œ± ‚Üî ‚àÄ x y, x ‚â† y ‚Üí ‚àÉ r ‚àà ùì§ Œ±, (x, y) ‚àâ r :=
separated_def.trans $ forall_congr $ Œª x, forall_congr $ Œª y,
by rw ‚Üê not_imp_not; simp [classical.not_forall]

instance separated_t2 [s : separated Œ±] : t2_space Œ± :=
‚ü®assume x y, assume h : x ‚â† y,
let ‚ü®d, hd, (hxy : (x, y) ‚àâ d)‚ü© := separated_def'.1 s x y h in
let ‚ü®d', hd', (hd'd' : comp_rel d' d' ‚äÜ d)‚ü© := comp_mem_uniformity_sets hd in
have {y | (x, y) ‚àà d'} ‚àà nhds x,
  from mem_nhds_left x hd',
let ‚ü®u, hu‚ÇÅ, hu‚ÇÇ, hu‚ÇÉ‚ü© := mem_nhds_sets_iff.mp this in
have {x | (x, y) ‚àà d'} ‚àà nhds y,
  from mem_nhds_right y hd',
let ‚ü®v, hv‚ÇÅ, hv‚ÇÇ, hv‚ÇÉ‚ü© := mem_nhds_sets_iff.mp this in
have u ‚à© v = ‚àÖ, from
  eq_empty_of_subset_empty $
  assume z ‚ü®(h‚ÇÅ : z ‚àà u), (h‚ÇÇ : z ‚àà v)‚ü©,
  have (x, y) ‚àà comp_rel d' d', from ‚ü®z, hu‚ÇÅ h‚ÇÅ, hv‚ÇÅ h‚ÇÇ‚ü©,
  hxy $ hd'd' this,
‚ü®u, v, hu‚ÇÇ, hv‚ÇÇ, hu‚ÇÉ, hv‚ÇÉ, this‚ü©‚ü©

instance separated_regular [separated Œ±] : regular_space Œ± :=
{ regular := Œªs a hs ha,
    have -s ‚àà nhds a,
      from mem_nhds_sets hs ha,
    have {p : Œ± √ó Œ± | p.1 = a ‚Üí p.2 ‚àà -s} ‚àà ùì§ Œ±,
      from mem_nhds_uniformity_iff.mp this,
    let ‚ü®d, hd, h‚ü© := comp_mem_uniformity_sets this in
    let e := {y:Œ±| (a, y) ‚àà d} in
    have hae : a ‚àà closure e, from subset_closure $ refl_mem_uniformity hd,
    have set.prod (closure e) (closure e) ‚äÜ comp_rel d (comp_rel (set.prod e e) d),
    begin
      rw [‚Üêclosure_prod_eq, closure_eq_inter_uniformity],
      change (‚®Öd' ‚àà ùì§ Œ±, _) ‚â§ comp_rel d (comp_rel _ d),
      exact (infi_le_of_le d $ infi_le_of_le hd $ le_refl _)
    end,
    have e_subset : closure e ‚äÜ -s,
      from assume a' ha',
        let ‚ü®x, (hx : (a, x) ‚àà d), y, ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©, (hy : (y, _) ‚àà d)‚ü© := @this ‚ü®a, a'‚ü© ‚ü®hae, ha'‚ü© in
        have (a, a') ‚àà comp_rel d d, from ‚ü®y, hx‚ÇÇ, hy‚ü©,
        h this rfl,
    have closure e ‚àà nhds a, from (nhds a).sets_of_superset (mem_nhds_left a hd) subset_closure,
    have nhds a ‚äì principal (-closure e) = ‚ä•,
      from (@inf_eq_bot_iff_le_compl _ _ _ (principal (- closure e)) (principal (closure e))
        (by simp [principal_univ, union_comm]) (by simp)).mpr (by simp [this]),
    ‚ü®- closure e, is_closed_closure, assume x h‚ÇÅ h‚ÇÇ, @e_subset x h‚ÇÇ h‚ÇÅ, this‚ü©,
  ..separated_t2 }

/- separation space -/
namespace uniform_space
def separation_setoid (Œ± : Type u) [uniform_space Œ±] : setoid Œ± :=
‚ü®Œªx y, (x, y) ‚àà separation_rel Œ±, separated_equiv‚ü©

local attribute [instance] separation_setoid

instance {Œ± : Type u} [u : uniform_space Œ±] : uniform_space (quotient (separation_setoid Œ±)) :=
{ to_topological_space := u.to_topological_space.coinduced (Œªx, ‚ü¶x‚üß),
  uniformity := map (Œªp:(Œ±√óŒ±), (‚ü¶p.1‚üß, ‚ü¶p.2‚üß)) u.uniformity,
  refl := le_trans (by simp [quotient.exists_rep]) (filter.map_mono refl_le_uniformity),
  symm := tendsto_map' $
    by simp [prod.swap, (‚àò)]; exact tendsto_map.comp tendsto_swap_uniformity,
  comp := calc (map (Œª (p : Œ± √ó Œ±), (‚ü¶p.fst‚üß, ‚ü¶p.snd‚üß)) u.uniformity).lift' (Œªs, comp_rel s s) =
          u.uniformity.lift' ((Œªs, comp_rel s s) ‚àò image (Œª (p : Œ± √ó Œ±), (‚ü¶p.fst‚üß, ‚ü¶p.snd‚üß))) :
      map_lift'_eq2 $ monotone_comp_rel monotone_id monotone_id
    ... ‚â§ u.uniformity.lift' (image (Œª (p : Œ± √ó Œ±), (‚ü¶p.fst‚üß, ‚ü¶p.snd‚üß)) ‚àò (Œªs:set (Œ±√óŒ±), comp_rel s (comp_rel s s))) :
      lift'_mono' $ assume s hs ‚ü®a, b‚ü© ‚ü®c, ‚ü®‚ü®a‚ÇÅ, a‚ÇÇ‚ü©, ha, a_eq‚ü©, ‚ü®‚ü®b‚ÇÅ, b‚ÇÇ‚ü©, hb, b_eq‚ü©‚ü©,
      begin
        simp at a_eq,
        simp at b_eq,
        have h : ‚ü¶a‚ÇÇ‚üß = ‚ü¶b‚ÇÅ‚üß, { rw [a_eq.right, b_eq.left] },
        have h : (a‚ÇÇ, b‚ÇÅ) ‚àà separation_rel Œ± := quotient.exact h,
        simp [function.comp, set.image, comp_rel, and.comm, and.left_comm, and.assoc],
        exact ‚ü®a‚ÇÅ, a_eq.left, b‚ÇÇ, b_eq.right, a‚ÇÇ, ha, b‚ÇÅ, h s hs, hb‚ü©
      end
    ... = map (Œªp:(Œ±√óŒ±), (‚ü¶p.1‚üß, ‚ü¶p.2‚üß)) (u.uniformity.lift' (Œªs:set (Œ±√óŒ±), comp_rel s (comp_rel s s))) :
      by rw [map_lift'_eq];
        exact monotone_comp_rel monotone_id (monotone_comp_rel monotone_id monotone_id)
    ... ‚â§ map (Œªp:(Œ±√óŒ±), (‚ü¶p.1‚üß, ‚ü¶p.2‚üß)) u.uniformity :
      map_mono comp_le_uniformity3,
  is_open_uniformity := assume s,
    have ‚àÄa, ‚ü¶a‚üß ‚àà s ‚Üí
        ({p:Œ±√óŒ± | p.1 = a ‚Üí ‚ü¶p.2‚üß ‚àà s} ‚àà ùì§ Œ± ‚Üî
          {p:Œ±√óŒ± | p.1 ‚âà a ‚Üí ‚ü¶p.2‚üß ‚àà s} ‚àà ùì§ Œ±),
      from assume a ha,
      ‚ü®assume h,
        let ‚ü®t, ht, hts‚ü© := comp_mem_uniformity_sets h in
        have hts : ‚àÄ{a‚ÇÅ a‚ÇÇ}, (a, a‚ÇÅ) ‚àà t ‚Üí (a‚ÇÅ, a‚ÇÇ) ‚àà t ‚Üí ‚ü¶a‚ÇÇ‚üß ‚àà s,
          from assume a‚ÇÅ a‚ÇÇ ha‚ÇÅ ha‚ÇÇ, @hts (a, a‚ÇÇ) ‚ü®a‚ÇÅ, ha‚ÇÅ, ha‚ÇÇ‚ü© rfl,
        have ht' : ‚àÄ{a‚ÇÅ a‚ÇÇ}, a‚ÇÅ ‚âà a‚ÇÇ ‚Üí (a‚ÇÅ, a‚ÇÇ) ‚àà t,
          from assume a‚ÇÅ a‚ÇÇ h, sInter_subset_of_mem ht h,
        u.uniformity.sets_of_superset ht $ assume ‚ü®a‚ÇÅ, a‚ÇÇ‚ü© h‚ÇÅ h‚ÇÇ, hts (ht' $ setoid.symm h‚ÇÇ) h‚ÇÅ,
        assume h, u.uniformity.sets_of_superset h $ by simp {contextual := tt}‚ü©,
    begin
      simp [topological_space.coinduced, u.is_open_uniformity, uniformity, forall_quotient_iff],
      exact ‚ü®Œªh a ha, (this a ha).mp $ h a ha, Œªh a ha, (this a ha).mpr $ h a ha‚ü©
    end }

lemma uniformity_quotient :
  ùì§ (quotient (separation_setoid Œ±)) = (ùì§ Œ±).map (Œªp:(Œ±√óŒ±), (‚ü¶p.1‚üß, ‚ü¶p.2‚üß)) :=
rfl

lemma uniform_continuous_quotient_mk :
  uniform_continuous (quotient.mk : Œ± ‚Üí quotient (separation_setoid Œ±)) :=
le_refl _

lemma uniform_continuous_quotient {f : quotient (separation_setoid Œ±) ‚Üí Œ≤}
  (hf : uniform_continuous (Œªx, f ‚ü¶x‚üß)) : uniform_continuous f :=
hf

lemma uniform_continuous_quotient_lift
  {f : Œ± ‚Üí Œ≤} {h : ‚àÄa b, (a, b) ‚àà separation_rel Œ± ‚Üí f a = f b}
  (hf : uniform_continuous f) : uniform_continuous (Œªa, quotient.lift f h a) :=
uniform_continuous_quotient hf

lemma uniform_continuous_quotient_lift‚ÇÇ [uniform_space Œ≥]
  {f : Œ± ‚Üí Œ≤ ‚Üí Œ≥} {h : ‚àÄa c b d, (a, b) ‚àà separation_rel Œ± ‚Üí (c, d) ‚àà separation_rel Œ≤ ‚Üí f a c = f b d}
  (hf : uniform_continuous (Œªp:Œ±√óŒ≤, f p.1 p.2)) :
  uniform_continuous (Œªp:_√ó_, quotient.lift‚ÇÇ f h p.1 p.2) :=
begin
  rw [uniform_continuous, uniformity_prod_eq_prod, uniformity_quotient, uniformity_quotient,
    filter.prod_map_map_eq, filter.tendsto_map'_iff, filter.tendsto_map'_iff],
  rwa [uniform_continuous, uniformity_prod_eq_prod, filter.tendsto_map'_iff] at hf
end

lemma comap_quotient_le_uniformity :
  (ùì§ $ quotient $ separation_setoid Œ±).comap (Œª (p : Œ± √ó Œ±), (‚ü¶p.fst‚üß, ‚ü¶p.snd‚üß)) ‚â§ (ùì§ Œ±) :=
assume t' ht',
let ‚ü®t, ht, tt_t'‚ü© := comp_mem_uniformity_sets ht' in
let ‚ü®s, hs, ss_t‚ü© := comp_mem_uniformity_sets ht in
‚ü®(Œªp:Œ±√óŒ±, (‚ü¶p.1‚üß, ‚ü¶p.2‚üß)) '' s,
  (ùì§ Œ±).sets_of_superset hs $ assume x hx, ‚ü®x, hx, rfl‚ü©,
  assume ‚ü®a‚ÇÅ, a‚ÇÇ‚ü© ‚ü®‚ü®b‚ÇÅ, b‚ÇÇ‚ü©, hb, ab_eq‚ü©,
  have ‚ü¶b‚ÇÅ‚üß = ‚ü¶a‚ÇÅ‚üß ‚àß ‚ü¶b‚ÇÇ‚üß = ‚ü¶a‚ÇÇ‚üß, from prod.mk.inj ab_eq,
  have b‚ÇÅ ‚âà a‚ÇÅ ‚àß b‚ÇÇ ‚âà a‚ÇÇ, from and.imp quotient.exact quotient.exact this,
  have ab‚ÇÅ : (a‚ÇÅ, b‚ÇÅ) ‚àà t, from (setoid.symm this.left) t ht,
  have ba‚ÇÇ : (b‚ÇÇ, a‚ÇÇ) ‚àà s, from this.right s hs,
  tt_t' ‚ü®b‚ÇÅ, show ((a‚ÇÅ, a‚ÇÇ).1, b‚ÇÅ) ‚àà t, from ab‚ÇÅ,
    ss_t ‚ü®b‚ÇÇ, show ((b‚ÇÅ, a‚ÇÇ).1, b‚ÇÇ) ‚àà s, from hb, ba‚ÇÇ‚ü©‚ü©‚ü©

lemma comap_quotient_eq_uniformity :
  (ùì§ $ quotient $ separation_setoid Œ±).comap (Œª (p : Œ± √ó Œ±), (‚ü¶p.fst‚üß, ‚ü¶p.snd‚üß)) = ùì§ Œ± :=
le_antisymm comap_quotient_le_uniformity le_comap_map


instance separated_separation : separated (quotient (separation_setoid Œ±)) :=
set.ext $ assume ‚ü®a, b‚ü©, quotient.induction_on‚ÇÇ a b $ assume a b,
  ‚ü®assume h,
    have a ‚âà b, from assume s hs,
      have s ‚àà (ùì§ $ quotient $ separation_setoid Œ±).comap (Œªp:(Œ±√óŒ±), (‚ü¶p.1‚üß, ‚ü¶p.2‚üß)),
        from comap_quotient_le_uniformity hs,
      let ‚ü®t, ht, hts‚ü© := this in
      hts begin dsimp [preimage], exact h t ht end,
    show ‚ü¶a‚üß = ‚ü¶b‚üß, from quotient.sound this,

  assume heq : ‚ü¶a‚üß = ‚ü¶b‚üß, assume h hs,
  heq ‚ñ∏ refl_mem_uniformity hs‚ü©

lemma separated_of_uniform_continuous {f : Œ± ‚Üí Œ≤} {x y : Œ±}
  (H : uniform_continuous f) (h : x ‚âà y) : f x ‚âà f y :=
assume _ h', h _ (H h')

lemma eq_of_separated_of_uniform_continuous [separated Œ≤] {f : Œ± ‚Üí Œ≤} {x y : Œ±}
  (H : uniform_continuous f) (h : x ‚âà y) : f x = f y :=
separated_def.1 (by apply_instance) _ _ $ separated_of_uniform_continuous H h

def separation_quotient (Œ± : Type*) [uniform_space Œ±] := quotient (separation_setoid Œ±)

namespace separation_quotient
instance : uniform_space (separation_quotient Œ±) := by dunfold separation_quotient ; apply_instance
instance : separated (separation_quotient Œ±) := by dunfold separation_quotient ; apply_instance

def lift [separated Œ≤] (f : Œ± ‚Üí Œ≤) : (separation_quotient Œ± ‚Üí Œ≤) :=
if h : uniform_continuous f then
  quotient.lift f (Œª x y, eq_of_separated_of_uniform_continuous h)
else
  Œª x, f (classical.inhabited_of_nonempty $ (nonempty_quotient_iff $ separation_setoid Œ±).1 ‚ü®x‚ü©).default

lemma lift_mk [separated Œ≤] {f : Œ± ‚Üí Œ≤} (h : uniform_continuous f) (a : Œ±) : lift f ‚ü¶a‚üß = f a :=
by rw [lift, dif_pos h]; refl

lemma uniform_continuous_lift [separated Œ≤] (f : Œ± ‚Üí Œ≤) : uniform_continuous (lift f) :=
begin
  by_cases hf : uniform_continuous f,
  { rw [lift, dif_pos hf], exact uniform_continuous_quotient_lift hf },
  { rw [lift, dif_neg hf], exact uniform_continuous_of_const (assume a b, rfl) }
end

def map (f : Œ± ‚Üí Œ≤) : separation_quotient Œ± ‚Üí separation_quotient Œ≤ :=
lift (quotient.mk ‚àò f)

lemma map_mk {f : Œ± ‚Üí Œ≤} (h : uniform_continuous f) (a : Œ±) : map f ‚ü¶a‚üß = ‚ü¶f a‚üß :=
by rw [map, lift_mk (uniform_continuous_quotient_mk.comp h)]

lemma uniform_continuous_map (f : Œ± ‚Üí Œ≤) : uniform_continuous (map f) :=
uniform_continuous_lift (quotient.mk ‚àò f)

lemma map_unique {f : Œ± ‚Üí Œ≤} (hf : uniform_continuous f)
  {g : separation_quotient Œ± ‚Üí separation_quotient Œ≤}
  (comm : quotient.mk ‚àò f = g ‚àò quotient.mk) : map f = g :=
by ext ‚ü®a‚ü©;
calc map f ‚ü¶a‚üß = ‚ü¶f a‚üß : map_mk hf a
  ... = g ‚ü¶a‚üß : congr_fun comm a

lemma map_id : map (@id Œ±) = id :=
map_unique uniform_continuous_id rfl

lemma map_comp {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (hf : uniform_continuous f) (hg : uniform_continuous g) :
  map g ‚àò map f = map (g ‚àò f) :=
(map_unique (hg.comp hf) $ by simp only [(‚àò), map_mk, hf, hg]).symm

end separation_quotient

lemma separation_prod {a‚ÇÅ a‚ÇÇ : Œ±} {b‚ÇÅ b‚ÇÇ : Œ≤} : (a‚ÇÅ, b‚ÇÅ) ‚âà (a‚ÇÇ, b‚ÇÇ) ‚Üî a‚ÇÅ ‚âà a‚ÇÇ ‚àß b‚ÇÅ ‚âà b‚ÇÇ :=
begin
  split,
  { assume h,
    exact ‚ü®separated_of_uniform_continuous uniform_continuous_fst h,
           separated_of_uniform_continuous uniform_continuous_snd h‚ü© },
  { rintros ‚ü®eqv_Œ±, eqv_Œ≤‚ü© r r_in,
    rw uniformity_prod at r_in,
    rcases r_in with ‚ü®t_Œ±, ‚ü®r_Œ±, r_Œ±_in, h_Œ±‚ü©, t_Œ≤, ‚ü®r_Œ≤, r_Œ≤_in, h_Œ≤‚ü©, H‚ü©,

    let p_Œ± := Œª(p : (Œ± √ó Œ≤) √ó (Œ± √ó Œ≤)), (p.1.1, p.2.1),
    let p_Œ≤ := Œª(p : (Œ± √ó Œ≤) √ó (Œ± √ó Œ≤)), (p.1.2, p.2.2),
    have key_Œ± : p_Œ± ((a‚ÇÅ, b‚ÇÅ), (a‚ÇÇ, b‚ÇÇ)) ‚àà r_Œ±, { simp [p_Œ±, eqv_Œ± r_Œ± r_Œ±_in] },
    have key_Œ≤ : p_Œ≤ ((a‚ÇÅ, b‚ÇÅ), (a‚ÇÇ, b‚ÇÇ)) ‚àà r_Œ≤, { simp [p_Œ≤, eqv_Œ≤ r_Œ≤ r_Œ≤_in] },
    exact H ‚ü®h_Œ± key_Œ±, h_Œ≤ key_Œ≤‚ü© },
end

instance separated.prod [separated Œ±] [separated Œ≤] : separated (Œ± √ó Œ≤) :=
separated_def.2 $ assume x y H, prod.ext
  (eq_of_separated_of_uniform_continuous uniform_continuous_fst H)
  (eq_of_separated_of_uniform_continuous uniform_continuous_snd H)
end uniform_space
