/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.separation
import topology.uniform_space.basic
import topology.uniform_space.cauchy

/-!
# Uniform convergence

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A sequence of functions `Fâ‚™` (with values in a metric space) converges uniformly on a set `s` to a
function `f` if, for all `Îµ > 0`, for all large enough `n`, one has for all `y âˆˆ s` the inequality
`dist (f y, Fâ‚™ y) < Îµ`. Under uniform convergence, many properties of the `Fâ‚™` pass to the limit,
most notably continuity. We prove this in the file, defining the notion of uniform convergence
in the more general setting of uniform spaces, and with respect to an arbitrary indexing set
endowed with a filter (instead of just `â„•` with `at_top`).

## Main results

Let `Î±` be a topological space, `Î²` a uniform space, `Fâ‚™` and `f` be functions from `Î±`to `Î²`
(where the index `n` belongs to an indexing type `Î¹` endowed with a filter `p`).

* `tendsto_uniformly_on F f p s`: the fact that `Fâ‚™` converges uniformly to `f` on `s`. This means
  that, for any entourage `u` of the diagonal, for large enough `n` (with respect to `p`), one has
  `(f y, Fâ‚™ y) âˆˆ u` for all `y âˆˆ s`.
* `tendsto_uniformly F f p`: same notion with `s = univ`.
* `tendsto_uniformly_on.continuous_on`: a uniform limit on a set of functions which are continuous
  on this set is itself continuous on this set.
* `tendsto_uniformly.continuous`: a uniform limit of continuous functions is continuous.
* `tendsto_uniformly_on.tendsto_comp`: If `Fâ‚™` tends uniformly to `f` on a set `s`, and `gâ‚™` tends
  to `x` within `s`, then `Fâ‚™ gâ‚™` tends to `f x` if `f` is continuous at `x` within `s`.
* `tendsto_uniformly.tendsto_comp`: If `Fâ‚™` tends uniformly to `f`, and `gâ‚™` tends to `x`, then
  `Fâ‚™ gâ‚™` tends to `f x`.

We also define notions where the convergence is locally uniform, called
`tendsto_locally_uniformly_on F f p s` and `tendsto_locally_uniformly F f p`. The previous theorems
all have corresponding versions under locally uniform convergence.

Finally, we introduce the notion of a uniform Cauchy sequence, which is to uniform
convergence what a Cauchy sequence is to the usual notion of convergence.

## Implementation notes

We derive most of our initial results from an auxiliary definition `tendsto_uniformly_on_filter`.
This definition in and of itself can sometimes be useful, e.g., when studying the local behavior
of the `Fâ‚™` near a point, which would typically look like `tendsto_uniformly_on_filter F f p (ð“ x)`.
Still, while this may be the "correct" definition (see
`tendsto_uniformly_on_iff_tendsto_uniformly_on_filter`), it is somewhat unwieldy to work with in
practice. Thus, we provide the more traditional definition in `tendsto_uniformly_on`.

Most results hold under weaker assumptions of locally uniform approximation. In a first section,
we prove the results under these weaker assumptions. Then, we derive the results on uniform
convergence from them.

## Tags

Uniform limit, uniform convergence, tends uniformly to
 -/

noncomputable theory
open_locale topology classical uniformity filter

open set filter

universes u v w
variables {Î± Î² Î³ Î¹ : Type*} [uniform_space Î²]
variables {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {s s' : set Î±} {x : Î±} {p : filter Î¹} {p' : filter Î±}
  {g : Î¹ â†’ Î±}

/-!
### Different notions of uniform convergence

We define uniform convergence and locally uniform convergence, on a set or in the whole space.
-/

/-- A sequence of functions `Fâ‚™` converges uniformly on a filter `p'` to a limiting function `f`
with respect to the filter `p` if, for any entourage of the diagonal `u`, one has
`p Ã—á¶  p'`-eventually `(f x, Fâ‚™ x) âˆˆ u`. -/
def tendsto_uniformly_on_filter (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : filter Î¹) (p' : filter Î±) :=
âˆ€ u âˆˆ ð“¤ Î², âˆ€á¶  (n : Î¹ Ã— Î±) in (p Ã—á¶  p'), (f n.snd, F n.fst n.snd) âˆˆ u

/--
A sequence of functions `Fâ‚™` converges uniformly on a filter `p'` to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) â†¦ (f x, Fâ‚™ x)` converges along `p Ã—á¶  p'` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit besides it being in `p'`.
-/
lemma tendsto_uniformly_on_filter_iff_tendsto :
  tendsto_uniformly_on_filter F f p p' â†”
  tendsto (Î» q : Î¹ Ã— Î±, (f q.2, F q.1 q.2)) (p Ã—á¶  p') (ð“¤ Î²) :=
forallâ‚‚_congr $ Î» u u_in, by simp [mem_map, filter.eventually, mem_prod_iff, preimage]

/-- A sequence of functions `Fâ‚™` converges uniformly on a set `s` to a limiting function `f` with
respect to the filter `p` if, for any entourage of the diagonal `u`, one has `p`-eventually
`(f x, Fâ‚™ x) âˆˆ u` for all `x âˆˆ s`. -/
def tendsto_uniformly_on (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : filter Î¹) (s : set Î±) :=
âˆ€ u âˆˆ ð“¤ Î², âˆ€á¶  n in p, âˆ€ (x : Î±), x âˆˆ s â†’ (f x, F n x) âˆˆ u

lemma tendsto_uniformly_on_iff_tendsto_uniformly_on_filter :
  tendsto_uniformly_on F f p s â†” tendsto_uniformly_on_filter F f p (ð“Ÿ s) :=
begin
  simp only [tendsto_uniformly_on, tendsto_uniformly_on_filter],
  apply forallâ‚‚_congr,
  simp_rw [eventually_prod_principal_iff],
  simp,
end

alias tendsto_uniformly_on_iff_tendsto_uniformly_on_filter â†”
  tendsto_uniformly_on.tendsto_uniformly_on_filter tendsto_uniformly_on_filter.tendsto_uniformly_on

/--
A sequence of functions `Fâ‚™` converges uniformly on a set `s` to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) â†¦ (f x, Fâ‚™ x)` converges along `p Ã—á¶  ð“Ÿ s` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit besides it being in `s`.
-/
lemma tendsto_uniformly_on_iff_tendsto {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {p : filter Î¹} {s : set Î±} :
  tendsto_uniformly_on F f p s â†” tendsto (Î» q : Î¹ Ã— Î±, (f q.2, F q.1 q.2)) (p Ã—á¶  ð“Ÿ s) (ð“¤ Î²) :=
by simp [tendsto_uniformly_on_iff_tendsto_uniformly_on_filter,
  tendsto_uniformly_on_filter_iff_tendsto]

/-- A sequence of functions `Fâ‚™` converges uniformly to a limiting function `f` with respect to a
filter `p` if, for any entourage of the diagonal `u`, one has `p`-eventually
`(f x, Fâ‚™ x) âˆˆ u` for all `x`. -/
def tendsto_uniformly (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : filter Î¹) :=
âˆ€ u âˆˆ ð“¤ Î², âˆ€á¶  n in p, âˆ€ (x : Î±), (f x, F n x) âˆˆ u

lemma tendsto_uniformly_iff_tendsto_uniformly_on_filter :
  tendsto_uniformly F f p â†” tendsto_uniformly_on_filter F f p âŠ¤ :=
begin
  simp only [tendsto_uniformly, tendsto_uniformly_on_filter],
  apply forallâ‚‚_congr,
  simp_rw [â† principal_univ, eventually_prod_principal_iff],
  simp,
end

lemma tendsto_uniformly.tendsto_uniformly_on_filter
  (h : tendsto_uniformly F f p) : tendsto_uniformly_on_filter F f p âŠ¤ :=
by rwa â† tendsto_uniformly_iff_tendsto_uniformly_on_filter

lemma tendsto_uniformly_on_iff_tendsto_uniformly_comp_coe :
  tendsto_uniformly_on F f p s â†” tendsto_uniformly (Î» i (x : s), F i x) (f âˆ˜ coe) p :=
begin
  apply forallâ‚‚_congr,
  intros u hu,
  simp,
end

/--
A sequence of functions `Fâ‚™` converges uniformly to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) â†¦ (f x, Fâ‚™ x)` converges along `p Ã—á¶  âŠ¤` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit.
-/
lemma tendsto_uniformly_iff_tendsto {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {p : filter Î¹} :
  tendsto_uniformly F f p â†” tendsto (Î» q : Î¹ Ã— Î±, (f q.2, F q.1 q.2)) (p Ã—á¶  âŠ¤) (ð“¤ Î²) :=
by simp [tendsto_uniformly_iff_tendsto_uniformly_on_filter, tendsto_uniformly_on_filter_iff_tendsto]

/-- Uniform converence implies pointwise convergence. -/
lemma tendsto_uniformly_on_filter.tendsto_at (h : tendsto_uniformly_on_filter F f p p')
  (hx : ð“Ÿ {x} â‰¤ p') : tendsto (Î» n, F n x) p $ ð“ (f x) :=
begin
  refine uniform.tendsto_nhds_right.mpr (Î» u hu, mem_map.mpr _),
  filter_upwards [(h u hu).curry],
  intros i h,
  simpa using (h.filter_mono hx),
end

/-- Uniform converence implies pointwise convergence. -/
lemma tendsto_uniformly_on.tendsto_at (h : tendsto_uniformly_on F f p s) {x : Î±} (hx : x âˆˆ s) :
  tendsto (Î» n, F n x) p $ ð“ (f x) :=
h.tendsto_uniformly_on_filter.tendsto_at
  (le_principal_iff.mpr $ mem_principal.mpr $ singleton_subset_iff.mpr $ hx)

/-- Uniform converence implies pointwise convergence. -/
lemma tendsto_uniformly.tendsto_at (h : tendsto_uniformly F f p) (x : Î±) :
  tendsto (Î» n, F n x) p $ ð“ (f x) :=
h.tendsto_uniformly_on_filter.tendsto_at le_top

lemma tendsto_uniformly_on_univ :
  tendsto_uniformly_on F f p univ â†” tendsto_uniformly F f p :=
by simp [tendsto_uniformly_on, tendsto_uniformly]

lemma tendsto_uniformly_on_filter.mono_left {p'' : filter Î¹}
  (h : tendsto_uniformly_on_filter F f p p') (hp : p'' â‰¤ p) :
  tendsto_uniformly_on_filter F f p'' p' :=
Î» u hu, (h u hu).filter_mono (p'.prod_mono_left hp)

lemma tendsto_uniformly_on_filter.mono_right {p'' : filter Î±}
  (h : tendsto_uniformly_on_filter F f p p') (hp : p'' â‰¤ p') :
  tendsto_uniformly_on_filter F f p p'' :=
Î» u hu, (h u hu).filter_mono (p.prod_mono_right hp)

lemma tendsto_uniformly_on.mono {s' : set Î±}
  (h : tendsto_uniformly_on F f p s) (h' : s' âŠ† s) : tendsto_uniformly_on F f p s' :=
tendsto_uniformly_on_iff_tendsto_uniformly_on_filter.mpr
  (h.tendsto_uniformly_on_filter.mono_right (le_principal_iff.mpr $ mem_principal.mpr h'))

lemma tendsto_uniformly_on_filter.congr {F' : Î¹ â†’ Î± â†’ Î²}
  (hf : tendsto_uniformly_on_filter F f p p')
  (hff' : âˆ€á¶  (n : Î¹ Ã— Î±) in (p Ã—á¶  p'), F n.fst n.snd = F' n.fst n.snd) :
  tendsto_uniformly_on_filter F' f p p' :=
begin
  refine (Î» u hu, ((hf u hu).and hff').mono (Î» n h, _)),
  rw â† h.right,
  exact h.left,
end

lemma tendsto_uniformly_on.congr {F' : Î¹ â†’ Î± â†’ Î²}
  (hf : tendsto_uniformly_on F f p s) (hff' : âˆ€á¶  n in p, set.eq_on (F n) (F' n) s) :
  tendsto_uniformly_on F' f p s :=
begin
  rw tendsto_uniformly_on_iff_tendsto_uniformly_on_filter at hf âŠ¢,
  refine hf.congr _,
  rw eventually_iff at hff' âŠ¢,
  simp only [set.eq_on] at hff',
  simp only [mem_prod_principal, hff', mem_set_of_eq],
end

lemma tendsto_uniformly_on.congr_right {g : Î± â†’ Î²}
  (hf : tendsto_uniformly_on F f p s) (hfg : eq_on f g s) :
  tendsto_uniformly_on F g p s :=
Î» u hu, by filter_upwards [hf u hu] with i hi a ha using hfg ha â–¸ hi a ha

protected lemma tendsto_uniformly.tendsto_uniformly_on
  (h : tendsto_uniformly F f p) : tendsto_uniformly_on F f p s :=
(tendsto_uniformly_on_univ.2 h).mono (subset_univ s)

/-- Composing on the right by a function preserves uniform convergence on a filter -/
lemma tendsto_uniformly_on_filter.comp (h : tendsto_uniformly_on_filter F f p p') (g : Î³ â†’ Î±) :
  tendsto_uniformly_on_filter (Î» n, F n âˆ˜ g) (f âˆ˜ g) p (p'.comap g) :=
begin
  intros u hu,
  obtain âŸ¨pa, hpa, pb, hpb, hpapbâŸ© := eventually_prod_iff.mp (h u hu),
  rw eventually_prod_iff,
  simp_rw eventually_comap,
  exact âŸ¨pa, hpa, pb âˆ˜ g, âŸ¨hpb.mono (Î» x hx y hy, by simp only [hx, hy, function.comp_app]),
    Î» x hx y hy, hpapb hx hyâŸ©âŸ©,
end

/-- Composing on the right by a function preserves uniform convergence on a set -/
lemma tendsto_uniformly_on.comp (h : tendsto_uniformly_on F f p s) (g : Î³ â†’ Î±) :
  tendsto_uniformly_on (Î» n, F n âˆ˜ g) (f âˆ˜ g) p (g â»Â¹' s) :=
begin
  rw tendsto_uniformly_on_iff_tendsto_uniformly_on_filter at h âŠ¢,
  simpa [tendsto_uniformly_on, comap_principal] using (tendsto_uniformly_on_filter.comp h g),
end

/-- Composing on the right by a function preserves uniform convergence -/
lemma tendsto_uniformly.comp (h : tendsto_uniformly F f p) (g : Î³ â†’ Î±) :
  tendsto_uniformly (Î» n, F n âˆ˜ g) (f âˆ˜ g) p :=
begin
  rw tendsto_uniformly_iff_tendsto_uniformly_on_filter at h âŠ¢,
  simpa [principal_univ, comap_principal] using (h.comp g),
end

/-- Composing on the left by a uniformly continuous function preserves
  uniform convergence on a filter -/
lemma uniform_continuous.comp_tendsto_uniformly_on_filter [uniform_space Î³] {g : Î² â†’ Î³}
  (hg : uniform_continuous g) (h : tendsto_uniformly_on_filter F f p p') :
  tendsto_uniformly_on_filter (Î» i, g âˆ˜ (F i)) (g âˆ˜ f) p p' :=
Î» u hu, h _ (hg hu)

/-- Composing on the left by a uniformly continuous function preserves
  uniform convergence on a set -/
lemma uniform_continuous.comp_tendsto_uniformly_on [uniform_space Î³] {g : Î² â†’ Î³}
  (hg : uniform_continuous g) (h : tendsto_uniformly_on F f p s) :
  tendsto_uniformly_on (Î» i, g âˆ˜ (F i)) (g âˆ˜ f) p s :=
Î» u hu, h _ (hg hu)

/-- Composing on the left by a uniformly continuous function preserves uniform convergence -/
lemma uniform_continuous.comp_tendsto_uniformly [uniform_space Î³] {g : Î² â†’ Î³}
  (hg : uniform_continuous g) (h : tendsto_uniformly F f p) :
  tendsto_uniformly (Î» i, g âˆ˜ (F i)) (g âˆ˜ f) p :=
Î» u hu, h _ (hg hu)

lemma tendsto_uniformly_on_filter.prod_map {Î¹' Î±' Î²' : Type*} [uniform_space Î²']
  {F' : Î¹' â†’ Î±' â†’ Î²'} {f' : Î±' â†’ Î²'} {q : filter Î¹'} {q' : filter Î±'}
  (h : tendsto_uniformly_on_filter F f p p') (h' : tendsto_uniformly_on_filter F' f' q q') :
  tendsto_uniformly_on_filter (Î» (i : Î¹ Ã— Î¹'), prod.map (F i.1) (F' i.2))
    (prod.map f f') (p.prod q) (p'.prod q') :=
begin
  intros u hu,
  rw [uniformity_prod_eq_prod, mem_map, mem_prod_iff] at hu,
  obtain âŸ¨v, hv, w, hw, hvwâŸ© := hu,
  apply (tendsto_swap4_prod.eventually ((h v hv).prod_mk (h' w hw))).mono,
  simp only [prod_map, and_imp, prod.forall],
  intros n n' x hxv hxw,
  have hout : ((f x.fst, F n x.fst), (f' x.snd, F' n' x.snd)) âˆˆ
    {x : (Î² Ã— Î²) Ã— Î²' Ã— Î²' | ((x.fst.fst, x.snd.fst), x.fst.snd, x.snd.snd) âˆˆ u},
  { exact mem_of_mem_of_subset (set.mem_prod.mpr âŸ¨hxv, hxwâŸ©) hvw, },
  exact hout,
end

lemma tendsto_uniformly_on.prod_map {Î¹' Î±' Î²' : Type*} [uniform_space Î²']
  {F' : Î¹' â†’ Î±' â†’ Î²'} {f' : Î±' â†’ Î²'} {p' : filter Î¹'} {s' : set Î±'}
  (h : tendsto_uniformly_on F f p s) (h' : tendsto_uniformly_on F' f' p' s') :
  tendsto_uniformly_on (Î» (i : Î¹ Ã— Î¹'), prod.map (F i.1) (F' i.2))
    (prod.map f f') (p.prod p') (s Ã—Ë¢ s') :=
begin
  rw tendsto_uniformly_on_iff_tendsto_uniformly_on_filter at h h' âŠ¢,
  simpa only [prod_principal_principal] using (h.prod_map h'),
end

lemma tendsto_uniformly.prod_map {Î¹' Î±' Î²' : Type*} [uniform_space Î²'] {F' : Î¹' â†’ Î±' â†’ Î²'}
  {f' : Î±' â†’ Î²'} {p' : filter Î¹'} (h : tendsto_uniformly F f p) (h' : tendsto_uniformly F' f' p') :
  tendsto_uniformly (Î» (i : Î¹ Ã— Î¹'), prod.map (F i.1) (F' i.2)) (prod.map f f') (p.prod p') :=
begin
  rw [â†tendsto_uniformly_on_univ, â†univ_prod_univ] at *,
  exact h.prod_map h',
end

lemma tendsto_uniformly_on_filter.prod {Î¹' Î²' : Type*} [uniform_space Î²']
  {F' : Î¹' â†’ Î± â†’ Î²'} {f' : Î± â†’ Î²'} {q : filter Î¹'}
  (h : tendsto_uniformly_on_filter F f p p') (h' : tendsto_uniformly_on_filter F' f' q p') :
  tendsto_uniformly_on_filter (Î» (i : Î¹ Ã— Î¹') a, (F i.1 a, F' i.2 a))
    (Î» a, (f a, f' a)) (p.prod q) p' :=
Î» u hu, ((h.prod_map h') u hu).diag_of_prod_right

lemma tendsto_uniformly_on.prod {Î¹' Î²' : Type*} [uniform_space Î²'] {F' : Î¹' â†’ Î± â†’ Î²'} {f' : Î± â†’ Î²'}
  {p' : filter Î¹'} (h : tendsto_uniformly_on F f p s) (h' : tendsto_uniformly_on F' f' p' s) :
  tendsto_uniformly_on (Î» (i : Î¹ Ã— Î¹') a, (F i.1 a, F' i.2 a)) (Î» a, (f a, f' a)) (p.prod p') s :=
(congr_arg _ s.inter_self).mp ((h.prod_map h').comp (Î» a, (a, a)))

lemma tendsto_uniformly.prod {Î¹' Î²' : Type*} [uniform_space Î²'] {F' : Î¹' â†’ Î± â†’ Î²'} {f' : Î± â†’ Î²'}
  {p' : filter Î¹'} (h : tendsto_uniformly F f p) (h' : tendsto_uniformly F' f' p') :
  tendsto_uniformly (Î» (i : Î¹ Ã— Î¹') a, (F i.1 a, F' i.2 a)) (Î» a, (f a, f' a)) (p.prod p') :=
(h.prod_map h').comp (Î» a, (a, a))

/-- Uniform convergence on a filter `p'` to a constant function is equivalent to convergence in
`p Ã—á¶  p'`. -/
lemma tendsto_prod_filter_iff {c : Î²} :
  tendsto â†¿F (p Ã—á¶  p') (ð“ c) â†” tendsto_uniformly_on_filter F (Î» _, c) p p' :=
begin
  simp_rw [tendsto, nhds_eq_comap_uniformity, map_le_iff_le_comap.symm, map_map, le_def, mem_map],
  exact forallâ‚‚_congr (Î» u hu, by simpa [eventually_iff]),
end

/-- Uniform convergence on a set `s` to a constant function is equivalent to convergence in
`p Ã—á¶  ð“Ÿ s`. -/
lemma tendsto_prod_principal_iff {c : Î²} :
  tendsto â†¿F (p Ã—á¶  ð“Ÿ s) (ð“ c) â†” tendsto_uniformly_on F (Î» _, c) p s :=
begin
  rw tendsto_uniformly_on_iff_tendsto_uniformly_on_filter,
  exact tendsto_prod_filter_iff,
end

/-- Uniform convergence to a constant function is equivalent to convergence in `p Ã—á¶  âŠ¤`. -/
lemma tendsto_prod_top_iff {c : Î²} : tendsto â†¿F (p Ã—á¶  âŠ¤) (ð“ c) â†” tendsto_uniformly F (Î» _, c) p :=
begin
  rw tendsto_uniformly_iff_tendsto_uniformly_on_filter,
  exact tendsto_prod_filter_iff,
end

/-- Uniform convergence on the empty set is vacuously true -/
lemma tendsto_uniformly_on_empty :
  tendsto_uniformly_on F f p âˆ… :=
Î» u hu, by simp

/-- Uniform convergence on a singleton is equivalent to regular convergence -/
lemma tendsto_uniformly_on_singleton_iff_tendsto :
  tendsto_uniformly_on F f p {x} â†” tendsto (Î» n : Î¹, F n x) p (ð“ (f x)) :=
begin
  simp_rw [tendsto_uniformly_on_iff_tendsto, uniform.tendsto_nhds_right, tendsto_def],
  exact forallâ‚‚_congr (Î» u hu, by simp [mem_prod_principal, preimage]),
end

/-- If a sequence `g` converges to some `b`, then the sequence of constant functions
`Î» n, Î» a, g n` converges to the constant function `Î» a, b` on any set `s` -/
lemma filter.tendsto.tendsto_uniformly_on_filter_const
  {g : Î¹ â†’ Î²} {b : Î²} (hg : tendsto g p (ð“ b)) (p' : filter Î±) :
  tendsto_uniformly_on_filter (Î» n : Î¹, Î» a : Î±, g n) (Î» a : Î±, b) p p' :=
begin
  rw tendsto_uniformly_on_filter_iff_tendsto,
  rw uniform.tendsto_nhds_right at hg,
  exact (hg.comp (tendsto_fst.comp ((@tendsto_id Î¹ p).prod_map (@tendsto_id Î± p')))).congr
    (Î» x, by simp),
end

/-- If a sequence `g` converges to some `b`, then the sequence of constant functions
`Î» n, Î» a, g n` converges to the constant function `Î» a, b` on any set `s` -/
lemma filter.tendsto.tendsto_uniformly_on_const
  {g : Î¹ â†’ Î²} {b : Î²} (hg : tendsto g p (ð“ b)) (s : set Î±) :
  tendsto_uniformly_on (Î» n : Î¹, Î» a : Î±, g n) (Î» a : Î±, b) p s :=
tendsto_uniformly_on_iff_tendsto_uniformly_on_filter.mpr
  (hg.tendsto_uniformly_on_filter_const (ð“Ÿ s))

lemma uniform_continuous_on.tendsto_uniformly [uniform_space Î±] [uniform_space Î³]
  {x : Î±} {U : set Î±} (hU : U âˆˆ ð“ x)
  {F : Î± â†’ Î² â†’ Î³} (hF : uniform_continuous_on â†¿F (U Ã—Ë¢ (univ : set Î²))) :
  tendsto_uniformly F (F x) (ð“ x) :=
begin
  let Ï† := (Î» q : Î± Ã— Î², ((x, q.2), q)),
  rw [tendsto_uniformly_iff_tendsto,
      show (Î» q : Î± Ã— Î², (F x q.2, F q.1 q.2)) = prod.map â†¿F â†¿F âˆ˜ Ï†, by { ext ; simpa }],
  apply hF.comp (tendsto_inf.mpr âŸ¨_, _âŸ©),
  { rw [uniformity_prod, tendsto_inf, tendsto_comap_iff, tendsto_comap_iff,
      show (Î»p : (Î± Ã— Î²) Ã— Î± Ã— Î², (p.1.1, p.2.1)) âˆ˜ Ï† = (Î»a, (x, a)) âˆ˜ prod.fst, by { ext, simp },
      show (Î»p : (Î± Ã— Î²) Ã— Î± Ã— Î², (p.1.2, p.2.2)) âˆ˜ Ï† = (Î»b, (b, b)) âˆ˜ prod.snd, by { ext, simp }],
    exact âŸ¨tendsto_left_nhds_uniformity.comp tendsto_fst,
           (tendsto_diag_uniformity id âŠ¤).comp tendsto_topâŸ© },
  { rw tendsto_principal,
    apply mem_of_superset (prod_mem_prod hU (mem_top.mpr rfl)) (Î» q h, _),
    simp [h.1, mem_of_mem_nhds hU] }
end

lemma uniform_continuousâ‚‚.tendsto_uniformly [uniform_space Î±] [uniform_space Î³]
  {f : Î± â†’ Î² â†’ Î³} (h : uniform_continuousâ‚‚ f) {x : Î±} : tendsto_uniformly f (f x) (ð“ x) :=
uniform_continuous_on.tendsto_uniformly univ_mem $
  by rwa [univ_prod_univ, uniform_continuous_on_univ]

/-- A sequence is uniformly Cauchy if eventually all of its pairwise differences are
uniformly bounded -/
def uniform_cauchy_seq_on_filter
  (F : Î¹ â†’ Î± â†’ Î²) (p : filter Î¹) (p' : filter Î±) : Prop :=
  âˆ€ u : set (Î² Ã— Î²), u âˆˆ ð“¤ Î² â†’ âˆ€á¶  (m : (Î¹ Ã— Î¹) Ã— Î±) in ((p Ã—á¶  p) Ã—á¶  p'),
    (F m.fst.fst m.snd, F m.fst.snd m.snd) âˆˆ u

/-- A sequence is uniformly Cauchy if eventually all of its pairwise differences are
uniformly bounded -/
def uniform_cauchy_seq_on
  (F : Î¹ â†’ Î± â†’ Î²) (p : filter Î¹) (s : set Î±) : Prop :=
  âˆ€ u : set (Î² Ã— Î²), u âˆˆ ð“¤ Î² â†’ âˆ€á¶  (m : Î¹ Ã— Î¹) in (p Ã—á¶  p), âˆ€ (x : Î±), x âˆˆ s â†’
    (F m.fst x, F m.snd x) âˆˆ u

lemma uniform_cauchy_seq_on_iff_uniform_cauchy_seq_on_filter :
  uniform_cauchy_seq_on F p s â†” uniform_cauchy_seq_on_filter F p (ð“Ÿ s) :=
begin
  simp only [uniform_cauchy_seq_on, uniform_cauchy_seq_on_filter],
  refine forallâ‚‚_congr (Î» u hu, _),
  rw eventually_prod_principal_iff,
end

lemma uniform_cauchy_seq_on.uniform_cauchy_seq_on_filter (hF : uniform_cauchy_seq_on F p s) :
  uniform_cauchy_seq_on_filter F p (ð“Ÿ s) :=
by rwa â†uniform_cauchy_seq_on_iff_uniform_cauchy_seq_on_filter

/-- A sequence that converges uniformly is also uniformly Cauchy -/
lemma tendsto_uniformly_on_filter.uniform_cauchy_seq_on_filter
  (hF : tendsto_uniformly_on_filter F f p p') :
  uniform_cauchy_seq_on_filter F p p' :=
begin
  intros u hu,
  rcases comp_symm_of_uniformity hu with âŸ¨t, ht, htsymm, htmemâŸ©,
  have := tendsto_swap4_prod.eventually ((hF t ht).prod_mk (hF t ht)),
  apply this.diag_of_prod_right.mono,
  simp only [and_imp, prod.forall],
  intros n1 n2 x hl hr,
  exact set.mem_of_mem_of_subset (prod_mk_mem_comp_rel (htsymm hl) hr) htmem,
end

/-- A sequence that converges uniformly is also uniformly Cauchy -/
lemma tendsto_uniformly_on.uniform_cauchy_seq_on (hF : tendsto_uniformly_on F f p s) :
  uniform_cauchy_seq_on F p s :=
uniform_cauchy_seq_on_iff_uniform_cauchy_seq_on_filter.mpr
  hF.tendsto_uniformly_on_filter.uniform_cauchy_seq_on_filter

/-- A uniformly Cauchy sequence converges uniformly to its limit -/
lemma uniform_cauchy_seq_on_filter.tendsto_uniformly_on_filter_of_tendsto [ne_bot p]
  (hF : uniform_cauchy_seq_on_filter F p p')
  (hF' : âˆ€á¶  (x : Î±) in p', tendsto (Î» n, F n x) p (ð“ (f x))) :
  tendsto_uniformly_on_filter F f p p' :=
begin
  -- Proof idea: |f_n(x) - f(x)| â‰¤ |f_n(x) - f_m(x)| + |f_m(x) - f(x)|. We choose `n`
  -- so that |f_n(x) - f_m(x)| is uniformly small across `s` whenever `m â‰¥ n`. Then for
  -- a fixed `x`, we choose `m` sufficiently large such that |f_m(x) - f(x)| is small.
  intros u hu,
  rcases comp_symm_of_uniformity hu with âŸ¨t, ht, htsymm, htmemâŸ©,

  -- We will choose n, x, and m simultaneously. n and x come from hF. m comes from hF'
  -- But we need to promote hF' to the full product filter to use it
  have hmc : âˆ€á¶  (x : (Î¹ Ã— Î¹) Ã— Î±) in p Ã—á¶  p Ã—á¶  p', tendsto (Î» (n : Î¹), F n x.snd) p (ð“ (f x.snd)),
  { rw eventually_prod_iff,
    refine âŸ¨(Î» x, true), by simp, _, hF', by simpâŸ©, },

  -- To apply filter operations we'll need to do some order manipulation
  rw filter.eventually_swap_iff,
  have := tendsto_prod_assoc.eventually (tendsto_prod_swap.eventually ((hF t ht).and hmc)),
  apply this.curry.mono,
  simp only [equiv.prod_assoc_apply, eventually_and, eventually_const, prod.snd_swap,
    prod.fst_swap, and_imp, prod.forall],

  -- Complete the proof
  intros x n hx hm',
  refine set.mem_of_mem_of_subset (mem_comp_rel.mpr _) htmem,
  rw uniform.tendsto_nhds_right at hm',
  have := hx.and (hm' ht),
  obtain âŸ¨m, hmâŸ© := this.exists,
  exact âŸ¨F m x, âŸ¨hm.2, htsymm hm.1âŸ©âŸ©,
end

/-- A uniformly Cauchy sequence converges uniformly to its limit -/
lemma uniform_cauchy_seq_on.tendsto_uniformly_on_of_tendsto [ne_bot p]
  (hF : uniform_cauchy_seq_on F p s) (hF' : âˆ€ x : Î±, x âˆˆ s â†’ tendsto (Î» n, F n x) p (ð“ (f x))) :
  tendsto_uniformly_on F f p s :=
tendsto_uniformly_on_iff_tendsto_uniformly_on_filter.mpr
  (hF.uniform_cauchy_seq_on_filter.tendsto_uniformly_on_filter_of_tendsto hF')

lemma uniform_cauchy_seq_on_filter.mono_left {p'' : filter Î¹}
  (hf : uniform_cauchy_seq_on_filter F p p') (hp : p'' â‰¤ p) :
  uniform_cauchy_seq_on_filter F p'' p' :=
begin
  intros u hu,
  have := (hf u hu).filter_mono (p'.prod_mono_left (filter.prod_mono hp hp)),
  exact this.mono (by simp),
end

lemma uniform_cauchy_seq_on_filter.mono_right {p'' : filter Î±}
  (hf : uniform_cauchy_seq_on_filter F p p') (hp : p'' â‰¤ p') :
  uniform_cauchy_seq_on_filter F p p'' :=
begin
  intros u hu,
  have := (hf u hu).filter_mono ((p Ã—á¶  p).prod_mono_right hp),
  exact this.mono (by simp),
end

lemma uniform_cauchy_seq_on.mono {s' : set Î±} (hf : uniform_cauchy_seq_on F p s) (hss' : s' âŠ† s) :
  uniform_cauchy_seq_on F p s' :=
begin
  rw uniform_cauchy_seq_on_iff_uniform_cauchy_seq_on_filter at hf âŠ¢,
  exact hf.mono_right (le_principal_iff.mpr $mem_principal.mpr hss'),
end

/-- Composing on the right by a function preserves uniform Cauchy sequences -/
lemma uniform_cauchy_seq_on_filter.comp {Î³ : Type*} (hf : uniform_cauchy_seq_on_filter F p p')
  (g : Î³ â†’ Î±) :
  uniform_cauchy_seq_on_filter (Î» n, F n âˆ˜ g) p (p'.comap g) :=
begin
  intros u hu,
  obtain âŸ¨pa, hpa, pb, hpb, hpapbâŸ© := eventually_prod_iff.mp (hf u hu),
  rw eventually_prod_iff,
  refine âŸ¨pa, hpa, pb âˆ˜ g, _, Î» x hx y hy, hpapb hx hyâŸ©,
  exact eventually_comap.mpr (hpb.mono (Î» x hx y hy, by simp only [hx, hy, function.comp_app])),
end

/-- Composing on the right by a function preserves uniform Cauchy sequences -/
lemma uniform_cauchy_seq_on.comp {Î³ : Type*} (hf : uniform_cauchy_seq_on F p s) (g : Î³ â†’ Î±) :
  uniform_cauchy_seq_on (Î» n, F n âˆ˜ g) p (g â»Â¹' s) :=
begin
  rw uniform_cauchy_seq_on_iff_uniform_cauchy_seq_on_filter at hf âŠ¢,
  simpa only [uniform_cauchy_seq_on, comap_principal] using (hf.comp g),
end

/-- Composing on the left by a uniformly continuous function preserves
uniform Cauchy sequences -/
lemma uniform_continuous.comp_uniform_cauchy_seq_on [uniform_space Î³] {g : Î² â†’ Î³}
  (hg : uniform_continuous g) (hf : uniform_cauchy_seq_on F p s) :
  uniform_cauchy_seq_on (Î» n, g âˆ˜ (F n)) p s :=
Î» u hu, hf _ (hg hu)

lemma uniform_cauchy_seq_on.prod_map {Î¹' Î±' Î²' : Type*} [uniform_space Î²']
  {F' : Î¹' â†’ Î±' â†’ Î²'} {p' : filter Î¹'} {s' : set Î±'}
  (h : uniform_cauchy_seq_on F p s) (h' : uniform_cauchy_seq_on F' p' s') :
  uniform_cauchy_seq_on (Î» (i : Î¹ Ã— Î¹'), prod.map (F i.1) (F' i.2))
    (p.prod p') (s Ã—Ë¢ s') :=
begin
  intros u hu,
  rw [uniformity_prod_eq_prod, mem_map, mem_prod_iff] at hu,
  obtain âŸ¨v, hv, w, hw, hvwâŸ© := hu,
  simp_rw [mem_prod, prod_map, and_imp, prod.forall],
  rw [â† set.image_subset_iff] at hvw,
  apply (tendsto_swap4_prod.eventually ((h v hv).prod_mk (h' w hw))).mono,
  intros x hx a b ha hb,
  refine hvw âŸ¨_, mk_mem_prod (hx.1 a ha) (hx.2 b hb), rflâŸ©,
end

lemma uniform_cauchy_seq_on.prod {Î¹' Î²' : Type*} [uniform_space Î²'] {F' : Î¹' â†’ Î± â†’ Î²'}
  {p' : filter Î¹'}
  (h : uniform_cauchy_seq_on F p s) (h' : uniform_cauchy_seq_on F' p' s) :
  uniform_cauchy_seq_on (Î» (i : Î¹ Ã— Î¹') a, (F i.fst a, F' i.snd a)) (p Ã—á¶  p') s :=
(congr_arg _ s.inter_self).mp ((h.prod_map h').comp (Î» a, (a, a)))

lemma uniform_cauchy_seq_on.prod' {Î²' : Type*} [uniform_space Î²'] {F' : Î¹ â†’ Î± â†’ Î²'}
  (h : uniform_cauchy_seq_on F p s) (h' : uniform_cauchy_seq_on F' p s) :
  uniform_cauchy_seq_on (Î» (i : Î¹) a, (F i a, F' i a)) p s :=
begin
  intros u hu,
  have hh : tendsto (Î» x : Î¹, (x, x)) p (p Ã—á¶  p), { exact tendsto_diag, },
  exact (hh.prod_map hh).eventually ((h.prod h') u hu),
end

/-- If a sequence of functions is uniformly Cauchy on a set, then the values at each point form
a Cauchy sequence. -/
lemma uniform_cauchy_seq_on.cauchy_map [hp : ne_bot p]
  (hf : uniform_cauchy_seq_on F p s) (hx : x âˆˆ s) :
  cauchy (map (Î» i, F i x) p) :=
begin
  simp only [cauchy_map_iff, hp, true_and],
  assume u hu,
  rw mem_map,
  filter_upwards [hf u hu] with p hp using hp x hx,
end

section seq_tendsto

lemma tendsto_uniformly_on_of_seq_tendsto_uniformly_on {l : filter Î¹} [l.is_countably_generated]
  (h : âˆ€ u : â„• â†’ Î¹, tendsto u at_top l â†’ tendsto_uniformly_on (Î» n, F (u n)) f at_top s) :
  tendsto_uniformly_on F f l s :=
begin
  rw [tendsto_uniformly_on_iff_tendsto, tendsto_iff_seq_tendsto],
  intros u hu,
  rw tendsto_prod_iff' at hu,
  specialize h (Î» n, (u n).fst) hu.1,
  rw tendsto_uniformly_on_iff_tendsto at h,
  have : ((Î» (q : Î¹ Ã— Î±), (f q.snd, F q.fst q.snd)) âˆ˜ u)
    = (Î» (q : â„• Ã— Î±), (f q.snd, F ((Î» (n : â„•), (u n).fst) q.fst) q.snd)) âˆ˜ (Î» n, (n, (u n).snd)),
  { ext1 n, simp, },
  rw this,
  refine tendsto.comp h _,
  rw tendsto_prod_iff',
  exact âŸ¨tendsto_id, hu.2âŸ©,
end

lemma tendsto_uniformly_on.seq_tendsto_uniformly_on {l : filter Î¹}
  (h : tendsto_uniformly_on F f l s) (u : â„• â†’ Î¹) (hu : tendsto u at_top l) :
  tendsto_uniformly_on (Î» n, F (u n)) f at_top s :=
begin
  rw tendsto_uniformly_on_iff_tendsto at h âŠ¢,
  have : (Î» (q : â„• Ã— Î±), (f q.snd, F (u q.fst) q.snd))
    = (Î» (q : Î¹ Ã— Î±), (f q.snd, F q.fst q.snd)) âˆ˜ (Î» p : â„• Ã— Î±, (u p.fst, p.snd)),
  { ext1 x, simp, },
  rw this,
  refine h.comp _,
  rw tendsto_prod_iff',
  exact âŸ¨hu.comp tendsto_fst, tendsto_sndâŸ©,
end

lemma tendsto_uniformly_on_iff_seq_tendsto_uniformly_on {l : filter Î¹} [l.is_countably_generated] :
  tendsto_uniformly_on F f l s
    â†” âˆ€ u : â„• â†’ Î¹, tendsto u at_top l â†’ tendsto_uniformly_on (Î» n, F (u n)) f at_top s :=
âŸ¨tendsto_uniformly_on.seq_tendsto_uniformly_on, tendsto_uniformly_on_of_seq_tendsto_uniformly_onâŸ©

lemma tendsto_uniformly_iff_seq_tendsto_uniformly {l : filter Î¹} [l.is_countably_generated] :
  tendsto_uniformly F f l
    â†” âˆ€ u : â„• â†’ Î¹, tendsto u at_top l â†’ tendsto_uniformly (Î» n, F (u n)) f at_top :=
begin
  simp_rw â† tendsto_uniformly_on_univ,
  exact tendsto_uniformly_on_iff_seq_tendsto_uniformly_on,
end

end seq_tendsto

variable [topological_space Î±]

/-- A sequence of functions `Fâ‚™` converges locally uniformly on a set `s` to a limiting function
`f` with respect to a filter `p` if, for any entourage of the diagonal `u`, for any `x âˆˆ s`, one
has `p`-eventually `(f y, Fâ‚™ y) âˆˆ u` for all `y` in a neighborhood of `x` in `s`. -/
def tendsto_locally_uniformly_on (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : filter Î¹) (s : set Î±) :=
  âˆ€ u âˆˆ ð“¤ Î², âˆ€ x âˆˆ s, âˆƒ t âˆˆ ð“[s] x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, (f y, F n y) âˆˆ u

/-- A sequence of functions `Fâ‚™` converges locally uniformly to a limiting function `f` with respect
to a filter `p` if, for any entourage of the diagonal `u`, for any `x`, one has `p`-eventually
`(f y, Fâ‚™ y) âˆˆ u` for all `y` in a neighborhood of `x`. -/
def tendsto_locally_uniformly (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : filter Î¹) :=
  âˆ€ u âˆˆ ð“¤ Î², âˆ€ (x : Î±), âˆƒ t âˆˆ ð“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, (f y, F n y) âˆˆ u

lemma tendsto_locally_uniformly_on_iff_tendsto_locally_uniformly_comp_coe :
  tendsto_locally_uniformly_on F f p s â†”
  tendsto_locally_uniformly (Î» i (x : s), F i x) (f âˆ˜ coe) p :=
begin
  refine forallâ‚‚_congr (Î» V hV, _),
  simp only [exists_prop, function.comp_app, set_coe.forall, subtype.coe_mk],
  refine forallâ‚‚_congr (Î» x hx, âŸ¨_, _âŸ©),
  { rintro âŸ¨t, htâ‚, htâ‚‚âŸ©,
    obtain âŸ¨u, huâ‚, huâ‚‚âŸ© := mem_nhds_within_iff_exists_mem_nhds_inter.mp htâ‚,
    exact âŸ¨coeâ»Â¹' u,
           (mem_nhds_subtype _ _ _).mpr âŸ¨u, huâ‚, rfl.subsetâŸ©,
           htâ‚‚.mono (Î» i hi y hyâ‚ hyâ‚‚, hi y (huâ‚‚ âŸ¨hyâ‚‚, hyâ‚âŸ©))âŸ©, },
  { rintro âŸ¨t, htâ‚, htâ‚‚âŸ©,
    obtain âŸ¨u, huâ‚, huâ‚‚âŸ© := (mem_nhds_subtype _ _ _).mp htâ‚,
    exact âŸ¨u âˆ© s,
           mem_nhds_within_iff_exists_mem_nhds_inter.mpr âŸ¨u, huâ‚, rfl.subsetâŸ©,
           htâ‚‚.mono (Î» i hi y hy, hi y hy.2 (huâ‚‚ (by simp [hy.1])))âŸ©, },
end

lemma tendsto_locally_uniformly_iff_forall_tendsto :
  tendsto_locally_uniformly F f p â†”
  âˆ€ x, tendsto (Î» (y : Î¹ Ã— Î±), (f y.2, F y.1 y.2)) (p Ã—á¶  (ð“ x)) (ð“¤ Î²) :=
begin
  simp only [tendsto_locally_uniformly, filter.forall_in_swap, tendsto_def, mem_prod_iff,
    set.prod_subset_iff],
  refine forallâ‚ƒ_congr (Î» x u hu, âŸ¨_, _âŸ©),
  { rintros âŸ¨n, hn, hpâŸ©,
    exact âŸ¨_, hp, n, hn, Î» i hi a ha, hi a haâŸ©, },
  { rintros âŸ¨I, hI, n, hn, huâŸ©,
    exact âŸ¨n, hn, by filter_upwards [hI] using huâŸ©, },
end

protected lemma tendsto_uniformly_on.tendsto_locally_uniformly_on
  (h : tendsto_uniformly_on F f p s) : tendsto_locally_uniformly_on F f p s :=
Î» u hu x hx,âŸ¨s, self_mem_nhds_within, by simpa using (h u hu)âŸ©

protected lemma tendsto_uniformly.tendsto_locally_uniformly
  (h : tendsto_uniformly F f p) : tendsto_locally_uniformly F f p :=
Î» u hu x, âŸ¨univ, univ_mem, by simpa using (h u hu)âŸ©

lemma tendsto_locally_uniformly_on.mono (h : tendsto_locally_uniformly_on F f p s) (h' : s' âŠ† s) :
  tendsto_locally_uniformly_on F f p s' :=
begin
  assume u hu x hx,
  rcases h u hu x (h' hx) with âŸ¨t, ht, HâŸ©,
  exact âŸ¨t, nhds_within_mono x h' ht, H.mono (Î» n, id)âŸ©
end

lemma tendsto_locally_uniformly_on_Union {S : Î³ â†’ set Î±} (hS : âˆ€ i, is_open (S i))
  (h : âˆ€ i, tendsto_locally_uniformly_on F f p (S i)) :
  tendsto_locally_uniformly_on F f p (â‹ƒ i, S i) :=
begin
  rintro v hv x âŸ¨_, âŸ¨i, rflâŸ©, hi : x âˆˆ S iâŸ©,
  obtain âŸ¨t, ht, ht'âŸ© := h i v hv x hi,
  refine âŸ¨t, _, ht'âŸ©,
  rw (hS _).nhds_within_eq hi at ht,
  exact mem_nhds_within_of_mem_nhds ht,
end

lemma tendsto_locally_uniformly_on_bUnion {s : set Î³} {S : Î³ â†’ set Î±}
  (hS : âˆ€ i âˆˆ s, is_open (S i)) (h : âˆ€ i âˆˆ s, tendsto_locally_uniformly_on F f p (S i)) :
  tendsto_locally_uniformly_on F f p (â‹ƒ i âˆˆ s, S i) :=
by { rw bUnion_eq_Union, exact tendsto_locally_uniformly_on_Union (Î» i, hS _ i.2) (Î» i, h _ i.2) }

lemma tendsto_locally_uniformly_on_sUnion (S : set (set Î±)) (hS : âˆ€ s âˆˆ S, is_open s)
  (h : âˆ€ s âˆˆ S, tendsto_locally_uniformly_on F f p s) :
  tendsto_locally_uniformly_on F f p (â‹ƒâ‚€ S) :=
by { rw sUnion_eq_bUnion, exact tendsto_locally_uniformly_on_bUnion hS h }

lemma tendsto_locally_uniformly_on.union {sâ‚ sâ‚‚ : set Î±} (hsâ‚ : is_open sâ‚) (hsâ‚‚ : is_open sâ‚‚)
  (hâ‚ : tendsto_locally_uniformly_on F f p sâ‚) (hâ‚‚ : tendsto_locally_uniformly_on F f p sâ‚‚) :
  tendsto_locally_uniformly_on F f p (sâ‚ âˆª sâ‚‚) :=
by { rw â†sUnion_pair, refine tendsto_locally_uniformly_on_sUnion _ _ _; simp [*] }

lemma tendsto_locally_uniformly_on_univ :
  tendsto_locally_uniformly_on F f p univ â†” tendsto_locally_uniformly F f p :=
by simp [tendsto_locally_uniformly_on, tendsto_locally_uniformly, nhds_within_univ]

protected lemma tendsto_locally_uniformly.tendsto_locally_uniformly_on
  (h : tendsto_locally_uniformly F f p) : tendsto_locally_uniformly_on F f p s :=
(tendsto_locally_uniformly_on_univ.mpr h).mono (subset_univ _)

/-- On a compact space, locally uniform convergence is just uniform convergence. -/
lemma tendsto_locally_uniformly_iff_tendsto_uniformly_of_compact_space [compact_space Î±] :
  tendsto_locally_uniformly F f p â†” tendsto_uniformly F f p :=
begin
  refine âŸ¨Î» h V hV, _, tendsto_uniformly.tendsto_locally_uniformlyâŸ©,
  choose U hU using h V hV,
  obtain âŸ¨t, htâŸ© := is_compact_univ.elim_nhds_subcover' (Î» k hk, U k) (Î» k hk, (hU k).1),
  replace hU := Î» (x : t), (hU x).2,
  rw â† eventually_all at hU,
  refine hU.mono (Î» i hi x, _),
  specialize ht (mem_univ x),
  simp only [exists_prop, mem_Union, set_coe.exists, exists_and_distrib_right,subtype.coe_mk] at ht,
  obtain âŸ¨y, âŸ¨hyâ‚, hyâ‚‚âŸ©, hyâ‚ƒâŸ© := ht,
  exact hi âŸ¨âŸ¨y, hyâ‚âŸ©, hyâ‚‚âŸ© x hyâ‚ƒ,
end

/-- For a compact set `s`, locally uniform convergence on `s` is just uniform convergence on `s`. -/
lemma tendsto_locally_uniformly_on_iff_tendsto_uniformly_on_of_compact (hs : is_compact s) :
  tendsto_locally_uniformly_on F f p s â†” tendsto_uniformly_on F f p s :=
begin
  haveI : compact_space s := is_compact_iff_compact_space.mp hs,
  refine âŸ¨Î» h, _, tendsto_uniformly_on.tendsto_locally_uniformly_onâŸ©,
  rwa [tendsto_locally_uniformly_on_iff_tendsto_locally_uniformly_comp_coe,
    tendsto_locally_uniformly_iff_tendsto_uniformly_of_compact_space,
    â† tendsto_uniformly_on_iff_tendsto_uniformly_comp_coe] at h,
end

lemma tendsto_locally_uniformly_on.comp [topological_space Î³] {t : set Î³}
  (h : tendsto_locally_uniformly_on F f p s)
  (g : Î³ â†’ Î±) (hg : maps_to g t s) (cg : continuous_on g t) :
  tendsto_locally_uniformly_on (Î» n, (F n) âˆ˜ g) (f âˆ˜ g) p t :=
begin
  assume u hu x hx,
  rcases h u hu (g x) (hg hx) with âŸ¨a, ha, HâŸ©,
  have : gâ»Â¹' a âˆˆ ð“[t] x :=
    ((cg x hx).preimage_mem_nhds_within' (nhds_within_mono (g x) hg.image_subset ha)),
  exact âŸ¨g â»Â¹' a, this, H.mono (Î» n hn y hy, hn _ hy)âŸ©
end

lemma tendsto_locally_uniformly.comp [topological_space Î³]
  (h : tendsto_locally_uniformly F f p) (g : Î³ â†’ Î±) (cg : continuous g) :
  tendsto_locally_uniformly (Î» n, (F n) âˆ˜ g) (f âˆ˜ g) p :=
begin
  rw â† tendsto_locally_uniformly_on_univ at h âŠ¢,
  rw continuous_iff_continuous_on_univ at cg,
  exact h.comp _ (maps_to_univ _ _) cg
end

lemma tendsto_locally_uniformly_on_tfae [locally_compact_space Î±]
  (G : Î¹ â†’ Î± â†’ Î²) (g : Î± â†’ Î²) (p : filter Î¹) (hs : is_open s) :
  tfae [(tendsto_locally_uniformly_on G g p s),
    (âˆ€ K âŠ† s, is_compact K â†’ tendsto_uniformly_on G g p K),
    (âˆ€ x âˆˆ s, âˆƒ v âˆˆ ð“[s] x, tendsto_uniformly_on G g p v)] :=
begin
  tfae_have : 1 â†’ 2,
  { rintro h K hK1 hK2,
    exact (tendsto_locally_uniformly_on_iff_tendsto_uniformly_on_of_compact hK2).mp (h.mono hK1) },
  tfae_have : 2 â†’ 3,
  { rintro h x hx,
    obtain âŸ¨K, âŸ¨hK1, hK2âŸ©, hK3âŸ© := (compact_basis_nhds x).mem_iff.mp (hs.mem_nhds hx),
    refine âŸ¨K, nhds_within_le_nhds hK1, h K hK3 hK2âŸ© },
  tfae_have : 3 â†’ 1,
  { rintro h u hu x hx,
    obtain âŸ¨v, hv1, hv2âŸ© := h x hx,
    exact âŸ¨v, hv1, hv2 u huâŸ© },
  tfae_finish
end

lemma tendsto_locally_uniformly_on_iff_forall_is_compact [locally_compact_space Î±]
  (hs : is_open s) :
  tendsto_locally_uniformly_on F f p s â†”
  âˆ€ K âŠ† s, is_compact K â†’ tendsto_uniformly_on F f p K :=
(tendsto_locally_uniformly_on_tfae F f p hs).out 0 1

lemma tendsto_locally_uniformly_on_iff_filter :
  tendsto_locally_uniformly_on F f p s â†”
  âˆ€ x âˆˆ s, tendsto_uniformly_on_filter F f p (ð“[s] x) :=
begin
  simp only [tendsto_uniformly_on_filter, eventually_prod_iff],
  split,
  { rintro h x hx u hu,
    obtain âŸ¨s, hs1, hs2âŸ© := h u hu x hx,
    exact âŸ¨_, hs2, _, eventually_of_mem hs1 (Î» x, id), Î» i hi y hy, hi y hyâŸ© },
  { rintro h u hu x hx,
    obtain âŸ¨pa, hpa, pb, hpb, hâŸ© := h x hx u hu,
    refine âŸ¨pb, hpb, eventually_of_mem hpa (Î» i hi y hy, h hi hy)âŸ© }
end

lemma tendsto_locally_uniformly_iff_filter :
  tendsto_locally_uniformly F f p â†”
  âˆ€ x, tendsto_uniformly_on_filter F f p (ð“ x) :=
by simpa [â† tendsto_locally_uniformly_on_univ, â† nhds_within_univ] using
    @tendsto_locally_uniformly_on_iff_filter _ _ _ _ F f univ p _

lemma tendsto_locally_uniformly_on.tendsto_at (hf : tendsto_locally_uniformly_on F f p s)
  {a : Î±} (ha : a âˆˆ s) :
  tendsto (Î» i, F i a) p (ð“ (f a)) :=
begin
  refine ((tendsto_locally_uniformly_on_iff_filter.mp hf) a ha).tendsto_at _,
  simpa only [filter.principal_singleton] using pure_le_nhds_within ha
end

lemma tendsto_locally_uniformly_on.unique [p.ne_bot] [t2_space Î²] {g : Î± â†’ Î²}
  (hf : tendsto_locally_uniformly_on F f p s) (hg : tendsto_locally_uniformly_on F g p s) :
  s.eq_on f g :=
Î» a ha, tendsto_nhds_unique (hf.tendsto_at ha) (hg.tendsto_at ha)

lemma tendsto_locally_uniformly_on.congr {G : Î¹ â†’ Î± â†’ Î²}
  (hf : tendsto_locally_uniformly_on F f p s) (hg : âˆ€ n, s.eq_on (F n) (G n)) :
  tendsto_locally_uniformly_on G f p s :=
begin
  rintro u hu x hx,
  obtain âŸ¨t, ht, hâŸ© := hf u hu x hx,
  refine âŸ¨s âˆ© t, inter_mem self_mem_nhds_within ht, _âŸ©,
  filter_upwards [h] with i hi y hy using hg i hy.1 â–¸ hi y hy.2
end

lemma tendsto_locally_uniformly_on.congr_right {g : Î± â†’ Î²}
  (hf : tendsto_locally_uniformly_on F f p s) (hg : s.eq_on f g) :
  tendsto_locally_uniformly_on F g p s :=
begin
  rintro u hu x hx,
  obtain âŸ¨t, ht, hâŸ© := hf u hu x hx,
  refine âŸ¨s âˆ© t, inter_mem self_mem_nhds_within ht, _âŸ©,
  filter_upwards [h] with i hi y hy using hg hy.1 â–¸ hi y hy.2
end

/-!
### Uniform approximation

In this section, we give lemmas ensuring that a function is continuous if it can be approximated
uniformly by continuous functions. We give various versions, within a set or the whole space, at
a single point or at all points, with locally uniform approximation or uniform approximation. All
the statements are derived from a statement about locally uniform approximation within a set at
a point, called `continuous_within_at_of_locally_uniform_approx_of_continuous_within_at`. -/

/-- A function which can be locally uniformly approximated by functions which are continuous
within a set at a point is continuous within this set at this point. -/
lemma continuous_within_at_of_locally_uniform_approx_of_continuous_within_at
  (hx : x âˆˆ s) (L : âˆ€ u âˆˆ ð“¤ Î², âˆƒ (t âˆˆ ð“[s] x) (F : Î± â†’ Î²), continuous_within_at F s x âˆ§
    âˆ€ y âˆˆ t, (f y, F y) âˆˆ u) : continuous_within_at f s x :=
begin
  apply uniform.continuous_within_at_iff'_left.2 (Î» uâ‚€ huâ‚€, _),
  obtain âŸ¨uâ‚, hâ‚, uâ‚â‚€âŸ© : âˆƒ (u : set (Î² Ã— Î²)) (H : u âˆˆ ð“¤ Î²), comp_rel u u âŠ† uâ‚€ :=
    comp_mem_uniformity_sets huâ‚€,
  obtain âŸ¨uâ‚‚, hâ‚‚, hsymm, uâ‚‚â‚âŸ© : âˆƒ (u : set (Î² Ã— Î²)) (H : u âˆˆ ð“¤ Î²),
    (âˆ€{a b}, (a, b) âˆˆ u â†’ (b, a) âˆˆ u) âˆ§ comp_rel u u âŠ† uâ‚ := comp_symm_of_uniformity hâ‚,
  rcases L uâ‚‚ hâ‚‚ with âŸ¨t, tx, F, hFc, hFâŸ©,
  have A : âˆ€á¶  y in ð“[s] x, (f y, F y) âˆˆ uâ‚‚ := eventually.mono tx hF,
  have B : âˆ€á¶  y in ð“[s] x, (F y, F x) âˆˆ uâ‚‚ :=
    uniform.continuous_within_at_iff'_left.1 hFc hâ‚‚,
  have C : âˆ€á¶  y in ð“[s] x, (f y, F x) âˆˆ uâ‚ :=
    (A.and B).mono (Î» y hy, uâ‚‚â‚ (prod_mk_mem_comp_rel hy.1 hy.2)),
  have : (F x, f x) âˆˆ uâ‚ :=
    uâ‚‚â‚ (prod_mk_mem_comp_rel (refl_mem_uniformity hâ‚‚) (hsymm (A.self_of_nhds_within hx))),
  exact C.mono (Î» y hy, uâ‚â‚€ (prod_mk_mem_comp_rel hy this))
end

/-- A function which can be locally uniformly approximated by functions which are continuous at
a point is continuous at this point. -/
lemma continuous_at_of_locally_uniform_approx_of_continuous_at
  (L : âˆ€ u âˆˆ ð“¤ Î², âˆƒ (t âˆˆ ð“ x) F, continuous_at F x âˆ§ âˆ€ y âˆˆ t, (f y, F y) âˆˆ u) :
  continuous_at f x :=
begin
  rw â† continuous_within_at_univ,
  apply continuous_within_at_of_locally_uniform_approx_of_continuous_within_at (mem_univ _) _,
  simpa only [exists_prop, nhds_within_univ, continuous_within_at_univ] using L
end

/-- A function which can be locally uniformly approximated by functions which are continuous
on a set is continuous on this set. -/
lemma continuous_on_of_locally_uniform_approx_of_continuous_within_at
  (L : âˆ€ (x âˆˆ s) (u âˆˆ ð“¤ Î²), âˆƒ (t âˆˆ ð“[s] x) F,
    continuous_within_at F s x âˆ§ âˆ€ y âˆˆ t, (f y, F y) âˆˆ u) : continuous_on f s :=
Î» x hx, continuous_within_at_of_locally_uniform_approx_of_continuous_within_at hx (L x hx)

/-- A function which can be uniformly approximated by functions which are continuous on a set
is continuous on this set. -/
lemma continuous_on_of_uniform_approx_of_continuous_on
  (L : âˆ€ u âˆˆ ð“¤ Î², âˆƒ F, continuous_on F s âˆ§ âˆ€ y âˆˆ s, (f y, F y) âˆˆ u) : continuous_on f s :=
continuous_on_of_locally_uniform_approx_of_continuous_within_at $
  Î» x hx u hu, âŸ¨s, self_mem_nhds_within, (L u hu).imp $
    Î» F hF, âŸ¨hF.1.continuous_within_at hx, hF.2âŸ©âŸ©

/-- A function which can be locally uniformly approximated by continuous functions is continuous. -/
lemma continuous_of_locally_uniform_approx_of_continuous_at
  (L : âˆ€ (x : Î±), âˆ€ u âˆˆ ð“¤ Î², âˆƒ t âˆˆ ð“ x, âˆƒ F, continuous_at F x âˆ§ âˆ€ y âˆˆ t, (f y, F y) âˆˆ u) :
  continuous f :=
continuous_iff_continuous_at.2 $ Î» x, continuous_at_of_locally_uniform_approx_of_continuous_at (L x)

/-- A function which can be uniformly approximated by continuous functions is continuous. -/
lemma continuous_of_uniform_approx_of_continuous
  (L : âˆ€ u âˆˆ ð“¤ Î², âˆƒ F, continuous F âˆ§ âˆ€ y, (f y, F y) âˆˆ u) : continuous f :=
continuous_iff_continuous_on_univ.mpr $ continuous_on_of_uniform_approx_of_continuous_on $
  by simpa [continuous_iff_continuous_on_univ] using L

/-!
### Uniform limits

From the previous statements on uniform approximation, we deduce continuity results for uniform
limits.
-/

/-- A locally uniform limit on a set of functions which are continuous on this set is itself
continuous on this set. -/
protected lemma tendsto_locally_uniformly_on.continuous_on
  (h : tendsto_locally_uniformly_on F f p s) (hc : âˆ€á¶  n in p, continuous_on (F n) s) [ne_bot p] :
  continuous_on f s :=
begin
  apply continuous_on_of_locally_uniform_approx_of_continuous_within_at (Î» x hx u hu, _),
  rcases h u hu x hx with âŸ¨t, ht, HâŸ©,
  rcases (hc.and H).exists with âŸ¨n, hFc, hFâŸ©,
  exact âŸ¨t, ht, âŸ¨F n, hFc.continuous_within_at hx, hFâŸ©âŸ©
end

/-- A uniform limit on a set of functions which are continuous on this set is itself continuous
on this set. -/
protected lemma tendsto_uniformly_on.continuous_on (h : tendsto_uniformly_on F f p s)
  (hc : âˆ€á¶  n in p, continuous_on (F n) s) [ne_bot p] : continuous_on f s :=
h.tendsto_locally_uniformly_on.continuous_on hc

/-- A locally uniform limit of continuous functions is continuous. -/
protected lemma tendsto_locally_uniformly.continuous (h : tendsto_locally_uniformly F f p)
  (hc : âˆ€á¶  n in p, continuous (F n)) [ne_bot p] : continuous f :=
continuous_iff_continuous_on_univ.mpr $ h.tendsto_locally_uniformly_on.continuous_on $
  hc.mono $ Î» n hn, hn.continuous_on

/-- A uniform limit of continuous functions is continuous. -/
protected lemma tendsto_uniformly.continuous (h : tendsto_uniformly F f p)
  (hc : âˆ€á¶  n in p, continuous (F n)) [ne_bot p] : continuous f :=
h.tendsto_locally_uniformly.continuous hc

/-!
### Composing limits under uniform convergence

In general, if `Fâ‚™` converges pointwise to a function `f`, and `gâ‚™` tends to `x`, it is not true
that `Fâ‚™ gâ‚™` tends to `f x`. It is true however if the convergence of `Fâ‚™` to `f` is uniform. In
this paragraph, we prove variations around this statement.
-/

/-- If `Fâ‚™` converges locally uniformly on a neighborhood of `x` within a set `s` to a function `f`
which is continuous at `x` within `s `, and `gâ‚™` tends to `x` within `s`, then `Fâ‚™ (gâ‚™)` tends
to `f x`. -/
lemma tendsto_comp_of_locally_uniform_limit_within
  (h : continuous_within_at f s x) (hg : tendsto g p (ð“[s] x))
  (hunif : âˆ€ u âˆˆ ð“¤ Î², âˆƒ t âˆˆ ð“[s] x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, (f y, F n y) âˆˆ u) :
  tendsto (Î» n, F n (g n)) p (ð“ (f x)) :=
begin
  apply uniform.tendsto_nhds_right.2 (Î» uâ‚€ huâ‚€, _),
  obtain âŸ¨uâ‚, hâ‚, uâ‚â‚€âŸ© : âˆƒ (u : set (Î² Ã— Î²)) (H : u âˆˆ ð“¤ Î²), comp_rel u u âŠ† uâ‚€ :=
    comp_mem_uniformity_sets huâ‚€,
  rcases hunif uâ‚ hâ‚ with âŸ¨s, sx, hsâŸ©,
  have A : âˆ€á¶  n in p, g n âˆˆ s := hg sx,
  have B : âˆ€á¶  n in p, (f x, f (g n)) âˆˆ uâ‚ := hg (uniform.continuous_within_at_iff'_right.1 h hâ‚),
  refine ((hs.and A).and B).mono (Î» y hy, _),
  rcases hy with âŸ¨âŸ¨H1, H2âŸ©, H3âŸ©,
  exact uâ‚â‚€ (prod_mk_mem_comp_rel H3 (H1 _ H2))
end

/-- If `Fâ‚™` converges locally uniformly on a neighborhood of `x` to a function `f` which is
continuous at `x`, and `gâ‚™` tends to `x`, then `Fâ‚™ (gâ‚™)` tends to `f x`. -/
lemma tendsto_comp_of_locally_uniform_limit (h : continuous_at f x) (hg : tendsto g p (ð“ x))
  (hunif : âˆ€ u âˆˆ ð“¤ Î², âˆƒ t âˆˆ ð“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, (f y, F n y) âˆˆ u) :
  tendsto (Î» n, F n (g n)) p (ð“ (f x)) :=
begin
  rw â† continuous_within_at_univ at h,
  rw â† nhds_within_univ at hunif hg,
  exact tendsto_comp_of_locally_uniform_limit_within h hg hunif
end

/-- If `Fâ‚™` tends locally uniformly to `f` on a set `s`, and `gâ‚™` tends to `x` within `s`, then
`Fâ‚™ gâ‚™` tends to `f x` if `f` is continuous at `x` within `s` and `x âˆˆ s`. -/
lemma tendsto_locally_uniformly_on.tendsto_comp (h : tendsto_locally_uniformly_on F f p s)
  (hf : continuous_within_at f s x) (hx : x âˆˆ s) (hg : tendsto g p (ð“[s] x)) :
  tendsto (Î» n, F n (g n)) p (ð“ (f x)) :=
tendsto_comp_of_locally_uniform_limit_within hf hg (Î» u hu, h u hu x hx)

/-- If `Fâ‚™` tends uniformly to `f` on a set `s`, and `gâ‚™` tends to `x` within `s`, then `Fâ‚™ gâ‚™`
tends to `f x` if `f` is continuous at `x` within `s`. -/
lemma tendsto_uniformly_on.tendsto_comp (h : tendsto_uniformly_on F f p s)
  (hf : continuous_within_at f s x) (hg : tendsto g p (ð“[s] x)) :
  tendsto (Î» n, F n (g n)) p (ð“ (f x)) :=
tendsto_comp_of_locally_uniform_limit_within hf hg (Î» u hu,
  âŸ¨s, self_mem_nhds_within, h u huâŸ©)

/-- If `Fâ‚™` tends locally uniformly to `f`, and `gâ‚™` tends to `x`, then `Fâ‚™ gâ‚™` tends to `f x`. -/
lemma tendsto_locally_uniformly.tendsto_comp (h : tendsto_locally_uniformly F f p)
  (hf : continuous_at f x) (hg : tendsto g p (ð“ x)) : tendsto (Î» n, F n (g n)) p (ð“ (f x)) :=
tendsto_comp_of_locally_uniform_limit hf hg (Î» u hu, h u hu x)

/-- If `Fâ‚™` tends uniformly to `f`, and `gâ‚™` tends to `x`, then `Fâ‚™ gâ‚™` tends to `f x`. -/
lemma tendsto_uniformly.tendsto_comp (h : tendsto_uniformly F f p)
  (hf : continuous_at f x) (hg : tendsto g p (ð“ x)) : tendsto (Î» n, F n (g n)) p (ð“ (f x)) :=
h.tendsto_locally_uniformly.tendsto_comp hf hg
