/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import topology.uniform_space.uniform_convergence
import topology.uniform_space.pi
import topology.uniform_space.equiv

/-!
# Topology and uniform structure of uniform convergence

This files endows `Î± â†’ Î²` with the topologies / uniform structures of
- uniform convergence on `Î±` (in the `uniform_convergence` namespace)
- uniform convergence on a specified family `ğ”–` of sets of `Î±`
  (in the `uniform_convergence_on` namespace), also called `ğ”–`-convergence

Usual examples of the second construction include :
- the topology of compact convergence, when `ğ”–` is the set of compacts of `Î±`
- the strong topology on the dual of a topological vector space (TVS) `E`, when `ğ”–` is the set of
  Von Neuman bounded subsets of `E`
- the weak-* topology on the dual of a TVS `E`, when `ğ”–` is the set of singletons of `E`.

This file contains a lot of technical facts, so it is heavily commented, proofs included!

## Main definitions

* `uniform_convergence.gen`: basis sets for the uniformity of uniform convergence. These are sets
  of the form `S(V) := {(f, g) | âˆ€ x : Î±, (f x, g x) âˆˆ V}` for some `V : set (Î² Ã— Î²)`
* `uniform_convergence.uniform_space`: uniform structure of uniform convergence. This is the
  `uniform_space` on `Î± â†’ Î²` whose uniformity is generated by the sets `S(V)` for `V âˆˆ ğ“¤ Î²`.
  We will denote this uniform space as `ğ’°(Î±, Î², uÎ²)`, both in the comments and as a local notation
  in the Lean code, where `uÎ²` is the uniform space structure on `Î²`.
* `uniform_convergence_on.uniform_space`: uniform structure of ğ”–-convergence, where
  `ğ”– : set (set Î±)`. This is the infimum, for `S âˆˆ ğ”–`, of the pullback of `ğ’° S Î²` by the map of
  restriction to `S`. We will denote it `ğ’±(Î±, Î², ğ”–, uÎ²)`, where `uÎ²` is the uniform space structure
  on `Î²`.

## Main statements

### Basic properties

* `uniform_convergence.uniform_continuous_eval`: evaluation is uniformly continuous for `ğ’°(Î±, uÎ²)`.
* `uniform_convergence.t2_space`: the topology of uniform convergence on `Î± â†’ Î²` is Tâ‚‚ if
  `Î²` is Tâ‚‚.
* `uniform_convergence.tendsto_iff_tendsto_uniformly`: `ğ’°(Î±, Î², uÎ²)` is
  indeed the uniform structure of uniform convergence
* `uniform_convergence_on.uniform_continuous_eval_of_mem`: evaluation at a point contained in a
  set of `ğ”–` is uniformly continuous for `ğ’±(Î±, Î², ğ”– uÎ²)`
* `uniform_convergence.t2_space`: the topology of `ğ”–`-convergence on `Î± â†’ Î²` is Tâ‚‚ if
  `Î²` is Tâ‚‚ and `ğ”–` covers `Î±`
* `uniform_convergence_on.tendsto_iff_tendsto_uniformly_on`:
  `ğ’±(Î±, Î², ğ”– uÎ²)` is indeed the uniform structure of `ğ”–`-convergence

### Functoriality and compatibility with product of uniform spaces

In order to avoid the need for filter bases as much as possible when using these definitions,
we develop an extensive API for manipulating these structures abstractly. As usual in the topology
section of mathlib, we first state results about the complete lattices of `uniform_space`s on
fixed types, and then we use these to deduce categorical-like results about maps between two
uniform spaces.

We only describe these in the harder case of `ğ”–`-convergence, as the names of the corresponding
results for uniform convergence can easily be guessed.

#### Order statements

* `uniform_convergence_on.mono`: let `uâ‚`, `uâ‚‚` be two uniform structures on `Î³` and
  `ğ”–â‚ ğ”–â‚‚ : set (set Î±)`. If `uâ‚ â‰¤ uâ‚‚` and `ğ”–â‚‚ âŠ† ğ”–â‚` then `ğ’±(Î±, Î³, ğ”–â‚, uâ‚) â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚‚)`.
* `uniform_convergence_on.infi_eq`: if `u` is a family of uniform structures on `Î³`, then
  `ğ’±(Î±, Î³, ğ”–, (â¨… i, u i)) = â¨… i, ğ’±(Î±, Î³, ğ”–, u i)`.
* `uniform_convergence_on.comap_eq`: if `u` is a uniform structures on `Î²` and `f : Î³ â†’ Î²`, then
  `ğ’±(Î±, Î³, ğ”–, comap f u) = comap (Î» g, f âˆ˜ g) ğ’±(Î±, Î³, ğ”–, uâ‚)`.

An interesting note about these statements is that they are proved without ever unfolding the basis
definition of the uniform structure of uniform convergence! Instead, we build a
(not very interesting) Galois connection `uniform_convergence.gc` and then rely on the Galois
connection API to do most of the work.

#### Morphism statements (unbundled)

* `uniform_convergence_on.postcomp_uniform_continuous`: if `f : (Î³, uÎ³) â†’ (Î², uÎ²)` is uniformly
  continuous, then `(Î» g, f âˆ˜ g) : (Î± â†’ Î³, ğ’±(Î±, Î³, ğ”–, uÎ³)) â†’ (Î± â†’ Î², ğ’±(Î±, Î², ğ”–, uÎ²))` is
  uniformly continuous.
* `uniform_convergence_on.postcomp_uniform_inducing`: if `f : (Î³, uÎ³) â†’ (Î², uÎ²)` is a uniform
  inducing, then `(Î» g, f âˆ˜ g) : (Î± â†’ Î³, ğ’±(Î±, Î³, ğ”–, uÎ³)) â†’ (Î± â†’ Î², ğ’±(Î±, Î², ğ”–, uÎ²))` is a
  uniform inducing.
* `uniform_convergence_on.precomp_uniform_continuous`: let `f : Î³ â†’ Î±`, `ğ”– : set (set Î±)`,
  `ğ”— : set (set Î³)`, and assume that `âˆ€ T âˆˆ ğ”—, f '' T âˆˆ ğ”–`. Then, the function
  `(Î» g, g âˆ˜ f) : (Î± â†’ Î², ğ’±(Î±, Î², ğ”–, uÎ²)) â†’ (Î³ â†’ Î², ğ’±(Î³, Î², ğ”— uÎ²))` is uniformly continuous.

#### Isomorphism statements (bundled)

* `uniform_convergence_on.congr_right`: turn a uniform isomorphism `(Î³, uÎ³) â‰ƒáµ¤ (Î², uÎ²)` into a
  uniform isomorphism `(Î± â†’ Î³, ğ’±(Î±, Î³, ğ”–, uÎ³)) â‰ƒáµ¤ (Î± â†’ Î², ğ’±(Î±, Î², ğ”–, uÎ²))` by post-composing.
* `uniform_convergence_on.congr_left`: turn a bijection `e : Î³ â‰ƒ Î±` such that we have both
  `âˆ€ T âˆˆ ğ”—, e '' T âˆˆ ğ”–` and `âˆ€ S âˆˆ ğ”–, e â»Â¹' S âˆˆ ğ”—` into a uniform isomorphism
  `(Î³ â†’ Î², ğ’°(Î³, Î², uÎ²)) â‰ƒáµ¤ (Î± â†’ Î², ğ’°(Î±, Î², uÎ²))` by pre-composing.
* `uniform_convergence_on.uniform_equiv_Pi_comm`: the natural bijection between `Î± â†’ Î  i, Î´ i`
  and `Î  i, Î± â†’ Î´ i`, upgraded to a uniform isomorphism between
  `(Î± â†’ (Î  i, Î´ i), ğ’±(Î±, (Î  i, Î´ i), ğ”–, (Î  i, uÎ´ i)))` and
  `((Î  i, Î± â†’ Î´ i), (Î  i, ğ’±(Î±, Î´ i, ğ”–, uÎ´ i)))`.

#### Important use cases

* If `(G, uG)` is a uniform group, then `(Î± â†’ G, ğ’±(Î±, G, ğ”–, uG))` is a uniform group: since
  `(/) : G Ã— G â†’ G` is uniformly continuous, `uniform_convergence_on.postcomp_uniform_continuous`
  tells us that `((/) âˆ˜ â€”) : (Î± â†’ G Ã— G) â†’ (Î± â†’ G)` is uniformly continuous. By precomposing with
  `uniform_convergence_on.uniform_equiv_prod_arrow`, this gives that
  `(/) : (Î± â†’ G) Ã— (Î± â†’ G) â†’ (Î± â†’ G)` is also uniformly continuous
* The transpose of a continuous linear map is continuous for the strong topologies: since
  continuous linear maps are uniformly continuous and map bounded sets to bounded sets,
  this is just a special case of `uniform_convergence_on.precomp_uniform_continuous`.

## Implementation details

We do not declare these structures as instances, since they would conflict with `Pi.uniform_space`.

## TODO

* Show that the uniform structure of `ğ”–`-convergence is exactly the structure of `ğ”–'`-convergence,
  where `ğ”–'` is the ***noncovering*** bornology (i.e ***not*** what `bornology` currently refers
  to in mathlib) generated by `ğ”–`.
* Add a type synonym for `Î± â†’ Î²` endowed with the structures of uniform convergence?

## References

* [N. Bourbaki, *General Topology, Chapter X*][bourbaki1966]

## Tags

uniform convergence
-/

noncomputable theory
open_locale topological_space classical uniformity filter

local attribute [-instance] Pi.uniform_space
local attribute [-instance] Pi.topological_space

open set filter

namespace uniform_convergence

variables (Î± Î² : Type*) {Î³ Î¹ : Type*}
variables {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {s s' : set Î±} {x : Î±} {p : filter Î¹} {g : Î¹ â†’ Î±}

/-- Basis sets for the uniformity of uniform convergence: `gen Î± Î² V` is the set of pairs `(f, g)`
of functions `Î± â†’ Î²` such that `âˆ€ x, (f x, g x) âˆˆ V`. -/
protected def gen (V : set (Î² Ã— Î²)) : set ((Î± â†’ Î²) Ã— (Î± â†’ Î²)) :=
  {uv : (Î± â†’ Î²) Ã— (Î± â†’ Î²) | âˆ€ x, (uv.1 x, uv.2 x) âˆˆ V}

/-- If `ğ“•` is a filter on `Î² Ã— Î²`, then the set of all `uniform_convergence.gen Î± Î² V` for
`V âˆˆ ğ“•` is too. This will only be applied to `ğ“• = ğ“¤ Î²` when `Î²` is equipped with a `uniform_space`
structure, but it is useful to define it for any filter in order to be able to state that it
has a lower adjoint (see `uniform_convergence.gc`). -/
protected lemma is_basis_gen (ğ“‘ : filter $ Î² Ã— Î²) :
  is_basis (Î» V : set (Î² Ã— Î²), V âˆˆ ğ“‘) (uniform_convergence.gen Î± Î²) :=
âŸ¨âŸ¨univ, univ_memâŸ©, Î» U V hU hV, âŸ¨U âˆ© V, inter_mem hU hV, Î» uv huv,
  âŸ¨Î» x, (huv x).left, Î» x, (huv x).rightâŸ©âŸ©âŸ©

/-- For `ğ“• : filter (Î² Ã— Î²)`, this is the set of all `uniform_convergence.gen Î± Î² V` for
`V âˆˆ ğ“•` is as a bundled `filter_basis`. This will only be applied to `ğ“• = ğ“¤ Î²` when `Î²` is
equipped with a `uniform_space` structure, but it is useful to define it for any filter in order
to be able to state that it has a lower adjoint (see `uniform_convergence.gc`). -/
protected def basis (ğ“• : filter $ Î² Ã— Î²) : filter_basis ((Î± â†’ Î²) Ã— (Î± â†’ Î²)) :=
(uniform_convergence.is_basis_gen Î± Î² ğ“•).filter_basis

/-- For `ğ“• : filter (Î² Ã— Î²)`, this is the filter generated by the filter basis
`uniform_convergence.basis Î± Î² ğ“•`. For `ğ“• = ğ“¤ Î²`, this will be the uniformity of uniform
convergence on `Î±`. -/
protected def filter (ğ“• : filter $ Î² Ã— Î²) : filter ((Î± â†’ Î²) Ã— (Î± â†’ Î²)) :=
(uniform_convergence.basis Î± Î² ğ“•).filter

local notation `Î¦` :=
Î» (Î± Î² : Type*) (uvx : ((Î± â†’ Î²) Ã— (Î± â†’ Î²)) Ã— Î±), (uvx.1.1 uvx.2, uvx.1.2 uvx.2)

/- This is a lower adjoint to `uniform_convergence.filter` (see `uniform_convergence.gc`).
The exact definition of the lower adjoint `l` is not interesting; we will only use that it exists
(in `uniform_convergence.mono` and `uniform_convergence.infi_eq`) and that
`l (filter.map (prod.map f f) ğ“•) = filter.map (prod.map ((âˆ˜) f) ((âˆ˜) f)) (l ğ“•)` for each
`ğ“• : filter (Î³ Ã— Î³)` and `f : Î³ â†’ Î±` (in `uniform_convergence.comap_eq`). -/
local notation `lower_adjoint` :=
Î» ğ“, map (Î¦ Î± Î²) (ğ“ Ã—á¶  âŠ¤)

/-- The function `uniform_convergence.filter Î± Î² : filter (Î² Ã— Î²) â†’ filter ((Î± â†’ Î²) Ã— (Î± â†’ Î²))`
has a lower adjoint `l` (in the sense of `galois_connection`). The exact definition of `l` is not
interesting; we will only use that it exists (in `uniform_convergence.mono` and
`uniform_convergence.infi_eq`) and that
`l (filter.map (prod.map f f) ğ“•) = filter.map (prod.map ((âˆ˜) f) ((âˆ˜) f)) (l ğ“•)` for each
`ğ“• : filter (Î³ Ã— Î³)` and `f : Î³ â†’ Î±` (in `uniform_convergence.comap_eq`). -/
protected lemma gc : galois_connection lower_adjoint
  (Î» ğ“•, uniform_convergence.filter Î± Î² ğ“•) :=
begin
  intros ğ“ ğ“•,
  symmetry,
  calc ğ“ â‰¤ uniform_convergence.filter Î± Î² ğ“•
      â†” (uniform_convergence.basis Î± Î² ğ“•).sets âŠ† ğ“.sets :
        by rw [uniform_convergence.filter, â† filter_basis.generate, sets_iff_generate]
  ... â†” âˆ€ U âˆˆ ğ“•, uniform_convergence.gen Î± Î² U âˆˆ ğ“ : image_subset_iff
  ... â†” âˆ€ U âˆˆ ğ“•, {uv | âˆ€ x, (uv, x) âˆˆ
          {t : ((Î± â†’ Î²) Ã— (Î± â†’ Î²)) Ã— Î± | (t.1.1 t.2, t.1.2 t.2) âˆˆ U}} âˆˆ ğ“ : iff.rfl
  ... â†” âˆ€ U âˆˆ ğ“•, {uvx : ((Î± â†’ Î²) Ã— (Î± â†’ Î²)) Ã— Î± | (uvx.1.1 uvx.2, uvx.1.2 uvx.2) âˆˆ U} âˆˆ
          ğ“ Ã—á¶  (âŠ¤ : filter Î±) : forallâ‚‚_congr (Î» U hU, mem_prod_top.symm)
  ... â†” lower_adjoint ğ“ â‰¤ ğ“• : iff.rfl,
end

variables [uniform_space Î²]

/-- Core of the uniform structure of uniform convergence. -/
protected def uniform_core : uniform_space.core (Î± â†’ Î²) :=
uniform_space.core.mk_of_basis (uniform_convergence.basis Î± Î² (ğ“¤ Î²))
  (Î» U âŸ¨V, hV, hVUâŸ© f, hVU â–¸ Î» x, refl_mem_uniformity hV)
  (Î» U âŸ¨V, hV, hVUâŸ©, hVU â–¸ âŸ¨uniform_convergence.gen Î± Î² (prod.swap â»Â¹' V),
    âŸ¨prod.swap â»Â¹' V, tendsto_swap_uniformity hV, rflâŸ©, Î» uv huv x, huv xâŸ©)
  (Î» U âŸ¨V, hV, hVUâŸ©, hVU â–¸ let âŸ¨W, hW, hWVâŸ© := comp_mem_uniformity_sets hV in
    âŸ¨uniform_convergence.gen Î± Î² W, âŸ¨W, hW, rflâŸ©, Î» uv âŸ¨w, huw, hwvâŸ© x, hWV
      âŸ¨w x, by exact âŸ¨huw x, hwv xâŸ©âŸ©âŸ©)

/-- Uniform structure of uniform convergence. We will denote it `ğ’°(Î±, Î², uÎ²)`. -/
protected def uniform_space : uniform_space (Î± â†’ Î²) :=
uniform_space.of_core (uniform_convergence.uniform_core Î± Î²)

local attribute [instance] uniform_convergence.uniform_space
local notation `ğ’°(` Î± `,` Î² `,` u `)` := @uniform_convergence.uniform_space Î± Î² u

/-- By definition, the uniformity of `Î± â†’ Î²` endowed with the structure of uniform convergence on
`Î±` admits the family `{(f, g) | âˆ€ x, (f x, g x) âˆˆ V}` for `V âˆˆ ğ“¤ Î²` as a filter basis. -/
protected lemma has_basis_uniformity :
  (ğ“¤ (Î± â†’ Î²)).has_basis (Î» V, V âˆˆ ğ“¤ Î²)
  (uniform_convergence.gen Î± Î²) :=
(uniform_convergence.is_basis_gen Î± Î² (ğ“¤ Î²)).has_basis

/-- Topology of uniform convergence. -/
protected def topological_space : topological_space (Î± â†’ Î²) :=
(ğ’°(Î±, Î², infer_instance)).to_topological_space

/-- If `Î± â†’ Î²` is endowed with the topology of uniform convergence, `ğ“ f` admits the family
`{g | âˆ€ x, (f x, g x) âˆˆ V}` for `V âˆˆ ğ“¤ Î²` as a filter basis. -/
protected lemma has_basis_nhds :
  (ğ“ f).has_basis (Î» V, V âˆˆ ğ“¤ Î²)
  (Î» V, {g | (f, g) âˆˆ uniform_convergence.gen Î± Î² V}) :=
nhds_basis_uniformity' (uniform_convergence.has_basis_uniformity Î± Î²)

variables {Î±}

/-- Evaluation at a fixed point is uniformly continuous for `ğ’°(Î±, Î², uÎ²)`. -/
lemma uniform_continuous_eval (x : Î±) : uniform_continuous (function.eval x : (Î± â†’ Î²) â†’ Î²) :=
begin
  change _ â‰¤ _,
  rw [map_le_iff_le_comap,
      (uniform_convergence.has_basis_uniformity Î± Î²).le_basis_iff ((ğ“¤ _).basis_sets.comap _)],
  exact Î» U hU, âŸ¨U, hU, Î» uv huv, huv xâŸ©
end

variables {Î²}

/-- If `uâ‚` and `uâ‚‚` are two uniform structures on `Î³` and `uâ‚ â‰¤ uâ‚‚`, then
`ğ’°(Î±, Î³, uâ‚) â‰¤ ğ’°(Î±, Î³, uâ‚‚)`. -/
protected lemma mono : monotone (@uniform_convergence.uniform_space Î± Î³) :=
Î» uâ‚ uâ‚‚ hu, (uniform_convergence.gc Î± Î³).monotone_u hu

/-- If `u` is a family of uniform structures on `Î³`, then
`ğ’°(Î±, Î³, (â¨… i, u i)) = â¨… i, ğ’°(Î±, Î³, u i)`. -/
protected lemma infi_eq {u : Î¹ â†’ uniform_space Î³} :
  (ğ’°(Î±, Î³, â¨… i, u i)) = â¨… i, ğ’°(Î±, Î³, u i) :=
begin
  -- This follows directly from the fact that the upper adjoint in a Galois connection maps
  -- infimas to infimas.
  ext : 1,
  change uniform_convergence.filter Î± Î³ (@uniformity _ (â¨… i, u i)) =
    @uniformity _ (â¨… i, (ğ’°(Î±, Î³, u i))),
  rw [infi_uniformity', infi_uniformity'],
  exact (uniform_convergence.gc Î± Î³).u_infi
end

/-- If `uâ‚` and `uâ‚‚` are two uniform structures on `Î³`, then
`ğ’°(Î±, Î³, uâ‚ âŠ“ uâ‚‚) = ğ’°(Î±, Î³, uâ‚) âŠ“ ğ’°(Î±, Î³, uâ‚‚)`. -/
protected lemma inf_eq {uâ‚ uâ‚‚ : uniform_space Î³} :
  (ğ’°(Î±, Î³, uâ‚ âŠ“ uâ‚‚)) = (ğ’°(Î±, Î³, uâ‚)) âŠ“ (ğ’°(Î±, Î³, uâ‚‚)) :=
begin
  -- This follows directly from the fact that the upper adjoint in a Galois connection maps
  -- infimas to infimas.
  rw [inf_eq_infi, inf_eq_infi, uniform_convergence.infi_eq],
  refine infi_congr (Î» i, _),
  cases i; refl
end

/-- If `u` is a uniform structures on `Î²` and `f : Î³ â†’ Î²`, then
`ğ’°(Î±, Î³, comap f u) = comap (Î» g, f âˆ˜ g) ğ’°(Î±, Î³, uâ‚)`. -/
protected lemma comap_eq {f : Î³ â†’ Î²} :
  (ğ’°(Î±, Î³, â€¹uniform_space Î²â€º.comap f)) = (ğ’°(Î±, Î², _)).comap ((âˆ˜) f) :=
begin
  letI : uniform_space Î³ := â€¹uniform_space Î²â€º.comap f,
  ext : 1,
  change (uniform_convergence.filter Î± Î³ ((ğ“¤ Î²).comap _)) =
    (uniform_convergence.filter Î± Î² ((ğ“¤ Î²))).comap _,
  -- We have the following four Galois connection which form a square diagram, and we want
  -- to show that the square of upper adjoints is commutative. The trick then is to use
  -- `galois_connection.u_comm_of_l_comm` to reduce it to commutativity of the lower adjoints,
  -- which is way easier to prove.
  have hâ‚ := filter.gc_map_comap (prod.map ((âˆ˜) f) ((âˆ˜) f)),
  have hâ‚‚ := filter.gc_map_comap (prod.map f f),
  have hâ‚ƒ := uniform_convergence.gc Î± Î²,
  have hâ‚„ := uniform_convergence.gc Î± Î³,
  refine galois_connection.u_comm_of_l_comm hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ (Î» ğ“, _),
  have : prod.map f f âˆ˜ (Î¦ Î± Î³) =
    (Î¦ Î± Î²) âˆ˜ prod.map (prod.map ((âˆ˜) f) ((âˆ˜) f)) id,
  { ext; refl },
  rw [map_comm this, â† prod_map_map_eq'],
  refl
end

/-- Post-composition by a uniformly continuous function is uniformly continuous for the
uniform structures of uniform convergence.

More precisely, if `f : (Î³, uÎ³) â†’ (Î², uÎ²)` is uniformly continuous, then
`(Î» g, f âˆ˜ g) : (Î± â†’ Î³, ğ’°(Î±, Î³, uÎ³)) â†’ (Î± â†’ Î², ğ’°(Î±, Î², uÎ²))` is uniformly continuous. -/
protected lemma postcomp_uniform_continuous [uniform_space Î³] {f : Î³ â†’ Î²}
  (hf : uniform_continuous f):
  uniform_continuous ((âˆ˜) f : (Î± â†’ Î³) â†’ Î± â†’ Î²) :=
-- This is a direct consequence of `uniform_convergence.comap_eq`
uniform_continuous_iff.mpr $
calc ğ’°(Î±, Î³, _)
    â‰¤ ğ’°(Î±, Î³, â€¹uniform_space Î²â€º.comap f) :
      uniform_convergence.mono (uniform_continuous_iff.mp hf)
... = (ğ’°(Î±, Î², _)).comap ((âˆ˜) f) :
      uniform_convergence.comap_eq

/-- Post-composition by a uniform inducing is a uniform inducing for the
uniform structures of uniform convergence.

More precisely, if `f : (Î³, uÎ³) â†’ (Î², uÎ²)` is a uniform inducing, then
`(Î» g, f âˆ˜ g) : (Î± â†’ Î³, ğ’°(Î±, Î³, uÎ³)) â†’ (Î± â†’ Î², ğ’°(Î±, Î², uÎ²))` is a uniform inducing. -/
protected lemma postcomp_uniform_inducing [uniform_space Î³] {f : Î³ â†’ Î²}
  (hf : uniform_inducing f):
  uniform_inducing ((âˆ˜) f : (Î± â†’ Î³) â†’ Î± â†’ Î²) :=
-- This is a direct consequence of `uniform_convergence.comap_eq`
begin
  split,
  replace hf : (ğ“¤ Î²).comap (prod.map f f) = _ := hf.comap_uniformity,
  change comap (prod.map ((âˆ˜) f) ((âˆ˜) f)) _ = _,
  rw [â† uniformity_comap rfl] at âŠ¢ hf,
  congr,
  rw [â† uniform_space_eq hf, uniform_convergence.comap_eq]
end

/-- Turn a uniform isomorphism `(Î³, uÎ³) â‰ƒáµ¤ (Î², uÎ²)` into a uniform isomorphism
`(Î± â†’ Î³, ğ’°(Î±, Î³, uÎ³)) â‰ƒáµ¤ (Î± â†’ Î², ğ’°(Î±, Î², uÎ²))` by post-composing. -/
protected def congr_right [uniform_space Î³] (e : Î³ â‰ƒáµ¤ Î²) :
  (Î± â†’ Î³) â‰ƒáµ¤ (Î± â†’ Î²) :=
{ uniform_continuous_to_fun :=
    uniform_convergence.postcomp_uniform_continuous e.uniform_continuous,
  uniform_continuous_inv_fun :=
    uniform_convergence.postcomp_uniform_continuous e.symm.uniform_continuous,
  .. equiv.Pi_congr_right (Î» a, e.to_equiv) }

/-- Pre-composition by a any function is uniformly continuous for the uniform structures of
uniform convergence.

More precisely, for any `f : Î³ â†’ Î±`, the function
`(Î» g, g âˆ˜ f) : (Î± â†’ Î², ğ’°(Î±, Î², uÎ²)) â†’ (Î³ â†’ Î², ğ’°(Î³, Î², uÎ²))` is uniformly continuous. -/
protected lemma precomp_uniform_continuous {f : Î³ â†’ Î±} :
  uniform_continuous (Î» g : Î± â†’ Î², g âˆ˜ f) :=
begin
  -- Here we simply go back to filter bases.
  rw uniform_continuous_iff,
  change ğ“¤ (Î± â†’ Î²) â‰¤ (ğ“¤ (Î³ â†’ Î²)).comap (prod.map (Î» g : Î± â†’ Î², g âˆ˜ f) (Î» g : Î± â†’ Î², g âˆ˜ f)),
  rw (uniform_convergence.has_basis_uniformity Î± Î²).le_basis_iff
    ((uniform_convergence.has_basis_uniformity Î³ Î²).comap _),
  exact Î» U hU, âŸ¨U, hU, Î» uv huv x, huv (f x)âŸ©
end

/-- Turn a bijection `Î³ â‰ƒ Î±` into a uniform isomorphism
`(Î³ â†’ Î², ğ’°(Î³, Î², uÎ²)) â‰ƒáµ¤ (Î± â†’ Î², ğ’°(Î±, Î², uÎ²))` by pre-composing. -/
protected def congr_left (e : Î³ â‰ƒ Î±) :
  (Î³ â†’ Î²) â‰ƒáµ¤ (Î± â†’ Î²) :=
{ uniform_continuous_to_fun :=
    uniform_convergence.precomp_uniform_continuous,
  uniform_continuous_inv_fun :=
    uniform_convergence.precomp_uniform_continuous,
  .. equiv.arrow_congr e (equiv.refl _) }

/-- The topology of uniform convergence is Tâ‚‚. -/
lemma t2_space [t2_space Î²] : t2_space (Î± â†’ Î²) :=
{ t2 :=
  begin
    letI : uniform_space (Î± â†’ Î²) := ğ’°(Î±, Î², _),
    letI : topological_space (Î± â†’ Î²) := uniform_convergence.topological_space Î± Î²,
    intros f g h,
    obtain âŸ¨x, hxâŸ© := not_forall.mp (mt funext h),
    exact separated_by_continuous (uniform_continuous_eval Î² x).continuous hx
  end }

/-- The uniform structure of uniform convergence is finer than that of pointwise convergence,
aka the product uniform structure. -/
protected lemma le_Pi : ğ’°(Î±, Î², _) â‰¤ Pi.uniform_space (Î» _, Î²) :=
begin
  -- By definition of the product uniform structure, this is just `uniform_continuous_eval`.
  rw [le_iff_uniform_continuous_id, uniform_continuous_pi],
  intros x,
  exact uniform_continuous_eval Î² x
end

/-- The topology of uniform convergence indeed gives the same notion of convergence as
`tendsto_uniformly`. -/
protected lemma tendsto_iff_tendsto_uniformly : tendsto F p (ğ“ f) â†” tendsto_uniformly F f p :=
begin
  letI : uniform_space (Î± â†’ Î²) := ğ’°(Î±, Î², _),
  rw [(uniform_convergence.has_basis_nhds Î± Î²).tendsto_right_iff, tendsto_uniformly],
  exact iff.rfl,
end

/-- The natural bijection between `Î± â†’ Î² Ã— Î³` and `(Î± â†’ Î²) Ã— (Î± â†’ Î³)`, upgraded to a uniform
isomorphism between `(Î± â†’ Î² Ã— Î³, ğ’°(Î±, Î² Ã— Î³, uÎ² Ã— uÎ³))` and
`((Î± â†’ Î²) Ã— (Î± â†’ Î³), ğ’°(Î±, Î², uÎ²) Ã— ğ’°(Î±, Î³, uÎ³))`. -/
protected def uniform_equiv_prod_arrow [uniform_space Î³] :
  (Î± â†’ Î² Ã— Î³) â‰ƒáµ¤ ((Î± â†’ Î²) Ã— (Î± â†’ Î³)) :=
-- Denote `Ï†` this bijection. We want to show that
-- `comap Ï† (ğ’°(Î±, Î², uÎ²) Ã— ğ’°(Î±, Î³, uÎ³)) = ğ’°(Î±, Î² Ã— Î³, uÎ² Ã— uÎ³)`.
-- But `uÎ² Ã— uÎ³` is defined as `comap fst uÎ² âŠ“ comap snd uÎ³`, so we just have to apply
-- `uniform_convergence.inf_eq` and `uniform_convergence.comap_eq`, and then chase
-- a diagram.
(equiv.arrow_prod_equiv_prod_arrow _ _ _).to_uniform_equiv_of_uniform_inducing
begin
  split,
  change comap (prod.map (equiv.arrow_prod_equiv_prod_arrow _ _ _)
    (equiv.arrow_prod_equiv_prod_arrow _ _ _)) _ = _,
  rw â† uniformity_comap rfl,
  congr,
  rw [prod.uniform_space, uniform_space.of_core_eq_to_core, prod.uniform_space,
      uniform_space.of_core_eq_to_core, uniform_space.comap_inf, uniform_convergence.inf_eq],
  congr;
  rw [â† uniform_space.comap_comap, uniform_convergence.comap_eq];
  refl -- this is the diagram chase
end

variables (Î±) (Î´ : Î¹ â†’ Type*) [Î  i, uniform_space (Î´ i)]

local attribute [-instance] uniform_convergence.uniform_space

/-- The natural bijection between `Î± â†’ Î  i, Î´ i` and `Î  i, Î± â†’ Î´ i`, upgraded to a uniform
isomorphism between `(Î± â†’ (Î  i, Î´ i), ğ’°(Î±, (Î  i, Î´ i), (Î  i, uÎ´ i)))` and
`((Î  i, Î± â†’ Î´ i), (Î  i, ğ’°(Î±, Î´ i, uÎ´ i)))`. -/
protected def uniform_equiv_Pi_comm : @uniform_equiv (Î± â†’ Î  i, Î´ i) (Î  i, Î± â†’ Î´ i)
  (ğ’°(Î±, Î  i, Î´ i, Pi.uniform_space Î´))
  (@Pi.uniform_space Î¹ (Î» i, Î± â†’ Î´ i) (Î» i, ğ’°(Î±, Î´ i, _))) :=
-- Denote `Ï†` this bijection. We want to show that
-- `comap Ï† (Î  i, ğ’°(Î±, Î´ i, uÎ´ i)) = ğ’°(Î±, (Î  i, Î´ i), (Î  i, uÎ´ i))`.
-- But `Î  i, uÎ´ i` is defined as `â¨… i, comap (eval i) (uÎ´ i)`, so we just have to apply
-- `uniform_convergence.infi_eq` and `uniform_convergence.comap_eq`, and then chase
-- a diagram.
@equiv.to_uniform_equiv_of_uniform_inducing _ _
  (ğ’°(Î±, Î  i, Î´ i, Pi.uniform_space Î´))
  (@Pi.uniform_space Î¹ (Î» i, Î± â†’ Î´ i) (Î» i, ğ’°(Î±, Î´ i, _)))
  (equiv.Pi_comm _)
begin
  split,
  change comap (prod.map function.swap function.swap) _ = _,
  rw â† uniformity_comap rfl,
  congr,
  rw [Pi.uniform_space, uniform_space.of_core_eq_to_core, Pi.uniform_space,
      uniform_space.of_core_eq_to_core, uniform_space.comap_infi, uniform_convergence.infi_eq],
  refine infi_congr (Î» i, _),
  rw [â† uniform_space.comap_comap, uniform_convergence.comap_eq]
  -- Like in the previous lemma, the diagram chase is actually true by defeq
end

end uniform_convergence

namespace uniform_convergence_on

variables (Î± Î² : Type*) {Î³ Î¹ : Type*} [uniform_space Î²] (ğ”– : set (set Î±))
variables {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {s s' : set Î±} {x : Î±} {p : filter Î¹} {g : Î¹ â†’ Î±}

local notation `ğ’°(` Î± `,` Î² `,` u `)` := @uniform_convergence.uniform_space Î± Î² u

/-- Uniform structure of `ğ”–`-convergence, i.e uniform convergence on the elements of `ğ”–`.
It is defined as the infimum, for `S âˆˆ ğ”–`, of the pullback of `ğ’° S Î²` by `S.restrict`, the
map of restriction to `S`. We will denote it `ğ’±(Î±, Î², ğ”–, uÎ²)`, where `uÎ²` is the uniform structure
on `Î²`. -/
protected def uniform_space : uniform_space (Î± â†’ Î²) :=
â¨… (s : set Î±) (hs : s âˆˆ ğ”–), uniform_space.comap s.restrict
  (ğ’°(s, Î², _))

local notation `ğ’±(` Î± `,` Î² `,` ğ”– `,` u `)` := @uniform_convergence_on.uniform_space Î± Î² u ğ”–

/-- Topology of `ğ”–`-convergence, i.e uniform convergence on the elements of `ğ”–`. -/
protected def topological_space : topological_space (Î± â†’ Î²) :=
(ğ’±(Î±, Î², ğ”–, _)).to_topological_space

/-- The topology of `ğ”–`-convergence is the infimum, for `S âˆˆ ğ”–`, of topology induced by the map
of restriction to `S`, where `â†¥S â†’ Î²` is endowed with the topology of uniform convergence. -/
protected lemma topological_space_eq :
  uniform_convergence_on.topological_space Î± Î² ğ”– = â¨… (s : set Î±) (hs : s âˆˆ ğ”–),
  topological_space.induced s.restrict (uniform_convergence.topological_space s Î²) :=
begin
  simp only [uniform_convergence_on.topological_space, to_topological_space_infi,
    to_topological_space_infi, to_topological_space_comap],
  refl
end

/-- If `S âˆˆ ğ”–`, then the restriction to `S` is a uniformly continuous map from `ğ’±(Î±, Î², ğ”–, uÎ²)` to
`ğ’°(â†¥S, Î², uÎ²)`. -/
protected lemma uniform_continuous_restrict (h : s âˆˆ ğ”–) :
  @uniform_continuous _ _ (ğ’±(Î±, Î², ğ”–, _))
  (ğ’°(s, Î², _)) s.restrict :=
begin
  change _ â‰¤ _,
  rw [uniform_convergence_on.uniform_space, map_le_iff_le_comap, uniformity, infi_uniformity],
  refine infi_le_of_le s _,
  rw infi_uniformity,
  exact infi_le _ h,
end

variables {Î±}

/-- Let `uâ‚`, `uâ‚‚` be two uniform structures on `Î³` and `ğ”–â‚ ğ”–â‚‚ : set (set Î±)`. If `uâ‚ â‰¤ uâ‚‚` and
`ğ”–â‚‚ âŠ† ğ”–â‚` then `ğ’±(Î±, Î³, ğ”–â‚, uâ‚) â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚‚)`. -/
protected lemma mono â¦ƒuâ‚ uâ‚‚ : uniform_space Î³â¦„ (hu : uâ‚ â‰¤ uâ‚‚) â¦ƒğ”–â‚ ğ”–â‚‚ : set (set Î±)â¦„
  (hğ”– : ğ”–â‚‚ âŠ† ğ”–â‚) :
  ğ’±(Î±, Î³, ğ”–â‚, uâ‚) â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚‚) :=
calc ğ’±(Î±, Î³, ğ”–â‚, uâ‚)
    â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚) : infi_le_infi_of_subset hğ”–
... â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚‚) : infiâ‚‚_mono
        (Î» i hi, uniform_space.comap_mono $ uniform_convergence.mono hu)

/-- If `x : Î±` is in some `S âˆˆ ğ”–`, then evaluation at `x` is uniformly continuous for
`ğ’±(Î±, Î², ğ”–, uÎ²)`. -/
lemma uniform_continuous_eval_of_mem {x : Î±} (hxs : x âˆˆ s) (hs : s âˆˆ ğ”–) :
  @uniform_continuous _ _ ğ’±(Î±, Î², ğ”–, _) _ (function.eval x) :=
@uniform_continuous.comp (Î± â†’ Î²) (s â†’ Î²) Î² (ğ’±(Î±, Î², ğ”–, _)) (ğ’°(s, Î², _)) _ _ _
  (uniform_convergence.uniform_continuous_eval Î² (âŸ¨x, hxsâŸ© : s))
  (uniform_convergence_on.uniform_continuous_restrict Î± Î² ğ”– hs)

variables {Î²} {ğ”–}

/-- If `u` is a family of uniform structures on `Î³`, then
`ğ’±(Î±, Î³, ğ”–, (â¨… i, u i)) = â¨… i, ğ’±(Î±, Î³, ğ”–, u i)`. -/
protected lemma infi_eq {u : Î¹ â†’ uniform_space Î³} :
  ğ’±(Î±, Î³, ğ”–, â¨… i, u i) =
  â¨… i, ğ’±(Î±, Î³, ğ”–, u i) :=
begin
  simp_rw [uniform_convergence_on.uniform_space, uniform_convergence.infi_eq,
    uniform_space.comap_infi],
  rw infi_comm,
  exact infi_congr (Î» s, infi_comm)
end

/-- If `uâ‚` and `uâ‚‚` are two uniform structures on `Î³`, then
`ğ’±(Î±, Î³, ğ”–, uâ‚ âŠ“ uâ‚‚) = ğ’±(Î±, Î³, ğ”–, uâ‚) âŠ“ ğ’±(Î±, Î³, ğ”–, uâ‚‚)`. -/
protected lemma inf_eq {uâ‚ uâ‚‚ : uniform_space Î³} :
  ğ’±(Î±, Î³, ğ”–, uâ‚ âŠ“ uâ‚‚) =
  ğ’±(Î±, Î³, ğ”–, uâ‚) âŠ“
  ğ’±(Î±, Î³, ğ”–, uâ‚‚) :=
begin
  rw [inf_eq_infi, inf_eq_infi, uniform_convergence_on.infi_eq],
  refine infi_congr (Î» i, _),
  cases i; refl
end

/-- If `u` is a uniform structures on `Î²` and `f : Î³ â†’ Î²`, then
`ğ’±(Î±, Î³, ğ”–, comap f u) = comap (Î» g, f âˆ˜ g) ğ’±(Î±, Î³, ğ”–, uâ‚)`. -/
protected lemma comap_eq {f : Î³ â†’ Î²} :
  ğ’±(Î±, Î³, ğ”–, â€¹uniform_space Î²â€º.comap f) =
  ğ’±(Î±, Î², ğ”–, _).comap ((âˆ˜) f) :=
begin
  -- We reduce this to `uniform_convergence.comap_eq` using the fact that `comap` distributes
  -- on `infi`.
  simp_rw [uniform_convergence_on.uniform_space, uniform_space.comap_infi,
            uniform_convergence.comap_eq, â† uniform_space.comap_comap],
  refl -- small diagram chase
end

/-- Post-composition by a uniformly continuous function is uniformly continuous for the
uniform structures of `ğ”–`-convergence.

More precisely, if `f : (Î³, uÎ³) â†’ (Î², uÎ²)` is uniformly continuous, then
`(Î» g, f âˆ˜ g) : (Î± â†’ Î³, ğ’±(Î±, Î³, ğ”–, uÎ³)) â†’ (Î± â†’ Î², ğ’±(Î±, Î², ğ”–, uÎ²))` is uniformly continuous. -/
protected lemma postcomp_uniform_continuous [uniform_space Î³] {f : Î³ â†’ Î²}
  (hf : uniform_continuous f):
  @uniform_continuous (Î± â†’ Î³) (Î± â†’ Î²) ğ’±(Î±, Î³, ğ”–, _) ğ’±(Î±, Î², ğ”–, _) ((âˆ˜) f) :=
begin
  -- This is a direct consequence of `uniform_convergence.comap_eq`
  rw uniform_continuous_iff,
  calc ğ’±(Î±, Î³, ğ”–, _)
      â‰¤ ğ’±(Î±, Î³, ğ”–, â€¹uniform_space Î²â€º.comap f) :
        uniform_convergence_on.mono (uniform_continuous_iff.mp hf) (subset_rfl)
  ... = ğ’±(Î±, Î², ğ”–, _).comap ((âˆ˜) f) :
        uniform_convergence_on.comap_eq
end

/-- Post-composition by a uniform inducing is a uniform inducing for the
uniform structures of `ğ”–`-convergence.

More precisely, if `f : (Î³, uÎ³) â†’ (Î², uÎ²)` is a uniform inducing, then
`(Î» g, f âˆ˜ g) : (Î± â†’ Î³, ğ’±(Î±, Î³, ğ”–, uÎ³)) â†’ (Î± â†’ Î², ğ’±(Î±, Î², ğ”–, uÎ²))` is a uniform inducing. -/
protected lemma postcomp_uniform_inducing [uniform_space Î³] {f : Î³ â†’ Î²}
  (hf : uniform_inducing f):
  @uniform_inducing (Î± â†’ Î³) (Î± â†’ Î²) ğ’±(Î±, Î³, ğ”–, _) ğ’±(Î±, Î², ğ”–, _) ((âˆ˜) f) :=
-- This is a direct consequence of `uniform_convergence.comap_eq`
begin
  split,
  replace hf : (ğ“¤ Î²).comap (prod.map f f) = _ := hf.comap_uniformity,
  change comap (prod.map ((âˆ˜) f) ((âˆ˜) f)) _ = _,
  rw [â† uniformity_comap rfl] at âŠ¢ hf,
  congr,
  rw [â† uniform_space_eq hf, uniform_convergence_on.comap_eq]
end

/-- Turn a uniform isomorphism `(Î³, uÎ³) â‰ƒáµ¤ (Î², uÎ²)` into a uniform isomorphism
`(Î± â†’ Î³, ğ’±(Î±, Î³, ğ”–, uÎ³)) â‰ƒáµ¤ (Î± â†’ Î², ğ’±(Î±, Î², ğ”–, uÎ²))` by post-composing. -/
protected def congr_right [uniform_space Î³] (e : Î³ â‰ƒáµ¤ Î²) :
  @uniform_equiv (Î± â†’ Î³) (Î± â†’ Î²)
  ğ’±(Î±, Î³, ğ”–, _) ğ’±(Î±, Î², ğ”–, _) :=
{ uniform_continuous_to_fun :=
    uniform_convergence_on.postcomp_uniform_continuous e.uniform_continuous,
  uniform_continuous_inv_fun :=
    uniform_convergence_on.postcomp_uniform_continuous e.symm.uniform_continuous,
  .. equiv.Pi_congr_right (Î» a, e.to_equiv) }

/-- Let `f : Î³ â†’ Î±`, `ğ”– : set (set Î±)`, `ğ”— : set (set Î³)`, and assume that `âˆ€ T âˆˆ ğ”—, f '' T âˆˆ ğ”–`.
Then, the function `(Î» g, g âˆ˜ f) : (Î± â†’ Î², ğ’±(Î±, Î², ğ”–, uÎ²)) â†’ (Î³ â†’ Î², ğ’±(Î³, Î², ğ”— uÎ²))` is
uniformly continuous.

Note that one can easily see that assuming `âˆ€ T âˆˆ ğ”—, âˆƒ S âˆˆ ğ”–, f '' T âŠ† S` would work too, but
we will get this for free when we prove that `ğ’±(Î±, Î², ğ”–, uÎ²) = ğ’±(Î±, Î², ğ”–', uÎ²)` for `ğ”–'` the
***noncovering*** bornology generated by `ğ”–`. -/
protected lemma precomp_uniform_continuous {ğ”— : set (set Î³)} {f : Î³ â†’ Î±}
  (hf : ğ”— âŠ† (image f) â»Â¹' ğ”–) :
  @uniform_continuous (Î± â†’ Î²) (Î³ â†’ Î²)
  ğ’±(Î±, Î², ğ”–, _) ğ’±(Î³, Î², ğ”—, _)
  (Î» g : Î± â†’ Î², g âˆ˜ f) :=
begin
  -- Since `comap` distributes on `infi`, it suffices to prove that
  -- `â¨… s âˆˆ ğ”–, comap s.restrict ğ’°(â†¥s, Î², uÎ²) â‰¤ â¨… t âˆˆ ğ”—, comap (t.restrict âˆ˜ (â€” âˆ˜ f)) ğ’°(â†¥t, Î², uÎ²)`.
  simp_rw [uniform_continuous_iff, uniform_convergence_on.uniform_space, uniform_space.comap_infi,
            â† uniform_space.comap_comap],
  -- For any `t âˆˆ ğ”—`, note `s := f '' t âˆˆ ğ”–`.
  -- We will show that `comap s.restrict ğ’°(â†¥s, Î², uÎ²) â‰¤ comap (t.restrict âˆ˜ (â€” âˆ˜ f)) ğ’°(â†¥t, Î², uÎ²)`.
  refine le_infiâ‚‚ (Î» t ht, infi_le_of_le (f '' t) $ infi_le_of_le (hf ht) _),
  -- Let `f'` be the map from `t` to `f '' t` induced by `f`.
  let f' : t â†’ f '' t := (maps_to_image f t).restrict f t (f '' t),
  -- By definition `t.restrict âˆ˜ (â€” âˆ˜ f) = (â€” âˆ˜ f') âˆ˜ (f '' t).restrict`.
  have : t.restrict âˆ˜ (Î» g : Î± â†’ Î², g âˆ˜ f) = (Î» g : (f '' t) â†’ Î², g âˆ˜ f') âˆ˜ (f '' t).restrict :=
    rfl,
  -- Thus, we have to show `comap (f '' t).restrict ğ’°(â†¥(f '' t), Î², uÎ²) â‰¤`
  -- `comap (f '' t).restrict (comap (â€” âˆ˜ f') ğ’°(â†¥t, Î², uÎ²))`.
  rw [this, @uniform_space.comap_comap (Î± â†’ Î²) ((f '' t) â†’ Î²)],
  -- But this is exactly monotonicity of `comap` applied to
  -- `uniform_convergence.precomp_continuous`.
  refine uniform_space.comap_mono _,
  rw â† uniform_continuous_iff,
  exact uniform_convergence.precomp_uniform_continuous
end

/-- Turn a bijection `e : Î³ â‰ƒ Î±` such that we have both `âˆ€ T âˆˆ ğ”—, e '' T âˆˆ ğ”–` and
`âˆ€ S âˆˆ ğ”–, e â»Â¹' S âˆˆ ğ”—` into a uniform isomorphism `(Î³ â†’ Î², ğ’°(Î³, Î², uÎ²)) â‰ƒáµ¤ (Î± â†’ Î², ğ’°(Î±, Î², uÎ²))`
by pre-composing. -/
protected def congr_left {ğ”— : set (set Î³)} (e : Î³ â‰ƒ Î±)
  (he : ğ”— âŠ† (image e) â»Â¹' ğ”–) (he' : ğ”– âŠ† (preimage e) â»Â¹' ğ”—) :
  @uniform_equiv (Î³ â†’ Î²) (Î± â†’ Î²)
  ğ’±(Î³, Î², ğ”—, _) ğ’±(Î±, Î², ğ”–, _) :=
{ uniform_continuous_to_fun :=
    uniform_convergence_on.precomp_uniform_continuous
    begin
      intros s hs,
      change e.symm '' s âˆˆ ğ”—,
      rw â† preimage_equiv_eq_image_symm,
      exact he' hs
    end,
  uniform_continuous_inv_fun :=
    uniform_convergence_on.precomp_uniform_continuous he,
  .. equiv.arrow_congr e (equiv.refl _) }

/-- If `ğ”–` covers `Î±`, then the topology of `ğ”–`-convergence is Tâ‚‚. -/
lemma t2_space_of_covering [t2_space Î²] (h : â‹ƒâ‚€ ğ”– = univ) :
  @t2_space _ (uniform_convergence_on.topological_space Î± Î² ğ”–) :=
{ t2 :=
  begin
    letI : uniform_space (Î± â†’ Î²) := ğ’±(Î±, Î², ğ”–, _),
    letI : topological_space (Î± â†’ Î²) := uniform_convergence_on.topological_space Î± Î² ğ”–,
    intros f g hfg,
    obtain âŸ¨x, hxâŸ© := not_forall.mp (mt funext hfg),
    obtain âŸ¨s, hs, hxsâŸ© : âˆƒ s âˆˆ ğ”–, x âˆˆ s := mem_sUnion.mp (h.symm â–¸ true.intro),
    exact separated_by_continuous (uniform_continuous_eval_of_mem Î² ğ”– hxs hs).continuous hx
  end }

/-- If `ğ”–` covers `Î±`, then the uniform structure of `ğ”–`-convergence is finer than that of
pointwise convergence. -/
protected lemma le_Pi_of_covering (h : â‹ƒâ‚€ ğ”– = univ) :
  ğ’±(Î±, Î², ğ”–, _) â‰¤ Pi.uniform_space (Î» _, Î²) :=
begin
  rw [le_iff_uniform_continuous_id, uniform_continuous_pi],
  intros x,
  obtain âŸ¨s : set Î±, hs : s âˆˆ ğ”–, hxs :  x âˆˆ sâŸ© := sUnion_eq_univ_iff.mp h x,
  exact uniform_continuous_eval_of_mem Î² ğ”– hxs hs
end

/-- Convergence in the topology of `ğ”–`-convergence means uniform convergence on `S` (in the sense
of `tendsto_uniformly_on`) for all `S âˆˆ ğ”–`. -/
protected lemma tendsto_iff_tendsto_uniformly_on :
  tendsto F p (@nhds _ (uniform_convergence_on.topological_space Î± Î² ğ”–) f) â†”
  âˆ€ s âˆˆ ğ”–, tendsto_uniformly_on F f p s :=
begin
  letI : uniform_space (Î± â†’ Î²) := ğ’±(Î±, Î², ğ”–, _),
  rw [uniform_convergence_on.topological_space_eq, nhds_infi, tendsto_infi],
  refine forall_congr (Î» s, _),
  rw [nhds_infi, tendsto_infi],
  refine forall_congr (Î» hs, _),
  rw [nhds_induced, tendsto_comap_iff, tendsto_uniformly_on_iff_tendsto_uniformly_comp_coe,
      uniform_convergence.tendsto_iff_tendsto_uniformly],
  refl
end

/-- The natural bijection between `Î± â†’ Î² Ã— Î³` and `(Î± â†’ Î²) Ã— (Î± â†’ Î³)`, upgraded to a uniform
isomorphism between `(Î± â†’ Î² Ã— Î³, ğ’±(Î±, Î² Ã— Î³, ğ”–, uÎ² Ã— uÎ³))` and
`((Î± â†’ Î²) Ã— (Î± â†’ Î³), ğ’±(Î±, Î², ğ”–, uÎ²) Ã— ğ’°(Î±, Î³, ğ”–, uÎ³))`. -/
protected def uniform_equiv_prod_arrow [uniform_space Î³] :
  @uniform_equiv (Î± â†’ Î² Ã— Î³) ((Î± â†’ Î²) Ã— (Î± â†’ Î³))
  ğ’±(Î±, Î² Ã— Î³, ğ”–, _)
  (@prod.uniform_space _ _ ğ’±(Î±, Î², ğ”–, _) ğ’±(Î±, Î³, ğ”–, _)) :=
-- Denote `Ï†` this bijection. We want to show that
-- `comap Ï† (ğ’±(Î±, Î², ğ”–, uÎ²) Ã— ğ’±(Î±, Î³, ğ”–, uÎ³)) = ğ’±(Î±, Î² Ã— Î³, ğ”–, uÎ² Ã— uÎ³)`.
-- But `uÎ² Ã— uÎ³` is defined as `comap fst uÎ² âŠ“ comap snd uÎ³`, so we just have to apply
-- `uniform_convergence_on.inf_eq` and `uniform_convergence_on.comap_eq`, and then chase
-- a diagram.
-- We could also deduce this from `uniform_convergence.uniform_equiv_prod_arrow`, but it turns out
-- to be more annoying.
@equiv.to_uniform_equiv_of_uniform_inducing _ _
  ğ’±(Î±, Î² Ã— Î³, ğ”–, _)
  (@prod.uniform_space _ _ ğ’±(Î±, Î², ğ”–, _) ğ’±(Î±, Î³, ğ”–, _))
  (equiv.arrow_prod_equiv_prod_arrow _ _ _)
begin
  split,
  change comap (prod.map (equiv.arrow_prod_equiv_prod_arrow _ _ _)
    (equiv.arrow_prod_equiv_prod_arrow _ _ _)) _ = _,
  rw â† uniformity_comap rfl,
  congr,
  rw [prod.uniform_space, uniform_space.of_core_eq_to_core, prod.uniform_space,
      uniform_space.of_core_eq_to_core, uniform_space.comap_inf, uniform_convergence_on.inf_eq],
  congr;
  rw [â† uniform_space.comap_comap, uniform_convergence_on.comap_eq];
  refl -- this is the diagram chase
end

variables (ğ”–) (Î´ : Î¹ â†’ Type*) [Î  i, uniform_space (Î´ i)]

/-- The natural bijection between `Î± â†’ Î  i, Î´ i` and `Î  i, Î± â†’ Î´ i`, upgraded to a uniform
isomorphism between `(Î± â†’ (Î  i, Î´ i), ğ’±(Î±, (Î  i, Î´ i), ğ”–, (Î  i, uÎ´ i)))` and
`((Î  i, Î± â†’ Î´ i), (Î  i, ğ’±(Î±, Î´ i, ğ”–, uÎ´ i)))`. -/
protected def uniform_equiv_Pi_comm : @uniform_equiv (Î± â†’ Î  i, Î´ i) (Î  i, Î± â†’ Î´ i)
  ğ’±(Î±, Î  i, Î´ i, ğ”–, Pi.uniform_space Î´)
  (@Pi.uniform_space Î¹ (Î» i, Î± â†’ Î´ i) (Î» i, ğ’±(Î±, Î´ i, ğ”–, _))) :=
-- Denote `Ï†` this bijection. We want to show that
-- `comap Ï† (Î  i, ğ’±(Î±, Î´ i, ğ”–, uÎ´ i)) = ğ’±(Î±, (Î  i, Î´ i), ğ”–, (Î  i, uÎ´ i))`.
-- But `Î  i, uÎ´ i` is defined as `â¨… i, comap (eval i) (uÎ´ i)`, so we just have to apply
-- `uniform_convergence_on.infi_eq` and `uniform_convergence_on.comap_eq`, and then chase
-- a diagram.
-- We could also deduce this from `uniform_convergence.uniform_equiv_Pi_comm`, but it turns out
-- to be more annoying.
@equiv.to_uniform_equiv_of_uniform_inducing _ _
  ğ’±(Î±, Î  i, Î´ i, ğ”–, Pi.uniform_space Î´)
  (@Pi.uniform_space Î¹ (Î» i, Î± â†’ Î´ i) (Î» i, ğ’±(Î±, Î´ i, ğ”–, _)))
  (equiv.Pi_comm _)
begin
  split,
  change comap (prod.map function.swap function.swap) _ = _,
  rw â† uniformity_comap rfl,
  congr,
  rw [Pi.uniform_space, uniform_space.of_core_eq_to_core, Pi.uniform_space,
      uniform_space.of_core_eq_to_core, uniform_space.comap_infi, uniform_convergence_on.infi_eq],
  refine infi_congr (Î» i, _),
  rw [â† uniform_space.comap_comap, uniform_convergence_on.comap_eq]
  -- Like in the previous lemma, the diagram chase is actually true by defeq
end

end uniform_convergence_on
