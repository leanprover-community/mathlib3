/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Sebastien Gouezel.
-/

import topology.topological_fiber_bundle
import analysis.calculus.deriv
import linear_algebra.dual

/-!
# Topological vector bundles

In this file we define topological vector bundles.

Let `B` be the base space. In our formalism, a topological vector bundle is by definition the type
`bundle.total_space E` where `E : B â†’ Type*` is a function associating to
`x : B` the fiber over `x`. This type `bundle.total_space E` is just a type synonym for
`Î£ (x : B), E x`, with the interest that one can put another topology than on `Î£ (x : B), E x`
which has the disjoint union topology.

To have a topological vector bundle structure on `bundle.total_space E`,
one should addtionally have the following data:

* `F` should be a topological vector space over a field `ğ•œ`;
* There should be a topology on `bundle.total_space E`, for which the projection to `B` is
a topological fiber bundle with fiber `F` (in particular, each fiber `E x` is homeomorphic to `F`);
* For each `x`, the fiber `E x` should be a topological vector space over `ğ•œ`, and the injection
from `E x` to `bundle.total_space F E` should be an embedding;
* The most important condition: around each point, there should be a bundle trivialization which
is a continuous linear equiv in the fibers.

If all these conditions are satisfied, we register the typeclass
`topological_vector_bundle ğ•œ F E`. We emphasize that the data is provided by other classes, and
that the `topological_vector_bundle` class is `Prop`-valued.

The point of this formalism is that it is unbundled in the sense that the total space of the bundle
is a type with a topology, with which one can work or put further structure, and still one can
perform operations on topological vector bundles (which are yet to be formalized). For instance,
assume that `Eâ‚ : B â†’ Type*` and `Eâ‚‚ : B â†’ Type*` define two topological vector bundles over `ğ•œ`
with fiber models `Fâ‚` and `Fâ‚‚` which are normed spaces. Then one can construct the vector bundle of
continuous linear maps from `Eâ‚ x` to `Eâ‚‚ x` with fiber `E x := (Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x)` (and with the
topology inherited from the norm-topology on `Fâ‚ â†’L[ğ•œ] Fâ‚‚`, without the need to define the strong
topology on continuous linear maps between general topological vector spaces). Let
`vector_bundle_continuous_linear_map ğ•œ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (x : B)` be a type synonym for `Eâ‚ x â†’L[ğ•œ] Eâ‚‚ x`.
Then one can endow
`bundle.total_space (vector_bundle_continuous_linear_map ğ•œ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)`
with a topology and a topological vector bundle structure.

Similar constructions can be done for tensor products of topological vector bundles, exterior
algebras, and so on, where the topology can be defined using a norm on the fiber model if this
helps.
-/

noncomputable theory


open bundle

variables (ğ•œ : Type*) {B : Type*} (F : Type*) (E : B â†’ Type*)
[normed_field ğ•œ] [âˆ€ x, add_comm_group (E x)] [âˆ€ x, semimodule ğ•œ (E x)]
[topological_space F] [add_comm_group F] [semimodule ğ•œ F]
[topological_space (total_space E)] [topological_space B]

section

/-- Local trivialization for vector bundles. -/
@[nolint has_inhabited_instance]
structure topological_vector_bundle.trivialization extends bundle_trivialization F (proj E) :=
(linear : âˆ€ x âˆˆ base_set, is_linear_map ğ•œ (Î» y : (E x), (to_fun y).2))

instance : has_coe_to_fun (topological_vector_bundle.trivialization ğ•œ F E) :=
âŸ¨Î» _, (total_space E â†’ B Ã— F), Î» e, e.to_bundle_trivializationâŸ©

instance : has_coe (topological_vector_bundle.trivialization ğ•œ F E)
  (bundle_trivialization F (proj E)) :=
âŸ¨topological_vector_bundle.trivialization.to_bundle_trivializationâŸ©

namespace topological_vector_bundle

variables {ğ•œ F E}

lemma trivialization.mem_source (e : trivialization ğ•œ F E)
  {x : total_space E} : x âˆˆ e.source â†” proj E x âˆˆ e.base_set := bundle_trivialization.mem_source e

end topological_vector_bundle

end

variables [âˆ€ x, topological_space (E x)]

/-- The space `total_space E` (for `E : B â†’ Type*` such that each `E x` is a topological vector
space) has a topological vector space structure with fiber `F` (denoted with
`topological_vector_bundle ğ•œ F E`) if around every point there is a fiber bundle trivialization
which is linear in the fibers. -/
class topological_vector_bundle : Prop :=
(inducing [] : âˆ€ (b : B), inducing (Î» x : (E b), (id âŸ¨b, xâŸ© : total_space E)))
(locally_trivial [] : âˆ€ b : B, âˆƒ e : topological_vector_bundle.trivialization ğ•œ F E, b âˆˆ e.base_set)

variable [topological_vector_bundle ğ•œ F E]

namespace topological_vector_bundle

/-- `trivialization_at ğ•œ F E b` is some choice of trivialization of a vector bundle whose base set
contains a given point `b`. -/
def trivialization_at : Î  b : B, trivialization ğ•œ F E :=
Î» b, classical.some (topological_vector_bundle.locally_trivial ğ•œ F E b)

@[simp, mfld_simps] lemma mem_base_set_trivialization_at (b : B) :
  b âˆˆ (trivialization_at ğ•œ F E b).base_set :=
classical.some_spec (topological_vector_bundle.locally_trivial ğ•œ F E b)

@[simp, mfld_simps] lemma mem_source_trivialization_at (z : total_space E) :
  z âˆˆ (trivialization_at ğ•œ F E z.1).source :=
by { rw bundle_trivialization.mem_source, apply mem_base_set_trivialization_at }

variables {ğ•œ F E}

/-- In a topological vector bundle, a trivialization in the fiber (which is a priori only linear)
is in fact a continuous linear equiv between the fibers and the model fiber. -/
def trivialization.continuous_linear_equiv_at (e : trivialization ğ•œ F E) (b : B)
  (hb : b âˆˆ e.base_set) : continuous_linear_equiv ğ•œ (E b) F :=
{ to_fun := Î» y, (e âŸ¨b, yâŸ©).2,
  inv_fun := Î» z, begin
    have : ((e.to_local_homeomorph.symm) (b, z)).fst = b :=
      bundle_trivialization.proj_symm_apply' _ hb,
    have C : E ((e.to_local_homeomorph.symm) (b, z)).fst = E b, by rw this,
    exact cast C (e.to_local_homeomorph.symm (b, z)).2
  end,
  left_inv := begin
    assume v,
    rw [â† heq_iff_eq],
    apply (cast_heq _ _).trans,
    have A : (b, (e âŸ¨b, vâŸ©).snd) = e âŸ¨b, vâŸ©,
    { refine prod.ext _ rfl,
      symmetry,
      exact bundle_trivialization.coe_fst' _ hb },
    have B : e.to_local_homeomorph.symm (e âŸ¨b, vâŸ©) = âŸ¨b, vâŸ©,
    { apply local_homeomorph.left_inv_on,
      rw bundle_trivialization.mem_source,
      exact hb },
    rw [A, B],
  end,
  right_inv := begin
    assume v,
    have B : e (e.to_local_homeomorph.symm (b, v)) = (b, v),
    { apply local_homeomorph.right_inv_on,
      rw bundle_trivialization.mem_target,
      exact hb },
    have C : (e (e.to_local_homeomorph.symm (b, v))).2 = v, by rw [B],
    conv_rhs { rw â† C },
    dsimp,
    congr,
    ext,
    { exact (bundle_trivialization.proj_symm_apply' _ hb).symm },
    { exact (cast_heq _ _).trans (by refl) },
  end,
  map_add' := Î» v w, (e.linear _ hb).map_add v w,
  map_smul' := Î» c v, (e.linear _ hb).map_smul c v,
  continuous_to_fun := begin
    refine continuous_snd.comp _,
    apply continuous_on.comp_continuous e.to_local_homeomorph.continuous_on
      (topological_vector_bundle.inducing ğ•œ F E b).continuous (Î» x, _),
    rw bundle_trivialization.mem_source,
    exact hb,
  end,
  continuous_inv_fun := begin
    rw (topological_vector_bundle.inducing ğ•œ F E b).continuous_iff,
    dsimp,
    have : continuous (Î» (z : F), (e.to_bundle_trivialization.to_local_homeomorph.symm) (b, z)),
    { apply e.to_local_homeomorph.symm.continuous_on.comp_continuous
        (continuous_const.prod_mk continuous_id') (Î» z, _),
      simp only [bundle_trivialization.mem_target, hb, local_equiv.symm_source,
        local_homeomorph.symm_to_local_equiv] },
    convert this,
    ext z,
    { exact (bundle_trivialization.proj_symm_apply' _ hb).symm },
    { exact cast_heq _ _ },
  end }

@[simp] lemma trivialization.continuous_linear_equiv_at_apply (e : trivialization ğ•œ F E) (b : B)
  (hb : b âˆˆ e.base_set) (y : E b) : e.continuous_linear_equiv_at b hb y = (e âŸ¨b, yâŸ©).2 := rfl

@[simp] lemma trivialization.continuous_linear_equiv_at_apply' (e : trivialization ğ•œ F E)
  (x : total_space E) (hx : x âˆˆ e.source) :
  e.continuous_linear_equiv_at (proj E x) (e.mem_source.1 hx) x.2 = (e x).2 :=
by { cases x, refl }

section
local attribute [reducible] bundle.trivial

instance {B : Type*} {F : Type*} [add_comm_group F] (b : B) :
  add_comm_group (bundle.trivial B F b) := by apply_instance

instance {B : Type*} {F : Type*} [add_comm_group F] [semimodule ğ•œ F] (b : B) :
  semimodule ğ•œ (bundle.trivial B F b) := by apply_instance

end

variables (ğ•œ B F)
/-- Local trivialization for trivial bundle. -/
def trivial_bundle_trivialization : trivialization ğ•œ F (bundle.trivial B F) :=
{ to_fun := Î» x, (x.fst, x.snd),
  inv_fun := Î» y, âŸ¨y.fst, y.sndâŸ©,
  source := set.univ,
  target := set.univ,
  map_source' := Î» x h, set.mem_univ (x.fst, x.snd),
  map_target' :=Î» y h,  set.mem_univ âŸ¨y.fst, y.sndâŸ©,
  left_inv' := Î» x h, sigma.eq rfl rfl,
  right_inv' := Î» x h, prod.ext rfl rfl,
  open_source := is_open_univ,
  open_target := is_open_univ,
  continuous_to_fun := by { rw [â†continuous_iff_continuous_on_univ, continuous_iff_le_induced],
    simp only [prod.topological_space, induced_inf, induced_compose], exact le_refl _, },
  continuous_inv_fun := by { rw [â†continuous_iff_continuous_on_univ, continuous_iff_le_induced],
    simp only [bundle.total_space.topological_space, induced_inf, induced_compose],
    exact le_refl _, },
  base_set := set.univ,
  open_base_set := is_open_univ,
  source_eq := rfl,
  target_eq := by simp only [set.univ_prod_univ],
  proj_to_fun := Î» y hy, rfl,
  linear := Î» x hx, âŸ¨Î» y z, rfl, Î» c y, rflâŸ© }

instance trivial_bundle.topological_vector_bundle :
  topological_vector_bundle ğ•œ F (bundle.trivial B F) :=
{ locally_trivial := Î» x, âŸ¨trivial_bundle_trivialization ğ•œ B F, set.mem_univ xâŸ©,
  inducing := Î» b, âŸ¨begin
    have : (Î» (x : trivial B F b), x) = @id F, by { ext x, refl },
    simp only [total_space.topological_space, induced_inf, induced_compose, function.comp, proj,
      induced_const, top_inf_eq, trivial.proj_snd, id.def, trivial.topological_space, this,
      induced_id],
  endâŸ© }

variables {ğ•œ B F}

/- Not registered as an instance because of a metavariable. -/
lemma is_topological_vector_bundle_is_topological_fiber_bundle :
  is_topological_fiber_bundle F (proj E) :=
Î» x, âŸ¨(trivialization_at ğ•œ F E x).to_bundle_trivialization, mem_base_set_trivialization_at ğ•œ F E xâŸ©

end topological_vector_bundle
