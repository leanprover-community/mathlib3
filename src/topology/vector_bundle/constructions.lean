/-
Copyright Â© 2022 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, SÃ©bastien GouÃ«zel, Heather Macbeth, Floris van Doorn
-/
import topology.fiber_bundle.constructions
import topology.vector_bundle.basic

/-!
# Standard constructions on vector bundles

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains several standard constructions on vector bundles:

* `bundle.trivial.vector_bundle ğ•œ B F`: the trivial vector bundle with scalar field `ğ•œ` and model
  fiber `F` over the base `B`

* `vector_bundle.prod`: for vector bundles `Eâ‚` and `Eâ‚‚` with scalar field `ğ•œ` over a common base,
  a vector bundle structure on their direct sum `Eâ‚ Ã—áµ‡ Eâ‚‚` (the notation stands for
  `Î» x, Eâ‚ x Ã— Eâ‚‚ x`).

* `vector_bundle.pullback`: for a vector bundle `E` over `B`, a vector bundle structure on its
  pullback `f *áµ– E` by a map `f : B' â†’ B` (the notation is a type synonym for `E âˆ˜ f`).

## Tags
Vector bundle, direct sum, pullback
-/

noncomputable theory

open bundle set fiber_bundle
open_locale classical bundle

/-! ### The trivial vector bundle -/

namespace bundle.trivial
variables (ğ•œ : Type*) (B : Type*) (F : Type*)
  [nontrivially_normed_field ğ•œ] [normed_add_comm_group F] [normed_space ğ•œ F] [topological_space B]

instance trivialization.is_linear : (trivialization B F).is_linear ğ•œ :=
{ linear := Î» x hx, âŸ¨Î» y z, rfl, Î» c y, rflâŸ© }

variables {ğ•œ}

lemma trivialization.coord_changeL (b : B) :
  (trivialization B F).coord_changeL ğ•œ (trivialization B F) b = continuous_linear_equiv.refl ğ•œ F :=
begin
  ext v,
  rw [trivialization.coord_changeL_apply'],
  exacts [rfl, âŸ¨mem_univ _, mem_univ _âŸ©]
end

variables (ğ•œ)

instance vector_bundle : vector_bundle ğ•œ F (bundle.trivial B F) :=
{ trivialization_linear' := begin
    introsI e he,
    rw eq_trivialization B F e,
    apply_instance
  end,
  continuous_on_coord_change' := begin
    introsI e e' he he',
    unfreezingI { obtain rfl := eq_trivialization B F e },
    unfreezingI { obtain rfl := eq_trivialization B F e' },
    simp_rw trivialization.coord_changeL,
    exact continuous_const.continuous_on
  end }

end bundle.trivial

/-! ### Direct sum of two vector bundles -/

section
variables (ğ•œ : Type*) {B : Type*} [nontrivially_normed_field ğ•œ] [topological_space B]
  (Fâ‚ : Type*) [normed_add_comm_group Fâ‚] [normed_space ğ•œ Fâ‚]
  (Eâ‚ : B â†’ Type*) [topological_space (total_space Eâ‚)]
  (Fâ‚‚ : Type*) [normed_add_comm_group Fâ‚‚] [normed_space ğ•œ Fâ‚‚]
  (Eâ‚‚ : B â†’ Type*) [topological_space (total_space Eâ‚‚)]

namespace trivialization
variables {Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}
  [Î  x, add_comm_monoid (Eâ‚ x)] [Î  x, module ğ•œ (Eâ‚ x)]
  [Î  x, add_comm_monoid (Eâ‚‚ x)] [Î  x, module ğ•œ (Eâ‚‚ x)]
  (eâ‚ eâ‚' : trivialization Fâ‚ (Ï€ Eâ‚)) (eâ‚‚ eâ‚‚' : trivialization Fâ‚‚ (Ï€ Eâ‚‚))

instance prod.is_linear [eâ‚.is_linear ğ•œ] [eâ‚‚.is_linear ğ•œ] : (eâ‚.prod eâ‚‚).is_linear ğ•œ :=
{ linear := Î» x âŸ¨hâ‚, hâ‚‚âŸ©, (((eâ‚.linear ğ•œ hâ‚).mk' _).prod_map ((eâ‚‚.linear ğ•œ hâ‚‚).mk' _)).is_linear }

@[simp]
lemma coord_changeL_prod [eâ‚.is_linear ğ•œ] [eâ‚'.is_linear ğ•œ] [eâ‚‚.is_linear ğ•œ] [eâ‚‚'.is_linear ğ•œ] â¦ƒbâ¦„
  (hb : b âˆˆ ((eâ‚.prod eâ‚‚).base_set âˆ© (eâ‚'.prod eâ‚‚').base_set)) :
  ((eâ‚.prod eâ‚‚).coord_changeL ğ•œ (eâ‚'.prod eâ‚‚') b : Fâ‚ Ã— Fâ‚‚ â†’L[ğ•œ] Fâ‚ Ã— Fâ‚‚) =
  (eâ‚.coord_changeL ğ•œ eâ‚' b : Fâ‚ â†’L[ğ•œ] Fâ‚).prod_map (eâ‚‚.coord_changeL ğ•œ eâ‚‚' b) :=
begin
  rw [continuous_linear_map.ext_iff, continuous_linear_map.coe_prod_map'],
  rintro âŸ¨vâ‚, vâ‚‚âŸ©,
  show (eâ‚.prod eâ‚‚).coord_changeL ğ•œ (eâ‚'.prod eâ‚‚') b (vâ‚, vâ‚‚) =
    (eâ‚.coord_changeL ğ•œ eâ‚' b vâ‚, eâ‚‚.coord_changeL ğ•œ eâ‚‚' b vâ‚‚),
  rw [eâ‚.coord_changeL_apply eâ‚', eâ‚‚.coord_changeL_apply eâ‚‚', (eâ‚.prod eâ‚‚).coord_changeL_apply'],
  exacts [rfl, hb, âŸ¨hb.1.2, hb.2.2âŸ©, âŸ¨hb.1.1, hb.2.1âŸ©]
end

variables {eâ‚ eâ‚‚} [Î  x : B, topological_space (Eâ‚ x)] [Î  x : B, topological_space (Eâ‚‚ x)]
  [fiber_bundle Fâ‚ Eâ‚] [fiber_bundle Fâ‚‚ Eâ‚‚]

lemma prod_apply [eâ‚.is_linear ğ•œ] [eâ‚‚.is_linear ğ•œ] {x : B} (hxâ‚ : x âˆˆ eâ‚.base_set)
  (hxâ‚‚ : x âˆˆ eâ‚‚.base_set) (vâ‚ : Eâ‚ x) (vâ‚‚ : Eâ‚‚ x) :
  prod eâ‚ eâ‚‚ âŸ¨x, (vâ‚, vâ‚‚)âŸ©
  = âŸ¨x, eâ‚.continuous_linear_equiv_at ğ•œ x hxâ‚ vâ‚, eâ‚‚.continuous_linear_equiv_at ğ•œ x hxâ‚‚ vâ‚‚âŸ© :=
rfl

end trivialization

open trivialization

variables [Î  x, add_comm_monoid (Eâ‚ x)] [Î  x, module ğ•œ (Eâ‚ x)]
  [Î  x, add_comm_monoid (Eâ‚‚ x)] [Î  x, module ğ•œ (Eâ‚‚ x)]
  [Î  x : B, topological_space (Eâ‚ x)] [Î  x : B, topological_space (Eâ‚‚ x)]
  [fiber_bundle Fâ‚ Eâ‚] [fiber_bundle Fâ‚‚ Eâ‚‚]

/-- The product of two vector bundles is a vector bundle. -/
instance vector_bundle.prod  [vector_bundle ğ•œ Fâ‚ Eâ‚] [vector_bundle ğ•œ Fâ‚‚ Eâ‚‚] :
  vector_bundle ğ•œ (Fâ‚ Ã— Fâ‚‚) (Eâ‚ Ã—áµ‡ Eâ‚‚) :=
{ trivialization_linear' := begin
    rintros _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ©, resetI,
    apply_instance
  end,
  continuous_on_coord_change' := begin
    rintros _ _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ© âŸ¨eâ‚', eâ‚‚', heâ‚', heâ‚‚', rflâŸ©, resetI,
    refine (((continuous_on_coord_change ğ•œ eâ‚ eâ‚').mono _).prod_mapL ğ•œ
      ((continuous_on_coord_change ğ•œ eâ‚‚ eâ‚‚').mono _)).congr _;
    dsimp only [base_set_prod] with mfld_simps,
    { mfld_set_tac },
    { mfld_set_tac },
    { rintro b hb,
      rw [continuous_linear_map.ext_iff],
      rintro âŸ¨vâ‚, vâ‚‚âŸ©,
      show (eâ‚.prod eâ‚‚).coord_changeL ğ•œ (eâ‚'.prod eâ‚‚') b (vâ‚, vâ‚‚) =
        (eâ‚.coord_changeL ğ•œ eâ‚' b vâ‚, eâ‚‚.coord_changeL ğ•œ eâ‚‚' b vâ‚‚),
      rw [eâ‚.coord_changeL_apply eâ‚', eâ‚‚.coord_changeL_apply eâ‚‚',
        (eâ‚.prod eâ‚‚).coord_changeL_apply'],
      exacts [rfl, hb, âŸ¨hb.1.2, hb.2.2âŸ©, âŸ¨hb.1.1, hb.2.1âŸ©] }
  end }

variables {ğ•œ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}

@[simp] lemma trivialization.continuous_linear_equiv_at_prod {eâ‚ : trivialization Fâ‚ (Ï€ Eâ‚)}
  {eâ‚‚ : trivialization Fâ‚‚ (Ï€ Eâ‚‚)} [eâ‚.is_linear ğ•œ] [eâ‚‚.is_linear ğ•œ] {x : B} (hxâ‚ : x âˆˆ eâ‚.base_set)
  (hxâ‚‚ : x âˆˆ eâ‚‚.base_set) :
  (eâ‚.prod eâ‚‚).continuous_linear_equiv_at ğ•œ x âŸ¨hxâ‚, hxâ‚‚âŸ©
  = (eâ‚.continuous_linear_equiv_at ğ•œ x hxâ‚).prod (eâ‚‚.continuous_linear_equiv_at ğ•œ x hxâ‚‚) :=
begin
  ext1,
  funext v,
  obtain âŸ¨vâ‚, vâ‚‚âŸ© := v,
  rw [(eâ‚.prod eâ‚‚).continuous_linear_equiv_at_apply ğ•œ, trivialization.prod],
  exact (congr_arg prod.snd (prod_apply ğ•œ hxâ‚ hxâ‚‚ vâ‚ vâ‚‚) : _)
end

end

/-! ### Pullbacks of vector bundles -/

section
variables (R ğ•œ : Type*) {B : Type*} (F : Type*) (E : B â†’ Type*) {B' : Type*} (f : B' â†’ B)

instance [âˆ€ (x : B), add_comm_monoid (E x)] : âˆ€ (x : B'), add_comm_monoid ((f *áµ– E) x) :=
by delta_instance bundle.pullback
instance [semiring R] [âˆ€ (x : B), add_comm_monoid (E x)] [âˆ€ x, module R (E x)] :
  âˆ€ (x : B'), module R ((f *áµ– E) x) :=
by delta_instance bundle.pullback

variables {E F} [topological_space B'] [topological_space (total_space E)]
  [nontrivially_normed_field ğ•œ] [normed_add_comm_group F] [normed_space ğ•œ F] [topological_space B]
  [âˆ€ x, add_comm_monoid (E x)] [âˆ€ x, module ğ•œ (E x)]
  {K : Type*} [continuous_map_class K B' B]

instance trivialization.pullback_linear (e : trivialization F (Ï€ E)) [e.is_linear ğ•œ] (f : K) :
  (@trivialization.pullback _ _ _ B' _ _ _ _ _ _ _ e f).is_linear ğ•œ :=
{ linear := Î» x h, e.linear ğ•œ h }

instance vector_bundle.pullback [âˆ€ x, topological_space (E x)]
  [fiber_bundle F E] [vector_bundle ğ•œ F E] (f : K) : vector_bundle ğ•œ F ((f : B' â†’ B) *áµ– E) :=
{ trivialization_linear' := begin
    rintro _ âŸ¨e, he, rflâŸ©, resetI,
    apply_instance,
  end,
  continuous_on_coord_change' := begin
    rintro _ _ âŸ¨e, he, rflâŸ© âŸ¨e', he', rflâŸ©, resetI,
    refine ((continuous_on_coord_change ğ•œ e e').comp (map_continuous f).continuous_on
      (Î» b hb, hb)).congr _,
    rintro b (hb : f b âˆˆ e.base_set âˆ© e'.base_set), ext v,
    show ((e.pullback f).coord_changeL ğ•œ (e'.pullback f) b) v = (e.coord_changeL ğ•œ e' (f b)) v,
    rw [e.coord_changeL_apply e' hb, (e.pullback f).coord_changeL_apply' _],
    exacts [rfl, hb]
  end }

end
