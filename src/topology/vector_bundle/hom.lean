/-
Copyright Â© 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Floris van Doorn
-/

import topology.vector_bundle.basic
import analysis.normed_space.operator_norm

/-!
# The vector bundle of continuous (semi)linear maps

We define the (topological) vector bundle of continuous (semi)linear maps between two vector bundles
over the same base.

Given bundles `Eâ‚ Eâ‚‚ : B â†’ Type*`, normed spaces `Fâ‚` and `Fâ‚‚`, and a ring-homomorphism `Ïƒ` between
their respective scalar fields, we define `bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ x` to be a
type synonym for `Î» x, Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x`. If the `Eâ‚` and `Eâ‚‚` are vector bundles with model fibers
`Fâ‚` and `Fâ‚‚`, then this will be a vector bundle with fiber `Fâ‚ â†’SL[Ïƒ] Fâ‚‚`.

The topology is constructed from the trivializations for `Eâ‚` and `Eâ‚‚` and the norm-topology on the
model fiber `Fâ‚ â†’SL[ğ•œ] Fâ‚‚` using the `vector_prebundle` construction.  This is a bit awkward because
it introduces a spurious (?) dependence on the normed space structure of the model fiber, rather
than just its topological vector space structure; this might be fixable now that we have
`continuous_linear_map.strong_topology`.

Similar constructions should be possible (but are yet to be formalized) for tensor products of
topological vector bundles, exterior algebras, and so on, where again the topology can be defined
using a norm on the fiber model if this helps.

## Main Definitions

* `bundle.continuous_linear_map.vector_bundle`: continuous semilinear maps between
  vector bundles form a vector bundle.

-/

noncomputable theory

open_locale bundle
open bundle set continuous_linear_map

section defs
variables {ğ•œâ‚ ğ•œâ‚‚ : Type*} [normed_field ğ•œâ‚] [normed_field ğ•œâ‚‚]
variables (Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚)
variables {B : Type*}
variables (Fâ‚ : Type*) (Eâ‚ : B â†’ Type*) [Î  x, add_comm_monoid (Eâ‚ x)] [Î  x, module ğ•œâ‚ (Eâ‚ x)]
variables [Î  x : B, topological_space (Eâ‚ x)]
variables (Fâ‚‚ : Type*) (Eâ‚‚ : B â†’ Type*) [Î  x, add_comm_monoid (Eâ‚‚ x)] [Î  x, module ğ•œâ‚‚ (Eâ‚‚ x)]
variables [Î  x : B, topological_space (Eâ‚‚ x)]

include Fâ‚ Fâ‚‚

-- In this definition we require the scalar rings `ğ•œâ‚` and `ğ•œâ‚‚` to be normed fields, although
-- something much weaker (maybe `comm_semiring`) would suffice mathematically -- this is because of
-- a typeclass inference bug with pi-types:
-- https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/vector.20bundles.20--.20typeclass.20inference.20issue
/-- The bundle of continuous `Ïƒ`-semilinear maps between the topological vector bundles `Eâ‚` and
`Eâ‚‚`. This is a type synonym for `Î» x, Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x`.

We intentionally add `Fâ‚` and `Fâ‚‚` as arguments to this type, so that instances on this type
(that depend on `Fâ‚` and `Fâ‚‚`) actually refer to `Fâ‚` and `Fâ‚‚`. -/
@[derive inhabited, nolint unused_arguments]
def bundle.continuous_linear_map (x : B) : Type* :=
Eâ‚ x â†’SL[Ïƒ] Eâ‚‚ x

instance bundle.continuous_linear_map.add_monoid_hom_class (x : B) :
  add_monoid_hom_class (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ x) (Eâ‚ x) (Eâ‚‚ x) :=
by delta_instance bundle.continuous_linear_map

variables [Î  x, has_continuous_add (Eâ‚‚ x)]

instance (x : B) : add_comm_monoid (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ x) :=
by delta_instance bundle.continuous_linear_map

variables [âˆ€ x, has_continuous_smul ğ•œâ‚‚ (Eâ‚‚ x)]

instance (x : B) : module ğ•œâ‚‚ (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ x) :=
by delta_instance bundle.continuous_linear_map

end defs

variables {ğ•œâ‚ : Type*} [nontrivially_normed_field ğ•œâ‚] {ğ•œâ‚‚ : Type*} [nontrivially_normed_field ğ•œâ‚‚]
  (Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚) [iÏƒ : ring_hom_isometric Ïƒ]

variables {B : Type*} [topological_space B]

variables (Fâ‚ : Type*) [normed_add_comm_group Fâ‚] [normed_space ğ•œâ‚ Fâ‚]
  (Eâ‚ : B â†’ Type*) [Î  x, add_comm_monoid (Eâ‚ x)] [Î  x, module ğ•œâ‚ (Eâ‚ x)]
  [topological_space (total_space Eâ‚)]
variables (Fâ‚‚ : Type*) [normed_add_comm_group Fâ‚‚][normed_space ğ•œâ‚‚ Fâ‚‚]
  (Eâ‚‚ : B â†’ Type*) [Î  x, add_comm_monoid (Eâ‚‚ x)] [Î  x, module ğ•œâ‚‚ (Eâ‚‚ x)]
  [topological_space (total_space Eâ‚‚)]

variables {Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚} (eâ‚ eâ‚' : trivialization Fâ‚ (Ï€ Eâ‚)) (eâ‚‚ eâ‚‚' : trivialization Fâ‚‚ (Ï€ Eâ‚‚))

namespace pretrivialization

include iÏƒ

/-- Assume `eáµ¢` and `eáµ¢'` are trivializations of the bundles `Eáµ¢` over base `B` with fiber `Fáµ¢`
(`i âˆˆ {1,2}`), then `continuous_linear_map_coord_change Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'` is the coordinate change
function between the two induced (pre)trivializations
`pretrivialization.continuous_linear_map Ïƒ eâ‚ eâ‚‚` and
`pretrivialization.continuous_linear_map Ïƒ eâ‚' eâ‚‚'` of `bundle.continuous_linear_map`. -/
def continuous_linear_map_coord_change
  [eâ‚.is_linear ğ•œâ‚] [eâ‚'.is_linear ğ•œâ‚] [eâ‚‚.is_linear ğ•œâ‚‚] [eâ‚‚'.is_linear ğ•œâ‚‚] (b : B) :
  (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) â†’L[ğ•œâ‚‚] Fâ‚ â†’SL[Ïƒ] Fâ‚‚ :=
((eâ‚'.coord_changeL ğ•œâ‚ eâ‚ b).symm.arrow_congrSL (eâ‚‚.coord_changeL ğ•œâ‚‚ eâ‚‚' b) :
  (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) â‰ƒL[ğ•œâ‚‚] Fâ‚ â†’SL[Ïƒ] Fâ‚‚)

variables {Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'}
variables [Î  x : B, topological_space (Eâ‚ x)] [fiber_bundle Fâ‚ Eâ‚]
variables [Î  x : B, topological_space (Eâ‚‚ x)] [fiber_bundle Fâ‚‚ Eâ‚‚]

lemma continuous_on_continuous_linear_map_coord_change
  [vector_bundle ğ•œâ‚ Fâ‚ Eâ‚] [vector_bundle ğ•œâ‚‚ Fâ‚‚ Eâ‚‚]
  [mem_trivialization_atlas eâ‚] [mem_trivialization_atlas eâ‚']
  [mem_trivialization_atlas eâ‚‚] [mem_trivialization_atlas eâ‚‚'] :
  continuous_on (continuous_linear_map_coord_change Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚')
    ((eâ‚.base_set âˆ© eâ‚‚.base_set) âˆ© (eâ‚'.base_set âˆ© eâ‚‚'.base_set)) :=
begin
  have hâ‚ := (compSL Fâ‚ Fâ‚‚ Fâ‚‚ Ïƒ (ring_hom.id ğ•œâ‚‚)).continuous,
  have hâ‚‚ := (continuous_linear_map.flip (compSL Fâ‚ Fâ‚ Fâ‚‚ (ring_hom.id ğ•œâ‚) Ïƒ)).continuous,
  have hâ‚ƒ := (continuous_on_coord_change ğ•œâ‚ eâ‚' eâ‚),
  have hâ‚„ := (continuous_on_coord_change ğ•œâ‚‚ eâ‚‚ eâ‚‚'),
  refine ((hâ‚.comp_continuous_on (hâ‚„.mono _)).clm_comp (hâ‚‚.comp_continuous_on (hâ‚ƒ.mono _))).congr _,
  { mfld_set_tac },
  { mfld_set_tac },
  { intros b hb, ext L v,
    simp only [continuous_linear_map_coord_change, continuous_linear_equiv.coe_coe,
      continuous_linear_equiv.arrow_congrSL_apply, comp_apply, function.comp, compSL_apply,
      flip_apply, continuous_linear_equiv.symm_symm] },
end

omit iÏƒ

variables (Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚')
  [eâ‚.is_linear ğ•œâ‚] [eâ‚'.is_linear ğ•œâ‚] [eâ‚‚.is_linear ğ•œâ‚‚] [eâ‚‚'.is_linear ğ•œâ‚‚]

/-- Given trivializations `eâ‚`, `eâ‚‚` for vector bundles `Eâ‚`, `Eâ‚‚` over a base `B`,
`pretrivialization.continuous_linear_map Ïƒ eâ‚ eâ‚‚` is the induced pretrivialization for the
continuous `Ïƒ`-semilinear maps from `Eâ‚` to `Eâ‚‚`. That is, the map which will later become a
trivialization, after the bundle of continuous semilinear maps is equipped with the right
topological vector bundle structure. -/
def continuous_linear_map :
  pretrivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)) :=
{ to_fun := Î» p, âŸ¨p.1, (eâ‚‚.continuous_linear_map_at ğ•œâ‚‚ p.1).comp $ p.2.comp $ eâ‚.symmL ğ•œâ‚ p.1âŸ©,
  inv_fun := Î» p, âŸ¨p.1, (eâ‚‚.symmL ğ•œâ‚‚ p.1).comp $ p.2.comp $ eâ‚.continuous_linear_map_at ğ•œâ‚ p.1âŸ©,
  source := (bundle.total_space.proj) â»Â¹' (eâ‚.base_set âˆ© eâ‚‚.base_set),
  target := (eâ‚.base_set âˆ© eâ‚‚.base_set) Ã—Ë¢ set.univ,
  map_source' := Î» âŸ¨x, LâŸ© h, âŸ¨h, set.mem_univ _âŸ©,
  map_target' := Î» âŸ¨x, fâŸ© h, h.1,
  left_inv' := Î» âŸ¨x, LâŸ© âŸ¨hâ‚, hâ‚‚âŸ©,
  begin
    simp_rw [sigma.mk.inj_iff, eq_self_iff_true, heq_iff_eq, true_and],
    ext v,
    simp only [comp_apply, trivialization.symmL_continuous_linear_map_at, hâ‚, hâ‚‚]
  end,
  right_inv' := Î» âŸ¨x, fâŸ© âŸ¨âŸ¨hâ‚, hâ‚‚âŸ©, _âŸ©,
  begin
    simp_rw [prod.mk.inj_iff, eq_self_iff_true, true_and],
    ext v,
    simp only [comp_apply, trivialization.continuous_linear_map_at_symmL, hâ‚, hâ‚‚]
  end,
  open_target := (eâ‚.open_base_set.inter eâ‚‚.open_base_set).prod is_open_univ,
  base_set := eâ‚.base_set âˆ© eâ‚‚.base_set,
  open_base_set := eâ‚.open_base_set.inter eâ‚‚.open_base_set,
  source_eq := rfl,
  target_eq := rfl,
  proj_to_fun := Î» âŸ¨x, fâŸ© h, rfl }

instance continuous_linear_map.is_linear
  [Î  x, has_continuous_add (Eâ‚‚ x)] [Î  x, has_continuous_smul ğ•œâ‚‚ (Eâ‚‚ x)] :
  (pretrivialization.continuous_linear_map Ïƒ eâ‚ eâ‚‚).is_linear ğ•œâ‚‚ :=
{ linear := Î» x h,
  { map_add := Î» L L',
    show (eâ‚‚.continuous_linear_map_at ğ•œâ‚‚ x).comp ((L + L').comp (eâ‚.symmL ğ•œâ‚ x)) = _,
    begin
      simp_rw [add_comp, comp_add],
      refl
    end,
    map_smul := Î» c L,
    show (eâ‚‚.continuous_linear_map_at ğ•œâ‚‚ x).comp ((c â€¢ L).comp (eâ‚.symmL ğ•œâ‚ x)) = _,
    begin
      simp_rw [smul_comp, comp_smulâ‚›â‚—, ring_hom.id_apply],
      refl
    end, } }

lemma continuous_linear_map_apply
  (p : total_space (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)) :
  (continuous_linear_map Ïƒ eâ‚ eâ‚‚) p =
  âŸ¨p.1, (eâ‚‚.continuous_linear_map_at ğ•œâ‚‚ p.1).comp $ p.2.comp $ eâ‚.symmL ğ•œâ‚ p.1âŸ© :=
rfl

lemma continuous_linear_map_symm_apply (p : B Ã— (Fâ‚ â†’SL[Ïƒ] Fâ‚‚)) :
  (continuous_linear_map Ïƒ eâ‚ eâ‚‚).to_local_equiv.symm p =
  âŸ¨p.1, (eâ‚‚.symmL ğ•œâ‚‚ p.1).comp $ p.2.comp $ eâ‚.continuous_linear_map_at ğ•œâ‚ p.1âŸ© :=
rfl

variables [Î  x, has_continuous_add (Eâ‚‚ x)]

lemma continuous_linear_map_symm_apply' {b : B} (hb : b âˆˆ eâ‚.base_set âˆ© eâ‚‚.base_set)
  (L : Fâ‚ â†’SL[Ïƒ] Fâ‚‚) :
  (continuous_linear_map Ïƒ eâ‚ eâ‚‚).symm b L =
  (eâ‚‚.symmL ğ•œâ‚‚ b).comp (L.comp $ eâ‚.continuous_linear_map_at ğ•œâ‚ b) :=
begin
  rw [symm_apply], refl, exact hb
end

lemma continuous_linear_map_coord_change_apply [ring_hom_isometric Ïƒ] (b : B)
  (hb : b âˆˆ (eâ‚.base_set âˆ© eâ‚‚.base_set) âˆ© (eâ‚'.base_set âˆ© eâ‚‚'.base_set)) (L : Fâ‚ â†’SL[Ïƒ] Fâ‚‚) :
  continuous_linear_map_coord_change Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚' b L =
  (continuous_linear_map Ïƒ eâ‚' eâ‚‚'
    (total_space_mk b ((continuous_linear_map Ïƒ eâ‚ eâ‚‚).symm b L))).2 :=
begin
  ext v,
  simp_rw [continuous_linear_map_coord_change, continuous_linear_equiv.coe_coe,
    continuous_linear_equiv.arrow_congrSL_apply,
    continuous_linear_map_apply, continuous_linear_map_symm_apply' Ïƒ eâ‚ eâ‚‚ hb.1,
    comp_apply, continuous_linear_equiv.coe_coe, continuous_linear_equiv.symm_symm,
    trivialization.continuous_linear_map_at_apply, trivialization.symmL_apply],
  dsimp only [total_space_mk],
  rw [eâ‚‚.coord_changeL_apply eâ‚‚', eâ‚'.coord_changeL_apply eâ‚, eâ‚.coe_linear_map_at_of_mem hb.1.1,
    eâ‚‚'.coe_linear_map_at_of_mem hb.2.2],
  exacts [âŸ¨hb.2.1, hb.1.1âŸ©, âŸ¨hb.1.2, hb.2.2âŸ©]
end

end pretrivialization

open pretrivialization
variables (Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚) [ring_hom_isometric Ïƒ]
variables [Î  x : B, topological_space (Eâ‚ x)] [fiber_bundle Fâ‚ Eâ‚] [vector_bundle ğ•œâ‚ Fâ‚ Eâ‚]
variables [Î  x : B, topological_space (Eâ‚‚ x)] [fiber_bundle Fâ‚‚ Eâ‚‚] [vector_bundle ğ•œâ‚‚ Fâ‚‚ Eâ‚‚]
variables [Î  x, has_continuous_add (Eâ‚‚ x)] [Î  x, has_continuous_smul ğ•œâ‚‚ (Eâ‚‚ x)]

/-- The continuous `Ïƒ`-semilinear maps between two topological vector bundles form a
`vector_prebundle` (this is an auxiliary construction for the
`vector_bundle` instance, in which the pretrivializations are collated but no topology
on the total space is yet provided). -/
def _root_.bundle.continuous_linear_map.vector_prebundle :
  vector_prebundle ğ•œâ‚‚ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚)
  (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚) :=
{ pretrivialization_atlas :=
    {e |  âˆƒ (eâ‚ : trivialization Fâ‚ (Ï€ Eâ‚)) (eâ‚‚ : trivialization Fâ‚‚ (Ï€ Eâ‚‚))
    [mem_trivialization_atlas eâ‚] [mem_trivialization_atlas eâ‚‚], by exactI
    e = pretrivialization.continuous_linear_map Ïƒ eâ‚ eâ‚‚},
  pretrivialization_linear' := begin
    rintro _ âŸ¨eâ‚, heâ‚, eâ‚‚, heâ‚‚, rflâŸ©,
    apply_instance
  end,
  pretrivialization_at := Î» x, pretrivialization.continuous_linear_map Ïƒ
    (trivialization_at Fâ‚ Eâ‚ x) (trivialization_at Fâ‚‚ Eâ‚‚ x),
  mem_base_pretrivialization_at := Î» x,
    âŸ¨mem_base_set_trivialization_at Fâ‚ Eâ‚ x, mem_base_set_trivialization_at Fâ‚‚ Eâ‚‚ xâŸ©,
  pretrivialization_mem_atlas := Î» x,
    âŸ¨trivialization_at Fâ‚ Eâ‚ x, trivialization_at Fâ‚‚ Eâ‚‚ x, _, _, rflâŸ©,
  exists_coord_change := by { rintro _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ© _ âŸ¨eâ‚', eâ‚‚', heâ‚', heâ‚‚', rflâŸ©,
    resetI,
    exact âŸ¨continuous_linear_map_coord_change Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚',
    continuous_on_continuous_linear_map_coord_change,
    continuous_linear_map_coord_change_apply Ïƒ eâ‚ eâ‚' eâ‚‚ eâ‚‚'âŸ© } }

/-- Topology on the continuous `Ïƒ`-semilinear_maps between the respective fibers at a point of two
"normable" vector bundles over the same base. Here "normable" means that the bundles have fibers
modelled on normed spaces `Fâ‚`, `Fâ‚‚` respectively.  The topology we put on the continuous
`Ïƒ`-semilinear_maps is the topology coming from the operator norm on maps from `Fâ‚` to `Fâ‚‚`. -/
instance (x : B) : topological_space (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ x) :=
(bundle.continuous_linear_map.vector_prebundle Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).fiber_topology x

/-- Topology on the total space of the continuous `Ïƒ`-semilinear_maps between two "normable" vector
bundles over the same base. -/
instance bundle.continuous_linear_map.topological_space_total_space :
  topological_space (total_space (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)) :=
(bundle.continuous_linear_map.vector_prebundle
  Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).total_space_topology

/-- The continuous `Ïƒ`-semilinear_maps between two vector bundles form a fiber bundle. -/
instance _root_.bundle.continuous_linear_map.fiber_bundle :
  fiber_bundle (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚) :=
(bundle.continuous_linear_map.vector_prebundle
  Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).to_fiber_bundle

/-- The continuous `Ïƒ`-semilinear_maps between two vector bundles form a vector bundle. -/
instance _root_.bundle.continuous_linear_map.vector_bundle :
  vector_bundle ğ•œâ‚‚ (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚) :=
(bundle.continuous_linear_map.vector_prebundle
  Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).to_vector_bundle

variables (eâ‚ eâ‚‚) [heâ‚ : mem_trivialization_atlas eâ‚] [heâ‚‚ : mem_trivialization_atlas eâ‚‚]
  {Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚}

include heâ‚ heâ‚‚

/-- Given trivializations `eâ‚`, `eâ‚‚` in the atlas for vector bundles `Eâ‚`, `Eâ‚‚` over a base `B`,
the induced trivialization for the continuous `Ïƒ`-semilinear maps from `Eâ‚` to `Eâ‚‚`,
whose base set is `eâ‚.base_set âˆ© eâ‚‚.base_set`. -/
def trivialization.continuous_linear_map :
  trivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)) :=
vector_prebundle.trivialization_of_mem_pretrivialization_atlas _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ©

instance _root_.bundle.continuous_linear_map.mem_trivialization_atlas :
  mem_trivialization_atlas (eâ‚.continuous_linear_map Ïƒ eâ‚‚ :
    trivialization (Fâ‚ â†’SL[Ïƒ] Fâ‚‚) (Ï€ (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚))) :=
{ out := âŸ¨_, âŸ¨eâ‚, eâ‚‚, by apply_instance, by apply_instance, rflâŸ©, rflâŸ© }

variables {eâ‚ eâ‚‚}

@[simp] lemma trivialization.base_set_continuous_linear_map :
  (eâ‚.continuous_linear_map Ïƒ eâ‚‚).base_set = eâ‚.base_set âˆ© eâ‚‚.base_set :=
rfl

lemma trivialization.continuous_linear_map_apply
  (p : total_space (bundle.continuous_linear_map Ïƒ Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)) :
  eâ‚.continuous_linear_map Ïƒ eâ‚‚ p =
  âŸ¨p.1, (eâ‚‚.continuous_linear_map_at ğ•œâ‚‚ p.1).comp $ p.2.comp $ eâ‚.symmL ğ•œâ‚ p.1âŸ© :=
rfl
