/-
Copyright ¬© 2022 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri, Sebastien Gouezel, Heather Macbeth, Floris van Doorn
-/

import topology.vector_bundle.basic

/-!
# Pullbacks of fiber and vector bundles

We construct the pullback bundle for a map `f : B' ‚Üí B` whose fiber map is given simply by
`f *·µñ E = E ‚àò f` (the type synonym is there for typeclass instance problems).
-/

noncomputable theory

open bundle set topological_space
open_locale classical bundle

variables (R ùïú : Type*) {B : Type*} (F : Type*) (E E' : B ‚Üí Type*)
variables {B' : Type*} (f : B' ‚Üí B)

instance [‚àÄ (x : B), topological_space (E' x)] : ‚àÄ (x : B'), topological_space ((f *·µñ E') x) :=
by delta_instance bundle.pullback
instance [‚àÄ (x : B), add_comm_monoid (E' x)] : ‚àÄ (x : B'), add_comm_monoid ((f *·µñ E') x) :=
by delta_instance bundle.pullback
instance [semiring R] [‚àÄ (x : B), add_comm_monoid (E' x)] [‚àÄ x, module R (E' x)] :
  ‚àÄ (x : B'), module R ((f *·µñ E') x) :=
by delta_instance bundle.pullback

variables [topological_space B'] [topological_space (total_space E)]

/-- Definition of `pullback.total_space.topological_space`, which we make irreducible. -/
@[irreducible] def pullback_topology : topological_space (total_space (f *·µñ E)) :=
induced total_space.proj ‚Äπtopological_space B'‚Ä∫ ‚äì
induced (pullback.lift f) ‚Äπtopological_space (total_space E)‚Ä∫

/-- The topology on the total space of a pullback bundle is the coarsest topology for which both
the projections to the base and the map to the original bundle are continuous. -/
instance pullback.total_space.topological_space :
  topological_space (total_space (f *·µñ E)) :=
pullback_topology E f

lemma pullback.continuous_proj (f : B' ‚Üí B) :
  continuous (@total_space.proj _ (f *·µñ E)) :=
begin
  rw [continuous_iff_le_induced, pullback.total_space.topological_space, pullback_topology],
  exact inf_le_left,
end

lemma pullback.continuous_lift (f : B' ‚Üí B) :
  continuous (@pullback.lift B E B' f) :=
begin
  rw [continuous_iff_le_induced, pullback.total_space.topological_space, pullback_topology],
  exact inf_le_right,
end

lemma inducing_pullback_total_space_embedding (f : B' ‚Üí B) :
  inducing (@pullback_total_space_embedding B E B' f) :=
begin
  constructor,
  simp_rw [prod.topological_space, induced_inf, induced_compose,
    pullback.total_space.topological_space, pullback_topology],
  refl
end

section fiber_bundle
variables (F) [topological_space F] [topological_space B]

lemma pullback.continuous_total_space_mk [‚àÄ x, topological_space (E x)]
  [fiber_bundle F E] {f : B' ‚Üí B} {x : B'} :
  continuous (@total_space_mk _ (f *·µñ E) x) :=
begin
  simp only [continuous_iff_le_induced, pullback.total_space.topological_space, induced_compose,
    induced_inf, function.comp, total_space_mk, total_space.proj, induced_const, top_inf_eq,
    pullback_topology],
  exact le_of_eq (fiber_bundle.total_space_mk_inducing F E (f x)).induced,
end

variables {E F} [‚àÄ b, has_zero (E b)] {K : Type*} [continuous_map_class K B' B]

/-- A fiber bundle trivialization can be pulled back to a trivialization on the pullback bundle. -/
def trivialization.pullback (e : trivialization F (œÄ E)) (f : K) :
  trivialization F (œÄ ((f : B' ‚Üí B) *·µñ E)) :=
{ to_fun := Œª z, (z.proj, (e (pullback.lift f z)).2),
  inv_fun := Œª y, @total_space_mk _ (f *·µñ E) y.1 (e.symm (f y.1) y.2),
  source := pullback.lift f ‚Åª¬π' e.source,
  base_set := f ‚Åª¬π' e.base_set,
  target := (f ‚Åª¬π' e.base_set) √óÀ¢ univ,
  map_source' := Œª x h, by { simp_rw [e.source_eq, mem_preimage, pullback.proj_lift] at h,
    simp_rw [prod_mk_mem_set_prod_eq, mem_univ, and_true, mem_preimage, h] },
  map_target' := Œª y h, by { rw [mem_prod, mem_preimage] at h,
    simp_rw [e.source_eq, mem_preimage, pullback.proj_lift, h.1] },
  left_inv' := Œª x h, by { simp_rw [mem_preimage, e.mem_source, pullback.proj_lift] at h,
    simp_rw [pullback.lift, e.symm_apply_apply_mk h, total_space.eta] },
  right_inv' := Œª x h, by { simp_rw [mem_prod, mem_preimage, mem_univ, and_true] at h,
    simp_rw [total_space.proj_mk, pullback.lift_mk, e.apply_mk_symm h, prod.mk.eta] },
  open_source := by { simp_rw [e.source_eq, ‚Üê preimage_comp], exact ((map_continuous f).comp $
    pullback.continuous_proj E f).is_open_preimage _ e.open_base_set },
  open_target := ((map_continuous f).is_open_preimage _ e.open_base_set).prod is_open_univ,
  open_base_set := (map_continuous f).is_open_preimage _ e.open_base_set,
  continuous_to_fun := (pullback.continuous_proj E f).continuous_on.prod
    (continuous_snd.comp_continuous_on $
    e.continuous_on.comp (pullback.continuous_lift E f).continuous_on subset.rfl),
  continuous_inv_fun := begin
    dsimp only,
    simp_rw [(inducing_pullback_total_space_embedding E f).continuous_on_iff, function.comp,
      pullback_total_space_embedding, total_space.proj_mk],
    dsimp only [total_space.proj_mk],
    refine continuous_on_fst.prod (e.continuous_on_symm.comp
      ((map_continuous f).prod_map continuous_id).continuous_on subset.rfl)
  end,
  source_eq := by { dsimp only, rw e.source_eq, refl, },
  target_eq := rfl,
  proj_to_fun := Œª y h, rfl }

instance fiber_bundle.pullback [‚àÄ x, topological_space (E x)]
  [fiber_bundle F E] (f : K) : fiber_bundle F ((f : B' ‚Üí B) *·µñ E) :=
{ total_space_mk_inducing := Œª x, inducing_of_inducing_compose
    (pullback.continuous_total_space_mk F E) (pullback.continuous_lift E f)
    (total_space_mk_inducing F E (f x)),
  trivialization_atlas :=
    {ef | ‚àÉ (e : trivialization F (œÄ E)) [mem_trivialization_atlas e], ef = e.pullback f},
  trivialization_at := Œª x, (trivialization_at F E (f x)).pullback f,
  mem_base_set_trivialization_at := Œª x, mem_base_set_trivialization_at F E (f x),
  trivialization_mem_atlas := Œª x, ‚ü®trivialization_at F E (f x), by apply_instance, rfl‚ü© }

end fiber_bundle

section vector_bundle
variables (F) [nontrivially_normed_field ùïú]
  [normed_add_comm_group F] [normed_space ùïú F] [topological_space B]
  [‚àÄ x, add_comm_monoid (E x)] [‚àÄ x, module ùïú (E x)]

variables {E F} {K : Type*} [continuous_map_class K B' B]

instance trivialization.pullback_linear (e : trivialization F (œÄ E)) [e.is_linear ùïú] (f : K) :
  (@trivialization.pullback _ _ _ B' _ _ _ _ _ _ _ e f).is_linear ùïú :=
{ linear := Œª x h, e.linear ùïú h }

instance vector_bundle.pullback [‚àÄ x, topological_space (E x)]
  [fiber_bundle F E] [vector_bundle ùïú F E] (f : K) : vector_bundle ùïú F ((f : B' ‚Üí B) *·µñ E) :=
{ trivialization_linear' := begin
    rintro _ ‚ü®e, he, rfl‚ü©, resetI,
    apply_instance,
  end,
  continuous_on_coord_change' := begin
    rintro _ _ ‚ü®e, he, rfl‚ü© ‚ü®e', he', rfl‚ü©, resetI,
    refine ((continuous_on_coord_change ùïú e e').comp (map_continuous f).continuous_on
      (Œª b hb, hb)).congr _,
    rintro b (hb : f b ‚àà e.base_set ‚à© e'.base_set), ext v,
    show ((e.pullback f).coord_changeL ùïú (e'.pullback f) b) v = (e.coord_changeL ùïú e' (f b)) v,
    rw [e.coord_changeL_apply e' hb, (e.pullback f).coord_changeL_apply' _],
    exacts [rfl, hb]
  end }

end vector_bundle
